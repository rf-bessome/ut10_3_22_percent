#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияИС.ОтработатьВходящийДокументПротоколаОбмена(ЭтаФорма, "СсылкаНаОбъект", "СсылкаНаОбъект");
	РеквизитыДокументаОснования = Неопределено;
	ТипСсылки                   = ТипЗнч(СсылкаНаОбъект);
	
	Если ЗначениеЗаполнено(СсылкаНаОбъект) Тогда
		Если Не ОпределяемыеТипы().ДокументыЗЕРНО.Тип.СодержитТип(ТипСсылки) Тогда
			РеквизитыДокументаОснования = Новый ФиксированнаяСтруктура(
				ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаОбъект, "Ссылка, Проведен"));
			Организация = ИнтеграцияИС.ОрганизацияИзПрикладногоОбъекта(СсылкаНаОбъект);
		Иначе
			РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаОбъект, "Организация,Подразделение");
			Организация   = РеквизитыДокумента.Организация;
			Подразделение = РеквизитыДокумента.Подразделение;
		КонецЕсли;
	КонецЕсли;
	
	Если ИнтеграцияЗЕРНОПовтИсп.ИспользоватьАвтоматическийОбменДанными(Организация, Подразделение) Тогда
		Элементы.ДеревоФайловДеревоФайловВыполнитьОбмен.Видимость = Ложь;
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	ЗаполнитьДеревоФайлов();
	
	// ИнтеграцияИС
	Если РеквизитыДокументаОснования <> Неопределено Тогда
		ИнтеграцияИС.ПриСозданииНаСервереВФормеДокументаОснования(
			ЭтаФорма,
			РеквизитыДокументаОснования,
			ИнтеграцияИС.ПараметрыИнтеграцииВФорме(
				ИнтеграцияЗЕРНОКлиентСервер.ИмяПодсистемы(),
				ИнтеграцияИС.ИмяЭлементаДляРазмещения()));
	КонецЕсли;
	// Конец ИнтеграцияИС
	
	СобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ИнтеграцияИСКлиент.РазвернутьДеревоРекурсивно(ДеревоФайлов, Элементы.ДеревоФайлов);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	ПерезаполнитьДерево = Ложь;
	
	Если ИмяСобытия = ИнтеграцияИСКлиентСервер.ИмяСобытияИзмененоСостояние(ИнтеграцияЗЕРНОКлиентСервер.ИмяПодсистемы())
	 И (Параметр.Ссылка = СсылкаНаОбъект Или (Параметр.Свойство("Основание") И Параметр.Основание = СсылкаНаОбъект)) Тогда
		
		ПерезаполнитьДерево = Истина;
		
	ИначеЕсли ИмяСобытия = ИнтеграцияИСКлиентСервер.ИмяСобытияВыполненОбмен(ИнтеграцияЗЕРНОКлиентСервер.ИмяПодсистемы())
	 И (Параметр = Неопределено
		Или (ТипЗнч(Параметр) = Тип("Структура") И Параметр.ОбновлятьСтатусВФормахДокументов)) Тогда
		
		ПерезаполнитьДерево = Истина;
		
	ИначеЕсли СтрНачинаетсяС(ИмяСобытия, ИнтеграцияИСКлиентСервер.ИмяСобытияИзмененОбъект(ИнтеграцияЗЕРНОКлиентСервер.ИмяПодсистемы())) Тогда
		
		ПерезаполнитьДерево = Истина;
		
	КонецЕсли;
	
	Если ПерезаполнитьДерево Тогда
		ОбновитьДерево();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТекстДокументаЗЕРНООбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	// ИнтеграцияИС
	Если РеквизитыДокументаОснования <> Неопределено Тогда
		ИнтеграцияИСКлиент.ОбработкаНавигационнойСсылкиВФормеДокументаОснования(
			ЭтаФорма,
			РеквизитыДокументаОснования,
			Элемент,
			НавигационнаяСсылкаФорматированнойСтроки,
			СтандартнаяОбработка);
	КонецЕсли;
	// Конец ИнтеграцияИС
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДереваФайлов

&НаКлиенте
Процедура ДеревоФайловВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПоказатьСообщенияОперации(ВыбраннаяСтрока);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьДерево();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОбмен(Команда)
	
	ОчиститьСообщения();
	
	РезультатОбмена = ИнтеграцияЗЕРНОВызовСервера.ВыполнитьОбмен(
		Организация, УникальныйИдентификатор, СсылкаНаОбъект);
	
	ИнтеграцияЗЕРНОСлужебныйКлиент.ОбработатьРезультатОбмена(
		РезультатОбмена, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСообщенияXML(Команда)
	
	ПоказатьСообщенияОперации(Элементы.ДеревоФайлов.ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСтатус(Команда)
	
	ОчиститьСообщения();
	
	ТекущиеДанные = Элементы.ДеревоФайлов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РассчитатьСтатусНаСервере(ТекущиеДанные.Документ);
	
	Оповестить(ИнтеграцияИСКлиентСервер.ИмяСобытияВыполненОбмен(ИнтеграцияЗЕРНОКлиентСервер.ИмяПодсистемы()));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьДерево()
	
	ЗаполнитьДеревоФайлов();
	ИнтеграцияИСКлиент.РазвернутьДеревоРекурсивно(ДеревоФайлов, Элементы.ДеревоФайлов);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	ИнтеграцияИС.УстановитьУсловноеОформлениеПротоколаОбмена(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьСтатусНаСервере(ДокументСсылка)
	
	ИнтеграцияЗЕРНО.РассчитатьСтатусДокументаПоДаннымПротоколаОбмена(ДокументСсылка);
	
КонецПроцедуры

&НаСервере
Функция ПредставлениеОперации(СтрокаПоследовательности, ДокументСсылка, ВыборкаПоФайлам = Неопределено, ТаблицаРасчетаКоличества = Неопределено)
	
	Если СтрокаПоследовательности = Неопределено Тогда
		
		Возврат "";
		
	ИначеЕсли СтрокаПоследовательности.ТипСообщения = Перечисления.ТипыЗапросовИС.Исходящий Тогда
		
		Если ВыборкаПоФайлам = Неопределено Тогда
			Возврат ИнтеграцияЗЕРНОСлужебныйКлиентСервер.ОписаниеОперацииПередачиДанных(
				СтрокаПоследовательности.Операция);
		Иначе
			
			ТекстПоКоличествуСообщений = Неопределено;
			
			Если ТаблицаРасчетаКоличества <> Неопределено
				И Не ЗначениеЗаполнено(ВыборкаПоФайлам.ДополнительноеОписание) Тогда
				
				СтруктураПоиска = Новый Структура();
				СтруктураПоиска.Вставить("Ссылка",   ВыборкаПоФайлам.Ссылка);
				СтруктураПоиска.Вставить("Версия",   ВыборкаПоФайлам.Версия);
				СтруктураПоиска.Вставить("Операция", ВыборкаПоФайлам.Операция);
				
				ПоискСтрок = ТаблицаРасчетаКоличества.НайтиСтроки(СтруктураПоиска);
				
				Если ПоискСтрок.Количество() > 0 И ПоискСтрок[0].СоставнаяОперация Тогда
					ТекстПоКоличествуСообщений = СтрШаблон(
						НСтр("ru = '%1 из %2'"),
						ПоискСтрок[0].ТекущийНомер,
						ПоискСтрок[0].Количество);
				КонецЕсли;
				
			КонецЕсли;
			
			Возврат ИнтеграцияЗЕРНОСлужебныйКлиентСервер.ОписаниеОперацииПередачиДанных(
				СтрокаПоследовательности.Операция,
				ВыборкаПоФайлам.ДополнительноеОписание,
				ВыборкаПоФайлам.Версия,
				ТекстПоКоличествуСообщений);
			
		КонецЕсли;
		
	ИначеЕсли СтрокаПоследовательности.ТипСообщения = Перечисления.ТипыЗапросовИС.Входящий Тогда
		
		Возврат ИнтеграцияЗЕРНОСлужебныйКлиентСервер.ОписаниеОперацииПолученияДанных(
			СтрокаПоследовательности.Операция);
		
	Иначе
		
		Возврат Строка(СтрокаПоследовательности.Операция);
		
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ТаблицаДокументы(Документ = Неопределено)
	
	ТаблицаДокументы = Новый ТаблицаЗначений;
	ТаблицаДокументы.Колонки.Добавить("Ссылка",              Метаданные.Справочники.ЗЕРНОПрисоединенныеФайлы.Реквизиты.Документ.Тип);
	ТаблицаДокументы.Колонки.Добавить("Статус",              Метаданные.РегистрыСведений.СтатусыОбъектовСинхронизацииЗЕРНО.Ресурсы.Статус.Тип);
	ТаблицаДокументы.Колонки.Добавить("ДальнейшееДействие1", Новый ОписаниеТипов("ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЗЕРНО"));
	ТаблицаДокументы.Колонки.Добавить("ДальнейшееДействие2", Новый ОписаниеТипов("ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЗЕРНО"));
	ТаблицаДокументы.Колонки.Добавить("ДальнейшееДействие3", Новый ОписаниеТипов("ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЗЕРНО"));
	
	Если Документ <> Неопределено Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Таблица.ОбъектСинхронизации КАК Ссылка,
		|	Таблица.Статус              КАК Статус,
		|	Таблица.ДальнейшееДействие1 КАК ДальнейшееДействие1,
		|	Таблица.ДальнейшееДействие2 КАК ДальнейшееДействие2,
		|	Таблица.ДальнейшееДействие3 КАК ДальнейшееДействие3
		|ИЗ
		|	РегистрСведений.СтатусыОбъектовСинхронизацииЗЕРНО КАК Таблица
		|ГДЕ
		|	Таблица.ОбъектСинхронизации = &СсылкаНаОбъект
		|");
		
		Запрос.УстановитьПараметр("СсылкаНаОбъект", СсылкаНаОбъект);
		
		УстановитьПривилегированныйРежим(Истина);
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(ТаблицаДокументы.Добавить(), Выборка);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ТаблицаДокументы;
	
КонецФункции

&НаСервере
Процедура РассчитатьКоличествоДляПредставления(ТаблицаРасчетаКоличества)

	ВременнаяТаблицаРасчета = ТаблицаРасчетаКоличества.Скопировать();
	ВременнаяТаблицаРасчета.Свернуть("Версия, Операция", "Количество");
	
	Для Каждого СтрокаГруппы Из ВременнаяТаблицаРасчета Цикл
		
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("Версия",   СтрокаГруппы.Версия);
		СтруктураПоиска.Вставить("Операция", СтрокаГруппы.Операция);
		
		ПоискСтрок = ТаблицаРасчетаКоличества.НайтиСтроки(СтруктураПоиска);
		Для Каждого НайденнаяСтрока Из ПоискСтрок Цикл
			НайденнаяСтрока.Количество        = СтрокаГруппы.Количество;
			НайденнаяСтрока.ТекущийНомер      = ПоискСтрок.Найти(НайденнаяСтрока) + 1;
			НайденнаяСтрока.СоставнаяОперация = ПоискСтрок.Количество() > 1;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция НоваяТаблицаРасчетаКоличестваОпераций()
	
	ТаблицаРасчетаКоличества = Новый ТаблицаЗначений();
	ТаблицаРасчетаКоличества.Колонки.Добавить("Версия");
	ТаблицаРасчетаКоличества.Колонки.Добавить("Операция");
	ТаблицаРасчетаКоличества.Колонки.Добавить("Ссылка");
	ТаблицаРасчетаКоличества.Колонки.Добавить("ТекущийНомер");
	ТаблицаРасчетаКоличества.Колонки.Добавить("Количество");
	ТаблицаРасчетаКоличества.Колонки.Добавить("СоставнаяОперация");
	
	ТаблицаРасчетаКоличества.Индексы.Добавить("Версия, Операция");
	ТаблицаРасчетаКоличества.Индексы.Добавить("Ссылка, Версия, Операция");
	
	Возврат ТаблицаРасчетаКоличества;
	
КонецФункции

#Область ЗаполнениеДереваФайлов

&НаСервере
Процедура ЗаполнитьПоОснованиюДокументаСписаниеПартийЗЕРНО(ДокументОснование)
	
	МетаданныеДокумента = Метаданные.Документы.СписаниеПартийЗЕРНО;
	
	ЗаполнитьПоОснованиюДокументаЗЕРНО(МетаданныеДокумента, ДокументОснование);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоОснованиюДокументаПогашениеСДИЗЗЕРНО(ДокументОснование)
	
	МетаданныеДокумента = Метаданные.Документы.ПогашениеСДИЗЗЕРНО;
	
	ЗаполнитьПоОснованиюДокументаЗЕРНО(МетаданныеДокумента, ДокументОснование);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоОснованиюДокументаФормированиеПартииЗЕРНО(ДокументОснование)
	
	МетаданныеДокумента = Метаданные.Документы.ФормированиеПартийЗЕРНО;
	
	ЗаполнитьПоОснованиюДокументаЗЕРНО(МетаданныеДокумента, ДокументОснование);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоОснованиюДокументаОформлениеСДИЗЗЕРНО(ДокументОснование)
	
	МетаданныеДокумента = Метаданные.Документы.ОформлениеСДИЗЗЕРНО;
	
	ЗаполнитьПоОснованиюДокументаЗЕРНО(МетаданныеДокумента, ДокументОснование);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоОснованиюДокументаВнесениеСведенийОСобранномУрожаеЗЕРНО(ДокументОснование)
	
	МетаданныеДокумента = Метаданные.Документы.ВнесениеСведенийОСобранномУрожаеЗЕРНО;
	
	ЗаполнитьПоОснованиюДокументаЗЕРНО(МетаданныеДокумента, ДокументОснование);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоОснованиюДокументаФормированиеПартииПриПроизводствеЗЕРНО(ДокументОснование)

	МетаданныеДокумента = Метаданные.Документы.ФормированиеПартийПриПроизводствеЗЕРНО;
	
	ЗаполнитьПоОснованиюДокументаЗЕРНО(МетаданныеДокумента, ДокументОснование);

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоОснованиюДокументаЗЕРНО(МетаданныеДокументаЗЕРНО, ДокументОснование, ДобавитьГиперссылку = Истина)
	
	ШаблонТекстаЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка                               КАК Ссылка,
	|	СтатусыОбъектовСинхронизацииЗЕРНО.Статус              КАК Статус,
	|	СтатусыОбъектовСинхронизацииЗЕРНО.ДальнейшееДействие1 КАК ДальнейшееДействие1,
	|	СтатусыОбъектовСинхронизацииЗЕРНО.ДальнейшееДействие2 КАК ДальнейшееДействие2,
	|	СтатусыОбъектовСинхронизацииЗЕРНО.ДальнейшееДействие3 КАК ДальнейшееДействие3
	|ИЗ
	|	Документ.%1 КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыОбъектовСинхронизацииЗЕРНО КАК СтатусыОбъектовСинхронизацииЗЕРНО
	|		ПО СтатусыОбъектовСинхронизацииЗЕРНО.ОбъектСинхронизации = ТаблицаДокумента.Ссылка
	|		И СтатусыОбъектовСинхронизацииЗЕРНО.ИдентификаторСтроки = """"
	|ГДЕ
	|	ТаблицаДокумента.ДокументОснование = &ДокументОснование
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаДокумента.Дата";
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ШаблонТекстаЗапроса,
		МетаданныеДокументаЗЕРНО.Имя);
	
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	
	ТаблицаДокументы = ТаблицаДокументы();
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаДокументы.Добавить(), Выборка);
	КонецЦикла;
	
	ЗаполнитьПоДокументу(ТаблицаДокументы, Истина);
	
	Если НЕ ДобавитьГиперссылку Тогда
		Возврат;
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СтатусыОформленияДокументовЗЕРНО.Документ,
	|	СтатусыОформленияДокументовЗЕРНО.СтатусОформления КАК Статус,
	|	СтатусыОформленияДокументовЗЕРНО.Архивный КАК Архивный
	|ИЗ
	|	РегистрСведений.СтатусыОформленияДокументовЗЕРНО КАК СтатусыОформленияДокументовЗЕРНО
	|ГДЕ
	|	СтатусыОформленияДокументовЗЕРНО.Основание = &ДокументОснование";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	СтатусыОформления = Новый Структура;
	Пока Выборка.Следующий() Цикл
		СтатусыОформления.Вставить(Выборка.Документ.Метаданные().Имя, 
			Новый Структура("Статус, Архивный", Выборка.Статус, Выборка.Архивный));
	КонецЦикла;
	
	Если СтатусыОформления.Свойство(МетаданныеДокументаЗЕРНО.Имя)
		И СтатусыОформления[МетаданныеДокументаЗЕРНО.Имя].Статус <> Перечисления.СтатусыОформленияДокументовГосИС.Оформлено
		И Не СтатусыОформления[МетаданныеДокументаЗЕРНО.Имя].Архивный Тогда
		
		ПравоДобавления = ПравоДоступа("Добавление", МетаданныеДокументаЗЕРНО);
		
		Шаблон = ИнтеграцияЗЕРНО.ШаблонПредставленияДокументаДляПоляИнтеграции(
			МетаданныеДокументаЗЕРНО,
			ДокументОснование);
		
		Если Не ПравоДобавления Тогда
			ТекстНадписи = Шаблон.ДокументНеСоздан;
			ИмяКоманды   = Неопределено;
		Иначе
			ТекстНадписи = Шаблон.ПредставлениеКомандыСоздать;
			ИмяКоманды   = Шаблон.ИмяКомандыСоздать;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекстНадписи) Тогда
			
			ФорматированнаяСтрока = Новый ФорматированнаяСтрока(
				ТекстНадписи,
				,
				?(ЗначениеЗаполнено(ИмяКоманды), ЦветаСтиля.ГиперссылкаЦвет, Неопределено),
				,
				ИмяКоманды);
			
			Строки = Новый Массив;
			
			Если ЗначениеЗаполнено(ТекстДокументаЗЕРНО) Тогда
				Строки.Добавить(ТекстДокументаЗЕРНО);
				Строки.Добавить(", ");
			КонецЕсли;
			Строки.Добавить(ФорматированнаяСтрока);
			
			ТекстДокументаЗЕРНО = Новый ФорматированнаяСтрока(Строки);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоДокументу(ТаблицаДокументы, ОтображатьСИерархией = Ложь)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ВременнаяТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВременнаяТаблицаДокументы
	|ИЗ
	|	&ТаблицаДокументы КАК ВременнаяТаблицаДокументы
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗЕРНОПрисоединенныеФайлы.ДатаМодификацииУниверсальная       КАК ДатаМодификацииУниверсальная,
	|	ЗЕРНОПрисоединенныеФайлы.Документ                           КАК Документ,
	|	ЗЕРНОПрисоединенныеФайлы.Ссылка                             КАК Ссылка,
	|	ЗЕРНОПрисоединенныеФайлы.ТипСообщения                       КАК ТипСообщения,
	|	ЗЕРНОПрисоединенныеФайлы.Операция                           КАК Операция,
	|	ЗЕРНОПрисоединенныеФайлы.СтатусОбработки                    КАК СтатусОбработки,
	|	
	|	// Версия и Описание - из присоединенных файлов
	|	ВЫРАЗИТЬ(ЗЕРНОПрисоединенныеФайлы.Описание КАК Строка(255)) КАК Описание,
	|	ЗЕРНОПрисоединенныеФайлы.Версия                             КАК Версия,
	|	ЗЕРНОПрисоединенныеФайлы.ДополнительноеОписание             КАК ДополнительноеОписание,
	|	НЕОПРЕДЕЛЕНО                                                КАК РеквизитыИсходящегоСообщения,
	|	НЕОПРЕДЕЛЕНО                                                КАК ПротоколОбмена
	|ПОМЕСТИТЬ Сообщения
	|ИЗ
	|	ВременнаяТаблицаДокументы КАК ВременнаяТаблицаДокументы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЗЕРНОПрисоединенныеФайлы КАК ЗЕРНОПрисоединенныеФайлы
	|		ПО ЗЕРНОПрисоединенныеФайлы.Документ = ВременнаяТаблицаДокументы.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОчередьСообщенийЗЕРНО.ДатаСоздания   КАК ДатаМодификацииУниверсальная,
	|	ОчередьСообщенийЗЕРНО.СсылкаНаОбъект КАК Документ,
	|	ОчередьСообщенийЗЕРНО.Сообщение      КАК Ссылка,
	|	ВЫБОР КОГДА ОчередьСообщенийЗЕРНО.ТекущееДействие = ЗНАЧЕНИЕ(Перечисление.ДействиеССообщениемЗЕРНО.Отправка) ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовИС.Исходящий)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовИС.Входящий)
	|	КОНЕЦ                                КАК ТипСообщения,
	|	ОчередьСообщенийЗЕРНО.Операция       КАК Операция,
	|	
	|	ВЫБОР КОГДА ОчередьСообщенийЗЕРНО.ТекущееДействие = ЗНАЧЕНИЕ(Перечисление.ДействиеССообщениемЗЕРНО.Отправка) ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиСообщенийЗЕРНО.КПередаче)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиСообщенийЗЕРНО.ЗаявкаОбрабатывается)
	|	КОНЕЦ КАК СтатусОбработки,
	|	
	|	// Версия и Описание - из реквизитов исходящего сообщения
	|	""""                                               КАК Описание,
	|	НЕОПРЕДЕЛЕНО                                       КАК Версия,
	|	НЕОПРЕДЕЛЕНО                                       КАК ДополнительноеОписание,
	|	ОчередьСообщенийЗЕРНО.РеквизитыИсходящегоСообщения КАК РеквизитыИсходящегоСообщения,
	|	ОчередьСообщенийЗЕРНО.ПротоколОбмена               КАК ПротоколОбмена
	|ИЗ
	|	РегистрСведений.ОчередьСообщенийЗЕРНО КАК ОчередьСообщенийЗЕРНО
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаДокументы КАК ВременнаяТаблицаДокументы
	|		ПО ОчередьСообщенийЗЕРНО.СсылкаНаОбъект = ВременнаяТаблицаДокументы.Ссылка
	|ГДЕ
	|	(ОчередьСообщенийЗЕРНО.СообщениеОснование В (&ПустоеОснование)
	|		ИЛИ ОчередьСообщенийЗЕРНО.Операция В (&БазовыеОперации))
	|	И (НЕ ОчередьСообщенийЗЕРНО.Операция В (&АбстрактныеОперации)
	|		ИЛИ ОчередьСообщенийЗЕРНО.Операция В (&ОперацииОснования))
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаДокументы.Ссылка                                     КАК Документ,
	|	ЕСТЬNULL(Сообщения.Ссылка, &ПустаяСсылка)                            КАК Ссылка,
	|	ЕСТЬNULL(Сообщения.Операция, НЕОПРЕДЕЛЕНО)                           КАК Операция,
	|	ЕСТЬNULL(Сообщения.ДатаМодификацииУниверсальная, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаМодификацииУниверсальная,
	|	ЕСТЬNULL(Сообщения.Описание, """")                                   КАК Описание,
	|	ЕСТЬNULL(Сообщения.Версия, 1)                                        КАК Версия,
	|	ЕСТЬNULL(Сообщения.ДополнительноеОписание, """")                     КАК ДополнительноеОписание,
	|	ЕСТЬNULL(Сообщения.РеквизитыИсходящегоСообщения, НЕОПРЕДЕЛЕНО)       КАК РеквизитыИсходящегоСообщения,
	|	ЕСТЬNULL(Сообщения.ПротоколОбмена, НЕОПРЕДЕЛЕНО)                     КАК ПротоколОбмена
	|ИЗ
	|	ВременнаяТаблицаДокументы КАК ВременнаяТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Сообщения КАК Сообщения
	|		ПО Сообщения.Документ = ВременнаяТаблицаДокументы.Ссылка
	|УПОРЯДОЧИТЬ ПО
	|	Сообщения.ДатаМодификацииУниверсальная
	|ИТОГИ
	|	МАКСИМУМ(ДатаМодификацииУниверсальная)
	|ПО
	|	Документ
	|");
	
	ПустыеОснования = Новый Массив;
	ПустыеОснования.Добавить(Неопределено);
	ПустыеОснования.Добавить("");
	ПустыеОснования.Добавить(Справочники.ЗЕРНОПрисоединенныеФайлы.ПустаяСсылка());
	
	Запрос.УстановитьПараметр("ТаблицаДокументы",    ТаблицаДокументы);
	Запрос.УстановитьПараметр("ПустоеОснование",     ПустыеОснования);
	Запрос.УстановитьПараметр("ПустаяСсылка",        Справочники.ЗЕРНОПрисоединенныеФайлы.ПустаяСсылка());
	Запрос.УстановитьПараметр("АбстрактныеОперации", Перечисления.ВидыОперацийЗЕРНО.АбстрактныеОперации());
	Запрос.УстановитьПараметр("ОперацииОснования",   Перечисления.ВидыОперацийЗЕРНО.ОперацииОснования());
	Запрос.УстановитьПараметр("БазовыеОперации",     Перечисления.ВидыОперацийЗЕРНО.БазовыеОперации());
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВыборкаПоДокументам = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПоДокументам.Следующий() Цикл
		
		ПолноеИмя = ВыборкаПоДокументам.Документ.Метаданные().ПолноеИмя();
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
		ПоследовательностьОпераций = МенеджерОбъекта.ПоследовательностьОпераций(ВыборкаПоДокументам.Документ, Истина);
		
		Если ОтображатьСИерархией Тогда
			СтрокаПервогоУровня = ДеревоФайлов.ПолучитьЭлементы().Добавить();
			СтрокаПервогоУровня.Документ       = ВыборкаПоДокументам.Документ;
			СтрокаПервогоУровня.Представление  = ВыборкаПоДокументам.Документ;
			СтрокаПервогоУровня.ИндексКартинки = 5;
		Иначе
			СтрокаПервогоУровня = ДеревоФайлов;
		КонецЕсли;
		
		Операция          = Неопределено;
		ЕстьОшибка        = Ложь;
		ТребуетсяОжидание = Ложь;
		
		ВыборкаПоФайлам = ВыборкаПоДокументам.Выбрать();
		
		ТаблицаРасчетаКоличества = НоваяТаблицаРасчетаКоличестваОпераций();
		
		Пока ВыборкаПоФайлам.Следующий() Цикл
			
			Версия = Неопределено;
			Если ТипЗнч(ВыборкаПоФайлам.Ссылка) = Тип("Строка") Тогда
				Реквизиты = ВыборкаПоФайлам.РеквизитыИсходящегоСообщения.Получить();
				Если Реквизиты <> Неопределено Тогда
					Версия = Реквизиты.Версия;
				КонецЕсли;
			Иначе
				Версия = ВыборкаПоФайлам.Версия;
			КонецЕсли;
			
			НоваяСтрока            = ТаблицаРасчетаКоличества.Добавить();
			НоваяСтрока.Версия     = Версия;
			НоваяСтрока.Операция   = ВыборкаПоФайлам.Операция;
			НоваяСтрока.Ссылка     = ВыборкаПоФайлам.Ссылка;
			НоваяСтрока.Количество = 1;
			
		КонецЦикла;
		
		РассчитатьКоличествоДляПредставления(ТаблицаРасчетаКоличества);
		
		ВыборкаПоФайлам.Сбросить();
		
		Пока ВыборкаПоФайлам.Следующий() Цикл
			
			Операция = ВыборкаПоФайлам.Операция;
			
			Если НЕ ЗначениеЗаполнено(ВыборкаПоФайлам.Ссылка) Тогда
				// Если по документу еще не создано файлов
				Если ЗначениеЗаполнено(ПоследовательностьОпераций) Тогда
					Операция = ПоследовательностьОпераций[0].Операция;
				Иначе
					Операция = Неопределено;
				КонецЕсли;
			КонецЕсли;
			
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("Операция", Операция);
			НайденныеСтроки = ПоследовательностьОпераций.НайтиСтроки(ПараметрыОтбора);
			Если НайденныеСтроки.Количество() = 0 Тогда
				СтрокаПоследовательности = Неопределено;
			Иначе
				СтрокаПоследовательности = НайденныеСтроки[НайденныеСтроки.Количество() - 1];
			КонецЕсли;
			
			СтрокаВторогоУровня = СтрокаПервогоУровня.ПолучитьЭлементы().Добавить();
			СтрокаВторогоУровня.Документ       = ВыборкаПоФайлам.Документ;
			СтрокаВторогоУровня.Файл           = ВыборкаПоФайлам.Ссылка;
			СтрокаВторогоУровня.Операция       = Операция;
			Если ЗначениеЗаполнено(ВыборкаПоФайлам.ДатаМодификацииУниверсальная) Тогда
				СтрокаВторогоУровня.Дата = МестноеВремя(ВыборкаПоФайлам.ДатаМодификацииУниверсальная);
			КонецЕсли;

			Если ТипЗнч(ВыборкаПоФайлам.Ссылка) = Тип("Строка") Тогда
				РеквизитыИсходящегоСообщения = ВыборкаПоФайлам.РеквизитыИсходящегоСообщения.Получить();
				РеквизитыИсходящегоСообщения.Вставить("Ссылка",         ВыборкаПоФайлам.Ссылка);
				РеквизитыИсходящегоСообщения.Вставить("Операция",       ВыборкаПоФайлам.Операция);
				РеквизитыИсходящегоСообщения.Вставить("ПротоколОбмена", ВыборкаПоФайлам.ПротоколОбмена);
				
				СтрокаВторогоУровня.Представление = ПредставлениеОперации(
					СтрокаПоследовательности,
					ВыборкаПоФайлам.Документ,
					РеквизитыИсходящегоСообщения,
					ТаблицаРасчетаКоличества);
				ДополнитьПредставлениеОперации(СтрокаВторогоУровня, РеквизитыИсходящегоСообщения);
			Иначе
				СтрокаВторогоУровня.Представление = ПредставлениеОперации(
					СтрокаПоследовательности,
					ВыборкаПоФайлам.Документ,
					ВыборкаПоФайлам,
					ТаблицаРасчетаКоличества);
				ДополнитьПредставлениеОперации(СтрокаВторогоУровня, ВыборкаПоФайлам);
			КонецЕсли;
			
			ЕстьОшибкаСтроки  = ЗначениеЗаполнено(ВыборкаПоФайлам.Описание);
			ТребуетсяОжидание = ТипЗнч(ВыборкаПоФайлам.Ссылка) = Тип("Строка");
			
			Если Не ТребуетсяОжидание И СтрокаПоследовательности <> Неопределено Тогда
				Для Каждого ДальнейшееДействие Из СтрокаПоследовательности.ДальнейшиеДействия Цикл
					СтрокаВторогоУровня.ДальнейшиеДействия.Добавить(ДальнейшееДействие);
				КонецЦикла;
			КонецЕсли;
			
			Если ЕстьОшибкаСтроки Тогда
				СтрокаВторогоУровня.УсловноеОформление = "УсловноеОформлениеОшибка";
				СтрокаВторогоУровня.ИндексКартинки     = 4;
				ЕстьОшибка = Истина;
			ИначеЕсли ТипЗнч(ВыборкаПоФайлам.Ссылка) = Тип("Строка") Тогда
				СтрокаВторогоУровня.ИндексКартинки = 3;
			ИначеЕсли Не ЗначениеЗаполнено(ВыборкаПоФайлам.Ссылка) Тогда
				СтрокаВторогоУровня.УсловноеОформление = "УсловноеОформлениеСерый";
				СтрокаВторогоУровня.ИндексКартинки     = ИнтеграцияИС.ИндексКартинкиЗапроса(СтрокаПоследовательности, Истина);
			Иначе
				СтрокаВторогоУровня.ИндексКартинки = ИнтеграцияИС.ИндексКартинкиЗапроса(СтрокаПоследовательности);
			КонецЕсли;
			
		КонецЦикла;
		
		Если ЕстьОшибка Тогда
			СтрокаПоследовательности = ИнтеграцияИС.ПредыдущаяОперация(ПоследовательностьОпераций, СтрокаПоследовательности);
		КонецЕсли;
		
		Если СтрокаПоследовательности <> Неопределено Тогда
			
			Индекс = ПоследовательностьОпераций.Индекс(СтрокаПоследовательности);
			
			Для Итератор = Индекс + 1 По ПоследовательностьОпераций.Количество() - 1 Цикл
				
				СтрокаСледующаяОперация = ПоследовательностьОпераций.Получить(Итератор);
				Если СтрокаСледующаяОперация.Индекс = 0
					Или СтрокаПоследовательности.Индекс = СтрокаСледующаяОперация.Индекс Тогда
					
					СтрокаВторогоУровня = СтрокаПервогоУровня.ПолучитьЭлементы().Добавить();
					СтрокаВторогоУровня.Документ           = ВыборкаПоДокументам.Документ;
					СтрокаВторогоУровня.Операция           = СтрокаСледующаяОперация.Операция;
					СтрокаВторогоУровня.УсловноеОформление = "УсловноеОформлениеСерый";
					СтрокаВторогоУровня.Представление      = ПредставлениеОперации(СтрокаСледующаяОперация, СтрокаВторогоУровня.Документ, Неопределено);
					СтрокаВторогоУровня.ИндексКартинки     = ИнтеграцияИС.ИндексКартинкиЗапроса(СтрокаСледующаяОперация, Истина);
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоФайлов()
	
	ТекстДокументаЗЕРНО = "";
	ДеревоФайлов.ПолучитьЭлементы().Очистить();
	ТипДокумента = ТипЗнч(СсылкаНаОбъект);
	Строки = Новый Массив;
	Строки.Добавить(НСтр("ru = 'Основание:'"));
	Строки.Добавить(" ");
	Строки.Добавить(Новый ФорматированнаяСтрока(Строка(СсылкаНаОбъект),,,, ПолучитьНавигационнуюСсылку(СсылкаНаОбъект)));
	
	ТекстДокумент    = Новый ФорматированнаяСтрока(Строки);
	ЭтоДокументЗЕРНО = ОпределяемыеТипы().ДокументыЗЕРНО.Тип.Типы().Найти(ТипДокумента) <> Неопределено;
	
	Если ЭтоДокументЗЕРНО Тогда
		
		Строки = Новый Массив;
		Строки.Добавить(НСтр("ru = 'Документ:'"));
		Строки.Добавить(" ");
		Строки.Добавить(Новый ФорматированнаяСтрока(Строка(СсылкаНаОбъект),,,, ПолучитьНавигационнуюСсылку(СсылкаНаОбъект)));
		ТекстДокумент = Новый ФорматированнаяСтрока(Строки);
		
		ЗаполнитьПоДокументу(ТаблицаДокументы(СсылкаНаОбъект));
		Возврат;
		
	КонецЕсли;
	
	Если ОпределяемыеТипы().ОснованиеПогашениеСДИЗЗЕРНО.Тип.Типы().Найти(ТипДокумента) <> Неопределено Тогда
		
		ЗаполнитьПоОснованиюДокументаПогашениеСДИЗЗЕРНО(СсылкаНаОбъект);
		
	КонецЕсли;
	
	Если ОпределяемыеТипы().ОснованиеСписаниеПартийЗЕРНО.Тип.Типы().Найти(ТипДокумента) <> Неопределено Тогда
		
		ЗаполнитьПоОснованиюДокументаСписаниеПартийЗЕРНО(СсылкаНаОбъект);
		
	КонецЕсли;
	
	Если ОпределяемыеТипы().ОснованиеФормированиеПартииЗЕРНО.Тип.Типы().Найти(ТипДокумента) <> Неопределено Тогда
		
		ЗаполнитьПоОснованиюДокументаФормированиеПартииЗЕРНО(СсылкаНаОбъект);
		
	КонецЕсли;
	
	Если ОпределяемыеТипы().ОснованиеОформлениеСДИЗЗЕРНО.Тип.Типы().Найти(ТипДокумента) <> Неопределено Тогда
		
		ЗаполнитьПоОснованиюДокументаОформлениеСДИЗЗЕРНО(СсылкаНаОбъект);
		
	КонецЕсли;
	
	Если ОпределяемыеТипы().ОснованиеВнесениеСведенийОСобранномУрожаеЗЕРНО.Тип.Типы().Найти(ТипДокумента) <> Неопределено Тогда
		
		ЗаполнитьПоОснованиюДокументаВнесениеСведенийОСобранномУрожаеЗЕРНО(СсылкаНаОбъект);
	
	КонецЕсли;
	
	Если ОпределяемыеТипы().ОснованиеФормированиеПартииПриПроизводствеЗЕРНО.Тип.Типы().Найти(ТипДокумента) <> Неопределено Тогда
		
		ЗаполнитьПоОснованиюДокументаФормированиеПартииПриПроизводствеЗЕРНО(СсылкаНаОбъект);
	
	КонецЕсли;
	
	Элементы.ИнтеграцияЗЕРНО_ИнформацияОДокументах.Видимость = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ДополнитьПредставлениеОперации(СтрокаВторогоУровня, ПараметрыОбновления)
	Возврат;
КонецПроцедуры

#КонецОбласти

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ВыполнитьОбменОбработкаОжидания()
	
	ИнтеграцияЗЕРНОСлужебныйКлиент.ПродолжитьВыполнениеОбмена(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСообщенияОперации(ВыбраннаяСтрока)
	
	ДанныеСтроки = Элементы.ДеревоФайлов.ДанныеСтроки(ВыбраннаяСтрока);
	Если ДанныеСтроки <> Неопределено И ДанныеСтроки.ПолучитьЭлементы().Количество() = 0 Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Источник",  ДанныеСтроки.Файл);
		ПараметрыФормы.Вставить(
			"Заголовок",
			СтрШаблон(
				НСтр("ru = 'Сообщения операции: %1'"),
				ИнтеграцияЗЕРНОСлужебныйКлиентСервер.ОписаниеОперацииПередачиДанных(
					ДанныеСтроки.Операция)));
		
		ОткрытьФорму(
			"ОбщаяФорма.ЛогОбменаЗЕРНО",
			ПараметрыФормы,
			ЭтаФорма,,,,,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	Иначе
		
		ИнтеграцияИСКлиент.ПоказатьСообщенияОперации(ЭтаФорма, ИнтеграцияЗЕРНОКлиентСервер.ИмяПодсистемы(), ВыбраннаяСтрока);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
