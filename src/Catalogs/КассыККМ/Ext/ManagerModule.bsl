#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция выполняет получение параметров кассы ККМ.
//
// Параметры:
//  КассаККМ - СправочникСсылка.КассыККМ - Касса ККМ.
//
// Возвращаемое значение:
//  Структура со свойствами:
//   * ИдентификаторУстройства - Строка - Идентификатор устройства.
//   * ИспользоватьБезПодключенияОборудования - Булево - Использовать без подключения оборудования.
//   * ЭтоФискальныйРегистратор - Булево - Признак фискального регистратора.
//   * ИспользоватьСкладскиеПомещения - Булево - Признак использования складских помещений.
//   * КассаККМ - СправочникСсылка.КассыККМ - Касса ККМ.
//   * НастройкиРМК - СправочникСсылка.НастройкиРМК - Ссылка на настройки РМК.
//
Функция ПараметрыКассыККМ(КассаККМ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст ="ВЫБРАТЬ
	              |	КассыККМ.Ссылка КАК КассаККМ,
	              |	КассыККМ.Владелец КАК Организация,
	              |	КассыККМ.СерийныйНомер КАК СерийныйНомер,
	              |	ТорговоеОборудование.Идентификатор КАК ИдентификаторУстройства
	              |ИЗ
	              |	Справочник.КассыККМ КАК КассыККМ
	              |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТорговоеОборудование КАК ТорговоеОборудование
	              |		ПО КассыККМ.Ссылка = ТорговоеОборудование.КассаККМ
	              |			И (ТорговоеОборудование.Компьютер = &ИмяКомпьютера)
	              |ГДЕ
	              |	КассыККМ.Ссылка = &Ссылка"; 
	
	Запрос.УстановитьПараметр("Ссылка", КассаККМ);
	Запрос.УстановитьПараметр("ИмяКомпьютера", ИмяКомпьютера());
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ИдентификаторУстройства",                Справочники.ТорговоеОборудование.ПустаяСсылка());
	ВозвращаемоеЗначение.Вставить("ИспользоватьБезПодключенияОборудования", Истина);
	ВозвращаемоеЗначение.Вставить("ЭтоФискальныйРегистратор",               Ложь);
	ВозвращаемоеЗначение.Вставить("ИспользоватьСкладскиеПомещения",         Ложь);
	ВозвращаемоеЗначение.Вставить("Организация",                            Справочники.Организации.ПустаяСсылка());
	ВозвращаемоеЗначение.Вставить("КассаККМ",                               КассаККМ);
	ВозвращаемоеЗначение.Вставить("Валюта",                                 Константы.ВалютаРегламентированногоУчета.Получить());
	ВозвращаемоеЗначение.Вставить("ДоступнаПередачаДанных",                 Ложь);
	ВозвращаемоеЗначение.Вставить("НастройкиРМК",                           Неопределено);
	ВозвращаемоеЗначение.Вставить("СерийныйНомер",                          "");
	
	Если Выборка.Следующий() Тогда
		
		ВозвращаемоеЗначение.Организация                            = Выборка.Организация;
		ВозвращаемоеЗначение.КассаККМ                               = Выборка.КассаККМ;
		ВозвращаемоеЗначение.СерийныйНомер                          = Выборка.СерийныйНомер;
		ВозвращаемоеЗначение.ИдентификаторУстройства                = Выборка.ИдентификаторУстройства;
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

#КонецОбласти

#КонецОбласти

#КонецЕсли
