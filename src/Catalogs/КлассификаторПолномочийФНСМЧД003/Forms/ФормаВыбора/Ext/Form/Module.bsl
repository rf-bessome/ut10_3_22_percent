#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.КодКлассификатора) Тогда
		
		Элементы.Список.ТекущаяСтрока = Справочники.КлассификаторПолномочийФНСМЧД003.НайтиПолномочиеПоКоду(
			Параметры.КодКлассификатора);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Структура = Новый Структура;
		Структура.Вставить("КодКлассификатора", ТекущиеДанные.КодКлассификатора);
		Структура.Вставить("Полномочие",        ТекущиеДанные.Полномочие);
		Структура.Вставить("Мнемоника",         ТекущиеДанные.Мнемоника);
		
		Закрыть(Структура);
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьКлассификатор(Команда)
		
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриПолученииОтветаНаВопросОЗагрузкеКлассификатораФНС_МЧД003",
		ЭтаФорма);
	ЗаголовокВопроса = НСтр("ru = 'Обновление классификатора'");
	ТекстВопроса = НСтр("ru = 'Обновить классификатор полномочий ФНС (МЧД 003) сейчас?'");
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить(КодВозвратаДиалога.Да);
	Кнопки.Добавить(КодВозвратаДиалога.Отмена);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки, , Кнопки[0].Значение, ЗаголовокВопроса);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПриПолученииОтветаНаВопросОЗагрузкеКлассификатораФНС_МЧД003(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьКлассификаторНаСервере();
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьКлассификаторНаСервере()
	
	ПараметрыОперации = МашиночитаемыеДоверенности.ЗаполнитьПараметрыСинхронизацииКлассификатораПолномочийФНС_МЧД003();
	
	МашиночитаемыеДоверенности.СинхронизироватьКлассификаторПолномочийФНС_МЧД003(ПараметрыОперации);
	
КонецПроцедуры

#КонецОбласти