
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка 
		ИЛИ ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	Если КодСуществует() Тогда
		
		ТекстСообщения = НСтр("ru = 'Код ""%1"" уже существует.'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Код);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка,, "Объект", Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

Функция КодСуществует()
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Код", Код);
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	1 КАК Поле1
	|ИЗ
	|	Справочник.КодыОперацийРаздела7ДекларацииПоНДС КАК КодыОперацийРаздела7ДекларацииПоНДС
	|ГДЕ
	|	КодыОперацийРаздела7ДекларацииПоНДС.Код = &Код
	|	И КодыОперацийРаздела7ДекларацииПоНДС.Ссылка <> &Ссылка
	|	И НЕ КодыОперацийРаздела7ДекларацииПоНДС.ПометкаУдаления"; 
	
	СтатусВозврата = НЕ Запрос.Выполнить().Пустой();
	
	Возврат СтатусВозврата;
	
КонецФункции

