// @strict-types

#Область ОбработчикиСобытий

// Выгружает доверенность в файл.
// 
// Параметры:
//  ПараметрКоманды - СправочникСсылка.МЧД003
//  ПараметрыВыполненияКоманды - ПараметрыВыполненияКоманды
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ДанныеВыгрузки = ДанныеВыгрузкиДоверенности(ПараметрКоманды);
	Если ДанныеВыгрузки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеСохраненияДоверенностиВФайл", ЭтотОбъект, ПараметрКоманды);
	
	СтруктураИмениФайла = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ДанныеВыгрузки.ИмяФайла);
	
	ДанныеФайла = Новый Структура("Расширение, ПолноеНаименование, АдресХранилища");
	ДанныеФайла.Расширение = СтрЗаменить(СтруктураИмениФайла.Расширение, ".", "");
	ДанныеФайла.ПолноеНаименование = СтруктураИмениФайла.ИмяБезРасширения;
	ДанныеФайла.АдресХранилища = ДанныеВыгрузки.Адрес;
	
	ВыбранныйПуть = ЭлектронныеДокументыСлужебныйКлиент.СохранитьКак(ДанныеФайла);
	
	Если ЗначениеЗаполнено(ВыбранныйПуть) Тогда
		ПолученныеФайлы = Новый Структура("ПолноеИмя");
		ПолученныеФайлы.ПолноеИмя = ВыбранныйПуть;
		ВыполнитьОбработкуОповещения(Оповещение, МашиночитаемыеДоверенностиКлиентСервер.ЗначениеВМассиве(ПолученныеФайлы));
	КонецЕсли;
	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Формирует архив с данными доверенности и помещает во временное хранилище.
// 
// Параметры:
//  Ссылка - СправочникСсылка.МЧД003
// 
// Возвращаемое значение:
//  Неопределено, Структура - результат сохранения:
// * Адрес - Строка
// * ИмяФайла - Строка
&НаСервере
Функция ДанныеВыгрузкиДоверенности(Знач Ссылка)
	
	РезультатВыгрузки = Справочники.МЧД003.ВыгрузитьДанныеДоверенности(Ссылка);
	
	Если РезультатВыгрузки.Ошибка Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(РезультатВыгрузки.ТекстОшибки);
		Возврат Неопределено;
	КонецЕсли;
	
	АдресФайла = ПоместитьВоВременноеХранилище(РезультатВыгрузки.ОписаниеФайла.ДвоичныеДанные);
	
	ПараметрыАрхива = Новый Структура;
	ПараметрыАрхива.Вставить("Адрес", АдресФайла);
	ПараметрыАрхива.Вставить("ИмяФайла", РезультатВыгрузки.ОписаниеФайла.ИмяФайла);
	
	Возврат ПараметрыАрхива;
	
КонецФункции

// Продолжение метода см. ФайловаяСистемаКлиент.СохранитьФайл.
// 
// Параметры:
//  ПолученныеФайлы - Массив из ОписаниеПереданногоФайла
//                  - Неопределено
//  Ссылка - СправочникСсылка.МЧД003
&НаКлиенте
Процедура ПослеСохраненияДоверенностиВФайл(ПолученныеФайлы, Ссылка) Экспорт
	
	Если ЗначениеЗаполнено(ПолученныеФайлы) Тогда
		ШаблонСообщения =
			НСтр("ru = 'Доверенность сохранена в файл %1 и может быть передана контрагентам любым удобным способом.'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, ПолученныеФайлы[0].ПолноеИмя);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		ЗаписатьФактВыгрузкиДоверенностиВФайл(Ссылка);
	КонецЕсли;
	
КонецПроцедуры

// Записывает факт выгрузки доверенности в файл в журнал регистрации.
// 
// Параметры:
//  Ссылка - СправочникСсылка.МЧД003
&НаСервере
Процедура ЗаписатьФактВыгрузкиДоверенностиВФайл(Знач Ссылка)
	
	Комментарий = НСтр("ru = 'Выгрузка машиночитаемой доверенности в файл.'");
	ЭлектронныеДокументыСлужебныйВызовСервера.ЗаписатьВЖурналРегистрации(Комментарий,
		1, "Информация", Ссылка);
	
КонецПроцедуры

#КонецОбласти
