
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИННДоверителяНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ИННДоверителяНачалоВыбораЗавершение", ЭтаФорма);
	ПоказатьВводЗначения(ОписаниеОповещения, Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ИННДоверителя = ПолучитьИННОрганизации(Организация);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Загрузить(Команда)
	
	Если НЕ СохранениеВозможно() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("НомерДоверенности", 	НомерДоверенности);
	СтруктураДанных.Вставить("Организация", 		Организация);
	СтруктураДанных.Вставить("ИННДоверителя", 		ИННДоверителя);
	
	Закрыть(СтруктураДанных);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ИННДоверителяНачалоВыбораЗавершение(Значение, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Значение) Тогда
		ИННДоверителя = ПолучитьИННОрганизации(Значение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция СохранениеВозможно()
	
	Отказ = Ложь;
	
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ИННДоверителя) Тогда
		ТекстСообщения = НСтр("ru = 'Не задан ИНН доверителя.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ИННДоверителя", , Отказ);
	КонецЕсли;
	
	Возврат НЕ Отказ;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьИННОрганизации(Организация)
	
	Результат = "";
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ИмяРеквизитаИННОрганизации = ЭлектронныеДокументыПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("ИННОрганизации");
		
		Результат = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация,	ИмяРеквизитаИННОрганизации);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
