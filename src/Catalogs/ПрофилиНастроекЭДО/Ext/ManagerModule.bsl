#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Обработчик обновления на БЭД 1.1.22.5
// Заполняет реквизиты табличных частей справочников "Профили настроек ЭДО"
// и "Соглашение об использовании ЭДО".
//
Процедура ЗаполнитьРегламентЭДО() Экспорт
	
	АктуальныеВидыЭД = Новый Массив();
	ЭлектронныеДокументыПереопределяемый.ПолучитьАктуальныеВидыЭД(АктуальныеВидыЭД);
	СтрокиКУдалению = Новый Массив();

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПрофилиНастроекЭДО.Ссылка КАК Профиль
	|ИЗ
	|	Справочник.ПрофилиНастроекЭДО КАК ПрофилиНастроекЭДО";
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СтрокиКУдалению.Очистить();
		
		Профиль = Выборка.Профиль;
		ПрофильОбъект = Профиль.ПолучитьОбъект();
		ИсходящиеДокументы = ПрофильОбъект.ИсходящиеДокументы;
		Для Каждого ТекСтрока Из ИсходящиеДокументы Цикл
			
			ТекСтрока.ТребуетсяИзвещениеОПолучении = Истина;
			ТекСтрока.ТребуетсяОтветнаяПодпись = ТекСтрока.ИспользоватьЭЦП;
			
			Если ТекСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.АктНаПередачуПрав Тогда
				ТекСтрока.Приоритет = 1;
			КонецЕсли;
			Если ТекСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.АктИсполнитель Тогда
				ТекСтрока.Приоритет = 2;
			КонецЕсли;
			Если ТекСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.ТОРГ12Продавец Тогда
				ТекСтрока.Приоритет = 3;
			КонецЕсли;
			Если ТекСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.СчетФактура Тогда
				ТекСтрока.ТребуетсяОтветнаяПодпись = Ложь;
				ТекСтрока.Приоритет = 4;
			КонецЕсли;
			Если ТекСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель Тогда
				ТекСтрока.Приоритет = 5;
			КонецЕсли;
			Если ТекСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.КорректировочныйСчетФактура Тогда
				ТекСтрока.ТребуетсяОтветнаяПодпись = Ложь;
				ТекСтрока.Приоритет = 6;
			КонецЕсли;
			Если ТекСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.ПроизвольныйЭД Тогда
				ТекСтрока.Приоритет = 7;
			КонецЕсли;
			Если ТекСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.КаталогТоваров Тогда
				ТекСтрока.Приоритет = 10;
			КонецЕсли;
			Если ТекСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.СчетНаОплату Тогда
				ТекСтрока.ТребуетсяОтветнаяПодпись = Ложь;
				ТекСтрока.Приоритет = 11;
			КонецЕсли;
			Если ТекСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.ПрайсЛист Тогда
				ТекСтрока.Приоритет = 12;
			КонецЕсли;
			Если ТекСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.ЗаказТовара Тогда
				ТекСтрока.Приоритет = 13;
			КонецЕсли;
			Если ТекСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.ОтветНаЗаказ Тогда
				ТекСтрока.Приоритет = 14;
			КонецЕсли;
			Если ТекСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.ОтчетОПродажахКомиссионногоТовара Тогда
				ТекСтрока.Приоритет = 15;
			КонецЕсли;
			Если ТекСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.ОтчетОСписанииКомиссионногоТовара Тогда
				ТекСтрока.Приоритет = 16;
			КонецЕсли;
			Если ТекСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.СведенияОРеализацииКомиссионером Тогда
				ТекСтрока.Приоритет = 17;
			КонецЕсли;
			Если ТекСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.СведенияОЗакупкеКомиссионером Тогда
				ТекСтрока.Приоритет = 18;
			КонецЕсли;
			Если ТекСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.КорректировкаСведенийОРеализацииКомиссионером Тогда
				ТекСтрока.Приоритет = 19;
			КонецЕсли;
			Если ТекСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.КорректировкаСведенийОЗакупкеКомиссионером Тогда
				ТекСтрока.Приоритет = 20;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(ТекСтрока.ДокументУчета) Тогда
				ТекСтрока.ДокументУчета = ЭлектронныеДокументыПовтИсп.ПредставлениеОснованияДляВидаЭД(ТекСтрока.ИсходящийДокумент);
			КонецЕсли;
			
			Если АктуальныеВидыЭД.Найти(ТекСтрока.ИсходящийДокумент) = Неопределено Тогда
				СтрокиКУдалению.Добавить(ТекСтрока);
			КонецЕсли;
			
		КонецЦикла;
		
		Для Каждого УдаляемаяСтрока Из СтрокиКУдалению Цикл
			ИсходящиеДокументы.Удалить(УдаляемаяСтрока);
		КонецЦикла;
		
		ПрофильОбъект.ИсходящиеДокументы.Сортировать("Приоритет Возр");
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ПрофильОбъект);
		
		ПрофильОбъект.ИзменитьДанныеВСвязанныхНастройкахЭДО(ПрофильОбъект, Ложь);
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СоглашенияОбИспользованииЭД.Ссылка КАК Настройка
	|ИЗ
	|	Справочник.СоглашенияОбИспользованииЭД КАК СоглашенияОбИспользованииЭД
	|ГДЕ
	|	СоглашенияОбИспользованииЭД.РасширенныйРежимНастройкиСоглашения = ИСТИНА";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СтрокиКУдалению.Очистить();
		
		Соглашение = Выборка.Настройка;
		СоглашениеОбъект = Соглашение.ПолучитьОбъект();
		Для Каждого ТекСтрока Из СоглашениеОбъект.ИсходящиеДокументы Цикл
			
			ВидЭД = ТекСтрока.ИсходящийДокумент;
			ПрофильЭДО = ТекСтрока.ПрофильНастроекЭДО;
			
			СвойстваЭД = Новый Структура("ТребуетсяИзвещение, ТребуетсяОтветнаяПодпись, ИспользоватьЭЦП, ДокументУчета, Приоритет");
			ЗаполнитьСвойстваЭДИзПрофиля(ПрофильЭДО, ВидЭД, СвойстваЭД);
			
			ЗаполнитьЗначенияСвойств(ТекСтрока, СвойстваЭД);
			
			Если АктуальныеВидыЭД.Найти(ТекСтрока.ИсходящийДокумент) = Неопределено Тогда
				СтрокиКУдалению.Добавить(ТекСтрока);
			КонецЕсли;

		КонецЦикла;
		
		Для Каждого УдаляемаяСтрока Из СтрокиКУдалению Цикл
			СоглашениеОбъект.ИсходящиеДокументы.Удалить(УдаляемаяСтрока);
		КонецЦикла;

		СоглашениеОбъект.ИсходящиеДокументы.Сортировать("Приоритет Возр");
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(СоглашениеОбъект);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСвойстваЭДИзПрофиля(ПрофильЭДО, ВидЭД, СвойстваЭД)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПрофилиНастроекЭДОИсходящиеДокументы.ТребуетсяИзвещениеОПолучении КАК ТребуетсяИзвещение,
	|	ПрофилиНастроекЭДОИсходящиеДокументы.ТребуетсяОтветнаяПодпись КАК ТребуетсяОтветнаяПодпись,
	|	ПрофилиНастроекЭДОИсходящиеДокументы.ИспользоватьЭЦП КАК ИспользоватьЭЦП,
	|	ПрофилиНастроекЭДОИсходящиеДокументы.ДокументУчета,
	|	ПрофилиНастроекЭДОИсходящиеДокументы.Приоритет
	|ИЗ
	|	Справочник.ПрофилиНастроекЭДО.ИсходящиеДокументы КАК ПрофилиНастроекЭДОИсходящиеДокументы
	|ГДЕ
	|	ПрофилиНастроекЭДОИсходящиеДокументы.Ссылка = &ПрофильЭДО
	|	И ПрофилиНастроекЭДОИсходящиеДокументы.ИсходящийДокумент = &ВидЭД";
	
	Запрос.УстановитьПараметр("ПрофильЭДО", ПрофильЭДО);
	Запрос.УстановитьПараметр("ВидЭД", ВидЭД);
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(СвойстваЭД, Выборка);
	КонецЦикла;
	
КонецПроцедуры

// Обработчик обновления на БЭД 1.1.27.11
// Добавляет вид ЭД "АктОРасхождениях"
Процедура ДобавитьВидЭлектронногоДокумента_АктОРасхождениях() Экспорт
	
	ОбновлениеИнформационнойБазыЭД.ОбновитьНастройкиЭДО(Перечисления.ВидыЭД.АктОРасхождениях);
	
КонецПроцедуры

// Обработчик обновления на БЭД 1.1.27.34
// Изменение представления хоз. операции для вида "КаталогТоваров".
Процедура ПереименоватьДокументУчетаДляКаталогаТоваров() Экспорт
	
	НовоеНаименование = НСтр("ru = 'Каталог товаров (Настройка ЭДО)'");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИсходящиеДокументы.Ссылка
	|ИЗ
	|	Справочник.ПрофилиНастроекЭДО.ИсходящиеДокументы КАК ИсходящиеДокументы
	|ГДЕ
	|	ИсходящиеДокументы.ДокументУчета <> &НовоеНаименование
	|	И ИсходящиеДокументы.ИсходящийДокумент = &ВидКаталогТоваров";
	Запрос.УстановитьПараметр("НовоеНаименование", НовоеНаименование);
	Запрос.УстановитьПараметр("ВидКаталогТоваров", Перечисления.ВидыЭД.КаталогТоваров);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ОбработанныхОбъектов = 0;
	ПроблемныхОбъектов = 0;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		Попытка
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Справочник.ПрофилиНастроекЭДО");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
			
			Объект = Выборка.Ссылка.ПолучитьОбъект();
			Если Объект = Неопределено Тогда
				ЗафиксироватьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			Записать = Ложь;
			
			ОтборСтрок = Новый Структура("ИсходящийДокумент", Перечисления.ВидыЭД.КаталогТоваров);
			НайденныеСтроки = Объект.ИсходящиеДокументы.НайтиСтроки(ОтборСтрок);
			
			Для каждого СтрокаКаталога Из НайденныеСтроки Цикл
				Если СтрокаКаталога.ДокументУчета <> НовоеНаименование Тогда
					СтрокаКаталога.ДокументУчета = НовоеНаименование;
					Записать = Истина;
				КонецЕсли;
			КонецЦикла;
			
			Если Записать Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(Объект);
				ОбработанныхОбъектов = ОбработанныхОбъектов + 1;
			КонецЕсли;
			
 			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			
			ПодробныйТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЭлектронныеДокументыСлужебныйВызовСервера.ОбработатьОшибку(
				Нстр("ru='Переименование документа учета для каталога товаров в профилях ЭДО'"), ПодробныйТекстОшибки);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Если ОбработанныхОбъектов = 0 И ПроблемныхОбъектов <> 0 Тогда
		ШаблонСообщения = НСтр("ru = 'Не удалось переименовать документ учета для каталога товаров в профилях ЭДО (пропущены): %1'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ШаблонСообщения = НСтр("ru = 'Переименован документ учета для каталога товаров очередной порции профилей ЭДО: %1'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, ОбработанныхОбъектов);
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация,
			Метаданные.Справочники.ПрофилиНастроекЭДО,, ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик обновления на БЭД 1.1.28.18
// Устанавливает требование ответной подписи для вида ЭД ТОРГ12Продавец.
Процедура УстановитьТребованиеОтветнойПодписиДляТОРГ12() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИсходящиеДокументы.Ссылка
	|ИЗ
	|	Справочник.ПрофилиНастроекЭДО.ИсходящиеДокументы КАК ИсходящиеДокументы
	|ГДЕ
	|	ИсходящиеДокументы.ИсходящийДокумент = &ТОРГ12
	|	И НЕ ИсходящиеДокументы.ТребуетсяОтветнаяПодпись";
	Запрос.УстановитьПараметр("ТОРГ12", Перечисления.ВидыЭД.ТОРГ12Продавец);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ОбработанныхОбъектов = 0;
	ПроблемныхОбъектов = 0;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		Попытка
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Справочник.ПрофилиНастроекЭДО");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
			
			Объект = Выборка.Ссылка.ПолучитьОбъект();
			Если Объект = Неопределено Тогда
				ЗафиксироватьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			Записать = Ложь;
			
			ОтборСтрок = Новый Структура("ИсходящийДокумент", Перечисления.ВидыЭД.ТОРГ12Продавец);
			НайденныеСтроки = Объект.ИсходящиеДокументы.НайтиСтроки(ОтборСтрок);
			
			Для каждого Строка Из НайденныеСтроки Цикл
				Если Не Строка.ТребуетсяОтветнаяПодпись Тогда
					Строка.ТребуетсяОтветнаяПодпись = Истина;
					Записать = Истина;
				КонецЕсли;
			КонецЦикла;
			
			Если Записать Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(Объект);
				ОбработанныхОбъектов = ОбработанныхОбъектов + 1;
			КонецЕсли;
			
 			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			
			ПодробныйТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЭлектронныеДокументыСлужебныйВызовСервера.ОбработатьОшибку(
				Нстр("ru='Установка требования ответной подписи для товарной накладной в профилях ЭДО'"), ПодробныйТекстОшибки);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Если ОбработанныхОбъектов = 0 И ПроблемныхОбъектов <> 0 Тогда
		ШаблонСообщения = НСтр("ru = 'Не удалось установить требование ответной подписи для товарной накладной в профилях ЭДО (пропущены): %1'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ШаблонСообщения = НСтр("ru = 'Установлено требование ответной подписи для товарной накладной очередной порции профилей ЭДО: %1'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, ОбработанныхОбъектов);
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация,
			Метаданные.Справочники.ПрофилиНастроекЭДО,, ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик обновления на БЭД 1.1.29.11
// Добавляет виды ЭД "СведенияОЗакупкеКомиссионером", "СведенияОРеализацииКомиссионером",
// "КорректировкаСведенийОЗакупкеКомиссионером", "КорректировкаСведенийОРеализацииКомиссионером".
Процедура ДобавитьВидыЭлектронногоДокумента_СведенияОРеализацииЗакупкеКомиссионером() Экспорт
	
	ВидыЭД = Новый Массив;
	ВидыЭД.Добавить(Перечисления.ВидыЭД.СведенияОЗакупкеКомиссионером);
	ВидыЭД.Добавить(Перечисления.ВидыЭД.СведенияОРеализацииКомиссионером);
	ВидыЭД.Добавить(Перечисления.ВидыЭД.КорректировкаСведенийОЗакупкеКомиссионером);
	ВидыЭД.Добавить(Перечисления.ВидыЭД.КорректировкаСведенийОРеализацииКомиссионером);
	
	ОбновлениеИнформационнойБазыЭД.ОбновитьНастройкиЭДО(ВидыЭД);
	
КонецПроцедуры

// Обработчик обновления на БЭД 1.1.30.3
// Добавляет вид ЭД "МашиночитаемаяДоверенность"
Процедура ДобавитьВидЭлектронногоДокумента_МашиночитаемаяДоверенность() Экспорт
	
	ОбновлениеИнформационнойБазыЭД.ОбновитьНастройкиЭДО(Перечисления.ВидыЭД.МашиночитаемаяДоверенность);
	
КонецПроцедуры

// Обработчик обновления на БЭД 1.1.30.4
// Добавляет вид ЭД "ЭПЛ"
Процедура ДобавитьВидЭлектронногоДокумента_ЭлектронныйПутевойЛист() Экспорт
	
	ОбновлениеИнформационнойБазыЭД.ОбновитьНастройкиЭДО(Перечисления.ВидыЭД.ЭПЛ);
	
КонецПроцедуры

// Обработчик обновления на БЭД 1.1.30.5
// Добавляет вид ЭД "ЭПЛ"
Процедура ДобавитьВидЭлектронногоДокумента_ЭлектроннаяТранспортнаяНакладная() Экспорт
	
	ОбновлениеИнформационнойБазыЭД.ОбновитьНастройкиЭДО(Перечисления.ВидыЭД.ЭТрН);
	
КонецПроцедуры

// Обработчик обновления на БЭД 1.1.30.9
// Добавляет вид ЭД "АктСверкиВзаиморасчетов"
Процедура ДобавитьВидЭлектронногоДокумента_АктСверкиВзаиморасчетов() Экспорт
	
	ОбновлениеИнформационнойБазыЭД.ОбновитьНастройкиЭДО(Перечисления.ВидыЭД.АктСверкиВзаиморасчетов);
	
КонецПроцедуры

#КонецЕсли
