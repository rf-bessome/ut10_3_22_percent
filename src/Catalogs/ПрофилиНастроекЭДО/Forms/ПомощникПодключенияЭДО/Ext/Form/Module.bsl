
&НаКлиенте
Перем ПараметрыОбработчикаОжидания;

&НаКлиенте
Перем ФормаДлительнойОперации;

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.СтраницыПомощника.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	
	УстановитьПривилегированныйРежим(Истина);
	КонтекстАвторизации  = Константы.КонтекстАвторизации.Получить();
	КонтекстКриптографии = Константы.КонтекстКриптографии.Получить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Параметры.Свойство("Контрагент") Тогда
		Контрагент = Параметры.Контрагент;
	КонецЕсли;

	Если Параметры.Свойство("Организация") Тогда
		Организация = Параметры.Организация;
		Если ОбщегоНазначенияКлиентСервер.ПодсистемаСуществует("РегламентированнаяОтчетность") Тогда
			МодульРегламентированнаяОтчетность = ОбщегоНазначенияКлиентСервер.ОбщийМодуль("РегламентированнаяОтчетность");
			
			СтруктураПараметров = Новый Структура("ОтпечатокСертификатаПодписи, ЭлектроннаяПодписьВМоделиСервиса, КодНО, ТипКриптоПровайдера");
			МодульРегламентированнаяОтчетность.ЗаполнитьПараметрыДляФормыПодключенияКСервисуЭлектронныхДокументов(
				Организация, СтруктураПараметров);
			
			Если ЗначениеЗаполнено(СтруктураПараметров) Тогда
				ЭПВМоделиСервиса = Ложь;
				Если СтруктураПараметров.Свойство("ЭлектроннаяПодписьВМоделиСервиса", ЭПВМоделиСервиса)
					И ЭПВМоделиСервиса <> Истина Тогда
					СтруктураПараметров.Свойство("ОтпечатокСертификатаПодписи", Отпечаток);
				КонецЕсли;
				Если Не ЗначениеЗаполнено(КодНалоговогоОргана) Тогда
					КодНалоговогоОргана = СтруктураПараметров.КодНО;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	СпособыОбменаЭД = "";
	Если Параметры.Свойство("СпособыОбменаЭД", СпособыОбменаЭД) И ЗначениеЗаполнено(СпособыОбменаЭД) Тогда
		Если СпособыОбменаЭД.Количество() = 1 Тогда
			ИнициализироватьПодключениеКТакском = Ложь;
			
			СпособОбменаЭД = СпособыОбменаЭД[0];
			Если СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезОператораЭДОТакском Тогда
				ИнициализироватьПодключениеКТакском = Истина;
				// Уточняем, какой способ обмена используется для оператора "Такском"
				СпособОбменаВСервисе = ЭлектронныеДокументыСлужебный.СпособОбменаОператораЭДО("2AL");
				Если ЗначениеЗаполнено(СпособОбменаВСервисе) Тогда
					СпособОбменаЭД = СпособОбменаВСервисе;
				КонецЕсли;
			КонецЕсли;
			
			Элементы.СпособОбменаЭД.СписокВыбора.Добавить(СпособОбменаЭД);
			Если СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезОператораЭДОТакском Тогда
				Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаПодключенияК1СТакском;
				ЭтаФорма.Заголовок = Элементы.СтраницаПодключенияК1СТакском.Заголовок;
				
			ИначеЕсли СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезСервис1СЭДО Тогда
				Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаПодключенияК1СЭДО;
				ЭтаФорма.Заголовок = Элементы.СтраницаПодключенияК1СЭДО.Заголовок;
				
				Если Параметры.Свойство("ДополнительныеПараметры") И ЗначениеЗаполнено(Параметры.ДополнительныеПараметры)
					И ЗначениеЗаполнено(Отпечаток) Тогда
					
					ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры.ДополнительныеПараметры);
					Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаФоновая;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(КодРегиона) Тогда
					СформироватьАдрес();
				КонецЕсли;
				
				ПодключенныеОператорыЭДО1СЭДО(ИнициализироватьПодключениеКТакском);
				ТипРегистрации = 2;
			КонецЕсли;
		Иначе
			Для каждого Строка Из СпособыОбменаЭД Цикл
				Элементы.СпособОбменаЭД.СписокВыбора.Добавить(Строка);
			КонецЦикла;
			Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаНастройкиПрямогоОбмена;
			СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезЭлектроннуюПочту;
			ЭтаФорма.Заголовок = Элементы.СтраницаНастройкиПрямогоОбмена.Заголовок;
		КонецЕсли;
	КонецЕсли;
	
	УстановитьЗначенияПоУмолчанию();
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезОператораЭДОТакском") Тогда
		
		СогласенСУсловиями = Истина;
		ЭлектронныеДокументыКлиентПереопределяемый.ЗапроситьСогласиеСУсловиямиЛицензионногоСоглашения(СогласенСУсловиями);
		Если СогласенСУсловиями <> Истина Тогда
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезСервис1СЭДО") Тогда
		УстановитьОператорЭДОПоИдентификаторОрганизации();
	КонецЕсли;	
	
	Если (КонтекстАвторизации = ПредопределенноеЗначение("Перечисление.КонтекстыРаботыСЭД.НаКлиенте")
		ИЛИ КонтекстКриптографии = ПредопределенноеЗначение("Перечисление.КонтекстыРаботыСЭД.НаКлиенте"))
		И ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьЗначениеФункциональнойОпции("ИспользоватьЭлектронныеЦифровыеПодписи") Тогда
		
		Если Не ПодключитьРасширениеРаботыСКриптографией() Тогда
			Если Не ЭлектронныеДокументыСлужебныйКлиент.УстановитьРасширениеРаботыСКриптографиейНаКлиенте() Тогда
				Отказ = Истина;
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ИспользоватьЭП Тогда
		
		ПерсональныеНастройкиРаботыСЭЦП = ЭлектроннаяЦифроваяПодписьКлиент.ПолучитьПерсональныеНастройкиРаботыСЭЦП();
		
		ЗаполнитьЗначенияСвойств(
			ЭтаФорма,
			ПерсональныеНастройкиРаботыСЭЦП,
			"ПровайдерЭЦП, ТипПровайдераЭЦП, АлгоритмПодписи, АлгоритмХеширования, АлгоритмШифрования");
		
		Если ЗначениеЗаполнено(ПерсональныеНастройкиРаботыСЭЦП.ПровайдерЭЦП) Тогда
			ЗаполинитьПараметрыМенеджераКриптографии(ПерсональныеНастройкиРаботыСЭЦП.ПровайдерЭЦП, ПерсональныеНастройкиРаботыСЭЦП.ПутьМодуляКриптографии,
				ПерсональныеНастройкиРаботыСЭЦП.ТипПровайдераЭЦП, Ложь);
		Иначе
			// Переберем все имеющиеся. Возможно, настройки криптографии еще не устанавливали.
			МенеджерыКриптографии = Новый Массив;
			ДобавитьМенеджераКриптографииВСписок(МенеджерыКриптографии, "Infotecs Cryptographic Service Provider", "", 2);
			ДобавитьМенеджераКриптографииВСписок(МенеджерыКриптографии, "Crypto-Pro GOST R 34.10-2001 Cryptographic Service Provider", "", 75);
			ДобавитьМенеджераКриптографииВСписок(МенеджерыКриптографии, "Infotecs GOST 2012/512 Cryptographic Service Provider", "", 77);
			ДобавитьМенеджераКриптографииВСписок(МенеджерыКриптографии, "Crypto-Pro GOST R 34.10-2012 Cryptographic Service Provider", "", 80);
			
			Для Каждого Менеджер Из МенеджерыКриптографии Цикл
				ЗаполинитьПараметрыМенеджераКриптографии(Менеджер.ПровайдерЭЦП, Менеджер.ПутьМодуляКриптографии, Менеджер.ТипПровайдераЭЦП, Ложь);
				Если ЗначениеЗаполнено(ПровайдерЭЦП) Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
		Если ПровайдерЭЦП = "Infotecs Cryptographic Service Provider" Тогда
			ПрограммаЗащитыИнформации = "1";
		ИначеЕсли ПровайдерЭЦП = "Crypto-Pro GOST R 34.10-2001 Cryptographic Service Provider" Тогда
			ПрограммаЗащитыИнформации = "2";
		ИначеЕсли ПровайдерЭЦП = "Infotecs GOST 2012/512 Cryptographic Service Provider" Тогда
			ПрограммаЗащитыИнформации = "3";
		ИначеЕсли ПровайдерЭЦП = "Crypto-Pro GOST R 34.10-2012 Cryptographic Service Provider" Тогда
			ПрограммаЗащитыИнформации = "4";
		КонецЕсли;
		
		Если КонтекстАвторизации = ПредопределенноеЗначение("Перечисление.КонтекстыРаботыСЭД.НаСервере") Тогда
			ЗаполнитьСпискиВыбораНаСервере();
		Иначе
			ЗаполнитьСпискиВыбораНаКлиенте();
		КонецЕсли;
	КонецЕсли;
	
	Если ЭлектронныеДокументыСлужебныйКлиент.ПроверитьИспользованиеИнтернетПоддержкаПользователей() Тогда
		Элементы.ГруппаЗаполненияИдентификатораБезИПП.Видимость = Ложь;
	Иначе
		Элементы.ГруппаЗаполненияИдентификатораСИПП.Видимость = Ложь;
	КонецЕсли;
	
	ЭлектронныеДокументыСлужебныйКлиент.ЗаполнитьДанныеСлужбыПоддержки(ТелефонСлужбыПоддержки, АдресЭлектроннойПочтыСлужбыПоддержки);
	
	Если ЗначениеЗаполнено(Отпечаток) Тогда
		ТипРегистрации = 2;
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаПодключенияК1СЭДО;
		ЭтаФорма.Заголовок = Элементы.СтраницаПодключенияК1СЭДО.Заголовок;
	
		УправлениеФормой(ЭтаФорма);
		
		Если ЗначениеЗаполнено(ПровайдерЭЦП) Тогда
			СертификатДобавленИзХранилища = Истина;
			ЗагрузитьСертификат(Отпечаток);
			ПолучитьИдентификаторУчастникаОбменаЭДЧерез1СЭДО();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	// Задаем вопрос только в случае досрочного закрытия помощника.
	Если Не ЗначениеЗаполнено(СсылкаНаПрофильНастроек) Тогда
		
		ТекстВопроса = НСтр("ru = 'Введенные данные не будут сохранены.
							|Прервать работу помощника?'");
		
		КодВозврата = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
		Если КодВозврата = КодВозвратаДиалога.Нет Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	Если ЗначениеЗаполнено(СсылкаНаСертификат) Тогда
		УдалитьИзВременногоХранилища(СсылкаНаСертификат);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// Механизм получения уникального идентификатора передает уникальный идентификатор
	// в виде строки в параметре оповещения с именем события
	// "ОповещениеОПолученииУникальногоИдентификатораУчастникаОбменаЭД"
	Если ИмяСобытия = "ОповещениеОПолученииУникальногоИдентификатораУчастникаОбменаЭД" Тогда
		ИдентификаторОрганизации = СокрЛП(Параметр);
		
		Элементы.НадписьИдентификаторУчастникаОбменаЭД.Заголовок = ИдентификаторОрганизации;
		Элементы.НадписьИдентификаторУчастникаОбменаЭД.Гиперссылка = Ложь;
		ШрифтЭлемента = Элементы.НадписьИдентификаторУчастникаОбменаЭД.Шрифт;
		Элементы.НадписьИдентификаторУчастникаОбменаЭД.Шрифт = Новый Шрифт(ШрифтЭлемента, , , Истина);
		
		Если ТестПрофиляНастроекЭДО() Тогда
			СоздатьНовыйПрофиль();
		КонецЕсли
	КонецЕсли;
	
	Если ИмяСобытия = "ОповещениеОПолученииУникальногоИдентификатораУчастникаОбменаЭД1СЭДО" Тогда
		ИдентификаторОрганизации = СокрЛП(Параметр);
		Элементы.ИдентификаторОрганизацииЧерез1СЭДО.СписокВыбора.Добавить(ИдентификаторОрганизации);
		
		Элементы.НадписьИдентификаторУчастникаОбменаЭДЧерезЭДО.Заголовок = ИдентификаторОрганизации;
		Элементы.НадписьИдентификаторУчастникаОбменаЭДЧерезЭДО.Гиперссылка = Ложь;
		ШрифтЭлемента = Элементы.НадписьИдентификаторУчастникаОбменаЭДЧерезЭДО.Шрифт;
		Элементы.НадписьИдентификаторУчастникаОбменаЭДЧерезЭДО.Шрифт = Новый Шрифт(ШрифтЭлемента, , , Истина);
		
		Если ТестПрофиляНастроекЭДО() Тогда
			СоздатьНовыйПрофиль();
		КонецЕсли
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Если СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезОператораЭДОТакском") Тогда
		
		ИдентификаторОрганизации = "";
		Элементы.НадписьИдентификаторУчастникаОбменаЭД.Заголовок = НСтр("ru = 'Получить уникальный идентификатор участника обмена ЭД.'");
		ШрифтЭлемента = Элементы.НадписьИдентификаторУчастникаОбменаЭД.Шрифт;
		Элементы.НадписьИдентификаторУчастникаОбменаЭД.Шрифт = Новый Шрифт(ШрифтЭлемента, , , Ложь);
		Элементы.НадписьИдентификаторУчастникаОбменаЭД.Гиперссылка = Истина;
		
	ИначеЕсли СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезСервис1СЭДО") Тогда
		
		ИдентификаторОрганизации = "";
		ИдентификаторОрганизацииСуществующий = "";
		Элементы.ИдентификаторОрганизацииЧерез1СЭДО.СписокВыбора.Очистить();
		
		Элементы.НадписьИдентификаторУчастникаОбменаЭДЧерезЭДО.Заголовок = НСтр("ru = 'Получить уникальный идентификатор участника обмена ЭД.'");
		ШрифтЭлемента = Элементы.НадписьИдентификаторУчастникаОбменаЭДЧерезЭДО.Шрифт;
		Элементы.НадписьИдентификаторУчастникаОбменаЭДЧерезЭДО.Шрифт = Новый Шрифт(ШрифтЭлемента, , , Ложь);
		Элементы.НадписьИдентификаторУчастникаОбменаЭДЧерезЭДО.Гиперссылка = Истина;
		
		// Очистить адрес организации
		АдресОрганизации = "";
		Индекс           = "";
		Регион           = "";
		Район            = "";
		Город            = "";
		НаселенныйПункт  = "";
		Улица            = "";
		Дом              = "";
		Корпус           = "";
		Квартира         = "";
		КодРегиона       = "";
		КодНалоговогоОргана = "";
		
	Иначе
		
		Если ЗначениеЗаполнено(ИдентификаторОрганизации) Тогда
			ТекстВопроса = НСтр("ru = 'Была изменена организация. Изменить идентификатор обмена организации?'");
			Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
			Если Ответ = КодВозвратаДиалога.Да Тогда
				УстановитьИдентификатор("Организации", Организация);
			КонецЕсли;
		Иначе
			УстановитьИдентификатор("Организации", Организация);
		КонецЕсли;
	КонецЕсли;
	
	УстановитьЗначенияПоУмолчанию();
	Если СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезСервис1СЭДО") Тогда
		УстановитьОператорЭДОПоИдентификаторОрганизации();
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОператорЭДОПодключенныйК1СЭДОПриИзменении(Элемент)
	
	ОбновитьДанныеОператорЭДО();	
	
КонецПроцедуры

&НаКлиенте
Процедура ТипРегистрацииПриИзменении(Элемент)
	
	ИдентификаторОрганизацииСуществующий = "";
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособОбменаЭДПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьЭЦППриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(СертификатКриптографии) Тогда
		
		ТекстВопроса = НСтр("ru = 'Данные по сертификату будут очищены. Продолжить?'");
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			СброситьНастройкиСертификата();
			СертификатКриптографии = ПредопределенноеЗначение("Справочник.СертификатыЭЦП.ПустаяСсылка");
		Иначе
			// Вернем все как было.
			ИспользоватьЭП = Не ИспользоватьЭП;
		КонецЕсли;
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатЭПНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Не ЗначениеЗаполнено(ПровайдерЭЦП) Тогда
		ТекстСообщения = НСтр("ru = 'Выбранная программа защиты информации не установлена на Вашем компьютере.
									|Требуется обратиться к администратору.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
		
	Если ЭлектронныеДокументыСлужебныйВызовСервера.ВыполнятьАвторизациюНаСервере() Тогда
		ЗаполнитьПараметрыМенеджераКриптографииНаСервере(ПровайдерЭЦП, Неопределено, ТипПровайдераЭЦП, Истина);
	Иначе
		СкомпоноватьИнформациюМенеджераКриптографииНаКлиенте(ПровайдерЭЦП, Неопределено, ТипПровайдераЭЦП, Истина);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПровайдерЭЦП) Тогда
		СертификатДобавленИзХранилища = Истина;
		ЗагрузитьСертификат();
	КонецЕсли;

	Доверенность = "";
	УправлениеФормой(ЭтаФорма);
	
	ДоверенностьНачалоВыбора(Элементы.Доверенность, Неопределено, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоверенностьНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка) 

	Если Элементы.ГруппаВыбораМЧД.Видимость Тогда  
		СписокВыбора = Элементы.Доверенность.СписокВыбора;
		ДвоичныеДанныеСертификата = ПолучитьИзВременногоХранилища(СсылкаНаСертификат);
		СпискоДоверенностей = МашиночитаемыеДоверенностиВызовСервера.СписокВыбораДоверенностей(Организация, ДвоичныеДанныеСертификата);
		СписокВыбора.ЗагрузитьЗначения(СпискоДоверенностей);
		Если Не СтандартнаяОбработка И СпискоДоверенностей.Количество() = 1 Тогда 
			Доверенность = СпискоДоверенностей[0];
		КонецЕсли;
	КонецЕсли;	

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеВидимостьюДоверенности(Форма)
	
	ТребуетсяДоверенность = ТребуетсяДоверенность(Форма.СпособОбменаЭД, Форма.Организация, Форма.СсылкаНаСертификат, Форма.ОператорЭДОТребуетсяМЧД);
	Форма.Элементы.ГруппаВыбораМЧД.Видимость = ТребуетсяДоверенность;
	Форма.Элементы.Доверенность.АвтоОтметкаНезаполненного = ТребуетсяДоверенность;
	
	Если Не ТребуетсяДоверенность Тогда
		Форма.Доверенность = "";
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатЭПЧерезОператора1ПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(СертификатКриптографии) Тогда
		СсылкаНаСертификат = "";
		Доверенность = "";
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма); 
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатОткрытие(Элемент, СтандартнаяОбработка)
	
	Если СертификатДобавленИзХранилища И ЗначениеЗаполнено(СсылкаНаСертификат) Тогда
		СтандартнаяОбработка = Ложь;
		#Если НЕ ВебКлиент Тогда
			ВремФайл = ПолучитьИмяВременногоФайла("cer");
			ДвоичныеДанныеСертификата = ПолучитьИзВременногоХранилища(СсылкаНаСертификат);
			ДвоичныеДанныеСертификата.Записать(ВремФайл);
			ЗапуститьПриложение(ВремФайл);
		#КонецЕсли
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПровайдерЭЦПОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = "1" Тогда
		ПровайдерЭЦП     = "Infotecs Cryptographic Service Provider";
		ТипПровайдераЭЦП = 2;
	ИначеЕсли ВыбранноеЗначение = "2" Тогда
		ПровайдерЭЦП     = "Crypto-Pro GOST R 34.10-2001 Cryptographic Service Provider";
		ТипПровайдераЭЦП = 75;
	ИначеЕсли ВыбранноеЗначение = "3" Тогда
		ПровайдерЭЦП     = "Infotecs GOST 2012/512 Cryptographic Service Provider";
		ТипПровайдераЭЦП = 77;
	ИначеЕсли ВыбранноеЗначение = "4" Тогда
		ПровайдерЭЦП     = "Crypto-Pro GOST R 34.10-2012 Cryptographic Service Provider";
		ТипПровайдераЭЦП = 80;
	КонецЕсли;
	
	АлгоритмПодписи     = "";
	АлгоритмШифрования  = "";
	АдгоритмХеширования = "";
	Если ЭлектронныеДокументыСлужебныйВызовСервера.ВыполнятьАвторизациюНаСервере() Тогда
		ЗаполнитьСпискиВыбораНаСервере();
	Иначе
		ЗаполнитьСпискиВыбораНаКлиенте();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьИдентификаторУчастникаОбменаЭДНажатие(Элемент)
	
	Если Не ПроверитьПринятиеУсловийИспользования() Тогда
		Возврат;
	КонецЕсли;
	
	РезультатПроверки = РезультатПроверкиПередПолучениемИдентификатораТакском();
	
	Если Не РезультатПроверки.ДанныеСохранены Тогда
		Возврат;
	КонецЕсли;
	
	Если СертификатДобавленИзХранилища Тогда
		ТекСертификат = НовыйСертификатСсылка;
	Иначе
		ТекСертификат = СертификатКриптографии;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекСертификат) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Сертификат ключа электронной подписи"),
			,
			"СертификатКриптографии");
		Возврат;
	КонецЕсли;
	
	ПродолжитьПолучениеИД = Истина;	
	ЗадатьВопросПоРезультатамПроверки(РезультатПроверки, ПродолжитьПолучениеИД);
	
	Если ПродолжитьПолучениеИД Тогда
		ЭлектронныеДокументыКлиентПереопределяемый.СтартоватьМеханизмРаботыСОператоромЭДО(ТекСертификат,
																					  Организация,
																					  "taxcomGetID",
																					  ИдентификаторОрганизации,
																					  ПарольПользователя,
																					  ЭтаФорма.УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиНаСайтКриптоПроНажатие(Элемент)
	
	ПерейтиНаСайтКриптоПро();
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиНаСайтИнфотексНажатие(Элемент)
	
	ПерейтиНаСайтИнфотекс();
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиНаСайтКриптоПро()
	
	ЭлектронныеДокументыСлужебныйКлиент.ПопытатьсяПерейтиПоНавигационнойСсылке("http://www.cryptopro.ru/");
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиНаСайтИнфотекс()
	
	ЭлектронныеДокументыСлужебныйКлиент.ПопытатьсяПерейтиПоНавигационнойСсылке("http://infotecs.ru/");
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСоздатьУчетнуюЗаписьНажатие(Элемент)
	
	ОткрытьФорму("Справочник.УчетныеЗаписиЭлектроннойПочты.Форма.ФормаЭлемента");
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогВходящихДокументовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	КаталогОбмена(КаталогВходящихДокументов);
КонецПроцедуры

&НаКлиенте
Процедура FTPКаталогВходящихДокументовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	КаталогОбмена(FTPКаталогВходящихДокументов);
КонецПроцедуры

&НаКлиенте
Процедура НадписьИдентификаторУчастникаОбменаЭДЧерез1СЭДОНажатие(Элемент)
	
	Если Не ПроверитьПринятиеУсловийИспользования() Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	ПолучитьИдентификаторУчастникаОбменаЭДЧерез1СЭДО();
	
КонецПроцедуры

&НаКлиенте
Процедура АдресНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	// Подготовка данных и открытие формы для ввода адреса
	СтандартнаяОбработка = Ложь;
	ВыбратьАдрес(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура АдресОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыбратьАдрес(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьУсловияИспользованияНажатие(Элемент)
	
	ЭлектронныеДокументыСлужебныйКлиент.ОткрытьФормуПринятияУсловийИспользования();
	
КонецПроцедуры

&НаКлиенте
Процедура ТехподдержкаНажатие(Элемент)
	
	ЭлектронныеДокументыСлужебныйКлиент.ПопытатьсяПерейтиПоНавигационнойСсылке(ЭлектронныеДокументыСлужебныйКлиент.СсылкаДляОбращенияВСлужбуПоддержки());
	
КонецПроцедуры

&НаКлиенте
Процедура Техподдержка1Нажатие(Элемент)
	
	ЭлектронныеДокументыСлужебныйКлиент.ПопытатьсяПерейтиПоНавигационнойСсылке(ЭлектронныеДокументыСлужебныйКлиент.СсылкаДляОбращенияВСлужбуПоддержки());
	
КонецПроцедуры

&НаКлиенте
Процедура ИдентификаторОрганизацииЧерез1СЭДОПриИзменении(Элемент)
	
	УстановитьОператорЭДОПоИдентификаторОрганизации();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Готово(Команда)
	
	Если Не ПроверитьПринятиеУсловийИспользования() Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	ПолучитьСпособОбменаПоИдентификатору();
	
	Если ТестПрофиляНастроекЭДО(Истина) Тогда
		СоздатьНовыйПрофиль();
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПодсказкуПоЭП(Команда)
	
	ОткрытьФорму("Справочник.ПрофилиНастроекЭДО.Форма.ФормаПодсказкиПоЭП");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПодсказкуПоМЧД(Команда)     
	
	ОткрытьФорму("Справочник.ПрофилиНастроекЭДО.Форма.ФормаПодсказкиПоМЧД");
	
КонецПроцедуры


&НаКлиенте
Процедура ОткрытьСсылкуНаСтатьюПо1СБухфон(Команда)
	ЭлектронныеДокументыСлужебныйКлиент.ОткрытьИнструкциюПо1СБухфон();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура УстановитьОператорЭДОПоИдентификаторОрганизации()
	
	Если ТипРегистрации <> 1 Тогда
		Возврат;
	КонецЕсли;		
		
	Для Каждого Оператор Из ОператорыЭДО Цикл
		
		ИдентификаторОператора = Оператор.Идентификатор;
		ДлинаИдентификатораОператора = СтрДлина(ИдентификаторОператора);
		
		Если Лев(ИдентификаторОрганизацииСуществующий, ДлинаИдентификатораОператора) = ИдентификаторОператора Тогда
			НаименованиеОператораЭДО = Оператор.Наименование;

			ИскомыйЭлемент = Элементы.ОператорЭДОПодключенныйК1СЭДО.СписокВыбора.НайтиПоЗначению(НаименованиеОператораЭДО);
			ОператорЭДО = ИскомыйЭлемент.Значение;
			
			ОбновитьДанныеОператорЭДО();
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры 

&НаКлиенте
Процедура ОбновитьДанныеОператорЭДО()
	
	ПодключенныеОператорыЭДО1СЭДО();
	
	УправлениеВидимостьюДоверенности(ЭтаФорма);
	
	Если Элементы.ГруппаВыбораМЧД.Видимость И Не ЗначениеЗаполнено(Доверенность) Тогда
		ДоверенностьНачалоВыбора(Элементы.Доверенность, Неопределено, Ложь);		
	КонецЕсли;	
	
КонецПроцедуры


&НаКлиентеНаСервереБезКонтекста
Функция ТребуетсяДоверенность(СпособОбменаЭД, Организация, ДанныеСертификата, ОператорЭДОТребуетсяМЧД)
	
	Если ОператорЭДОТребуетсяМЧД
		И СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезСервис1СЭДО") 
		И ЗначениеЗаполнено(Организация)
		И ЗначениеЗаполнено(ДанныеСертификата) Тогда
		
		Если ТипЗнч(ДанныеСертификата) = Тип("ДвоичныеДанные") Тогда
			Сертификат = ДанныеСертификата;
		Иначе
		    Сертификат = ПолучитьИзВременногоХранилища(ДанныеСертификата);
		КонецЕсли;
		
		Возврат ЭлектронныеДокументыСлужебныйВызовСервера.ТребуетсяМашиночитаемаяДоверенность(Организация, Сертификат);
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ДобавитьМенеджераКриптографииВСписок(МенеджерыКриптографии, ПровайдерЭЦП, ПутьМодуляКриптографии, ТипПровайдераЭЦП)

	МенеджерыКриптографии.Добавить(Новый Структура("ПровайдерЭЦП, ПутьМодуляКриптографии, ТипПровайдераЭЦП", ПровайдерЭЦП, ПутьМодуляКриптографии, ТипПровайдераЭЦП));

КонецПроцедуры

&НаСервере
Функция РезультатПроверкиПередПолучениемИдентификатораТакском()
	
	Результат = Новый Структура("ДанныеСохранены, ЕстьДействующийПрофиль,ИННОтличается, КППОтличается", Истина, Ложь, Ложь, Ложь);
	
	СохранитьНастройкиКриптосредстваИСертификата(Результат.ДанныеСохранены);
	Результат.ЕстьДействующийПрофиль = ЕстьДействующийПрофиль(Организация, СпособОбменаЭД);
	
	Возврат Результат;

КонецФункции

&НаСервереБезКонтекста
Функция ЕстьДействующийПрофиль(Организация, СпособОбменаЭД)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СтатусПрисоединен", Перечисления.СтатусыУчастниковОбменаЭД.Присоединен);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СпособОбменаЭД", СпособОбменаЭД);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПрофилиНастроекЭДО.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СоглашенияОбИспользованииЭД.ИсходящиеДокументы КАК СоглашенияОбИспользованииЭДИсходящиеДокументы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПрофилиНастроекЭДО КАК ПрофилиНастроекЭДО
	|		ПО СоглашенияОбИспользованииЭДИсходящиеДокументы.ПрофильНастроекЭДО = ПрофилиНастроекЭДО.Ссылка
	|			И (СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.СтатусПодключения = &СтатусПрисоединен)
	|			И (ПрофилиНастроекЭДО.ПометкаУдаления = ЛОЖЬ)
	|			И (ПрофилиНастроекЭДО.Организация = &Организация)
	|			И (ПрофилиНастроекЭДО.СпособОбменаЭД = &СпособОбменаЭД)";
	Возврат Не Запрос.Выполнить().Пустой();

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	ЗначениеФОИспользоватьЭП = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьЗначениеФункциональнойОпции("ИспользоватьЭлектронныеЦифровыеПодписи");
	Элементы.ГруппаЧерезЭлектроннуюПочтуНастройкаКриптографии.Видимость = ЗначениеФОИспользоватьЭП;
	Элементы.ГруппаЧерезКаталогНастройкаКриптографии.Видимость          = ЗначениеФОИспользоватьЭП;
	Элементы.ГруппаЧерезFTPНастройкаКриптографии.Видимость              = ЗначениеФОИспользоватьЭП;
	
	Элементы.СертификатЭПЧерезЭлектроннуюПочту.Доступность = Форма.ИспользоватьЭП;
	Элементы.ПровайдерЭПЧерезЭлектроннуюПочту.Доступность  = Форма.ИспользоватьЭП;
	Элементы.СертификатЭПЧерезКаталог.Доступность          = Форма.ИспользоватьЭП;
	Элементы.ПровайдерЭПЧерезКаталог.Доступность           = Форма.ИспользоватьЭП;
	Элементы.СертификатЭПЧерезFTP.Доступность              = Форма.ИспользоватьЭП;
	Элементы.ПровайдерЭПЧерезFTP.Доступность               = Форма.ИспользоватьЭП;
	
	Если Форма.СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезЭлектроннуюПочту") Тогда
		Элементы.ГруппаНастройкиУчастника.ТекущаяСтраница = Элементы.ГруппаНастроекУчастникаЧерезЭлектроннуюПочту;
	ИначеЕсли Форма.СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезКаталог") Тогда
		Элементы.ГруппаНастройкиУчастника.ТекущаяСтраница = Элементы.ГруппаНастроекУчастникаЧерезКаталог;
	ИначеЕсли Форма.СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезFTP") Тогда
		Элементы.ГруппаНастройкиУчастника.ТекущаяСтраница = Элементы.ГруппаНастроекУчастникаЧерезFTP;
	ИначеЕсли Форма.СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезСервис1СЭДО") Тогда
		Элементы.ИдентификаторОрганизацииЧерез1СЭДО.Доступность = Форма.ТипРегистрации = 1;
		Элементы.ГруппаРегистрацииНовойУчетнойЗаписиДанные.Доступность = Форма.ТипРегистрации = 2;
		Элементы.ЭлектроннаяПочта.Доступность = Форма.ТипРегистрации = 2;
	КонецЕсли;
	
	УправлениеВидимостьюДоверенности(Форма);

	Если ОбщегоНазначенияКлиентСервер.ЭтоПлатформа83() Тогда
		Элементы.СертификатЭПЧерезОператора.КнопкаСоздания = Ложь;
		Элементы.СертификатЭПЧерезЭлектроннуюПочту.КнопкаСоздания = Ложь;
		Элементы.СертификатЭПЧерезКаталог.КнопкаСоздания = Ложь;
		Элементы.СертификатЭПЧерезFTP.КнопкаСоздания = Ложь;
 	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗначенияПоУмолчанию()
	
	ИспользоватьЭП = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьЗначениеФункциональнойОпции("ИспользоватьЭлектронныеЦифровыеПодписи");
	СертификатКриптографии = Справочники.СертификатыЭЦП.ПустаяСсылка();
	СсылкаНаСертификат = "";
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		ЭлектронныеДокументыСлужебныйВызовСервера.ОпределитьОрганизацию(Организация);
	КонецЕсли;
	
	Если СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезСервис1СЭДО
		И ЗначениеЗаполнено(Организация) ТОгда
		
		ПолучитьИдентификаторыНаСервисе1СЭДО();
		ТипРегистрации = ?(ЗначениеЗаполнено(ИдентификаторОрганизацииСуществующий), 1, 2);
		
		ЗаполнитьРеквизитыОрганизацииДляРегистрационногоПакета();
		СформироватьАдрес();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Организация)
		И (СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезFTP
			ИЛИ  СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезКаталог
			ИЛИ СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезЭлектроннуюПочту) Тогда
			
		УстановитьИдентификатор("Организации", Организация);
	КонецЕсли;
	
	Если СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезЭлектроннуюПочту
		И НЕ ЗначениеЗаполнено(ЭлектроннаяПочтаОрганизации) Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
		|	УчетныеЗаписиЭлектроннойПочты.Ссылка
		|ИЗ
		|	Справочник.УчетныеЗаписиЭлектроннойПочты КАК УчетныеЗаписиЭлектроннойПочты
		|ГДЕ
		|	НЕ УчетныеЗаписиЭлектроннойПочты.ПометкаУдаления
		|	И УчетныеЗаписиЭлектроннойПочты.ИспользоватьДляОтправки
		|	И УчетныеЗаписиЭлектроннойПочты.ИспользоватьДляПолучения");
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Количество() = 1 И Выборка.Следующий() Тогда
			ЭлектроннаяПочтаОрганизации = Выборка.Ссылка;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(КонтекстАвторизации) Тогда
		КонтекстАвторизации = ПредопределенноеЗначение("Перечисление.КонтекстыРаботыСЭД.НаКлиенте");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(КонтекстКриптографии) Тогда
		КонтекстКриптографии = ПредопределенноеЗначение("Перечисление.КонтекстыРаботыСЭД.НаКлиенте");
	КонецЕсли;
	
	Если ИспользоватьЭП Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СертификатыЭЦП.Ссылка
		|ИЗ
		|	Справочник.СертификатыЭЦП КАК СертификатыЭЦП
		|ГДЕ
		|	СертификатыЭЦП.Организация = &Организация
		|	И НЕ СертификатыЭЦП.ПометкаУдаления
		|	И НЕ СертификатыЭЦП.Отозван";
		Запрос.УстановитьПараметр("Организация", Организация);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Количество() = 1 И Выборка.Следующий() Тогда
			СертификатКриптографии = Выборка.Ссылка;
			ЗаполнитьРеквизитыСертификатаНаСервере(СертификатКриптографии.Отпечаток);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогОбмена(ПутьККаталогу)
	
	ОбщегоНазначенияКлиент.ПредложитьУстановкуРасширенияРаботыСФайлами();
	
	Если ПодключитьРасширениеРаботыСФайлами() Тогда
		
		ДиалогКаталога = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
		ДиалогКаталога.Заголовок = НСтр("ru = 'Выберите сетевой каталог для обмена'");
		ДиалогКаталога.Каталог   = ПутьККаталогу;
		Если ДиалогКаталога.Выбрать() Тогда
			ПутьККаталогу = ДиалогКаталога.Каталог;
			ЭлектронныеДокументыСлужебныйКлиент.ПроверитьДоступностьКаталогаДляПрямогоОбмена(ПутьККаталогу);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьИдентификатор(ИмяСправочника, СсылкаНаИсточникИдентификатора)
	
	Если ИмяСправочника = "Организации" Тогда
		ИмяРеквизитаИННОрганизации = ЭлектронныеДокументыПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("ИННОрганизации");
		ИмяРеквизитаКППОрганизации = ЭлектронныеДокументыПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("КППОрганизации");
		
		ПараметрыОрганиазции = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаИсточникИдентификатора,
			ИмяРеквизитаИННОрганизации + ", " + ИмяРеквизитаКППОрганизации);
		
		СтрокаЗаполнения = Строка(ПараметрыОрганиазции[ИмяРеквизитаИННОрганизации])
			+ "_" + Строка(ПараметрыОрганиазции[ИмяРеквизитаКППОрганизации]);
		Если Прав(СтрокаЗаполнения, 1) = "_" Тогда
			СтрокаЗаполнения = СтрЗаменить(СтрокаЗаполнения, "_", "");
		КонецЕсли;
		ИдентификаторОрганизации = СокрЛП(СтрокаЗаполнения);
		
	ИначеЕсли ИмяСправочника = "Контрагенты" Тогда
		ИмяРеквизитаИННКонтрагента = ЭлектронныеДокументыПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("ИННКонтрагента");
		ИмяРеквизитаКППКонтрагента = ЭлектронныеДокументыПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("КППКонтрагента");
		
		ПараметрыКонтрагента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаИсточникИдентификатора,
			ИмяРеквизитаИННКонтрагента + ", " + ИмяРеквизитаКППКонтрагента);
		
		СтрокаЗаполнения = Строка(ПараметрыКонтрагента[ИмяРеквизитаИННКонтрагента])
			+ "_" + Строка(ПараметрыКонтрагента[ИмяРеквизитаКППКонтрагента]);
		Если Прав(СтрокаЗаполнения, 1) = "_" Тогда
			СтрокаЗаполнения = СтрЗаменить(СтрокаЗаполнения, "_", "");
		КонецЕсли;
		ИдентификаторКонтрагента = СокрЛП(СтрокаЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ФамилияИнициалыФизЛица(ФИОВладельца, Фамилия, Имя, Отчество)
	
	ЭлектронныеДокументы.ФамилияИнициалыФизЛица(ФИОВладельца, Фамилия, Имя, Отчество);
	
КонецПроцедуры

// Открывает форму выбора адреса в модальном режиме и возвращает
// реквизиты адреса в виде структуры с соответствующими полями
//
// Параметры:
// - ТолькоДляПросмотра (Булево): Истина - открыть форму выбора адреса только для просмотра
//
// Возвращаемое значение: Структура с полями - реквизитыми адреса;
//						  Неопределено, если на форме адреса при закрытии не была нажата кнопка "ОК"
// 
&НаКлиенте
Процедура ВыбратьАдрес(ТолькоДляПросмотра = Ложь)
	
	ПараметрыФормы = Новый Структура("ТолькоПросмотр", ТолькоДляПросмотра);
	
	ПараметрыФормы.Вставить("Индекс"    , Индекс);
	ПараметрыФормы.Вставить("Регион"    , Регион);
	ПараметрыФормы.Вставить("Район"     , Район);
	ПараметрыФормы.Вставить("Город"     , Город);
	ПараметрыФормы.Вставить("НасПункт"  , НаселенныйПункт);
	ПараметрыФормы.Вставить("Улица"     , Улица);
	ПараметрыФормы.Вставить("Дом"       , Дом);
	ПараметрыФормы.Вставить("Корпус"    , Корпус);
	ПараметрыФормы.Вставить("Квартира"  , Квартира);
	ПараметрыФормы.Вставить("КодРегиона", КодРегиона);
	
	ПараметрыАдреса = ОткрытьФормуМодально("ОбщаяФорма.АдресУчастникаОбменаЭД", ПараметрыФормы);

	Если ТипЗнч(ПараметрыАдреса) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	// Если адрес изменен, то применение изменений
	Индекс          = ПараметрыАдреса.Индекс;
	Регион          = ПараметрыАдреса.Регион;
	Район           = ПараметрыАдреса.Район;
	Город           = ПараметрыАдреса.Город;
	НаселенныйПункт = ПараметрыАдреса.НаселенныйПункт;
	Улица           = ПараметрыАдреса.Улица;
	Дом             = ПараметрыАдреса.Дом;
	Корпус          = ПараметрыАдреса.Корпус;
	Квартира        = ПараметрыАдреса.Квартира;
	КодРегиона      = ПараметрыАдреса.КодРегиона;
	
	СформироватьАдрес();
	
КонецПроцедуры

// Выполняет формирование строки адреса по реквизитам адреса
&НаСервере
Процедура СформироватьАдрес() Экспорт
	
	Адр = "";
	
	ДобавитьПодстроку(Адр, Индекс);
	ДобавитьПодстроку(Адр, Регион);
	ДобавитьПодстроку(Адр, КодРегиона, "регион ");
	ДобавитьПодстроку(Адр, Район);
	ДобавитьПодстроку(Адр, Город);
	ДобавитьПодстроку(Адр, НаселенныйПункт);
	ДобавитьПодстроку(Адр, Улица);
	ДобавитьПодстроку(Адр, Дом     , "д. ");
	ДобавитьПодстроку(Адр, Корпус  , "корп. ");
	ДобавитьПодстроку(Адр, Квартира, "кв. ");
	
	АдресОрганизации = Адр;
	
КонецПроцедуры

// Процедура добавления подстроки к строке
// Параметры:
// - ИсходнаяСтрока - Строка - исходная строка;
// - Подстрока      - Строка - строка, которая должна быть добавлена в конец исходной строки;
// - Префикс        - Строка - строка, которая добавляется перед подстрокой;
// - Разделитель    - строка - строка, которая служит разделителем между строкой и подстрокой.
//
&НаСервере
Процедура ДобавитьПодстроку(ИсходнаяСтрока, Знач Подстрока, Префикс = "", Разделитель = ", ")
	
	Если НЕ ПУстаяСтрока(ИсходнаяСтрока) И НЕ ПустаяСтрока(Подстрока) Тогда
		ИсходнаяСтрока = ИсходнаяСтрока + Разделитель;
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(Подстрока) Тогда
		ИсходнаяСтрока = ИсходнаяСтрока + Префикс + Подстрока;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовыйПрофиль()
	
	ДанныеСохранены = Истина;
	СохранитьПараметры(ДанныеСохранены);
	Если ДанныеСохранены Тогда
		Если ЗначениеЗаполнено(Контрагент) Тогда
			
			ПараметрыФормы = Новый Структура;
			ЗначенияЗаполнения = Новый Структура;
			ЗначенияЗаполнения.Вставить("Контрагент",  Контрагент);
			ЗначенияЗаполнения.Вставить("Организация", Организация);
			ЗначенияЗаполнения.Вставить("ПрофильНастроекЭДО", СсылкаНаПрофильНастроек);
			
			ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
			ОткрытьФорму("Справочник.СоглашенияОбИспользованииЭД.Форма.ФормаЭлемента", ПараметрыФормы);
			
		Иначе
			
			Если СертификатДобавленИзХранилища Тогда
				ПоказатьОповещениеПользователя("Создание",
				ПолучитьНавигационнуюСсылку(НовыйСертификатСсылка),
				НовыйСертификатСсылка);
			КонецЕсли;
			ПоказатьОповещениеПользователя("Создание",
			ПолучитьНавигационнуюСсылку(СсылкаНаПрофильНастроек),
			СсылкаНаПрофильНастроек);
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Ключ", СсылкаНаПрофильНастроек);
			ОткрытьФорму("Справочник.ПрофилиНастроекЭДО.Форма.ФормаЭлемента", ПараметрыФормы);
			
		КонецЕсли;
		
		Оповестить("ОбновитьСостояниеЭД");
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

// Получение данных по организации.

&НаСервере
Процедура ЗаполнитьРеквизитыОрганизацииДляРегистрационногоПакета()
	
	ДанныеОрганизации = Новый Структура;
	ЭлектронныеДокументыПереопределяемый.ЗаполнитьРегистрационныеДанныеОрганизации(Организация, ДанныеОрганизации);
	
	Если ДанныеОрганизации.Свойство("КодИМНС") Тогда
		КодНалоговогоОргана = ДанныеОрганизации.КодИМНС;
	КонецЕсли;
	Если ДанныеОрганизации.Свойство("Индекс") Тогда
		Индекс = ДанныеОрганизации.Индекс;
	КонецЕсли;
	Если ДанныеОрганизации.Свойство("Регион") Тогда
		Регион = ДанныеОрганизации.Регион;
	КонецЕсли;
	Если ДанныеОрганизации.Свойство("КодРегиона") Тогда
		КодРегиона = ДанныеОрганизации.КодРегиона;
	КонецЕсли;
	Если ДанныеОрганизации.Свойство("Район") Тогда
		Район = ДанныеОрганизации.Район;
	КонецЕсли;
	Если ДанныеОрганизации.Свойство("Город") Тогда
		Город = ДанныеОрганизации.Город;
	КонецЕсли;
	Если ДанныеОрганизации.Свойство("НаселенныйПункт") Тогда
		НаселенныйПункт = ДанныеОрганизации.НаселенныйПункт;
	КонецЕсли;
	Если ДанныеОрганизации.Свойство("Улица") Тогда
		Улица = ДанныеОрганизации.Улица;
	КонецЕсли;
	Если ДанныеОрганизации.Свойство("Дом") Тогда
		Дом = ДанныеОрганизации.Дом;
	КонецЕсли;
	Если ДанныеОрганизации.Свойство("Корпус") Тогда
		Корпус = ДанныеОрганизации.Корпус;
	КонецЕсли;
	Если ДанныеОрганизации.Свойство("Квартира") Тогда
		Квартира = ДанныеОрганизации.Квартира;
	КонецЕсли;
	
КонецПроцедуры

// Методы работы с сервисом 1СЭДО

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗаданияДляЭД()

	Попытка
		Если ФормаДлительнойОперации.Открыта() Тогда
			ОбменССерверомПолучитьОтветЭДО(УникальныйИдентификаторЗаявки1СЭДО);
			Если ЗначениеЗаполнено(ИдентификаторОрганизации) Тогда
				Оповестить("ОповещениеОПолученииУникальногоИдентификатораУчастникаОбменаЭД1СЭДО", ИдентификаторОрганизации);
				ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
			Иначе
				ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
				ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗаданияДляЭД",
				ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
			КонецЕсли;
		КонецЕсли;
	Исключение
		ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
		ВызватьИсключение ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;

КонецПроцедуры

&НаСервере
Процедура ОбменССерверомПолучитьОтветЭДО(ИдентификаторЗапросаНаРегистрацию)
	
	ИмяФайлаОтвета = ЭлектронныеДокументыСлужебный.ПолучитьФайлОтветаОтСервераЭДО("GetInfo", ИдентификаторЗапросаНаРегистрацию);
	Если ИмяФайлаОтвета <> Неопределено Тогда
		ДанныеФайла = ЭлектронныеДокументыСлужебный.ПрочитатьТекстИзФайла(ИмяФайлаОтвета, , Истина);
		МассивИдентификаторов = ПолучитьИдентификаторАбонента(ДанныеФайла);
		Если ЗначениеЗаполнено(МассивИдентификаторов) Тогда
			ИдентификаторОрганизации = МассивИдентификаторов[0];
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПолучитьИдентификаторыНаСервисе1СЭДО()
	
	ИмяРеквизитаИННОрганизации = ЭлектронныеДокументыПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("ИННОрганизации");
	ИмяРеквизитаКППОрганизации = ЭлектронныеДокументыПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("КППОрганизации");
	
	ПараметрыОрганиазции = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация,
		ИмяРеквизитаИННОрганизации + ", " + ИмяРеквизитаКППОрганизации);
		
	ПараметрРесурса = ПараметрыОрганиазции[ИмяРеквизитаИННОрганизации];
	Если ЗначениеЗаполнено(ПараметрыОрганиазции[ИмяРеквизитаКППОрганизации]) Тогда
		ПараметрРесурса = ПараметрРесурса + "_" + ПараметрыОрганиазции[ИмяРеквизитаКППОрганизации];
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрРесурса) Тогда
		
		ИмяФайлаОтвета = ЭлектронныеДокументыСлужебный.ПолучитьФайлОтветаОтСервераЭДО("GetInfo", ПараметрРесурса);
		Если ИмяФайлаОтвета <> Неопределено Тогда
			ДанныеФайла = ЭлектронныеДокументыСлужебный.ПрочитатьТекстИзФайла(ИмяФайлаОтвета, , Истина);
			МассивИдентификтаторов = ПолучитьИдентификаторАбонента(ДанныеФайла);
			Если ЗначениеЗаполнено(МассивИдентификтаторов) Тогда
				ИдентификаторОрганизацииСуществующий = МассивИдентификтаторов[0];
				Элементы.ИдентификаторОрганизацииЧерез1СЭДО.СписокВыбора.ЗагрузитьЗначения(МассивИдентификтаторов);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПодключенныеОператорыЭДО1СЭДО(ИнициализироватьПодключениеКТакском = Ложь)
	
	ОператорыЭДОПодключенныеК1СЭДО(ИнициализироватьПодключениеКТакском);
		
	Если ЗначениеЗаполнено(ОператорыЭДО) Тогда
		Элементы.ОператорЭДОПодключенныйК1СЭДО.СписокВыбора.ЗагрузитьЗначения(ОператорыЭДО.Выгрузить().ВыгрузитьКолонку("Наименование"));
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьИдентификаторАбонента(ДанныеФайла)
	
	МассивИдентификаторов = Новый Массив;
	Если НЕ ПустаяСтрока(ДанныеФайла) Тогда
		
		ДеревоXML = ЭлектронныеДокументыСлужебный.ЗагрузитьСтрокуXMLВДеревоЗначений(ДанныеФайла);
		Если ДеревоXML <> Неопределено Тогда
			
			УзелАбоненты = ДеревоXML.Строки.Найти("Abonents", "Имя");
			
			Если УзелАбоненты <> Неопределено Тогда
				Для каждого УзелАбонент Из УзелАбоненты.Строки Цикл
					УзелИдентификаторАбонента = УзелАбонент.Строки.Найти("Identifier", "Имя");
					Если УзелИдентификаторАбонента <> Неопределено Тогда
						МассивИдентификаторов.Добавить(УзелИдентификаторАбонента.Значение);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат МассивИдентификаторов;
	
КонецФункции

&НаСервере
Процедура ОператорыЭДОПодключенныеК1СЭДО(ИнициализироватьПодключениеКТакском = Ложь)
	
	Если Не ЗначениеЗаполнено(ОператорыЭДО) Тогда
		
		ИмяФайлаОтвета = ЭлектронныеДокументыСлужебный.ПолучитьФайлОтветаОтСервераЭДО("GetOperators");
		Если ИмяФайлаОтвета <> Неопределено Тогда
			ДанныеФайла = ЭлектронныеДокументыСлужебный.ПрочитатьТекстИзФайла(ИмяФайлаОтвета, , Истина);
			
			ДеревоXML = ЭлектронныеДокументыСлужебный.ЗагрузитьСтрокуXMLВДеревоЗначений(ДанныеФайла);
			
			Если ДеревоXML <> Неопределено Тогда
				
				УзелОператорыЭДО = ДеревоXML.Строки.Найти("Operators", "Имя");
				Если УзелОператорыЭДО <> Неопределено Тогда
					Для Каждого УзелОператорЭДО Из УзелОператорыЭДО.Строки Цикл
						
						УзелОператорЭДОНаимОрг = УзелОператорЭДО.Строки.Найти("НаимОрг", "Имя");
						УзелОператорЭДОИд = УзелОператорЭДО.Строки.Найти("ИдОперЭДО", "Имя"); 
						УзелОператорЭДОИНН = УзелОператорЭДО.Строки.Найти("ИННЮЛ", "Имя"); 
						УзелОператорЭДОКПП = УзелОператорЭДО.Строки.Найти("КПП", "Имя");
						УзелОператорЭДООГРН = УзелОператорЭДО.Строки.Найти("ОГРН", "Имя");
						УзелОператорЭДОСертификат = УзелОператорЭДО.Строки.Найти("Сертификат", "Имя");
						
						Оператор = ОператорыЭДО.Добавить();
						Оператор.Наименование  = УзелОператорЭДОНаимОрг.Значение;
						Оператор.Идентификатор = УзелОператорЭДОИд.Значение;
						Оператор.ИНН = УзелОператорЭДОИНН.Значение;
						Оператор.КПП = УзелОператорЭДОКПП.Значение;

						Если УзелОператорЭДОСертификат <> Неопределено Тогда
							Оператор.Сертификат = УзелОператорЭДОСертификат.Значение;
						КонецЕсли;
						
						Если УзелОператорЭДООГРН <> Неопределено Тогда
							Оператор.ОГРН = УзелОператорЭДООГРН.Значение;
						КонецЕсли;
						
						РеквизитыОператораЭДО = ЭлектронныеДокументыСлужебный.РеквизитыОператораЭДО(УзелОператорЭДОИд.Значение);
						Если РеквизитыОператораЭДО = Неопределено Тогда
							Оператор.ТребуетсяМЧД = Не УзелОператорЭДОИд = "2AL";
							Оператор.ТипыМЧД = "";
						Иначе
							Оператор.ТребуетсяМЧД = НРег(РеквизитыОператораЭДО.ПриРегистрацииПредставителяТребуетсяМЧД) = "требуется";
							Оператор.ТипыМЧД = РеквизитыОператораЭДО.ПоддерживаемыеПриРегистрацииПредставителяТипыМЧД;							
						КонецЕсли;
						
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;	
			
	ОператорЭДОИд = "";
	Для Каждого Оператор Из ОператорыЭДО Цикл
		
		Если Не ЗначениеЗаполнено(ОператорЭДО)
			ИЛИ (ИнициализироватьПодключениеКТакском 
				И Найти(НРег(Оператор.Наименование), "такском") <> 0) Тогда
			
			ОператорЭДО = Оператор.Наименование;
		КонецЕсли;
		
		Если Оператор.Наименование = ОператорЭДО Тогда						
			ОператорЭДОИд = Оператор.Идентификатор;
			ОператорЭДОИНН = Оператор.ИНН;
			ОператорЭДОКПП = Оператор.КПП;
			ОператорЭДООГРН = Оператор.ОГРН;
			ОператорЭДОСертификат = Оператор.Сертификат;
			
			ОператорЭДОТребуетсяМЧД = Оператор.ТребуетсяМЧД;
			ОператорЭДОТипыМЧД = Оператор.ТипыМЧД;							
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИдентификаторУчастникаОбменаЭДЧерез1СЭДО()
	
	Если Не ЗначениеЗаполнено(СертификатКриптографии) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Сертификат ключа электронной подписи"),
			,
			"СертификатКриптографии");
		Возврат;
	КонецЕсли;
	
	Если Элементы.ГруппаВыбораМЧД.Видимость Тогда
		Если Не ЗначениеЗаполнено(Доверенность) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Доверенность"),
				,
				"Доверенность");
			Возврат;
		КонецЕсли;	
		
		Если Не МашиночитаемыеДоверенностиВызовСервера.ТипПоддержанОператором(ОператорЭДОИд, ТипЗнч(Доверенность)) Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;	
	
	РезультатПроверки = РезультатПроверкиПередПолучениемИдентификатораТакском();
	
	Если СертификатДобавленИзХранилища Тогда
		ТекСертификат = НовыйСертификатСсылка;
	Иначе
		ТекСертификат = СертификатКриптографии;
	КонецЕсли;

	Если Не РезультатПроверки.ДанныеСохранены Тогда
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаПодключенияК1СЭДО;
		ЭтаФорма.Заголовок = Элементы.СтраницаПодключенияК1СЭДО.Заголовок;
		
		УправлениеФормой(ЭтаФорма);
		Возврат;
	КонецЕсли;
	
	Если ПроверитьЗаполнение() Тогда
		ПродолжитьПолучениеИД = Истина;	
		ЗадатьВопросПоРезультатамПроверки(РезультатПроверки, ПродолжитьПолучениеИД);
		
		Если ПродолжитьПолучениеИД Тогда
			ПараметрыАутентификации = ЭлектронныеДокументыСлужебныйКлиент.АутентификацияНаСайте(Истина);
			Если ПараметрыАутентификации = Неопределено Тогда
				
				Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаПодключенияК1СЭДО;
				ЭтаФорма.Заголовок = Элементы.СтраницаПодключенияК1СЭДО.Заголовок;
			
				УправлениеФормой(ЭтаФорма);
				Возврат;
			КонецЕсли;
			
			ТекПарольПользователя = Неопределено;
			// Для дальнейших операций необходим пароль сертификата.
			ПараметрыСертификата = ЭлектронныеДокументыСлужебныйВызовСервера.РеквизитыСертификата(ТекСертификат);
			
			Соответствие = Новый Соответствие;
			Соответствие.Вставить(ТекСертификат, ПараметрыСертификата);
			ВидОперации = НСтр("ru = 'Подписание регистрационного пакета'");
			Если ЭлектронныеДокументыСлужебныйКлиент.ПарольКСертификатуПолучен(Соответствие, ВидОперации)
				И Соответствие.Количество() > 0 Тогда
				Для Каждого КлючИЗначение Из Соответствие Цикл
					ПараметрыСертификата = КлючИЗначение.Значение;
					Прервать;
				КонецЦикла;
			Иначе
				Возврат;
			КонецЕсли;
			ТекПарольПользователя = ПараметрыСертификата.ПарольПользователя;
			
			Если ЗапомнитьПарольКСертификату Тогда
				ПарольПользователя = ТекПарольПользователя;
			КонецЕсли;
			
			ЕстьОшибки = Ложь;
			Если ЭлектронныеДокументыСлужебныйВызовСервера.ВыполнятьАвторизациюНаСервере() Тогда
				ПодготовитьДанныеДляРегистрационногоПакета1СЭДОНаСервере(ТекПарольПользователя, ПараметрыАутентификации, ЕстьОшибки);
			Иначе
				ПодготовитьДанныеДляРегистрационногоПакета1СЭДОНаКлиенте(ТекПарольПользователя, ПараметрыАутентификации, ЕстьОшибки);
			КонецЕсли;
			
			// Для служебного использования.
			Если ЗначениеЗаполнено(АдресРегистрационногоПакета) Тогда
				Оповестить("СохраненРегистрационныйПакет", АдресРегистрационногоПакета, ЭтаФорма);
			КонецЕсли;
			
			Если Не ЕстьОшибки Тогда
				// Запустим обработчик ожидания резальтата регистрации органиазации у оператора.
				ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
				ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗаданияДляЭД", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
				ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(ЭтаФорма, УникальныйИдентификаторЗаявки1СЭДО);
			Иначе
				Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаПодключенияК1СЭДО;
				ЭтаФорма.Заголовок = Элементы.СтраницаПодключенияК1СЭДО.Заголовок;
			
				УправлениеФормой(ЭтаФорма);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗадатьВопросПоРезультатамПроверки(РезультатыПроверки, ПродолжитьПолучениеИД)

	Если РезультатыПроверки.ЕстьДействующийПрофиль Тогда
		Вопросы = Новый Массив;
		Вопросы.Добавить(НСтр("ru = 'Обратите внимание:'"));
		Если РезультатыПроверки.ЕстьДействующийПрофиль Тогда
			ПредупреждениеОДействующемПрофиле = НСтр("ru = ' - в информационной базе уже существует профиль настроек %1 по выбранной организации;'");
			ПредставлениеВидаПрофиля = ?(СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезСервис1СЭДО"),
				НСтр("ru = '1С-ЭДО'"), НСтр("ru = '1С-Такском'"));
			ПредупреждениеОДействующемПрофиле = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ПредупреждениеОДействующемПрофиле, ПредставлениеВидаПрофиля);
			Вопросы.Добавить(ПредупреждениеОДействующемПрофиле);
		КонецЕсли;
		
		Вопросы[Вопросы.Количество() - 1] = Лев(Вопросы[Вопросы.Количество() - 1], СтрДлина(Вопросы[Вопросы.Количество() - 1]) - 1) + ".";
		Вопросы.Добавить(НСтр("ru = 'Продолжить получение идентификатора?'"));
		
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПолучитьСтрокуИзМассиваПодстрок(Вопросы, Символы.ПС);
		
		КодВозврата = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
		Если КодВозврата = КодВозвратаДиалога.Нет Тогда
			ПродолжитьПолучениеИД = Ложь;
		КонецЕсли;
	КонецЕсли;	

КонецПроцедуры


// Методы создания и отправки рег. пакета 1СЭДО

&НаКлиенте
Процедура ПодготовитьДанныеДляРегистрационногоПакета1СЭДОНаКлиенте(ТекущийПароль, ПараметрыАутентификации, ЕстьОшибки)
	
	Попытка
		МенеджерКриптографии = ЭлектроннаяЦифроваяПодписьКлиент.ПолучитьМенеджерКриптографии();
		МенеджерКриптографии.ПарольДоступаКЗакрытомуКлючу = ТекущийПароль;
	Исключение
		ТекстСообщения = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("100");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
		ЕстьОшибки = Истина;
		Возврат;
	КонецПопытки;
	
	// Подготовим пользовательский сертификат
	Если СертификатДобавленИзХранилища Тогда
		ДвоичныеДанныеСертификата = ПолучитьИзВременногоХранилища(СсылкаНаСертификат);
		Сертификат = Новый СертификатКриптографии(ДвоичныеДанныеСертификата);
	Иначе
		ПараметрыСертификата = ЭлектронныеДокументыСлужебныйВызовСервера.РеквизитыСертификата(СертификатКриптографии);
		
		ДвоичныеДанныеОтпечатка = Base64Значение(ПараметрыСертификата.Отпечаток);
		ХранилищеСертификатов = МенеджерКриптографии.ПолучитьХранилищеСертификатов(ТипХранилищаСертификатовКриптографии.ПерсональныеСертификаты);
		Сертификат = ХранилищеСертификатов.НайтиПоОтпечатку(ДвоичныеДанныеОтпечатка);
		
		ДатаНачалаДействияСертификата = Сертификат.ДатаНачала;
	КонецЕсли;
	
	// Подготовим данные для оператора ЭДО
	РеквизитыПакета = Новый Структура;
	ДвоичныеДанныеДляОператораЭДО = Неопределено;
	ДвоичныеДанныеСоглашенияНаПодключениеЭДО = Неопределено;
	
	ПодготовитьДанныеПоРегистрационнойИнформации(РеквизитыПакета, ДвоичныеДанныеДляОператораЭДО, ДвоичныеДанныеСоглашенияНаПодключениеЭДО);
	Если Не ЗначениеЗаполнено(ДвоичныеДанныеДляОператораЭДО) ИЛИ Не ЗначениеЗаполнено(ДвоичныеДанныеСоглашенияНаПодключениеЭДО) Тогда		
		ЕстьОшибки = Истина;
		Возврат;
	КонецЕсли;
	
	Попытка
		ПодписанныеДвоичныеДанныеДляОператораЭДО = МенеджерКриптографии.Подписать(ДвоичныеДанныеДляОператораЭДО, Сертификат);
		ПодписанныеДвоичныеДанныеСоглашенияНаПодключениеЭДО = МенеджерКриптографии.Подписать(ДвоичныеДанныеСоглашенияНаПодключениеЭДО, Сертификат);
	Исключение
		ШаблонРезультата = НСтр("ru = '%1
		|%2'");
		ТекстОшибки = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("104");
		РезультатТеста = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонРезультата, ТекстОшибки,
			КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(РезультатТеста);
		
		ЕстьОшибки = Истина;
		Возврат;
	КонецПопытки;
	
	// Подготовим сертификат оператора ЭДО для шифрования информации
	Если Не ЗначениеЗаполнено(ОператорЭДОСертификат) Тогда
		ПодключенныеОператорыЭДО1СЭДО();
		
		ТекстСообщения = НСтр("ru = 'Ошибка получения сертификата оператора ЭДО.
									|Необходимо повторить получение идентификатора участника ЭДО.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		ЕстьОшибки = Истина;
		Возврат;
	КонецЕсли;
	
	ТекстСертификата = ОператорЭДОСертификат;
	Если Найти(ТекстСертификата, "-----BEGIN CERTIFICATE-----") > 0 Тогда
		ТекстСертификата = СтрЗаменить(ТекстСертификата, "-----BEGIN CERTIFICATE-----", "");
		ТекстСертификата = СтрЗаменить(ТекстСертификата, "-----END CERTIFICATE-----", "");
	КонецЕсли;
	ТекстСертификата = СтрЗаменить(ТекстСертификата, " ", ""); // из-за пробелов получаются пустые двоичные данные дальше
	ДвоичныеДанныеСертификатаОператораЭДО = Base64Значение(ТекстСертификата);
	
	Попытка
		СертификатОператораЭДО = Новый СертификатКриптографии(ДвоичныеДанныеСертификатаОператораЭДО);
		ЗашифрованныеДвоичныеДанныеОператораЭДО = МенеджерКриптографии.Зашифровать(ДвоичныеДанныеДляОператораЭДО, СертификатОператораЭДО);
	Исключение
		ШаблонРезультата = НСтр("ru = '%1
		|%2'");
		ТекстОшибки = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("103");
		РезультатТеста = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонРезультата, ТекстОшибки,
			КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(РезультатТеста);
		
		ЕстьОшибки = Истина;
		Возврат;
	КонецПопытки;
	
	СформироватьИОтправитьРегистрационныйПакет1СЭДО(РеквизитыПакета, ЗашифрованныеДвоичныеДанныеОператораЭДО, ПодписанныеДвоичныеДанныеДляОператораЭДО,
		ДвоичныеДанныеСоглашенияНаПодключениеЭДО, ПодписанныеДвоичныеДанныеСоглашенияНаПодключениеЭДО, ПараметрыАутентификации, ЕстьОшибки);
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьДанныеДляРегистрационногоПакета1СЭДОНаСервере(ТекущийПароль, ПараметрыАутентификации, ЕстьОшибки)
	
	Попытка
		МенеджерКриптографии = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьМенеджерКриптографии();
		МенеджерКриптографии.ПарольДоступаКЗакрытомуКлючу = ТекущийПароль;
	Исключение
		ТекстСообщения = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("100");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
		ЕстьОшибки = Истина;
		Возврат;
	КонецПопытки;
	
	// Подготовим пользовательский сертификат
	Если СертификатДобавленИзХранилища Тогда
		ДвоичныеДанныеСертификата = ПолучитьИзВременногоХранилища(СсылкаНаСертификат);
		Сертификат = Новый СертификатКриптографии(ДвоичныеДанныеСертификата);
	Иначе
		ПараметрыСертификата = ЭлектронныеДокументыСлужебныйВызовСервера.РеквизитыСертификата(СертификатКриптографии);
		
		ДвоичныеДанныеОтпечатка = Base64Значение(ПараметрыСертификата.Отпечаток);
		ХранилищеСертификатов = МенеджерКриптографии.ПолучитьХранилищеСертификатов(ТипХранилищаСертификатовКриптографии.ПерсональныеСертификаты);
		Сертификат = ХранилищеСертификатов.НайтиПоОтпечатку(ДвоичныеДанныеОтпечатка);
		
		ДатаНачалаДействияСертификата = Сертификат.ДатаНачала;
	КонецЕсли;
	
	// Подготовим данные для оператора ЭДО
	РеквизитыПакета = Новый Структура;
	ДвоичныеДанныеДляОператораЭДО = Неопределено;
	ДвоичныеДанныеСоглашенияНаПодключениеЭДО = Неопределено;
	
	ПодготовитьДанныеПоРегистрационнойИнформации(РеквизитыПакета, ДвоичныеДанныеДляОператораЭДО, ДвоичныеДанныеСоглашенияНаПодключениеЭДО);
	Если Не ЗначениеЗаполнено(ДвоичныеДанныеДляОператораЭДО) ИЛИ Не ЗначениеЗаполнено(ДвоичныеДанныеСоглашенияНаПодключениеЭДО) Тогда
		
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаПодключенияК1СЭДО;
		ЭтаФорма.Заголовок = Элементы.СтраницаПодключенияК1СЭДО.Заголовок;
		
		УправлениеФормой(ЭтаФорма);
		ЕстьОшибки = Истина;
		Возврат;
	КонецЕсли;
	
	Попытка
		ПодписанныеДвоичныеДанныеДляОператораЭДО = МенеджерКриптографии.Подписать(ДвоичныеДанныеДляОператораЭДО, Сертификат);
		ПодписанныеДвоичныеДанныеСоглашенияНаПодключениеЭДО = МенеджерКриптографии.Подписать(ДвоичныеДанныеСоглашенияНаПодключениеЭДО, Сертификат);
	Исключение
		ШаблонРезультата = НСтр("ru = '%1
		|%2'");
		ТекстОшибки = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("104");
		РезультатТеста = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонРезультата, ТекстОшибки,
			КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(РезультатТеста);
		
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаПодключенияК1СЭДО;
		ЭтаФорма.Заголовок = Элементы.СтраницаПодключенияК1СЭДО.Заголовок;
		
		УправлениеФормой(ЭтаФорма);
		ЕстьОшибки = Истина;
		Возврат;
	КонецПопытки;
	
	// Подготовим сертификат оператора ЭДО для шифрования информации
	Если Не ЗначениеЗаполнено(ОператорЭДОСертификат) Тогда
		ПодключенныеОператорыЭДО1СЭДО();
		
		ТекстСообщения = НСтр("ru = 'Ошибка получения сертификата оператора ЭДО.
									|Необходимо повторить получение идентификатора участника ЭДО.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаПодключенияК1СЭДО;
		ЭтаФорма.Заголовок = Элементы.СтраницаПодключенияК1СЭДО.Заголовок;
		
		УправлениеФормой(ЭтаФорма);
		ЕстьОшибки = Истина;
		Возврат;
	КонецЕсли;
	
	ТекстСертификата = ОператорЭДОСертификат;
	Если Найти(ТекстСертификата, "-----BEGIN CERTIFICATE-----") > 0 Тогда
		ТекстСертификата = СтрЗаменить(ТекстСертификата, "-----BEGIN CERTIFICATE-----", "");
		ТекстСертификата = СтрЗаменить(ТекстСертификата, "-----END CERTIFICATE-----", "");
	КонецЕсли;
	ДвоичныеДанныеСертификатаОператораЭДО = Base64Значение(ТекстСертификата);
	
	Попытка
		СертификатОператораЭДО = Новый СертификатКриптографии(ДвоичныеДанныеСертификатаОператораЭДО);
		ЗашифрованныеДвоичныеДанныеОператораЭДО = МенеджерКриптографии.Зашифровать(ДвоичныеДанныеДляОператораЭДО, СертификатОператораЭДО);
	Исключение
		ШаблонРезультата = НСтр("ru = '%1
		|%2'");
		ТекстОшибки = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("103");
		РезультатТеста = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонРезультата, ТекстОшибки,
			КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(РезультатТеста);
		
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаПодключенияК1СЭДО;
		ЭтаФорма.Заголовок = Элементы.СтраницаПодключенияК1СЭДО.Заголовок;
		
		УправлениеФормой(ЭтаФорма);
		
		ЕстьОшибки = Истина;
		Возврат;
	КонецПопытки;
	
	СформироватьИОтправитьРегистрационныйПакет1СЭДО(РеквизитыПакета, ЗашифрованныеДвоичныеДанныеОператораЭДО, ПодписанныеДвоичныеДанныеДляОператораЭДО,
		ДвоичныеДанныеСоглашенияНаПодключениеЭДО, ПодписанныеДвоичныеДанныеСоглашенияНаПодключениеЭДО, ПараметрыАутентификации, ЕстьОшибки);
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьДанныеПоРегистрационнойИнформации(РеквизитыПакета, ДвоичныеДанныеДляОператораЭДО, ДвоичныеДанныеСоглашенияНаПодключениеЭДО)
	
	УникальныйИдентификаторЗаявки1СЭДО = ЭлектронныеДокументыСлужебный.СгенерироватьUUID();
	
	// Подготовим данные для 1СЭДО
	ИмяРеквизитаИННОрганизации = ЭлектронныеДокументыПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("ИННОрганизации");
	ИмяРеквизитаКППОрганизации = ЭлектронныеДокументыПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("КППОрганизации");
	ИмяРеквизитаОГРНОрганизации = ЭлектронныеДокументыПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("ОГРНОрганизации");
	ИмяРеквизитаНаименованиеОрганизации = ЭлектронныеДокументыПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("НаименованиеОрганизации");
	
	ПараметрыОрганиазции = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация,
		ИмяРеквизитаИННОрганизации + ", " + ИмяРеквизитаКППОрганизации + ", " + ИмяРеквизитаНаименованиеОрганизации + ", " + ИмяРеквизитаОГРНОрганизации);
		
	Фамилия = СтроковыеФункцииКлиентСервер.ЗаменитьОдниСимволыДругими("_", Фамилия, " ");
	Имя = СтроковыеФункцииКлиентСервер.ЗаменитьОдниСимволыДругими("_", Имя, " ");
	Отчество = СтроковыеФункцииКлиентСервер.ЗаменитьОдниСимволыДругими("_", Отчество, " ");
	
	ОсновноеЛицоБезДоверенностиФамилия = Фамилия;
	ОсновноеЛицоБезДоверенностиИмя = Имя;
	ОсновноеЛицоБезДоверенностиОтчество = Отчество;
	
	Если ЭлектронныеДокументыПереопределяемый.ЭтоФизЛицо(Организация) Тогда
		ПараметрыОрганизацииБЭД = ЭлектронныеДокументыПереопределяемый.ПолучитьДанныеЮрФизЛица(Организация);
		
		Если Не ПустаяСтрока(ПараметрыОрганизацииБЭД.Фамилия)
			И Не ПустаяСтрока(ПараметрыОрганизацииБЭД.Имя) Тогда
			
			Отчество = ?(ЗначениеЗаполнено(ПараметрыОрганизацииБЭД.Отчество), ПараметрыОрганизацииБЭД.Отчество, "");
			ОсновноеЛицоБезДоверенностиФамилия = ПараметрыОрганизацииБЭД.Фамилия;
			ОсновноеЛицоБезДоверенностиИмя = ПараметрыОрганизацииБЭД.Имя;
			ОсновноеЛицоБезДоверенностиОтчество = Отчество;
		КонецЕсли;		
	КонецЕсли;

	РеквизитыПакета.Вставить("УникальныйИдентификаторЗаявки1СЭДО", УникальныйИдентификаторЗаявки1СЭДО);
	РеквизитыПакета.Вставить("НаименованиеОрганизации",  ПараметрыОрганиазции[ИмяРеквизитаНаименованиеОрганизации]);
	РеквизитыПакета.Вставить("ИННОрганизации",           ПараметрыОрганиазции[ИмяРеквизитаИННОрганизации]);
	РеквизитыПакета.Вставить("КППОрганизации",           ПараметрыОрганиазции[ИмяРеквизитаКППОрганизации]);
	РеквизитыПакета.Вставить("ОГРНОрганизации",          ПараметрыОрганиазции[ИмяРеквизитаОГРНОрганизации]);
	РеквизитыПакета.Вставить("ИдентификаторОрганизации", "");
	РеквизитыПакета.Вставить("ОператорЭДО",              ОператорЭДО);
	РеквизитыПакета.Вставить("ОператорЭДОИд",            ОператорЭДОИд);
	РеквизитыПакета.Вставить("Сертификат",               Новый Массив);
	РеквизитыПакета.Вставить("ЭлектроннаяПочта",         ЭлектроннаяПочта);
	РеквизитыПакета.Вставить("Фамилия",                  ОсновноеЛицоБезДоверенностиФамилия);
	РеквизитыПакета.Вставить("Имя",                      ОсновноеЛицоБезДоверенностиИмя);
	РеквизитыПакета.Вставить("Отчество",                 ОсновноеЛицоБезДоверенностиОтчество);
	
	// Подготовим данные для оператора ЭДО
	ДеревоРегистрационнойИнформации = ОбщегоНазначенияЭД.ДеревоДокумента("РегистрационнаяИнформация");
	
	// Служебные данные
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "ИдФайл",   "registration_" + УникальныйИдентификаторЗаявки1СЭДО);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "ВерсПрог", "1С:Предприятие 8");
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "ВерсФорм", "5.02");
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "КНД",      "1115109");
	
	// Данные по налоговому органу
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "КодНО", КодНалоговогоОргана);
	
	// Данные по оператору ЭДО
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "ОперЭДО.НаимОрг",   ОператорЭДО);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "ОперЭДО.ИдОперЭДО", ОператорЭДОИд);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "ОперЭДО.ИННЮЛ",     ОператорЭДОИНН);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "ОперЭДО.КПП",       ОператорЭДОКПП);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "ОперЭДО.ОГРН",      ОператорЭДООГРН);
	
	// Данные по абоненту
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипЗаявления", "1");
	
	ФамилияБезПодчеркивания = СтрЗаменить(ОсновноеЛицоБезДоверенностиФамилия, "_", " ");
	ИмяБезПодчеркивания = СтрЗаменить(ОсновноеЛицоБезДоверенностиИмя, "_", " ");
	ОтчествоБезПодчеркивания = СтрЗаменить(ОсновноеЛицоБезДоверенностиОтчество, "_", " ");	
	
	Если Не ЭлектронныеДокументыПереопределяемый.ЭтоФизЛицо(Организация) Тогда
		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.НаимОрг", ПараметрыОрганиазции[ИмяРеквизитаНаименованиеОрганизации]);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.ИННЮЛ",   ПараметрыОрганиазции[ИмяРеквизитаИННОрганизации]);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.КПП",     ПараметрыОрганиазции[ИмяРеквизитаКППОрганизации]);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.ОГРН",    ПараметрыОрганиазции[ИмяРеквизитаОГРНОрганизации]);
		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.ДатаПодклЭДО", ТекущаяДатаСеанса());
		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.АдрРФ.Индекс",     Индекс);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.АдрРФ.КодРегион",  КодРегиона);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.АдрРФ.Район",      Район);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.АдрРФ.Город",      Город);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.АдрРФ.НаселПункт", НаселенныйПункт);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.АдрРФ.Улица",      Улица);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.АдрРФ.Дом",        Дом);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.АдрРФ.Корпус",     Корпус);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.АдрРФ.Кварт",      Квартира);
		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.ФИООтв.Фамилия",  ФамилияБезПодчеркивания);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.ФИООтв.Имя",      ИмяБезПодчеркивания);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.ФИООтв.Отчество", ОтчествоБезПодчеркивания);
		
	Иначе
		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.ИННФЛ",  ПараметрыОрганиазции[ИмяРеквизитаИННОрганизации]);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.ОГРНИП", ПараметрыОрганиазции[ИмяРеквизитаОГРНОрганизации]);
		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.ДатаПодклЭДО", ТекущаяДатаСеанса());
		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.АдрРФ.Индекс",     Индекс);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.АдрРФ.КодРегион",  КодРегиона);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.АдрРФ.Район",      Район);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.АдрРФ.Город",      Город);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.АдрРФ.НаселПункт", НаселенныйПункт);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.АдрРФ.Улица",      Улица);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.АдрРФ.Дом",        Дом);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.АдрРФ.Корпус",     Корпус);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.АдрРФ.Кварт",      Квартира);
		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.ФИО.Фамилия",  ФамилияБезПодчеркивания);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.ФИО.Имя",      ИмяБезПодчеркивания);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.ФИО.Отчество", ОтчествоБезПодчеркивания);
		
	КонецЕсли;
	
	СертификатыПользователя = Новый ТаблицаЗначений;
	СертификатыПользователя.Колонки.Добавить("ДатаНачСертиф");
	СертификатыПользователя.Колонки.Добавить("ДатаКонСертиф");
	СертификатыПользователя.Колонки.Добавить("ОтпСертиф");
	СертификатыПользователя.Колонки.Добавить("Сертификат");
	СертификатыПользователя.Колонки.Добавить("НомерМЧД");
	СертификатыПользователя.Колонки.Добавить("Фамилия");
	СертификатыПользователя.Колонки.Добавить("Имя");
	СертификатыПользователя.Колонки.Добавить("Отчество");
	
	НоваяСтрока = СертификатыПользователя.Добавить();
	НоваяСтрока.ДатаНачСертиф = ДатаНачалаДействияСертификата;
	НоваяСтрока.ДатаКонСертиф = ДатаОкончания;
	
	ОтпечатокСертификата = НРег(СТРЗаменить(Строка(Base64Значение(Отпечаток))," ",""));
	НоваяСтрока.ОтпСертиф = ОтпечатокСертификата;
	
	
	// Подготовим пользовательский сертификат
	Если СертификатДобавленИзХранилища Тогда
		ДвоичныеДанныеСертификата = ПолучитьИзВременногоХранилища(СсылкаНаСертификат);
	Иначе
		ПараметрыСертификата = ЭлектронныеДокументыСлужебныйВызовСервера.РеквизитыСертификата(СертификатКриптографии);
		ДвоичныеДанныеСертификата = ПараметрыСертификата.ДвоичныеДанныеСертификата;
	КонецЕсли;
	
	СертификатТекст = Base64Строка(ДвоичныеДанныеСертификата);
	НоваяСтрока.Сертификат = "-----BEGIN CERTIFICATE-----" + символы.пс
							+ СертификатТекст + символы.пс
							+ "-----END CERTIFICATE-----";
							
	Если ЭтаФорма.Элементы.ГруппаВыбораМЧД.Видимость Тогда
		Доверенности = МашиночитаемыеДоверенностиКлиентСервер.ЗначениеВМассиве(Доверенность);
		НомераДоверенностей = МашиночитаемыеДоверенности.ПолучитьНомераДоверенностей(Доверенности);

		НоваяСтрока.НомерМЧД = НомераДоверенностей[Доверенность];
	КонецЕсли;
							
	РеквизитыПакета.Сертификат.Добавить(СертификатТекст);
	
	НоваяСтрока.Фамилия  = Фамилия;
	НоваяСтрока.Имя      = Имя;
	НоваяСтрока.Отчество = Отчество;
	
	ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоРегистрационнойИнформации, СертификатыПользователя, "УчастЭДО.СертифДолжн");
	
	//Данные по подписанту
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "Подписант.Должность", ДолжностьПоСертификату);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "Подписант.ФИО.Фамилия", Фамилия);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "Подписант.ФИО.Имя", Имя);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "Подписант.ФИО.Отчество", Отчество);
	
	ДвоичныеДанныеДляОператораЭДО = ЭлектронныеДокументыВнутренний.РегистрационныеДанныеДляОператораЭДО(ДеревоРегистрационнойИнформации);
	
	
	// Подготовим данные для соглашения с оператором ЭДО
	ИмяВременногоФайлаСоглашениеЭДО = ЭлектронныеДокументыСлужебный.ТекущееИмяВременногоФайла("xml");
	
	// LicenseCard
	СоглашениеЭДО = Новый ЗаписьXML;
	СоглашениеЭДО.ОткрытьФайл(ИмяВременногоФайлаСоглашениеЭДО, "windows-1251");
	СоглашениеЭДО.ЗаписатьОбъявлениеXML();
	
	СоглашениеЭДО.ЗаписатьНачалоЭлемента("LicenseCard");
		СоглашениеЭДО.ЗаписатьНачалоЭлемента("info");
			СоглашениеЭДО.ЗаписатьАтрибут("EDOAgreed", XMLСтрока(Истина));
		СоглашениеЭДО.ЗаписатьКонецЭлемента(); // info
	СоглашениеЭДО.ЗаписатьКонецЭлемента(); // LicenseCard
	
	СоглашениеЭДО.Закрыть();
	
	ДвоичныеДанныеСоглашенияНаПодключениеЭДО = Новый ДвоичныеДанные(ИмяВременногоФайлаСоглашениеЭДО);
	УдалитьФайлы(ИмяВременногоФайлаСоглашениеЭДО);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьИОтправитьРегистрационныйПакет1СЭДО(РеквизитыПакета, ЗашифрованныеДвоичныеДанныеОператораЭДО, ПодписанныеДвоичныеДанныеДляОператораЭДО,
		ДвоичныеДанныеСоглашенияНаПодключениеЭДО, ПодписанныеДвоичныеДанныеСоглашенияНаПодключениеЭДО, ПараметрыАутентификации, ЕстьОшибки)
	
	ИмяФайлаПакетаДля1СЭДО = ЭлектронныеДокументыСлужебный.РегистрационныйПакетДляОператораЭДО(РеквизитыПакета,
		ЗашифрованныеДвоичныеДанныеОператораЭДО, ПодписанныеДвоичныеДанныеДляОператораЭДО,
		ДвоичныеДанныеСоглашенияНаПодключениеЭДО, ПодписанныеДвоичныеДанныеСоглашенияНаПодключениеЭДО);
	
	АдресРегистрационногоПакета = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ИмяФайлаПакетаДля1СЭДО), УникальныйИдентификатор);
	
	ЭлектронныеДокументыСлужебный.ОтправитьРегистрационныйПакет1СЭДО(ИмяФайлаПакетаДля1СЭДО, ПараметрыАутентификации, ЕстьОшибки);
	
КонецПроцедуры

// Профиль настроек ЭДО

&НаКлиенте
Функция ТестПрофиляНастроекЭДО(ПолучитьНастройкиУведомления = Ложь)
	
	Отказ = Ложь;
	// Тестируем заполненность формы
	Если Не ЗначениеЗаполнено(Организация) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Организация"),
		,
		"Организация",
		,
		Отказ);
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ИдентификаторОрганизации)
		И ЭлектронныеДокументыСлужебныйВызовСервера.ЭтоПрямойОбмен(СпособОбменаЭД) Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Идентификатор организации"),
			,
			"ИдентификаторОрганизации",
			,
			Отказ);
	КонецЕсли;

	Если Не ЗначениеЗаполнено(СпособОбменаЭД) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Способ Обмена ЭД"),
		,
		"СпособОбменаЭД",
		,
		Отказ);
	КонецЕсли;
	
	Если СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезЭлектроннуюПочту") Тогда
		Если Не ЗначениеЗаполнено(ЭлектроннаяПочтаОрганизации) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Электронная почта организации"),
			,
			"ЭлектроннаяПочтаОрганизации",
			,
			Отказ);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ПрограммаЗащитыИнформации) И ИспользоватьЭП Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Программа защиты информации"),
				,
				"ПрограммаЗащитыИнформации",
				,
				Отказ);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СертификатКриптографии) И ИспользоватьЭП Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Сертификат ключа электронной подписи"),
				,
				"СертификатКриптографии",
				,
				Отказ);
		КонецЕсли;

	ИначеЕсли СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезКаталог") Тогда
		Если Не ЗначениеЗаполнено(КаталогВходящихДокументов) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Каталог входящих документов"),
			,
			"КаталогВходящихДокументов",
			,
			Отказ);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ПрограммаЗащитыИнформации) И ИспользоватьЭП Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Программа защиты информации"),
				,
				"ПрограммаЗащитыИнформации",
				,
				Отказ);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СертификатКриптографии) И ИспользоватьЭП Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Сертификат ключа электронной подписи"),
				,
				"СертификатКриптографии",
				,
				Отказ);
		КонецЕсли;

	ИначеЕсли СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезFTP") Тогда
		Если Не ЗначениеЗаполнено(АдресСервераFTP) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Адрес сервера"),
			,
			"АдресСервераFTP",
			,
			Отказ);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(FTPКаталогВходящихДокументов) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Каталог входящих документов"),
			,
			"FTPКаталогВходящихДокументов",
			,
			Отказ);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ПрограммаЗащитыИнформации) И ИспользоватьЭП Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Программа защиты информации"),
				,
				"ПрограммаЗащитыИнформации",
				,
				Отказ);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СертификатКриптографии) И ИспользоватьЭП Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Сертификат ключа электронной подписи"),
				,
				"СертификатКриптографии",
				,
				Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
	Если СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезОператораЭДОТакском")
		ИЛИ СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезСервис1СЭДО") Тогда
		
		Если Не ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьЗначениеФункциональнойОпции(
				"ИспользоватьЭлектронныеЦифровыеПодписи") Тогда
		
			ТекстСообщения = ЭлектронныеДокументыСлужебныйВызовСервера.ТекстСообщенияОНеобходимостиНастройкиСистемы(
				"НАСТРОЙКАКРИПТОГРАФИИ");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ПрограммаЗащитыИнформации) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Программа защиты информации"),
				,
				"ПрограммаЗащитыИнформации",
				,
				Отказ);
		КонецЕсли;

		Если Не ЗначениеЗаполнено(СертификатКриптографии) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Сертификат ключа электронной подписи"),
				,
				"СертификатКриптографии",
				,
				Отказ);
		КонецЕсли;
			
		Если Элементы.ГруппаВыбораМЧД.Видимость Тогда
			Если Не ЗначениеЗаполнено(Доверенность) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Доверенность"),
					,
					"Доверенность",
					,
					Отказ);
			КонецЕсли;
				
			Если Не МашиночитаемыеДоверенностиВызовСервера.ТипПоддержанОператором(ОператорЭДОИд, ТипЗнч(Доверенность)) Тогда
				Отказ = Истина;
			КонецЕсли;				
		КонецЕсли;			
		
		Если СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезСервис1СЭДО") Тогда
			Если ТипРегистрации = 1 И Не ЗначениеЗаполнено(ИдентификаторОрганизацииСуществующий) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Идентификатор организации"),
					,
					"ИдентификаторОрганизацииСуществующий",
					,
					Отказ);
					
			ИначеЕсли ТипРегистрации = 2 И Не ЗначениеЗаполнено(ИдентификаторОрганизации) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					Нстр("ru = 'Необходимо получить уникальный идентификатор участника обмена ЭД.'"), , , , Отказ);
			КонецЕсли;
		ИначеЕсли СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезОператораЭДОТакском") Тогда
			
			ИмяПоляИдентификатора = ?(Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаПодключенияК1СЭДО,
				"ИдентификаторОрганизацииСуществующий", "ИдентификаторОрганизации");
			ИдентификаторДляПроверки = ЭтаФорма[ИмяПоляИдентификатора];
			
			Если Не ЗначениеЗаполнено(ИдентификаторДляПроверки) Тогда
				Если ЭлектронныеДокументыСлужебныйКлиент.ПроверитьИспользованиеИнтернетПоддержкаПользователей() Тогда
					
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					Нстр("ru = 'Необходимо получить уникальный идентификатор участника обмена ЭД.'"), , , , Отказ);
				Иначе
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Идентификатор организации"),
							,
							ИмяПоляИдентификатора,
							,
							Отказ);
				КонецЕсли;
			Иначе
				
				ИдентификаторДляПроверки = СокрЛП(ИдентификаторДляПроверки);
				ДлинаИдентификатора = СтрДлина(ИдентификаторДляПроверки);
				Если ДлинаИдентификатора <> 46 Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "КОРРЕКТНОСТЬ", "Идентификатор организации", , ,
						Нстр("ru = 'Длина поля не равна 46.'")),
						,
						ИмяПоляИдентификатора,
						,
						Отказ);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат Ложь;
	КонецЕсли;

	// Тестируем криптографию
	Если ИспользоватьЭП Тогда
		
		Если КонтекстАвторизации = ПредопределенноеЗначение("Перечисление.КонтекстыРаботыСЭД.НаСервере") Тогда
			ТестНастроекКриптографииНаСервере(ПровайдерЭЦП, ТипПровайдераЭЦП, Отказ);
		Иначе
			ТестНастроекКриптографииНаКлиенте(Отказ);
		КонецЕсли;
		
		Если Отказ Тогда
			Возврат Ложь;
		КонецЕсли;
		
		ДанныеСохранены = Истина;
		СохранитьНастройкиКриптосредстваИСертификатаПриНеобходимости(ДанныеСохранены);
		Отказ = Не ДанныеСохранены;
		
		Если Отказ Тогда
			Возврат Ложь;
		КонецЕсли;
		
		// Тестируем сертификат
		ТестСертификата(Отказ);
		Если Отказ Тогда
			Возврат Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	ТестСвязиПройден = Истина;
	Если СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезОператораЭДОТакском")
		ИЛИ СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезСервис1СЭДО") Тогда
		
		Состояние(НСтр("ru = 'Создание профиля настроек ЭДО.'"),
			,
			НСтр("ru = 'Выполняется тестирование связи с оператором. Подождите...'"));
		ТестСвязиССервисомЭДО(ТестСвязиПройден, ПолучитьНастройкиУведомления);
		
	ИначеЕсли СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезЭлектроннуюПочту") Тогда
		
		Состояние(НСтр("ru = 'Создание профиля настроек ЭДО.'"),
			,
			НСтр("ru = 'Выполняется тестирование  обмена ЭД через электронную почту. Подождите...'"));
		
		СообщениеОбОшибке = "";
		ДополнительноеСообщение = "";
		ЭлектроннаяПочтаВызовСервера.ПроверитьВозможностьОтправкиИПолученияЭлектроннойПочты(ЭлектроннаяПочтаОрганизации,
			Неопределено, СообщениеОбОшибке, ДополнительноеСообщение);
		
		Если ЗначениеЗаполнено(СообщениеОбОшибке) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Проверка параметров учетной записи завершилась с ошибками:
							|%1'"), СообщениеОбОшибке ),,
				НСтр("ru = 'Проверка учетной записи'"));
			ТестСвязиПройден = Ложь;
		КонецЕсли;
		
	ИначеЕсли СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезКаталог") Тогда
		
		Состояние(НСтр("ru = 'Создание профиля настроек ЭДО.'"),
			,
			НСтр("ru = 'Выполняется тестирование обмена ЭД через каталог. Подождите...'"));
		ТестСвязиПрямогоОбменаНаСервере(КаталогВходящихДокументов, ТестСвязиПройден);
		
	ИначеЕсли СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезFTP") Тогда
		
		Состояние(НСтр("ru = 'Создание профиля настроек ЭДО.'"),
			,
			НСтр("ru = 'Выполняется тестирование обмена ЭД через FTP. Подождите...'"));
		ТестСвязиОбменаЧерезFTPНаСервере(ТестСвязиПройден);
		
	КонецЕсли;
	
	Если Не ТестСвязиПройден Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Процедура СохранитьНастройкиКриптосредстваИСертификатаПриНеобходимости(ДанныеСохранены)

	СохранитьНастройкиКриптосредстваИСертификата(ДанныеСохранены);

КонецПроцедуры


&НаСервере
Процедура СохранитьПараметры(ДанныеСохранены)
	
	Попытка
		НачатьТранзакцию();
		НовыйПрофильНастроек = Справочники.ПрофилиНастроекЭДО.СоздатьЭлемент();
		
		ШаблонНаименование = НСтр("ru = '%1, %2'");
		НовыйПрофильНастроек.Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонНаименование,
			Организация, СпособОбменаЭД);
		НовыйПрофильНастроек.НазначениеУчетнойЗаписи = НазначениеУчетнойЗаписи;
		НовыйПрофильНастроек.ПодробноеОписаниеУчетнойЗаписи = ПодробноеОписаниеУчетнойЗаписи;
		НовыйПрофильНастроек.ПринятыУсловияИспользования = ПринятыУсловияИспользования;
		
		Если СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезСервис1СЭДО 
			ИЛИ Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаПодключенияК1СЭДО Тогда
			
			Если ТипРегистрации = 1 Тогда
				Идентификатор = ИдентификаторОрганизацииСуществующий;	
			ИначеЕсли ТипРегистрации = 2 Тогда
				Идентификатор = ИдентификаторОрганизации;	
			КонецЕсли;
		ИначеЕсли СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезОператораЭДОТакском
			Или ЭлектронныеДокументыСлужебныйВызовСервера.ЭтоПрямойОбмен(СпособОбменаЭД) Тогда
			Идентификатор = ИдентификаторОрганизации;
		КонецЕсли;
		
		НовыйПрофильНастроек.Организация = Организация;
		НовыйПрофильНастроек.ИдентификаторОрганизации = СокрЛП(Идентификатор);
		НовыйПрофильНастроек.СпособОбменаЭД = СпособОбменаЭД;
		
		Если ТипРегистрации = 1 Тогда
			НовыйПрофильНастроек.ЭлектроннаяПочтаДляУведомлений = ЭлектроннаяПочтаСуществующая;
			
		Иначе
			НовыйПрофильНастроек.ЭлектроннаяПочтаДляУведомлений = ЭлектроннаяПочта;
			
		КонецЕсли;
		
		
		// Настройки сертификатов
		Если ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьЗначениеФункциональнойОпции("ИспользоватьЭлектронныеЦифровыеПодписи") Тогда
			Если Не ЭлектронныеДокументыСлужебныйВызовСервера.ЭтоПрямойОбмен(СпособОбменаЭД) ИЛИ ИспользоватьЭП Тогда
				
				СохранитьНастройкиКриптосредстваИСертификата(ДанныеСохранены);
				
				НоваяСтрока = НовыйПрофильНастроек.СертификатыПодписейОрганизации.Добавить();
				Если СертификатДобавленИзХранилища Тогда
					НоваяСтрока.Сертификат = НовыйСертификатСсылка;
				Иначе
					НоваяСтрока.Сертификат = СертификатКриптографии;
				КонецЕсли;				
				Если ЭтаФорма.Элементы.ГруппаВыбораМЧД.Видимость Тогда
					НоваяСтрока.Доверенность = Доверенность;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		// Загружаем ТЧ из профиля настроек ЭДО.
		АктуальныеВидыЭД = ЭлектронныеДокументыПовтИсп.ПолучитьАктуальныеВидыЭД();
		Для Каждого ЗначениеПеречисления Из АктуальныеВидыЭД Цикл
			Если ЗначениеПеречисления <> Перечисления.ВидыЭД.Подтверждение
				И ЗначениеПеречисления <> Перечисления.ВидыЭД.ВозвратТоваровМеждуОрганизациями
				И ЗначениеПеречисления <> Перечисления.ВидыЭД.ПередачаТоваровМеждуОрганизациями
				И ЗначениеПеречисления <> Перечисления.ВидыЭД.Подтверждение
				И ЗначениеПеречисления <> Перечисления.ВидыЭД.УведомлениеОбУточнении
				И ЗначениеПеречисления <> Перечисления.ВидыЭД.Ошибка
				И ЗначениеПеречисления <> Перечисления.ВидыЭД.ИзвещениеОПолучении
				И ЗначениеПеречисления <> Перечисления.ВидыЭД.ПлатежноеПоручение
				И ЗначениеПеречисления <> Перечисления.ВидыЭД.ЗапросВыписки
				И ЗначениеПеречисления <> Перечисления.ВидыЭД.ВыпискаБанка
				И ЗначениеПеречисления <> Перечисления.ВидыЭД.ПредложениеОбАннулировании
				И ЗначениеПеречисления <> Перечисления.ВидыЭД.ТОРГ12Покупатель
				И ЗначениеПеречисления <> Перечисления.ВидыЭД.АктЗаказчик
				И ЗначениеПеречисления <> Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиПолучатель Тогда
				
				НоваяСтрока = НовыйПрофильНастроек.ИсходящиеДокументы.Добавить();
				НоваяСтрока.Формировать = Истина;
				НоваяСтрока.ИсходящийДокумент = ЗначениеПеречисления;
				
				НоваяСтрока.ИспользоватьЭЦП = ИспользоватьЭП;
				
				Если (ЗначениеПеречисления = Перечисления.ВидыЭД.СчетФактура
					ИЛИ ЗначениеПеречисления = Перечисления.ВидыЭД.КорректировочныйСчетФактура)
					И ЭлектронныеДокументыСлужебныйВызовСервера.ЭтоПрямойОбмен(СпособОбменаЭД) Тогда
					
					НоваяСтрока.Формировать = Ложь;
					НоваяСтрока.ИспользоватьЭЦП = Ложь;
					
				КонецЕсли;
				
				// Проставим в новые настройки ЭДО версию формата обмена.
				ВерсияФормата = "CML 2.08";
				Если ЗначениеПеречисления = Перечисления.ВидыЭД.ПроизвольныйЭД Тогда
					ВерсияФормата = "";
				Иначе
					ВерсияФормата = ЭлектронныеДокументыСлужебный.АктуальнаяВерсияФорматаЭД(ЗначениеПеречисления);
				КонецЕсли;
				
				НоваяСтрока.ВерсияФормата = ВерсияФормата;
				
				Если НоваяСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.АктНаПередачуПрав Тогда
					НоваяСтрока.Приоритет = 1;
				КонецЕсли;
				Если НоваяСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.АктИсполнитель Тогда
					НоваяСтрока.Приоритет = 2;
				КонецЕсли;
				Если НоваяСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.ТОРГ12Продавец Тогда
					НоваяСтрока.Приоритет = 3;
				КонецЕсли;
				Если НоваяСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.СчетФактура Тогда
					НоваяСтрока.Приоритет = 4;
				КонецЕсли;
				Если НоваяСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель Тогда
					НоваяСтрока.Приоритет = 5;
				КонецЕсли;
				Если НоваяСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.КорректировочныйСчетФактура Тогда
					НоваяСтрока.Приоритет = 6;
				КонецЕсли;
				Если НоваяСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.ПроизвольныйЭД Тогда
					НоваяСтрока.Приоритет = 7;
				КонецЕсли;
				Если НоваяСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.КаталогТоваров Тогда
					НоваяСтрока.Приоритет = 10;
				КонецЕсли;
				Если НоваяСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.СчетНаОплату Тогда
					НоваяСтрока.Приоритет = 11;
				КонецЕсли;
				Если НоваяСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.ПрайсЛист Тогда
					НоваяСтрока.Приоритет = 12;
				КонецЕсли;
				Если НоваяСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.ЗаказТовара Тогда
					НоваяСтрока.Приоритет = 13;
				КонецЕсли;
				Если НоваяСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.ОтветНаЗаказ Тогда
					НоваяСтрока.Приоритет = 14;
				КонецЕсли;
				Если НоваяСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.ОтчетОПродажахКомиссионногоТовара Тогда
					НоваяСтрока.Приоритет = 15;
				КонецЕсли;
				Если НоваяСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.ОтчетОСписанииКомиссионногоТовара Тогда
					НоваяСтрока.Приоритет = 16;
				КонецЕсли;
				Если НоваяСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.СведенияОРеализацииКомиссионером Тогда
					НоваяСтрока.Приоритет = 17;
				КонецЕсли;
				Если НоваяСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.СведенияОЗакупкеКомиссионером Тогда
					НоваяСтрока.Приоритет = 18;
				КонецЕсли;
				Если НоваяСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.КорректировкаСведенийОРеализацииКомиссионером Тогда
					НоваяСтрока.Приоритет = 19;
				КонецЕсли;
				Если НоваяСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.КорректировкаСведенийОЗакупкеКомиссионером Тогда
					НоваяСтрока.Приоритет = 20;
				КонецЕсли;
				
				Если Не ЗначениеЗаполнено(НоваяСтрока.ДокументУчета) Тогда
					НоваяСтрока.ДокументУчета = ЭлектронныеДокументыПовтИсп.ПредставлениеОснованияДляВидаЭД(ЗначениеПеречисления);
				КонецЕсли;
				
				НоваяСтрока.ТребуетсяИзвещениеОПолучении = Истина;
				Если Не (НоваяСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.СчетФактура
					Или НоваяСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.КорректировочныйСчетФактура) Тогда
					
					НоваяСтрока.ТребуетсяОтветнаяПодпись = НоваяСтрока.ИспользоватьЭЦП;
					
				Иначе
					НоваяСтрока.ТребуетсяОтветнаяПодпись = Ложь;
					
				КонецЕсли;
				
				Если НоваяСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.АктОРасхождениях
					Или НоваяСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.СведенияОРеализацииКомиссионером
					Или НоваяСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.СведенияОЗакупкеКомиссионером
					Или НоваяСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.КорректировкаСведенийОРеализацииКомиссионером
					Или НоваяСтрока.ИсходящийДокумент = Перечисления.ВидыЭД.КорректировкаСведенийОЗакупкеКомиссионером Тогда
					НоваяСтрока.ТребуетсяОтветнаяПодпись = Ложь;
				КонецЕсли;
				
			КонецЕсли;
		КонецЦикла;
		
		НовыйПрофильНастроек.ИсходящиеДокументы.Сортировать("Приоритет");
		
		// Настройки обмена ЭД
		Если СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезКаталог Тогда
			НовыйПрофильНастроек.РесурсВходящихДокументов = КаталогВходящихДокументов;
			
		ИначеЕсли СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезЭлектроннуюПочту Тогда
			НовыйПрофильНастроек.РесурсВходящихДокументов = ЭлектроннаяПочтаОрганизации;
		
		ИначеЕсли СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезFTP Тогда
			НовыйПрофильНастроек.АдресСервера             = АдресСервераFTP;
			НовыйПрофильНастроек.Порт                     = ПортFTP;
			НовыйПрофильНастроек.ПассивноеСоединение      = ПассивноеСоединениеFTP;
			НовыйПрофильНастроек.Логин                    = ПользовательFTP;
			НовыйПрофильНастроек.Пароль                   = ПарольFTP;
			НовыйПрофильНастроек.РесурсВходящихДокументов = FTPКаталогВходящихДокументов;
			
		ИначеЕсли СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезСервис1СЭДО Тогда
			Если ТипРегистрации = 1 Тогда
				НовыйПрофильНастроек.ИдентификаторОрганизации = СокрЛП(ИдентификаторОрганизацииСуществующий);
			КонецЕсли;
			
			// Заполним данные по адресу организации
			НовыйПрофильНастроек.Индекс          = Индекс;
			НовыйПрофильНастроек.КодРегиона      = КодРегиона;
			НовыйПрофильНастроек.Регион          = Регион;
			НовыйПрофильНастроек.Район           = Район;
			НовыйПрофильНастроек.Город           = Город;
			НовыйПрофильНастроек.НаселенныйПункт = НаселенныйПункт;
			НовыйПрофильНастроек.Улица           = Улица;
			НовыйПрофильНастроек.Дом             = Дом;
			НовыйПрофильНастроек.Корпус          = Корпус;
			НовыйПрофильНастроек.Квартира        = Квартира;
			
			// Заполним данные код налогового органа организации
			НовыйПрофильНастроек.КодНалоговогоОргана = КодНалоговогоОргана;
			
			// Заполним данные по оператору ЭДО
			НовыйПрофильНастроек.ОператорЭДО     = ОператорЭДО;
			НовыйПрофильНастроек.ОператорЭДОИд   = ОператорЭДОИд;
			НовыйПрофильНастроек.ОператорЭДОИНН  = ОператорЭДОИНН;
			НовыйПрофильНастроек.ОператорЭДОКПП  = ОператорЭДОКПП;
			НовыйПрофильНастроек.ОператорЭДООГРН = ОператорЭДООГРН;
		КонецЕсли;
		
		Если НовыйПрофильНастроек.ПрофильНастроекЭДОУникален() Тогда
			НовыйПрофильНастроек.Записать();
		Иначе
			ОтменитьТранзакцию();
			ДанныеСохранены = Ложь;
			Возврат;
		КонецЕсли;
		СсылкаНаПрофильНастроек = НовыйПрофильНастроек.Ссылка;
		ЗафиксироватьТранзакцию();
		
		УстановитьДатуСостоянияОбмена(СсылкаНаПрофильНастроек);
		
		ОбновитьПовторноИспользуемыеЗначения();
	Исключение
		ОтменитьТранзакцию();
		ДанныеСохранены = Ложь;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
	КонецПопытки
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиКриптосредстваИСертификата(ДанныеСохранены)
	
	УстановитьПривилегированныйРежим(Истина);
	Попытка
		НачатьТранзакцию();
		Константы.ПровайдерЭЦП.Установить(ПровайдерЭЦП);
		Константы.АлгоритмПодписи.Установить(АлгоритмПодписи);
		Константы.ТипПровайдераЭЦП.Установить(ТипПровайдераЭЦП);
		Константы.АлгоритмПодписи.Установить(АлгоритмПодписи);
		Константы.АлгоритмШифрования.Установить(АлгоритмШифрования);
		Константы.АлгоритмХеширования.Установить(АлгоритмХеширования);
		
		Если Не ОбщегоНазначенияПовтИсп.РазделениеВключено() Тогда
			Константы.КонтекстАвторизации.Установить(КонтекстАвторизации);
			Константы.КонтекстКриптографии.Установить(КонтекстКриптографии);
		КонецЕсли;
		
		Если СертификатДобавленИзХранилища Тогда
			Если ЗначениеЗаполнено(НовыйСертификатСсылка) Тогда
				НовыйСертификат = НовыйСертификатСсылка.ПолучитьОбъект();
			Иначе
				НовыйСертификат = Справочники.СертификатыЭЦП.СоздатьЭлемент();
			КонецЕсли;
			НовыйСертификат.ВидыДокументов.Загрузить(ВидыДокументов.Выгрузить());
			НовыйСертификат.Отпечаток                    = Отпечаток;
			НовыйСертификат.Организация                  = Организация;
			НовыйСертификат.ДатаОкончания                = ДатаОкончания;
			НовыйСертификат.ПарольПользователя           = ПарольПользователя;
			
			НовыйСертификат.Фамилия                      = Фамилия;
			НовыйСертификат.Имя                          = Имя;
			НовыйСертификат.Отчество                     = Отчество;
			НовыйСертификат.ДолжностьПоСертификату       = ДолжностьПоСертификату;
			
			НовыйСертификат.Назначение                   = НазначениеСертификата;
			НовыйСертификат.Наименование                 = НаименованиеСертификата;
			НовыйСертификат.ЗапомнитьПарольКСертификату  = ЗапомнитьПарольКСертификату;
			НовыйСертификат.ОграничитьДоступКСертификату = ОграничитьДоступКСертификату;
			НовыйСертификат.ФайлСертификата = Новый ХранилищеЗначения(
											ПолучитьИзВременногоХранилища(СсылкаНаСертификат),
											Новый СжатиеДанных(9));
			НовыйСертификат.Записать();
			НовыйСертификатСсылка = НовыйСертификат.Ссылка;
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		ОбновитьПовторноИспользуемыеЗначения();
	Исключение
		ОтменитьТранзакцию();
		ДанныеСохранены = Ложь;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
	КонецПопытки

КонецПроцедуры

&НаСервере
Процедура УстановитьДатуСостоянияОбмена(СсылкаНаПрофильНастроек)
	
	Если ЭлектронныеДокументыСлужебныйВызовСервера.ЭтоПрямойОбмен(СпособОбменаЭД) Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.СостоянияОбменовЭДЧерезОператоровЭДО.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ПрофильНастроекЭДО.Установить(СсылкаНаПрофильНастроек);
	НаборЗаписей.Прочитать();
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.ДатаПолученияЭД = НачалоДня(ТекущаяДатаСеанса());
	НоваяЗапись.ПрофильНастроекЭДО = СсылкаНаПрофильНастроек;
	НаборЗаписей.Записать();
	
	ЭлектронныеДокументыСлужебный.ОбновитьКешиОператоровЭДОИФорматов();
	
КонецПроцедуры

// Настройка криптографии
&НаКлиентеНаСервереБезКонтекста
Функция АлгоритмыПровайдераЭЦППоУмолчанию(ПровайдерЭЦП)
	
	Результат = Новый Структура();
	Результат.Вставить("АлгоритмПодписи");
	Результат.Вставить("АлгоритмШифрования");
	Результат.Вставить("АлгоритмХеширования");
	
	Если ПровайдерЭЦП = "Crypto-Pro GOST R 34.10-2012 Cryptographic Service Provider" Тогда
		Результат.АлгоритмПодписи = "GR 34.10-2012 256";
		Результат.АлгоритмШифрования = "GOST 28147-89";
		Результат.АлгоритмХеширования = "GR 34.11-2012 256";
	ИначеЕсли ПровайдерЭЦП = "Crypto-Pro GOST R 34.10-2001 Cryptographic Service Provider" Тогда
		Результат.АлгоритмПодписи = "GOST R 34.10-2001";
		Результат.АлгоритмШифрования = "GOST 28147-89";
		Результат.АлгоритмХеширования = "GOST R 34.11-94";
	ИначеЕсли ПровайдерЭЦП = "Infotecs GOST 2012/512 Cryptographic Service Provider" Тогда
		Результат.АлгоритмПодписи = "GR 34.10-2012 256";
		Результат.АлгоритмШифрования = "GOST 28147-89";
		Результат.АлгоритмХеширования = "GR 34.11-2012 256";
	ИначеЕсли ПровайдерЭЦП = "Infotecs Cryptographic Service Provider" Тогда
		Результат.АлгоритмПодписи = "GOST R 34.10-2001";
		Результат.АлгоритмШифрования = "GOST 28147-89";
		Результат.АлгоритмХеширования = "GOST R 34.11-94";
	ИначеЕсли ПровайдерЭЦП = "Signal-COM CPGOST Cryptographic Provider" Тогда
		Результат.АлгоритмПодписи = "ECR3410-CP";
		Результат.АлгоритмШифрования = "GOST28147";
		Результат.АлгоритмХеширования = "RUS-HASH-CP";
	ИначеЕсли ПровайдерЭЦП = "Microsoft Enhanced Cryptographic Provider v1.0" Тогда
		Результат.АлгоритмПодписи = "RSA_SIGN";
		Результат.АлгоритмШифрования = "RC2";
		Результат.АлгоритмХеширования = "MD5";
	ИначеЕсли ПровайдерЭЦП = "Microsoft Strong Cryptographic Provider" Тогда
		Результат.АлгоритмПодписи = "RSA_SIGN";
		Результат.АлгоритмШифрования = "RC2";
		Результат.АлгоритмХеширования = "MD5";
	Иначе
		Результат.АлгоритмПодписи = "";
		Результат.АлгоритмШифрования = "";
		Результат.АлгоритмХеширования = "";
	КонецЕсли;	
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ПроверитьИсправитьТекущиеЗначенияАлгоритмов(ИнформацияМенеджераКриптографии, АлгоритмПодписи, АлгоритмШифрования, АлгоритмХеширования)
	
	АлгоритмыПровайдераПоУмлочанию = АлгоритмыПровайдераЭЦППоУмолчанию(ИнформацияМенеджераКриптографии.Имя);
	
	Если ИнформацияМенеджераКриптографии.АлгоритмыПодписи.Найти(АлгоритмПодписи) = Неопределено Тогда
		АлгоритмПодписи = АлгоритмыПровайдераПоУмлочанию.АлгоритмПодписи;
	КонецЕсли;
	
	Если ИнформацияМенеджераКриптографии.АлгоритмыШифрования.Найти(АлгоритмШифрования) = Неопределено Тогда 
		АлгоритмШифрования = АлгоритмыПровайдераПоУмлочанию.АлгоритмШифрования;
	КонецЕсли;
		
	Если ИнформацияМенеджераКриптографии.АлгоритмыХеширования.Найти(АлгоритмХеширования) = Неопределено Тогда 
		АлгоритмХеширования = АлгоритмыПровайдераПоУмлочанию.АлгоритмХеширования;
	КонецЕсли;
		
КонецПроцедуры


&НаКлиенте
Процедура ЗаполнитьСпискиВыбораНаКлиенте()
	
	Если ЗначениеЗаполнено(ПровайдерЭЦП) Тогда
		ИнформацияМенеджера = СкомпоноватьИнформациюМенеджераКриптографииНаКлиенте(
									ПровайдерЭЦП,
									Неопределено,
									ТипПровайдераЭЦП);

		Если ИнформацияМенеджера = Неопределено Тогда
			Возврат;
		КонецЕсли;

		ПроверитьИсправитьТекущиеЗначенияАлгоритмов(ИнформацияМенеджера, АлгоритмПодписи, АлгоритмШифрования, АлгоритмХеширования);
	Иначе
		ИнформацияМенеджера = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСпискиВыбораНаСервере()
	
	Если ЗначениеЗаполнено(ПровайдерЭЦП) Тогда
		ИнформацияМенеджера = СкомпоноватьИнформациюМенеджераКриптографииНаСервере(
										ПровайдерЭЦП,
										Неопределено,
										ТипПровайдераЭЦП);

		Если ИнформацияМенеджера = Неопределено Тогда
			Возврат;
		КонецЕсли;

		ПроверитьИсправитьТекущиеЗначенияАлгоритмов(ИнформацияМенеджера, АлгоритмПодписи, АлгоритмШифрования, АлгоритмХеширования);										
	Иначе
		ИнформацияМенеджера = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполинитьПараметрыМенеджераКриптографии(ИмяМодуляКриптографии, ПутьМодуляКриптографии, ТипМодуляКриптографии, СообщатьОшибки = Истина)
	
	Если ЭлектронныеДокументыСлужебныйВызовСервера.ВыполнятьАвторизациюНаСервере() Тогда
		ЗаполнитьПараметрыМенеджераКриптографииНаСервере(
				ИмяМодуляКриптографии,
				ПутьМодуляКриптографии,
				ТипМодуляКриптографии,
				СообщатьОшибки);
	Иначе
		СкомпоноватьИнформациюМенеджераКриптографииНаКлиенте(
				ИмяМодуляКриптографии,
				ПутьМодуляКриптографии,
				ТипМодуляКриптографии,
				СообщатьОшибки);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПараметрыМенеджераКриптографииНаСервере(ИмяМодуляКриптографии, ПутьМодуляКриптографии, ТипМодуляКриптографии, СообщатьОшибки = Истина)
	
	СкомпоноватьИнформациюМенеджераКриптографииНаСервере(
			ИмяМодуляКриптографии,
			ПутьМодуляКриптографии,
			ТипМодуляКриптографии,
			СообщатьОшибки);
	
КонецПроцедуры

&НаСервере
Функция СкомпоноватьИнформациюМенеджераКриптографииНаСервере(ИмяМодуляКриптографии,
															ПутьМодуляКриптографии,
															ТипМодуляКриптографии,
															СообщатьОшибки = Истина)
	
	Если ПутьМодуляКриптографии = Неопределено Тогда
		ПутьМодуляКриптографии = ЭлектроннаяЦифроваяПодпись.ПолучитьПерсональныеНастройкиРаботыСЭЦПСервер().ПутьМодуляКриптографии;
	КонецЕсли;
	
	ИнформацияМенеджера = Неопределено;
	
	Попытка
		
		МенеджерКриптографии = Новый МенеджерКриптографии(ИмяМодуляКриптографии,
														  ПутьМодуляКриптографии,
														  ТипМодуляКриптографии);
		ИнформацияМенеджера = МенеджерКриптографии.ПолучитьИнформациюМодуляКриптографии();
		
	Исключение
		
		Если СообщатьОшибки Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецЕсли;
		
	КонецПопытки;
	
	Если ИнформацияМенеджера <> Неопределено И ИнформацияМенеджера.Имя = ИмяМодуляКриптографии Тогда
		ПровайдерЭЦП     = ИмяМодуляКриптографии;
		ТипПровайдераЭЦП = ТипМодуляКриптографии;
	КонецЕсли;
	
	Возврат ИнформацияМенеджера;
	
КонецФункции

&НаКлиенте
Функция СкомпоноватьИнформациюМенеджераКриптографииНаКлиенте(ИмяМодуляКриптографии,
															ПутьМодуляКриптографии,
															ТипМодуляКриптографии,
															СообщатьОшибки = Истина)
	
	Если ПутьМодуляКриптографии = Неопределено Тогда
		ПутьМодуляКриптографии = ЭлектроннаяЦифроваяПодписьКлиент.ПолучитьПерсональныеНастройкиРаботыСЭЦП().ПутьМодуляКриптографии;
	КонецЕсли;
	
	ИнформацияМенеджера = Неопределено;
		
	Попытка
		
		МенеджерКриптографии = Новый МенеджерКриптографии(ИмяМодуляКриптографии,
														  ПутьМодуляКриптографии,
														  ТипМодуляКриптографии);
		ИнформацияМенеджера = МенеджерКриптографии.ПолучитьИнформациюМодуляКриптографии();
		
	Исключение
		
		Если СообщатьОшибки Тогда
			ТекстСообщения = НСтр("ru = 'Выбранная программа защиты информации не установлена на Вашем компьютере.
										|Требуется обратиться к администратору.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		
	КонецПопытки;
	
	Если ИнформацияМенеджера <> Неопределено И ИнформацияМенеджера.Имя = ИмяМодуляКриптографии Тогда
		ПровайдерЭЦП     = ИмяМодуляКриптографии;
		ТипПровайдераЭЦП = ТипМодуляКриптографии;
	КонецЕсли;
	
	Возврат ИнформацияМенеджера;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ТестНастроекКриптографииНаСервере(ПровайдерЭЦП, ТипПровайдераЭЦП, ЕстьОшибка)
	
	Попытка
		МенеджерКриптографии = Новый МенеджерКриптографии(ПровайдерЭЦП, "", ТипПровайдераЭЦП);
	Исключение
		ТекстСообщения = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("110");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		ЕстьОшибка = Истина;
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ТестНастроекКриптографииНаКлиенте(ЕстьОшибка)
	
	Если Не ПодключитьРасширениеРаботыСКриптографией() Тогда
		Если Не ЭлектронныеДокументыСлужебныйКлиент.УстановитьРасширениеРаботыСКриптографиейНаКлиенте() Тогда
			ЕстьОшибка = Истина;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Попытка
		МенеджерКриптографии = Новый МенеджерКриптографии(ПровайдерЭЦП, "", ТипПровайдераЭЦП);
	Исключение
		ТекстСообщения = НСтр("ru = 'Выбранная программа защиты информации не установлена на Вашем компьютере.
									|Требуется обратиться к администратору.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		ЕстьОшибка = Истина;
	КонецПопытки;
	
КонецПроцедуры

// Настройка сертификата

&НаКлиенте
Процедура ЗагрузитьСертификат(ОтпечатокСертификата = Неопределено, ЭтоПодключениеК1СЭДО = Ложь)
	
	ВыполнятьАвторизациюНаСервере = ЭлектронныеДокументыСлужебныйВызовСервера.ВыполнятьАвторизациюНаСервере();
	
	Если ВыполнятьАвторизациюНаСервере Тогда
		МассивСтруктурЛичныхСертификатов = ПолучитьМассивСтруктурСертификатовНаСервере(ПровайдерЭЦП, ТипПровайдераЭЦП);
	Иначе
		Если Не ПодключитьРасширениеРаботыСКриптографией() Тогда
			Если Не ЭлектронныеДокументыСлужебныйКлиент.УстановитьРасширениеРаботыСКриптографиейНаКлиенте() Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
		МассивСтруктурЛичныхСертификатов = ПолучитьМассивСтруктурСертификатовНаКлиенте();
	КонецЕсли;
	
	СтруктураВозврата = Неопределено;
	Если ОтпечатокСертификата = Неопределено Тогда
		ПараметрыФормы = Новый Структура("МассивСтруктурСертификатов", МассивСтруктурЛичныхСертификатов);
		ПараметрыФормы.Вставить("ЗагрузкаСертификата", Истина);
		СтруктураВозврата = ОткрытьФормуМодально("ОбщаяФорма.ВыборСертификата", ПараметрыФормы);
	Иначе
		Для каждого CтруктураСертификата Из МассивСтруктурЛичныхСертификатов Цикл
			Если ОтпечатокСертификата = НРег(СТРЗаменить(Строка(Base64Значение(CтруктураСертификата.Отпечаток))," ","")) Тогда
				СтруктураВозврата = Новый Структура;
				СтруктураВозврата.Вставить("Отпечаток", CтруктураСертификата.Отпечаток);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ТипЗнч(СтруктураВозврата) = Тип("Структура") Тогда
		СброситьНастройкиСертификата();
		
		Отпечаток = СтруктураВозврата.Отпечаток;
		
		// Поищем сертификат в справочнике
		Если ЗаполнитьРеквизитыСертификатаНаСервере(Отпечаток) Тогда
			Возврат;
		КонецЕсли;
		
		Если ВыполнятьАвторизациюНаСервере Тогда
			СтруктураСертификата = ЗаполнитьСтруктуруСертификатаПоОтпечаткуНаСервере(Отпечаток, ПровайдерЭЦП, ТипПровайдераЭЦП);
		Иначе
			СтруктураСертификата = ЗаполнитьСтруктуруСертификатаПоОтпечаткуНаКлиенте(Отпечаток);
		КонецЕсли;
		
		Если СтруктураСертификата <> Неопределено Тогда
			ПредставлениеСертификата = Организация;
			НаименованиеСертификата  = Организация;
			ДатаОкончания            = СтруктураСертификата.ДействителенДо;
			
			// Проверка сертификата на соответствие 63 ФЗ.
			СистемнаяИнформация = Новый СистемнаяИнформация;
			Если ОбщегоНазначенияКлиентСервер.СравнитьВерсии(СистемнаяИнформация.ВерсияПриложения, "8.2.18.108") >= 0 Тогда
				НовыйСертификат = Новый СертификатКриптографии(СтруктураСертификата.ДвоичныеДанныеСертификата);
				
				// Корректно работаем только с сертификатами для подписи стандартной структуры.
				Если НовыйСертификат.Субъект.Свойство("SN") Тогда
					
					ШаблонФИОВладельца = НСтр("ru = '%1 %2'");
					ФИОВладельца = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонФИОВладельца,
					НовыйСертификат.Субъект.SN, НовыйСертификат.Субъект.GN);
				ИначеЕсли НовыйСертификат.Субъект.Свойство("CN") Тогда
					
					ФИОВладельца = НовыйСертификат.Субъект.CN;
				КонецЕсли;
				ДолжностьПоСертификату = 0;
				Если НовыйСертификат.Субъект.Свойство("T") И ЗначениеЗаполнено(НовыйСертификат.Субъект.T) Тогда
					ДолжностьПоСертификату = НовыйСертификат.Субъект.T;
				КонецЕсли;
				
				ФамилияИнициалыФизЛица(ФИОВладельца, Фамилия, Имя, Отчество);
				
				// Предзаполним код региона данными из сертификата пользователя
				Если Не ЗначениеЗаполнено(КодРегиона) И НовыйСертификат.Субъект.Свойство("ST")
					И СтрДлина(СокрЛ(НовыйСертификат.Субъект.ST)) >= 2 Тогда
					
					КодРегиона = Лев(СокрЛ(НовыйСертификат.Субъект.ST), 2);
					СформироватьАдрес();
				КонецЕсли;
				ПредставлениеСертификата = СтруктураСертификата.КомуВыдан;
				СертификатКриптографии   = СтруктураСертификата.КомуВыдан;
				НаименованиеСертификата  = СтруктураСертификата.КомуВыдан;
				
				ДатаНачалаДействияСертификата = НовыйСертификат.ДатаНачала;
			КонецЕсли;
			
			ШаблонНазначения = НСтр("ru = 'Кем выдан: %1
									|Действителен до: %2
									|
									|%3'");
			НовоеНазначение = "";
			ЗаполнитьНазначениеСертификата(СтруктураСертификата.Назначение, НовоеНазначение, Истина);
			НазначениеСертификата = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонНазначения,
				СтруктураСертификата.КемВыдан, СтруктураСертификата.ДействителенДо, НовоеНазначение);
			
			Если ВыполнятьАвторизациюНаСервере Тогда
				ПолучитьСертификатПоОтпечаткуНаСервере(Отпечаток, СсылкаНаСертификат, ПровайдерЭЦП,  ТипПровайдераЭЦП, УникальныйИдентификатор);
			Иначе
				ПолучитьСертификатПоОтпечаткуНаКлиенте(Отпечаток);
			КонецЕсли;
			
			ЗаполнитьТабличнуюЧастьПоВидамДокументов();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьМассивСтруктурСертификатовНаСервере(ПровайдерЭЦП, ТипПровайдераЭЦП)
	
	МассивСтруктурСертификатов = Новый Массив;
	
	Попытка
		МенеджерКриптографии = Новый МенеджерКриптографии(ПровайдерЭЦП, "", ТипПровайдераЭЦП);
	Исключение
		ТекстСообщения = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("110");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
		Возврат МассивСтруктурСертификатов;
	КонецПопытки;
	
	ТекущаяДата = ТекущаяДатаСеанса();
	
	Хранилище = МенеджерКриптографии.ПолучитьХранилищеСертификатов(ТипХранилищаСертификатовКриптографии.ПерсональныеСертификаты);
	СертификатыХранилища = Хранилище.ПолучитьВсе();
	
	МассивСтрокОтпечатков = Новый Массив;
	
	Для Каждого Сертификат Из СертификатыХранилища Цикл
		Если Сертификат.ДатаОкончания < ТекущаяДата Тогда
			Продолжить; // отфильтровываем истекшие сертификаты
		КонецЕсли;
		
		СтруктураСертификата = ЭлектроннаяЦифроваяПодписьКлиентСервер.ЗаполнитьСтруктуруСертификата(Сертификат);
		Если СтруктураСертификата <> Неопределено Тогда
			СтрокаОтпечатка = Base64Строка(Сертификат.Отпечаток);
			
			Если МассивСтрокОтпечатков.Найти(СтрокаОтпечатка) = Неопределено Тогда
				МассивСтрокОтпечатков.Добавить(СтрокаОтпечатка);
				МассивСтруктурСертификатов.Добавить(СтруктураСертификата);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
		
	Возврат МассивСтруктурСертификатов;
	
КонецФункции

&НаКлиенте
Функция ПолучитьМассивСтруктурСертификатовНаКлиенте()
	
	МассивСтруктурСертификатов = Новый Массив;
	
	Попытка
		МенеджерКриптографии = Новый МенеджерКриптографии(ПровайдерЭЦП, "", ТипПровайдераЭЦП);
	Исключение
		ТекстСообщения = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("100");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
		Возврат МассивСтруктурСертификатов;
	КонецПопытки;
	
	ТекущаяДата = ОбщегоНазначенияКлиент.ДатаСеанса();
	
	// Получаем сертификаты для подписи.
	Хранилище = МенеджерКриптографии.ПолучитьХранилищеСертификатов(ТипХранилищаСертификатовКриптографии.ПерсональныеСертификаты);
	СертификатыХранилища = Хранилище.ПолучитьВсе();
	
	МассивСтрокОтпечатков = Новый Массив;
	
	Для Каждого Сертификат Из СертификатыХранилища Цикл
		Если Сертификат.ДатаОкончания < ТекущаяДата Тогда 
			Продолжить; // отфильтровываем истекшие сертификаты
		КонецЕсли;
		
		СтруктураСертификата = ЭлектроннаяЦифроваяПодписьКлиентСервер.ЗаполнитьСтруктуруСертификата(Сертификат);
		Если СтруктураСертификата <> Неопределено Тогда
			СтрокаОтпечатка = Base64Строка(Сертификат.Отпечаток);
			
			Если МассивСтрокОтпечатков.Найти(СтрокаОтпечатка) = Неопределено Тогда
				МассивСтрокОтпечатков.Добавить(СтрокаОтпечатка);
				МассивСтруктурСертификатов.Добавить(СтруктураСертификата);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
		
	Возврат МассивСтруктурСертификатов;
	
КонецФункции

&НаКлиенте
Функция ЗаполнитьСтруктуруСертификатаПоОтпечаткуНаКлиенте(Отпечаток)
	
	ДвоичныеДанныеОтпечатка = Base64Значение(Отпечаток);
	
	Попытка
		МенеджерКриптографии = Новый МенеджерКриптографии(ПровайдерЭЦП, "", ТипПровайдераЭЦП);
	Исключение
		ТекстСообщения = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("100");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
		Возврат Неопределено;
	КонецПопытки;
	
	ХранилищеСертификатов = МенеджерКриптографии.ПолучитьХранилищеСертификатов();
	Сертификат = ХранилищеСертификатов.НайтиПоОтпечатку(ДвоичныеДанныеОтпечатка);
	
	Если Сертификат = Неопределено Тогда
		Предупреждение(НСтр("ru = 'Сертификат не найден'"));
		Возврат Неопределено;
	КонецЕсли;
	
	СтруктураСертификата = ЭлектроннаяЦифроваяПодписьКлиентСервер.ЗаполнитьСтруктуруСертификата(Сертификат);
	СтруктураСертификата.Вставить("ДвоичныеДанныеСертификата", Сертификат.Выгрузить());
	
	Возврат СтруктураСертификата;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗаполнитьСтруктуруСертификатаПоОтпечаткуНаСервере(Отпечаток, ПровайдерЭЦП, ТипПровайдераЭЦП)
	
	ДвоичныеДанныеОтпечатка = Base64Значение(Отпечаток);
	
	Попытка
		МенеджерКриптографии = Новый МенеджерКриптографии(ПровайдерЭЦП, "", ТипПровайдераЭЦП);
	Исключение
		ТекстСообщения = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("110");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
		Возврат Неопределено;
	КонецПопытки;
	
	ХранилищеСертификатов = МенеджерКриптографии.ПолучитьХранилищеСертификатов();
	Сертификат = ХранилищеСертификатов.НайтиПоОтпечатку(ДвоичныеДанныеОтпечатка);
	
	Если Сертификат = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Сертификат не найден.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат Неопределено;
	КонецЕсли;
	
	СтруктураСертификата = ЭлектроннаяЦифроваяПодписьКлиентСервер.ЗаполнитьСтруктуруСертификата(Сертификат);
	СтруктураСертификата.Вставить("ДвоичныеДанныеСертификата", Сертификат.Выгрузить());
	
	Возврат СтруктураСертификата;
	
КонецФункции

&НаКлиенте
Процедура ПолучитьСертификатПоОтпечаткуНаКлиенте(Отпечаток)
	
	ДвоичныеДанныеОтпечатка = Base64Значение(Отпечаток);
	
	Попытка
		МенеджерКриптографии = Новый МенеджерКриптографии(ПровайдерЭЦП, "", ТипПровайдераЭЦП);
	Исключение
		ТекстСообщения = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("100");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
		Возврат;
	КонецПопытки;
	
	ХранилищеСертификатов = Неопределено;
	ХранилищеСертификатов = МенеджерКриптографии.ПолучитьХранилищеСертификатов(ТипХранилищаСертификатовКриптографии.ПерсональныеСертификаты);
	
	Сертификат = ХранилищеСертификатов.НайтиПоОтпечатку(ДвоичныеДанныеОтпечатка);
	
	ДвоичныеДанныеСертификата = Сертификат.Выгрузить();
		
	СсылкаНаСертификат = ПоместитьВоВременноеХранилище(ДвоичныеДанныеСертификата, УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПолучитьСертификатПоОтпечаткуНаСервере(Отпечаток, СсылкаНаСертификат, ПровайдерЭЦП, ТипПровайдераЭЦП, УникальныйИдентификатор)
	
	ДвоичныеДанныеОтпечатка = Base64Значение(Отпечаток);
	
	Попытка
		МенеджерКриптографии = Новый МенеджерКриптографии(ПровайдерЭЦП, "", ТипПровайдераЭЦП);
	Исключение
		ТекстСообщения = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("110");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
		Возврат;
	КонецПопытки;
	
	ХранилищеСертификатов = Неопределено;
	ХранилищеСертификатов = МенеджерКриптографии.ПолучитьХранилищеСертификатов(ТипХранилищаСертификатовКриптографии.ПерсональныеСертификаты);
	
	Сертификат = ХранилищеСертификатов.НайтиПоОтпечатку(ДвоичныеДанныеОтпечатка);
	
	ДвоичныеДанныеСертификата = Сертификат.Выгрузить();
		
	СсылкаНаСертификат = ПоместитьВоВременноеХранилище(ДвоичныеДанныеСертификата, УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьРеквизитыСертификатаНаСервере(ИскомыйОтпечаток)
	
	Результат = Ложь;
	
	// Поиск уже загруженных сертификатов в справочнике.
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СертификатыЭЦП.Ссылка
	|ИЗ
	|	Справочник.СертификатыЭЦП КАК СертификатыЭЦП
	|ГДЕ
	|	НЕ СертификатыЭЦП.ПометкаУдаления
	|	И НЕ СертификатыЭЦП.Отозван
	|	И СертификатыЭЦП.Отпечаток = &ИскомыйОтпечаток
	|	И СертификатыЭЦП.Организация = &Организация";
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ИскомыйОтпечаток", ИскомыйОтпечаток);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СертификатДобавленИзХранилища = Ложь;
		Результат = Истина;
		
		СертификатКриптографии = Выборка.Ссылка;
		
		РеквизитыСертификата = ЭлектронныеДокументыСлужебныйВызовСервера.РеквизитыСертификата(СертификатКриптографии);
		
		ЗапомнитьПарольКСертификату  = РеквизитыСертификата.ЗапомнитьПарольКСертификату;
		НазначениеСертификата        = РеквизитыСертификата.Назначение;
		ОграничитьДоступКСертификату = РеквизитыСертификата.ОграничитьДоступКСертификату;
		ОрганизацияПоСертификату     = РеквизитыСертификата.Организация;
		ПарольПользователя           = РеквизитыСертификата.ПарольПользователя;
		НаименованиеСертификата      = РеквизитыСертификата.Наименование;
		Отпечаток                    = РеквизитыСертификата.Отпечаток;
		
		Фамилия                      = РеквизитыСертификата.Фамилия;
		Имя                          = РеквизитыСертификата.Имя;
		Отчество                     = РеквизитыСертификата.Отчество;
		ДолжностьПоСертификату       = РеквизитыСертификата.ДолжностьПоСертификату;
		
		ДатаОкончания                = РеквизитыСертификата.ДатаОкончания;
		Если Не ЗначениеЗаполнено(Организация) Тогда
			Организация = ОрганизацияПоСертификату;
		конецЕсли;
		ВидыДокументов.Загрузить(СертификатКриптографии.ВидыДокументов.Выгрузить());
		ВидыДокументов.Сортировать("ВидДокумента");
		СсылкаНаСертификат = ПоместитьВоВременноеХранилище(РеквизитыСертификата.ДвоичныеДанныеСертификата, УникальныйИдентификатор);
	конецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗаполнитьНазначениеСертификата(Назначение, НовоеНазначение, ДобавлятьКодНазначения = Ложь)
	
	ЭлектроннаяЦифроваяПодпись.ЗаполнитьНазначениеСертификата(Назначение, НовоеНазначение, ДобавлятьКодНазначения);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТабличнуюЧастьПоВидамДокументов()
	
	ВидыДокументов.Очистить();
	
	АктуальныеЭД = ЭлектронныеДокументыПовтИсп.ПолучитьАктуальныеВидыЭД();
	
	ДоступныеВидыДокументовПоЭДО = ЭлектронныеДокументыСлужебный.ДоступныеВидыДокументовПоЭДО();
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(АктуальныеЭД, ДоступныеВидыДокументовПоЭДО, Истина);
	
	Для Каждого ЗначениеПеречисления Из АктуальныеЭД Цикл
		Если ЗначениеПеречисления = Перечисления.ВидыЭД.Ошибка
			ИЛИ ЗначениеПеречисления = Перечисления.ВидыЭД.Подтверждение
			ИЛИ ЗначениеПеречисления = Перечисления.ВидыЭД.Квитанция
			ИЛИ ЗначениеПеречисления = Перечисления.ВидыЭД.ДопДанные
			ИЛИ ЗначениеПеречисления = Перечисления.ВидыЭД.ПлатежноеПоручение
			ИЛИ ЗначениеПеречисления = Перечисления.ВидыЭД.ВыпискаБанка
			ИЛИ ЗначениеПеречисления = Перечисления.ВидыЭД.ЗапросВыписки
			ИЛИ ЗначениеПеречисления = Перечисления.ВидыЭД.ЗапросНочнойВыписки
			ИЛИ ЗначениеПеречисления = Перечисления.ВидыЭД.АктВыполненныхРабот
			ИЛИ ЗначениеПеречисления = Перечисления.ВидыЭД.ТОРГ12 Тогда
			
			Продолжить;
		КонецЕсли;
		
		НоваяСтрокаТЧ = ВидыДокументов.Добавить();
		НоваяСтрокаТЧ.ВидДокумента = ЗначениеПеречисления;
		НоваяСтрокаТЧ.ИспользоватьДляПодписи = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ТестСертификата(Отказ)
	
	ТекПарольПользователя = Неопределено;
	Если СертификатДобавленИзХранилища Тогда
		ТекСертификат = НовыйСертификатСсылка;
	Иначе
		ТекСертификат = СертификатКриптографии;
	КонецЕсли;
	ПараметрыСертификата = ЭлектронныеДокументыСлужебныйВызовСервера.РеквизитыСертификата(ТекСертификат);
		
	Соответствие = Новый Соответствие;
	Соответствие.Вставить(ТекСертификат, ПараметрыСертификата);
	ВидОперации = НСтр("ru = 'Проверка сертификата'");
	Если ЭлектронныеДокументыСлужебныйКлиент.ПарольКСертификатуПолучен(Соответствие, ВидОперации)
		И Соответствие.Количество() > 0 Тогда
		Для Каждого КлючИЗначение Из Соответствие Цикл
			ПараметрыСертификата = КлючИЗначение.Значение;
			Прервать;
		КонецЦикла;
	Иначе
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	ТекПарольПользователя = ПараметрыСертификата.ПарольПользователя;
	
	Если ЗапомнитьПарольКСертификату Тогда
		ПарольПользователя = ТекПарольПользователя;
	КонецЕсли;
	
	Если КонтекстАвторизации = ПредопределенноеЗначение("Перечисление.КонтекстыРаботыСЭД.НаСервере") Тогда
		Результат = ТестСертификатаНаСервере(ТекПарольПользователя);
	Иначе
		Результат = ТестСертификатаНаКлиенте(ТекПарольПользователя);
	КонецЕсли;
	
	Если Не Результат Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ТестСертификатаНаКлиенте(ТекущийПароль)
			
	ШаблонСообщения = НСтр("ru = '%1
								 |%2'");
	
	// Блок проверки наличия сертификата в хранилище сертификатов компьютера.
	ОписаниеТеста = НСтр("ru = 'Тест. Проверка наличия сертификата на клиенте.'");

	Попытка
		МенеджерКриптографии = Новый МенеджерКриптографии(ПровайдерЭЦП, "", ТипПровайдераЭЦП);
	Исключение
		ТекстСообщения = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("100");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
		Возврат Ложь;
	КонецПопытки;
	
	МенеджерКриптографии.ПарольДоступаКЗакрытомуКлючу = ТекущийПароль;
	
	Если Не СертификатДобавленИзХранилища Тогда
		ПараметрыСертификата = ЭлектронныеДокументыСлужебныйВызовСервера.РеквизитыСертификата(СертификатКриптографии);
		
		ДвоичныеДанныеОтпечатка = Base64Значение(ПараметрыСертификата.Отпечаток);
		ХранилищеСертификатов = МенеджерКриптографии.ПолучитьХранилищеСертификатов(ТипХранилищаСертификатовКриптографии.ПерсональныеСертификаты);
		Сертификат = ХранилищеСертификатов.НайтиПоОтпечатку(ДвоичныеДанныеОтпечатка);
		
		Если Сертификат = Неопределено Тогда
			РезультатТеста = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("101");
		КонецЕсли;
		Если ЗначениеЗаполнено(РезультатТеста) Тогда 
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения,
																					 ОписаниеТеста,
																					 РезультатТеста);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		
		// Критичная ошибка - дальше тесты не проводим, будет ошибка платформы.
		Если Сертификат = Неопределено Тогда
			Возврат Ложь;
		КонецЕсли;
		
	Иначе
		
		ДвоичныеДанныеСертификата = ПолучитьИзВременногоХранилища(СсылкаНаСертификат);
		Сертификат = Новый СертификатКриптографии(ДвоичныеДанныеСертификата);
		
	КонецЕсли;
	
	Результат = Истина;
	// Блок проверки сертификата на корректность.
	ОписаниеТеста = НСтр("ru = 'Тест. Проверка реквизитов сертификата на клиенте.'");
	Попытка
		МенеджерКриптографии.ПроверитьСертификат(Сертификат, РежимПроверкиСертификатаКриптографии.РазрешитьТестовыеСертификаты);
	Исключение
		ШаблонРезультата = НСтр("ru = '%1
		|%2'");
		ТекстОшибки = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("102");
		РезультатТеста = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонРезультата, 
																				 ТекстОшибки,
																				 КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		Результат = Ложь;
	КонецПопытки;
	Если ЗначениеЗаполнено(РезультатТеста) Тогда 
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения,
																				 ОписаниеТеста,
																				 РезультатТеста);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	// Блок проверки шифрования/расшифрования.
	ОтпечатокДвоичныеДанные = Base64Значение(Отпечаток);
	
	РезультатТеста = "";
	ОписаниеТеста = НСтр("ru = 'Тест. Проверка операций шифрования/расшифровки на клиенте.'");
	ДвоичныеДанные = МенеджерКриптографии.Зашифровать(ОтпечатокДвоичныеДанные, Сертификат);
	Попытка
		РасшифрованныеДвоичныеДанные = МенеджерКриптографии.Расшифровать(ДвоичныеДанные);
	Исключение
		РезультатТеста = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("103");
		Результат = Ложь;
	КонецПопытки;
	Если ЗначениеЗаполнено(РезультатТеста) Тогда 
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения,
									ОписаниеТеста,
									РезультатТеста);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;

	// Блок проверки ЭЦП.
	РезультатТеста = "";
	ОписаниеТеста = НСтр("ru = 'Тест. Проверка операций формирования/проверки ЭЦП на клиенте.'");
	Попытка
		ДвоичныеДанные = МенеджерКриптографии.Подписать(Сертификат.Отпечаток, Сертификат);
		ЭлектроннаяЦифроваяПодписьКлиент.ПроверитьПодпись(МенеджерКриптографии, Сертификат.Отпечаток, ДвоичныеДанные);
	Исключение
		ШаблонРезультата = НСтр("ru = '%1
		|%2'");
		ТекстОшибки = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("104");
		РезультатТеста = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонРезультата, ТекстОшибки,
			КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		Результат = Ложь;
	КонецПопытки;
	
	Если ЗначениеЗаполнено(РезультатТеста) Тогда 
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения,
																				 ОписаниеТеста,
																				 РезультатТеста);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ТестСертификатаНаСервере(ТекущийПароль)
			
	ШаблонСообщения = НСтр("ru = '%1
								 |%2'");
	
	// Блок проверки наличия сертификата в хранилище сертификатов компьютера.
	ОписаниеТеста = НСтр("ru = 'Тест. Проверка наличия сертификата на сервере.'");

	Попытка
		МенеджерКриптографии = Новый МенеджерКриптографии(ПровайдерЭЦП, "", ТипПровайдераЭЦП);
	Исключение
		ТекстСообщения = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("110");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
		Возврат Ложь;
	КонецПопытки;
	
	МенеджерКриптографии.ПарольДоступаКЗакрытомуКлючу = ТекущийПароль;
	
	Если Не СертификатДобавленИзХранилища Тогда
	
		Отпечаток = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СертификатКриптографии, "Отпечаток");
		ДвоичныеДанныеОтпечатка = Base64Значение(Отпечаток);
		ХранилищеСертификатов = МенеджерКриптографии.ПолучитьХранилищеСертификатов(ТипХранилищаСертификатовКриптографии.ПерсональныеСертификаты);
		Сертификат = ХранилищеСертификатов.НайтиПоОтпечатку(ДвоичныеДанныеОтпечатка);
		
		Если Сертификат = Неопределено Тогда
			РезультатТеста = ЭлектронныеДокументыПовтИсп.ПолучитьСообщениеОбОшибке("111");
		КонецЕсли;
		Если ЗначениеЗаполнено(РезультатТеста) Тогда 
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения,
																					 ОписаниеТеста,
																					 РезультатТеста);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		
		// Критичная ошибка - дальше тесты не проводим, будет ошибка платформы.
		Если Сертификат = Неопределено Тогда
			Возврат Ложь;
		КонецЕсли;
		
	Иначе
		
		ДвоичныеДанныеСертификата = ПолучитьИзВременногоХранилища(СсылкаНаСертификат);
		Сертификат = Новый СертификатКриптографии(ДвоичныеДанныеСертификата);
		
	КонецЕсли;
	
	Результат = Истина;
	// Блок проверки сертификата на корректность.
	ОписаниеТеста = НСтр("ru = 'Тест. Проверка реквизитов сертификата на сервере.'");
	Попытка
		МенеджерКриптографии.ПроверитьСертификат(Сертификат,
												 РежимПроверкиСертификатаКриптографии.РазрешитьТестовыеСертификаты);
	Исключение
		ШаблонРезультата = НСтр("ru = '%1
		|%2'");
		ТекстОшибки = ЭлектронныеДокументыПовтИсп.ПолучитьСообщениеОбОшибке("112");
		РезультатТеста = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонРезультата,
																				 ТекстОшибки,
																				 КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		Результат = Ложь;
	КонецПопытки;
	Если ЗначениеЗаполнено(РезультатТеста) Тогда 
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения,
																				 ОписаниеТеста,
																				 РезультатТеста);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	// Блок проверки шифрования/расшифрования.
	ОтпечатокДвоичныеДанные = Base64Значение(Отпечаток);
	
	РезультатТеста = "";
	ОписаниеТеста = НСтр("ru = 'Тест. Проверка операций шифрования/расшифровки на сервере.'");
	ДвоичныеДанные = МенеджерКриптографии.Зашифровать(ОтпечатокДвоичныеДанные, Сертификат);
	Попытка
		РасшифрованныеДвоичныеДанные = МенеджерКриптографии.Расшифровать(ДвоичныеДанные);
	Исключение
		РезультатТеста = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("113");
		Результат = Ложь;
	КонецПопытки;
	Если ЗначениеЗаполнено(РезультатТеста) Тогда 
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения,
									ОписаниеТеста,
									РезультатТеста);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	// Блок проверки ЭЦП.
	РезультатТеста = "";
	ОписаниеТеста = НСтр("ru = 'Тест. Проверка операций формирования/проверки ЭЦП на сервере.'");
	Попытка
		ДвоичныеДанные = МенеджерКриптографии.Подписать(Сертификат.Отпечаток, Сертификат);
		ЭлектронныеДокументыСлужебный.ПроверитьПодпись(МенеджерКриптографии, Сертификат.Отпечаток, ДвоичныеДанные);
	Исключение
		ШаблонРезультата = НСтр("ru = '%1
		|%2'");
		ТекстОшибки = ЭлектронныеДокументыПовтИсп.ПолучитьСообщениеОбОшибке("114");
		РезультатТеста = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонРезультата,
																				 ТекстОшибки,
																				 КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		Результат = Ложь;
	КонецПопытки;
	
	Если ЗначениеЗаполнено(РезультатТеста) Тогда 
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения,
																				 ОписаниеТеста,
																				 РезультатТеста);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура СброситьНастройкиСертификата()
	
	НазначениеСертификата                             = "";
	НаименованиеСертификата                           = "";
	ДатаОкончания                                     = Дата(1,1,1);
	ОграничитьДоступКСертификату                      = Ложь;
	ЗапомнитьПарольКСертификату                       = Ложь;
	ПарольПользователя                                = "";
	
	Фамилия                                           = "";
	Имя                                               = "";
	Отчество                                          = "";
	ДолжностьПоСертификату                            = "";
	ВидыДокументов.Очистить();
	
	Если СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезОператораЭДОТакском")
		И ЭлектронныеДокументыСлужебныйКлиент.ПроверитьИспользованиеИнтернетПоддержкаПользователей() Тогда
		ИдентификаторОрганизации = "";
		Элементы.НадписьИдентификаторУчастникаОбменаЭД.Заголовок = НСтр("ru = 'Получить уникальный идентификатор участника обмена ЭД.'");
		ШрифтЭлемента = Элементы.НадписьИдентификаторУчастникаОбменаЭД.Шрифт;
		Элементы.НадписьИдентификаторУчастникаОбменаЭД.Шрифт = Новый Шрифт(ШрифтЭлемента, , , Ложь);
		Элементы.НадписьИдентификаторУчастникаОбменаЭД.Гиперссылка = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Обмен через оператора

&НаКлиенте
Процедура ТестСвязиССервисомЭДО(ТестПройден, ПолучитьНастройкиУведомления)
	
	Если СертификатДобавленИзХранилища Тогда
		СертификатПодписи = НовыйСертификатСсылка;
	Иначе
		СертификатПодписи = СертификатКриптографии;
	КонецЕсли;
	ЭлектронныеДокументыСлужебныйКлиент.ПроверитьСрокДействияСертификата(СертификатПодписи);
	ПараметрыСертификата = ЭлектронныеДокументыСлужебныйВызовСервера.РеквизитыСертификата(СертификатПодписи);
	ПараметрыСертификата.Вставить("СертификатПодписи", СертификатПодписи);
	
	ПараметрыАутентификации = Неопределено;
	Если СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезСервис1СЭДО") Тогда
		ПараметрыАутентификации = ЭлектронныеДокументыСлужебныйКлиент.АутентификацияНаСайте(Истина);
		Если ПараметрыАутентификации = Неопределено Тогда
			ТестПройден = Ложь;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Соответствие = Новый Соответствие;
	Соответствие.Вставить(СертификатПодписи, ПараметрыСертификата);
	ВидОперации = НСтр("ru = 'Аутентификация в сервисе ЭДО'");
	Если ЭлектронныеДокументыСлужебныйКлиент.ПарольКСертификатуПолучен(Соответствие, ВидОперации)
		И Соответствие.Количество() > 0 Тогда
		Для Каждого КлючИЗначение Из Соответствие Цикл
			ПараметрыСертификата = КлючИЗначение.Значение;
			Прервать;
		КонецЦикла;
		НастройкиКриптографии = Новый Структура;
		НастройкиКриптографии.Вставить("ПровайдерЭЦП",           ПровайдерЭЦП);
		НастройкиКриптографии.Вставить("ТипПровайдераЭЦП",       ТипПровайдераЭЦП);
		НастройкиКриптографии.Вставить("АлгоритмПодписи",        АлгоритмПодписи);
		НастройкиКриптографии.Вставить("АлгоритмШифрования",     АлгоритмШифрования);
		НастройкиКриптографии.Вставить("АлгоритмХеширования",    АлгоритмХеширования);
		НастройкиКриптографии.Вставить("ПутьМодуляКриптографии", "");
		ПараметрыСертификата.Вставить("НастройкиКриптографии", НастройкиКриптографии);
		
		СтруктураПараметровЗапросаМаркера = ПараметрыСертификата;
		СтруктураПараметровЗапросаМаркера.Вставить("СпособОбменаЭД", СпособОбменаЭД);
		Если ТипРегистрации = 1 Тогда
		
			Идентификатор = ИдентификаторОрганизацииСуществующий;
		Иначе
			Идентификатор = ИдентификаторОрганизации;
		КонецЕсли;
		
		СтруктураПараметровЗапросаМаркера.Вставить("ИдентификаторОрганизации", Идентификатор);
		
		Если КонтекстАвторизации = ПредопределенноеЗначение("Перечисление.КонтекстыРаботыСЭД.НаСервере") Тогда
			ТестПройден = ЭлектронныеДокументыСлужебныйВызовСервера.ТестСвязиСОператоромЭДО(СтруктураПараметровЗапросаМаркера, Ложь, ПараметрыАутентификации);
		Иначе
			ТестПройден = ЭлектронныеДокументыСлужебныйКлиент.ТестСвязиСОператоромЭДО(СтруктураПараметровЗапросаМаркера, Ложь, ПараметрыАутентификации);
		КонецЕсли;
		
		Если СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезСервис1СЭДО") 
			И ПолучитьНастройкиУведомления Тогда
			
			Если СтруктураПараметровЗапросаМаркера.Свойство("МаркерРасшифрованный") Тогда
				
				СвойстваУведомлений = НастройкиОповещения(СтруктураПараметровЗапросаМаркера);
				
				Если Не СвойстваУведомлений = Неопределено Тогда
					ЭлектроннаяПочтаСуществующая = СвойстваУведомлений.ЭлектроннаяПочта;
					УведомлятьОСобытиях = СвойстваУведомлений.ВключитьПодписку;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция НастройкиОповещения(СтруктураПараметровЗапросаМаркера)
	
	СвойстваУведомлений = Неопределено;
	Идентификатор = СтруктураПараметровЗапросаМаркера.ИдентификаторОрганизации;
	АдресРесурса = "GetSubscriptions/?query=" + Идентификатор;
	ВидОперации = НСтр("ru = 'Получение информации о свойствах подписки ЭДО'");
	
	ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСвойстваУведомленийЭДО(АдресРесурса, ВидОперации, СтруктураПараметровЗапросаМаркера,
		СвойстваУведомлений);
		
	Возврат СвойстваУведомлений;
	
КонецФункции

// Обмен через каталог

&НаСервереБезКонтекста
Процедура ТестСвязиПрямогоОбменаНаСервере(КаталогВходящихДокументов, Отказ)
	
	// Блок проверки доступа к каталогам.
	Попытка
		Если Не ЭлектронныеДокументыСлужебныйВызовСервера.ПроверитьДоступностьКаталогаДляПрямогоОбмена(КаталогВходящихДокументов) Тогда
			ТекстОшибки = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("107");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
			Отказ = Истина;
		КонецЕсли;
	Исключение
		ТекстОшибки = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("107");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		Отказ = Истина;
	КонецПопытки;
	
КонецПроцедуры

// Обмен через FTP

&НаСервере
Процедура ТестСвязиОбменаЧерезFTPНаСервере(Отказ)
	
	ИспользоватьПрокси = Ложь;
	
	НастройкаПроксиСервера = ПолучениеФайловИзИнтернета.НастройкиПроксиНаСервере();
	Если НастройкаПроксиСервера <> Неопределено Тогда
		ПараметрИспользоватьПрокси = НастройкаПроксиСервера.Получить("ИспользоватьПрокси");
		Если НЕ ПараметрИспользоватьПрокси=Неопределено Тогда
			ИспользоватьПрокси = ПараметрИспользоватьПрокси;
		КонецЕсли;
	КонецЕсли;
	
	Если ИспользоватьПрокси Тогда
		Если НастройкаПроксиСервера.Получить("ИспользоватьСистемныеНастройки") Тогда
			// Системные настройки прокси-сервера.
			Прокси = Новый ИнтернетПрокси(Истина);
		Иначе
			// Ручные настройки прокси-сервера.
			Прокси = Новый ИнтернетПрокси;
			Прокси.Установить("ftp", НастройкаПроксиСервера["Сервер"], НастройкаПроксиСервера["Порт"]);
			Прокси.НеИспользоватьПроксиДляЛокальныхАдресов = НастройкаПроксиСервера["НеИспользоватьПроксиДляЛокальныхАдресов"];
			Прокси.Пользователь = НастройкаПроксиСервера["Пользователь"];
			Прокси.Пароль = НастройкаПроксиСервера["Пароль"];
		КонецЕсли;
	Иначе
		Прокси = Новый ИнтернетПрокси(Ложь);
	КонецЕсли;
	
	Попытка
		FTPСоединение = Новый FTPСоединение(АдресСервераFTP,
											ПортFTP,
											ПользовательFTP,
											ПарольFTP,
											Прокси,
											ПассивноеСоединениеFTP);
	Исключение
		ТекстОшибки = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("121");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		Отказ = Истина;
		Возврат;
	КонецПопытки;
	
	Попытка
		ЭлектронныеДокументыСлужебный.ПодготовитьПутьFTP(FTPКаталогВходящихДокументов);
		FTPСоединение.УстановитьТекущийКаталог(FTPКаталогВходящихДокументов);
	Исключение
		ЭлектронныеДокументыСлужебный.СоздатьКаталогиFTP(FTPСоединение, FTPКаталогВходящихДокументов, Истина, ТекстОшибки);
	КонецПопытки;
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ПроверитьФайл(FTPСоединение, ТекстОшибки);
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьФайл(FTPСоединение, ТекстОшибки)
	
	ВремФайл = ПолучитьИмяВременногоФайла();
	ТекстовыйДокумент = Новый ТекстовыйДокумент();
	ТестоваяСтрока = "Тестовая строка 1С: Предприятие";
	ТекстовыйДокумент.УстановитьТекст(ТестоваяСтрока);
	ТекстовыйДокумент.Записать(ВремФайл);
	ФайлТест = Новый Файл(ВремФайл);
		
	ЭлектронныеДокументыСлужебный.ЗаписатьФайлНаFTP(FTPСоединение, ВремФайл, ФайлТест.Имя, Истина, ТекстОшибки);
	УдалитьФайлы(ВремФайл);

	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		Возврат;
	КонецЕсли;
	
	ФайлПолучатель = ПолучитьИмяВременногоФайла();
	
	ЭлектронныеДокументыСлужебный.ПолучитьФайлСFTP(FTPСоединение, ФайлТест.Имя, ФайлПолучатель, Истина, ТекстОшибки);
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		УдалитьФайлы(ФайлПолучатель);
		Возврат;
	КонецЕсли;
		
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.Прочитать(ФайлПолучатель);
	СтрокаРезультата = ТекстовыйДокумент.ПолучитьТекст();
	УдалитьФайлы(ФайлПолучатель);

	Если НЕ СтрокаРезультата = ТестоваяСтрока Тогда
		ШаблонСообщения = НСтр("ru = '%1 %2.'");
		ТекстСообщения = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("126");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ТекстСообщения,
			FTPСоединение.ТекущийКаталог());
		Возврат;
	КонецЕсли;
	
	ЭлектронныеДокументыСлужебный.УдалитьФайлFTP(FTPСоединение, ФайлТест.Имя, Истина, ТекстОшибки);
	
КонецПроцедуры


&НаКлиенте
Функция ПроверитьПринятиеУсловийИспользования()
	
	Если Не ЭлектронныеДокументыСлужебныйВызовСервера.ЭтоПрямойОбмен(СпособОбменаЭД) И Не ПринятыУсловияИспользования Тогда
		ТекстСообщения = НСтр("ru = 'Для продолжения необходимо принять условия использования.'");
		Предупреждение(ТекстСообщения);
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ПолучитьСпособОбменаПоИдентификатору()
	
	ИдентификаторНаПроверку = ?(ЗначениеЗаполнено(ИдентификаторОрганизацииСуществующий), ИдентификаторОрганизацииСуществующий, ИдентификаторОрганизации);
	Если Не ЗначениеЗаполнено(ИдентификаторНаПроверку) Тогда
		Возврат;
	КонецЕсли;
	СпособОбменаАбонента = СпособОбменаАбонентаНаСервере(ИдентификаторНаПроверку);
	Если ЗначениеЗаполнено(СпособОбменаАбонента) Тогда
		СпособОбменаЭД = СпособОбменаАбонента;
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция СпособОбменаАбонентаНаСервере(Знач ИдентификаторЭДО)
	
	Возврат ЭлектронныеДокументыСлужебный.СпособОбменаАбонентаЭДО(ИдентификаторЭДО);
	
КонецФункции

