#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьБыстрыйОтборСервер();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	СобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
	ИнтеграцияИС.УстановитьПризнакПравоИзмененияФормыСписка(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьБыстрыйОтборСервер()
	
	Если Параметры.Свойство("СтруктураБыстрогоОтбора") Тогда
		
		Организации = Неопределено;
		Статус      = Неопределено;
		ОКПД2       = Неопределено;
		
		Параметры.СтруктураБыстрогоОтбора.Свойство("Организации", Организации);
		Параметры.СтруктураБыстрогоОтбора.Свойство("Статус",      Статус);
		Параметры.СтруктураБыстрогоОтбора.Свойство("ОКПД2",       ОКПД2);
		
		Если ЗначениеЗаполнено(Организации) Тогда
			ТипОрганизации = ТипЗнч(Организации);
			Если ТипОрганизации = Тип("Массив") Тогда
				МассивОрганизаций = Организации;
			ИначеЕсли ТипОрганизации = Тип("СписокЗначений") Тогда
				МассивОрганизаций = Организации.ВыгрузитьЗначения();
			Иначе
				МассивОрганизаций = Новый Массив;
				МассивОрганизаций.Добавить(Организации);
			КонецЕсли;
			КлючиРеквизитовОрганизаций = Новый СписокЗначений;
			КлючиРеквизитовОрганизаций.ЗагрузитьЗначения(
				Справочники.КлючиРеквизитовОрганизацийЗЕРНО.КлючиПоОрганизациямКонтрагентам(МассивОрганизаций));
			
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				Список,
				"Товаропроизводитель",
				КлючиРеквизитовОрганизаций,,,
				Истина);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Статус) Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				Список,
				"Статус",
				Статус,,,
				Истина);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОКПД2) Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				Список,
				"ОКПД2",
				ОКПД2,,,
				Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#Область ПодключаемыеКоманды

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтаФорма, Команда, Элементы.Список);
КонецПроцедуры

//@skip-warning
&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтаФорма, Контекст, Элементы.Список, Результат);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтаФорма, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормИСКлиентПереопределяемый.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

