#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ПометкаУдаления Тогда
		ПроверяемыеРеквизиты.Очистить();
		Возврат;
	КонецЕсли;
	
	МодульОбменСГИСЭПД = Неопределено;
	Если ОбщегоНазначенияКлиентСервер.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСГИСЭПД") Тогда
		МодульОбменСГИСЭПД = ОбщегоНазначенияКлиентСервер.ОбщийМодуль("ОбменСГИСЭПД");	
	КонецЕсли;
	
	Если НЕ ЭтоИнтеркампани
		И (СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезОператораЭДОТакском
			ИЛИ СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезСервис1СЭДО
			ИЛИ СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезЭлектроннуюПочту
			ИЛИ СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезКаталог
			ИЛИ СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезFTP) Тогда
		
		Если Не ЗначениеЗаполнено(Контрагент) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Контрагент"),
				ЭтотОбъект,
				"Контрагент",
				,
				Отказ);
		КонецЕсли;
		
		ПроверитьНастройкиПрямогоОбмена = Ложь;
		
		Если РасширенныйРежимНастройкиСоглашения Тогда
			
			МассивСтрокЭлектронныхДокументов = ИсходящиеДокументы.НайтиСтроки(Новый Структура("Формировать", Истина));
			Для каждого СтрокаТаблицы Из МассивСтрокЭлектронныхДокументов Цикл
				
				Префикс = "ИсходящиеДокументы[" + Формат(СтрокаТаблицы.НомерСтроки - 1, "ЧН=0; ЧГ=") + "].";
				
				Если Не ЗначениеЗаполнено(СтрокаТаблицы.ПрофильНастроекЭДО) Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Колонка", "ЗАПОЛНЕНИЕ", "Профиль настроек ЭДО",
						СтрокаТаблицы.НомерСтроки, "Виды электронных документов"),
						ЭтотОбъект,
						Префикс + "ПрофильНастроекЭДО",
						,
						Отказ);
					КонецЕсли;
				Если Не ЗначениеЗаполнено(СтрокаТаблицы.ВерсияФормата)
					И СтрокаТаблицы.ИсходящийДокумент <> Перечисления.ВидыЭД.ПроизвольныйЭД
					И СтрокаТаблицы.ИсходящийДокумент <> Перечисления.ВидыЭД.МашиночитаемаяДоверенность
					И (МодульОбменСГИСЭПД <> Неопределено
						И НЕ МодульОбменСГИСЭПД.ЭтоВидДокументаЭПД(СтрокаТаблицы.ИсходящийДокумент)) Тогда
					
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Колонка", "ЗАПОЛНЕНИЕ", "Формат электронного документа",
						СтрокаТаблицы.НомерСтроки, "Виды электронных документов"),
						ЭтотОбъект,
						Префикс + "ВерсияФормата",
						,
						Отказ);
				КонецЕсли;
				Если ЭлектронныеДокументыСлужебныйВызовСервера.ЭтоПрямойОбмен(СтрокаТаблицы.СпособОбменаЭД)
					И Не ЗначениеЗаполнено(СтрокаТаблицы.ИдентификаторКонтрагента) Тогда
					
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Колонка", "ЗАПОЛНЕНИЕ", "Идентификатор контрагента",
						СтрокаТаблицы.НомерСтроки, "Виды электронных документов"),
						ЭтотОбъект,
						Префикс + "ИдентификаторКонтрагента",
						,
						Отказ);
				КонецЕсли;
				
				Если СтрокаТаблицы.СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезКаталог Тогда
					ПроверитьНастройкиПрямогоОбмена = Истина;
					
					ПроверяемыеРеквизиты.Добавить("КаталогВходящихДокументов");
					ПроверяемыеРеквизиты.Добавить("КаталогИсходящихДокументов");
				ИначеЕсли СтрокаТаблицы.СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезЭлектроннуюПочту Тогда
					ПроверитьНастройкиПрямогоОбмена = Истина;
					
					ПроверяемыеРеквизиты.Добавить("ЭлектроннаяПочтаКонтрагента");
				ИначеЕсли СтрокаТаблицы.СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезFTP Тогда
					ПроверитьНастройкиПрямогоОбмена = Истина;
					
					ПроверяемыеРеквизиты.Добавить("КаталогВходящихДокументовFTP");
					ПроверяемыеРеквизиты.Добавить("КаталогИсходящихДокументовFTP");
				КонецЕсли;
			КонецЦикла;
			
		Иначе
			
			Если ЭлектронныеДокументыСлужебныйВызовСервера.ЭтоПрямойОбмен(СпособОбменаЭД)
				И Не ЗначениеЗаполнено(ИдентификаторКонтрагента) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Идентификатор контрагента"),
					ЭтотОбъект,
					"ИдентификаторКонтрагента",
					,
					Отказ);
			КонецЕсли;
			
			Если СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезКаталог Тогда
				ПроверяемыеРеквизиты.Добавить("КаталогВходящихДокументов");
				ПроверяемыеРеквизиты.Добавить("КаталогИсходящихДокументов");
			ИначеЕсли СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезЭлектроннуюПочту Тогда
				
				ПроверяемыеРеквизиты.Добавить("ЭлектроннаяПочтаКонтрагента");
			ИначеЕсли СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезFTP Тогда
				
				ПроверяемыеРеквизиты.Добавить("КаталогВходящихДокументовFTP");
				ПроверяемыеРеквизиты.Добавить("КаталогИсходящихДокументовFTP");
			КонецЕсли;
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	ПроверяемыеРеквизиты.Очистить();
	Если ЭтоИнтеркампани И СтатусСоглашения = Перечисления.СтатусыСоглашенийЭД.Действует Тогда
		
		Если НЕ ЗначениеЗаполнено(Организация) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Организация-отправитель"),
				ЭтотОбъект,
				"Организация",
				,
				Отказ);
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(ИдентификаторОрганизации) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Идентификатор отправителя"),
				ЭтотОбъект,
				"ИдентификаторОрганизации",
				,
				Отказ);
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Контрагент) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Организация-получатель"),
				ЭтотОбъект,
				"Контрагент",
				,
				Отказ);
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(ИдентификаторКонтрагента) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Идентификатор получателя"),
				ЭтотОбъект,
				"ИдентификаторКонтрагента",
				,
				Отказ);
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	ПроверяемыеРеквизиты.Добавить("Наименование");
	Если СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезВебРесурсБанка
		И СтатусСоглашения = Перечисления.СтатусыСоглашенийЭД.Действует Тогда
		
		
		Если НЕ ЗначениеЗаполнено(Контрагент) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Банк"),
				ЭтотОбъект,
				"Контрагент",
				,
				Отказ);
		КонецЕсли;
		
		Если ПрограммаБанка = Перечисления.ПрограммыБанка.СбербанкОнлайн Тогда
			Если НЕ ЗначениеЗаполнено(ИдентификаторОрганизации) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Идентификатор организации"),
						ЭтотОбъект,
						"ИдентификаторОрганизации",
						,
						Отказ);
			КонецЕсли;
		ИначеЕсли ПрограммаБанка = Перечисления.ПрограммыБанка.ОбменЧерезДопОбработку Тогда
			Если НЕ ЗначениеЗаполнено(ДополнительнаяОбработка) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Дополнительная обработка"),
					ЭтотОбъект,
					"ДополнительнаяОбработка",
					,
					Отказ);
			КонецЕсли;
		Иначе
			Если НЕ ЗначениеЗаполнено(АдресСервера) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Адрес сервера банка"),
					ЭтотОбъект,
					"АдресСервера",
					,
					Отказ);
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(РесурсИсходящихДокументов) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Ресурс для отправки"),
					ЭтотОбъект,
					"РесурсИсходящихДокументов",
					,
					Отказ);
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(РесурсВходящихДокументов) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Ресурс для получения"),
					ЭтотОбъект,
					"РесурсВходящихДокументов",
					,
					Отказ);
			КонецЕсли;
		КонецЕсли;
		
		Если ПрограммаБанка = Перечисления.ПрограммыБанка.ОбменЧерезДопОбработку ИЛИ ИспользуетсяКриптография Тогда
			Если СертификатыПодписейОрганизации.Количество() = 0 Тогда
				ТекстСообщения = ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Список",
				                                                                         "Заполнение",
				                                                                         ,
				                                                                         ,
				                                                                         "Подписи платежных поручений");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
				                                                  ЭтотОбъект,
				                                                  "СертификатыПодписейОрганизации",
				                                                  ,
				                                                  Отказ);
			КонецЕсли;
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не НастройкаЭДОУникальна() Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если ЕстьНастройкаДляОтправки() И ИспользуетсяДляОтправки Тогда
		Отказ = Истина;
		ТекстСообщения = НСтр("ru = 'Для данного сочетания организации, контрагента, договора уже есть настройка для отправки. Операция отменена.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Если НЕ ПометкаУдаления Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СоглашенияОбИспользованииЭДИсходящиеДокументы.ПрофильНастроекЭДО
		|ИЗ
		|	Справочник.СоглашенияОбИспользованииЭД.ИсходящиеДокументы КАК СоглашенияОбИспользованииЭДИсходящиеДокументы
		|ГДЕ
		|	СоглашенияОбИспользованииЭДИсходящиеДокументы.ПрофильНастроекЭДО.ПометкаУдаления
		|	И СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка = &Ссылка";
		Запрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
		
		Результат = Запрос.Выполнить().Выбрать();
		Если Результат.Следующий() Тогда
			ШаблонСообщения = НСтр("ru = 'Операция отменена.
				|В текущей настройке ЭДО используется помеченный на удаление профиль настроек ЭДО:
				|%1'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, Результат.ПрофильНастроекЭДО);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		КонецЕсли;
	КонецЕсли;
	
	ЭтотОбъект.ДополнительныеСвойства.Вставить("ЕстьСоглашение", Истина);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Только для внутреннего использования
Функция НастройкаЭДОУникальна() Экспорт
	
	Если ПометкаУдаления Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если СтатусПодключения <> Перечисления.СтатусыУчастниковОбменаЭД.Присоединен Тогда
		Возврат Истина;
	КонецЕсли;
	
	ТекущаяНастройкаУникальна = Истина;
	
	// Проверка на уникальное использование настройки по реквизитам: Организация, Контрагент, ДоговорКонтрагента.
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НастройкиЭДО.Ссылка КАК Настройка,
	|	НастройкиЭДО.Контрагент КАК Контрагент,
	|	НастройкиЭДО.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	НастройкиЭДО.Организация КАК Организация
	|ИЗ
	|	Справочник.СоглашенияОбИспользованииЭД КАК НастройкиЭДО
	|ГДЕ
	|	НЕ НастройкиЭДО.ПометкаУдаления
	|	И НастройкиЭДО.Организация = &Организация
	|	И НастройкиЭДО.Контрагент = &Контрагент
	|	И НастройкиЭДО.ДоговорКонтрагента = &ДоговорКонтрагента
	|	И НастройкиЭДО.СтатусПодключения = ЗНАЧЕНИЕ(Перечисление.СтатусыУчастниковОбменаЭД.Присоединен)
	|	И НастройкиЭДО.Ссылка <> &ТекущаяНастройка
	|	И (ВЫРАЗИТЬ(НастройкиЭДО.ИдентификаторКонтрагента КАК СТРОКА(300))) = &ИдентификаторКонтрагента
	|	И (ВЫРАЗИТЬ(НастройкиЭДО.ИдентификаторОрганизации КАК СТРОКА(300))) = &ИдентификаторОрганизации";
	Запрос.УстановитьПараметр("ТекущаяНастройка",   ЭтотОбъект.Ссылка);
	Запрос.УстановитьПараметр("Организация",        ЭтотОбъект.Организация);
	Запрос.УстановитьПараметр("Контрагент",         ЭтотОбъект.Контрагент);
	Запрос.УстановитьПараметр("ДоговорКонтрагента", ЭтотОбъект.ДоговорКонтрагента);
	Запрос.УстановитьПараметр("ИдентификаторКонтрагента", ИдентификаторКонтрагента);
	Запрос.УстановитьПараметр("ИдентификаторОрганизации", ИдентификаторОрганизации);
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		ТекущаяНастройкаУникальна = Ложь;
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			ШаблонСообщения = НСтр("ru = 'В информационной базе уже существует настройка ЭДО со статусом подключения ""Присоединен""
										|между контрагентом %1 и организацией %2'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, Выборка.Контрагент,
				Выборка.Организация);
			Если ЗначениеЗаполнено(Выборка.ДоговорКонтрагента) Тогда
				Шаблон = НСтр("ru = '%1 по договору %2'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
										Шаблон, ТекстСообщения, Выборка.ДоговорКонтрагента);
			КонецЕсли;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ТекущаяНастройкаУникальна;
	
КонецФункции

Функция ЕстьНастройкаДляОтправки() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Соглашения.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СоглашенияОбИспользованииЭД КАК Соглашения
	|ГДЕ
	|	Соглашения.ИспользуетсяДляОтправки
	|	И Соглашения.Ссылка <> &ТекущаяСсылка
	|	И Соглашения.Организация = &Организация
	|	И Соглашения.Контрагент = &Контрагент
	|	И Соглашения.ДоговорКонтрагента = &ДоговорКонтрагента
	|	И Соглашения.СтатусПодключения <> &СтатусПодключенияОтсоединен";
	Запрос.УстановитьПараметр("ТекущаяСсылка", Ссылка);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("ДоговорКонтрагента", ДоговорКонтрагента);
	Запрос.УстановитьПараметр("СтатусПодключенияОтсоединен", Перечисления.СтатусыУчастниковОбменаЭД.Отсоединен);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецЕсли