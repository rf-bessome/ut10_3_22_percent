&НаКлиенте
Перем мПрофиль;


//////////////////////////////////////////////////////////
// ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ИсходящийДокумент = Параметры.ИсходящийДокумент;
	СпособОбменаЭД = Параметры.СпособОбменаЭД;
	ПрофильНастроекЭДО = Параметры.ПрофильНастроекЭДО;
	ИдентификаторОрганизации = Параметры.ИдентификаторОрганизации;
	ИспользоватьЭЦП = Параметры.ИспользоватьЭЦП;
	ТребуетсяИзвещениеОПолучении = Параметры.ТребуетсяИзвещениеОПолучении;
	ТребуетсяОтветнаяПодпись = Параметры.ТребуетсяОтветнаяПодпись;
	Организация = Параметры.Организация;
	ИспользоватьУПД = Параметры.ИспользоватьУПД;
	НастройкаЭДО = Параметры.НастройкаЭДО;
	ВерсияФормата = Параметры.ВерсияФормата;
	ЗаполнениеКодаТовара = Параметры.ЗаполнениеКодаТовара;
	РасширенныйРежимНастройки = Параметры.РасширенныйРежимНастройки;
	
	ИспользоватьЭЦПВидимость = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьЗначениеФункциональнойОпции(
			"ИспользоватьЭлектронныеЦифровыеПодписи");
			
	ИнициализироватьНастройкиФормированияДокумента();
	
	ИнициализироватьНастройкиЗаполненияДополнительныхПолей();
	
	ЗаполнитьТекстПоясняющейНадписи(ИспользоватьЭЦПВидимость И ИспользоватьЭЦП);
	
	УстановитьВидимость(ИспользоватьЭЦПВидимость);
	
	УстановитьДоступность(Параметры.НастройкиРегламентаЭДО);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	мПрофиль = ПрофильНастроекЭДО;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ИсточникВыбора.ИмяФормы = "Обработка.ЭлектронныеДокументы.Форма.НастройкаЗаполненияДополнительныхПолей" Тогда

		ОбработатьЗавершениеНастройкиЗаполненияДополнительныхПолей(ВыбранноеЗначение);
		
	КонецЕсли;
	
КонецПроцедуры

//////////////////////////////////////////////////////////
// ОбработчикиСобытийПолейФормы

&НаКлиенте
Процедура ИспользоватьЭЦППриИзменении(Элемент)
	
	Если Не ИсходящийДокумент = ПредопределенноеЗначение("Перечисление.ВидыЭД.СчетНаОплату") Тогда
		ТребуетсяОтветнаяПодпись = ИспользоватьЭЦП;
		Элементы.ОжидатьОтветнуюПодпись.Доступность = ИспользоватьЭЦП;
	КонецЕсли;
	
	ЗаполнитьТекстПоясняющейНадписи();

КонецПроцедуры

&НаКлиенте
Процедура ОжидатьИзвещениеОПолученииПриИзменении(Элемент)
	
	ЗаполнитьТекстПоясняющейНадписи();

КонецПроцедуры

&НаКлиенте
Процедура ОжидатьОтветнуюПодписьПриИзменении(Элемент)
	
	Если Не ТребуетсяОтветнаяПодпись Тогда
		ТекстВопроса = НСтр("ru = 'Отказ от ответной подписи разрешается, если:
			|- документ выписан на оказание услуг, по которым ГК РФ не требует актирования;
			|- в соглашении об обмене электронными документами предусмотрено отсутствие ответной подписи для данного вида документа.
			|Если документ не соответствует перечисленным критериям, отказ от ожидания ответной подписи может привести к тому, что документ будет признан недействительным.
			|
			|Вы действительно хотите отказаться от ожидания ответной подписи?'");
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет,"");
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			ТребуетсяОтветнаяПодпись = Истина;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьТекстПоясняющейНадписи();
	
КонецПроцедуры

&НаКлиенте
Процедура ЕстьНастройкиЗаполненияДополнительныхПолейНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаЭДО", НастройкаЭДО);
	ПараметрыФормы.Вставить("ВидЭлектронногоДокумента", ИсходящийДокумент);
	ПараметрыФормы.Вставить("Формат", ВерсияФормата);
	
	ОткрытьФорму("Обработка.ЭлектронныеДокументы.Форма.НастройкаЗаполненияДополнительныхПолей", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнениеКодаТовараОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

//////////////////////////////////////////////////////////
// ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаЗаписатьИЗакрыть(Команда)
	
	Если Не ЗначениеЗаполнено(ПрофильНастроекЭДО) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", НСтр("ru = 'Профиль настроек ЭДО'")),
			,
			"ПрофильНастроекЭДО");
		Возврат;
	КонецЕсли;
	
	ПараметрыЗакрытия = ЭлектронныеДокументыСлужебныйКлиент.СвойстваДокументооборотаЭД();
	
	ПараметрыЗакрытия.ИсходящийДокумент = ИсходящийДокумент;
	ПараметрыЗакрытия.ИспользоватьЭЦП = ИспользоватьЭЦП;
	ПараметрыЗакрытия.ПрофильНастроекЭДО = ПрофильНастроекЭДО;
	ПараметрыЗакрытия.СпособОбменаЭД = СпособОбменаЭД;
	ПараметрыЗакрытия.ИдентификаторОрганизации = ИдентификаторОрганизации;
	ПараметрыЗакрытия.ТребуетсяИзвещениеОПолучении = ТребуетсяИзвещениеОПолучении;
	ПараметрыЗакрытия.ТребуетсяОтветнаяПодпись = ТребуетсяОтветнаяПодпись;
	
	// Дополнительные служебные свойства
	ПараметрыЗакрытия.Вставить("Модифицированность", Модифицированность);
	ПараметрыЗакрытия.Вставить("ЗаполнениеКодаТовара", ЗаполнениеКодаТовара);
	
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗакрыть(Команда)
	
	Закрыть();
	
КонецПроцедуры


//////////////////////////////////////////////////////////
// СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТекстПоясняющейНадписи(ИспользоватьЭЦПВидимость = Неопределено)
	
	Если РасширенныйРежимНастройки Тогда
		Возврат;
	КонецЕсли;
	
	ТекстНадписи = "";
	
	Если ИспользоватьЭЦПВидимость = Неопределено Тогда
		ФОИспользоватьЭЦП = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьЗначениеФункциональнойОпции(
			"ИспользоватьЭлектронныеЦифровыеПодписи");
		ИспользоватьЭЦПВидимость = ИспользоватьЭЦП И ФОИспользоватьЭЦП;
	КонецЕсли;
	
	Если ИспользоватьЭЦПВидимость Тогда
		ТекстНадписи = НСтр("ru = 'Документ будет подписываться электронной подписью;'");
	Иначе
		ТекстНадписи = НСтр("ru = 'Документ не будет подписываться электронной подписью;'");
	КонецЕсли;
	
	Если ТребуетсяИзвещениеОПолучении Тогда
		ТекстНадписи = ТекстНадписи + Символы.ПС + НСтр("ru = 'ожидается извещение о получении документа;'");
	Иначе
		ТекстНадписи = ТекстНадписи + Символы.ПС + НСтр("ru = 'извещение о получении документа не требуется;'");
	КонецЕсли;
	
	Если ТребуетсяОтветнаяПодпись И ИспользоватьЭЦПВидимость Тогда
		ТекстНадписи = ТекстНадписи + Символы.ПС + НСтр("ru = 'ожидается ответная подпись документа.'");
	Иначе
		Если ИспользоватьУПД И ИсходящийДокумент = Перечисления.ВидыЭД.СчетФактура Тогда
			ТекстНадписи = ТекстНадписи + Символы.ПС + НСтр("ru = '* ответная подпись не требуется для документов оказания услуг.'");
		Иначе
			ТекстНадписи = ТекстНадписи + Символы.ПС + НСтр("ru = 'ответная подпись документа не требуется.'");
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ДекорацияПоясняющийТекст.Заголовок = ТекстНадписи;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрофильПриИзменении(Элемент)
	
	Отказ = Ложь;
	ПрофильПриИзмененииНаСервере(Отказ);
	Если Отказ Тогда
		ПрофильНастроекЭДО = мПрофиль;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПрофильПриИзмененииНаСервере(Отказ)
	
	// Если выбранный профиль прямого обмена
	// и э.д. счет фактура или корректировочный счет фактура,
	// то при выборе такого профиля необходимо показать предупреждение.
	
	Если ИсходящийДокумент = Перечисления.ВидыЭД.СчетФактура
		Или ИсходящийДокумент = Перечисления.ВидыЭД.КорректировочныйСчетФактура Тогда
		
		
		СпособОбмена = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПрофильНастроекЭДО, "СпособОбменаЭД");
		Если ЭлектронныеДокументыСлужебныйВызовСервера.ЭтоПрямойОбмен(СпособОбмена) Тогда
			
			ШаблонСообщения = НСтр("ru='Отправка документа %1 возможна только через оператора ЭДО.'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, ИсходящийДокумент);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			
			Отказ = Истина;
			
			Возврат;
			
		КонецЕсли;
	КонецЕсли;
	
	// Ищет исходящий документ в выбранном профиле
	// и заполняет регламент ЭДО из нового профиля.
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПрофилиНастроекЭДОИсходящиеДокументы.ИспользоватьЭЦП КАК ИспользоватьЭЦП,
	|	ПрофилиНастроекЭДОИсходящиеДокументы.ТребуетсяОтветнаяПодпись КАК ТребуетсяОтветнаяПодпись,
	|	ПрофилиНастроекЭДОИсходящиеДокументы.ТребуетсяИзвещениеОПолучении КАК ТребуетсяИзвещениеОПолучении,
	|	ПрофилиНастроекЭДОИсходящиеДокументы.ВерсияФормата КАК ВерсияФормата,
	|	ПрофилиНастроекЭДОИсходящиеДокументы.Ссылка.СпособОбменаЭД КАК СпособОбменаЭД,
	|	ПрофилиНастроекЭДОИсходящиеДокументы.Ссылка.ИдентификаторОрганизации КАК ИдентификаторОрганизации
	|ИЗ
	|	Справочник.ПрофилиНастроекЭДО.ИсходящиеДокументы КАК ПрофилиНастроекЭДОИсходящиеДокументы
	|ГДЕ
	|	ПрофилиНастроекЭДОИсходящиеДокументы.Ссылка = &Ссылка
	|	И ПрофилиНастроекЭДОИсходящиеДокументы.ИсходящийДокумент = &ИсходящийДокумент";
	Запрос.УстановитьПараметр("Ссылка", ПрофильНастроекЭДО);
	Запрос.УстановитьПараметр("ИсходящийДокумент", ИсходящийДокумент);
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ИспользоватьЭЦП = Выборка.ИспользоватьЭЦП;
		ТребуетсяОтветнаяПодпись = Выборка.ТребуетсяОтветнаяПодпись;
		ТребуетсяИзвещениеОПолучении = Выборка.ТребуетсяИзвещениеОПолучении;
		ВерсияФормата = Выборка.ВерсияФормата;
		СпособОбменаЭД = Выборка.СпособОбменаЭД;
		ИдентификаторОрганизации = Выборка.ИдентификаторОрганизации;
		
	КонецЦикла;
	
	СпособОбменаЭД = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПрофильНастроекЭДО, "СпособОбменаЭД");

	НастройкиРегламентаЭДО = ЭлектронныеДокументыСлужебныйВызовСервера.РегламентПрофиляЭДО(ИсходящийДокумент, ВерсияФормата, СпособОбменаЭД);
	УстановитьДоступность(НастройкиРегламентаЭДО);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступность(НастройкиРегламента)
	
	Элементы.ИспользоватьЭЦП.Доступность = НастройкиРегламента.РедактироватьПодпись;
	Элементы.ОжидатьИзвещениеОПолучении.Доступность = НастройкиРегламента.РедактироватьИзвещение;
	Элементы.ОжидатьОтветнуюПодпись.Доступность = НастройкиРегламента.РедактироватьОтветнуюПодпись И ИспользоватьЭЦП;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимость(ИспользоватьЭЦПВидимость = Неопределено)
	
	Если ИспользоватьЭЦПВидимость = Неопределено Тогда
		ФОИспользоватьЭЦП = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьЗначениеФункциональнойОпции(
			"ИспользоватьЭлектронныеЦифровыеПодписи");
		ИспользоватьЭЦПВидимость = ИспользоватьЭЦП И ФОИспользоватьЭЦП
	КонецЕсли;
	
	Элементы.ИспользоватьЭЦП.Видимость = ИспользоватьЭЦПВидимость;
	Элементы.ОжидатьОтветнуюПодпись.Видимость = ИспользоватьЭЦПВидимость;
	
	Элементы.ДекорацияПоясняющийТекст.Видимость = НЕ РасширенныйРежимНастройки;
	Элементы.ГруппаНастройкиФормированияДокумента.Видимость = РасширенныйРежимНастройки;
	Если ЭлектронныеДокументыСлужебныйВызовСервера.ЭтоПрямойОбмен(СпособОбменаЭД)
		Или ЭлектронныеДокументыВнутренний.РазделыДополнительныхПолейФорматаЭлектронногоДокумента(
		ИсходящийДокумент, ВерсияФормата).Количество() = 0 Тогда
		Элементы.ЕстьНастройкиЗаполненияДополнительныхПолей.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьНастройкиФормированияДокумента()
	
	Если НЕ РасширенныйРежимНастройки Тогда
		Возврат;
	КонецЕсли;
	
	ВариантыЗаполненияПолей = ЭлектронныеДокументыВнутренний.ВариантыЗаполненияПолейЭлектронныхДокументов(
		ИсходящийДокумент, ВерсияФормата);
	
	ЗначениеСвойства = Неопределено;
	Если ВариантыЗаполненияПолей.Свойство("ТоварКод", ЗначениеСвойства) Тогда
		Для Каждого Вариант Из ЗначениеСвойства Цикл
			ЗаполнитьЗначенияСвойств(Элементы.ЗаполнениеКодаТовара.СписокВыбора.Добавить(), Вариант);
		КонецЦикла;
	Иначе
		Элементы.ЗаполнениеКодаТовара.Видимость = ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьНастройкиЗаполненияДополнительныхПолей()
	
	Если НЕ РасширенныйРежимНастройки Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиЗаполненияДополнительныхПолей.НастройкаЭДО КАК НастройкаЭДО
		|ИЗ
		|	РегистрСведений.НастройкиЗаполненияДополнительныхПолей КАК НастройкиЗаполненияДополнительныхПолей
		|ГДЕ
		|	НастройкиЗаполненияДополнительныхПолей.НастройкаЭДО = &НастройкаЭДО
		|	И НастройкиЗаполненияДополнительныхПолей.ВидЭлектронногоДокумента = &ВидЭлектронногоДокумента
		|	И НастройкиЗаполненияДополнительныхПолей.Формат = &Формат";
	
	Запрос.УстановитьПараметр("ВидЭлектронногоДокумента", ИсходящийДокумент);
	Запрос.УстановитьПараметр("НастройкаЭДО", НастройкаЭДО);
	Запрос.УстановитьПараметр("Формат", ВерсияФормата);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ЕстьНастройкиЗаполненияДополнительныхПолей = НЕ РезультатЗапроса.Пустой();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьЗавершениеНастройкиЗаполненияДополнительныхПолей(Результат) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьНастройкиЗаполненияДополнительныхПолей = Результат;
	
КонецПроцедуры




