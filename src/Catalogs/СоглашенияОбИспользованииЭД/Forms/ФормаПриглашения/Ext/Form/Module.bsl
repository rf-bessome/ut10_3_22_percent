////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НастройкаЭДО = "";
	Если Параметры.Свойство("НастройкаЭДО", НастройкаЭДО) И ЗначениеЗаполнено(НастройкаЭДО) Тогда
		ПараметрыНастроекЭДО = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НастройкаЭДО,
			"Организация, Контрагент, СтатусПодключения, ТекстПриглашения, ИдентификаторКонтрагента,
			|ЭлектроннаяПочтаДляПриглашения, ИсходящиеДокументы, ПричинаОтказа");
			
		ИсходящиеДокументы = ПараметрыНастроекЭДО.ИсходящиеДокументы.Выгрузить();
		
		Отбор = Новый Структура;
		Отбор.Вставить("СпособОбменаЭД", Перечисления.СпособыОбменаЭД.ЧерезОператораЭДОТакском);
		НайденныеСтроки = ИсходящиеДокументы.НайтиСтроки(Отбор);
		Если Не ЗначениеЗаполнено(НайденныеСтроки) Тогда
			Отбор = Новый Структура;
			Отбор.Вставить("СпособОбменаЭД", Перечисления.СпособыОбменаЭД.ЧерезСервис1СЭДО);
			НайденныеСтроки = ИсходящиеДокументы.НайтиСтроки(Отбор);
		КонецЕсли;
		
		ПричинаОтказаОтПриглашения = ПараметрыНастроекЭДО.ПричинаОтказа;
		
		// Для исходящих и входящих приглашений заполнение полей отличается.
		СтатусПодключения = ПараметрыНастроекЭДО.СтатусПодключения;
		СпособОбменаЭД     = НайденныеСтроки[0].СпособОбменаЭД;
		Элементы.ЭлектроннаяПочта.АвтоОтметкаНезаполненного = Истина;
		
		Если СтатусПодключения = Перечисления.СтатусыУчастниковОбменаЭД.ТребуетсяПригласить
			ИЛИ СтатусПодключения = Перечисления.СтатусыУчастниковОбменаЭД.Отсоединен
			ИЛИ СтатусПодключения = Перечисления.СтатусыУчастниковОбменаЭД.Ошибка
			ИЛИ СтатусПодключения = Перечисления.СтатусыУчастниковОбменаЭД.ОжидаемСогласия Тогда
			
			Отправитель      = ПараметрыНастроекЭДО.Организация;
			Получатель       = ПараметрыНастроекЭДО.Контрагент;
			Если Не ЗначениеЗаполнено(ПараметрыНастроекЭДО.ЭлектроннаяПочтаДляПриглашения) Тогда
				ЭлектроннаяПочта = ЭлектронныеДокументыПереопределяемый.АдресЭлектроннойПочтыКонтрагента(
					ПараметрыНастроекЭДО.Контрагент);
			Иначе
				ЭлектроннаяПочта = ПараметрыНастроекЭДО.ЭлектроннаяПочтаДляПриглашения;
			КонецЕсли;
			
			Если СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезСервис1СЭДО Тогда
				ПолучитьИдентификаторыНаСервисе1СЭДО();
			КонецЕсли;
			
		ИначеЕсли СтатусПодключения = Перечисления.СтатусыУчастниковОбменаЭД.ТребуетсяСогласие
			ИЛИ СтатусПодключения = Перечисления.СтатусыУчастниковОбменаЭД.Присоединен Тогда
			Отправитель              = ПараметрыНастроекЭДО.Контрагент;
			Получатель               = ПараметрыНастроекЭДО.Организация;
			ИдентификаторКонтрагента = НайденныеСтроки[0].ИдентификаторКонтрагента;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПараметрыНастроекЭДО.ИдентификаторКонтрагента) Тогда
			ИдентификаторКонтрагента = ПараметрыНастроекЭДО.ИдентификаторКонтрагента;
			Элементы.Идентификатор.Доступность = Ложь;
		КонецЕсли;		
		
		Если Не ЗначениеЗаполнено(ПараметрыНастроекЭДО.ТекстПриглашения) И Не ЗначениеЗаполнено(НастройкаЭДО) Тогда
			ТекстПриглашения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НайденныеСтроки[0].ПрофильНастроекЭДО,
				"ШаблонТекстаПриглашений");
		Иначе
			ТекстПриглашения = ПараметрыНастроекЭДО.ТекстПриглашения;
		КонецЕсли;
		ПрофильНастроекЭДО   = НайденныеСтроки[0].ПрофильНастроекЭДО;
		
		Параметры.Свойство("ФормаОткрытаИзНастройкиЭДО", ФормаОткрытаИзНастройкиЭДО);
		Параметры.Свойство("Принять",                    Принять);
		Параметры.Свойство("Отклонить",                  Отклонить);
		
		ШаблонНастройкиЭДО = НСтр("ru = 'Настройка ЭДО с контрагентом %1'");
		ТекстНастройкиЭДО  = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонНастройкиЭДО,
			ПараметрыНастроекЭДО.Контрагент);
		Элементы.ДекорацияНастройкаЭДО.Заголовок = ТекстНастройкиЭДО;
		
	КонецЕсли;
	
	Элементы.ГруппаРеквизитыКонтрагента.Видимость = Не ЗначениеЗаполнено(Отправитель);
	
	Элементы.ПричинаОтказаОтПриглашения.Видимость = ЗначениеЗаполнено(ПричинаОтказаОтПриглашения);
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	Если Форма.ФормаОткрытаИзНастройкиЭДО Тогда
		Элементы.ДекорацияНастройкаЭДО.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.КнопкаОтозватьПриглашение.Видимость = Ложь;
	
	Если Форма.СтатусПодключения = ПредопределенноеЗначение("Перечисление.СтатусыУчастниковОбменаЭД.ТребуетсяПригласить")
		ИЛИ Форма.СтатусПодключения = ПредопределенноеЗначение("Перечисление.СтатусыУчастниковОбменаЭД.Ошибка")
		ИЛИ Форма.СтатусПодключения = ПредопределенноеЗначение("Перечисление.СтатусыУчастниковОбменаЭД.Отсоединен") Тогда
		Элементы.ГруппаКнопокТребуетсяСогласие.Видимость = Ложь;
		
	ИначеЕсли Форма.СтатусПодключения = ПредопределенноеЗначение("Перечисление.СтатусыУчастниковОбменаЭД.ОжидаемСогласия") Тогда
		
		Элементы.ГруппаКнопокТребуетсяСогласие.Видимость   = Ложь;
		Элементы.ГруппаКнопокТребуетсяПригласить.Видимость = Ложь;
		Элементы.ТекстПриглашения.ТолькоПросмотр = Истина;
		Элементы.ГруппаЭлектроннаяПочта.Доступность = Ложь;
		Элементы.КнопкаОтмена.КнопкаПоУмолчанию = Истина;
		Элементы.КнопкаОтозватьПриглашение.Видимость = Истина;
		
	ИначеЕсли Форма.СтатусПодключения = ПредопределенноеЗначение("Перечисление.СтатусыУчастниковОбменаЭД.ТребуетсяСогласие")
		ИЛИ Форма.СтатусПодключения = ПредопределенноеЗначение("Перечисление.СтатусыУчастниковОбменаЭД.Присоединен") Тогда
		
		Элементы.ГруппаЭлектроннаяПочта.Видимость = Ложь;
		Элементы.ТекстПриглашения.ТолькоПросмотр = Истина;
		Элементы.ГруппаКнопокТребуетсяПригласить.Видимость = Ложь;
		
		Элементы.КнопкаПринять.КнопкаПоУмолчанию = Истина;
		Если Форма.Принять Тогда
			Элементы.КнопкаОтклонить.Видимость       = Ложь;
		КонецЕсли;
		Если Форма.Отклонить Тогда
			Элементы.КнопкаПринять.Видимость           = Ложь;
			Элементы.КнопкаОтклонить.КнопкаПоУмолчанию = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ПОЛЕЙ ФОРМЫ

&НаКлиенте
Процедура ДекорацияНастройкаЭДОНажатие(Элемент)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", НастройкаЭДО);
	Если ЗначениеЗаполнено(Отправитель) Тогда
		ПараметрыФормы.Вставить("ТолькоПросмотр", Истина);
	КонецЕсли;
	ОткрытьФорму("Справочник.СоглашенияОбИспользованииЭД.Форма.ФормаЭлемента", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ИдентификаторПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ОтправитьПриглашение(Команда)
	
	ОчиститьСообщения();
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Маркер = Маркер();
	
	Если ЗначениеЗаполнено(Маркер) Тогда
		ОчиститьСообщения();
		ТекстЗаголовка = НСтр("ru = 'Отправка приглашений получателям'");
		
		ОтправленоПриглашений = 0;
		ОтправитьПриглашенияСервер(ОтправленоПриглашений, Маркер);
		
		ШаблонСообщения = НСтр("ru = 'Отправлено приглашений: %1'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, ОтправленоПриглашений);
		
		ПоказатьОповещениеПользователя(ТекстЗаголовка, , ТекстСообщения);
		
		Если ЗначениеЗаполнено(ОтправленоПриглашений) Тогда
			Оповестить("ОбновитьСостояниеЭД");
			Закрыть();
		КонецЕсли;
	Иначе
		ШаблонОшибки = НСтр("ru = 'При принятии приглашения возникли ошибки.
			|Необходимо выполнить тест настроек ЭДО с контрагентом %1.'");
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОшибки, Получатель);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПринятьПриглашение(Команда)    
	
	Если Отправитель = Получатель Тогда
		ОбновитьНастройкиЭДО(НастройкаЭДО);
		Оповестить("ОбновитьСостояниеЭД");
		Закрыть();  
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	ТекстЗаголовка = НСтр("ru = 'Принимаются приглашения'");
	КоличествоПринятыхПриглашений = 0;
	
	Маркер = Маркер();
	Если ЗначениеЗаполнено(Маркер) Тогда
		Результат = ПринятьОтклонитьКонтактЧерезОператораЭДОНаСервере(ИдентификаторКонтрагента, Истина, Маркер);
		Если Результат Тогда
			СтатусПодключения = ПредопределенноеЗначение("Перечисление.СтатусыУчастниковОбменаЭД.Присоединен");
			СостояниеСоглашения = ПредопределенноеЗначение("Перечисление.СостоянияСоглашенийЭД.ПроверкаТехническойСовместимости");
			ОбновитьНастройкиЭДО(НастройкаЭДО);
			
			КоличествоПринятыхПриглашений = 1;
		КонецЕсли;
		
		ШаблонСообщения = НСтр("ru = 'Принято приглашений: %1'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, КоличествоПринятыхПриглашений);
		
		ПоказатьОповещениеПользователя(ТекстЗаголовка, , ТекстСообщения);
		
		Если ЗначениеЗаполнено(КоличествоПринятыхПриглашений) Тогда
			Оповестить("ОбновитьСостояниеЭД");
			Закрыть();
		КонецЕсли;
	Иначе
		ШаблонОшибки = НСтр("ru = 'При принятии приглашения возникли ошибки.
			|Необходимо выполнить тест настроек ЭДО с контрагентом %1.'");
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОшибки, Получатель);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтклонитьПриглашение(Команда)
	
	ПричинаОтказа = "";
	Если ЭлектронныеДокументыСлужебныйКлиент.ТребуетсяУказаниеПричиныОказаОтПриглашения(СпособОбменаЭД) Тогда
		ПараметрыВводаСтроки = ЭлектронныеДокументыСлужебныйКлиент.ДополнительныеПараметрыВводаСтроки();
		ПараметрыВводаСтроки.Многострочность = Истина;
		ПараметрыВводаСтроки.Обязательность = Истина;
		ПараметрыВводаСтроки.ЗаголовокФормы = НСтр("ru = 'Укажите причину отказа от приглашения'");
		ПричинаОтказа = ЭлектронныеДокументыСлужебныйКлиент.ВвестиСтрокуБЭД(ПараметрыВводаСтроки);
		Если Не ЗначениеЗаполнено(ПричинаОтказа) Тогда	
			Возврат;
		КонецЕсли;			
	КонецЕсли;
	
	ОчиститьСообщения();
	
	ТекстЗаголовка = НСтр("ru = 'Отклоняются приглашения'");
	КоличествоОтклоненныхПриглашений = 0;
	
	Маркер = Маркер();
	Если ЗначениеЗаполнено(Маркер) Тогда
		Результат = ПринятьОтклонитьКонтактЧерезОператораЭДОНаСервере(ИдентификаторКонтрагента, Ложь, Маркер, ПричинаОтказа);
		Если Результат Тогда
			СтатусПодключения = ПредопределенноеЗначение("Перечисление.СтатусыУчастниковОбменаЭД.Отсоединен");
			СостояниеСоглашения = ПредопределенноеЗначение("Перечисление.СостоянияСоглашенийЭД.Закрыто");
			ОбновитьНастройкиЭДО(НастройкаЭДО);
			
			КоличествоОтклоненныхПриглашений = 1;
		КонецЕсли;
		
		ШаблонСообщения = НСтр("ru = 'Отклонено приглашений: %1'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, КоличествоОтклоненныхПриглашений);
		
		ПоказатьОповещениеПользователя(ТекстЗаголовка, , ТекстСообщения);
		
		Если ЗначениеЗаполнено(КоличествоОтклоненныхПриглашений) Тогда
			Оповестить("ОбновитьСостояниеЭД");
			Закрыть();
		КонецЕсли;
	Иначе
		ШаблонОшибки = НСтр("ru = 'При отклонении приглашения возникли ошибки.
			|Необходимо выполнить тест настроек ЭДО с контрагентом %1.'");
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОшибки, Получатель);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервере
Процедура ОбновитьНастройкиЭДО(НастройкаЭДО)
	
	ИскомаяНастройкаЭДО = НастройкаЭДО.ПолучитьОбъект();
	ИскомаяНастройкаЭДО.ЭлектроннаяПочтаДляПриглашения = ЭлектроннаяПочта;
	ИскомаяНастройкаЭДО.ТекстПриглашения               = ТекстПриглашения;
	ИскомаяНастройкаЭДО.СтатусПодключения              = СтатусПодключения;
	ИскомаяНастройкаЭДО.СостояниеСоглашения            = СостояниеСоглашения;
	ИскомаяНастройкаЭДО.ОписаниеОшибки                 = "";
	ЭлектронныеДокументыВнутренний.ОбновитьДатуИзмененияСтатуса(ИскомаяНастройкаЭДО);
	ИскомаяНастройкаЭДО.Записать();
	
КонецПроцедуры

&НаСервере
Функция ПринятьОтклонитьКонтактЧерезОператораЭДОНаСервере(Идентификатор, ПриглашениеПринято, Маркер, ПричинаОтказа = "")
	
	Результат = Ложь;
	
	ИскомаяНастройкаЭДО = НастройкаЭДО.ПолучитьОбъект();
	Если ПриглашениеПринято Тогда
		Если Не ЭлектронныеДокументыСлужебный.ПроверитьЗаполнениеНастройкиЭДО(ИскомаяНастройкаЭДО) Тогда
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	Если ИскомаяНастройкаЭДО.НастройкаЭДОУникальна() Тогда
		Результат = ЭлектронныеДокументыВнутренний.ПринятьОтклонитьКонтактЧерезОператораЭДО(Идентификатор, ПриглашениеПринято,
			Маркер, ПрофильНастроекЭДО, ПричинаОтказа);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ОтправитьПриглашенияСервер(ОтправленоПриглашений, Маркер)
	
	// Готовим таблицу с реквизитами контрагентов.
	ТаблицаПриглашений = Новый ТаблицаЗначений;
	ТаблицаПриглашений.Колонки.Добавить("ПрофильНастроекЭДО");
	ТаблицаПриглашений.Колонки.Добавить("НастройкаЭДО");
	ТаблицаПриглашений.Колонки.Добавить("Получатель");
	ТаблицаПриглашений.Колонки.Добавить("Идентификатор");
	ТаблицаПриглашений.Колонки.Добавить("Наименование");
	ТаблицаПриглашений.Колонки.Добавить("НаименованиеДляСообщенияПользователю");
	ТаблицаПриглашений.Колонки.Добавить("ИНН");
	ТаблицаПриглашений.Колонки.Добавить("КПП");
	ТаблицаПриглашений.Колонки.Добавить("АдресЭП");
	ТаблицаПриглашений.Колонки.Добавить("ТекстПриглашения");
	ТаблицаПриглашений.Колонки.Добавить("ВнешнийКод");
	
	ИмяРеквизитаИННКонтрагента = ЭлектронныеДокументыПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("ИННКонтрагента");
	ИмяРеквизитаКППКонтрагента = ЭлектронныеДокументыПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("КППКонтрагента");
	ИмяРеквизитаНаименованиеКонтрагента = ЭлектронныеДокументыПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("НаименованиеКонтрагента");
	ИмяРеквизитаВнешнийКодКонтрагента = ЭлектронныеДокументыПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("ВнешнийКодКонтрагента");
	ИмяРеквизитаНаименованиеКонтрагентаДляСообщенияПользователю = ЭлектронныеДокументыПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("НаименованиеКонтрагентаДляСообщенияПользователю");
	
	СтруктураПараметровКонтрагента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Получатель,
		ИмяРеквизитаИННКонтрагента + ", " + ИмяРеквизитаКППКонтрагента + ", " + ИмяРеквизитаНаименованиеКонтрагента + ", "
		+ ИмяРеквизитаВнешнийКодКонтрагента + ", " + ИмяРеквизитаНаименованиеКонтрагентаДляСообщенияПользователю);
		
	Если (Элементы.Идентификатор.СписокВыбора.Количество() = 0
		ИЛИ СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезОператораЭДОТакском)
		И Не ЗначениеЗаполнено(ЭлектроннаяПочта) Тогда
		
		ШаблонСообщения = НСтр("ru = 'Для отправки приглашения к обмену ЭД для получателя %1
									|необходимо заполнить электронную почту.'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, Получатель);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СтруктураПараметровКонтрагента[ИмяРеквизитаИННКонтрагента]) Тогда
		ШаблонСообщения = НСтр("ru = 'Для отправки приглашения к обмену ЭД для получателя %1
									|необходимо заполнить ИНН.'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, Получатель);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
		Возврат;
	КонецЕсли;
	
	НоваяСтрока = ТаблицаПриглашений.Добавить();
	НоваяСтрока.ПрофильНастроекЭДО = ПрофильНастроекЭДО;
	НоваяСтрока.НастройкаЭДО       = НастройкаЭДО;
	НоваяСтрока.Получатель         = Получатель;
	НоваяСтрока.Идентификатор      = ИдентификаторКонтрагента;
	НоваяСтрока.ТекстПриглашения   = ТекстПриглашения;
	НоваяСтрока.АдресЭП            = ЭлектроннаяПочта;
	НоваяСтрока.Наименование       = СтруктураПараметровКонтрагента[ИмяРеквизитаНаименованиеКонтрагента];
	НоваяСтрока.НаименованиеДляСообщенияПользователю = СтруктураПараметровКонтрагента[ИмяРеквизитаНаименованиеКонтрагентаДляСообщенияПользователю];
	НоваяСтрока.ИНН = СтруктураПараметровКонтрагента[ИмяРеквизитаИННКонтрагента];
	НоваяСтрока.КПП = СтруктураПараметровКонтрагента[ИмяРеквизитаКППКонтрагента];
	НоваяСтрока.ВнешнийКод = СтруктураПараметровКонтрагента[ИмяРеквизитаВнешнийКодКонтрагента];
		
	Если Не ЗначениеЗаполнено(ТаблицаПриглашений) Тогда
		Возврат;
	КонецЕсли;
	
	ДопПараметры = Новый Структура;
	ИмяФайла = ЭлектронныеДокументыВнутренний.ИсходящийЗапросПриглашенияОператораЭДО(ТаблицаПриглашений, ДопПараметры);
	Если Не ЗначениеЗаполнено(ИмяФайла) Тогда
		Возврат;
	КонецЕсли;
	
	ПутьДляПриглашений = ЭлектронныеДокументыСлужебный.РабочийКаталог("invite", Новый УникальныйИдентификатор);
	ИмяФайлаПриглашения = ПутьДляПриглашений + "SendContacts.xml";
	КопироватьФайл(ИмяФайла, ИмяФайлаПриглашения);
	УдалитьФайлы(ИмяФайла);
	РезультатОтправки = ЭлектронныеДокументыВнутренний.ОтправитьЧерезОператораЭДО(
																	Маркер,
																	ПутьДляПриглашений,
																	"SendContacts",
																	ПрофильНастроекЭДО);
	УдалитьФайлы(ПутьДляПриглашений);
	
	Если РезультатОтправки <> 0 Тогда
		Для каждого СтрокаТаблицы Из ТаблицаПриглашений Цикл
			СтатусПодключения = Перечисления.СтатусыУчастниковОбменаЭД.ОжидаемСогласия;
			СостояниеСоглашения = Перечисления.СостоянияСоглашенийЭД.ОжидаетсяСогласование;
			ОбновитьНастройкиЭДО(СтрокаТаблицы.НастройкаЭДО);
		КонецЦикла;
		
		// Определим сколько отправлено приглашений.
		ОтправленоПриглашений = ТаблицаПриглашений.Количество();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция Маркер()
	
	Маркер = Неопределено;
	Массив = Новый Массив;
	Массив.Добавить(ПрофильНастроекЭДО);
	Соответствие = ЭлектронныеДокументыСлужебныйКлиент.СоотвСоглашенийИСтруктурСертификатовДляАвторизации(Массив);
	СтСертификата = Соответствие.Получить(ПрофильНастроекЭДО);
	
	Если ТипЗнч(СтСертификата) = Тип("Структура") Тогда
		СтСертификата.Свойство("МаркерРасшифрованный", Маркер);
	КонецЕсли;
	
	Возврат Маркер;
	
КонецФункции

&НаСервере
Процедура ПолучитьИдентификаторыНаСервисе1СЭДО()
	
	ИмяРеквизитаИННПолучателя = ЭлектронныеДокументыПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("ИННКонтрагента");
	ИмяРеквизитаКПППолучателя = ЭлектронныеДокументыПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("КППКонтрагента");
	
	ПараметрыПолучателя = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Получатель,
		ИмяРеквизитаИННПолучателя + ", " + ИмяРеквизитаКПППолучателя);
		
	ПараметрРесурса = ПараметрыПолучателя[ИмяРеквизитаИННПолучателя];
	Если ЗначениеЗаполнено(ПараметрыПолучателя[ИмяРеквизитаКПППолучателя]) Тогда
		ПараметрРесурса = ПараметрРесурса + "_" + ПараметрыПолучателя[ИмяРеквизитаКПППолучателя];
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрРесурса) Тогда
		
		ИмяФайлаОтвета = ЭлектронныеДокументыСлужебный.ПолучитьФайлОтветаОтСервераЭДО("GetInfo", ПараметрРесурса);
		Если ИмяФайлаОтвета <> Неопределено Тогда
			ДанныеФайла = ЭлектронныеДокументыСлужебный.ПрочитатьТекстИзФайла(ИмяФайлаОтвета, , Истина);
			МассивИдентификаторов = ПолучитьИдентификаторАбонента(ДанныеФайла);
			Если ЗначениеЗаполнено(МассивИдентификаторов) Тогда
				Элементы.Идентификатор.СписокВыбора.ЗагрузитьЗначения(МассивИдентификаторов);
				Если МассивИдентификаторов.Количество() = 1 Тогда
					ИдентификаторКонтрагента = МассивИдентификаторов[0];
				КонецЕсли;
				Элементы.ЭлектроннаяПочта.АвтоОтметкаНезаполненного = Ложь;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьИдентификаторАбонента(ДанныеФайла)
	
	МассивИдентификаторов = Новый Массив;
	Если НЕ ПустаяСтрока(ДанныеФайла) Тогда
		
		ДеревоXML = ЭлектронныеДокументыСлужебный.ЗагрузитьСтрокуXMLВДеревоЗначений(ДанныеФайла);
		Если ДеревоXML <> Неопределено Тогда
			
			УзелАбоненты = ДеревоXML.Строки.Найти("Abonents", "Имя");
			
			Если УзелАбоненты <> Неопределено Тогда
				Для каждого УзелАбонент Из УзелАбоненты.Строки Цикл
					УзелИдентификаторАбонента = УзелАбонент.Строки.Найти("Identifier", "Имя");
					Если УзелИдентификаторАбонента <> Неопределено Тогда
						МассивИдентификаторов.Добавить(УзелИдентификаторАбонента.Значение);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат МассивИдентификаторов;
	
КонецФункции


