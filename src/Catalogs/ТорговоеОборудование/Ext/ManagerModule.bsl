	
Процедура ЗаполнитьСпособФЛК() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТорговоеОборудование.Ссылка
	|ИЗ
	|	Справочник.ТорговоеОборудование КАК ТорговоеОборудование
	|ГДЕ
	|	ТорговоеОборудование.ОбработкаОбслуживания.Вид = ЗНАЧЕНИЕ(Перечисление.ВидыТорговогоОборудования.ККТ)
	|	И ТорговоеОборудование.СпособФЛК = ЗНАЧЕНИЕ(Перечисление.СпособыФорматоЛогическогоКонтроля.ПустаяСсылка)";
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		ТО = Выборка.Ссылка.ПолучитьОбъект();
		ТО.СпособФЛК = ПредопределенноеЗначение("Перечисление.СпособыФорматоЛогическогоКонтроля.НеКонтролировать");
		ТО.ДопустимоеРасхождениеФЛК = 0.01;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ТО, Истина);
	КонецЦикла;
КонецПроцедуры

// Процедура предназначена для сохранения параметров регистрации устройства
//
// Возвращаемое значение:
//   Булево
//
Функция СохранитьПараметрыРегистрацииУстройства(Идентификатор, ПараметрыРегистрации) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Попытка
		
		Запрос = Новый Запрос( "ВЫБРАТЬ ПЕРВЫЕ 1
		                       |	ТорговоеОборудование.Модель КАК Ссылка
		                       |ИЗ
		                       |	РегистрСведений.ТорговоеОборудование КАК ТорговоеОборудование
		                       |ГДЕ
		                       |	ТорговоеОборудование.Идентификатор = &Идентификатор");
		
		
		Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
		ТаблицаРезультатов = Запрос.Выполнить().Выбрать();
		
		Пока ТаблицаРезультатов.Следующий() Цикл
			ОбъектСправочника = ТаблицаРезультатов.Ссылка.ПолучитьОбъект(); // СправочникОбъект.ТорговоеОборудование
			ОбъектСправочника.ПараметрыРегистрации.Очистить();
			Для Каждого ПараметрРегистрации Из ПараметрыРегистрации Цикл
				НоваяСтрока = ОбъектСправочника.ПараметрыРегистрации.Добавить();
				НоваяСтрока.НаименованиеПараметра = ПараметрРегистрации.Ключ;
			Если ТипЗнч(ПараметрРегистрации.Значение) = Тип("Булево") Тогда
				НоваяСтрока.ЗначениеПараметра = XMLСтрока(ПараметрРегистрации.Значение);
			Иначе
				НоваяСтрока.ЗначениеПараметра = ПараметрРегистрации.Значение;
			КонецЕсли;
			КонецЦикла;
			ОбъектСправочника.Записать();
			Результат = Истина;
		КонецЦикла;
		
	Исключение
		Результат = Ложь;
	КонецПопытки;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Результат;

КонецФункции

Функция ПолучитьТекущийИдентификаторУстройства(НаименованиеМодели) Экспорт

	Запрос = Новый Запрос( "ВЫБРАТЬ ПЕРВЫЕ 1
	                       |	ТорговоеОборудование.Идентификатор КАК Идентификатор
	                       |ИЗ
	                       |	РегистрСведений.ТорговоеОборудование КАК ТорговоеОборудование
	                       |ГДЕ
	                       |	ТорговоеОборудование.Модель.Наименование = &НаименованиеМодели
	                       |	И ТорговоеОборудование.Подключено");
	
	Запрос.УстановитьПараметр("НаименованиеМодели", НаименованиеМодели);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Идентификатор;
	КонецЕсли;
	
	Возврат НаименованиеМодели;
	
КонецФункции

// Функция возвращает по идентификатору устройства параметры регистрации.
//
Функция ПолучитьПараметрыРегистрацииУстройства(Идентификатор) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТорговоеОборудованиеПараметрыРегистрации.Ссылка,
	               |	ТорговоеОборудованиеПараметрыРегистрации.НаименованиеПараметра,
	               |	ТорговоеОборудованиеПараметрыРегистрации.ЗначениеПараметра
	               |ИЗ
	               |	РегистрСведений.ТорговоеОборудование КАК ТорговоеОборудование
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТорговоеОборудование.ПараметрыРегистрации КАК ТорговоеОборудованиеПараметрыРегистрации
	               |		ПО ТорговоеОборудование.Модель = ТорговоеОборудованиеПараметрыРегистрации.Ссылка
	               |ГДЕ
	               |	ТорговоеОборудование.Идентификатор = &Идентификатор";
	
	Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ЕстьДанные = Ложь;
	ДанныеХранения = Новый Структура();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если Не ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.НаименованиеПараметра) Тогда
			Продолжить;
		КонецЕсли;
		ДанныеХранения.Вставить(ВыборкаДетальныеЗаписи.НаименованиеПараметра, ВыборкаДетальныеЗаписи.ЗначениеПараметра);
		ЕстьДанные = Истина;
	КонецЦикла;
	
	РезультатОперации = МенеджерОборудованияКлиентСервер.ПараметрыРегистрацииККТ();
	РезультатОперации.Вставить("ЕстьДанные", ЕстьДанные);
	
	Если ЕстьДанные Тогда
		РезультатОперации.РегистрационныйНомерККТ = ДанныеХранения.РегистрационныйНомерККТ;
		РезультатОперации.ОрганизацияНазвание     = ДанныеХранения.ОрганизацияНазвание;
		РезультатОперации.ОрганизацияИНН          = ДанныеХранения.ОрганизацияИНН;
		
		Если ДанныеХранения.Свойство("АдресПроведенияРасчетов") Тогда
			РезультатОперации.АдресПроведенияРасчетов = ДанныеХранения.АдресПроведенияРасчетов; 
		КонецЕсли;
		Если ДанныеХранения.Свойство("МестоПроведенияРасчетов") Тогда
			РезультатОперации.МестоПроведенияРасчетов = ДанныеХранения.МестоПроведенияРасчетов; 
		КонецЕсли;
		
		РезультатОперации.КодыСистемыНалогообложения = ДанныеХранения.КодыСистемыНалогообложения;
		
		Если ДанныеХранения.Свойство("ПризнакАвтономногоРежима") И НЕ ПустаяСтрока(ДанныеХранения.ПризнакАвтономногоРежима) Тогда
			РезультатОперации.ПризнакАвтономногоРежима = Булево(ДанныеХранения.ПризнакАвтономногоРежима); 
		КонецЕсли;
		Если ДанныеХранения.Свойство("ПризнакШифрованиеДанных") И НЕ ПустаяСтрока(ДанныеХранения.ПризнакШифрованиеДанных) Тогда
			РезультатОперации.ПризнакШифрованиеДанных = Булево(ДанныеХранения.ПризнакШифрованиеДанных); 
		КонецЕсли;
		Если ДанныеХранения.Свойство("ПризнакРасчетовЗаУслуги") И НЕ ПустаяСтрока(ДанныеХранения.ПризнакРасчетовЗаУслуги) Тогда
			РезультатОперации.ПризнакАвтономногоРежима = Булево(ДанныеХранения.ПризнакАвтономногоРежима); 
		КонецЕсли;
		Если ДанныеХранения.Свойство("ПризнакФормированияБСО") И НЕ ПустаяСтрока(ДанныеХранения.ПризнакФормированияБСО) Тогда
			РезультатОперации.ПризнакФормированияБСО = Булево(ДанныеХранения.ПризнакФормированияБСО); 
		КонецЕсли;
		Если ДанныеХранения.Свойство("ПризнакРасчетовТолькоВИнтернет") И НЕ ПустаяСтрока(ДанныеХранения.ПризнакРасчетовТолькоВИнтернет) Тогда
			РезультатОперации.ПризнакРасчетовТолькоВИнтернет = Булево(ДанныеХранения.ПризнакРасчетовТолькоВИнтернет); 
		КонецЕсли;
		Если ДанныеХранения.Свойство("ПродажаПодакцизногоТовара") И НЕ ПустаяСтрока(ДанныеХранения.ПродажаПодакцизногоТовара) Тогда
			РезультатОперации.ПродажаПодакцизногоТовара = Булево(ДанныеХранения.ПродажаПодакцизногоТовара); 
		КонецЕсли;
		Если ДанныеХранения.Свойство("ПроведенияАзартныхИгр") И НЕ ПустаяСтрока(ДанныеХранения.ПроведенияАзартныхИгр) Тогда
			РезультатОперации.ПроведенияАзартныхИгр = Булево(ДанныеХранения.ПроведенияАзартныхИгр); 
		КонецЕсли;
		Если ДанныеХранения.Свойство("ПроведенияЛотерей") И НЕ ПустаяСтрока(ДанныеХранения.ПроведенияЛотерей) Тогда
			РезультатОперации.ПроведенияЛотерей = Булево(ДанныеХранения.ПроведенияЛотерей); 
		КонецЕсли;
		Если ДанныеХранения.Свойство("УстановкаПринтераВАвтомате") И НЕ ПустаяСтрока(ДанныеХранения.УстановкаПринтераВАвтомате) Тогда
			РезультатОперации.УстановкаПринтераВАвтомате = Булево(ДанныеХранения.УстановкаПринтераВАвтомате); 
		КонецЕсли;
		
		Если ДанныеХранения.Свойство("ПризнакиАгента") Тогда
			РезультатОперации.ПризнакиАгента = ДанныеХранения.ПризнакиАгента; 
		КонецЕсли;
		РезультатОперации.НомерАвтоматаДляАвтоматическогоРежима = ДанныеХранения.НомерАвтоматаДляАвтоматическогоРежима;
		РезультатОперации.ОрганизацияОФДИНН      = ДанныеХранения.ОрганизацияОФДИНН;
		РезультатОперации.ОрганизацияОФДНазвание = ДанныеХранения.ОрганизацияОФДНазвание;
		РезультатОперации.ЗаводскойНомерККТ      = ДанныеХранения.ЗаводскойНомерККТ;
		РезультатОперации.ЗаводскойНомерФН       = ДанныеХранения.ЗаводскойНомерФН;
		РезультатОперации.ВерсияФФДККТ           = ДанныеХранения.ВерсияФФДККТ;
		РезультатОперации.ВерсияФФДФН            = ДанныеХранения.ВерсияФФДФН;
		Если ДанныеХранения.Свойство("ПризнакФискализации") И НЕ ПустаяСтрока(ДанныеХранения.ПризнакФискализации) Тогда
			РезультатОперации.ПризнакФискализации = Булево(ДанныеХранения.ПризнакФискализации); 
		КонецЕсли;
	КонецЕсли;
	
	Возврат РезультатОперации;
	
КонецФункции

// Функция возвращает список идентификаторов подключенных устройств заданного вида.
//
// Параметры:
//  Вид      - <ПеречислениеСсылка.ВидыТорговогоОборудования>
//           - Вид торгового оборудования, информацию о котором необходимо получить.
//
//  Отбор - <СправочникСсылка.КассыККМ> или <СправочникСсылка.Организации>
//          Касса ККМ, к которой подключено данное оборудование или 
//			организация указанная как владелец ККМ к которым подключено 
//			данное оборудование
//          В случае, если данный параметр соответствует пустой ссылке,
//          будет возвращено всё торговое оборудование указанного вида.
//
// Возвращаемое значение:
//  <Массив> - Список идентификаторов устройств.
//
Функция ПолучитьСписокУстройств(Вид, Отбор = Неопределено) Экспорт
	
	ДополнительноеУсловие = "";
	Если ЗначениеЗаполнено(Отбор) Тогда
		Если ТипЗнч(Отбор) = Тип("СправочникСсылка.КассыККМ") Тогда
			ДополнительноеУсловие  = "    И РегТО.КассаККМ = &Отбор";
		ИначеЕсли ТипЗнч(Отбор) = Тип("СправочникСсылка.Организации") Тогда
			ДополнительноеУсловие  = "    И РегТО.КассаККМ.Владелец = &Отбор";
		ИначеЕсли ТипЗнч(Отбор) = Тип("Строка") Тогда
			ДополнительноеУсловие  = "    И РегТО.Идентификатор = &Отбор";
		КонецЕсли;
	КонецЕсли;

	Результат = Новый Массив();

	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|    РегТО.Модель КАК Модель
	|ИЗ
	|    РегистрСведений.ТорговоеОборудование КАК РегТО
	|ГДЕ
	|    РегТО.Вид = &Вид
	|    И РегТО.Подключено
	|" + ДополнительноеУсловие + "
	|
	|УПОРЯДОЧИТЬ ПО  РегТО.Идентификатор
	|");
	
	Запрос.УстановитьПараметр("Вид"       , Вид);
	Если ЗначениеЗаполнено(Отбор) Тогда
		Запрос.УстановитьПараметр("Отбор", Отбор);
	КонецЕсли;

	Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку(0);

	Возврат Результат;

КонецФункции // ПолучитьСписокУстройств()

