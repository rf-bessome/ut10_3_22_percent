
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененыСтатусыМЧД" Тогда
		Элементы.ДоверенностиОрганизаций.Обновить();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЕстьПравоИзменения = Справочники.МашиночитаемыеДоверенностиОрганизаций.ЕстьПравоИзменения();
	Элементы.ДоверенностиОрганизаций_Загрузить.Видимость = ЕстьПравоИзменения;
	Элементы.ДоверенностиОрганизаций_Отменить.Видимость = ЕстьПравоИзменения;
	Элементы.ДоверенностиОрганизацийЗагрузитьПерезаполнитьДоверенностьОрганизацииИзФайла.Видимость = ЕстьПравоИзменения;
	
	ЕстьПравоИзменения = Справочники.МашиночитаемыеДоверенностиКонтрагентов.ЕстьПравоИзменения();
	Элементы.ДоверенностиКонтрагентов_Загрузить.Видимость = ЕстьПравоИзменения;
	Элементы.ДоверенностиКонтрагентовЗагрузитьПерезаполнитьДоверенностьКонтрагентаИзФайла.Видимость = ЕстьПравоИзменения;
	
	УстановитьОтборПоПомеченнымНаУдаление(Ложь);
	ОбновитьДанные();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДоверенностиКонтрагентов

&НаКлиенте
Процедура ДоверенностиКонтрагентовПриИзменении(Элемент)
	
	ОбновитьДанныеПоМЧДКонтрагентов();

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДоверенностиКонтрагентовПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	МашиночитаемыеДоверенности.ПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДоверенностиОрганизаций

&НаКлиенте
Процедура ДоверенностиОрганизацийПриАктивацииСтроки(Элемент)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		Элементы.ДоверенностиОрганизаций_Отменить.Видимость = ПолучитьВидимостьКомандыОтменыМЧД(ТекущиеДанные);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДоверенностиОрганизацийПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	МашиночитаемыеДоверенности.ОпределитьВидимостьКомандМЧДПриПолученииДанныхСпискаНаСервере(Строки);
	МашиночитаемыеДоверенности.ПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки);
КонецПроцедуры

&НаКлиенте
Процедура ДоверенностиОрганизацийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.ДоверенностиОрганизацийПредставлениеСостояния Тогда
		СтандартнаяОбработка = Ложь;
		ЭлектронныеДокументыСлужебныйКлиент.ОткрытьДеревоЭД(ВыбраннаяСтрока);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДоверенностиОрганизацийПриИзменении(Элемент)
	
	ОбновитьДанныеПоМЧДОрганизаций();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьПерезаполнитьМЧДОрганизацииИзФайла(Команда)

	ОбработчикЗавершения = Новый ОписаниеОповещения("ЗагрузитьПерезаполнитьМЧДОрганизацииИзФайлаЗавершение", ЭтаФорма);
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.Заголовок          = НСтр("ru = 'Выберите архив с доверенностью и подписью'"); 
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
	ДиалогВыбораФайла.Фильтр             = НСтр("ru = 'Архив|*.zip'");
	ДиалогВыбораФайла.Расширение         = "zip";	 
	
	МассивПомещенных = Новый Массив;
	Если ПоместитьФайлы(, МассивПомещенных, ДиалогВыбораФайла,,УникальныйИдентификатор) Тогда
		Для Каждого ПомещенныйФайл из МассивПомещенных Цикл
			ВыполнитьОбработкуОповещения(ОбработчикЗавершения, ПомещенныйФайл); 
		КонецЦикла;
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьМЧДОрганизацииВФайл(Команда)
	
	ТекущаяСтрока = Элементы.ДоверенностиОрганизаций.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыВыгрузки = ВыгрузитьДанныеДоверенностиОрганизации(ТекущаяСтрока);
	Если ПараметрыВыгрузки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеСохраненияДоверенностиОрганизацииВФайл", ЭтаФорма, ТекущаяСтрока);
	
	СтруктураИмениФайла = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ПараметрыВыгрузки.ИмяФайла);
	
	ДанныеФайла = Новый Структура("Расширение, ПолноеНаименование, АдресХранилища");
	ДанныеФайла.Расширение = СтрЗаменить(СтруктураИмениФайла.Расширение, ".", "");
	ДанныеФайла.ПолноеНаименование = СтруктураИмениФайла.ИмяБезРасширения;
	ДанныеФайла.АдресХранилища = ПараметрыВыгрузки.Адрес;
	
	ВыбранныйПуть = ЭлектронныеДокументыСлужебныйКлиент.СохранитьКак(ДанныеФайла);
	
	Если ЗначениеЗаполнено(ВыбранныйПуть) Тогда
		ПолученныеФайлы = Новый Структура("ПолноеИмя");
		ПолученныеФайлы.ПолноеИмя = ВыбранныйПуть;
		ВыполнитьОбработкуОповещения(Оповещение, МашиночитаемыеДоверенностиКлиентСервер.ЗначениеВМассиве(ПолученныеФайлы));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьМЧДОрганизации(Команда)

	ТекущаяСтрока = Элементы.ДоверенностиОрганизаций.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;

	МашиночитаемыеДоверенностиКлиент.ОтменитьМЧД( , ТекущаяСтрока);

КонецПроцедуры

&НаКлиенте
Процедура ПометитьОтозваннойМЧДОрганизации(Команда)
	
	ТекущаяСтрока = Элементы.ДоверенностиОрганизаций.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("Ключ", ТекущаяСтрока);
	ОткрытьФорму("Справочник.МашиночитаемыеДоверенностиОрганизаций.Форма.ФормаПросмотра", ПараметрыФормы);
	
	ТекстСообщения = НСтр("ru = 'Для изменения пометки отозвана выполните: Еще - Пометить отозванной/Вернуть в работу'");
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьОтозваннойМЧДКонтрагента(Команда)
	
	ТекущаяСтрока = Элементы.ДоверенностиКонтрагентов.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("Ключ", ТекущаяСтрока);	
	ОткрытьФорму("Справочник.МашиночитаемыеДоверенностиКонтрагентов.Форма.ФормаЭлемента", ПараметрыФормы);
	
	ТекстСообщения = НСтр("ru = 'Для изменения пометки отозвана выполните: Еще - Пометить отозванной/Вернуть в работу'");
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);

КонецПроцедуры

&НаКлиенте
Процедура ОтправитьМЧДОрганизацииПоЭДО(Команда)

	ТекущиеДанные = Элементы.ДоверенностиОрганизаций.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		МашиночитаемыеДоверенностиКлиент.ОтправитьПоЭДО(ТекущиеДанные.Ссылка);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЭлектронныеДокументыПоМЧДОрганизации(Команда)

	ТекущиеДанные = Элементы.ДоверенностиОрганизаций.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ЭлектронныеДокументыСлужебныйКлиент.ОткрытьДеревоЭД(ТекущиеДанные.Ссылка);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьМЧДОрганизации(Команда)

	ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузитьМЧДОрганизацииЗавершение", ЭтаФорма);
	МашиночитаемыеДоверенностиКлиент.ПолучитьДанныеМЧД(ОписаниеОповещения, ЭтаФорма, Истина, Истина);

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьПерезаполнитьМЧДКонтрагентаИзФайла(Команда)

	ОбработчикЗавершения = Новый ОписаниеОповещения("ЗагрузитьПерезаполнитьМЧДКонтрагентаИзФайлаЗавершение", ЭтаФОрма);

	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.Заголовок          = НСтр("ru = 'Выберите архив с доверенностью и подписью'"); 
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
	ДиалогВыбораФайла.Фильтр             = НСтр("ru = 'Архив|*.zip'");
	ДиалогВыбораФайла.Расширение         = "zip";	 
	
	МассивПомещенных = Новый Массив;
	Если ПоместитьФайлы(, МассивПомещенных, ДиалогВыбораФайла,,УникальныйИдентификатор) Тогда
		Для Каждого ПомещенныйФайл из МассивПомещенных Цикл
			ВыполнитьОбработкуОповещения(ОбработчикЗавершения, ПомещенныйФайл); 
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьМЧДКонтрагента(Команда)

	ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузитьМЧДКонтрагентаЗавершение", ЭтаФорма);
	МашиночитаемыеДоверенностиКлиент.ПолучитьДанныеМЧД(ОписаниеОповещения, ЭтаФорма, Истина);

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьМЧДОрганизаций(Команда)

	ОбновитьДанныеПоМЧДОрганизаций();
	Элементы.ДоверенностиОрганизаций.Обновить();

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьМЧДКонтрагентов(Команда)

	ОбновитьДанныеПоМЧДКонтрагентов();
	Элементы.ДоверенностиКонтрагентов.Обновить();

КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьПомеченныеНаУдалениеМЧДОрганизаций(Команда)
	
	ПоказыватьПомеченныеНаУдаление = Элементы.ДоверенностиОрганизацийПоказыватьПомеченныеНаУдалениеМЧДОрганизаций;
	ПоказыватьПомеченныеНаУдаление.Пометка = Не ПоказыватьПомеченныеНаУдаление.Пометка;
	УстановитьОтборПоПомеченнымНаУдалениеМЧДОрганизаций(ПоказыватьПомеченныеНаУдаление.Пометка);

КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьПомеченныеНаУдалениеМЧДКонтрагентов(Команда)
	
	ПоказыватьПомеченныеНаУдаление = Элементы.ДоверенностиКонтрагентовПоказыватьПомеченныеНаУдалениеМЧДКонтрагентов;
	ПоказыватьПомеченныеНаУдаление.Пометка = Не ПоказыватьПомеченныеНаУдаление.Пометка;
	УстановитьОтборПоПомеченнымНаУдалениеМЧДКонтрагентов(ПоказыватьПомеченныеНаУдаление.Пометка);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьДанные()

	ОбновитьДанныеПоМЧДОрганизаций();
	ОбновитьДанныеПоМЧДКонтрагентов();

КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеПоМЧДОрганизаций()
	
	ПараметрыДанных = ДоверенностиОрганизаций.КомпоновщикНастроек.Настройки.ПараметрыДанных;
	ПараметрыДанных.УстановитьЗначениеПараметра("ТекущаяДата", ТекущаяДатаСеанса());
	
	ОбновитьЗаголовокСтраницыМЧДОрганизаций();

КонецПроцедуры

&НаСервере
Процедура ОбновитьЗаголовокСтраницыМЧДОрганизаций()
	
	ПоказыватьПомеченныеНаУдаление = Элементы.ДоверенностиОрганизацийПоказыватьПомеченныеНаУдалениеМЧДОрганизаций;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПоказыватьПомеченныеНаУдаление", ПоказыватьПомеченныеНаУдаление.Пометка);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(ДоверенностиОрганизаций.Ссылка) КАК КоличествоЭлементов
	|ИЗ
	|	Справочник.МашиночитаемыеДоверенностиОрганизаций КАК ДоверенностиОрганизаций
	|ГДЕ
	|	ВЫБОР
	|		КОГДА &ПоказыватьПомеченныеНаУдаление
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ДоверенностиОрганизаций.ПометкаУдаления = ЛОЖЬ
	|	КОНЕЦ";
	Результат = Запрос.Выполнить().Выбрать();

	КоличествоЭлементов = 0;
	Если Результат.Следующий() Тогда
		КоличествоЭлементов = Результат.КоличествоЭлементов;
	КонецЕсли;	

	ЗаголовокСтраницы = НСтр("ru = 'Доверенности организаций'") + ?(КоличествоЭлементов > 0, " (" + КоличествоЭлементов + ")", "");
	Элементы.Доверенности.ПодчиненныеЭлементы.СтраницаДоверенностиОрганизаций.Заголовок = ЗаголовокСтраницы;
	
КонецПроцедуры	

&НаСервере
Процедура ОбновитьДанныеПоМЧДКонтрагентов()
	
	ПараметрыДанных = ДоверенностиКонтрагентов.КомпоновщикНастроек.Настройки.ПараметрыДанных;
	ПараметрыДанных.УстановитьЗначениеПараметра("ТекущаяДата", ТекущаяДатаСеанса());
	
	ОбновитьЗаголовокСтраницыМЧДКонтрагентов();

КонецПроцедуры

&НаСервере
Процедура ОбновитьЗаголовокСтраницыМЧДКонтрагентов()
	
	ПоказыватьПомеченныеНаУдаление = Элементы.ДоверенностиКонтрагентовПоказыватьПомеченныеНаУдалениеМЧДКонтрагентов;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПоказыватьПомеченныеНаУдаление", ПоказыватьПомеченныеНаУдаление.Пометка);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(ДоверенностиКонтрагентов.Ссылка) КАК КоличествоЭлементов
	|ИЗ
	|	Справочник.МашиночитаемыеДоверенностиКонтрагентов КАК ДоверенностиКонтрагентов
	|ГДЕ
	|	ВЫБОР
	|		КОГДА &ПоказыватьПомеченныеНаУдаление
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ДоверенностиКонтрагентов.ПометкаУдаления = ЛОЖЬ
	|	КОНЕЦ";
	Результат = Запрос.Выполнить().Выбрать();

	КоличествоЭлементов = 0;
	Если Результат.Следующий() Тогда
		КоличествоЭлементов = Результат.КоличествоЭлементов;
	КонецЕсли;	

	ЗаголовокСтраницы = НСтр("ru = 'Доверенности контрагентов'") + ?(КоличествоЭлементов > 0, " (" + КоличествоЭлементов + ")", "");
	Элементы.Доверенности.ПодчиненныеЭлементы.СтраницаДоверенностиКонтрагентов.Заголовок = ЗаголовокСтраницы;
	
КонецПроцедуры	

&НаСервере
Процедура УстановитьОтборПоПомеченнымНаУдаление(ПоказыватьПомеченныеНаУдаление)
	
	УстановитьОтборПоПомеченнымНаУдалениеМЧДОрганизаций(ПоказыватьПомеченныеНаУдаление);
	УстановитьОтборПоПомеченнымНаУдалениеМЧДКонтрагентов(ПоказыватьПомеченныеНаУдаление);
	
КонецПроцедуры	

&НаСервере
Процедура УстановитьОтборПоПомеченнымНаУдалениеМЧДОрганизаций(ПоказыватьПомеченныеНаУдаление)
	
	ПараметрПоказыватьПомеченныеНаУдаление = Новый ПараметрКомпоновкиДанных("ПоказыватьПомеченныеНаУдаление");
	ПараметрыДанных = ДоверенностиОрганизаций.КомпоновщикНастроек.Настройки.ПараметрыДанных;
	ЗначениеПараметра = ПараметрыДанных.НайтиЗначениеПараметра(ПараметрПоказыватьПомеченныеНаУдаление);
	ЗначениеПараметра.Значение = ПоказыватьПомеченныеНаУдаление;
	ЗначениеПараметра.Использование = Не ПоказыватьПомеченныеНаУдаление;		
	
	ОбновитьЗаголовокСтраницыМЧДОрганизаций();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоПомеченнымНаУдалениеМЧДКонтрагентов(ПоказыватьПомеченныеНаУдаление)
	
	ПараметрПоказыватьПомеченныеНаУдаление = Новый ПараметрКомпоновкиДанных("ПоказыватьПомеченныеНаУдаление");
	ПараметрыДанных = ДоверенностиКонтрагентов.КомпоновщикНастроек.Настройки.ПараметрыДанных;
	ЗначениеПараметра = ПараметрыДанных.НайтиЗначениеПараметра(ПараметрПоказыватьПомеченныеНаУдаление);
	ЗначениеПараметра.Значение = ПоказыватьПомеченныеНаУдаление;
	ЗначениеПараметра.Использование = Не ПоказыватьПомеченныеНаУдаление;
	
	ОбновитьЗаголовокСтраницыМЧДКонтрагентов();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьПерезаполнитьМЧДОрганизацииИзФайлаЗавершение(ПомещенныйФайл, ДополнительныеПараметры) Экспорт

	Если ПомещенныйФайл = Неопределено Тогда
		Возврат;
	КонецЕсли;

	АдресВоВременномХранилище = ПомещенныйФайл.Хранение;
	МашиночитаемыеДоверенностиКлиент.ЗагрузитьПерезаполнитьМЧДОрганизацииИзФайла(АдресВоВременномХранилище);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьМЧДОрганизацииЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если ЗначениеЗаполнено(Результат.СсылкаНаДоверенность) Тогда
		ПараметрыФормы = Новый Структура("Ключ, ОбновитьСостояниеПриОткрытии", Результат.СсылкаНаДоверенность, Истина);
		УИДФормы = Новый УникальныйИдентификатор;
		ОткрытьФорму("Справочник.МашиночитаемыеДоверенностиОрганизаций.ФормаОбъекта", ПараметрыФормы, , УИДФормы);
	ИначеЕсли Результат.Свойство("ТекстОшибки") Тогда
		МашиночитаемыеДоверенностиКлиент.ПоказатьПредупреждениеПриЗагрузкеМЧД(Результат.ТекстОшибки);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьПерезаполнитьМЧДКонтрагентаИзФайлаЗавершение(ПомещенныйФайл, ДополнительныеПараметры) Экспорт

	Если ПомещенныйФайл = Неопределено Тогда
		Возврат;
	КонецЕсли;

	АдресВоВременномХранилище = ПомещенныйФайл.Хранение;
	МашиночитаемыеДоверенностиКлиент.ЗагрузитьПерезаполнитьМЧДКонтрагентаИзФайла(АдресВоВременномХранилище);

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьМЧДКонтрагентаЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Не ЗначениеЗаполнено(Результат.СсылкаНаДоверенность) Тогда
		Если Результат.Свойство("ТекстОшибки") Тогда
			МашиночитаемыеДоверенностиКлиент.ПоказатьПредупреждениеПриЗагрузкеМЧД(Результат.ТекстОшибки);
		КонецЕсли;
		Возврат;
	КонецЕсли;

	Элементы.ДоверенностиКонтрагентов.Обновить();
	ПараметрыФормы = Новый Структура("Ключ, ОбновитьСостояниеПриОткрытии", Результат.СсылкаНаДоверенность, Истина);
	УИДФормы = Новый УникальныйИдентификатор;
	ОткрытьФорму("Справочник.МашиночитаемыеДоверенностиКонтрагентов.ФормаОбъекта", ПараметрыФормы, , УИДФормы);

	Если Результат.ОткрытьФормуДляОбновления Тогда
		ТекстСообщения = НСтр("ru = 'Для обновления выполните: Еще - Обновить из реестра ФНС.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	Иначе
		ТекстСообщения = НСтр("ru = 'Доверенность успешно загружена.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ВыгрузитьДанныеДоверенностиОрганизации(Знач Ссылка)
	
	РезультатВыгрузки = МашиночитаемыеДоверенности.ВыгрузитьДанныеДоверенности(Ссылка);
	
	Если РезультатВыгрузки.Ошибка Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(РезультатВыгрузки.ТекстОшибки);
		Возврат Неопределено;
	КонецЕсли;
	
	АдресФайла = ПоместитьВоВременноеХранилище(РезультатВыгрузки.ОписаниеФайла.ДвоичныеДанные);
	
	ПараметрыАрхива = Новый Структура;
	ПараметрыАрхива.Вставить("Адрес", АдресФайла);
	ПараметрыАрхива.Вставить("ИмяФайла", РезультатВыгрузки.ОписаниеФайла.ИмяФайла);
	
	Возврат ПараметрыАрхива;
	
КонецФункции

&НаКлиенте
Процедура ПослеСохраненияДоверенностиОрганизацииВФайл(ПолученныеФайлы, Ссылка) Экспорт
	
	Если ЗначениеЗаполнено(ПолученныеФайлы) Тогда
		ШаблонСообщения = НСтр("ru = 'Подписанная доверенность сохранена в файл %1 и может быть передана контрагентам любым удобным способом.'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, ПолученныеФайлы[0].ПолноеИмя);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		ЗаписатьФактВыгрузкиДоверенностиОрганизацииВФайл(Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьФактВыгрузкиДоверенностиОрганизацииВФайл(Знач Ссылка)
	
	Комментарий = НСтр("ru = 'Выгрузка машиночитаемой доверенности в файл.'");
	ЭлектронныеДокументыСлужебныйВызовСервера.ЗаписатьВЖурналРегистрации(Комментарий,
		1, "Информация", Ссылка);	
	
КонецПроцедуры

#КонецОбласти
          

////////////////////////////////////////////////////////////////////////////////
// Адаптация
 
&НаСервере
Функция ПолучитьВидимостьКомандыОтменыМЧД(МЧД)
		
	Возврат МашиночитаемыеДоверенности.ДоверенностьДействительнаПоСвойствам(МЧД.Ссылка, ТекущаяДата());
	
КонецФункции