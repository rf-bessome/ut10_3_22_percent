
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЭлектронныеДокументыПереопределяемый.ЕстьПравоНастройкиПараметровЭД() Тогда
		ТекстСообщения = ЭлектронныеДокументыСлужебныйВызовСервера.ТекстСообщенияОНеобходимостиНастройкиСистемы("Доступ");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , , Отказ);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Если ЗначениеЗаполнено(Константы.ПровайдерЭЦП.Получить()) И ЗначениеЗаполнено(Константы.АлгоритмПодписи.Получить())
		И ЗначениеЗаполнено(Константы.АлгоритмХеширования.Получить()) И ЗначениеЗаполнено(Константы.АлгоритмШифрования.Получить()) Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаСертификатыЭЦП;
	КонецЕсли;
		
	Если НЕ ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьЗначениеФункциональнойОпции("ИспользоватьЭлектронныеЦифровыеПодписи") Тогда
		ТекстСообщения = ЭлектронныеДокументыСлужебныйВызовСервера.ТекстСообщенияОНеобходимостиНастройкиСистемы("НастройкаКриптографии");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , , Отказ);
	КонецЕсли;
	
	ИнициализироватьНачальныеЗначенияРеквизитов();
	
	НеобходимаБлокировкаКонстант = НеобходимаБлокировкаВРамкахРИБ();
	
	Если НеобходимаБлокировкаКонстант Тогда
		Элементы.ПровайдерЭЦП.ТолькоПросмотр = Истина;
		Элементы.ТипПровайдераЭЦП.ТолькоПросмотр = Истина;
		Элементы.АлгоритмПодписи.ТолькоПросмотр = Истина;
		Элементы.АлгоритмХеширования.ТолькоПросмотр = Истина;
		Элементы.АлгоритмШифрования.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	// Скроем не разделенные константы в разделенном режиме сервиса
	Если ОбщегоНазначенияПовтИсп.РазделениеВключено() Тогда
		Элементы.ГруппаКонтекст.Видимость = Ложь;
	КонецЕсли;
			
	Если ОбщегоНазначенияКлиентСервер.ЭтоПлатформа83БезРежимаСовместимости() Тогда
		Элементы.ПровайдерЭЦП.КнопкаВыпадающегоСписка = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ВыполнятьКриптооперацииНаСервере = ЭлектронныеДокументыСлужебныйВызовСервера.ВыполнятьКриптооперацииНаСервере();
	
	Если НЕ ВыполнятьКриптооперацииНаСервере Тогда
		ПодключитьМодульКриптографии();
	КонецЕсли;
		
	ПерсональныеНастройкиРаботыСЭЦП = ЭлектроннаяЦифроваяПодписьКлиент.ПолучитьПерсональныеНастройкиРаботыСЭЦП();
	
	ЗаполнитьЗначенияСвойств(
			ЭтаФорма,
			ПерсональныеНастройкиРаботыСЭЦП,
			"ПровайдерЭЦП, ТипПровайдераЭЦП, АлгоритмПодписи, АлгоритмШифрования, АлгоритмХеширования");
		
	Элементы.ПровайдерЭЦП.СписокВыбора.Очистить();
	
	ДобавитьМенеджераКриптографииВСписок("Crypto-Pro GOST R 34.10-2012 Cryptographic Service Provider", "", 80);
	ДобавитьМенеджераКриптографииВСписок("Infotecs GOST 2012/512 Cryptographic Service Provider", "", 77);
	ДобавитьМенеджераКриптографииВСписок("Crypto-Pro GOST R 34.10-2001 Cryptographic Service Provider", "", 75);
	ДобавитьМенеджераКриптографииВСписок("Signal-COM CPGOST Cryptographic Provider", "", 75);
	ДобавитьМенеджераКриптографииВСписок("Signal-COM ECGOST Cryptographic Provider", "", 129);
	ДобавитьМенеджераКриптографииВСписок("Infotecs Cryptographic Service Provider", "", 2);
	ДобавитьМенеджераКриптографииВСписок("Microsoft Enhanced Cryptographic Provider v1.0", "", 1);
	ДобавитьМенеджераКриптографииВСписок("Microsoft Strong Cryptographic Provider", "", 1);
	ДобавитьМенеджераКриптографииВСписок("", "", 75);
	
	Если ВыполнятьКриптооперацииНаСервере Тогда
		ЗаполнитьСпискиВыбораНаСервере();
	Иначе
		ЗаполнитьСпискиВыбораНаКлиенте();
	КонецЕсли;
	
	ЗаполнитьПараметрыНастройкиКриптографииПоУмолчанию();
	
	ПроверитьИсправитьТекущиеЗначенияАлгоритмов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьСписокСертификатов" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура НастройкиКриптографии(Команда)
	
	ПараметрыФормы = Новый Структура("ПоказыватьНастройкиШифрования", Истина);
	ПараметрыФормы.Вставить("РежимОткрытияОкна", РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	ОткрытьФорму("ОбщаяФорма.НастройкиЭЦП", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ТипПровайдераЭЦППриИзменении(Элемент)
	
	ЗаписатьКонстанту(Элемент.Имя, ТипПровайдераЭЦП);
	
	Если ЭлектронныеДокументыСлужебныйВызовСервера.ВыполнятьКриптооперацииНаСервере() Тогда
		ЗаполнитьСпискиВыбораНаСервере();
	Иначе
		ЗаполнитьСпискиВыбораНаКлиенте();
	КонецЕсли;

	ЗаполнитьАлгоритмыПоУмолчанию();
		
КонецПроцедуры

&НаКлиенте
Процедура ПровайдерЭЦППриИзменении(Элемент)
	
	ЗаписатьКонстанту(Элемент.Имя,        ПровайдерЭЦП);
	ЗаписатьКонстанту("ТипПровайдераЭЦП", ТипПровайдераЭЦП);
	
	Если ЭлектронныеДокументыСлужебныйВызовСервера.ВыполнятьКриптооперацииНаСервере() Тогда
		ЗаполнитьСпискиВыбораНаСервере();
	Иначе
		ЗаполнитьСпискиВыбораНаКлиенте();
	КонецЕсли;
	
	ЗаполнитьАлгоритмыПоУмолчанию();
	
КонецПроцедуры

&НаКлиенте
Процедура ПровайдерЭЦПОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтрокаТипаПровайдера = "";
	Пока Прав(ВыбранноеЗначение, 1) <> "/" Цикл
		СтрокаТипаПровайдера = Прав(ВыбранноеЗначение, 1) + СтрокаТипаПровайдера;
		ВыбранноеЗначение = Лев(ВыбранноеЗначение, СтрДлина(ВыбранноеЗначение) - 1);
	КонецЦикла;
	ВыбранноеЗначение = Лев(ВыбранноеЗначение, СтрДлина(ВыбранноеЗначение) - 1);
	
	ТипПровайдераЭЦП = Число(СтрокаТипаПровайдера);
	
КонецПроцедуры

&НаКлиенте
Процедура АлгоритмШифрованияПриИзменении(Элемент)
	
	ЗаписатьКонстанту(Элемент.Имя, АлгоритмШифрования);
	
КонецПроцедуры

&НаКлиенте
Процедура АлгоритмХешированияПриИзменении(Элемент)
	
	ЗаписатьКонстанту(Элемент.Имя, АлгоритмХеширования);
	
КонецПроцедуры

&НаКлиенте
Процедура АлгоритмПодписиПриИзменении(Элемент)
	
	ЗаписатьКонстанту(Элемент.Имя, АлгоритмПодписи);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтекстАвторизацииПриИзменении(Элемент)
	
	СохранитьЗначениеКонстанты(Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтекстКриптографииПриИзменении(Элемент)
	
	СохранитьЗначениеКонстанты(Элемент.Имя);
	ПриОткрытии(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура АлгоритмШифрованияОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура АлгоритмПодписиОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура АлгоритмХешированияОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтекстКриптографииОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтекстАвторизацииОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервереБезКонтекста
Функция РазделениеВключено()
	
	Возврат ОбщегоНазначенияПовтИсп.РазделениеВключено();
	
КонецФункции

&НаКлиенте
Процедура ПодключитьМодульКриптографии()
	
	Если НЕ ПодключитьРасширениеРаботыСКриптографией() Тогда
		
		ЗаголовокВопроса = НСтр("ru = 'Расширение работы с криптографией'");
		
		ТекстВопроса =
			НСтр("ru = 'Для настройки ЭЦП необходимо установить
			           |расширение работы с криптографией.'");
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(1, НСтр("ru = 'Установить'"));
		Кнопки.Добавить(КодВозвратаДиалога.Отмена);
		
		Ответ = Вопрос(ТекстВопроса, Кнопки, 60, 1, ЗаголовокВопроса);
		
		Если Ответ <> 1 Тогда
			Возврат;
		КонецЕсли;
		
		УстановитьРасширениеРаботыСКриптографией();
		
		Если НЕ ПодключитьРасширениеРаботыСКриптографией() Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьИсправитьТекущиеЗначенияАлгоритмов()
	
	АлгоритмыПровайдера = АлгоритмыПровайдераЭЦППоУмолчанию(ПровайдерЭЦП);
	
	Если Элементы.АлгоритмПодписи.СписокВыбора.НайтиПоЗначению(АлгоритмПодписи) = Неопределено Тогда
		Если Элементы.АлгоритмПодписи.СписокВыбора.Количество() = 1	Тогда
			АлгоритмПодписи = Элементы.АлгоритмПодписи.СписокВыбора[0];
		Иначе
			АлгоритмПодписи = АлгоритмыПровайдера.АлгоритмПодписи;
		КонецЕсли;
		
		Шаблон = Нстр("ru = 'Алгоритм подписи автоматически изменен на %1'");						
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон, АлгоритмПодписи);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		ЗаписатьКонстанту("АлгоритмПодписи",     АлгоритмПодписи);
	КонецЕсли;
	
	Если Элементы.АлгоритмШифрования.СписокВыбора.НайтиПоЗначению(АлгоритмШифрования) = Неопределено Тогда 
		Если Элементы.АлгоритмШифрования.СписокВыбора.Количество() = 1 	Тогда
			АлгоритмШифрования = Элементы.АлгоритмШифрования.СписокВыбора[0];
		Иначе
			АлгоритмШифрования = АлгоритмыПровайдера.АлгоритмШифрования;
		КонецЕсли;
		
		Шаблон = Нстр("ru = 'Алгоритм шифрования автоматически изменен на %1'");						
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон, АлгоритмШифрования);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);		
		ЗаписатьКонстанту("АлгоритмШифрования",  АлгоритмШифрования);
	КонецЕсли;
		
	Если Элементы.АлгоритмХеширования.СписокВыбора.НайтиПоЗначению(АлгоритмХеширования) = Неопределено Тогда 
		Если Элементы.АлгоритмХеширования.СписокВыбора.Количество() = 1 Тогда
			АлгоритмХеширования = Элементы.АлгоритмХеширования.СписокВыбора[0];
		Иначе
			АлгоритмХеширования = АлгоритмыПровайдера.АлгоритмХеширования;
		КонецЕсли;
		
		Шаблон = Нстр("ru = 'Алгоритм хеширования автоматически изменен на %1'");						
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон, АлгоритмХеширования);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);		
		ЗаписатьКонстанту("АлгоритмХеширования", АлгоритмХеширования);
	КонецЕсли;
		
КонецПроцедуры



&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗначенияАлгоритмов(Элементы, ИнформацияМенеджера, АлгоритмПодписи, АлгоритмШифрования, АлгоритмХеширования)
	
	Элементы.АлгоритмПодписи.СписокВыбора.Очистить();
	Элементы.АлгоритмШифрования.СписокВыбора.Очистить();
	Элементы.АлгоритмХеширования.СписокВыбора.Очистить();	
	
	Если ИнформацияМенеджера = Неопределено Тогда		
		
		СпискиАлгоритмовУспешноЗаполнены = Ложь;
	Иначе
		СпискиАлгоритмовУспешноЗаполнены = Истина;
		
		Для Каждого Строка Из ИнформацияМенеджера.АлгоритмыПодписи Цикл
			Элементы.АлгоритмПодписи.СписокВыбора.Добавить(Строка);
		КонецЦикла;
		
		Для Каждого Строка Из ИнформацияМенеджера.АлгоритмыШифрования Цикл
			Элементы.АлгоритмШифрования.СписокВыбора.Добавить(Строка);
		КонецЦикла;
		
		Для Каждого Строка Из ИнформацияМенеджера.АлгоритмыХеширования Цикл
			Элементы.АлгоритмХеширования.СписокВыбора.Добавить(Строка);
		КонецЦикла;
		
	КонецЕсли; 
	        	
	
	Элементы.АлгоритмПодписи.Доступность     = СпискиАлгоритмовУспешноЗаполнены;
	Элементы.АлгоритмШифрования.Доступность  = СпискиАлгоритмовУспешноЗаполнены;
	Элементы.АлгоритмХеширования.Доступность = СпискиАлгоритмовУспешноЗаполнены;
	
	Если ОбщегоНазначенияПовтИсп.РазделениеВключено() Тогда
		Элементы.КонтекстАвторизации.Доступность  = Ложь;
		Элементы.КонтекстКриптографии.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры	

&НаКлиенте
Функция АлгоритмыПровайдераЭЦППоУмолчанию(ПровайдерЭЦПНаименование)
	
	Результат = Новый Структура();
	Результат.Вставить("АлгоритмПодписи");
	Результат.Вставить("АлгоритмШифрования");
	Результат.Вставить("АлгоритмХеширования");
	
	Если ПровайдерЭЦПНаименование = "Crypto-Pro GOST R 34.10-2012 Cryptographic Service Provider" Тогда
		Результат.АлгоритмПодписи = "GR 34.10-2012 256";
		Результат.АлгоритмШифрования = "GOST 28147-89";
		Результат.АлгоритмХеширования = "GR 34.11-2012 256";
	ИначеЕсли ПровайдерЭЦПНаименование = "Crypto-Pro GOST R 34.10-2001 Cryptographic Service Provider" Тогда
		Результат.АлгоритмПодписи = "GOST R 34.10-2001";
		Результат.АлгоритмШифрования = "GOST 28147-89";
		Результат.АлгоритмХеширования = "GOST R 34.11-94";
	ИначеЕсли ПровайдерЭЦПНаименование = "Infotecs GOST 2012/512 Cryptographic Service Provider" Тогда
		Результат.АлгоритмПодписи = "GR 34.10-2012 256";
		Результат.АлгоритмШифрования = "GOST 28147-89";
		Результат.АлгоритмХеширования = "GR 34.11-2012 256";
	ИначеЕсли ПровайдерЭЦПНаименование = "Infotecs Cryptographic Service Provider" Тогда
		Результат.АлгоритмПодписи = "GOST R 34.10-2001";
		Результат.АлгоритмШифрования = "GOST 28147-89";
		Результат.АлгоритмХеширования = "GOST R 34.11-94";
	ИначеЕсли ПровайдерЭЦПНаименование = "Signal-COM CPGOST Cryptographic Provider" Тогда
		Результат.АлгоритмПодписи = "ECR3410-CP";
		Результат.АлгоритмШифрования = "GOST28147";
		Результат.АлгоритмХеширования = "RUS-HASH-CP";
	ИначеЕсли ПровайдерЭЦПНаименование = "Microsoft Enhanced Cryptographic Provider v1.0" Тогда
		Результат.АлгоритмПодписи = "RSA_SIGN";
		Результат.АлгоритмШифрования = "RC2";
		Результат.АлгоритмХеширования = "MD5";
	ИначеЕсли ПровайдерЭЦПНаименование = "Microsoft Strong Cryptographic Provider" Тогда
		Результат.АлгоритмПодписи = "RSA_SIGN";
		Результат.АлгоритмШифрования = "RC2";
		Результат.АлгоритмХеширования = "MD5";
	Иначе
		Результат.АлгоритмПодписи = "";
		Результат.АлгоритмШифрования = "";
		Результат.АлгоритмХеширования = "";
	КонецЕсли;	
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьСпискиВыбораНаКлиенте()
	
	ОчиститьСообщения();

	Если ЗначениеЗаполнено(ПровайдерЭЦП) Тогда
		ИнформацияМенеджера = СкомпоноватьИнформациюМенеджераКриптографииНаКлиенте(
								ПровайдерЭЦП,
								Неопределено,
								ТипПровайдераЭЦП);
	Иначе
		ИнформацияМенеджера = Неопределено;
	КонецЕсли;
	
	УстановитьЗначенияАлгоритмов(Элементы, ИнформацияМенеджера, АлгоритмПодписи, АлгоритмШифрования, АлгоритмХеширования);
	
	
КонецПроцедуры
 
&НаСервере
Процедура ЗаполнитьСпискиВыбораНаСервере()
		
	Если ЗначениеЗаполнено(ПровайдерЭЦП) Тогда
		ИнформацияМенеджера = СкомпоноватьИнформациюМенеджераКриптографииНаСервере(
								ПровайдерЭЦП,
								Неопределено,
								ТипПровайдераЭЦП);
	Иначе
		ИнформацияМенеджера = Неопределено;
	КонецЕсли;
	
	УстановитьЗначенияАлгоритмов(Элементы, ИнформацияМенеджера, АлгоритмПодписи, АлгоритмШифрования, АлгоритмХеширования);
		
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПараметрыНастройкиКриптографииПоУмолчанию()
	
	Если ЗначениеЗаполнено(ПровайдерЭЦП) Тогда
		Возврат;
	КонецЕсли;
	
	ОбщееКоличествоКриптосредствНаКомпьютере = Элементы.ПровайдерЭЦП.СписокВыбора.Количество();
	ЕстьИсключаемоеКриптосредство = НЕ Элементы.ПровайдерЭЦП.СписокВыбора.НайтиПоЗначению("Microsoft Enhanced Cryptographic Provider v1.0/1") = Неопределено;
	ОжидаемоеКоличествоКриптосредств = ?(ЕстьИсключаемоеКриптосредство, 2, 1);
	Если ОбщееКоличествоКриптосредствНаКомпьютере = ОжидаемоеКоличествоКриптосредств Тогда
		Для Каждого Криптосредство ИЗ Элементы.ПровайдерЭЦП.СписокВыбора Цикл
			Если НЕ Криптосредство.Значение = "Microsoft Enhanced Cryptographic Provider v1.0/1" Тогда
				ПровайдерЭЦП = Криптосредство.Представление;
				СтрокаТипаПровайдера = "";
				ВыбранноеЗначение = Криптосредство.Значение;
				Пока Прав(ВыбранноеЗначение, 1) <> "/" Цикл
					СтрокаТипаПровайдера = Прав(ВыбранноеЗначение, 1) + СтрокаТипаПровайдера;
					ВыбранноеЗначение = Лев(ВыбранноеЗначение, СтрДлина(ВыбранноеЗначение) - 1);
				КонецЦикла;
				ТипПровайдераЭЦП = Число(СтрокаТипаПровайдера);
				Если ЭлектронныеДокументыСлужебныйВызовСервера.ВыполнятьКриптооперацииНаСервере() Тогда
					ЗаполнитьСпискиВыбораНаСервере();
				Иначе
					ЗаполнитьСпискиВыбораНаКлиенте();
				КонецЕсли;
				ЗаполнитьАлгоритмыПоУмолчанию();
			КонецЕсли
		КонецЦикла;
	КонецЕсли;
	
	ЗаписатьКонстанту("ПровайдерЭЦП",        ПровайдерЭЦП);
	ЗаписатьКонстанту("ТипПровайдераЭЦП",    ТипПровайдераЭЦП);
	ЗаписатьКонстанту("АлгоритмПодписи",     АлгоритмПодписи);
	ЗаписатьКонстанту("АлгоритмШифрования",  АлгоритмШифрования);
	ЗаписатьКонстанту("АлгоритмХеширования", АлгоритмХеширования);
	
КонецПроцедуры

&НаКлиенте
Функция СкомпоноватьИнформациюМенеджераКриптографииНаКлиенте(ИмяМодуляКриптографии,
															ПутьМодуляКриптографии,
															ТипМодуляКриптографии,
															СообщатьОшибки = Истина)
	
	Если ПутьМодуляКриптографии = Неопределено Тогда
		ПутьМодуляКриптографии = ЭлектроннаяЦифроваяПодписьКлиент.ПолучитьПерсональныеНастройкиРаботыСЭЦП().ПутьМодуляКриптографии;
	КонецЕсли;
	
	ИнформацияМенеджера = Неопределено;
	
	Попытка
		
		МенеджерКриптографии = Новый МенеджерКриптографии(ИмяМодуляКриптографии,
														  ПутьМодуляКриптографии,
														  ТипМодуляКриптографии);
		ИнформацияМенеджера = МенеджерКриптографии.ПолучитьИнформациюМодуляКриптографии();
		
	Исключение
		
		Если СообщатьОшибки Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецЕсли;
		
	КонецПопытки;
	
	Если ИнформацияМенеджера <> Неопределено Тогда
		
		ЗначениеСпискаВыбора = ИнформацияМенеджера.Имя + "/" + ТипМодуляКриптографии;
		
		Если Элементы.ПровайдерЭЦП.СписокВыбора.НайтиПоЗначению(ЗначениеСпискаВыбора) = Неопределено Тогда
			Элементы.ПровайдерЭЦП.СписокВыбора.Добавить(ЗначениеСпискаВыбора, ИнформацияМенеджера.Имя);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ИнформацияМенеджера;
	
КонецФункции

&НаСервере
Функция СкомпоноватьИнформациюМенеджераКриптографииНаСервере(ИмяМодуляКриптографии,
															ПутьМодуляКриптографии,
															ТипМодуляКриптографии,
															СообщатьОшибки = Истина)
	
	Если ПутьМодуляКриптографии = Неопределено Тогда
		ПутьМодуляКриптографии = ЭлектроннаяЦифроваяПодпись.ПолучитьПерсональныеНастройкиРаботыСЭЦПСервер().ПутьМодуляКриптографии;
	КонецЕсли;
	
	ИнформацияМенеджера = Неопределено;
	
	Попытка
		
		МенеджерКриптографии = Новый МенеджерКриптографии(ИмяМодуляКриптографии,
														  ПутьМодуляКриптографии,
														  ТипМодуляКриптографии);
		ИнформацияМенеджера = МенеджерКриптографии.ПолучитьИнформациюМодуляКриптографии();
		
	Исключение
		
		Если СообщатьОшибки Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецЕсли;
		
	КонецПопытки;
	
	Если ИнформацияМенеджера <> Неопределено Тогда
		
		ЗначениеСпискаВыбора = ИнформацияМенеджера.Имя + "/" + ТипМодуляКриптографии;
		
		Если Элементы.ПровайдерЭЦП.СписокВыбора.НайтиПоЗначению(ЗначениеСпискаВыбора) = Неопределено Тогда
			Элементы.ПровайдерЭЦП.СписокВыбора.Добавить(ЗначениеСпискаВыбора, ИнформацияМенеджера.Имя);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ИнформацияМенеджера;
	
КонецФункции

&НаКлиенте
Процедура ДобавитьМенеджераКриптографииВСписок(ИмяМодуляКриптографии, ПутьМодуляКриптографии, ТипМодуляКриптографии)
	
	Если ЭлектронныеДокументыСлужебныйВызовСервера.ВыполнятьКриптооперацииНаСервере() Тогда
		ДобавитьМенеджераКриптографииВСписокНаСервере(
				ИмяМодуляКриптографии,
				ПутьМодуляКриптографии,
				ТипМодуляКриптографии);
	Иначе
		 СкомпоноватьИнформациюМенеджераКриптографииНаКлиенте(
				ИмяМодуляКриптографии,
				ПутьМодуляКриптографии,
				ТипМодуляКриптографии,
				Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьАлгоритмыПоУмолчанию()
	
	АлгоритмыПровайдера = АлгоритмыПровайдераЭЦППоУмолчанию(ПровайдерЭЦП);
	
	АлгоритмПодписи      = АлгоритмыПровайдера.АлгоритмПодписи;
	АлгоритмШифрования   = АлгоритмыПровайдера.АлгоритмШифрования;
	АлгоритмХеширования  = АлгоритмыПровайдера.АлгоритмХеширования;
	
	ЗаписатьКонстанту("АлгоритмПодписи",     АлгоритмПодписи);
	ЗаписатьКонстанту("АлгоритмШифрования",  АлгоритмШифрования);
	ЗаписатьКонстанту("АлгоритмХеширования", АлгоритмХеширования);
	
	Если НЕ РазделениеВключено() Тогда
		ЗаписатьКонстанту("КонтекстКриптографии", ПредопределенноеЗначение("Перечисление.КонтекстыРаботыСЭД.НаКлиенте"));
		ЗаписатьКонстанту("КонтекстАвторизации",  ПредопределенноеЗначение("Перечисление.КонтекстыРаботыСЭД.НаКлиенте"));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьМенеджераКриптографииВСписокНаСервере(ИмяМодуляКриптографии,
														ПутьМодуляКриптографии,
														ТипМодуляКриптографии)
	
	СкомпоноватьИнформациюМенеджераКриптографииНаСервере(
			ИмяМодуляКриптографии,
			ПутьМодуляКриптографии,
			ТипМодуляКриптографии,
			Ложь);
		
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьНачальныеЗначенияРеквизитов()
	
	// Инициализация набора констант
	НаборКонстантОбъект = РеквизитФормыВЗначение("НаборКонстант");
	НаборКонстантОбъект.Прочитать();
	ЗначениеВРеквизитФормы(НаборКонстантОбъект, "НаборКонстант");
	
КонецПроцедуры

&НаСервере
Процедура СохранитьЗначениеКонстанты(ИмяКонстанты)
	
	Если НЕ ЗначениеЗаполнено(НаборКонстант[ИмяКонстанты]) Тогда
		НаборКонстант[ИмяКонстанты] = Константы[ИмяКонстанты].Получить();
	КонецЕсли;
	
	УстановитьЗначениеКонстантыПоИмени(ИмяКонстанты);
	
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗначениеКонстантыПоИмени(ИмяКонстанты)
	
	Если Константы[ИмяКонстанты].Получить() <> НаборКонстант[ИмяКонстанты] Тогда
		Константы[ИмяКонстанты].Установить(НаборКонстант[ИмяКонстанты]);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьКонстанту(ИмяКонстанты, ЗначениеКонстанты)
	
	Константы[ИмяКонстанты].Установить(ЗначениеКонстанты);
	ОбновитьПовторноИспользуемыеЗначения();
	
конецПроцедуры

&НаСервереБезКонтекста
Функция НеобходимаБлокировкаВРамкахРИБ()

	// В подчиненном узле проверяем, мигрируют ли константы БСП.
	// Если в прикладном решении не предусмотрена миграция констант, то блокировать нет необходимости.
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		ПроверяемыеМетаданные = Новый Массив;
		ПроверяемыеМетаданные.Добавить(Метаданные.Константы.ПровайдерЭЦП);
		ПроверяемыеМетаданные.Добавить(Метаданные.Константы.АлгоритмПодписи);
		ПроверяемыеМетаданные.Добавить(Метаданные.Константы.АлгоритмХеширования);
		ПроверяемыеМетаданные.Добавить(Метаданные.Константы.АлгоритмШифрования);
		Для Каждого ПланОбмена Из Метаданные.ПланыОбмена Цикл
			Если ПланОбмена.РаспределеннаяИнформационнаяБаза Тогда
				Для Каждого ЭлементПроверяемыхМетаданных Из ПроверяемыеМетаданные Цикл
					Если ПланОбмена.Состав.Найти(ЭлементПроверяемыхМетаданных) <> Неопределено Тогда
						Возврат Истина;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Ложь;

КонецФункции

