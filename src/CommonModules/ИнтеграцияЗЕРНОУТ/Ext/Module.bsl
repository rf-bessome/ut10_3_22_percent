Функция КоэффициентыЕдиницИзмеренияЗЕРНО(ДанныеСтрокЗЕРНО) Экспорт
	
	ШаблонРезультата = Новый Структура;
	ШаблонРезультата.Вставить("КодОшибки",                     0);
	ШаблонРезультата.Вставить("Коэффициент",                   1);
	
	Номенклатура = Новый Массив;
	Для каждого СтрокаИсточника Из ДанныеСтрокЗЕРНО Цикл
		Номенклатура.Добавить(СтрокаИсточника.Номенклатура);
	КонецЦикла;
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Номенклатура.Ссылка КАК Номенклатура,
	|	Номенклатура.ЕдиницаХраненияОстатков КАК Упаковка,
	|	ВЫБОР
	|		КОГДА Номенклатура.ЕдиницаХраненияОстатков.Вес = 0 ТОГДА 1
	|		ИНАЧЕ Номенклатура.ЕдиницаХраненияОстатков.Вес
	|	КОНЕЦ КАК Коэффициент
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ Номенклатура.Ссылка В (&Номенклатура)";

	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	КоэффициентыУпаковокНоменклатуры = Новый Соответствие;
	
	Пока Выборка.Следующий() Цикл
		
		Результат = ОбщегоНазначения.СкопироватьРекурсивно(ШаблонРезультата);
		
		Если Выборка.Коэффициент = 0 Тогда
			Результат.Коэффициент = 1;
			Результат.КодОшибки = 3;
		Иначе
			Результат.Коэффициент = Окр(1/Выборка.Коэффициент,7);
		КонецЕсли;
		
		КоэффициентыУпаковокНоменклатуры.Вставить(Выборка.Номенклатура, Результат);
		
	КонецЦикла;
	
	Возврат КоэффициентыУпаковокНоменклатуры;
	
КонецФункции

Процедура ОбработкаЗаполненияДокументаФормированиеПартийЗЕРНО(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	ТипОснования = ОпределитьТипОснования(ДанныеЗаполнения);
	
	Если ТипОснования = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
		ЗаполнитьФормированиеПартийЗЕРНОНаОснованииЗаказаПоставщику(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);	
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ОприходованиеТоваров") Тогда 
		ЗаполнитьФормированиеПартийЗЕРНОНаОснованииОприходованиеТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполненияДокументаОформлениеСДИЗЗЕРНО(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	ТипОснования = ОпределитьТипОснования(ДанныеЗаполнения);
	
	Если ТипОснования = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		ЗаполнитьОформлениеСДИЗЗЕРНОНаОснованииРеализацииТоваров(
			ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
		ЗаполнитьОформлениеСДИЗЗЕРНОНаОснованииВозвратаТоваровПоставщику(
			ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
		ЗаполнитьОформлениеСДИЗЗЕРНОНаОснованииПеремещенияТоваров(
			ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	КонецЕсли;
		
КонецПроцедуры

Процедура ОбработкаЗаполненияДокументаВнесениеСведенийОСобранномУрожаеЗЕРНО(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	ТипОснования = ОпределитьТипОснования(ДанныеЗаполнения);
	//Если ТипОснования = Тип("ДокументСсылка.ОтчетПроизводстваЗаСмену") Тогда
	//	ЗаполнитьВнесениеСведенийОСобранномУрожаеЗЕРНОНаОснованииОтчетПроизводстваЗаСмену(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	//КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполненияДокументаСписаниеПартийЗЕРНО(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	ТипОснования = ОпределитьТипОснования(ДанныеЗаполнения);
	
	Если ТипОснования = Тип("ДокументСсылка.ТребованиеНакладная") Тогда
		ЗаполнитьСписаниеПартийЗЕРНОНаОснованииТребованиеНакладная(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);	
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		ЗаполнитьСписаниеПартийЗЕРНОНаОснованииРеализацияТоваровУслуг(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.СписаниеТоваров") Тогда 
		ЗаполнитьСписаниеПартийЗЕРНОНаОснованииСписанияТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.КомплектацияНоменклатуры") Тогда
		ЗаполнитьСписаниеПартийЗЕРНОНаОснованииКомплектацияНоменклатуры(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполненияДокументаПогашениеСДИЗЗЕРНО(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	Если ДокументОбъект.Товары.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДокументОснование = ДокументОснованиеИзДанныхЗаполнения(ДанныеЗаполнения);
	
	ТипДокументаОснования = ТипЗнч(ДокументОснование);
	
	Если ТипДокументаОснования = Тип("ДокументСсылка.ПоступлениеТоваровУслуг")
		Или ТипДокументаОснования = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя")
		Или ТипДокументаОснования = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
	Иначе
		ДокументОснование = Неопределено;
	КонецЕсли;
	
	Если ДокументОснование <> Неопределено Тогда
		
		ИмяДокументаОснования = ДокументОснование.Метаданные().Имя;
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Таблица", ДокументОбъект.Товары.Выгрузить(,"НомерСтроки,СДИЗ,Номенклатура"));
		Запрос.УстановитьПараметр("Ссылка", ДокументОснование);
		Запрос.Текст = СтрШаблон(
		"ВЫБРАТЬ
		|	Таблица.НомерСтроки,
		|	Таблица.СДИЗ КАК СДИЗ
		|ПОМЕСТИТЬ Таблица
		|ИЗ
		|	&Таблица КАК Таблица
		|ГДЕ
		|	Таблица.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|;
		|ВЫБРАТЬ
		|	Товары.Номенклатура.ОКПД2.Код КАК ОКПД2,
		|	Товары.Номенклатура КАК Номенклатура,
		|	МИНИМУМ(Товары.ХарактеристикаНоменклатуры) КАК Характеристика,
		|	МИНИМУМ(Товары.СерияНоменклатуры) КАК Серия
		|ПОМЕСТИТЬ ТаблицаДокумента
		|ИЗ
		|	Документ.%1.Товары КАК Товары
		|ГДЕ
		|	Товары.Ссылка = &Ссылка
		|	И Товары.Номенклатура.ОКПД2 <> ЗНАЧЕНИЕ(Справочник.КлассификаторОКПД2.ПустаяСсылка)
		|СГРУППИРОВАТЬ ПО
		|	Товары.Номенклатура
		|;
		|ВЫБРАТЬ
		|	Таблица.НомерСтроки,
		|	ТаблицаДокумента.Номенклатура,
		|	ТаблицаДокумента.Характеристика,
		|	ТаблицаДокумента.Серия
		|ИЗ
		|	Таблица КАК Таблица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДокумента КАК ТаблицаДокумента
		|		ПО Таблица.СДИЗ.Партия.ОКПД2 = ТаблицаДокумента.ОКПД2",
		ИмяДокументаОснования);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Если ЗначениеЗаполнено(Выборка.Номенклатура) Тогда
				СтрокаДокумента = ДокументОбъект.Товары[Выборка.НомерСтроки - 1];
				Если Не ЗначениеЗаполнено(СтрокаДокумента.Номенклатура) Тогда
					ЗаполнитьЗначенияСвойств(СтрокаДокумента, Выборка,, "НомерСтроки");
				ИначеЕсли СтрокаДокумента.Номенклатура = Выборка.Номенклатура 
						И Не ЗначениеЗаполнено(СтрокаДокумента.Характеристика) Тогда
					ЗаполнитьЗначенияСвойств(СтрокаДокумента, Выборка,, "НомерСтроки");
				ИначеЕсли СтрокаДокумента.Номенклатура = Выборка.Номенклатура 
						И СтрокаДокумента.Характеристика = Выборка.Характеристика
						И Не ЗначениеЗаполнено(СтрокаДокумента.Серия) Тогда
					ЗаполнитьЗначенияСвойств(СтрокаДокумента, Выборка,, "НомерСтроки");
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаЗаполненияДокументаФормированиеПартийПриПроизводствеЗЕРНО(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	ТипОснования = ОпределитьТипОснования(ДанныеЗаполнения);
	
	Если ТипОснования = Тип("ДокументСсылка.КомплектацияНоменклатуры")Тогда 
		ЗаполнитьФормированиеПартийПриПроизводствеЗЕРНОНаОснованииКомплектацияНоменклатуры(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

#Область Серии

// Параметры указания серий внесение сведений о собранном урожае ЗЕРНО.
// 
// Параметры:
//  Объект - ДокументОбъект - Объект
// 
// Возвращаемое значение:
//  См. НоменклатураКлиентСервер.ПараметрыУказанияСерий
Функция ПараметрыУказанияСерийВнесениеСведенийОСобранномУрожаеЗЕРНО(Объект) Экспорт
	
	ПараметрыУказанияСерий        = ИнтеграцияЕГАИСУТ.ПараметрыУказанияСерий();
	ИспользоватьСерииНоменклатуры = Константы.ИспользоватьСерииНоменклатуры.Получить();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта               = "Документ.ВнесениеСведенийОСобранномУрожаеЗЕРНО";
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.ИмяТЧСерии                     = "Товары";
	ПараметрыУказанияСерий.ИмяПоляСклад                   = Неопределено;
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

// Параметры указания серий запрос остатков партий ЗЕРНО.
// 
// Параметры:
//  Объект - ДокументОбъект - Объект
// 
// Возвращаемое значение:
//  См. НоменклатураКлиентСервер.ПараметрыУказанияСерий
Функция ПараметрыУказанияСерийЗапросОстатковПартийЗЕРНО(Объект) Экспорт
	
	ПараметрыУказанияСерий        = ИнтеграцияЕГАИСУТ.ПараметрыУказанияСерий();
	ИспользоватьСерииНоменклатуры = Константы.ИспользоватьСерииНоменклатуры.Получить();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта               = "Документ.ЗапросОстатковПартийЗЕРНО";
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.ИмяТЧТовары                    = "ОстаткиПоДаннымЗЕРНО";
	ПараметрыУказанияСерий.ИмяТЧСерии                     = "ОстаткиПоДаннымЗЕРНО";
	ПараметрыУказанияСерий.ИмяПоляСклад                   = Неопределено;
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

// Параметры указания серий оформление СДИЗ ЗЕРНО.
// 
// Параметры:
//  Объект - ДокументОбъект - Объект
// 
// Возвращаемое значение:
//  См. НоменклатураКлиентСервер.ПараметрыУказанияСерий
Функция ПараметрыУказанияСерийОформлениеСДИЗЗЕРНО(Объект) Экспорт
	
	ПараметрыУказанияСерий        = ИнтеграцияЕГАИСУТ.ПараметрыУказанияСерий();
	ИспользоватьСерииНоменклатуры = Константы.ИспользоватьСерииНоменклатуры.Получить();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта               = "Документ.ОформлениеСДИЗЗЕРНО";
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.ИмяТЧСерии                     = "Товары";
	ПараметрыУказанияСерий.ИмяПоляСклад                   = Неопределено;
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

// Параметры указания серий погашение СДИЗ ЗЕРНО.
// 
// Параметры:
//  Объект - ДокументОбъект - Объект
// 
// Возвращаемое значение:
//  См. НоменклатураКлиентСервер.ПараметрыУказанияСерий
Функция ПараметрыУказанияСерийПогашениеСДИЗЗЕРНО(Объект) Экспорт
	
	ПараметрыУказанияСерий        = ИнтеграцияЕГАИСУТ.ПараметрыУказанияСерий();
	ИспользоватьСерииНоменклатуры = Константы.ИспользоватьСерииНоменклатуры.Получить();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта               = "Документ.ПогашениеСДИЗЗЕРНО";
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.ИмяТЧСерии                     = "Товары";
	ПараметрыУказанияСерий.ИмяПоляСклад                   = Неопределено;
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

// Параметры указания серий списание партий ЗЕРНО.
// 
// Параметры:
//  Объект - ДокументОбъект - Объект
// 
// Возвращаемое значение:
//  См. НоменклатураКлиентСервер.ПараметрыУказанияСерий
Функция ПараметрыУказанияСерийСписаниеПартийЗЕРНО(Объект) Экспорт
	
	ПараметрыУказанияСерий        = ИнтеграцияЕГАИСУТ.ПараметрыУказанияСерий();
	ИспользоватьСерииНоменклатуры = Константы.ИспользоватьСерииНоменклатуры.Получить();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта               = "Документ.СписаниеПартийЗЕРНО";
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.ИмяТЧСерии                     = "Товары";
	ПараметрыУказанияСерий.ИмяПоляСклад                   = Неопределено;
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

// Параметры указания серий формирование партий ЗЕРНО.
// 
// Параметры:
//  Объект - ДокументОбъект - Объект
// 
// Возвращаемое значение:
//  См. НоменклатураКлиентСервер.ПараметрыУказанияСерий
Функция ПараметрыУказанияСерийФормированиеПартийЗЕРНО(Объект) Экспорт
	
	ПараметрыУказанияСерий        = ИнтеграцияЕГАИСУТ.ПараметрыУказанияСерий();
	ИспользоватьСерииНоменклатуры = Константы.ИспользоватьСерииНоменклатуры.Получить();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта               = "Документ.ФормированиеПартийЗЕРНО";
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.ИмяТЧСерии                     = "Товары";
	ПараметрыУказанияСерий.ИмяПоляСклад                   = Неопределено;
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

// Параметры указания серий формирование партий из других партий ЗЕРНО.
// 
// Параметры:
//  Объект - ДокументОбъект - Объект
// 
// Возвращаемое значение:
//  См. НоменклатураКлиентСервер.ПараметрыУказанияСерий
Функция ПараметрыУказанияСерийФормированиеПартийИзДругихПартийЗЕРНО(Объект) Экспорт
	
	ПараметрыУказанияСерий        = ИнтеграцияЕГАИСУТ.ПараметрыУказанияСерий();
	ИспользоватьСерииНоменклатуры = Константы.ИспользоватьСерииНоменклатуры.Получить();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта               = "Документ.ФормированиеПартийИзДругихПартийЗЕРНО";
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.ТоварВШапке                    = Истина;
	ПараметрыУказанияСерий.ИмяПоляСклад                   = Неопределено;
	ПараметрыУказанияСерий.ИмяТЧТовары                    = Неопределено;
	ПараметрыУказанияСерий.ИмяТЧСерии                     = Неопределено;
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

// Параметры указания серий формирование партий при производстве ЗЕРНО.
// 
// Параметры:
//  Объект - ДокументОбъект - Объект
// 
// Возвращаемое значение:
//  Структура - параметры указания серий объектов:
//  * Шапка - См. НоменклатураКлиентСервер.ПараметрыУказанияСерий,
//  * Сырье - См. НоменклатураКлиентСервер.ПараметрыУказанияСерий.
Функция ПараметрыУказанияСерийФормированиеПартийПриПроизводствеЗЕРНО(Объект) Экспорт
	
	ИспользоватьСерииНоменклатуры = Константы.ИспользоватьСерииНоменклатуры.Получить();
	
	ПараметрыУказанияСерий  = ИнтеграцияЕГАИСУТ.ПараметрыУказанияСерий();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта               = "Документ.ФормированиеПартийПриПроизводствеЗЕРНО";
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.ИмяТЧТовары                    = "Сырье";
	ПараметрыУказанияСерий.ИмяТЧСерии                     = "Сырье";
	ПараметрыУказанияСерий.ИмяПоляСклад                   = Неопределено;
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

// Параметры указания серий соответствие партий ЗЕРНО.
// 
// Параметры:
//  Объект - ДокументОбъект - Объект
// 
// Возвращаемое значение:
//  См. НоменклатураКлиентСервер.ПараметрыУказанияСерий
Функция ПараметрыУказанияСерийСоответствиеПартийЗЕРНО(Объект) Экспорт
	
	ПараметрыУказанияСерий        = ИнтеграцияЕГАИСУТ.ПараметрыУказанияСерий();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта = "РегистрСведений.СоответствиеПартийЗЕРНО";
	ИспользоватьСерииНоменклатуры = Константы.ИспользоватьСерииНоменклатуры.Получить();
	ПараметрыУказанияСерий.ТоварВШапке = Истина;
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.ИмяТЧСерии = "";
	ПараметрыУказанияСерий.ИмяТЧТовары = "";
	ПараметрыУказанияСерий.ИмяПоляКоличество = Неопределено;
	ПараметрыУказанияСерий.ИмяПоляСклад = Неопределено;
	ПараметрыУказанияСерий.ИмяИсточникаЗначенийВФормеОбъекта = "ЭтаФорма";
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

#КонецОбласти

#Область ОбработкаЗаполнения

#Область ФормированиеПартийЗЕРНО

Процедура ЗаполнитьШапкуФормированиеПартийЗЕРНО(ДокументОбъект, ДанныеЗаполнения, Запрос)
	
	Если ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьШапку") Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ДокументОснование = ДанныеЗаполнения.Основание;
	Иначе
		ДокументОснование = ДанныеЗаполнения;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ДокументОснование",            ДокументОснование);
	Запрос.УстановитьПараметр("ФормированиеПартииИзОстатков", Перечисления.ВидыОперацийЗЕРНО.ФормированиеПартииИзОстатков);
	Запрос.УстановитьПараметр("ФормированиеПартииИмпорт",     Перечисления.ВидыОперацийЗЕРНО.ФормированиеПартииИмпорт);
	Запрос.УстановитьПараметр("ПричинаСписанияИспорчено",     Справочники.КлассификаторНСИЗЕРНО.ПричинаСписанияИспорчено);
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	
	Если Не ЭтоПерезаполнение
		И Не ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьШапку") Тогда
		
		ИнтеграцияИСУТ.ПроверитьВозможностьВводаНаОсновании(
			Реквизиты.ДокументОснование,,
			Реквизиты.ЕстьОшибкиПроведен,,);
		
		ЗаполнитьЗначенияСвойств(ДокументОбъект, Реквизиты);
		
		ДокументОбъект.ГодУрожая = Год(Реквизиты.Дата);
		
		Если ДокументОбъект.Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийЗЕРНО.ФормированиеПартииИмпорт") Тогда
			ДокументОбъект.НазначениеПартии = Справочники.КлассификаторНСИЗЕРНО.НазначениеПартииВвозНаТерриториюРФ;
		Иначе
			ДокументОбъект.НазначениеПартии = Справочники.КлассификаторНСИЗЕРНО.НазначениеПартииХранениеОбработка;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьТабличнуюЧастьФормированиеПартийЗЕРНО(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента, ДопустимыйЗнак)
	
	Если ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьТабличнуюЧасть") Тогда
		Возврат;
	КонецЕсли;
	
	ФильтрВидПродукции = ИнтеграцияЗЕРНОКлиентСерверПовтИсп.УчитываемыеВидыПродукции();
	ОсобенностьУчета   = Новый Массив;
	Для Каждого ВидПродукции Из ФильтрВидПродукции Цикл
		ОсобенностьУчета.Добавить(ВидПродукции);
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ДокументОбъект.ВидПродукции) Тогда
		ФильтрВидПродукции = ДокументОбъект.ВидПродукции;
		ОсобенностьУчета   = ФильтрВидПродукции;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Если ДанныеЗаполнения.ИмяФильтра = "ЗаполнениеСВидомПродукции" Тогда
			ФильтрВидПродукции = ДанныеЗаполнения.ЗначениеФильтра;
			ОсобенностьУчета   = ФильтрВидПродукции;
		КонецЕсли;
		Основание = ДанныеЗаполнения.Основание;
	Иначе
		Основание = ДанныеЗаполнения;
	КонецЕсли;
	
	ДокументОбъект.Товары.Очистить();
	ДокументОбъект.ПотребительскиеСвойства.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаЗаполненияФормированиеПартийЗЕРНОИзПрикладногоДокумента(ТекстТоварыДокумента, ДопустимыйЗнак);
	
	Запрос.УстановитьПараметр("ЭтаСсылка",         ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", Основание);
	Запрос.УстановитьПараметр("ПустаяСерия",       ОпределяемыеТипы().СерияНоменклатуры.Тип.ПривестиЗначение());
	Запрос.УстановитьПараметр("ОсобенностьУчета",  ОсобенностьУчета);
	
	КолонкиКоличестваЗЕРНО = Новый Структура;
	КолонкиКоличестваЗЕРНО.Вставить("Количество", "КоличествоЗЕРНО");
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПотребительскиеСвойства", ДокументОбъект.ПотребительскиеСвойства);
	
	ЗаполнитьТабличнуюЧастьДокумента(ДокументОбъект.Товары, Запрос.Выполнить(), ДанныеЗаполнения, КолонкиКоличестваЗЕРНО,,ДополнительныеПараметры);
	
	Если ТипЗнч(ОсобенностьУчета) = Тип("Массив") Тогда
		ИнтеграцияИС.ЗаполнитьВидПродукцииПоТабличнойЧасти(ДокументОбъект);
	Иначе
		ДокументОбъект.ВидПродукции = ФильтрВидПродукции;
	КонецЕсли;
	
КонецПроцедуры

Функция ТекстЗапросаЗаполненияФормированиеПартийЗЕРНОИзПрикладногоДокумента(ТекстТоварыДокумента, ДопустимыйЗнак)
	Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ФормированиеПартийЗЕРНО КАК ТаблицаДокументы
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|;"
	+
	ТекстТоварыДокумента
	+
	"
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыСерии.Номенклатура    КАК Номенклатура,
	|	ТоварыСерии.Характеристика  КАК Характеристика,
	|	ТоварыСерии.Серия           КАК Серия,
	|	ТоварыСерии.Количество      КАК Количество,
	|	ТоварыСерии.СкладКонтрагент КАК СкладКонтрагент
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	ТоварыСерии КАК ТоварыСерии
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура    КАК Номенклатура,
	|	ОформленныеТовары.Характеристика  КАК Характеристика,
	|	ОформленныеТовары.Серия           КАК Серия,
	|	-ОформленныеТовары.Количество     КАК Количество,
	|	ОформленныеТовары.СкладКонтрагент КАК СкладКонтрагент
	|ИЗ
	|	Документ.ФормированиеПартийЗЕРНО.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В
	|		(ВЫБРАТЬ
	|			Т.Ссылка
	|		ИЗ
	|			ОформленныеДокументы КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура           КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика         КАК Характеристика,
	|	ТоварыКОформлению.Серия                  КАК Серия,
	|	СУММА(ТоварыКОформлению.Количество)      КАК Количество,
	|	СУММА(ТоварыКОформлению.Количество)      КАК КоличествоЗЕРНО,
	|	ТоварыКОформлению.Серия.ДатаПроизводства КАК ДатаИзготовления,
	|	ТоварыКОформлению.Серия.СрокГодности     КАК СрокГодности,
	|	СправочникНоменклатура.КодТНВЭД.Код      КАК КодТНВЭД,
	|	СправочникНоменклатура.ОКПД2.Код         КАК ОКПД2,
	|	ТоварыКОформлению.СкладКонтрагент 		 КАК СкладКонтрагент
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТоварыКОформлению.Номенклатура = СправочникНоменклатура.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	ТоварыКОформлению.Серия.ДатаПроизводства,
	|	ТоварыКОформлению.Серия.СрокГодности,
	|	СправочникНоменклатура.КодТНВЭД.Код,
	|	СправочникНоменклатура.ОКПД2.Код,
	|	ТоварыКОформлению.СкладКонтрагент
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0";
	
	Возврат Текст;
	
КонецФункции

Процедура ЗаполнитьФормированиеПартийЗЕРНОНаОснованииЗаказаПоставщику(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка         КАК ДокументОснование,
	|	НЕ ДокументСсылка.Проведен    КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Организация    КАК Организация,
	|	ДокументСсылка.Дата           КАК Дата,
	|	ДокументСсылка.Склад          КАК Склад,
	|	&ФормированиеПартииИмпорт     КАК Операция
	|ИЗ
	|	Документ.ЗаказПоставщику КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуФормированиеПартийЗЕРНО(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура      КАК Номенклатура,
	|	ТаблицаТовары.ХарактеристикаНоменклатуры    КАК Характеристика,
	|	&ПустаяСерия                    КАК Серия,
	|	ТаблицаДокумента.Склад          КАК СкладКонтрагент,
	|	СУММА(ТаблицаТовары.Количество * ВЫБОР КОГДА ТаблицаТовары.Коэффициент = 0 ТОГДА 1 ИНАЧЕ ТаблицаТовары.Коэффициент КОНЕЦ) КАК Количество
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	Документ.ЗаказПоставщику.Товары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТаблицаТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПоставщику КАК ТаблицаДокумента
	|		ПО ТаблицаТовары.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &ДокументОснование
	|	И СправочникНоменклатура.ВидПродукцииИС В (&ОсобенностьУчета)
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.ХарактеристикаНоменклатуры,
	|	ТаблицаДокумента.Склад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура              КАК Номенклатура,
	|	Товары.Характеристика            КАК Характеристика,
	|	Товары.Серия                     КАК Серия,
	|	Товары.СкладКонтрагент           КАК СкладКонтрагент,
	|	Товары.Количество                КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	Товары КАК Товары
	|;
	|";
	
	ЗаполнитьТабличнуюЧастьФормированиеПартийЗЕРНО(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента, 1);
	
КонецПроцедуры

Процедура ЗаполнитьФормированиеПартийЗЕРНОНаОснованииОприходованиеТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
КонецПроцедуры

#КонецОбласти

#Область ОформлениеСДИЗЗЕРНО

Процедура ЗаполнитьШапкуОформлениеСДИЗЗЕРНО(ДокументОбъект, ДанныеЗаполнения, Запрос)
	
	Если ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьШапку") Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ДокументОснование = ДанныеЗаполнения.Основание;
	Иначе
		ДокументОснование = ДанныеЗаполнения;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	ДокументОбъект.Товары.Очистить();
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	
	Если Не ЭтоПерезаполнение
			И Не ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьШапку") Тогда
		
		ДокументОбъект.СвязанныеДокументы.Очистить();
		ДокументОбъект.ТранспортныеСредства.Очистить();
		ДокументОбъект.СвязанныеДокументыПрочие.Очистить();
		
		ИнтеграцияИСУТ.ПроверитьВозможностьВводаНаОсновании(
			Реквизиты.ДокументОснование,,
			Реквизиты.ЕстьОшибкиПроведен,,);
		
		РеквизитыПеревозки   = "ПунктНазначения,ПунктНазначенияСтрокой,ПунктНазначенияСопоставленныйОбъект,ПунктОтправления";
		ИсключаемыеРеквизиты = Новый Массив;
		Для Каждого Имя Из СтрРазделить(РеквизитыПеревозки, ",") Цикл 
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Реквизиты, Имя) Тогда
				ИсключаемыеРеквизиты.Добавить(Имя);
			КонецЕсли;
		КонецЦикла;
		РеквизитыПеревозки = СтрСоединить(ИсключаемыеРеквизиты, ",");
		
		Если Не Реквизиты.Реализация Тогда
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Реквизиты, "Покупатель") Тогда
				ИсключаемыеРеквизиты.Добавить("Покупатель");
			КонецЕсли;
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Реквизиты, "НомерГПД") Тогда
				ИсключаемыеРеквизиты.Добавить("НомерГПД");
			КонецЕсли;
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Реквизиты, "ДатаГПД") Тогда
				ИсключаемыеРеквизиты.Добавить("ДатаГПД");
			КонецЕсли;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ДокументОбъект, Реквизиты,, СтрСоединить(ИсключаемыеРеквизиты, ","));
		
		Если ЗначениеЗаполнено(ДокументОбъект.ПунктНазначенияСтрокой) Тогда
			ДокументОбъект.Перевозка = Истина;
		КонецЕсли; 
		
		Если Реквизиты.Реализация Тогда
			НоваяСтрока              = ДокументОбъект.СвязанныеДокументы.Добавить();
			НоваяСтрока.ТипДокумента = Справочники.КлассификаторНСИЗЕРНО.ДокументПраваСобственностиТОРГ12;
			НоваяСтрока.Номер        = Реквизиты.НомерДокумента;
			НоваяСтрока.Номер        = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(НоваяСтрока.Номер);
			НоваяСтрока.Дата         = Реквизиты.ДатаДокумента;
		КонецЕсли;
			
		Если ДокументОбъект.Перевозка Тогда
		
			ЗаполнитьЗначенияСвойств(ДокументОбъект, Реквизиты, РеквизитыПеревозки);
			
			Если НЕ ЗначениеЗаполнено(ДокументОбъект.ПунктНазначенияСтрокой) 
				И НЕ ЗначениеЗаполнено(ДокументОбъект.ПунктНазначения) 
				И ЗначениеЗаполнено(ДокументОбъект.ПунктНазначенияСопоставленныйОбъект) Тогда
				
				ДанныеДляЗаполненияПунктаНазначения = ИнтеграцияЗЕРНОВызовСервера.ДанныеАдресаОбъекта(ДокументОбъект.ПунктНазначенияСопоставленныйОбъект);
	
				ДокументОбъект.ПунктНазначения        = ДанныеДляЗаполненияПунктаНазначения.Адрес;
				ДокументОбъект.ПунктНазначенияСтрокой = ДанныеДляЗаполненияПунктаНазначения.ПредставлениеАдреса;
				
			ИначеЕсли ЗначениеЗаполнено(ДокументОбъект.ПунктНазначенияСтрокой) И Не ЗначениеЗаполнено(ДокументОбъект.ПунктНазначения) Тогда
				
				ВидКонтактнойИнформации        = УправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации(Перечисления.ТипыКонтактнойИнформации.Адрес);	
				ДокументОбъект.ПунктНазначения = ИнтеграцияЗЕРНОВызовСервера.ЗначенияПолейКонтактнойИнформации(ДокументОбъект.ПунктНазначенияСтрокой, ВидКонтактнойИнформации);
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если ДокументОбъект.Реализация Тогда
			
			ДанныеСтатусаЭД = ОбменСКонтрагентами.СтатусДокументооборота(Реквизиты.ДокументОснование);
			
			Если ДанныеСтатусаЭД.Статус = "Утвержден" Или ДанныеСтатусаЭД.Статус = "ВОбработке" Тогда
				
				НоваяСтрока = ДокументОбъект.СвязанныеДокументы.Добавить();
				НоваяСтрока.ТипДокумента = Справочники.КлассификаторНСИЗЕРНО.ДокументПраваСобственностиУПД;
				
				РеквизитыЭлектронногоДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
					ДанныеСтатусаЭД.ЭлектронныйДокумент,
					"ДатаДокумента,НомерДокумента");
				
				НоваяСтрока.Номер = РеквизитыЭлектронногоДокумента.НомерДокумента;
				НоваяСтрока.Дата  = РеквизитыЭлектронногоДокумента.ДатаДокумента;
				
			КонецЕсли;
			
			СчетФактура = Документы.СчетФактураВыданный.НайтиПоРеквизиту("ДокументОснование",Реквизиты.ДокументОснование);
			Если ЗначениеЗаполнено(СчетФактура) Тогда
				
				НоваяСтрока = ДокументОбъект.СвязанныеДокументы.Добавить();
				НоваяСтрока.ТипДокумента = Справочники.КлассификаторНСИЗЕРНО.ДокументПраваСобственностиСчетФактура;
				РеквизитыСФ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СчетФактура, "Номер,Дата");
				НоваяСтрока.Номер        = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(РеквизитыСФ.Номер);
				НоваяСтрока.Дата         = РеквизитыСФ.Дата;
				
			КонецЕсли;
			
		КонецЕсли;                                     
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьТабличнуюЧастьОформлениеСДИЗЗЕРНО(ДокументОбъект, ДанныеЗаполнения, ИмяДокумента)
	
	Если ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьТабличнуюЧасть") Тогда
		Возврат;
	КонецЕсли;
	
	ФильтрВидПродукции = ИнтеграцияЗЕРНОКлиентСерверПовтИсп.УчитываемыеВидыПродукции();
	ОсобенностьУчета   = Новый Массив;
	Для Каждого ВидПродукции Из ФильтрВидПродукции Цикл
		ОсобенностьУчета.Добавить(ВидПродукции);
	КонецЦикла;
	Если ЗначениеЗаполнено(ДокументОбъект.ВидПродукции) Тогда
		ФильтрВидПродукции = ДокументОбъект.ВидПродукции;
		ОсобенностьУчета   = ФильтрВидПродукции;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Если ДанныеЗаполнения.ИмяФильтра = "ЗаполнениеСВидомПродукции" Тогда
			ФильтрВидПродукции = ДанныеЗаполнения.ЗначениеФильтра;
			ОсобенностьУчета   = ФильтрВидПродукции;
		КонецЕсли;
		Основание = ДанныеЗаполнения.Основание;
	Иначе
		Основание = ДанныеЗаполнения;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаЗаполненияОформлениеСДИЗЗЕРНОИзПрикладногоДокумента(ИмяДокумента);
	
	Запрос.УстановитьПараметр("ЭтаСсылка",         ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", Основание);
	Запрос.УстановитьПараметр("ПустаяСерия",       ОпределяемыеТипы().СерияНоменклатуры.Тип.ПривестиЗначение());
	Запрос.УстановитьПараметр("КонечныеСтатусы",   Документы.ОформлениеСДИЗЗЕРНО.КонечныеСтатусы());
	Запрос.УстановитьПараметр("ОсобенностьУчета",  ОсобенностьУчета);
	
	КолонкиКоличестваЗЕРНО = Новый Структура;
	КолонкиКоличестваЗЕРНО.Вставить("Количество", "КоличествоЗЕРНО");
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ДобавленныеСтроки", Новый Массив);
	
	ЗаполнитьТабличнуюЧастьДокумента(ДокументОбъект.Товары, Запрос.Выполнить(), ДанныеЗаполнения, КолонкиКоличестваЗЕРНО,,ДополнительныеПараметры);
	
	Если ТипЗнч(ОсобенностьУчета) = Тип("Массив") Тогда
		ИнтеграцияИС.ЗаполнитьВидПродукцииПоТабличнойЧасти(ДокументОбъект);
	Иначе
		ДокументОбъект.ВидПродукции = ФильтрВидПродукции;
	КонецЕсли;
	
	СписокСтрок = Новый Массив();
	Для Каждого СтрокаТаблицы Из ДокументОбъект.Товары Цикл
		СписокСтрок.Добавить(СтрокаТаблицы);
	КонецЦикла;
	
	Документы.ОформлениеСДИЗЗЕРНО.ЗаполнитьПартии(ДокументОбъект, СписокСтрок);
	
	//
	
	НоменклатураСертификатов = Новый Массив();
	СтрокиПоНоменклатурам    = Новый Соответствие();
	
	Для Каждого СтрокаТаблицы Из ДополнительныеПараметры.ДобавленныеСтроки Цикл
		Если ЗначениеЗаполнено(СтрокаТаблицы.Номенклатура) Тогда
			НоменклатураСертификатов.Добавить(СтрокаТаблицы.Номенклатура);
			СтрокиПоНоменклатуре = СтрокиПоНоменклатурам[СтрокаТаблицы.Номенклатура];
			Если СтрокиПоНоменклатуре = Неопределено Тогда
				СтрокиПоНоменклатуре = Новый Массив();
				СтрокиПоНоменклатурам[СтрокаТаблицы.Номенклатура] = СтрокиПоНоменклатуре;
			КонецЕсли;
			СтрокиПоНоменклатуре.Добавить(СтрокаТаблицы);
		КонецЕсли;
	КонецЦикла;
	
	СертификатыНоменклатуры = Новый Соответствие;
	ИнтеграцияИСПереопределяемый.ПриЗаполненииСертификатовНоменклатуры(НоменклатураСертификатов, СертификатыНоменклатуры);
	
	Если СертификатыНоменклатуры.Количество() Тогда
		
		ДобавленныеТипыПоСтрокам = Новый Соответствие();
		
		Для Каждого КлючИЗначение Из СертификатыНоменклатуры Цикл
			
			Для Каждого СтрокаТовары Из СтрокиПоНоменклатурам[КлючИЗначение.Ключ] Цикл
				
				Для Каждого ДанныеСертификата Из КлючИЗначение.Значение Цикл
					
					Если Не ДанныеСертификата.Бессрочный
						И ЗначениеЗаполнено(ДанныеСертификата.СрокДействия)
						И ДанныеСертификата.СрокДействия < ТекущаяДатаСеанса() Тогда
						Продолжить;
					КонецЕсли;
					
					СтруктураПоиска = Новый Структура;
					СтруктураПоиска.Вставить("ИдентификаторСтрокиТоваров", СтрокаТовары.Идентификатор);
					СтруктураПоиска.Вставить("ТипДокумента",               Неопределено);
					СтруктураПоиска.Вставить("Номер",                      ДанныеСертификата.НомерСертификации);
					СтруктураПоиска.Вставить("Дата",                       ДанныеСертификата.ДатаСертификации);
					СтруктураПоиска.Вставить("СрокДействия",               ДанныеСертификата.СрокДействия);
					
					Если ДанныеСертификата.ВидСертификации = Перечисления.ВидыДокументовОбязательнойСертификацииИС.СертификатСоответствия Тогда
						СтруктураПоиска.ТипДокумента = Справочники.КлассификаторНСИЗЕРНО.ДокументНаПартиюФитосанитарныйСертификат;
					ИначеЕсли ДанныеСертификата.ВидСертификации = Перечисления.ВидыДокументовОбязательнойСертификацииИС.ДекларацияСоответствия Тогда
						СтруктураПоиска.ТипДокумента = Справочники.КлассификаторНСИЗЕРНО.ДокументНаПартиюДекларацияСоответствия;
					КонецЕсли;
					
					ДобавленныеТипыПоСтроке = ДобавленныеТипыПоСтрокам[СтрокаТовары];
					Если ДобавленныеТипыПоСтроке = Неопределено Тогда
						ДобавленныеТипыПоСтроке = Новый Массив();
						ДобавленныеТипыПоСтрокам[СтрокаТовары] = ДобавленныеТипыПоСтроке;
					КонецЕсли;
					
					Если ДобавленныеТипыПоСтроке.Найти(СтруктураПоиска.ТипДокумента) = Неопределено
						И ДокументОбъект.ДокументыСертификации.НайтиСтроки(СтруктураПоиска).Количество() = 0 Тогда
							
						НоваяСтрока = ДокументОбъект.ДокументыСертификации.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, СтруктураПоиска);
						ДобавленныеТипыПоСтроке.Добавить(СтруктураПоиска.ТипДокумента);
						
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЦикла;
	
	КонецЕсли;
	
КонецПроцедуры

Функция ТекстЗапросаЗаполненияОформлениеСДИЗЗЕРНОИзПрикладногоДокумента(ИмяДокумента)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура      КАК Номенклатура,
	|	ТаблицаТовары.ХарактеристикаНоменклатуры    КАК Характеристика,
	|	ТаблицаТовары.СерияНоменклатуры             КАК Серия,
	|	СУММА(ТаблицаТовары.Количество * ВЫБОР КОГДА ТаблицаТовары.Коэффициент = 0 ТОГДА 1 ИНАЧЕ ТаблицаТовары.Коэффициент КОНЕЦ) КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	&ОписаниеДокументаТовары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТаблицаТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &ДокументОснование
	|	И СправочникНоменклатура.ВидПродукцииИС В (&ОсобенностьУчета)
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.ХарактеристикаНоменклатуры,
	|	ТаблицаТовары.СерияНоменклатуры
	|";
		
	ОписаниеДокумента = СтрШаблон("Документ.%1", ИмяДокумента);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&ОписаниеДокументаТовары", СтрШаблон("%1.%2", ОписаниеДокумента,"Товары"));
	
	ТекстЗапроса = ТекстЗапроса + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура   КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Серия          КАК Серия,
	|	Товары.Количество     КАК Количество
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	ТоварыСерии КАК Товары
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура,
	|	ОформленныеТовары.Характеристика,
	|	ОформленныеТовары.Серия,
	|	-ОформленныеТовары.Количество
	|ИЗ
	|	Документ.ОформлениеСДИЗЗЕРНО.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка.Проведен
	|	И ОформленныеТовары.Ссылка.ДокументОснование = &ДокументОснование
	|	И ОформленныеТовары.Ссылка <> &ЭтаСсылка
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура      КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика    КАК Характеристика,
	|	ТоварыКОформлению.Серия             КАК Серия,
	|	ОписаниеНоменклатуры.КодТНВЭД.Код   КАК КодТНВЭД,
	|	ОписаниеНоменклатуры.ОКПД2.Код      КАК ОКПД2,
	|	СУММА(ТоварыКОформлению.Количество) КАК Количество,
	|	СУММА(ТоварыКОформлению.Количество) КАК КоличествоУпаковок
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ОписаниеНоменклатуры
	|	ПО ТоварыКОформлению.Номенклатура = ОписаниеНоменклатуры.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	ОписаниеНоменклатуры.КодТНВЭД.Код,
	|	ОписаниеНоменклатуры.ОКПД2.Код
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0
	|;
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ЗаполнитьОформлениеСДИЗЗЕРНОНаОснованииРеализацииТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка                        КАК ДокументОснование,
	|	НЕ ДокументСсылка.Проведен                   КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Организация                   КАК Организация,
	|	ДокументСсылка.Грузоотправитель              КАК Грузоотправитель,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Контрагенты.СтранаРегистрации, ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия)) = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия)
	|			ТОГДА ДокументСсылка.Грузополучатель
	|		КОГДА Контрагенты.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.ПустаяСсылка)
	|			ТОГДА ДокументСсылка.Грузополучатель
	|		ИНАЧЕ ДокументСсылка.Контрагент
	|	КОНЕЦ                                        КАК Грузополучатель,
	|	ДокументСсылка.Контрагент                    КАК Покупатель,
	|	ДокументСсылка.АдресДоставки                 КАК ПунктНазначенияСтрокой,
	|	ДокументСсылка.Контрагент                    КАК ПунктНазначенияСопоставленныйОбъект,
	|	ДокументСсылка.Склад                         КАК ПунктОтправления,
	|	ДокументСсылка.Дата                          КАК ДатаДокумента,
	|	ДокументСсылка.Номер                         КАК НомерДокумента,
	|	ДоговорыКонтрагентов.Дата                    КАК ДатаГПД,
	|	ДоговорыКонтрагентов.Номер                   КАК НомерГПД,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Контрагенты.СтранаРегистрации, ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия)) = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗЕРНО.ОформлениеСДИЗРФ)
	|		КОГДА Контрагенты.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗЕРНО.ОформлениеСДИЗРФ)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗЕРНО.ОформлениеСДИЗЭкспорт)
	|	КОНЕЦ                                        КАК Операция,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Контрагенты.СтранаРегистрации, ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия)) = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия)
	|			ТОГДА ИСТИНА
	|		КОГДА Контрагенты.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                                        КАК Реализация,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Контрагенты.СтранаРегистрации, ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия)) = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия)
	|			ТОГДА ЛОЖЬ
	|		КОГДА Контрагенты.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.ПустаяСсылка)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ                                        КАК Перевозка
	|ИЗ
	|	Документ.РеализацияТоваровУслуг              КАК ДокументСсылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты      КАК Контрагенты
	|	ПО Контрагенты.Ссылка = ДокументСсылка.Контрагент
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|	ПО ДокументСсылка.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|	
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуОформлениеСДИЗЗЕРНО(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ЗаполнитьТабличнуюЧастьОформлениеСДИЗЗЕРНО(ДокументОбъект, ДанныеЗаполнения, "РеализацияТоваровУслуг");
	
КонецПроцедуры

Процедура ЗаполнитьОформлениеСДИЗЗЕРНОНаОснованииВозвратаТоваровПоставщику(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка                        КАК ДокументОснование,
	|	НЕ ДокументСсылка.Проведен                   КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Организация                   КАК Организация,
	|	ДокументСсылка.Грузоотправитель              КАК Грузоотправитель,
	|	ДокументСсылка.Грузополучатель               КАК Грузополучатель,
	|	ДокументСсылка.Контрагент                    КАК Покупатель,
	|	ДокументСсылка.Контрагент                    КАК ПунктНазначенияСопоставленныйОбъект,
	|	ДокументСсылка.Склад                         КАК ПунктОтправления,
	|	ДокументСсылка.Дата                          КАК ДатаДокумента,
	|	ДокументСсылка.Номер                         КАК НомерДокумента,
	|	ДоговорыКонтрагентов.Дата                    КАК ДатаГПД,
	|	ДоговорыКонтрагентов.Номер                   КАК НомерГПД,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Контрагенты.СтранаРегистрации, ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия)) = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗЕРНО.ОформлениеСДИЗРФ)
	|		КОГДА Контрагенты.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗЕРНО.ОформлениеСДИЗРФ)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗЕРНО.ОформлениеСДИЗЭкспорт)
	|	КОНЕЦ                                     КАК Операция,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Контрагенты.СтранаРегистрации, ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия)) = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия)
	|			ТОГДА ИСТИНА
	|		КОГДА Контрагенты.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                                     КАК Реализация,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Контрагенты.СтранаРегистрации, ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия)) = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия)
	|			ТОГДА ЛОЖЬ
	|		КОГДА Контрагенты.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.ПустаяСсылка)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ                                     КАК Перевозка
	|ИЗ
	|	Документ.ВозвратТоваровПоставщику КАК ДокументСсылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|	ПО Контрагенты.Ссылка = ДокументСсылка.Контрагент
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|	ПО ДокументСсылка.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|	
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуОформлениеСДИЗЗЕРНО(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ЗаполнитьТабличнуюЧастьОформлениеСДИЗЗЕРНО(ДокументОбъект, ДанныеЗаполнения, "ВозвратТоваровПоставщику");
	
КонецПроцедуры

Процедура ЗаполнитьОформлениеСДИЗЗЕРНОНаОснованииПеремещенияТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка                        КАК ДокументОснование,
	|	НЕ ДокументСсылка.Проведен                   КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Организация                   КАК Организация,
	|	ДокументСсылка.ПодразделениеОрганизации      КАК Подразделение,
	|	ДокументСсылка.Организация                   КАК Грузоотправитель,
	|	ДокументСсылка.Организация                   КАК Грузополучатель,
	|	ДокументСсылка.Организация                   КАК Покупатель,
	|	ДокументСсылка.СкладПолучатель               КАК ПунктНазначенияСопоставленныйОбъект,
	|	ДокументСсылка.СкладОтправитель              КАК ПунктОтправления,
	|	ДокументСсылка.Дата                          КАК ДатаДокумента,
	|	ДокументСсылка.Номер                         КАК НомерДокумента,
	|	ДАТАВРЕМЯ(1, 1, 1)                           КАК ДатаГПД,
	|	""""                                         КАК НомерГПД,
	|	ЛОЖЬ                                         КАК ГосударственныйКонтракт,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗЕРНО.ОформлениеСДИЗРФ) КАК Операция,
	|	ЛОЖЬ                                      КАК Реализация,
	|	ИСТИНА                                    КАК Перевозка
	|ИЗ
	|	Документ.ПеремещениеТоваров КАК ДокументСсылка
	|	
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуОформлениеСДИЗЗЕРНО(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ЗаполнитьТабличнуюЧастьОформлениеСДИЗЗЕРНО(ДокументОбъект, ДанныеЗаполнения, "ПеремещениеТоваров");
	
КонецПроцедуры

#КонецОбласти

#Область ВнесениеСведенийОСобранномУрожаеЗЕРНО

Процедура ЗаполнитьШапкуВнесениеСведенийОСобранномУрожаеЗЕРНО(ДокументОбъект, ДанныеЗаполнения, Запрос)
	
	Если ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьШапку") Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ДокументОснование = ДанныеЗаполнения.Основание;
	Иначе
		ДокументОснование = ДанныеЗаполнения;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	
	Если Не ЭтоПерезаполнение
		И Не ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьШапку") Тогда
		
		ИнтеграцияИСУТ.ПроверитьВозможностьВводаНаОсновании(
			Реквизиты.ДокументОснование,,
			Реквизиты.ЕстьОшибкиПроведен,,);
		
		ЗаполнитьЗначенияСвойств(ДокументОбъект, Реквизиты);
		
		ДокументОбъект.ДатаСбораУрожая = Реквизиты.Дата;
	
		КонтактнаяИнформация = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Реквизиты.СкладКонтрагент, Перечисления.ТипыКонтактнойИнформации.Адрес, Реквизиты.Дата, Ложь);
		
		Если КонтактнаяИнформация.Количество() > 0 Тогда
			ДокументОбъект.МестоХранения = КонтактнаяИнформация[0].Значение;
			ДокументОбъект.МестоХраненияСтрокой = КонтактнаяИнформация[0].Представление;
		КонецЕсли;
		
	КонецЕсли;
	
	
КонецПроцедуры

Процедура ЗаполнитьТабличнуюЧастьВнесениеСведенийОСобранномУрожаеЗЕРНО(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента, ДопустимыйЗнак)
	
	Если ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьТабличнуюЧасть") Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Основание = ДанныеЗаполнения.Основание;
	Иначе
		Основание = ДанныеЗаполнения;
	КонецЕсли;
	
	ДокументОбъект.Товары.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаЗаполненияВнесениеСведенийОСобранномУрожаеЗЕРНОИзПрикладногоДокумента(ТекстТоварыДокумента, ДопустимыйЗнак);
	
	Запрос.УстановитьПараметр("ЭтаСсылка",         ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", Основание);
	Запрос.УстановитьПараметр("ПустаяСерия",       ОпределяемыеТипы().СерияНоменклатуры.Тип.ПривестиЗначение());
	
	КолонкиКоличестваЗЕРНО = Новый Структура;
	КолонкиКоличестваЗЕРНО.Вставить("Количество", "КоличествоЗЕРНО");
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ДокументОбъект", ДокументОбъект);
	
	ЗаполнитьТабличнуюЧастьДокумента(ДокументОбъект.Товары, Запрос.Выполнить(), ДанныеЗаполнения, КолонкиКоличестваЗЕРНО,, ДополнительныеПараметры);
	
КонецПроцедуры

Функция ТекстЗапросаЗаполненияВнесениеСведенийОСобранномУрожаеЗЕРНОИзПрикладногоДокумента(ТекстТоварыДокумента, ДопустимыйЗнак)
	Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ВнесениеСведенийОСобранномУрожаеЗЕРНО КАК ТаблицаДокументы
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|;"
	+
	ТекстТоварыДокумента
	+
	"
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыСерии.Номенклатура    КАК Номенклатура,
	|	ТоварыСерии.Характеристика  КАК Характеристика,
	|	ТоварыСерии.Серия           КАК Серия,
	|	ТоварыСерии.СкладКонтрагент КАК СкладКонтрагент,
	|	ТоварыСерии.Количество      КАК Количество
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	ТоварыСерии КАК ТоварыСерии
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура           КАК Номенклатура,
	|	ОформленныеТовары.Характеристика         КАК Характеристика,
	|	ОформленныеТовары.Серия                  КАК Серия,
	|	ОформленныеТовары.Ссылка.СкладКонтрагент КАК СкладКонтрагент,
	|	-ОформленныеТовары.Количество            КАК Количество
	|ИЗ
	|	Документ.ВнесениеСведенийОСобранномУрожаеЗЕРНО.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В
	|		(ВЫБРАТЬ
	|			Т.Ссылка
	|		ИЗ
	|			ОформленныеДокументы КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура      КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика    КАК Характеристика,
	|	ТоварыКОформлению.Серия             КАК Серия,
	|	СУММА(ТоварыКОформлению.Количество) КАК Количество,
	|	СправочникНоменклатура.КодТНВЭД.Код КАК ТНВЭД,
	|	СправочникНоменклатура.ОКПД2.Код КАК ОКПД2
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТоварыКОформлению.Номенклатура = СправочникНоменклатура.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	СправочникНоменклатура.КодТНВЭД.Код,
	|	СправочникНоменклатура.ОКПД2.Код
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0";
	
	Возврат Текст;
	
КонецФункции

Процедура ЗаполнитьВнесениеСведенийОСобранномУрожаеЗЕРНОНаОснованииОтчетПроизводстваЗаСмену(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка        КАК ДокументОснование,
	|	НЕ ДокументСсылка.Проведен   КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Дата          КАК Дата,
	|	ДокументСсылка.ПодразделениеОрганизации КАК Подразделение,
	|	ДокументСсылка.Склад КАК СкладКонтрагент,
	|	ДокументСсылка.Организация   КАК Организация
	|ИЗ
	|	Документ.ОтчетПроизводстваЗаСмену КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуВнесениеСведенийОСобранномУрожаеЗЕРНО(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура      КАК Номенклатура,
	|	ТабличнаяЧасть.ХарактеристикаНоменклатуры    КАК Характеристика,
	|	ТабличнаяЧасть.СерияНоменклатуры             КАК Серия,
	|	СУММА(ТабличнаяЧасть.Количество * ВЫБОР КОГДА ТабличнаяЧасть.Коэффициент = 0 ТОГДА 1 ИНАЧЕ ТабличнаяЧасть.Коэффициент КОНЕЦ) КАК Количество
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	Документ.ОтчетПроизводстваЗаСмену.Продукция КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И СправочникНоменклатура.ВидПродукцииИС = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Зерно)
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.ХарактеристикаНоменклатуры,
	|	ТабличнаяЧасть.СерияНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура    КАК Номенклатура,
	|	Товары.Характеристика  КАК Характеристика,
	|	Товары.Серия           КАК Серия,
	|	Товары.Количество      КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	Товары КАК Товары
	|;
	|";
	
	ЗаполнитьТабличнуюЧастьВнесениеСведенийОСобранномУрожаеЗЕРНО(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента, -1);
	
КонецПроцедуры

#КонецОбласти

#Область СписаниеПартийЗЕРНО

Процедура ЗаполнитьШапкуСписаниеПартийЗЕРНО(ДокументОбъект, ДанныеЗаполнения, Запрос)
	
	Если ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьШапку") Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ДокументОснование = ДанныеЗаполнения.Основание;
	Иначе
		ДокументОснование = ДанныеЗаполнения;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ДокументОснование",                 ДокументОснование);
	Запрос.УстановитьПараметр("ПричинаСписанияУтилизация",         Справочники.КлассификаторНСИЗЕРНО.ПричинаСписанияУтилизация);
	Запрос.УстановитьПараметр("ПричинаСписанияРеализацияФизЛицам", Справочники.КлассификаторНСИЗЕРНО.ПричинаСписанияРеализацияФизЛицам);
	Запрос.УстановитьПараметр("ПричинаСписанияИспорчено",          Справочники.КлассификаторНСИЗЕРНО.ПричинаСписанияИспорчено);
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	
	Если Не ЭтоПерезаполнение
		И Не ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьШапку") Тогда
		
		ДокументОбъект.Товары.Очистить();
		
		ИнтеграцияИСУТ.ПроверитьВозможностьВводаНаОсновании(
			Реквизиты.ДокументОснование,,
			Реквизиты.ЕстьОшибкиПроведен,,);
		
		ЗаполнитьЗначенияСвойств(ДокументОбъект, Реквизиты);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьТабличнуюЧастьСписаниеПартийЗЕРНО(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента, ДопустимыйЗнак)
	
	Если ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьТабличнуюЧасть") Тогда
		Возврат;
	КонецЕсли;
	
	ФильтрВидПродукции = ИнтеграцияЗЕРНОКлиентСерверПовтИсп.УчитываемыеВидыПродукции();
	ОсобенностьУчета   = Новый Массив;
	
	Для Каждого ВидПродукции Из ФильтрВидПродукции Цикл
		ОсобенностьУчета.Добавить(ВидПродукции);
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ДокументОбъект.ВидПродукции) Тогда
		ФильтрВидПродукции = ДокументОбъект.ВидПродукции;
		ОсобенностьУчета   = ФильтрВидПродукции;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Если ДанныеЗаполнения.ИмяФильтра = "ЗаполнениеСВидомПродукции" Тогда
			ФильтрВидПродукции = ДанныеЗаполнения.ЗначениеФильтра;
			ОсобенностьУчета   = ФильтрВидПродукции;
		КонецЕсли;
		Основание = ДанныеЗаполнения.Основание;
	Иначе
		Основание = ДанныеЗаполнения;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаЗаполненияСписаниеПартийЗЕРНОИзПрикладногоДокумента(ТекстТоварыДокумента, ДопустимыйЗнак);
	
	Запрос.УстановитьПараметр("ЭтаСсылка",         ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", Основание);
	Запрос.УстановитьПараметр("ПустаяСерия",       ИнтеграцияИС.ПустоеЗначениеОпределяемогоТипа("СерияНоменклатуры"));
	Запрос.УстановитьПараметр("ОсобенностьУчета",  ОсобенностьУчета);
	
	КолонкиКоличестваЗЕРНО = Новый Структура;
	КолонкиКоличестваЗЕРНО.Вставить("Количество", "КоличествоЗЕРНО");
	
	ДополнительныеПараметры = Новый Структура();
	
	ЗаполнитьТабличнуюЧастьДокумента(ДокументОбъект.Товары, Запрос.Выполнить(), ДанныеЗаполнения, КолонкиКоличестваЗЕРНО,,ДополнительныеПараметры);
		
	Если ТипЗнч(ОсобенностьУчета) = Тип("Массив") Тогда
		ИнтеграцияИС.ЗаполнитьВидПродукцииПоТабличнойЧасти(ДокументОбъект);
	Иначе
		ДокументОбъект.ВидПродукции = ФильтрВидПродукции;
	КонецЕсли;
	
	СписокСтрок = Новый Массив();
	Для Каждого СтрокаТаблицы Из ДокументОбъект.Товары Цикл
		СписокСтрок.Добавить(СтрокаТаблицы);
	КонецЦикла;
	
	Документы.СписаниеПартийЗЕРНО.ЗаполнитьПартии(ДокументОбъект, СписокСтрок);
	
КонецПроцедуры

Функция ТекстЗапросаЗаполненияСписаниеПартийЗЕРНОИзПрикладногоДокумента(ТекстТоварыДокумента, ДопустимыйЗнак)
	
	Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.СписаниеПартийЗЕРНО КАК ТаблицаДокументы
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|;"
	
	+
	ТекстТоварыДокумента
	+
	
	"
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыСерии.Номенклатура    КАК Номенклатура,
	|	ТоварыСерии.Характеристика  КАК Характеристика,
	|	ТоварыСерии.Серия           КАК Серия,
	|	ТоварыСерии.Склад           КАК Склад,
	|	ТоварыСерии.Количество      КАК Количество
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	ТоварыСерии КАК ТоварыСерии
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура   КАК Номенклатура,
	|	ОформленныеТовары.Характеристика КАК Характеристика,
	|	ОформленныеТовары.Серия          КАК Серия,
	|	ОформленныеТовары.Ссылка.Склад   КАК Склад,
	|	-ОформленныеТовары.Количество    КАК Количество
	|ИЗ
	|	Документ.СписаниеПартийЗЕРНО.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В
	|		(ВЫБРАТЬ
	|			Т.Ссылка
	|		ИЗ
	|			ОформленныеДокументы КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура        КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика      КАК Характеристика,
	|	ТоварыКОформлению.Серия               КАК Серия,
	|	ОписаниеНоменклатуры.КодТНВЭД.Код         КАК КодТНВЭД,
	|	ОписаниеНоменклатуры.ОКПД2.Код         КАК ОКПД2,
	|	ОписаниеНоменклатуры.ВидПродукцииИС КАК ОсобенностьУчета,
	|	ТоварыКОформлению.Склад               КАК Склад,
	|	СУММА(ТоварыКОформлению.Количество)   КАК Количество
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ОписаниеНоменклатуры
	|		ПО ТоварыКОформлению.Номенклатура = ОписаниеНоменклатуры.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	ОписаниеНоменклатуры.КодТНВЭД.Код,
	|	ОписаниеНоменклатуры.ОКПД2.Код,
	|	ТоварыКОформлению.Склад,
	|	ОписаниеНоменклатуры.ВидПродукцииИС
	|
	|ИМЕЮЩИЕ СУММА(ТоварыКОформлению.Количество) > 0
	|;
	|";
	
	Если СтрНайти(ТекстТоварыДокумента, "Склад") = 0 Тогда
		
		УдаляемыеСтроки = СтрРазделить("ТоварыСерии.Склад           КАК Склад,;"
		+"ОформленныеТовары.Ссылка.Склад   КАК Склад,;"
		+"ТоварыКОформлению.Склад               КАК Склад,;"
		+"ТоварыКОформлению.Склад,;", ";");
		
		Для Каждого Строка Из УдаляемыеСтроки Цикл
			Текст = СтрЗаменить(Текст, Строка, "");
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Текст;
	
КонецФункции

Процедура ЗаполнитьСписаниеПартийЗЕРНОНаОснованииТребованиеНакладная(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка          КАК ДокументОснование,
	|	НЕ ДокументСсылка.Проведен     КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Организация     КАК Организация,
	|	ДокументСсылка.ПодразделениеОрганизации   КАК Подразделение,
	|	ДокументСсылка.Склад           КАК Склад,
	|	&ПричинаСписанияУтилизация     КАК ПричинаСписания
	|ИЗ
	|	Документ.ТребованиеНакладная КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуСписаниеПартийЗЕРНО(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура                                КАК Номенклатура,
	|	Товары.ХарактеристикаНоменклатуры                              КАК Характеристика,
	|	Товары.СерияНоменклатуры                                       КАК Серия,
	|	СУММА(Товары.Количество * ВЫБОР КОГДА Товары.Коэффициент = 0 ТОГДА 1 ИНАЧЕ Товары.Коэффициент КОНЕЦ) КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	Документ.ТребованиеНакладная.Материалы КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И СправочникНоменклатура.ВидПродукцииИС В (&ОсобенностьУчета)
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.ХарактеристикаНоменклатуры,
	|	Товары.СерияНоменклатуры
	|;
	|";
	
	ЗаполнитьТабличнуюЧастьСписаниеПартийЗЕРНО(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента, -1);
	
КонецПроцедуры

Процедура ЗаполнитьСписаниеПартийЗЕРНОНаОснованииСписанияТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка         КАК ДокументОснование,
	|	НЕ ДокументСсылка.Проведен    КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Организация    КАК Организация,
	|	ДокументСсылка.Склад          КАК Склад,
	|	&ПричинаСписанияУтилизация    КАК ПричинаСписания
	|ИЗ
	|	Документ.СписаниеТоваров КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуСписаниеПартийЗЕРНО(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура                                КАК Номенклатура,
	|	Товары.ХарактеристикаНоменклатуры                              КАК Характеристика,
	|	Товары.СерияНоменклатуры                                       КАК Серия,
	|	СУММА(Товары.Количество * ВЫБОР КОГДА Товары.Коэффициент = 0 ТОГДА 1 ИНАЧЕ Товары.Коэффициент КОНЕЦ) КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	Документ.СписаниеТоваров.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И СправочникНоменклатура.ВидПродукцииИС В (&ОсобенностьУчета)
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.ХарактеристикаНоменклатуры,
	|	Товары.СерияНоменклатуры
	|
	|ИМЕЮЩИЕ СУММА(Товары.Количество) > 0
	|;
	|";
	
	ЗаполнитьТабличнуюЧастьСписаниеПартийЗЕРНО(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента, -1);
	
КонецПроцедуры

Процедура ЗаполнитьСписаниеПартийЗЕРНОНаОснованииОтчетПроизводстваЗаСмену(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка        КАК ДокументОснование,
	|	НЕ ДокументСсылка.Проведен   КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Организация   КАК Организация,
	|	ДокументСсылка.ПодразделениеОрганизации КАК Подразделение,
	|	ДокументСсылка.Склад КАК СкладКонтрагент
	|ИЗ
	|	Документ.ОтчетПроизводстваЗаСмену КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуСписаниеПартийЗЕРНО(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура      КАК Номенклатура,
	|	Товары.ХарактеристикаНоменклатуры    КАК Характеристика,
	|	Товары.СерияНоменклатуры             КАК Серия,
	|	СУММА(Товары.Количество * ВЫБОР КОГДА Товары.Коэффициент = 0 ТОГДА 1 ИНАЧЕ Товары.Коэффициент КОНЕЦ) КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	Документ.ОтчетПроизводстваЗаСмену.Материалы КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И СправочникНоменклатура.ВидПродукцииИС В (&ОсобенностьУчета)
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.ХарактеристикаНоменклатуры,
	|	Товары.СерияНоменклатуры
	|ИМЕЮЩИЕ
	|	СУММА(Товары.Количество) > 0
	|;
	|";
	
	ЗаполнитьТабличнуюЧастьСписаниеПартийЗЕРНО(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента, -1);
	
КонецПроцедуры

Процедура ЗаполнитьСписаниеПартийЗЕРНОНаОснованииКомплектацияНоменклатуры(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка        КАК ДокументОснование,
	|	НЕ ДокументСсылка.Проведен   КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Организация   КАК Организация,
	|	ДокументСсылка.Подразделение КАК Подразделение
	|ИЗ
	|	Документ.КомплектацияНоменклатуры КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуСписаниеПартийЗЕРНО(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура      КАК Номенклатура,
	|	Товары.ХарактеристикаНоменклатуры    КАК Характеристика,
	|	Товары.СерияНоменклатуры             КАК Серия,
	|	СУММА(Товары.Количество * ВЫБОР КОГДА Товары.Коэффициент = 0 ТОГДА 1 ИНАЧЕ Товары.Коэффициент КОНЕЦ) КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	Документ.КомплектацияНоменклатуры.Комплектующие КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И Товары.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Комплектация)
	|	И СправочникНоменклатура.ВидПродукцииИС В (&ОсобенностьУчета)
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.ХарактеристикаНоменклатуры,
	|	Товары.СерияНоменклатуры
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.Номенклатура      КАК Номенклатура,
	|	Товары.ХарактеристикаНоменклатуры    КАК Характеристика,
	|	Товары.СерияНоменклатуры             КАК Серия,
	|	СУММА(Товары.Количество * ВЫБОР КОГДА Товары.Коэффициент = 0 ТОГДА 1 ИНАЧЕ Товары.Коэффициент КОНЕЦ) КАК Количество
	|ИЗ
	|	Документ.КомплектацияНоменклатуры КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И Товары.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Разукомплектация)
	|	И СправочникНоменклатура.ВидПродукцииИС В (&ОсобенностьУчета)
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.ХарактеристикаНоменклатуры,
	|	Товары.СерияНоменклатуры
	|ИМЕЮЩИЕ
	|	СУММА(Товары.Количество) > 0
	|;
	|";
	
	ЗаполнитьТабличнуюЧастьСписаниеПартийЗЕРНО(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента, -1);
	
КонецПроцедуры

Процедура ЗаполнитьСписаниеПартийЗЕРНОНаОснованииРеализацияТоваровУслуг(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) 
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка              КАК ДокументОснование,
	|	НЕ ДокументСсылка.Проведен         КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Организация         КАК Организация,
	|	ДокументСсылка.Склад               КАК Склад,
	|	&ПричинаСписанияРеализацияФизЛицам КАК ПричинаСписания
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуСписаниеПартийЗЕРНО(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура                                КАК Номенклатура,
	|	Товары.ХарактеристикаНоменклатуры                              КАК Характеристика,
	|	Товары.СерияНоменклатуры                                       КАК Серия,
	|	СУММА(Товары.Количество * ВЫБОР КОГДА Товары.Коэффициент = 0 ТОГДА 1 ИНАЧЕ Товары.Коэффициент КОНЕЦ)                           КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И СправочникНоменклатура.ВидПродукцииИС В (&ОсобенностьУчета)
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.ХарактеристикаНоменклатуры,
	|	Товары.СерияНоменклатуры
	|
	|ИМЕЮЩИЕ СУММА(Товары.Количество) > 0
	|;
	|";
	
	ЗаполнитьТабличнуюЧастьСписаниеПартийЗЕРНО(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента, -1);	
	
КонецПроцедуры

#КонецОбласти

#Область ФормированиеПартийПриПроизводствеЗЕРНО

Процедура ЗаполнитьШапкуФормированиеПартийПриПроизводствеЗЕРНО(ДокументОбъект, ДанныеЗаполнения, Запрос)
	
	Если ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьШапку") Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ДокументОснование = ДанныеЗаполнения.Основание;
	Иначе
		ДокументОснование = ДанныеЗаполнения;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Запрос.УстановитьПараметр("ЭтаСсылка",         ДокументОбъект.Ссылка);
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Если Не Реквизиты.Следующий() Тогда
		ВызватьИсключение НСтр("ru = 'Нет данных для заполнения'");
	КонецЕсли;
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	
	Если Не ЭтоПерезаполнение
		И Не ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьШапку") Тогда
		
		ИнтеграцияИСУТ.ПроверитьВозможностьВводаНаОсновании(
			Реквизиты.ДокументОснование,,
			Реквизиты.ЕстьОшибкиПроведен,,);
		
		ЗаполнитьЗначенияСвойств(ДокументОбъект, Реквизиты);
		
		ДокументОбъект.НазначениеПартии = Справочники.КлассификаторНСИЗЕРНО.НазначениеПартииХранениеОбработка;
		
		Если ЗначениеЗаполнено(ДокументОбъект.Номенклатура) Тогда
			МассивДляПересчета = Новый Массив;
			СтруктураШапки = Новый Структура("Номенклатура", ДокументОбъект.Номенклатура);
			МассивДляПересчета.Добавить(СтруктураШапки);
			ДанныеДляПересчета = КоэффициентыЕдиницИзмеренияЗЕРНО(МассивДляПересчета);
			ДанныеЕдиницыИзмерения = ДанныеДляПересчета.Получить(ДокументОбъект.Номенклатура);
			
			Если ДанныеЕдиницыИзмерения.КодОшибки <> 0 Тогда
				ДокументОбъект.КоличествоЗЕРНО = Неопределено;
			КонецЕсли;
			
			ДокументОбъект.КоличествоЗЕРНО = ДокументОбъект.Количество / ДанныеЕдиницыИзмерения.Коэффициент;
			
			ПотребительскиеСвойстваПоОДКПД2 = ИнтеграцияЗЕРНО.ПотребительскиеСвойстваПродукцииПоДаннымОКПД2(ДокументОбъект.ОКПД2);
			
			Для Каждого ЭлементДанных Из ПотребительскиеСвойстваПоОДКПД2 Цикл
				СтрокаТЧ = ДокументОбъект.ПотребительскиеСвойства.Добавить();
				СтрокаТЧ.ПотребительскоеСвойство = ЭлементДанных.ПотребительскоеСвойство;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьТабличнуюЧастьФормированиеПартийПриПроизводствеЗЕРНО(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента)
	
	Если ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьТабличнуюЧасть") Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Основание = ДанныеЗаполнения.Основание;
	Иначе
		Основание = ДанныеЗаполнения;
	КонецЕсли;
	
	ДокументОбъект.Сырье.Очистить();
	ДокументОбъект.ПотребительскиеСвойства.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаЗаполненияФормированиеПартийПриПроизводствеЗЕРНОИзПрикладногоДокумента(ТекстТоварыДокумента, ДопустимыйЗнак);
	
	Запрос.УстановитьПараметр("ЭтаСсылка",         ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", Основание);
	Запрос.УстановитьПараметр("ПустаяСерия",       ОпределяемыеТипы().СерияНоменклатуры.Тип.ПривестиЗначение());
	
	КолонкиКоличестваЗЕРНО = Новый Структура;
	КолонкиКоличестваЗЕРНО.Вставить("Количество", "КоличествоЗЕРНО");	
	
	ЗаполнитьТабличнуюЧастьДокумента(ДокументОбъект.Сырье, Запрос.Выполнить(), ДанныеЗаполнения, КолонкиКоличестваЗЕРНО);
	
	СписокСтрок = Новый Массив();
	Для Каждого СтрокаТаблицы Из ДокументОбъект.Сырье Цикл
		СписокСтрок.Добавить(СтрокаТаблицы);
	КонецЦикла;
	
	Документы.ФормированиеПартийПриПроизводствеЗЕРНО.ЗаполнитьПартии(ДокументОбъект, СписокСтрок);
	
КонецПроцедуры

Функция ТекстЗапросаЗаполненияФормированиеПартийПриПроизводствеЗЕРНОИзПрикладногоДокумента(ТекстТоварыДокумента, ДопустимыйЗнак)
	
	Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ФормированиеПартийПриПроизводствеЗЕРНО КАК ТаблицаДокументы
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|;"
	+
	ТекстТоварыДокумента
	+
	"
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыСерии.Номенклатура    КАК Номенклатура,
	|	ТоварыСерии.Характеристика  КАК Характеристика,
	|	ТоварыСерии.Серия           КАК Серия,
	|	ТоварыСерии.Количество      КАК Количество
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Сырье КАК ТоварыСерии
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура    КАК Номенклатура,
	|	ОформленныеТовары.Характеристика  КАК Характеристика,
	|	ОформленныеТовары.Серия           КАК Серия,
	|	-ОформленныеТовары.Количество     КАК Количество
	|ИЗ
	|	Документ.ФормированиеПартийПриПроизводствеЗЕРНО.Сырье КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В
	|		(ВЫБРАТЬ
	|			Т.Ссылка
	|		ИЗ
	|			ОформленныеДокументы КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура           КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика         КАК Характеристика,
	|	ТоварыКОформлению.Серия                  КАК Серия,
	|	СУММА(ТоварыКОформлению.Количество)      КАК Количество,
	|	СУММА(ТоварыКОформлению.Количество)      КАК КоличествоЗЕРНО,
	|	ТоварыКОформлению.Серия.ДатаПроизводства КАК ДатаИзготовления,
	|	ТоварыКОформлению.Серия.СрокГодности          КАК СрокГодности,
	|	СправочникНоменклатура.КодТНВЭД.Код      КАК ТНВЭД,
	|	СправочникНоменклатура.ОКПД2.Код      КАК ОКПД2
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТоварыКОформлению.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	СправочникНоменклатура.ВидПродукцииИС = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Зерно)
	|		ИЛИ СправочникНоменклатура.ВидПродукцииИС = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПродуктыПереработкиЗерна)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	ТоварыКОформлению.Серия.ДатаПроизводства,
	|	ТоварыКОформлению.Серия.СрокГодности,
	|	СправочникНоменклатура.КодТНВЭД.Код,
	|	СправочникНоменклатура.ОКПД2.Код
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0";
	
	Возврат Текст;
	
КонецФункции

Процедура ЗаполнитьФормированиеПартийПриПроизводствеЗЕРНОНаОснованииОтчетПроизводстваЗаСмену(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ФормированиеПартийПриПроизводствеЗЕРНО КАК ТаблицаДокументы
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|;
	|ВЫБРАТЬ
	|	Товары.Номенклатура     КАК Номенклатура,
	|	Товары.ХарактеристикаНоменклатуры   КАК Характеристика,
	|	Товары.СерияНоменклатуры            КАК Серия,
	|	Товары.Количество * ВЫБОР КОГДА Товары.Коэффициент = 0 ТОГДА 1 ИНАЧЕ Товары.Коэффициент КОНЕЦ КАК Количество
	|ПОМЕСТИТЬ ТоварВШапку
	|ИЗ
	|	Документ.ОтчетПроизводстваЗаСмену.Продукция КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И СправочникНоменклатура.ВидПродукцииИС = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПродуктыПереработкиЗерна)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура     КАК Номенклатура,
	|	ОформленныеТовары.Характеристика   КАК Характеристика,
	|	ОформленныеТовары.Серия            КАК Серия,
	|	-ОформленныеТовары.Количество      КАК Количество
	|ИЗ
	|	Документ.ФормированиеПартийПриПроизводствеЗЕРНО КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В
	|		(ВЫБРАТЬ
	|			Т.Ссылка
	|		ИЗ
	|			ОформленныеДокументы КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДокументСсылка.Ссылка        КАК ДокументОснование,
	|	НЕ ДокументСсылка.Проведен   КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Дата          КАК Дата,
	|	ДокументСсылка.ПодразделениеОрганизации КАК Подразделение,
	|	ДокументСсылка.Склад КАК СкладКонтрагент,
	|	ДокументСсылка.Организация   КАК Организация,
	|	Товары.Номенклатура      КАК Номенклатура,
	|	Товары.Характеристика    КАК Характеристика,
	|	Товары.Серия             КАК Серия,
	|	СправочникНоменклатура.КодТНВЭД.Код     КАК ТНВЭД,
	|	СправочникНоменклатура.ОКПД2.Код     КАК ОКПД2,
	|	СУММА(Товары.Количество) КАК Количество
	|ИЗ
	|	Документ.ОтчетПроизводстваЗаСмену КАК ДокументСсылка,
	|	ТоварВШапку КАК Товары
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|	Справочник.Номенклатура КАК СправочникНоменклатура
	|	ПО СправочникНоменклатура.Ссылка = Товары.Номенклатура
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия,
	|	ДокументСсылка.Ссылка,
	|	НЕ ДокументСсылка.Проведен,
	|	ДокументСсылка.Дата,
	|	ДокументСсылка.ПодразделениеОрганизации,
	|	ДокументСсылка.Склад,
	|	СправочникНоменклатура.КодТНВЭД.Код,
	|	СправочникНоменклатура.ОКПД2.Код,
	|	ДокументСсылка.Организация
	|ИМЕЮЩИЕ
	|	СУММА(Товары.Количество) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|");
	
	ЗаполнитьШапкуФормированиеПартийПриПроизводствеЗЕРНО(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	 ТекстСырье = "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Материалы.Номенклатура      КАК Номенклатура,
	|	Материалы.ХарактеристикаНоменклатуры КАК Характеристика,
	|	Материалы.СерияНоменклатуры КАК Серия,
	|	СУММА(Материалы.Количество * ВЫБОР КОГДА Материалы.Коэффициент = 0 ТОГДА 1 ИНАЧЕ Материалы.Коэффициент КОНЕЦ) КАК Количество
	|ПОМЕСТИТЬ Сырье
	|ИЗ
	|	Документ.ОтчетПроизводстваЗаСмену.Материалы КАК Материалы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Материалы.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Материалы.Ссылка = &ДокументОснование
	|	И СправочникНоменклатура.ВидПродукцииИС В (
	|		ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПродуктыПереработкиЗерна),
	|		ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Зерно))
	|СГРУППИРОВАТЬ ПО
	|	Материалы.Номенклатура,
	|	Материалы.ХарактеристикаНоменклатуры,
	|	Материалы.СерияНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	ЗаполнитьТабличнуюЧастьФормированиеПартийПриПроизводствеЗЕРНО(ДокументОбъект, ДанныеЗаполнения, ТекстСырье);
	
КонецПроцедуры


Процедура ЗаполнитьФормированиеПартийПриПроизводствеЗЕРНОНаОснованииКомплектацияНоменклатуры(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ФормированиеПартийПриПроизводствеЗЕРНО КАК ТаблицаДокументы
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|;
	|ВЫБРАТЬ
	|	ДокументСсылка.Номенклатура    КАК Номенклатура,
	|	ДокументСсылка.ХарактеристикаНоменклатуры  КАК Характеристика,
	|	ДокументСсылка.СерияНоменклатуры           КАК Серия,
	|	ДокументСсылка.Количество * ВЫБОР КОГДА ДокументСсылка.Коэффициент = 0 ТОГДА 1 ИНАЧЕ ДокументСсылка.Коэффициент КОНЕЦ      КАК Количество
	|ПОМЕСТИТЬ ДанныеДокументов
	|ИЗ
	|	Документ.КомплектацияНоменклатуры КАК ДокументСсылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ДокументСсылка.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование
	|	И ДокументСсылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Комплектация)
	|	И СправочникНоменклатура.ВидПродукцииИС = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПродуктыПереработкиЗерна)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументСсылка.Номенклатура    КАК Номенклатура,
	|	ДокументСсылка.ХарактеристикаНоменклатуры  КАК Характеристика,
	|	ДокументСсылка.СерияНоменклатуры           КАК Серия,
	|	ДокументСсылка.Количество * ВЫБОР КОГДА ДокументСсылка.Коэффициент = 0 ТОГДА 1 ИНАЧЕ ДокументСсылка.Коэффициент КОНЕЦ      КАК Количество
	|ИЗ
	|	Документ.КомплектацияНоменклатуры.Комплектующие КАК ДокументСсылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ДокументСсылка.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование
	|	И ДокументСсылка.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Разукомплектация)
	|	И СправочникНоменклатура.ВидПродукцииИС = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПродуктыПереработкиЗерна)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура    КАК Номенклатура,
	|	ОформленныеТовары.Характеристика  КАК Характеристика,
	|	ОформленныеТовары.Серия           КАК Серия,
	|	-ОформленныеТовары.Количество     КАК Количество
	|ИЗ
	|	Документ.ФормированиеПартийПриПроизводствеЗЕРНО КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В
	|		(ВЫБРАТЬ
	|			Т.Ссылка
	|		ИЗ
	|			ОформленныеДокументы КАК Т)
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДокументСсылка.Ссылка                   КАК ДокументОснование,
	|	НЕ ДокументСсылка.Проведен              КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Организация              КАК Организация,
	|	ДокументСсылка.Подразделение            КАК Подразделение,
	|	ДокументСсылка.Подразделение            КАК ПодразделениеТоваропроизводителя,
	|	ДокументСсылка.Организация              КАК ТовароПроизводитель,
	|	ДокументСсылка.Склад                    КАК СкладКонтрагент,
	|	СправочникНоменклатура.Ссылка           КАК Номенклатура,
	|	СправочникНоменклатура.КодТНВЭД.Код     КАК ТНВЭД,
	|	СправочникНоменклатура.ОКПД2.Код     КАК ОКПД2,
	|	ДанныеДокументов.Серия.ДатаПроизводства КАК ДатаИзготовления,
	|	ДанныеДокументов.Серия.СрокГодности          КАК СрокГодности,
	|	СУММА(ДанныеДокументов.Количество)             КАК Количество
	|ИЗ
	|	Документ.КомплектацияНоменклатуры КАК ДокументСсылка,
	|	ДанныеДокументов КАК ДанныеДокументов
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|	Справочник.Номенклатура КАК СправочникНоменклатура
	|	ПО СправочникНоменклатура.Ссылка = ДанныеДокументов.Номенклатура
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование
	|СГРУППИРОВАТЬ ПО
	|	ДокументСсылка.Ссылка,
	|	НЕ ДокументСсылка.Проведен,
	|	ДокументСсылка.Организация,
	|	ДокументСсылка.Подразделение,
	|	ДокументСсылка.Подразделение,
	|	ДокументСсылка.Организация,
	|	ДокументСсылка.Склад,
	|	СправочникНоменклатура.Ссылка,
	|	СправочникНоменклатура.КодТНВЭД.Код,
	|	СправочникНоменклатура.ОКПД2.Код,
	|	ДанныеДокументов.Серия.ДатаПроизводства,
	|	ДанныеДокументов.Серия.СрокГодности
	|ИМЕЮЩИЕ
	|	СУММА(ДанныеДокументов.Количество) > 0");
	
	ЗаполнитьШапкуФормированиеПартийПриПроизводствеЗЕРНО(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументСсылка.Номенклатура    КАК Номенклатура,
	|	ДокументСсылка.ХарактеристикаНоменклатуры  КАК Характеристика,
	|	ДокументСсылка.СерияНоменклатуры           КАК Серия,
	|	ДокументСсылка.Количество * ВЫБОР КОГДА ДокументСсылка.Коэффициент = 0 ТОГДА 1 ИНАЧЕ ДокументСсылка.Коэффициент КОНЕЦ КАК Количество
	|ПОМЕСТИТЬ Сырье
	|ИЗ
	|	Документ.КомплектацияНоменклатуры КАК ДокументСсылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ДокументСсылка.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование
	|	И ДокументСсылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Разукомплектация)
	|	И СправочникНоменклатура.ВидПродукцииИС В (
	|		ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПродуктыПереработкиЗерна),
	|		ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Зерно))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументСсылка.Номенклатура    КАК Номенклатура,
	|	ДокументСсылка.ХарактеристикаНоменклатуры  КАК Характеристика,
	|	ДокументСсылка.СерияНоменклатуры           КАК Серия,
	|	ДокументСсылка.Количество * ВЫБОР КОГДА ДокументСсылка.Коэффициент = 0 ТОГДА 1 ИНАЧЕ ДокументСсылка.Коэффициент КОНЕЦ КАК Количество
	|ИЗ
	|	Документ.КомплектацияНоменклатуры.Комплектующие КАК ДокументСсылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ДокументСсылка.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование
	|	И ДокументСсылка.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Комплектация)
	|	И СправочникНоменклатура.ВидПродукцииИС В (
	|		ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПродуктыПереработкиЗерна),
	|		ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Зерно))
	|
	|;
	|
	|";
	
	ЗаполнитьТабличнуюЧастьФормированиеПартийПриПроизводствеЗЕРНО(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента);
	
КонецПроцедуры

#КонецОбласти

Функция ОпределитьТипОснования(ДанныеЗаполнения)
	
	Основание = ДанныеЗаполнения;
	Если ТипЗнч(Основание) = Тип("Структура") И Основание.Свойство("Основание") Тогда
		Основание = Основание.Основание;
	КонецЕсли;
	Возврат ТипЗнч(Основание);
	
КонецФункции

Процедура ЗаполнитьТабличнуюЧастьДокумента(ТабличнаяЧасть, РезультатЗапроса, ДанныеЗаполнения, СоответствиеКолонокКоличествоЗЕРНО, БезСопоставления = Истина, ДополнительныеПараметры = Неопределено)
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Основание = ДанныеЗаполнения.Основание;
	Иначе
		Основание = ДанныеЗаполнения;
	КонецЕсли;
	
	Если Выборка.Количество() = 0 
		И ТипЗнч(ТабличнаяЧасть)<> Тип("ДокументТабличнаяЧасть.ФормированиеПартийПриПроизводствеЗЕРНО.Сырье") Тогда
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'В %1 отсутствует продукция для заполнения.'"),
			Основание);
		
	КонецЕсли;
	
	КолонкиТабличнойЧасти              = ТабличнаяЧасть.Выгрузить(Новый Структура("НомерСтроки",Ложь));
	ЕстьКоличествоЗЕРНО                = КолонкиТабличнойЧасти.Колонки.Найти("КоличествоЗЕРНО") <> Неопределено;
	ЕстьСкладКонтрагент                = КолонкиТабличнойЧасти.Колонки.Найти("СкладКонтрагент") <> Неопределено;
	ЕстьМестоположение                 = КолонкиТабличнойЧасти.Колонки.Найти("Местоположение") <> Неопределено;
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ТабличнаяЧасть.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(НоваяСтрока, "Идентификатор")
			И НЕ ЗначениеЗаполнено(НоваяСтрока.Идентификатор) Тогда
			НоваяСтрока.Идентификатор = Строка(Новый УникальныйИдентификатор);
		КонецЕсли;
		
		// Связанные табличные части
		Если ДополнительныеПараметры <> Неопределено
			И ДополнительныеПараметры.Свойство("ПотребительскиеСвойства") Тогда
			
			ПотребительскиеСвойстваПоОДКПД2 = ИнтеграцияЗЕРНО.ПотребительскиеСвойстваПродукцииПоДаннымОКПД2(НоваяСтрока.ОКПД2);
			
			Для Каждого ЭлементДанных Из ПотребительскиеСвойстваПоОДКПД2 Цикл
				СтрокаТЧ = ДополнительныеПараметры.ПотребительскиеСвойства.Добавить();
				СтрокаТЧ.ПотребительскоеСвойство = ЭлементДанных.ПотребительскоеСвойство;
				СтрокаТЧ.ИдентификаторСтрокиТоваров = НоваяСтрока.Идентификатор;
			КонецЦикла;
			
		КонецЕсли;
		
		Если ДополнительныеПараметры <> Неопределено
			И ДополнительныеПараметры.Свойство("ЗаполнитьСкладВШапке")
			И НоваяСтрока.НомерСтроки = 1 Тогда
			
			ДополнительныеПараметры.ДокументОбъект[ДополнительныеПараметры.ИмяПоляСклад] = Выборка[ДополнительныеПараметры.ИмяПоляСклад];
			
		КонецЕсли;
		
		Если ДополнительныеПараметры <> Неопределено
			И ДополнительныеПараметры.Свойство("ДобавленныеСтроки") Тогда
			
			ДополнительныеПараметры.ДобавленныеСтроки.Добавить(НоваяСтрока);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЕстьСкладКонтрагент И ЕстьМестоположение Тогда
		
		МассивСкладов = ОбщегоНазначения.ВыгрузитьКолонку(ТабличнаяЧасть, "СкладКонтрагент", Истина);
		
		КонтактнаяИнформация = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(
			МассивСкладов, Перечисления.ТипыКонтактнойИнформации.Адрес,, Основание.Дата);
			
		Для Каждого СтрокаКИ из КонтактнаяИнформация Цикл
			
			СтруктураОтбора = Новый Структура("СкладКонтрагент", СтрокаКИ.Объект);
			МассивСтрок = ТабличнаяЧасть.НайтиСтроки(СтруктураОтбора);
				
			Для Каждого СтрокаТаблицы Из МассивСтрок Цикл	
				СтрокаТаблицы.Местоположение = СтрокаКИ.Значение;
				СтрокаТаблицы.МестоположениеСтрокой = СтрокаКИ.Представление;
			КонецЦикла;
			
		КонецЦикла;

	КонецЕсли;
	
	Если ЕстьКоличествоЗЕРНО Тогда
		
		ДанныеДляПересчета = КоэффициентыЕдиницИзмеренияЗЕРНО(ТабличнаяЧасть);
		
		Для Каждого СтрокаТабличнойЧасти Из ТабличнаяЧасть Цикл
			Для Каждого Колонка Из СоответствиеКолонокКоличествоЗЕРНО Цикл
				
				Если Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.Номенклатура) Тогда
					СтрокаТабличнойЧасти[Колонка.Значение] = Неопределено;
				Иначе
					ДанныеЕдиницыИзмерения = ДанныеДляПересчета.Получить(СтрокаТабличнойЧасти.Номенклатура);
					Если ДанныеЕдиницыИзмерения.КодОшибки <> 0 Тогда
						СтрокаТабличнойЧасти[Колонка.Значение] = Неопределено;
					КонецЕсли;
					
					СтрокаТабличнойЧасти[Колонка.Значение] = СтрокаТабличнойЧасти[Колонка.Ключ] / ДанныеЕдиницыИзмерения.Коэффициент;
				КонецЕсли;
				
			КонецЦикла;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ДокументОснованиеИзДанныхЗаполнения(ДанныеЗаполнения)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("Основание") Тогда
		Возврат ДанныеЗаполнения.Основание;
	Иначе
		Возврат ДанныеЗаполнения;
	КонецЕсли;                                            
	
КонецФункции

#КонецОбласти
