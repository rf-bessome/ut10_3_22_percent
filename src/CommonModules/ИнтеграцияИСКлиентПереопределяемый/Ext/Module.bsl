#Область СлужебныйПрограммныйИнтерфейс

#Область СерииНоменклатуры

// Процедура обновляет кеш ключевых реквизитов текущей строки товаров. По ключевым реквизитам осуществляется связь
//  между ТЧ серий и ТЧ товаров.
//
// Параметры:
// 
//  ТаблицаФормы - ТаблицаФормы - таблица, в которой отображается ТЧ с товарами.
//  КэшированныеЗначения - Структура - переменная модуля формы, в которой хранятся кешируемые значения.
//  ПараметрыУказанияСерий - ФормаКлиентскогоПриложения, Структура - 
//     управляемая форма содержащая структуру или непосредственно структура параметров указания серий.
//  Копирование - Булево - признак, что кешированная строка скопирована (параметр события ПриНачалеРедактирования).
Процедура ОбновитьКешированныеЗначенияДляУчетаСерий(Элемент, КэшированныеЗначения, ПараметрыУказанияСерий = "", Копирование = Ложь) Экспорт
	
	НоменклатураКлиент.ОбновитьКешированныеЗначенияДляУчетаСерий(
		Элемент,
		КэшированныеЗначения,
		?(ТипЗнч(ПараметрыУказанияСерий) = Тип("ФормаКлиентскогоПриложения"),ПараметрыУказанияСерий.ПараметрыУказанияСерий,ПараметрыУказанияСерий),
		Копирование);
	
КонецПроцедуры

// Процедура проверяет необходимость обновления статусов указания серий при окончании редактирования строки товаров.
//
// Параметры:
//  Обновить - Булево - (исходящий) - необходимость обновления статусов указания серий;
//  Форма   - ФормаКлиентскогоПриложения - форма-источник вызова;
//  Элемент - ТаблицаФормы	 - таблица формы, отображающая ТЧ товаров;
//  КэшированныеЗначения - Структура   - переменная модуля формы, в которой хранятся кешируемые значения;
//  ПараметрыУказанияСерий - Структура - параметры указания серий, возвращаемые соответствующей процедурой
//                                       модуля менеджера документа;
//  Удаление - Булево, Истина - признак, что проверка вызывается при удалении строки ТЧ.
//
Процедура УстановитьОбновитьСтатусыСерий(Обновить, Форма, Элемент, КэшированныеЗначения, ПараметрыУказанияСерий = "", Удаление) Экспорт
	
	Если ПараметрыУказанияСерий = "" Тогда
		ПараметрыУказанияСерий = Форма.ПараметрыУказанияСерий;
	КонецЕсли;
	
	Обновить = НоменклатураКлиент.НеобходимоОбновитьСтатусыСерий(Элемент, КэшированныеЗначения, ПараметрыУказанияСерий, Удаление);
	
КонецПроцедуры

// процедура проверяет необходимость указания серий в строке, если возможно, открывает форму указания,
//  если форма указания не требует контекстного вызова сервера.
//
// Параметры:
//  Нужен                  - Булево              - (исходящий) признак необходимости контекстного вызова сервера;
//  Форма                  - ФормаКлиентскогоПриложения    - форма документа, в которой инициировано указание серий;
//  ПараметрыУказанияСерий - Произвольный        - параметры указания серий строки;
//  Текст                  - Строка              - текст, введенный в поле ввода (параметр событий ОкончаниеВводаТекста 
//                                                 и АвтоПодборВводаТекста).
//  ТекущиеДанные          - Структура, ДанныеФормыЭлементКоллекции - данные строки, в которой указывается серия,
//                         - Неопределено        - текущие данные табличного поля с именем ПараметрыУказанияСерий.ИмяТЧТовары;
//  СтандартнаяОбработка   - Булево              - открытие формы выбора серий по умолчанию.
//
Процедура ЗаполнитьДляУказанияСерийНуженСерверныйВызов(
	Нужен, Форма, ПараметрыУказанияСерий, Текст, ТекущиеДанные, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	Нужен                = Ложь;
	
	Если ТекущиеДанные <> Неопределено Тогда
		ИсточникДанных = ТекущиеДанные;
	ИначеЕсли ПараметрыУказанияСерий.ТоварВШапке Тогда
		ИсточникДанных = Форма.Объект;
	Иначе
		ТекущиеДанные = Форма.Элементы[ПараметрыУказанияСерий.ИмяТЧТовары].ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда
			ТекстСообщения = НСтр("ru='Выберите строку товаров, для которой необходимо указать серии.'");
			Предупреждение(ТекстСообщения);
			Возврат;
		КонецЕсли;
		ИсточникДанных = ТекущиеДанные;
	КонецЕсли;
	
	ФормаВыбора = Справочники.СерииНоменклатуры.ПолучитьФормуВыбора(, Форма);
	ФормаВыбора.ПараметрВыборПоВладельцу = ИсточникДанных.Номенклатура;
	ФормаВыбора.ПараметрОтборПоВладельцу = ИсточникДанных.Номенклатура;
	ФормаВыбора.ЭлементыФормы.СправочникСписок.НастройкаОтбора.Владелец.Доступность = Ложь;
	
	Если ЗначениеЗаполнено(ИсточникДанных.Серия) Тогда
		ФормаВыбора.НачальноеЗначениеВыбора = ИсточникДанных.Серия;
	КонецЕсли;

	ФормаВыбора.Открыть();
	
КонецПроцедуры

// Процедура проверяет, что переданная форма является формой указания серий, выставляя результат в первый параметр и
//  нормализуя в этом случае результат выбора в структуру с параметром Значение = выбранная серия.
// 
// Параметры:
//  ЭтоФормаУказанияСерий - Булево - (исходящий) Это форма указания серий.
//  Форма - ФормаКлиентскогоПриложения - Проверяемая форма.
//  ВыбранноеЗначение - Произвольный - (входящий/исходящий) результат выбора до/после нормализации, можно модифицировать
//    если это форма указания серии (должна быть структура с ключем Значение = выбранная серия)
Процедура ЗаполнитьЭтоФормаУказанияСерий(ЭтоФормаУказанияСерий, Форма, ВыбранноеЗначение) Экспорт
	
	//++ НЕ ГОСИС
	ЭтоФормаУказанияСерий = НоменклатураКлиент.ЭтоУказаниеСерий(Форма);
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.СерииНоменклатуры") Тогда
		ВыбранноеЗначение = Новый Структура("Значение", ВыбранноеЗначение);
	КонецЕсли;
	//-- НЕ ГОСИС
	Возврат;
	
КонецПроцедуры

Процедура ПриПолученииПараметровУказанияСерий(ПараметрыУказанияСерий, Форма) Экспорт
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма,"ПараметрыУказанияСерий") Тогда
		ПараметрыУказанияСерий = Форма.ПараметрыУказанияСерий;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПрикладныеКлассификаторы

// Открывает форму списка видов номенклатуры.
//
Процедура ОткрытьФормуСпискаВидыНоменклатуры(ВладелецФормы) Экспорт
	
	ОткрытьФорму("Справочник.ВидыНоменклатуры.Форма.ФормаСписка");
	
КонецПроцедуры

// Открывает форму списка номенклатуры.
//
Процедура ОткрытьФормуСпискаНоменклатуры(ВладелецФормы) Экспорт
	
	ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаСписка");
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

// Обработчик специфических сценариев записи объекта в форме (например, после дополнительных ответов пользователя)
//   При переопределении действия:
//     ** Вызвать обработчик ДействиеПослеЗаписи после окончания записи
//     ** Установить признак СтандартнаяОбработка в значение Ложь
//
// Параметры:
//   Форма                - ФормаКлиентскогоПриложения     - источник события записи
//   Объект               - ДанныеФормыСтруктура - записываемый из формы объект
//   ДействиеПослеЗаписи  - ОписаниеОповещения   - действие которое требуется выполнить после записи объекта из формы
//   СтандартнаяОбработка - Булево               - признак стандартной обработки события (запись без блокирующих вызовов)
//
Процедура ВыполнитьЗаписьОбъектаВФорме(Форма, Объект, ДействиеПослеЗаписи, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;

	ПараметрыЗаписи = Новый Структура;

	ПараметрыЗаписи.Вставить("РежимЗаписи", РежимЗаписиДокумента.Запись);
	ПараметрыЗаписи.Вставить("РежимПроведения", РежимПроведенияДокумента.Неоперативный);

	Если Объект.Проведен Тогда
		ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение;
	КонецЕсли;

	Если ТипЗнч(Форма) = Тип("ФормаКлиентскогоПриложения") Тогда 
		РезультатЗаписи = Форма.Записать(ПараметрыЗаписи);
		Если ДействиеПослеЗаписи <> Неопределено Тогда 
			ВыполнитьОбработкуОповещения(ДействиеПослеЗаписи, РезультатЗаписи);
		КонецЕсли;
	Иначе 
		Форма.Записать(ПараметрыЗаписи.РежимЗаписи, ПараметрыЗаписи.РежимПроведения);
		Если ДействиеПослеЗаписи <> Неопределено И Не Форма.Модифицированность() Тогда 
			ВыполнитьОбработкуОповещения(ДействиеПослеЗаписи, Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

Процедура ОткрытьФормуСпискаЗемельныхУчастков(ВладелецФормы) Экспорт
КонецПроцедуры
