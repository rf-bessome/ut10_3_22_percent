#Область ПрограммныйИнтерфейс

Процедура ОпределитьСкладДокументаОснования(Склад, ДокументОснование) Экспорт
	
	СкладОснования      = Справочники.Склады.ПустаяСсылка();
	МетаданныеОснования = ДокументОснование.Метаданные();
	
	Если МетаданныеОснования.Реквизиты.Найти("Склад") <> Неопределено Тогда
		СкладОснования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "Склад");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СкладОснования)
		Или ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СкладОснования, "ЭтоГруппа") Тогда
		
		ИменаТабличныхЧастей = Новый Массив();
		ИменаТабличныхЧастей.Добавить("Товары");
		ИменаТабличныхЧастей.Добавить("Продукция");
		
		Для Каждого ИмяТабличнойЧасти Из ИменаТабличныхЧастей Цикл
			МетаданныеТабличнойЧасти = МетаданныеОснования.ТабличныеЧасти.Найти(ИмяТабличнойЧасти);
			Если МетаданныеТабличнойЧасти <> Неопределено Тогда
				Если МетаданныеТабличнойЧасти.Реквизиты.Найти("Склад") <> Неопределено Тогда
					ТекстЗапроса =
					"ВЫБРАТЬ ПЕРВЫЕ 1
					|	Склад
					|ИЗ
					|	Документ." + МетаданныеОснования.Имя + "." + ИмяТабличнойЧасти + "
					|ГДЕ
					|	Склад <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
					|	И НЕ Склад.ЭтоГруппа";
					Запрос = Новый Запрос(ТекстЗапроса);
					Выборка = Запрос.Выполнить().Выбрать();
					
					Если Выборка.Следующий() Тогда
						СкладОснования = Выборка.Склад;
						Прервать;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СкладОснования) Тогда
		Склад = СкладОснования;
	КонецЕсли;
	
КонецПроцедуры

// Отражает результаты проверки и подбора в документе, из которого была вызвана соответствующая форма.
// 
// Параметры:
// 	ПараметрыОкончанияПроверки - Структура - (См. ПроверкаИПодборИСМП.ЗафиксироватьРезультатПроверкиИПодбора)
Процедура ОтразитьРезультатыСканированияВДокументе(ПараметрыОкончанияПроверки) Экспорт
	
	ТипПроверяемогоДокумента = ТипЗнч(ПараметрыОкончанияПроверки.ПроверяемыйДокумент);
	
	Если ТипПроверяемогоДокумента = Тип("ДокументСсылка.ПоступлениеТоваровУслуг")
		Или ТипПроверяемогоДокумента = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя")  Тогда
		
		ОтразитьРезультатыПроверкиИПодбораВДокументеПоступления(ПараметрыОкончанияПроверки);
		
	ИначеЕсли ТипПроверяемогоДокумента = Тип("ДокументСсылка.РеализацияТоваровУслуг")
		ИЛИ ТипПроверяемогоДокумента = Тип("ДокументСсылка.КорректировкаРеализации")
		ИЛИ ТипПроверяемогоДокумента = Тип("ДокументСсылка.ВозвратТоваровПоставщику") 
		ИЛИ ТипПроверяемогоДокумента = Тип("ДокументСсылка.ЧекККМ") Тогда
			
		ОтразитьРезультатыПроверкиИПодбораВИсходящемДокументе(ПараметрыОкончанияПроверки, ТипПроверяемогоДокумента);
		
	КонецЕсли; 
	
КонецПроцедуры

// Заполняет таблицу маркированный товаров по выбранным документам. Требуется обеспечить формирование временной таблицы
//   "МаркированныеТоварыОснований" с колонками по порядку:
//   ДокументОснование,Номенклатура,Характеристика,Шаблон,СпособВводаВОборот,Количество.
// Текст запроса уже содержит выбираемые данные во временную таблицу "ТоварыПредварительно",
// поэтому должен начинаться с конструкции: ОБЪЕДИНИТЬ ВСЕ и для присоединения к существующему тексту запроса.
// Параметры:
//  Запрос - Запрос - запрос, в котором требуется сформировать временную таблицу.
//  ИсточникОснований - Строка - Имя временной таблицы с колонкой "ДокументОснование".
Процедура СформироватьТаблицуМаркированныхТоваровОснований(Запрос, ИсточникОснований) Экспорт

	ТекстЗапроса = "
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.Ссылка,
	|	Товары.Ссылка.Организация,
	|	Товары.Номенклатура,
	|	Товары.ХарактеристикаНоменклатуры,
	|	СправочникНоменклатура.ВидПродукцииИС,
	|	&ОпределениеШаблонаКодаМаркировкиСУЗ,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыВводаВОборотСУЗ.Импорт),
	|	Товары.Количество
	|ИЗ
	|	Документ.ЗаказПоставщику.Товары КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ %1 КАК Основания
	|		ПО Основания.ДокументОснование = Товары.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	СправочникНоменклатура.ВидПродукцииИС В (&ВидыПродукцииИСМП)
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыПредварительно.Ссылка             КАК ДокументОснование,
	|	ТоварыПредварительно.Организация        КАК Организация,
	|	ТоварыПредварительно.Номенклатура       КАК Номенклатура,
	|	ТоварыПредварительно.Характеристика     КАК Характеристика,
	|	ТоварыПредварительно.ВидПродукции       КАК ВидПродукции,
	|	ТоварыПредварительно.Шаблон             КАК Шаблон,
	|	ТоварыПредварительно.СпособВводаВОборот КАК СпособВводаВОборот,
	|	СУММА(ТоварыПредварительно.Количество)  КАК Количество
	|ПОМЕСТИТЬ МаркированныеТоварыОснований
	|ИЗ
	|	ТоварыПредварительно КАК ТоварыПредварительно
	|СГРУППИРОВАТЬ ПО
	|	ТоварыПредварительно.Ссылка,
	|	ТоварыПредварительно.Организация,
	|	ТоварыПредварительно.Номенклатура,
	|	ТоварыПредварительно.Характеристика,
	|	ТоварыПредварительно.ВидПродукции,
	|	ТоварыПредварительно.Шаблон,
	|	ТоварыПредварительно.СпособВводаВОборот
	|";
	
	ОпределитьШаблоныКодовМаркировкиСУЗТекстаЗапроса(ТекстЗапроса, "СправочникНоменклатура");
	Запрос.УстановитьПараметр("ВидыПродукцииИСМП", ИнтеграцияИСКлиентСервер.ВидыПродукцииИСМП(Истина));
	Запрос.Текст = Запрос.Текст + СтрШаблон(ТекстЗапроса, ИсточникОснований);
	
КонецПроцедуры

// Зполняет переданную таблицу данные из ТЧ документа.
// 
// Параметры:
//   Документ - ДокументСсылка - Документ из ТЧ которого будет происходить заполнение.
//   ТаблицаПродукции - ТаблицаЗначений - Таблица для заполнения данными из документа.
//   ВидыМаркируемойПродукции - ПеречислениеСсылка.ВидыПродукцииИС, Массив - вид(ы) маркируемой продукции, которым(и) необходимо заполнить таблицу.
//
Процедура СформироватьТаблицуМаркируемойПродукцииДокумента(Документ, ТаблицаПродукции, ВидыМаркируемойПродукции) Экспорт
	
	ТипДокумента = ТипЗнч(Документ);
	Параметры    = ПараметрыФормировнияЗапросаМаркируемойПродукции();
	
	Если ТипДокумента = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя")
		Или ТипДокумента = Тип("ДокументСсылка.ВозвратТоваровПоставщику")
		Или ТипДокумента = Тип("ДокументСсылка.ИнвентаризацияТоваровНаСкладе")
		Или ТипДокумента = Тип("ДокументСсылка.ОприходованиеТоваров")
		Или ТипДокумента = Тип("ДокументСсылка.ОтчетОРозничныхПродажах")
		Или ТипДокумента = Тип("ДокументСсылка.ПоступлениеТоваровУслуг")
		Или ТипДокумента = Тип("ДокументСсылка.РеализацияТоваровУслуг")
		Или ТипДокумента = Тип("ДокументСсылка.СписаниеТоваров")
		Или ТипДокумента = Тип("ДокументСсылка.ЧекККМ")Тогда
	
		Параметры.ИмяПоляХарактеристика = "ХарактеристикаНоменклатуры";
		Параметры.ИмяПоляСерия          = "СерияНоменклатуры";
		
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.КомплектацияНоменклатуры") Тогда
		
		Параметры.ИмяТабЧастиТовары		= "Комплектующие";
		Параметры.ИмяПоляХарактеристика = "ХарактеристикаНоменклатуры";
		Параметры.ИмяПоляСерия          = "СерияНоменклатуры";
				
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ТребованиеНакладная") Тогда
		
		Параметры.ИмяТабЧастиТовары		= "Материалы";
		Параметры.ИмяПоляХарактеристика = "ХарактеристикаНоменклатуры";
		Параметры.ИмяПоляСерия          = "СерияНоменклатуры";
				
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапросаМаркируемойПродукции(Документ, Параметры);
		
	УстановитьУсловиеПоМаркируемойПродукции(ТекстЗапроса, ВидыМаркируемойПродукции);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Документ", Документ);
	Запрос.УстановитьПараметр("ВидМаркируемойПродукции", ВидыМаркируемойПродукции);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ТаблицаПродукции.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла;
	
КонецПроцедуры

// Устанавливает в тексте запроса условие отбора по маркируемой продукции требуемого вида.
// Текст запроса должен содержать строку вида "&УсловиеМаркируемаяПродукция" в условии соединения или условии отбора
// и таблицу с синонимом "СправочникНоменклатура", которой как правило является таблица справочника "Номенклатура".
// Параметры:
//   ТекстЗапроса - Строка - строка с текстом запроса, удовлетворяющая приведенным выше условиям.
//   ВидМаркируемойПродукции - ПеречислениеСсылка.ВидыПродукцииИС, Массив Из ПеречислениеСсылка.ВидыПродукцииИС - 
//     вид или виды маркируемой продукции, условие отбора по которым необходимо установить.
Процедура УстановитьУсловиеПоМаркируемойПродукции(ТекстЗапроса, Знач ВидМаркируемойПродукции) Экспорт
	
	Если ТипЗнч(ВидМаркируемойПродукции) = Тип("ПеречислениеСсылка.ВидыПродукцииИС")
		Или ТипЗнч(ВидМаркируемойПродукции) = Тип("Массив") Тогда 
		УсловиеМаркируемаяПродукция = "СправочникНоменклатура.ВидПродукцииИС В (&ВидМаркируемойПродукции)"
	Иначе
		УсловиеМаркируемаяПродукция = "ЛОЖЬ";
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеМаркируемаяПродукция", УсловиеМаркируемаяПродукция);
	
КонецПроцедуры

#Область Серии

Функция ПараметрыУказанияСерийВыводИзОборотаИСМП(Объект) Экспорт
	
	ПараметрыУказанияСерий        = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ИспользоватьСерииНоменклатуры = Константы.ИспользоватьСерииНоменклатуры.Получить();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта               = "Документ.ВыводИзОборотаИСМП";
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.ИмяТЧСерии                     = "Товары";
	ПараметрыУказанияСерий.ИмяПоляСклад                   = Неопределено;
	
	ПараметрыУказанияСерий.ПоляСвязи.Добавить("Упаковка");
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

Функция ПараметрыУказанияСерийОтгрузкаТоваровИСМП(Объект) Экспорт
	
	ПараметрыУказанияСерий        = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ИспользоватьСерииНоменклатуры = Константы.ИспользоватьСерииНоменклатуры.Получить();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта               = "Документ.ОтгрузкаТоваровИСМП";
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.ИмяТЧСерии                     = "Товары";
	ПараметрыУказанияСерий.ИмяПоляСклад                   = Неопределено;
	
	ПараметрыУказанияСерий.ПоляСвязи.Добавить("Упаковка");
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

Функция ПараметрыУказанияСерийСписаниеКодовМаркировки(Объект) Экспорт
	
	ПараметрыУказанияСерий        = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ИспользоватьСерииНоменклатуры = Константы.ИспользоватьСерииНоменклатуры.Получить();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта               = "Документ.СписаниеКодовМаркировкиИСМП";
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = Ложь;
	ПараметрыУказанияСерий.ИмяТЧСерии                     = "Товары";
	ПараметрыУказанияСерий.ИмяПоляСклад                   = Неопределено;
	
	ПараметрыУказанияСерий.ПоляСвязи.Добавить("Упаковка");
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

Процедура СгенерироватьСерии(ДанныеДляГенерации, ВидМаркируемойПродукции) Экспорт

	ТаблицаДанныхДляГенерацииСерий = ТаблицаДанныхДляГенерацииСерий(ВидМаркируемойПродукции);
	
	Для Каждого ЭлементМассива Из ДанныеДляГенерации Цикл
		НоваяСтрока = ТаблицаДанныхДляГенерацииСерий.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭлементМассива);
				
		Если Не ЗначениеЗаполнено(НоваяСтрока.Серия) Тогда
			НоваяСтрока.Серия = ИнтеграцияИС.ПустоеЗначениеОпределяемогоТипа("СерияНоменклатуры"); 
		КонецЕсли;
	КонецЦикла;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ";
	
	Для Каждого Колонка Из ТаблицаДанныхДляГенерацииСерий.Колонки Цикл
		ТекстЗапроса = ТекстЗапроса + "
		|Таблица." + Колонка.Имя + " КАК " + Колонка.Имя + ",";
	КонецЦикла;
	
	ТекстЗапроса = Сред(ТекстЗапроса, 1, СтрДлина(ТекстЗапроса) - 1);
	
	ТекстЗапроса = ТекстЗапроса + "
	|ПОМЕСТИТЬ
	|	ВтТовары
	|ИЗ
	|	&Таблица КАК Таблица
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВтТовары.Номенклатура                                    КАК Номенклатура,
	|	ЕСТЬNULL(ВидыНоменклатуры.ИспользоватьСерии, ЛОЖЬ)       КАК ИспользоватьСерии,
	|	ЕСТЬNULL(ВладельцыСерии.Ссылка, ВидыНоменклатуры.Ссылка) КАК ВидНоменклатуры,
	|	ЕСТЬNULL(ВладельцыСерии.АвтоматическиГенерироватьСерии,
	|		ВидыНоменклатуры.АвтоматическиГенерироватьСерии)      КАК АвтоматическиГенерироватьСерии
	|	//ДополнительныеПоляВидаНоменклатуры
	|ПОМЕСТИТЬ
	|	ВтВидыНоменклатуры
	|ИЗ
	|	ВтТовары КАК ВтТовары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ВтТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|		ПО СправочникНоменклатура.ВидНоменклатуры = ВидыНоменклатуры.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВладельцыСерии
	|		ПО СправочникНоменклатура.ВладелецСерий = ВладельцыСерии.Ссылка
	|ИНДЕКСИРОВАТЬ ПО
	|	ВтТовары.Номенклатура
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтТовары.Номенклатура                             КАК Номенклатура,
	|	ВтВидыНоменклатуры.ВидНоменклатуры                КАК ВидНоменклатуры,
	|	ВтВидыНоменклатуры.ИспользоватьСерии              КАК ИспользоватьСерии,
	|	ВтВидыНоменклатуры.АвтоматическиГенерироватьСерии КАК АвтоматическиГенерироватьСерии,
	|	СправочникНоменклатура.Наименование               КАК НоменклатураНаименование,
	|	//ДополнительныеПоляВыборки
	|	МАКСИМУМ(ВЫБОР
	|		КОГДА ВтВидыНоменклатуры.АвтоматическиГенерироватьСерии
	|			ТОГДА ЕСТЬNULL(СерииНоменклатуры.Ссылка,
	|				ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка))
	|		ИНАЧЕ ВтТовары.Серия
	|	КОНЕЦ)                                            КАК Серия
	|ИЗ
	|	ВтТовары КАК ВтТовары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтВидыНоменклатуры
	|		ПО ВтТовары.Номенклатура = ВтВидыНоменклатуры.Номенклатура
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ВтТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|		ПО СерииНоменклатуры.ВидНоменклатуры = ВтВидыНоменклатуры.ВидНоменклатуры
	|		И ВтВидыНоменклатуры.АвтоматическиГенерироватьСерии
	|		//ДополнительныеПоляСвязи
	|СГРУППИРОВАТЬ ПО
	|	ВтТовары.Номенклатура,
	|	ВтВидыНоменклатуры.ВидНоменклатуры,
	|	ВтВидыНоменклатуры.ИспользоватьСерии,
	|	ВтВидыНоменклатуры.АвтоматическиГенерироватьСерии,
	|	СправочникНоменклатура.Наименование
	|	//ДополнительныеПоляГруппировки
	|";
	
	ДополнитьТекстЗапросаГенерацииСерийПоВидуПродукции(ТекстЗапроса, ВидМаркируемойПродукции);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Таблица", ТаблицаДанныхДляГенерацииСерий);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ДанныеДляГенерации = Новый Массив();
	
	Пока Выборка.Следующий() Цикл
		ДанныеСерии = ИнтеграцияИСМПУТКлиентСервер.СтруктураДанныхДляГенерацииСерии(ВидМаркируемойПродукции); 
		ЗаполнитьЗначенияСвойств(ДанныеСерии, Выборка);
		
		Если Не Выборка.ИспользоватьСерии Тогда
			
			ДанныеСерии.ЕстьОшибка = Истина;
			ДанныеСерии.ТекстОшибки = СтрШаблон(НСтр("ru = 'Для номенклатуры %1 серии не используются.'"), Выборка.НоменклатураНаименование);
			
		ИначеЕсли НЕ Выборка.АвтоматическиГенерироватьСерии И Не ЗначениеЗаполнено(Выборка.Серия) Тогда
			
			ДанныеСерии.ЕстьОшибка = Истина;
			ДанныеСерии.ТекстОшибки = СтрШаблон(НСтр("ru = 'Для номенклатуры %1 не предусмотрена автоматическая генерация серий.'"), Выборка.НоменклатураНаименование);
			
		ИначеЕсли Выборка.АвтоматическиГенерироватьСерии И Не ЗначениеЗаполнено(Выборка.Серия) Тогда
				
			Попытка
				
				НоваяСерия = Справочники.СерииНоменклатуры.СоздатьЭлемент();
				НоваяСерия.Заполнить(Выборка);
				НоваяСерия.Записать();
				
				ДанныеСерии.Серия = НоваяСерия.Ссылка;
				
			Исключение
				
				ДанныеСерии.ЕстьОшибка = Истина;
				ДанныеСерии.ТекстОшибки = СтрШаблон(НСтр("ru = 'Не удалось сгенерировать серию для номенклатуры %1 по причине: %2'"),
					Выборка.НоменклатураНаименование,
					КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));

			КонецПопытки;
		 
		КонецЕсли;
		
		ДанныеДляГенерации.Добавить(ДанныеСерии); 
	КонецЦикла;

КонецПроцедуры

Функция ПараметрыУказанияСерийВиртуальнаяАгрегацияУпаковокИСМП(Объект) Экспорт
	
	ПараметрыУказанияСерий        = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ИспользоватьСерииНоменклатуры = Константы.ИспользоватьСерииНоменклатуры.Получить();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта               = "Обработка.ВиртуальнаяАгрегацияУпаковокИСМП";
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.ИмяТЧТовары                    = "";
	ПараметрыУказанияСерий.ИмяТЧСерии                     = "";
	ПараметрыУказанияСерий.ИмяПоляСклад                   = Неопределено;
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

Функция ПараметрыУказанияСерийПроверкаИПодборПродукцииИСМП(Объект) Экспорт
	
	ПараметрыУказанияСерий = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта                  = Объект.ИмяФормы;
	
	ПараметрыУказанияСерий.ИмяТЧТовары       = "ПодобраннаяМаркируемаяПродукция";
	ПараметрыУказанияСерий.ИмяТЧСерии        = "ПодобраннаяМаркируемаяПродукция";
	ПараметрыУказанияСерий.ИмяПоляСклад      = "Склад";
	ПараметрыУказанияСерий.ИмяПоляКоличество = "Количество";
	
	ПараметрыУказанияСерий.ИменаПолейДополнительные.Добавить("КоличествоПодобрано");
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры = ИнтеграцияИС.СерииИспользуются();
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

Функция ПараметрыУказанияСерийМаркировкаТоваровИСМП(Объект) Экспорт
	
	ПараметрыУказанияСерий        = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ИспользоватьСерииНоменклатуры = Константы.ИспользоватьСерииНоменклатуры.Получить();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта               = "Документ.МаркировкаТоваровИСМП";
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.ИмяТЧСерии                     = "Товары";
	ПараметрыУказанияСерий.ИмяПоляСклад                   = Неопределено;
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

Функция ПараметрыУказанияСерийВозвратВОборотИСМП(Объект) Экспорт
	
	ПараметрыУказанияСерий        = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ИспользоватьСерииНоменклатуры = Константы.ИспользоватьСерииНоменклатуры.Получить();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта               = "Документ.ВозвратВОборотИСМП";
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.ИмяТЧСерии                     = "Товары";
	ПараметрыУказанияСерий.ИмяПоляСклад                   = Неопределено;
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

Функция ПараметрыУказанияСерийПриемкаТоваровИСМП(Объект) Экспорт
	
	ПараметрыУказанияСерий        = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ИспользоватьСерииНоменклатуры = Константы.ИспользоватьСерииНоменклатуры.Получить();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта               = "Документ.ПриемкаТоваровИСМП";
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.ИмяТЧСерии                     = "Товары";
	ПараметрыУказанияСерий.ИмяПоляСклад                   = Неопределено;
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

Функция ПараметрыУказанияСерийУточнениеСведенийОКодахМаркировкиИСМП(Объект) Экспорт
	
	ПараметрыУказанияСерий        = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ИспользоватьСерииНоменклатуры = Константы.ИспользоватьСерииНоменклатуры.Получить();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта               = "Документ.УточнениеСведенийОКодахМаркировкиИСМП";
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.ИмяТЧСерии                     = "Товары";
	ПараметрыУказанияСерий.ИмяПоляСклад                   = Неопределено;
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

// Формирует текст запроса для расчета статусов указания серий в обработках проверки и подбора товаров
//
//Параметры:
//   ПараметрыУказанияСерий - Структура - (См. НоменклатураКлиентСервер.ПараметрыУказанияСерий).
//
//Возвращаемое значение:
//   Строка - сформированный текст запроса.
//
Функция ТекстЗапросаЗаполненияСтатусовУказанияСерийПроверкаИПодборПродукцииИСМП(ПараметрыУказанияСерий) Экспорт
	
	Проверяемыйдокумент = Неопределено;
	ПараметрыУказанияСерий.Свойство("ПроверяемыйДокумент", Проверяемыйдокумент);

	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Товары.Номенклатура,
		|	Товары.Серия,
		|	Товары.СтатусУказанияСерий,
		|	Товары.НомерСтроки
		|ПОМЕСТИТЬ Товары
		|ИЗ
		|	&Товары КАК Товары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.НомерСтроки КАК НомерСтроки,
		|	Товары.СтатусУказанияСерий КАК СтарыйСтатусУказанияСерий,
		|	ВЫБОР
		|		КОГДА СправочникНоменклатура.Ссылка ЕСТЬ NULL
		|			ТОГДА 0
		|		КОГДА НЕ СправочникНоменклатура.ВестиУчетПоСериям
		|			ТОГДА 0
		|		КОГДА Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
		|			ТОГДА 2
		|		ИНАЧЕ 21
		|	КОНЕЦ КАК СтатусУказанияСерий
		|ПОМЕСТИТЬ ТаблицаСтатусов
		|ИЗ
		|	Товары КАК Товары
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаСтатусов.НомерСтроки КАК НомерСтроки,
		|	ТаблицаСтатусов.СтатусУказанияСерий КАК СтатусУказанияСерий
		|ИЗ
		|	ТаблицаСтатусов КАК ТаблицаСтатусов
		|ГДЕ
		|	ТаблицаСтатусов.СтарыйСтатусУказанияСерий <> ТаблицаСтатусов.СтатусУказанияСерий
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаЗаполненияСтатусовУказанияСерийБезАнализаСклада(ПараметрыУказанияСерий) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.Серия,
	|	Товары.СтатусУказанияСерий,
	|	Товары.НомерСтроки
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.СтатусУказанияСерий КАК СтарыйСтатусУказанияСерий,
	|	ВЫБОР
	|		КОГДА СправочникНоменклатура.Ссылка ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА НЕ СправочникНоменклатура.ВестиУчетПоСериям
	|			ТОГДА 0
	|		КОГДА Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|			ТОГДА 2
	|		ИНАЧЕ 21
	|	КОНЕЦ КАК СтатусУказанияСерий
	|ПОМЕСТИТЬ ТаблицаСтатусов
	|ИЗ
	|	Товары КАК Товары
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСтатусов.НомерСтроки КАК НомерСтроки,
	|	ТаблицаСтатусов.СтатусУказанияСерий КАК СтатусУказанияСерий
	|ИЗ
	|	ТаблицаСтатусов КАК ТаблицаСтатусов
	|ГДЕ
	|	ТаблицаСтатусов.СтарыйСтатусУказанияСерий <> ТаблицаСтатусов.СтатусУказанияСерий
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ОбработкаЗаполнения

#Область ПрикладныеДокументы

#Область МаркировкаТоваровИСМП

Процедура ЗаполнитьОприходованиеИзлишковТоваровНаОснованииМаркировкиТоваровИСМП(ДокументОбъект, ДанныеЗаполнения) Экспорт
	
	Реквизиты = ДанныеПрикладныхДокументовИзМаркировкиТоваровИСМП(ДанныеЗаполнения);
	Реквизиты.Следующий();
	
	Если Не ЗначениеЗаполнено(ДокументОбъект.Ссылка) Тогда
		
		ИнтеграцияИСУТ.ПроверитьВозможностьВводаНаОсновании(
			ДанныеЗаполнения,
			,
			Реквизиты.ЕстьОшибкиПроведен,,);
		
		ЗаполнитьЗначенияСвойств(ДокументОбъект, Реквизиты, , "Товары");
		
	КонецЕсли;
	
	ДокументОбъект.Товары.Очистить();
	
	Выборка = Реквизиты.Товары.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ДокументОбъект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Выборка);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСборкуТоваровНаОснованииМаркировкиТоваровИСМП(ДокументОбъект, ДанныеЗаполнения) Экспорт
	
	Реквизиты = ДанныеПрикладныхДокументовИзМаркировкиТоваровИСМП(ДанныеЗаполнения);
	Реквизиты.Следующий();
	
	Если Не ЗначениеЗаполнено(ДокументОбъект.Ссылка) Тогда
		
		ИнтеграцияИСУТ.ПроверитьВозможностьВводаНаОсновании(
			ДанныеЗаполнения,
			,
			Реквизиты.ЕстьОшибкиПроведен,,);
		
		ЗаполнитьЗначенияСвойств(ДокументОбъект, Реквизиты, , "Товары");
		ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийКомплектацияНоменклатуры.Комплектация;
		
	КонецЕсли;
	
	ДокументОбъект.Товары.Очистить();
	
	Выборка = Реквизиты.Товары.Выбрать();
	Выборка.Следующий();
	ЗаполнитьЗначенияСвойств(ДокументОбъект,Выборка);
	
КонецПроцедуры

#КонецОбласти

#Область ВыводИзОборотаИСМП

Процедура ЗаполнитьСборкуТоваровНаОснованииВыводаИзОборотаИСМП(ДокументОбъект, ДанныеЗаполнения) Экспорт
	
	Реквизиты = ДанныеПрикладныхДокументовИзВыводаИзОборотаИСМП(ДанныеЗаполнения);
	Реквизиты.Следующий();
	
	Если Не ЗначениеЗаполнено(ДокументОбъект.Ссылка) Тогда
		
		ИнтеграцияИСУТ.ПроверитьВозможностьВводаНаОсновании(
			ДанныеЗаполнения,
			,
			Реквизиты.ЕстьОшибкиПроведен,,);
		
		ЗаполнитьЗначенияСвойств(ДокументОбъект, Реквизиты, , "Товары");
		ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийКомплектацияНоменклатуры.Комплектация;
		
	КонецЕсли;
	
	ДокументОбъект.Товары.Очистить();
	
	Выборка = Реквизиты.Товары.Выбрать();
	Выборка.Следующий();
	ЗаполнитьЗначенияСвойств(ДокументОбъект,Выборка);
	
КонецПроцедуры

Процедура ЗаполнитьСписаниеНедостачТоваровНаОснованииВыводаИзОборотаИСМП(ДокументОбъект, ДанныеЗаполнения) Экспорт
	
	Реквизиты = ДанныеПрикладныхДокументовИзВыводаИзОборотаИСМП(ДанныеЗаполнения);
	Реквизиты.Следующий();
	
	Если Не ЗначениеЗаполнено(ДокументОбъект.Ссылка) Тогда
		
		ИнтеграцияИСУТ.ПроверитьВозможностьВводаНаОсновании(
			ДанныеЗаполнения,
			,
			Реквизиты.ЕстьОшибкиПроведен,,);
		
		ЗаполнитьЗначенияСвойств(ДокументОбъект, Реквизиты, , "Товары");
		
	КонецЕсли;
	
	ДокументОбъект.Товары.Очистить();
	
	Выборка = Реквизиты.Товары.Выбрать();
	Выборка.Следующий();
	ЗаполнитьЗначенияСвойств(ДокументОбъект,Выборка);
	
КонецПроцедуры

Процедура ЗаполнитьВнутреннееПотреблениеТоваровНаОснованииВыводаИзОборотаИСМП(ДокументОбъект, ДанныеЗаполнения) Экспорт
	
	Реквизиты = ДанныеПрикладныхДокументовИзВыводаИзОборотаИСМП(ДанныеЗаполнения);
	Реквизиты.Следующий();
	
	Если Не ЗначениеЗаполнено(ДокументОбъект.Ссылка) Тогда
		
		ИнтеграцияИСУТ.ПроверитьВозможностьВводаНаОсновании(
			ДанныеЗаполнения,
			,
			Реквизиты.ЕстьОшибкиПроведен,,);
		
		ЗаполнитьЗначенияСвойств(ДокументОбъект, Реквизиты, , "Товары");
		
	КонецЕсли;
	
	ДокументОбъект.Товары.Очистить();
	
	Выборка = Реквизиты.Товары.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ДокументОбъект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Выборка);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

Процедура ОбработкаЗаполненияДокументаВыводИзОборотаИСМП(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	ТипОснования = ОпределитьТипОснования(ДанныеЗаполнения);
	
	Если ТипОснования = Тип("ДокументСсылка.ЧекККМ") Тогда
		ЗаполнитьВыводИзОборотаИСМПНаОснованииЧекаККМ(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		ЗаполнитьВыводИзОборотаИСМПНаОснованииРеализацииТоваровУслуг(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ОтчетОРозничныхПродажах") Тогда
		ЗаполнитьВыводИзОборотаИСМПНаОснованииОтчетаОРозничныхПродажах(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ТребованиеНакладная") Тогда
		ЗаполнитьВыводИзОборотаИСМПНаОснованииТребованияНакладной(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.СписаниеТоваров") Тогда
		ЗаполнитьВыводИзОборотаИСМПНаОснованииСписанияНедостачТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
		ЗаполнитьВыводИзОборотаИСМПНаОснованииВозвратаТоваровПоставщику(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.КомплектацияНоменклатуры") Тогда
		ЗаполнитьВыводИзОборотаИСМПНаОснованииСборкиТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполненияДокументаЗаказНаЭмиссиюКодовМаркировкиСУЗ(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	ТипОснования = ОпределитьТипОснования(ДанныеЗаполнения);
	
	Если ТипОснования = Тип("ДокументСсылка.ЗаказПоставщику")Тогда
		ЗаполнитьЗаказНаЭмиссиюКодовМаркировкиСУЗНаОснованииЗаказаПоставщику(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполненияДокументаМаркировкаТоваровИСМП(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	ТипОснования = ОпределитьТипОснования(ДанныеЗаполнения);
	
	Если ТипОснования = Тип("ДокументСсылка.КомплектацияНоменклатуры") Тогда
		ЗаполнитьМаркировкаТоваровИСМПНаОснованииСборкиТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
		ЗаполнитьМаркировкаТоваровИСМПНаОснованииПриобретенияТоваровУслуг(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ИнвентаризацияТоваровНаСкладе") Тогда
		ЗаполнитьМаркировкаТоваровИСМПНаОснованииПересчетаТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ОприходованиеТоваров") Тогда
		ЗаполнитьМаркировкаТоваровИСМПНаОснованииОприходованияИзлишковТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДокументОбъект.ВидПродукции)
		И ДокументОбъект.ОперацияНанесенияТолькоДляНаборов
		И Не ИнтеграцияИСКлиентСервер.ВидПродукцииИспользуетОтчетыОНанесенииКодовМаркировки(ДокументОбъект.ВидПродукции) Тогда
		ДокументОбъект.Операция = Перечисления.ВидыОперацийИСМП.Агрегация;
		ДокументОбъект.ОперацияНанесенияТолькоДляНаборов = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполненияДокументаОтгрузкаТоваровИСМП(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	ТипОснования = ОпределитьТипОснования(ДанныеЗаполнения);
	                        
	Если ТипОснования = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		ЗаполнитьОтгрузкуТоваровИСМПНаОснованииРеализацииТоваровУслуг(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
		ЗаполнитьОтгрузкуТоваровИСМПНаОснованииВозвратаТоваровПоставщику(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполненияДокументаПеремаркировкаТоваровИСМП(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	ТипОснования = ОпределитьТипОснования(ДанныеЗаполнения);
	
	Если ТипОснования = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
		ЗаполнитьСписаниеКодовМаркировкиИСМПНаОснованииВозвратаТоваровОтКлиента(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполненияДокументаСписаниеКодовМаркировкиИСМП(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполненияДокументаВозвратВОборотИСМП(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс


#Область Серии

Функция ТаблицаДанныхДляГенерацииСерий(ВидМаркируемойПродукции)
	
	ТаблицаДанных = Новый ТаблицаЗначений();
	ТаблицаДанных.Колонки.Добавить("Номенклатура", ОпределяемыеТипы().Номенклатура.Тип);
	ТаблицаДанных.Колонки.Добавить("Серия",        ОпределяемыеТипы().СерияНоменклатуры.Тип);
	ТаблицаДанных.Колонки.Добавить("ЕстьОшибка",   Новый ОписаниеТипов("Булево"));
	ТаблицаДанных.Колонки.Добавить("ТекстОшибки",  ОбщегоНазначения.ОписаниеТипаСтрока(500));
	
	ДополнитьТаблицуДляГенерацииСерийПоВидуПродукции(ТаблицаДанных, ВидМаркируемойПродукции);
	
	Возврат ТаблицаДанных;
	
КонецФункции

Процедура ДополнитьТаблицуДляГенерацииСерийПоВидуПродукции(ТаблицаДанных, ВидМаркируемойПродукции)
	
	Если ВидМаркируемойПродукции = Перечисления.ВидыПродукцииИС.Табак Тогда
		ОписаниеТипаМРЦ = Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(10, 2));
		ТаблицаДанных.Колонки.Добавить("МРЦ", ОписаниеТипаМРЦ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДополнитьТекстЗапросаГенерацииСерийПоВидуПродукции(ТекстЗапроса, ВидМаркируемойПродукции)
	
	ДополнительныеПоляВидаНоменклатуры = "";
	ДополнительныеПоляВыборки          = "";
	ДополнительныеПоляСвязи            = "";
	ДополнительныеПоляГруппировки      = "";
	
	Если ВидМаркируемойПродукции = Перечисления.ВидыПродукцииИС.Табак Тогда
		ДополнительныеПоляВидаНоменклатуры = ",
		|	ЕСТЬNULL(ВладельцыСерии.ИспользоватьМРЦМОТПСерии,
		|		ВидыНоменклатуры.ИспользоватьМРЦМОТПСерии) КАК ИспользоватьМРЦМОТПСерии";
		
		ДополнительныеПоляВыборки = "
		|	ВтТовары.МРЦ КАК МаксимальнаяРозничнаяЦенаМОТП,";
		
		ДополнительныеПоляСвязи = "
		|		И (ВЫБОР
		|			КОГДА ВтВидыНоменклатуры.ИспользоватьМРЦМОТПСерии = ИСТИНА
		|				ТОГДА СерииНоменклатуры.МаксимальнаяРозничнаяЦенаМОТП = ВтТовары.МРЦ
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ)";
		
		ДополнительныеПоляГруппировки = ",
		|	ВтТовары.МРЦ";
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ДополнительныеПоляВидаНоменклатуры", ДополнительныеПоляВидаНоменклатуры);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ДополнительныеПоляВыборки",          ДополнительныеПоляВыборки);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ДополнительныеПоляСвязи",            ДополнительныеПоляСвязи);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ДополнительныеПоляГруппировки",      ДополнительныеПоляГруппировки);
	
КонецПроцедуры

#КонецОбласти

#Область ОтражениеРезультатаПроверкиИПодбораВДокументе

#Область ВходящиеДокументы

// Переносит результат проверки и подбора маркируемой продукции во входящий документ.
//   Общая схема:
//    * Заполняет серии номенклатуры в документе, при необходимости создавая их,
//    * При использовании актов расхождений - создает акт, иначе
//    * Обновляет табличные части "Товары" и "Штрихкоды упаковок" актуальной маркируемой продукцией,
//    * Перезаписывает документ.
//
// Параметры:
//   ПараметрыОкончанияПроверки - Структура - (См. ПроверкаИПодборИСМП.ЗафиксироватьРезультатПроверкиИПодбора)
//
Процедура ОтразитьРезультатыПроверкиИПодбораВДокументеПоступления(ПараметрыОкончанияПроверки)
	
	ДокументОбъект = ПараметрыОкончанияПроверки.ПроверяемыйДокумент.ПолучитьОбъект();
		
	Если Не ПараметрыОкончанияПроверки.СоздаватьАктОРасхождениях Тогда
		
		ОтразитьИзмененияТабличнойЧастиШтрихкодыУпаковок(
			ДокументОбъект,
			ПараметрыОкончанияПроверки.ТаблицаШтрихкодовВерхнегоУровня,
			ПараметрыОкончанияПроверки.ВидПродукцииИС);
		
		ИзменитьДокументПоступленияПоРезультатамПроверкиИПодбораИСМП(ДокументОбъект, ПараметрыОкончанияПроверки);
		
	КонецЕсли;
	
	Если ДокументОбъект.Проведен Тогда
		
		Попытка
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
		КонецПопытки;
		
	Иначе
		ДокументОбъект.Записать(РежимЗаписиДокумента.Запись)
	КонецЕсли;
	
КонецПроцедуры

Процедура ИзменитьДокументПоступленияПоРезультатамПроверкиИПодбораИСМП(ДокументОбъект, ПараметрыОкончанияСканирования) Экспорт
	
	ПровереннаяИПодобраннаяПродукция = ПараметрыОкончанияСканирования.ТаблицаПодобраннойПровереннойПродукции;
	МетаданныеДокумента = ДокументОбъект.Ссылка.Метаданные();
	МенеджерДокумента = Документы[МетаданныеДокумента.Имя];
	ЕстьСкладВТЧ = МетаданныеДокумента.ТабличныеЧасти.Товары.Реквизиты.Найти("Склад") <> Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ПодобраннаяПродукция.Номенклатура        КАК Номенклатура,
	|	ПодобраннаяПродукция.Характеристика      КАК Характеристика,
	|	ПодобраннаяПродукция.Серия               КАК Серия, 
	|	ПодобраннаяПродукция.Количество          КАК Количество
	|ПОМЕСТИТЬ ПодобраннаяПродукция
	|ИЗ
	|	&ПодобраннаяПродукция КАК ПодобраннаяПродукция
	|;
	|///////////////////////////////////////////////////////////////////////////1
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПодобраннаяПродукция.Номенклатура               КАК Номенклатура,
	|	ПодобраннаяПродукция.Характеристика             КАК Характеристика,
	|	ПодобраннаяПродукция.Количество                 КАК Количество
	|ИЗ
	|	ПодобраннаяПродукция КАК ПодобраннаяПродукция
	|СГРУППИРОВАТЬ ПО
	|	ПодобраннаяПродукция.Номенклатура,
	|	ПодобраннаяПродукция.Характеристика,
	|	ПодобраннаяПродукция.Количество
	|;
	|///////////////////////////////////////////////////////////////////////////2
	|";
	
	ТекстЗапросаОпределениеСклада = ТекстЗапросаОпределениеСклада(ПараметрыОкончанияСканирования.ПроверяемыйДокумент);
	
	Запрос.Текст = Запрос.Текст + ТекстЗапросаОпределениеСклада;
	
	Запрос.УстановитьПараметр("ПроверяемыйДокумент",  ПараметрыОкончанияСканирования.ПроверяемыйДокумент);
	Запрос.УстановитьПараметр("ПодобраннаяПродукция", ПровереннаяИПодобраннаяПродукция);
	
	Результат = Запрос.ВыполнитьПакет();
	
	ПровереннаяПродукцияГруппированная = Результат[1].Выгрузить();
	СкладыДокумента                    = Результат[2].Выгрузить().ВыгрузитьКолонку("Склад");
	
	Склад = Неопределено;
	Если СкладыДокумента.Количество() Тогда
		Склад = СкладыДокумента[0];
	КонецЕсли;
	
	//СтруктураДействийОпределениеФактическогоКоличества = РасхожденияСервер.СтруктураДействийПоТЧОснованияАкта(ПараметрыОкончанияСканирования.ПроверяемыйДокумент);
	СтруктураДействийОпределениеФактическогоКоличества = Новый Структура;
	СтруктураДействийНоваяСтрока                       =  СтруктураДействийПриДобавленииНовойСтроки(ДокументОбъект);
	
	Для Каждого СтрокаПодобраннойПродукцииГруппированная Из ПровереннаяПродукцияГруппированная Цикл
		
		ПараметрыПоискаПоДокументу = Новый Структура;
		ПараметрыПоискаПоДокументу.Вставить("Номенклатура",       СтрокаПодобраннойПродукцииГруппированная.Номенклатура);
		ПараметрыПоискаПоДокументу.Вставить("ХарактеристикаНоменклатуры",     СтрокаПодобраннойПродукцииГруппированная.Характеристика);
		
		ПараметрыПоискаПродукция = Новый Структура;
		ПараметрыПоискаПродукция.Вставить("Номенклатура",   СтрокаПодобраннойПродукцииГруппированная.Номенклатура);
		ПараметрыПоискаПродукция.Вставить("Характеристика", СтрокаПодобраннойПродукцииГруппированная.Характеристика);
		
		НайденныеСтрокиДокумента = ДокументОбъект.Товары.НайтиСтроки(ПараметрыПоискаПоДокументу);
		НайденныеСтрокиПродукция = ПровереннаяИПодобраннаяПродукция.НайтиСтроки(ПараметрыПоискаПродукция);
		
		Если НайденныеСтрокиДокумента.Количество() > 0 Тогда
			
			РаспределитьПодобранноеНаСуществующиеСтроки(ДокументОбъект,
			                                            НайденныеСтрокиДокумента,
			                                            НайденныеСтрокиПродукция, 
			                                            СтруктураДействийОпределениеФактическогоКоличества);
			
		Иначе
			ДобавитьНовуюСтрокуМаркированнойПродукции(ДокументОбъект, 
			                                          НайденныеСтрокиПродукция, 
			                                          ПараметрыОкончанияСканирования.ПроверяемыйДокумент, 
			                                          СтруктураДействийНоваяСтрока,
			                                          Склад,
			                                          ЕстьСкладВТЧ);
			
		КонецЕсли;
	КонецЦикла;
	
	ОчиститьСтрокиСНулевымКоличеством(ДокументОбъект);
	
КонецПроцедуры

Функция ТекстЗапросаОпределениеСклада(ПроверяемыйДокумент) Экспорт
	
	Если ТипЗнч(ПроверяемыйДокумент) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
		
		Возврат "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПоступлениеТоваровУслугТовары.Склад КАК Склад
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
		|ГДЕ ПоступлениеТоваровУслугТовары.Ссылка = &ПроверяемыйДокумент
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ПоступлениеТоваровУслуг.СкладОрдер
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
		|ГДЕ ПоступлениеТоваровУслуг.Ссылка = &ПроверяемыйДокумент
		|	И ПоступлениеТоваровУслуг.СкладОрдер ССЫЛКА Справочник.Склады";
		
	ИначеЕсли ТипЗнч(ПроверяемыйДокумент) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
		
		Возврат "ВЫБРАТЬ
		|	ВозвратТоваровОтКлиента.СкладОрдер КАК Склад
		|ИЗ
		|	Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтКлиента
		|ГДЕ
		|	ВозвратТоваровОтКлиента.Ссылка = &ПроверяемыйДокумент";
		
	КонецЕсли;
	
КонецФункции

Процедура РаспределитьПодобранноеНаСуществующиеСтроки(ДокументОбъект, НайденныеСтрокиДокумента, 
                                                      НайденныеСтрокиПродукция, СтруктураДействийОпределениеФактическогоКоличества)
	
	РаспределитьПодобранное(ДокументОбъект, НайденныеСтрокиДокумента, НайденныеСтрокиПродукция, 
		                                 СтруктураДействийОпределениеФактическогоКоличества);

КонецПроцедуры

Процедура РаспределитьПодобранное(ДокументОбъект, НайденныеСтрокиДокумента, НайденныеСтрокиПродукция, 
                                           СтруктураДействий)
										   
	//Оставим и расклонируем 1 строку с продукцией
	ПерваяСтрока = Истина;
	Для Каждого СтрокаТЧ Из НайденныеСтрокиДокумента Цикл 
		Если ПерваяСтрока Тогда 
			Для Каждого СтрокаПодобрано Из НайденныеСтрокиПродукция Цикл 
				Если ПерваяСтрока Тогда 
					ОбработатьСтроку = СтрокаТЧ;
					ПерваяСтрока = Ложь;
				Иначе 
					ОбработатьСтроку = ДокументОбъект.Товары.Добавить();
					ЗаполнитьЗначенияСвойств(ОбработатьСтроку, СтрокаТЧ);
				КонецЕсли;
				ОбработатьСтроку.СерияНоменклатуры = СтрокаПодобрано.Серия;
				Если ОбработатьСтроку.Количество <> СтрокаПодобрано.Количество Тогда
					СтруктураДействий.Очистить();
					
					СтруктураДействий.Вставить("ПересчитатьСумму", "Количество");
					СтруктураДействий.Вставить("ПересчитатьСуммуНДС");
					СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Истина));
					СтруктураДействий.Вставить("ОчиститьСуммуВзаиморасчетов");
					
				КонецЕсли;
				ОбработатьСтроку.Количество = СтрокаПодобрано.Количество;
				ОбработатьСтрокуТабличнойЧасти(ОбработатьСтроку, СтруктураДействий);
			КонецЦикла;
			Если НайденныеСтрокиПродукция.Количество() = 0 Тогда
				СтрокаТЧ.Количество = 0;
			КонецЕсли;
			ПерваяСтрока = Ложь;
		Иначе 
			СтрокаТЧ.Количество = 0;
			ОбработатьСтрокуТабличнойЧасти(СтрокаТЧ, СтруктураДействий);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОчиститьСтрокиСНулевымКоличеством(ДокументОбъект)
	
	ПараметрыПоиска  = Новый Структура;
	МетаданныеТовары = ДокументОбъект.Метаданные().ТабличныеЧасти["Товары"];
	
	Если МетаданныеТовары.Реквизиты.Найти("Количество") <> Неопределено Тогда
		ПараметрыПоиска.Вставить("Количество", 0);
	КонецЕсли;
	
	Если МетаданныеТовары.Реквизиты.Найти("КоличествоУпаковок") <> Неопределено Тогда
		ПараметрыПоиска.Вставить("КоличествоУпаковок", 0);
	КонецЕсли;
	
	НайденныеСтроки = ДокументОбъект.Товары.НайтиСтроки(ПараметрыПоиска);
	
	Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
		ДокументОбъект.Товары.Удалить(НайденнаяСтрока);
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьНовуюСтрокуМаркированнойПродукции(ДокументОбъект, НайденныеСтрокиПродукция,
                                                    ПроверяемыйДокумент, СтруктураДействий, Склад, ЕстьСкладВТЧ)
	
	Для Каждого НайденнаяСтрокаПродукция Из НайденныеСтрокиПродукция Цикл
		
		НоваяСтрока = ДокументОбъект.Товары.Добавить();
	
		НоваяСтрока.Номенклатура        = НайденнаяСтрокаПродукция.Номенклатура;
		НоваяСтрока.ХарактеристикаНоменклатуры      = НайденнаяСтрокаПродукция.Характеристика;
		НоваяСтрока.СерияНоменклатуры               = НайденнаяСтрокаПродукция.Серия;
		НоваяСтрока.Количество                      = НайденнаяСтрокаПродукция.Количество;
		
		ОбработатьСтрокуТабличнойЧастиДокумента(НоваяСтрока, ДокументОбъект, "Добавление");
		
		СтруктураШапкиДокумента = Новый Структура("Контрагент, ТипЦен, ДоговорКонтрагента, ДатаДокумента, ВалютаДокумента, УчитыватьНДС, СуммаВключаетНДС",
		ДокументОбъект.Контрагент, ДокументОбъект.ТипЦен, ДокументОбъект.ДоговорКонтрагента, ДокументОбъект.Дата, ДокументОбъект.ВалютаДокумента, ДокументОбъект.УчитыватьНДС, ДокументОбъект.СуммаВключаетНДС);

		ОбработкаТабличныхЧастей.ЗаполнитьЕдиницуЦенуПокупкиТабЧасти(НоваяСтрока, ДокументОбъект, СтруктураШапкиДокумента, ДокументОбъект.мВалютаРегламентированногоУчета);
		
		Если ЕстьСкладВТЧ Тогда
			НоваяСтрока.Склад      = Склад;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ИсходящиеДокументы

// Переносит результат проверки и подбора маркируемой продукции в исходящий документ.
//   Общая схема:
//    * Обновляет табличную часть "Штрихкоды упаковок" актуальными штрихкодами упаковок маркируемой продукции,
//    * Обновляет табличную часть "Товары" (при наличии - также "Серии") актуальной маркируемой продукцией,
//    * Перезаписывает документ.
//   Недостача маркируемой продукции списывается с первых найденных товарных строк с тем же ключом
//     (номенклатура / характеристика / серия).
//   Излишки прибавляются к первой найденной строке с тем же ключом, а если ее нет в документе - строка добавляется
//     с параметрами заполнения по умолчанию для документа.
//
// Параметры:
//   ПараметрыОкончанияПроверки - Структура - (См. ПроверкаИПодборИСМП.ЗафиксироватьРезультатПроверкиИПодбора)
//   ТипОбъекта                     - Тип       - Тип проверяемого документа
//
Процедура ОтразитьРезультатыПроверкиИПодбораВИсходящемДокументе(ПараметрыОкончанияПроверки, ТипОбъекта)
	
	ДокументОбъект = ПараметрыОкончанияПроверки.ПроверяемыйДокумент.ПолучитьОбъект();
	
	ОтразитьИзмененияТабличнойЧастиШтрихкодыУпаковок(
		ДокументОбъект,
		ПараметрыОкончанияПроверки.ТаблицаШтрихкодовВерхнегоУровня,
		ПараметрыОкончанияПроверки.ВидПродукцииИС);
	
	Если ТипОбъекта = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		ОтразитьИзменениеКоличестваВТабличныхЧастяхТоварыСерииРеализацииТоваровУслуг(ДокументОбъект, ПараметрыОкончанияПроверки);
	ИначеЕсли ТипОбъекта = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
		ОтразитьИзменениеКоличестваВТабличныхЧастяхТоварыСерииВозвратаТоваровПоставщику(ДокументОбъект, ПараметрыОкончанияПроверки);
	ИначеЕсли ТипОбъекта = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
		ОтразитьИзменениеКоличестваВТабличнойЧастиТоварыКорректировкиРеализации(ДокументОбъект, ПараметрыОкончанияПроверки);
	ИначеЕсли ТипОбъекта = Тип("ДокументСсылка.ЧекККМ") Тогда
		ОтразитьИзменениеКоличестваВТабличныхЧастяхТоварыСерииЧекККМ(ДокументОбъект, ПараметрыОкончанияПроверки);
	КонецЕсли;
	
	Если ДокументОбъект.Проведен Тогда
		
		Попытка
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
		КонецПопытки;
		
	Иначе
		ДокументОбъект.Записать(РежимЗаписиДокумента.Запись)
	КонецЕсли;
	
КонецПроцедуры

// Переносит маркируемую продукцию из формы проверки и подбора в документ Реализация товаров услуг
//   * Удаляет из табличных частей Товары, Серии отсутствующую в данных проверки маркируемую продукцию.
//   * Не меняет прочие товарные строки.
// 
// Параметры:
//  ДокументОбъект           - ДокументОбъект.РеализацияТоваровУслуг - документ к изменению
//  ПараметрыОкончанияПроверки - Структура - (См. ПроверкаИПодборИСМП.ЗафиксироватьРезультатПроверкиИПодбора).
//
Процедура ОтразитьИзменениеКоличестваВТабличныхЧастяхТоварыСерииРеализацииТоваровУслуг(ДокументОбъект, ПараметрыОкончанияПроверки)
	
	Запрос = Новый Запрос;
	ТаблицаТовары = ДокументОбъект.Товары.Выгрузить();
	
	Запрос.УстановитьПараметр("Товары", ТаблицаТовары);
	
	Запрос.УстановитьПараметр("Подобрано", ПараметрыОкончанияПроверки.ТаблицаПодобраннойПровереннойПродукции);
	Запрос.УстановитьПараметр("ВидПродукции", ПараметрыОкончанияПроверки.ВидПродукцииИС);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.ХарактеристикаНоменклатуры КАК Характеристика,
	|	Товары.СерияНоменклатуры КАК Серия,
	|	Товары.Склад,
	|	Товары.Количество,
	|	Товары.Коэффициент
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	&Товары КАК Товары
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Подобрано.Номенклатура,
	|	Подобрано.Характеристика,
	|	Подобрано.Серия,
	|	Подобрано.КоличествоПодобрано КАК Количество
	|ПОМЕСТИТЬ Подобрано
	|ИЗ
	|	&Подобрано КАК Подобрано
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия,
	|	Товары.Количество * Товары.Коэффициент КАК Количество
	|ПОМЕСТИТЬ МаркируемыеТовары
	|ИЗ
	|	ТоварыСерии КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО СправочникНоменклатура.Ссылка = Товары.Номенклатура
	|		И &ВидПродукции = СправочникНоменклатура.ВидПродукцииИС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия,
	|	-Товары.Количество
	|ИЗ
	|	Подобрано КАК Товары
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.Характеристика КАК ХарактеристикаНоменклатуры,
	|	Товары.Серия КАК СерияНоменклатуры,
	|	СУММА(Товары.Количество) КАК Недостача
	|ИЗ
	|	МаркируемыеТовары КАК Товары
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|ИМЕЮЩИЕ
	|	СУММА(Товары.Количество) <> 0
	|УПОРЯДОЧИТЬ ПО
	|	Недостача Убыв";
	
	ВыборкаРасхождения = Запрос.Выполнить().Выбрать();
	Если ВыборкаРасхождения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	КлючПоиска      = "Номенклатура, ХарактеристикаНоменклатуры, СерияНоменклатуры";
	ТаблицаТовары.Колонки.Добавить("Удалить", Новый ОписаниеТипов("Булево"));
	ТаблицаТовары.Индексы.Добавить(КлючПоиска);
	
	СтруктураПоиска = Новый Структура(КлючПоиска);
		
	Пока ВыборкаРасхождения.Следующий() Цикл
		
		Недостача = ВыборкаРасхождения.Недостача;

		ЗаполнитьЗначенияСвойств(СтруктураПоиска, ВыборкаРасхождения);
		
		СтрокиТовары = ДокументОбъект.Товары.НайтиСтроки(СтруктураПоиска);
		
		Если Недостача>0 Тогда
			Для Каждого СтрокаТаблицыТовары Из СтрокиТовары Цикл
				К = ?(СтрокаТаблицыТовары.Коэффициент = 0, 1, СтрокаТаблицыТовары.Коэффициент);
				Списать = СтрокаТаблицыТовары.Количество * К;
				Списать = Мин(Списать, Недостача);
				
				СтрокаТаблицыТовары.Количество = СтрокаТаблицыТовары.Количество - Списать / К;
				Недостача = Недостача - Списать;
				
				Если СтрокаТаблицыТовары.Количество = 0 Тогда
					ДокументОбъект.Товары.Удалить(СтрокаТаблицыТовары);
				Иначе
					ОбработатьСтрокуТабличнойЧастиДокумента(СтрокаТаблицыТовары, ДокументОбъект, "Изменение");
				КонецЕсли;
				
				Если Недостача = 0 Тогда
					Прервать;
				КонецЕсли;

			КонецЦикла;
		Иначе
			
			Если СтрокиТовары.Количество() > 0 Тогда
				СтрокаТовары = СтрокиТовары[0];
				К = ?(СтрокаТовары.Коэффициент = 0, 1, СтрокаТовары.Коэффициент);
				СтрокаТовары.Количество = СтрокаТовары.Количество - Недостача / К;
				ОбработатьСтрокуТабличнойЧастиДокумента(СтрокаТовары, ДокументОбъект, "Изменение");
			Иначе
				СтрокаТовары = ДокументОбъект.Товары.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТовары, СтруктураПоиска);
				СтрокаТовары.Количество = - Недостача;
				ОбработатьСтрокуТабличнойЧастиДокумента(СтрокаТовары, ДокументОбъект, "Добавление");
			КонецЕсли;

		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Переносит маркируемую продукцию из формы проверки и подбора в документ Возврат товаров поставщику.
// Удаляет из табличных частей Товары, Серии отсутствующую в данных проверки маркируемую продукцию.
// Не меняет прочие товарные строки.
//
// Параметры:
//  ДокументОбъект           - ДокументОбъект.ВозвратТоваровПоставщику - документ к изменению
//  ПараметрыОкончанияПроверки - Структура - (См. ПроверкаИПодборИСМП.ЗафиксироватьРезультатПроверкиИПодбора).
//
Процедура ОтразитьИзменениеКоличестваВТабличныхЧастяхТоварыСерииВозвратаТоваровПоставщику(ДокументОбъект, ПараметрыОкончанияПроверки)
	
	Запрос = Новый Запрос;
	ТаблицаТовары = ДокументОбъект.Товары.Выгрузить();
	
	Запрос.УстановитьПараметр("Товары", ТаблицаТовары);
	
	Запрос.УстановитьПараметр("Подобрано", ПараметрыОкончанияПроверки.ТаблицаПодобраннойПровереннойПродукции);
	Запрос.УстановитьПараметр("ВидПродукции", ПараметрыОкончанияПроверки.ВидПродукцииИС);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.ХарактеристикаНоменклатуры КАК Характеристика,
	|	Товары.СерияНоменклатуры КАК Серия,
	|	Товары.Количество,
	|	Товары.Коэффициент
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	&Товары КАК Товары
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия,
	|	Товары.КоличествоПодобрано КАК Количество
	|ПОМЕСТИТЬ Подобрано
	|ИЗ
	|	&Подобрано КАК Товары
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия,
	|	Товары.Количество * Товары.Коэффициент КАК Количество
	|ПОМЕСТИТЬ МаркируемыеТовары
	|ИЗ
	|	ТоварыСерии КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО СправочникНоменклатура.Ссылка = Товары.Номенклатура
	|		И &ВидПродукции = СправочникНоменклатура.ВидПродукцииИС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия,
	|	-Товары.Количество
	|ИЗ
	|	Подобрано КАК Товары
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.Характеристика КАК ХарактеристикаНоменклатуры,
	|	Товары.Серия КАК СерияНоменклатуры,
	|	СУММА(Товары.Количество) КАК Недостача
	|ИЗ
	|	МаркируемыеТовары КАК Товары
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|ИМЕЮЩИЕ
	|	СУММА(Товары.Количество) <> 0
	|УПОРЯДОЧИТЬ ПО
	|	Недостача Убыв";
	
	ВыборкаРасхождения = Запрос.Выполнить().Выбрать();
	Если ВыборкаРасхождения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	КлючПоиска      = "Номенклатура, ХарактеристикаНоменклатуры, СерияНоменклатуры";

	ТаблицаТовары.Колонки.Добавить("Удалить", Новый ОписаниеТипов("Булево"));
	ТаблицаТовары.Индексы.Добавить(КлючПоиска);
	
	СтруктураПоиска = Новый Структура(КлючПоиска);
	
	Пока ВыборкаРасхождения.Следующий() Цикл
		
		Недостача = ВыборкаРасхождения.Недостача;

		ЗаполнитьЗначенияСвойств(СтруктураПоиска, ВыборкаРасхождения);
		СтрокиТовары = ДокументОбъект.Товары.НайтиСтроки(СтруктураПоиска);
		
		Если Недостача>0 Тогда
			Для Каждого СтрокаТаблицыТовары Из СтрокиТовары Цикл
				К = ?(СтрокаТаблицыТовары.Коэффициент = 0, 1, СтрокаТаблицыТовары.Коэффициент);
				Списать = СтрокаТаблицыТовары.Количество * К;
				Списать = Мин(Списать, Недостача);
				
				СтрокаТаблицыТовары.Количество = СтрокаТаблицыТовары.Количество - Списать / К;
				Недостача = Недостача - Списать;
				Если СтрокаТаблицыТовары.Количество = 0 Тогда
					ДокументОбъект.Товары.Удалить(СтрокаТаблицыТовары);
				Иначе
					ОбработатьСтрокуТабличнойЧастиДокумента(СтрокаТаблицыТовары, ДокументОбъект, "Изменение");
				КонецЕсли;
				
				Если Недостача = 0 Тогда
					Прервать;
				КонецЕсли;

			КонецЦикла;
		Иначе
			Если СтрокиТовары.Количество() > 0 Тогда
				СтрокаТовары = СтрокиТовары[0];
				К = ?(СтрокаТовары.Коэффициент = 0, 1, СтрокаТовары.Коэффициент);
				СтрокаТовары.Количество = СтрокаТовары.Количество - Недостача / К;
				ОбработатьСтрокуТабличнойЧастиДокумента(СтрокаТовары, ДокументОбъект, "Изменение");
			Иначе
				СтрокаТовары = ДокументОбъект.Товары.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТовары, СтруктураПоиска);
				СтрокаТовары.Количество = - Недостача;
				ОбработатьСтрокуТабличнойЧастиДокумента(СтрокаТовары, ДокументОбъект, "Добавление");
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Переносит маркируемую продукцию из формы проверки и подбора в документ Корректировка реализации
//   * Удаляет из табличной части Товары отсутствующую в данных проверки маркируемую продукцию.
//   * Не меняет прочие товарные строки.
//
// Параметры:
//  ДокументОбъект             - ДокументОбъект.КорректировкаРеализации - документ к изменению
//  ПараметрыОкончанияПроверки - Структура - (См. ПроверкаИПодборИСМП.ЗафиксироватьРезультатПроверкиИПодбора).
//
Процедура ОтразитьИзменениеКоличестваВТабличнойЧастиТоварыКорректировкиРеализации(ДокументОбъект, ПараметрыОкончанияПроверки)
	
	Запрос = Новый Запрос;
	ТаблицаТовары = ДокументОбъект.Товары.Выгрузить();
	Запрос.УстановитьПараметр("Товары", ТаблицаТовары);
	
	Запрос.УстановитьПараметр("Подобрано", ПараметрыОкончанияПроверки.ТаблицаПодобраннойПровереннойПродукции);
	Запрос.УстановитьПараметр("ВидПродукции", ПараметрыОкончанияПроверки.ВидПродукцииИС);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.ХарактеристикаНоменклатуры КАК Характеристика,
	|	Товары.СерияНоменклатуры КАК Серия,
	|	Товары.Количество
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия,
	|	Товары.КоличествоПодобрано КАК Количество
	|ПОМЕСТИТЬ Подобрано
	|ИЗ
	|	&Подобрано КАК Товары
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия,
	|	Товары.Количество
	|ПОМЕСТИТЬ МаркируемыеТовары
	|ИЗ
	|	Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО СправочникНоменклатура.Ссылка = Товары.Номенклатура
	|		И &ВидПродукции = СправочникНоменклатура.ВидПродукцииИС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия,
	|	-Товары.Количество
	|ИЗ
	|	Подобрано КАК Товары
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.Характеристика КАК ХарактеристикаНоменклатуры,
	|	Товары.Серия КАК СерияНоменклатуры,
	|	СУММА(Товары.Количество) КАК Недостача
	|ИЗ
	|	МаркируемыеТовары КАК Товары
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|ИМЕЮЩИЕ
	|	СУММА(Товары.Количество) <> 0
	|УПОРЯДОЧИТЬ ПО
	|	Недостача Убыв";
	
	ВыборкаРасхождения = Запрос.Выполнить().Выбрать();
	Если ВыборкаРасхождения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	КлючПоиска      = "Номенклатура, ХарактеристикаНоменклатуры, СерияНоменклатуры";
	СтруктураПоиска = Новый Структура(КлючПоиска);
	
	ТаблицаТовары.Колонки.Добавить("Удалить", Новый ОписаниеТипов("Булево"));
	ТаблицаТовары.Индексы.Добавить(КлючПоиска);
		
	Пока ВыборкаРасхождения.Следующий() Цикл
		
		Недостача = ВыборкаРасхождения.Недостача;

		ЗаполнитьЗначенияСвойств(СтруктураПоиска, ВыборкаРасхождения);
		СтрокиТовары = ДокументОбъект.Товары.НайтиСтроки(СтруктураПоиска);
		
		Если Недостача>0 Тогда
			Для Каждого СтрокаТаблицыТовары Из СтрокиТовары Цикл
				
				Списать = Мин(СтрокаТаблицыТовары.Количество, Недостача);
				СтрокаТаблицыТовары.Количество = СтрокаТаблицыТовары.Количество - Списать;
				Недостача = Недостача - Списать;
				
				Если СтрокаТаблицыТовары.Количество = 0 Тогда
					ДокументОбъект.Товары.Удалить(СтрокаТаблицыТовары);
				Иначе
					ОбработатьСтрокуТабличнойЧастиДокумента(СтрокаТаблицыТовары, ДокументОбъект, "Изменение");
				КонецЕсли;
				
				Если Недостача = 0 Тогда
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
		Иначе
			
			Если СтрокиТовары.Количество() > 0 Тогда
				СтрокаТовары = СтрокиТовары[0];
				СтрокаТовары.Количество = СтрокаТовары.Количество - Недостача;
				ОбработатьСтрокуТабличнойЧастиДокумента(СтрокаТовары, ДокументОбъект, "Изменение");
			Иначе
				СтрокаТовары = ДокументОбъект.Товары.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТовары, СтруктураПоиска);
				СтрокаТовары.Количество = - Недостача;
				ОбработатьСтрокуТабличнойЧастиДокумента(СтрокаТовары, ДокументОбъект, "Добавление");
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры


// Переносит маркируемую продукцию из формы проверки и подбора в документ Реализация товаров услуг
//   * Удаляет из табличных частей Товары, Серии отсутствующую в данных проверки маркируемую продукцию.
//   * Не меняет прочие товарные строки.
// 
// Параметры:
//  ДокументОбъект           - ДокументОбъект.РеализацияТоваровУслуг - документ к изменению
//  ПараметрыОкончанияПроверки - Структура - (См. ПроверкаИПодборИСМП.ЗафиксироватьРезультатПроверкиИПодбора).
//
Процедура ОтразитьИзменениеКоличестваВТабличныхЧастяхТоварыСерииЧекККМ(ДокументОбъект, ПараметрыОкончанияПроверки)
	
	Запрос = Новый Запрос;
	ТаблицаТовары = ДокументОбъект.Товары.Выгрузить();
	
	Запрос.УстановитьПараметр("Товары", ТаблицаТовары);
	
	Запрос.УстановитьПараметр("Подобрано", ПараметрыОкончанияПроверки.ТаблицаПодобраннойПровереннойПродукции);
	Запрос.УстановитьПараметр("ВидПродукции", ПараметрыОкончанияПроверки.ВидПродукцииИС);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.ХарактеристикаНоменклатуры КАК Характеристика,
	|	Товары.СерияНоменклатуры КАК Серия,
	|	Товары.Количество,
	|	Товары.Коэффициент
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	&Товары КАК Товары
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Подобрано.Номенклатура,
	|	Подобрано.Характеристика,
	|	Подобрано.Серия,
	|	Подобрано.КоличествоПодобрано КАК Количество
	|ПОМЕСТИТЬ Подобрано
	|ИЗ
	|	&Подобрано КАК Подобрано
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия,
	|	Товары.Количество * Товары.Коэффициент КАК Количество
	|ПОМЕСТИТЬ МаркируемыеТовары
	|ИЗ
	|	ТоварыСерии КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО СправочникНоменклатура.Ссылка = Товары.Номенклатура
	|		И &ВидПродукции = СправочникНоменклатура.ВидПродукцииИС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия,
	|	-Товары.Количество
	|ИЗ
	|	Подобрано КАК Товары
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.Характеристика КАК ХарактеристикаНоменклатуры,
	|	Товары.Серия КАК СерияНоменклатуры,
	|	СУММА(Товары.Количество) КАК Недостача
	|ИЗ
	|	МаркируемыеТовары КАК Товары
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|ИМЕЮЩИЕ
	|	СУММА(Товары.Количество) <> 0
	|УПОРЯДОЧИТЬ ПО
	|	Недостача Убыв";
	
	ВыборкаРасхождения = Запрос.Выполнить().Выбрать();
	Если ВыборкаРасхождения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	КлючПоиска      = "Номенклатура, ХарактеристикаНоменклатуры, СерияНоменклатуры";
	ТаблицаТовары.Колонки.Добавить("Удалить", Новый ОписаниеТипов("Булево"));
	ТаблицаТовары.Индексы.Добавить(КлючПоиска);
	
	СтруктураПоиска = Новый Структура(КлючПоиска);
	
	Пока ВыборкаРасхождения.Следующий() Цикл
		
		Недостача = ВыборкаРасхождения.Недостача;

		ЗаполнитьЗначенияСвойств(СтруктураПоиска, ВыборкаРасхождения);
		
		СтрокиТовары = ДокументОбъект.Товары.НайтиСтроки(СтруктураПоиска);
		
		Если Недостача>0 Тогда
			Для Каждого СтрокаТаблицыТовары Из СтрокиТовары Цикл
				К = ?(СтрокаТаблицыТовары.Коэффициент = 0, 1, СтрокаТаблицыТовары.Коэффициент);
				Списать = СтрокаТаблицыТовары.Количество * К;
				Списать = Мин(Списать, Недостача);
				
				СтрокаТаблицыТовары.Количество = СтрокаТаблицыТовары.Количество - Списать / К;
				Недостача = Недостача - Списать;
				
				Если СтрокаТаблицыТовары.Количество = 0 Тогда
					ДокументОбъект.Товары.Удалить(СтрокаТаблицыТовары);
				Иначе
					ОбработатьСтрокуТабличнойЧастиДокумента(СтрокаТаблицыТовары, ДокументОбъект, "Изменение");
				КонецЕсли;

				Если Недостача = 0 Тогда
					Прервать;
				КонецЕсли;

			КонецЦикла;
		Иначе			
			Если СтрокиТовары.Количество() > 0 Тогда
				СтрокаТовары = СтрокиТовары[0];
				К = ?(СтрокаТовары.Коэффициент = 0, 1, СтрокаТовары.Коэффициент);
				СтрокаТовары.Количество = СтрокаТовары.Количество - Недостача / К;
				ОбработатьСтрокуТабличнойЧастиДокумента(СтрокаТовары, ДокументОбъект, "Изменение");
			Иначе
				СтрокаТовары = ДокументОбъект.Товары.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТовары, СтруктураПоиска);
				СтрокаТовары.Количество = - Недостача;
				ОбработатьСтрокуТабличнойЧастиДокумента(СтрокаТовары, ДокументОбъект, "Добавление");
			КонецЕсли;

		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область ТабличнаяЧастьШтрихкодыУпаковок

// Переносит таблицу штрихкодов верхнего уровня в документ
//   * Удаляет из табличной части "ШтрихкодыУпаковок" документа отсутствующие (содержащие внутри хотя бы 1 шт 
//   маркируемой продукции) штрихкоды верхнего уровня.
//   * Добавляет в табличную часть "ШтрихкодыУпаковок" документа отсутствующие там фактические штрихкоды.
//   * Не меняет прочие штрихкоды.
// 
// Параметры:
//   ДокументОбъект                  - ДокументОбъект  - документ для изменения
//   ТаблицаШтрихкодовВерхнегоУровня - ТаблицаЗначений - таблица с колонкой "ШтрихкодУпаковки" (фактические)
//   ВидПродукцииИС                  - ПеречислениеСсылка.ВидыПродукцииИС - обрабатываемый вид продукции
//
Процедура ОтразитьИзмененияТабличнойЧастиШтрихкодыУпаковок(ДокументОбъект, ТаблицаШтрихкодовВерхнегоУровня, ВидПродукцииИС)
	
	ШтрихкодыДляПроверки = ДокументОбъект.ШтрихкодыУпаковок.Выгрузить().ВыгрузитьКолонку("ШтрихкодУпаковки");
	Для Каждого ЭлементВНаличии Из ТаблицаШтрихкодовВерхнегоУровня Цикл
		ЭлементМассива = ШтрихкодыДляПроверки.Найти(ЭлементВНаличии.ШтрихкодУпаковки);
		Если ЭлементМассива<>Неопределено Тогда
			ШтрихкодыДляПроверки.Удалить(ЭлементМассива);
		Иначе
			ЗаполнитьЗначенияСвойств(ДокументОбъект.ШтрихкодыУпаковок.Добавить(), ЭлементВНаличии);
		КонецЕсли;
	КонецЦикла;
	
	ШтрихкодыСодержащиеМаркируемуюПродукцию = ИнтеграцияИСУТ.ШтрихкодыСодержащиеВидыПродукции(ШтрихкодыДляПроверки, ВидПродукцииИС);
	
	Для Каждого ЭлементОтсутствует Из ШтрихкодыСодержащиеМаркируемуюПродукцию Цикл
		ДокументОбъект.ШтрихкодыУпаковок.Удалить(ДокументОбъект.ШтрихкодыУпаковок.Найти(ЭлементОтсутствует, "ШтрихкодУпаковки"));
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаМаркируемойПродукции(ДокументСсылка, Параметры)
	
	МетаданныеДокумента = ДокументСсылка.Метаданные();
		
	Если МетаданныеДокумента.ТабличныеЧасти[Параметры.ИмяТабЧастиТовары].Реквизиты.Найти(Параметры.ИмяПоляСклад) = Неопределено Тогда
		СкладВТабличнойЧастиТовары = Ложь;
	Иначе
		СкладВТабличнойЧастиТовары = Истина;
	КонецЕсли;
	
	Если МетаданныеДокумента.ТабличныеЧасти.Найти(Параметры.ИмяТабЧастиСерии) = Неопределено Тогда
		ЕстьТабличнаяЧастьСерии        = Ложь;
		СкладВТабличнойЧастиСерии      = Ложь;
		НазначениеВТабличнойЧастиСерии = Ложь;
	Иначе
		ЕстьТабличнаяЧастьСерии = Истина;
		
		Если МетаданныеДокумента.ТабличныеЧасти[Параметры.ИмяТабЧастиСерии].Реквизиты.Найти(Параметры.ИмяПоляСклад) = Неопределено Тогда
			СкладВТабличнойЧастиСерии = Ложь;
		Иначе
			СкладВТабличнойЧастиСерии = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	ДобавитьСвязьПоСкладуТабличнойЧасти = СкладВТабличнойЧастиТовары И СкладВТабличнойЧастиСерии;
	
	Если ЕстьТабличнаяЧастьСерии Тогда
		
		ТекстЗапросаШаблон = "ВЫБРАТЬ
		|	Товары.Номенклатура                                 КАК Номенклатура,
		|	Товары.%4                                           КАК Характеристика,
		|	ЕСТЬNULL(Серии.%5, Товары.%5)                       КАК Серия,
		|	СУММА(ЕСТЬNULL(Серии.Количество,Товары.Количество)) КАК Количество
		|ИЗ
		|	Документ.%1.%2 КАК Товары
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.%1.%3 КАК Серии
		|		ПО Товары.Номенклатура = Серии.Номенклатура
		|		И Товары.Характеристика = Серии.Характеристика
		|		И Серии.Ссылка = &Документ
		|		%4
		|ГДЕ
		|	Товары.Ссылка = &Документ
		|	И &УсловиеМаркируемаяПродукция
		|СГРУППИРОВАТЬ ПО
		|	Товары.Номенклатура,
		|	Товары.%4,
		|	ЕСТЬNULL(Серии.%5, Товары.%5)";
		
		СвязьПоСкладу = "";
		Если ДобавитьСвязьПоСкладуТабличнойЧасти Тогда
			ШаблонСвязьПоСкладу = "И Товары.%1 = Серии.%1";
			СвязьПоСкладу = СтрШаблон(ШаблонСвязьПоСкладу, Параметры.ИмяПоляСклад);
		КонецЕсли;
		
		ТекстЗапроса = СтрШаблон(ТекстЗапросаШаблон, МетаданныеДокумента.Имя, Параметры.ИмяТабЧастиТовары, Параметры.ИмяТабЧастиСерии, СвязьПоСкладу);
		
	Иначе
		
		ТекстЗапросаШаблон = "ВЫБРАТЬ
		|	Товары.Номенклатура КАК Номенклатура,
		|	Товары.%4           КАК Характеристика,
		|	Товары.%5           КАК Серия,
		|	СУММА(Товары.%3)    КАК Количество
		|ИЗ
		|	Документ.%1.%2 КАК Товары
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
		|ГДЕ
		|	Товары.Ссылка = &Документ
		|	И &УсловиеМаркируемаяПродукция
		|СГРУППИРОВАТЬ ПО
		|	Товары.Номенклатура,
		|	Товары.%4,
		|	Товары.%5";
		
		ТекстЗапроса = СтрШаблон(ТекстЗапросаШаблон, МетаданныеДокумента.Имя, Параметры.ИмяТабЧастиТовары, Параметры.ИмяПоляКоличество,
			Параметры.ИмяПоляХарактеристика, Параметры.ИмяПоляСерия);
		
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПараметрыФормировнияЗапросаМаркируемойПродукции()
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("ИмяТабЧастиТовары",     "Товары");
	Параметры.Вставить("ИмяТабЧастиСерии",      "Серии");
	Параметры.Вставить("ИмяПоляСклад",          "Склад");
	Параметры.Вставить("ИмяПоляХарактеристика", "Характеристика");
	Параметры.Вставить("ИмяПоляСерия",          "Серия");
	Параметры.Вставить("ИмяПоляКоличество",     "Количество");
	
	Возврат Параметры;
	
КонецФункции

#Область ОбработкаЗаполнения

#Область ОбщиеПроцедуры

Процедура ОставитьОдинВидПродукции(ДокументОбъект)
	
	Если ДокументОбъект.Товары.Количество()<2 Тогда
		Возврат;
	КонецЕсли;
	ВидПродукции = ДокументОбъект.ВидПродукции;
	Товары = ДокументОбъект.Товары.Выгрузить();
	Товары.Колонки.Добавить("Удалить", Новый ОписаниеТипов("Булево"));
	Номенклатура = ДокументОбъект.Товары.Выгрузить().ВыгрузитьКолонку("Номенклатура");
	ВидыПродукции = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(Номенклатура, "ВидПродукцииИС");
	ВидПродукцииПервойСтроки = ВидыПродукции.Получить(Товары[0].Номенклатура);
	Для Каждого СтрокаТЧ Из Товары Цикл 
		ВидПродукции = ВидыПродукции.Получить(СтрокаТЧ.Номенклатура);
		СтрокаТЧ.Удалить = ВидПродукцииПервойСтроки <> ВидПродукции;
	КонецЦикла;
	ДокументОбъект.Товары.Загрузить(Товары.Скопировать(Новый Структура("Удалить", Ложь)));
	
КонецПроцедуры

Функция УсловиеПоВидуПродукции(ВидПродукции)
	
	Условие = "((СправочникНоменклатура.ВидПродукцииИС <> ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПустаяСсылка) И "+
		"СправочникНоменклатура.ВидПродукцииИС <> ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Алкогольная))" + ")";
	Если ЗначениеЗаполнено(ВидПродукции) Тогда
		Условие = "СправочникНоменклатура.ВидПродукцииИС В (&ВидПродукцииИС)";
	КонецЕсли;
	Возврат Условие;
	
КонецФункции

Функция ОпределитьТипОснования(Знач ДанныеЗаполнения)
	
	ОснованиеЗаполнения = ДанныеЗаполнения;
	Если ТипЗнч(ОснованиеЗаполнения) = Тип("Структура") И ОснованиеЗаполнения.Свойство("Основание") Тогда
		ОснованиеЗаполнения = ОснованиеЗаполнения.Основание;
	КонецЕсли;
	Возврат ТипЗнч(ОснованиеЗаполнения);
	
КонецФункции

Процедура ЗаполнитьТабличнуюЧастьДокумента(ТабличнаяЧасть, РезультатЗапроса, ДанныеЗаполнения, ДополнительныеПараметры = Неопределено)
	
	Выборка = РезультатЗапроса.Выбрать();
	
	ЗаполнениеНеобязательно = ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
		И ДополнительныеПараметры.Свойство("ВозможноПустая");
	
	Если Выборка.Количество() = 0 И Не ЗаполнениеНеобязательно Тогда
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru='В %1 отсутствует продукция для заполнения.'"),
			ДанныеЗаполнения);
		
	КонецЕсли;
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ТабличнаяЧасть.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьШапкуДокумента(ДокументОбъект, ДанныеЗаполнения, Запрос)
	
	Если ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьШапку") Тогда
		Возврат;
	КонецЕсли;
	
	Основание = ДанныеЗаполнения;
	Если ТипЗнч(Основание) = Тип("Структура") Тогда
		Основание = Основание.Основание;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ДокументОснование", Основание);
	
	РеквизитыДокумента = Запрос.Выполнить().Выгрузить();
	Реквизиты = РеквизитыДокумента[0];
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	
	Если Не ЭтоПерезаполнение Тогда
		
		ИнтеграцияИСУТ.ПроверитьВозможностьВводаНаОсновании(
			Реквизиты.ДокументОснование,,
			Реквизиты.ЕстьОшибкиПроведен,,);
			
		ЗаполнитьЗначенияСвойств(ДокументОбъект, Реквизиты);
		
	Иначе
		
		Для Каждого Колонка Из РеквизитыДокумента.Колонки Цикл
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДокументОбъект, Колонка.Имя)
					И Не ЗначениеЗаполнено(ДокументОбъект[Колонка.Имя]) Тогда
				ДокументОбъект[Колонка.Имя] = Реквизиты[Колонка.Имя];
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ТекстЗапросаТабличнойЧастиШтрихкодыУпаковок(ИмяДокумента)
	
	Возврат СтрШаблон(
	"ВЫБРАТЬ
	|	Таблица.ШтрихкодУпаковки,
	|	Таблица.ЗначениеШтрихкода
	|ИЗ
	|	Документ.%1.ШтрихкодыУпаковок КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка = &Ссылка",
	ИмяДокумента);
	
КонецФункции

#КонецОбласти

#Область ЗаказНаЭмиссиюКодовМаркировкиСУЗ

Процедура ЗаполнитьШапкуЗаказаНаЭмиссиюКодовМаркировкиСУЗ(ДокументОбъект, ДанныеЗаполнения, Запрос)
	
	ЗаполнитьШапкуДокумента(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
КонецПроцедуры

Процедура ЗаполнитьТабличнуюЧастьЗаказаНаЭмиссиюКодовМаркировкиСУЗ(ДокументОбъект, ДанныеЗаполнения, ИмяДокумента, ИмяТабличнойЧасти = "Товары")
	
	Если ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьТабличнуюЧасть") Тогда
		Возврат;
	КонецЕсли;
	
	ДокументОбъект.Товары.Очистить();
	ДокументОбъект.СерийныеНомера.Очистить();
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Если ДанныеЗаполнения.ИмяФильтра = "ЗаполнениеСВидомПродукции" Тогда
			ФильтрВидПродукции = ДанныеЗаполнения.ЗначениеФильтра;
		КонецЕсли;
		Основание = ДанныеЗаполнения.Основание;
	Иначе
		Основание          = ДанныеЗаполнения;
		ФильтрВидПродукции = ИнтеграцияИСКлиентСервер.ВидыПродукцииИСМП(Истина, Ложь);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаЗаполненияЗаказаНаЭмиссиюКодовМаркировкиСУЗИзПрикладногоДокумента(ИмяДокумента, ФильтрВидПродукции, ИмяТабличнойЧасти);
	
	Запрос.УстановитьПараметр("ЭтаСсылка",         ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", Основание);
	Запрос.УстановитьПараметр("КонечныеСтатусы",   Документы.ЗаказНаЭмиссиюКодовМаркировкиСУЗ.КонечныеСтатусы());
	Запрос.УстановитьПараметр("ВидПродукцииИС",    ФильтрВидПродукции);
	
	ЗаполнитьТабличнуюЧастьДокумента(ДокументОбъект.Товары, Запрос.Выполнить(), ДанныеЗаполнения);
	
	Если ТипЗнч(ФильтрВидПродукции) = Тип("Массив") Тогда
		ИнтеграцияИС.ЗаполнитьВидПродукцииПоТабличнойЧасти(ДокументОбъект);
		ОставитьОдинВидПродукции(ДокументОбъект);
	Иначе
		ДокументОбъект.ВидПродукции = ФильтрВидПродукции;
	КонецЕсли;
	
КонецПроцедуры

Функция ТекстЗапросаЗаполненияЗаказаНаЭмиссиюКодовМаркировкиСУЗИзПрикладногоДокумента(ИмяДокумента, ВидПродукции, ИмяТабличнойЧасти)
	
	ТекстЗапроса = СтрШаблон(
		"ВЫБРАТЬ
		|	Товары.Номенклатура               КАК Номенклатура,
		|	Товары.ХарактеристикаНоменклатуры КАК Характеристика,
		|	СУММА(Товары.Количество)          КАК Количество
		|ПОМЕСТИТЬ Товары
		|ИЗ
		|	Документ.%1.%2 КАК Товары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
		|ГДЕ
		|	Товары.Ссылка = &ДокументОснование
		|	И %3
		|СГРУППИРОВАТЬ ПО
		|	Товары.Номенклатура,
		|	Товары.ХарактеристикаНоменклатуры",
			ИмяДокумента,
			ИмяТабличнойЧасти,
			УсловиеПоВидуПродукции(ВидПродукции))
	+"
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура   КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Количество     КАК Количество
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Товары КАК Товары
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура,
	|	ОформленныеТовары.Характеристика,
	|	-1
	|ИЗ
	|	РегистрСведений.ПулКодовМаркировкиСУЗ КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.ДокументОснование = &ДокументОснование
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура      КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика    КАК Характеристика,
	|	СУММА(ТоварыКОформлению.Количество) КАК Количество,
	|	СУММА(ТоварыКОформлению.Количество) КАК КоличествоУпаковок
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ЗаполнитьЗаказНаЭмиссиюКодовМаркировкиСУЗНаОснованииЗаказаНаПроизводство(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка                                        КАК ДокументОснование,
	|	ДокументСсылка.Организация                                   КАК Организация,
	|	ДокументСсылка.Подразделение                                 КАК ПроизводственныйОбъект,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыВводаВОборотСУЗ.Производство) КАК СпособВводаВОборот,
	|	НЕ ДокументСсылка.Проведен   КАК ЕстьОшибкиПроведен
	|ИЗ
	|	Документ.ЗаказНаПроизводство КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование
	|");
	
	ЗаполнитьШапкуЗаказаНаЭмиссиюКодовМаркировкиСУЗ(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ЗаполнитьТабличнуюЧастьЗаказаНаЭмиссиюКодовМаркировкиСУЗ(ДокументОбъект, ДанныеЗаполнения, "ЗаказНаПроизводство", "Продукция");
	
КонецПроцедуры

Процедура ЗаполнитьЗаказНаЭмиссиюКодовМаркировкиСУЗНаОснованииЗаказаПоставщику(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка                                  КАК ДокументОснование,
	|	ДокументСсылка.Организация                             КАК Организация,
	|	ДокументСсылка.Подразделение                           КАК ПроизводственныйОбъект,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыВводаВОборотСУЗ.Импорт) КАК СпособВводаВОборот,
	|	НЕ ДокументСсылка.Проведен   КАК ЕстьОшибкиПроведен
	|ИЗ
	|	Документ.ЗаказПоставщику КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование
	|");
	
	ЗаполнитьШапкуЗаказаНаЭмиссиюКодовМаркировкиСУЗ(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ЗаполнитьТабличнуюЧастьЗаказаНаЭмиссиюКодовМаркировкиСУЗ(ДокументОбъект, ДанныеЗаполнения, "ЗаказПоставщику");
	
КонецПроцедуры

#КонецОбласти

#Область МаркировкаТоваровИСМП

Функция ДанныеПрикладныхДокументовИзМаркировкиТоваровИСМП(ДокументСсылка)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МаркировкаТоваровИСМП.Организация КАК Организация,
	|	МаркировкаТоваровИСМП.Комментарий КАК Комментарий,
	|	МаркировкаТоваровИСМП.Дата        КАК Дата,
	|	Не МаркировкаТоваровИСМП.Проведен КАК ЕстьОшибкиПроведен,
	|	МаркировкаТоваровИСМП.Товары.(
	|		Номенклатура           КАК Номенклатура,
	|		Номенклатура.СтавкаНДС КАК СтавкаНДС,
	|		Характеристика         КАК Характеристика,
	|		Серия                  КАК Серия,
	|		Упаковка               КАК Упаковка,
	|		Количество             КАК Количество,
	|		КоличествоУпаковок     КАК КоличествоУпаковок,
	|		СтатусУказанияСерий    КАК СтатусУказанияСерий
	|	) КАК Товары
	|ИЗ
	|	Документ.МаркировкаТоваровИСМП КАК МаркировкаТоваровИСМП
	|ГДЕ
	|	МаркировкаТоваровИСМП.Ссылка = &Ссылка";
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

Процедура ЗаполнитьШапкуМаркировкиТоваровИСМП(ДокументОбъект, ДанныеЗаполнения, Запрос)
	
	ЗаполнитьШапкуДокумента(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
КонецПроцедуры

Процедура ЗаполнитьТабличныеЧастиМаркировкиТоваровИСМП(
	ДокументОбъект, ДанныеЗаполнения, ИмяДокумента, ТоварыСерии = Неопределено, ЕстьШтрихкодыУпаковок = Ложь)
	
	Если ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьТабличнуюЧасть") Тогда
		Возврат;
	КонецЕсли;
	
	Если ТоварыСерии = Неопределено Тогда
		ТоварыСерии = Новый Массив;
		ТоварыСерии.Добавить(Новый Структура("Товары, Серии", "Товары", ""));
	КонецЕсли;
	
	ДокументОбъект.Товары.Очистить();
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Если ДанныеЗаполнения.ИмяФильтра = "ЗаполнениеСВидомПродукции" Тогда
			ФильтрВидПродукции = ДанныеЗаполнения.ЗначениеФильтра;
		КонецЕсли;
		Основание = ДанныеЗаполнения.Основание;
	Иначе
		Основание          = ДанныеЗаполнения;
		ФильтрВидПродукции = ИнтеграцияИСКлиентСервер.ВидыПродукцииИСМП(Истина, Ложь);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаЗаполненияМаркировкиТоваровИСМПИзПрикладногоДокумента(
		ИмяДокумента, ФильтрВидПродукции, ТоварыСерии);
	
	Запрос.УстановитьПараметр("ЭтаСсылка",         ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", Основание);
	Запрос.УстановитьПараметр("КонечныеСтатусы",   Документы.ЗаказНаЭмиссиюКодовМаркировкиСУЗ.КонечныеСтатусы());
	Запрос.УстановитьПараметр("ВидПродукцииИС",    ФильтрВидПродукции);
	
	ЗаполнитьТабличнуюЧастьДокумента(ДокументОбъект.Товары, Запрос.Выполнить(), ДанныеЗаполнения);
	
	Если ЕстьШтрихкодыУпаковок Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапросаТабличнойЧастиШтрихкодыУпаковок(ИмяДокумента);
		Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
		ДокументОбъект.ШтрихкодыУпаковок.Загрузить(Запрос.Выполнить().Выгрузить());
	КонецЕсли;
	
	Если ТипЗнч(ФильтрВидПродукции) = Тип("Массив") Тогда
		ИнтеграцияИС.ЗаполнитьВидПродукцииПоТабличнойЧасти(ДокументОбъект);
		ОставитьОдинВидПродукции(ДокументОбъект);
	Иначе
		ДокументОбъект.ВидПродукции = ФильтрВидПродукции;
	КонецЕсли;
	
КонецПроцедуры

Функция ТекстЗапросаЗаполненияМаркировкиТоваровИСМПИзПрикладногоДокумента(
	ИмяДокумента, ВидПродукции, ТабличныеЧастиТоварыСерии, ВыражениеКоличество="ТаблицаТовары.Количество")
	
	УсловиеВидПродукции = УсловиеПоВидуПродукции(ВидПродукции);
	
	ТоварыСерииПоТабличнымЧастям = Новый Массив;
	ЭтоПервыйЭлемент = Истина;
	Для Каждого ТоварыСерии Из ТабличныеЧастиТоварыСерии Цикл
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТаблицаТовары.Номенклатура         КАК Номенклатура,
		|	ТаблицаТовары.ХарактеристикаНоменклатуры       КАК Характеристика,
		|	ТаблицаТовары.СерияНоменклатуры                КАК Серия,
		|	СУММА(" + ВыражениеКоличество + ") КАК Количество
		|ПОМЕСТИТЬ ТоварыСерии
		|ИЗ
		|	&ОписаниеДокументаТовары КАК ТаблицаТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО ТаблицаТовары.Номенклатура = СправочникНоменклатура.Ссылка
		|ГДЕ
		|	ТаблицаТовары.Ссылка = &ДокументОснование
		|	И %1
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаТовары.Номенклатура,
		|	ТаблицаТовары.ХарактеристикаНоменклатуры,
		|	ТаблицаТовары.СерияНоменклатуры
		|ИМЕЮЩИЕ
		|	СУММА(" + ВыражениеКоличество + ") > 0
		|";
		
		Если ЭтоПервыйЭлемент Тогда
			ЭтоПервыйЭлемент = Ложь;
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ТоварыСерии", "");
		КонецЕсли;
		
		ОписаниеДокумента = СтрШаблон("Документ.%1", ИмяДокумента);
		Если ЗначениеЗаполнено(ТоварыСерии.Товары) Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&ОписаниеДокументаТовары", СтрШаблон("%1.%2", ОписаниеДокумента,ТоварыСерии.Товары));
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&ОписаниеДокументаТовары", ОписаниеДокумента);
		КонецЕсли;
		ТоварыСерииПоТабличнымЧастям.Добавить(ТекстЗапроса);
	КонецЦикла;
	ТекстЗапроса = СтрСоединить(ТоварыСерииПоТабличнымЧастям, "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|");
	ТекстЗапроса = ТекстЗапроса + "
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура   КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Серия          КАК Серия,
	|	Товары.Количество     КАК Количество
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	ТоварыСерии КАК Товары
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура,
	|	ОформленныеТовары.Характеристика,
	|	ОформленныеТовары.Серия,
	|	-ОформленныеТовары.Количество
	|ИЗ
	|	Документ.МаркировкаТоваровИСМП.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка.Проведен
	|	И ОформленныеТовары.Ссылка.ДокументОснование = &ДокументОснование
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура      КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика    КАК Характеристика,
	|	ТоварыКОформлению.Серия             КАК Серия,
	|	СУММА(ТоварыКОформлению.Количество) КАК Количество,
	|	СУММА(ТоварыКОформлению.Количество) КАК КоличествоУпаковок
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0
	|";
	
	ТекстЗапроса = СтрШаблон(ТекстЗапроса, УсловиеВидПродукции);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ЗаполнитьМаркировкаТоваровИСМПНаОснованииПересчетаТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка           КАК ДокументОснование,
	|	НЕОПРЕДЕЛЕНО                    КАК Организация,
	|	ДокументСсылка.Склад            КАК Склад,
	|	&Операция                       КАК Операция,
	|	&ВидПродукции                   КАК ВидПродукции,
	|	ЛОЖЬ      КАК ЕстьОшибкиПроведен
	|ИЗ
	|	Документ.ИнвентаризацияТоваровНаСкладе КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование
	|");
	Запрос.УстановитьПараметр("Операция",     Перечисления.ВидыОперацийИСМП.ВводВОборотМаркировкаОстатков);
	Запрос.УстановитьПараметр("ВидПродукции", Перечисления.ВидыПродукцииИС.Обувь);
	
	ЗаполнитьШапкуМаркировкиТоваровИСМП(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ТабличныеЧасти = Новый Массив;
	ТабличныеЧасти.Добавить(Новый Структура("Товары, Серии", "Товары", ""));
	ЗаполнитьТабличныеЧастиМаркировкиТоваровИСМП(ДокументОбъект, ДанныеЗаполнения, "ИнвентаризацияТоваровНаСкладе");
	
КонецПроцедуры

Процедура ЗаполнитьМаркировкаТоваровИСМПНаОснованииОприходованияИзлишковТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка           КАК ДокументОснование,
	|	ДокументСсылка.Организация      КАК Организация,
	|	ДокументСсылка.Склад            КАК Склад,
	|	&Операция                       КАК Операция,
	|	НЕ ДокументСсылка.Проведен      КАК ЕстьОшибкиПроведен
	|ИЗ
	|	Документ.ОприходованиеТоваров КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование
	|");
	
	Запрос.УстановитьПараметр("Операция", ДокументОбъект.Операция);
	
	ЗаполнитьШапкуМаркировкиТоваровИСМП(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ЗаполнитьТабличныеЧастиМаркировкиТоваровИСМП(ДокументОбъект, ДанныеЗаполнения, "ОприходованиеТоваров");
	
КонецПроцедуры

Процедура ЗаполнитьМаркировкаТоваровИСМПНаОснованииПриобретенияТоваровУслуг(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка      КАК ДокументОснование,
	|	ДокументСсылка.Организация КАК Организация,
	|	НЕ ДокументСсылка.Проведен КАК ЕстьОшибкиПроведен,
	|	ВЫБОР
	|		КОГДА ДокументСсылка.Контрагент.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВводВОборотПолучениеПродукцииОтФизическихЛиц)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВводВОборотПроизводствоВнеЕАЭС)
	|	КОНЕЦ                      КАК Операция
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование
	|");
	
	ЗаполнитьШапкуМаркировкиТоваровИСМП(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ТабличныеЧасти = Новый Массив;
	ТабличныеЧасти.Добавить(Новый Структура("Товары, Серии", "Товары", "Серии"));
	ЗаполнитьТабличныеЧастиМаркировкиТоваровИСМП(ДокументОбъект, ДанныеЗаполнения, "ПоступлениеТоваровУслуг", ТабличныеЧасти, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьМаркировкаТоваровИСМПНаОснованииСборкиТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка КАК ДокументОснование,
	|	ДокументСсылка.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВводВОборотПроизводствоРФ) КАК Операция,
	|	НЕ ДокументСсылка.Проведен КАК ЕстьОшибкиПроведен,
	|	1 В
	|		(ВЫБРАТЬ
	|			1
	|		ИЗ
	|			РегистрСведений.Штрихкоды КАК Штрихкоды
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОписаниеGTINИС КАК ОписаниеGTINИС
	|				ПО
	|					ОписаниеGTINИС.GTIN = Штрихкоды.Штрихкод
	|						ИЛИ ОписаниеGTINИС.GTIN = ""0"" + Штрихкоды.Штрихкод
	|						ИЛИ ОписаниеGTINИС.GTIN = ""00"" + Штрихкоды.Штрихкод
	|						ИЛИ ОписаниеGTINИС.GTIN = ""000000"" + Штрихкоды.Штрихкод
	|		ГДЕ
	|			(ВЫРАЗИТЬ(Штрихкоды.Владелец КАК Справочник.Номенклатура)) = ДокументСсылка.Номенклатура
	|			И Штрихкоды.ХарактеристикаНоменклатуры = ДокументСсылка.ХарактеристикаНоменклатуры
	|			И ОписаниеGTINИС.ВидУпаковки = ЗНАЧЕНИЕ(Перечисление.ВидыУпаковокИС.Набор)
	|			И ОписаниеGTINИС.КоличествоПотребительскихУпаковок < 2) КАК ОперацияНанесенияТолькоДляНаборов
	|ИЗ
	|	Документ.КомплектацияНоменклатуры КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуМаркировкиТоваровИСМП(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ТабличныеЧасти = Новый Массив;
	ОписаниеТабличнойЧасти = Новый Структура("Товары, Серии", "Комплектующие", "");
	
	Если Не ДокументОбъект.ОперацияНанесенияТолькоДляНаборов
			Или ДокументОбъект.Операция = Перечисления.ВидыОперацийИСМП.Агрегация Тогда
		ОписаниеТабличнойЧасти.Товары = "";
	КонецЕсли;
	
	ТабличныеЧасти.Добавить(ОписаниеТабличнойЧасти);
	
	ЗаполнитьТабличныеЧастиМаркировкиТоваровИСМП(ДокументОбъект, ДанныеЗаполнения, "КомплектацияНоменклатуры", ТабличныеЧасти);
	
КонецПроцедуры

Процедура ЗаполнитьМаркировкаТоваровИСМПНаОснованииОтчетаПроизводстваЗаСмену(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка      КАК ДокументОснование,
	|	ДокументСсылка.Организация КАК Организация,
	|	НЕ ДокументСсылка.Проведен КАК ЕстьОшибкиПроведен,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВводВОборотПроизводствоРФ) КАК Операция
	|ИЗ
	|	Документ.ОтчетПроизводстваЗаСмену КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование
	|");
	
	ЗаполнитьШапкуМаркировкиТоваровИСМП(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ТабличныеЧасти = Новый Массив;
	ТабличныеЧасти.Добавить(Новый Структура("Товары, Серии", "Продукция", ""));
	ЗаполнитьТабличныеЧастиМаркировкиТоваровИСМП(ДокументОбъект, ДанныеЗаполнения, "ОтчетПроизводстваЗаСмену", ТабличныеЧасти);
	
КонецПроцедуры

#КонецОбласти

#Область ВыводИзОборотаИСМП

Функция ДанныеПрикладныхДокументовИзВыводаИзОборотаИСМП(ДокументСсылка)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВыводИзОборотаИСМП.Организация КАК Организация,
	|	ВыводИзОборотаИСМП.Комментарий КАК Комментарий,
	|	ВыводИзОборотаИСМП.Дата        КАК Дата,
	|	Не ВыводИзОборотаИСМП.Проведен КАК ЕстьОшибкиПроведен,
	|	ВыводИзОборотаИСМП.Товары.(
	|		Номенклатура           КАК Номенклатура,
	|		Номенклатура.СтавкаНДС КАК СтавкаНДС,
	|		Характеристика         КАК Характеристика,
	|		Серия                  КАК Серия,
	|		Упаковка               КАК Упаковка,
	|		Количество             КАК Количество,
	|		КоличествоУпаковок     КАК КоличествоУпаковок,
	|		СтатусУказанияСерий    КАК СтатусУказанияСерий
	|	) КАК Товары
	|ИЗ
	|	Документ.ВыводИзОборотаИСМП КАК ВыводИзОборотаИСМП
	|ГДЕ
	|	ВыводИзОборотаИСМП.Ссылка = &Ссылка";
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

Процедура ЗаполнитьШапкуВыводаИзОборотаИСМП(ДокументОбъект, ДанныеЗаполнения, Запрос)
	
	ЗаполнитьШапкуДокумента(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
КонецПроцедуры

Процедура ЗаполнитьТабличныеЧастиВыводаИзОборотаИСМП(
	ДокументОбъект, ДанныеЗаполнения, ИмяДокумента, ТоварыСерии = Неопределено, Штрихкоды = Неопределено)
	
	Если ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьТабличнуюЧасть") Тогда
		Возврат;
	КонецЕсли;
	
	Если ТоварыСерии = Неопределено Тогда
		ТоварыСерии = Новый Массив;
		ТоварыСерии.Добавить(Новый Структура("Товары, Серии, ПолеСумма", "Товары", "", ""));
	КонецЕсли;
	
	ДокументОбъект.Товары.Очистить();
	ДокументОбъект.ШтрихкодыУпаковок.Очистить();
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Если ДанныеЗаполнения.ИмяФильтра = "ЗаполнениеСВидомПродукции" Тогда
			ФильтрВидПродукции = ДанныеЗаполнения.ЗначениеФильтра;
		КонецЕсли;
		Основание = ДанныеЗаполнения.Основание;
	Иначе
		Основание          = ДанныеЗаполнения;
		ФильтрВидПродукции = ИнтеграцияИСКлиентСервер.ВидыПродукцииИСМП();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаЗаполненияВыводаИзОборотаИСМПИзПрикладногоДокумента(ИмяДокумента, ФильтрВидПродукции, ТоварыСерии, Штрихкоды);
	
	Запрос.УстановитьПараметр("ЭтаСсылка",         ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", Основание);
	Запрос.УстановитьПараметр("КонечныеСтатусы",   Документы.ВыводИзОборотаИСМП.КонечныеСтатусы());
	Запрос.УстановитьПараметр("ВидПродукцииИС",    ФильтрВидПродукции);
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("СуммаВключаетНДС", Основание.Метаданные()) Тогда
		Запрос.УстановитьПараметр("СуммаВключаетНДС",  Основание.СуммаВключаетНДС);
	КонецЕсли;
	
	Пакет = Запрос.ВыполнитьПакет();
	ИндексТовары    = Пакет.Количество() - ?(Штрихкоды = Неопределено, 1, 2);
	ЗаполнитьТабличнуюЧастьДокумента(ДокументОбъект.Товары, Пакет[ИндексТовары], ДанныеЗаполнения);
	
	Если ТипЗнч(ФильтрВидПродукции) = Тип("Массив") Тогда
		ИнтеграцияИС.ЗаполнитьВидПродукцииПоТабличнойЧасти(ДокументОбъект);
		ОставитьОдинВидПродукции(ДокументОбъект);
	Иначе
		ДокументОбъект.ВидПродукции = ФильтрВидПродукции;
	КонецЕсли;
	
	Если Штрихкоды <> Неопределено Тогда
		ШтрихкодыОснования = Пакет[Пакет.Количество() - 1].Выгрузить().ВыгрузитьКолонку("ШтрихкодУпаковки");
		ШтрихкодыПоВидуПродукции = ИнтеграцияИСУТ.ШтрихкодыСодержащиеВидыПродукции(ШтрихкодыОснования, ДокументОбъект.ВидПродукции);
		Для Каждого ЭлементМассива Из ШтрихкодыПоВидуПродукции Цикл
			ДокументОбъект.ШтрихкодыУпаковок.Добавить().ШтрихкодУпаковки = ЭлементМассива;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры


Функция ТекстЗапросаЗаполненияВыводаИзОборотаИСМПИзПрикладногоДокумента(ИмяДокумента, ВидПродукции, ТабличныеЧастиТоварыСерии, ТабличнаяЧастьШтрихкоды)
	
	УсловиеВидПродукции = УсловиеПоВидуПродукции(ВидПродукции);
		
	ТоварыСерииПоТабличнымЧастям = Новый Массив;
	ЭтоПервыйЭлемент = Истина;
	Для Каждого ТоварыСерии Из ТабличныеЧастиТоварыСерии Цикл
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТаблицаТовары.Номенклатура               КАК Номенклатура,
		|	ТаблицаТовары.ХарактеристикаНоменклатуры КАК Характеристика,
		|	ТаблицаТовары.СерияНоменклатуры          КАК Серия,
		|	&ПолеСтавкаНДС                           КАК СтавкаНДС,
		|	СУММА(ТаблицаТовары.Количество)          КАК Количество,
		|	СУММА(&ПолеСумма)                        КАК Сумма,
		|	СУММА(&ПолеСуммаНДС)                     КАК СуммаНДС,
		|	СУММА(&ПолеСуммаСНДС)                    КАК СуммаСНДС
		|ПОМЕСТИТЬ ТоварыСерии
		|ИЗ
		|	&ОписаниеДокументаТовары КАК ТаблицаТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО ТаблицаТовары.Номенклатура = СправочникНоменклатура.Ссылка
		|ГДЕ
		|	ТаблицаТовары.Ссылка = &ДокументОснование
		|	И %1
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаТовары.Номенклатура,
		|	ТаблицаТовары.ХарактеристикаНоменклатуры,
		|	ТаблицаТовары.СерияНоменклатуры,
		|	&ПолеСтавкаНДС
		|;";
		
		Если ЭтоПервыйЭлемент Тогда
			ЭтоПервыйЭлемент = Ложь;
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ТоварыСерии", "");
		КонецЕсли;
		
		ОписаниеДокумента = СтрШаблон("Документ.%1", ИмяДокумента);
		Если ЗначениеЗаполнено(ТоварыСерии.Товары) Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&ОписаниеДокументаТовары", СтрШаблон("%1.%2", ОписаниеДокумента,ТоварыСерии.Товары));
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&ОписаниеДокументаТовары", ОписаниеДокумента);
		КонецЕсли;
		
		Если ТоварыСерии.Свойство("ПолеСуммаСНДС") Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолеСуммаСНДС", "ВЫБОР КОГДА &СуммаВключаетНДС ТОГДА ТаблицаТовары.Сумма ИНАЧЕ ТаблицаТовары.Сумма + ТаблицаТовары.СуммаНДС КОНЕЦ");
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолеСуммаСНДС", "0");
		КонецЕсли;
		
		Если ТоварыСерии.Свойство("ПолеСуммаНДС") И ЗначениеЗаполнено(ТоварыСерии.ПолеСумма) Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолеСуммаНДС", СтрШаблон("ТаблицаТовары.%1",ТоварыСерии.ПолеСуммаНДС));
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолеСуммаНДС", "0");
		КонецЕсли;
		
		Если ТоварыСерии.Свойство("ПолеСумма") И ЗначениеЗаполнено(ТоварыСерии.ПолеСумма) Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолеСумма", СтрШаблон("ТаблицаТовары.%1",ТоварыСерии.ПолеСумма));
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолеСумма", "0");
		КонецЕсли;
		
		Если ТоварыСерии.Свойство("ПолеСтавкаНДС") И ЗначениеЗаполнено(ТоварыСерии.ПолеСтавкаНДС) Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолеСтавкаНДС", СтрШаблон("ТаблицаТовары.%1",ТоварыСерии.ПолеСтавкаНДС));
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолеСтавкаНДС", "ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.ПустаяСсылка)");
		КонецЕсли;
		
		ТоварыСерииПоТабличнымЧастям.Добавить(ТекстЗапроса);
		
	КонецЦикла;
	
	
	ТекстЗапроса = СтрСоединить(ТоварыСерииПоТабличнымЧастям, "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|");
	ТекстЗапроса = ТекстЗапроса + "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура   КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Серия          КАК Серия,
	|	Товары.СтавкаНДС      КАК СтавкаНДС,
	|	Товары.Количество     КАК Количество,
	|	Товары.Сумма          КАК Сумма,
	|	Товары.СуммаНДС       КАК СуммаНДС,
	|	Товары.СуммаСНДС      КАК СуммаСНДС
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	ТоварыСерии КАК Товары
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура,
	|	ОформленныеТовары.Характеристика,
	|	ОформленныеТовары.Серия,
	|	ОформленныеТовары.СтавкаНДС,
	|	-ОформленныеТовары.Количество,
	|	-ОформленныеТовары.Сумма,
	|	-ОформленныеТовары.СуммаНДС,
	|	-ОформленныеТовары.СуммаСНДС
	|ИЗ
	|	Документ.ВыводИзОборотаИСМП.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка.Проведен
	|	И ОформленныеТовары.Ссылка.ДокументОснование = &ДокументОснование
	|	И ОформленныеТовары.Ссылка <> &ЭтаСсылка
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура      КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика    КАК Характеристика,
	|	ТоварыКОформлению.Серия             КАК Серия,
	|	ТоварыКОформлению.СтавкаНДС         КАК СтавкаНДС,
	|	СУММА(ТоварыКОформлению.Количество) КАК Количество,
	|	СУММА(ТоварыКОформлению.Количество) КАК КоличествоУпаковок,
	|	СУММА(ТоварыКОформлению.Сумма)      КАК Сумма,
	|	СУММА(ТоварыКОформлению.СуммаНДС)   КАК СуммаНДС,
	|	СУММА(ТоварыКОформлению.СуммаСНДС)   КАК СуммаСНДС,
	|	ВЫБОР КОГДА СУММА(ТоварыКОформлению.Количество) > 0 И СУММА(ТоварыКОформлению.Сумма) > 0
	|		ТОГДА СУММА(ТоварыКОформлению.Сумма) / СУММА(ТоварыКОформлению.Количество)
	|	ИНАЧЕ 0 КОНЕЦ                       КАК Цена
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	ТоварыКОформлению.СтавкаНДС
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0
	|;
	|";
	Если ТабличнаяЧастьШтрихкоды <> Неопределено Тогда
		ТекстЗапроса = ТекстЗапроса + СтрШаблон(
		"ВЫБРАТЬ
		|	Штрихкоды.%3 КАК ШтрихкодУпаковки
		|ИЗ
		|	Документ.%1.%2 КАК Штрихкоды
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО Штрихкоды.%3.Номенклатура = СправочникНоменклатура.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВыводИзОборотаИСМП.ШтрихкодыУпаковок КАК ОформленныеШтрихкоды
		|		ПО ОформленныеШтрихкоды.Ссылка.ДокументОснование = &ДокументОснование
		|		И ОформленныеШтрихкоды.Ссылка <> &ЭтаСсылка
		|		И ОформленныеШтрихкоды.ШтрихкодУпаковки = Штрихкоды.%3
		|		И ОформленныеШтрихкоды.Ссылка.Проведен
		|ГДЕ
		|	Штрихкоды.Ссылка = &ДокументОснование
		|	И %4
		|	И ОформленныеШтрихкоды.ШтрихкодУпаковки ЕСТЬ NULL
		|",
		ИмяДокумента,
		ТабличнаяЧастьШтрихкоды.ИмяТаблицыШтрихкодыУпаковок,
		ТабличнаяЧастьШтрихкоды.ИмяКолонкиШтрихкодУпаковки,
		УсловиеВидПродукции);
	КонецЕсли;
	
	ТекстЗапроса = СтрШаблон(ТекстЗапроса, УсловиеВидПродукции);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ЗаполнитьВыводИзОборотаИСМПНаОснованииЧекаККМ(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка           КАК ДокументОснование,
	|	ДокументСсылка.Организация      КАК Организация,
	|	НЕ ДокументСсылка.Проведен      КАК ЕстьОшибкиПроведен,
	|	ФискальныеОперации.НомерЧекаККМ КАК НомерПервчиногоДокумента,
	|	ФискальныеОперации.Дата         КАК ДатаПервчиногоДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВыводИзОборотаРозничнаяПродажа)        КАК Операция,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПервичныхДокументовИСМП.КассовыйЧек) КАК ВидПервичногоДокумента
	|ИЗ
	|	Документ.ЧекККМ КАК ДокументСсылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФискальныеОперации КАК ФискальныеОперации
	|		ПО ФискальныеОперации.ДокументОснование = &ДокументОснование
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование
	|");
	
	ЗаполнитьШапкуМаркировкиТоваровИСМП(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ТабличныеЧасти = Новый Массив;
	ТабличныеЧасти.Добавить(Новый Структура("Товары, ПолеСумма", "Товары", "Сумма"));
	ТабличнаяЧастьШтрихкоды = Новый Структура("ИмяТаблицыШтрихкодыУпаковок, ИмяКолонкиШтрихкодУпаковки", "АкцизныеМарки", "АкцизнаяМарка");
	ЗаполнитьТабличныеЧастиВыводаИзОборотаИСМП(ДокументОбъект, ДанныеЗаполнения, "ЧекККМ", ТабличныеЧасти, ТабличнаяЧастьШтрихкоды);
	
КонецПроцедуры

Процедура ЗаполнитьВыводИзОборотаИСМПНаОснованииРеализацииТоваровУслуг(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка           КАК ДокументОснование,
	|	ДокументСсылка.Организация      КАК Организация,
	|	НЕ ДокументСсылка.Проведен      КАК ЕстьОшибкиПроведен,
	|	ЕСТЬNULL(ФискальныеОперации.НомерЧекаККМ, ДокументСсылка.Номер) КАК НомерПервичногоДокумента,
	|	ЕСТЬNULL(ФискальныеОперации.Дата        , ДокументСсылка.Дата)  КАК ДатаПервичногоДокумента,
	|	ВЫБОР
	|		КОГДА Не ФискальныеОперации.НомерЧекаККМ ЕСТЬ NULL
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВыводИзОборотаРозничнаяПродажа)
	|		КОГДА Контрагенты.НеЯвляетсяРезидентом = ЛОЖЬ
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВыводИзОборотаИспользованиеДляСобственныхНуждПредприятия)
	|		КОГДА ЕСТЬNULL(Контрагенты.СтранаРегистрации.УчастникЕАЭС, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВыводИзОборотаЭкспортВСтраныЕАЭС)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВыводИзОборотаЭкспортЗаПределыСтранЕАЭС) 
	|	КОНЕЦ КАК Операция,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Контрагенты.СтранаРегистрации.УчастникЕАЭС, ИСТИНА)
	|			ТОГДА ДокументСсылка.Контрагент
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|	КОНЕЦ КАК Контрагент,
	
	|	ВЫБОР
	|		КОГДА Не ФискальныеОперации.НомерЧекаККМ ЕСТЬ NULL
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПервичныхДокументовИСМП.КассовыйЧек)
	|		КОГДА Контрагенты.НеЯвляетсяРезидентом = ЛОЖЬ
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПервичныхДокументовИСМП.Прочее)
	|		КОГДА Контрагенты.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПервичныхДокументовИСМП.Прочее)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыПервичныхДокументовИСМП.УПД)
	|	КОНЕЦ КАК ВидПервичногоДокумента
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК ДокументСсылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФискальныеОперации КАК ФискальныеОперации
	|		ПО ФискальныеОперации.ДокументОснование = &ДокументОснование
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО Контрагенты.Ссылка = ДокументСсылка.Контрагент
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование
	|");
		
	ЗаполнитьШапкуВыводаИзОборотаИСМП(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ТабличныеЧасти = Новый Массив;
	ТабличныеЧасти.Добавить(Новый Структура("Товары, ПолеСумма, ПолеСтавкаНДС, ПолеСуммаНДС, ПолеСуммаСНДС, СуммаВключаетНДС", "Товары", "Сумма", "СтавкаНДС", "СуммаНДС", "", ДокументОбъект.ДокументОснование.СуммаВключаетНДС));
	ТабличнаяЧастьШтрихкоды = Новый Структура("ИмяТаблицыШтрихкодыУпаковок, ИмяКолонкиШтрихкодУпаковки", "ШтрихкодыУпаковок", "ШтрихкодУпаковки");
	ЗаполнитьТабличныеЧастиВыводаИзОборотаИСМП(ДокументОбъект, ДанныеЗаполнения, "РеализацияТоваровУслуг", ТабличныеЧасти, ТабличнаяЧастьШтрихкоды);
	
КонецПроцедуры

Процедура ЗаполнитьВыводИзОборотаИСМПНаОснованииОтчетаОРозничныхПродажах(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка           КАК ДокументОснование,
	|	ДокументСсылка.Организация      КАК Организация,
	|	НЕ ДокументСсылка.Проведен      КАК ЕстьОшибкиПроведен,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВыводИзОборотаРозничнаяПродажа)        КАК Операция,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПервичныхДокументовИСМП.КассовыйЧек) КАК ВидПервичногоДокумента
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование
	|");
	
	ЗаполнитьШапкуВыводаИзОборотаИСМП(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ТабличныеЧасти = Новый Массив;
	ТабличныеЧасти.Добавить(Новый Структура("Товары, ПолеСумма, ПолеСтавкаНДС, ПолеСуммаНДС, ПолеСуммаСНДС", "Товары", "Сумма", "СтавкаНДС", "СуммаНДС", "СуммаСНДС"));
	ТабличнаяЧастьШтрихкоды = Новый Структура("ИмяТаблицыШтрихкодыУпаковок, ИмяКолонкиШтрихкодУпаковки", "ШтрихкодыУпаковок", "ШтрихкодУпаковки");
	ЗаполнитьТабличныеЧастиВыводаИзОборотаИСМП(ДокументОбъект, ДанныеЗаполнения, "ОтчетОРозничныхПродажах", ТабличныеЧасти, ТабличнаяЧастьШтрихкоды);
	
КонецПроцедуры

Процедура ЗаполнитьВыводИзОборотаИСМПНаОснованииТребованияНакладной(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка           КАК ДокументОснование,
	|	ДокументСсылка.Организация      КАК Организация,
	|	НЕ ДокументСсылка.Проведен      КАК ЕстьОшибкиПроведен,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВыводИзОборотаИспользованиеДляСобственныхНуждПредприятия) КАК Операция,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПервичныхДокументовИСМП.Прочее)                         КАК ВидПервичногоДокумента
	|ИЗ
	|	Документ.ТребованиеНакладная КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование
	|");
	
	ЗаполнитьШапкуВыводаИзОборотаИСМП(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ТабличныеЧасти = Новый Массив;
	ТабличныеЧасти.Добавить(Новый Структура("Товары,Серии,ПолеСумма", "Материалы", "", ""));
	ЗаполнитьТабличныеЧастиВыводаИзОборотаИСМП(ДокументОбъект, ДанныеЗаполнения, "ТребованиеНакладная", ТабличныеЧасти);
	
КонецПроцедуры


Процедура ЗаполнитьВыводИзОборотаИСМПНаОснованииСписанияНедостачТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка           КАК ДокументОснование,
	|	ДокументСсылка.Организация      КАК Организация,
	|	НЕ ДокументСсылка.Проведен      КАК ЕстьОшибкиПроведен,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВыводИзОборотаУтратаПовреждениеТовара) КАК Операция,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПервичныхДокументовИСМП.Прочее)      КАК ВидПервичногоДокумента
	|ИЗ
	|	Документ.СписаниеТоваров КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование
	|");
	
	ЗаполнитьШапкуВыводаИзОборотаИСМП(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ЗаполнитьТабличныеЧастиВыводаИзОборотаИСМП(ДокументОбъект, ДанныеЗаполнения, "СписаниеТоваров");
	
КонецПроцедуры

Процедура ЗаполнитьВыводИзОборотаИСМПНаОснованииВозвратаТоваровПоставщику(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка           КАК ДокументОснование,
	|	ДокументСсылка.Организация      КАК Организация,
	|	НЕ ДокументСсылка.Проведен      КАК ЕстьОшибкиПроведен,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВыводИзОборотаВозвратФизическомуЛицу)  КАК Операция,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПервичныхДокументовИСМП.УПД) КАК ВидПервичногоДокумента
	|ИЗ
	|	Документ.ВозвратТоваровПоставщику КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование
	|");
	
	ЗаполнитьШапкуВыводаИзОборотаИСМП(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ТабличныеЧасти = Новый Массив;
	ТабличныеЧасти.Добавить(Новый Структура("Товары, ПолеСумма, ПолеСтавкаНДС, ПолеСуммаНДС, ПолеСуммаСНДС", "Товары", "Сумма", "СтавкаНДС", "СуммаНДС", ""));
	ТабличнаяЧастьШтрихкоды = Новый Структура("ИмяТаблицыШтрихкодыУпаковок, ИмяКолонкиШтрихкодУпаковки", "ШтрихкодыУпаковок", "ШтрихкодУпаковки");
	ЗаполнитьТабличныеЧастиВыводаИзОборотаИСМП(ДокументОбъект, ДанныеЗаполнения, "ВозвратТоваровПоставщику", ТабличныеЧасти, ТабличнаяЧастьШтрихкоды);
	
КонецПроцедуры

Процедура ЗаполнитьВыводИзОборотаИСМПНаОснованииСборкиТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка           КАК ДокументОснование,
	|	ДокументСсылка.Организация      КАК Организация,
	|	НЕ ДокументСсылка.Проведен      КАК ЕстьОшибкиПроведен,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВыводИзОборотаИспользованиеДляСобственныхНуждПредприятия) КАК Операция,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПервичныхДокументовИСМП.Прочее)      КАК ВидПервичногоДокумента
	|ИЗ
	|	Документ.КомплектацияНоменклатуры КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование
	|");
	
	ТабличныеЧасти = Новый Массив;
	ТабличныеЧасти.Добавить(Новый Структура("Товары, ПолеСумма", "", ""));
	ЗаполнитьШапкуВыводаИзОборотаИСМП(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ЗаполнитьТабличныеЧастиВыводаИзОборотаИСМП(ДокументОбъект, ДанныеЗаполнения, "КомплектацияНоменклатуры.Комплектующие", ТабличныеЧасти);
	
КонецПроцедуры


#КонецОбласти

#Область ОтгрузкаТоваровИСМП

Процедура ЗаполнитьШапкуОтгрузкиТоваровИСМП(ДокументОбъект, ДанныеЗаполнения, Запрос)
	
	Основание = ДанныеЗаполнения;
	Если ТипЗнч(Основание) = Тип("Структура") Тогда
		Основание = Основание.Основание;
	КонецЕсли;
	
	Если ЭлектронноеВзаимодействиеИСМП.ДокументСвязанСЭлектронным(Основание, Истина) Тогда
		ВызватьИсключение НСтр("ru = 'По текущему документу уже оформлен электронный документ с информацией о маркируемых товарах'");
	КонецЕсли;
	
	ЗаполнитьШапкуДокумента(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
КонецПроцедуры

Процедура ЗаполнитьТабличныеЧастиОтгрузкиТоваровИСМП(
	ДокументОбъект, ДанныеЗаполнения, ИмяДокумента, ТоварыСерии = Неопределено, Штрихкоды = Неопределено)
	
	Если ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьТабличнуюЧасть") Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоОтгрузкаЕАЭСПриОСУ = (ДокументОбъект.Операция = Перечисления.ВидыОперацийИСМП.ОтгрузкаВЕАЭСПриОСУ);
	ЭтоОтгрузкаЕАЭС = (ДокументОбъект.Операция = Перечисления.ВидыОперацийИСМП.ОтгрузкаЕАЭССПризнаниемКИ);
	ФильтрВидПродукцииДо    = ИнтеграцияИСМПКлиентСерверПовтИсп.УчитываемыеВидыМаркируемойПродукции(Истина, Ложь);
	ФильтрВидПродукции = Новый Массив;
	Для Каждого ВидПродукции Из ФильтрВидПродукцииДо Цикл
		Если ВидПродукции = Перечисления.ВидыПродукцииИС.Пиво Тогда
			Продолжить;
		ИначеЕсли (ИнтеграцияИСКлиентСервер.ЭтоМолочнаяПродукцияИСМП(ВидПродукции)
				И Не ЭтоОтгрузкаЕАЭСПриОСУ) Тогда
			Продолжить;
		ИначеЕсли ЭтоОтгрузкаЕАЭС
				И ВидПродукции <> Перечисления.ВидыПродукцииИС.Обувь
				И ВидПродукции <> Перечисления.ВидыПродукцииИС.ЛегкаяПромышленность
				И ВидПродукции <> Перечисления.ВидыПродукцииИС.Шины Тогда
			Продолжить;
		КонецЕсли;
		ФильтрВидПродукции.Добавить(ВидПродукции);
	КонецЦикла;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Если ДанныеЗаполнения.ИмяФильтра = "ЗаполнениеСВидомПродукции" Тогда
			ФильтрВидПродукции = ДанныеЗаполнения.ЗначениеФильтра;
		КонецЕсли;
		Основание = ДанныеЗаполнения.Основание;
	Иначе
		Основание          = ДанныеЗаполнения;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДокументОбъект.ВидПродукции) Тогда
		ФильтрВидПродукции = ДокументОбъект.ВидПродукции;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаЗаполненияОтгрузкиТоваровИСМПИзПрикладногоДокумента(ИмяДокумента, ФильтрВидПродукции, ТоварыСерии, Штрихкоды);
	
	Запрос.УстановитьПараметр("ЭтаСсылка",         ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", Основание);
	Запрос.УстановитьПараметр("КонечныеСтатусы",   Документы.ОтгрузкаТоваровИСМП.КонечныеСтатусы());
	Запрос.УстановитьПараметр("ВидПродукцииИС",    ФильтрВидПродукции);
	
	Пакет = Запрос.ВыполнитьПакет();
	ИндексТовары    = Пакет.Количество() - ?(Штрихкоды = Неопределено, 1, 2);
	ЗаполнитьТабличнуюЧастьДокумента(ДокументОбъект.Товары, Пакет[ИндексТовары], Основание);
	
	Если ТипЗнч(ФильтрВидПродукции) = Тип("Массив") Тогда
		ИнтеграцияИС.ЗаполнитьВидПродукцииПоТабличнойЧасти(ДокументОбъект);
		ОставитьОдинВидПродукции(ДокументОбъект);
	Иначе
		ДокументОбъект.ВидПродукции = ФильтрВидПродукции;
	КонецЕсли;
	
	Если Штрихкоды <> Неопределено Тогда
		
		Если Не ЭтоОтгрузкаЕАЭСПриОСУ Тогда
		ШтрихкодыОснования = Пакет[ИндексТовары+1].Выгрузить().ВыгрузитьКолонку("ШтрихкодУпаковки");
		ШтрихкодыПоВидуПродукции = ИнтеграцияИСУТ.ШтрихкодыСодержащиеВидыПродукции(ШтрихкодыОснования, ДокументОбъект.ВидПродукции);
		Для Каждого ЭлементМассива Из ШтрихкодыПоВидуПродукции Цикл
			ДокументОбъект.ШтрихкодыУпаковок.Добавить().ШтрихкодУпаковки = ЭлементМассива;
		КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция ОписаниеТабличнойЧастиДляФормированияОтгрузкиТоваровИСМП(ИмяТабличнойЧастиСерии = "")
	
	Результат = Новый Структура;
	Результат.Вставить("Товары",         "Товары");
	Результат.Вставить("Серии",          ИмяТабличнойЧастиСерии);
	Результат.Вставить("ПолеСтавкаНДС",  "ТаблицаТовары.СтавкаНДС");
	Результат.Вставить("ПолеКоличество", "ТаблицаТовары.Количество");
	Результат.Вставить("ПолеСумма",      "ТаблицаТовары.Сумма");
	Результат.Вставить("ПолеСуммаСНДС",  "ВЫБОР КОГДА ТаблицаТовары.Ссылка.СуммаВключаетНДС ТОГДА ТаблицаТовары.Сумма ИНАЧЕ ТаблицаТовары.Сумма + ТаблицаТовары.СуммаНДС КОНЕЦ");
	Результат.Вставить("ПолеСуммаНДС",   "ТаблицаТовары.СуммаНДС");
	Возврат Результат;
	
КонецФункции

Функция ТекстЗапросаЗаполненияОтгрузкиТоваровИСМПИзПрикладногоДокумента(ИмяДокумента, ВидПродукции, ТабличныеЧастиТоварыСерии, ТабличнаяЧастьШтрихкоды)
	
	УсловиеВидПродукции = УсловиеПоВидуПродукции(ВидПродукции);
	
	Если ТабличныеЧастиТоварыСерии = Неопределено Тогда
		ТабличныеЧастиТоварыСерии = Новый Массив;
		ТабличныеЧастиТоварыСерии.Добавить(ОписаниеТабличнойЧастиДляФормированияОтгрузкиТоваровИСМП());
	КонецЕсли;
	
	ТоварыСерииПоТабличнымЧастям = Новый Массив;
	ЭтоПервыйЭлемент = Истина;
	Для Каждого ТоварыСерии Из ТабличныеЧастиТоварыСерии Цикл
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТаблицаТовары.Номенклатура        КАК Номенклатура,
		|	ТаблицаТовары.ХарактеристикаНоменклатуры      КАК Характеристика,
		|	ТаблицаТовары.СерияНоменклатуры               КАК Серия,
		|	&ПолеСтавкаНДС                    КАК СтавкаНДС,
		|	СУММА(&ПолеКоличество)            КАК Количество,
		|	СУММА(&ПолеСумма)                 КАК Сумма,
		|	СУММА(&ПолеСуммаНДС)              КАК СуммаНДС,
		|	СУММА(&ПолеСуммаСНДС)             КАК СуммаСНДС
		|ПОМЕСТИТЬ ТоварыСерии
		|ИЗ
		|	&ОписаниеДокументаТовары КАК ТаблицаТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО ТаблицаТовары.Номенклатура = СправочникНоменклатура.Ссылка
		|ГДЕ
		|	ТаблицаТовары.Ссылка = &ДокументОснование
		|	И &УсловиеПоВидуПродукции
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаТовары.Номенклатура,
		|	ТаблицаТовары.ХарактеристикаНоменклатуры,
		|	ТаблицаТовары.СерияНоменклатуры,
		|	&ПолеСтавкаНДС
		|;";
		Если ТоварыСерии.Серии <> "" Тогда
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ТаблицаТовары.Номенклатура        КАК Номенклатура,
			|	ТаблицаТовары.ХарактеристикаНоменклатуры      КАК Характеристика,
			|	ТаблицаТовары.СерияНоменклатуры               КАК Серия,
			|	&ПолеСтавкаНДС                    КАК СтавкаНДС,
			|	СУММА(&ПолеКоличество)            КАК Количество,
			|	СУММА(&ПолеСумма)                 КАК Сумма,
			|	СУММА(&ПолеСуммаНДС)              КАК СуммаНДС,
			|	СУММА(&ПолеСуммаСНДС)             КАК СуммаСНДС
			|ПОМЕСТИТЬ ТоварыСерии
			|ИЗ
			|	&ОписаниеДокументаТовары КАК ТаблицаТовары
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
			|		ПО ТаблицаТовары.Номенклатура = СправочникНоменклатура.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ &ОписаниеДокументаСерии КАК ТаблицаСерии
			|		ПО ТаблицаТовары.Номенклатура = ТаблицаСерии.Номенклатура
			|		И ТаблицаТовары.ХарактеристикаНоменклатуры = ТаблицаСерии.ХарактеристикаНоменклатуры
			|		И ТаблицаСерии.Ссылка = &ДокументОснование
			|ГДЕ
			|	ТаблицаТовары.Ссылка = &ДокументОснование
			|	И ТаблицаСерии.Ссылка ЕСТЬ NULL
			|	И &УсловиеПоВидуПродукции
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаТовары.Номенклатура,
			|	ТаблицаТовары.ХарактеристикаНоменклатуры,
			|	ТаблицаТовары.СерияНоменклатуры,
			|	&ПолеСтавкаНДС
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТаблицаСерии.Номенклатура,
			|	ТаблицаСерии.ХарактеристикаНоменклатуры,
			|	ТаблицаСерии.Серия,
			|	ТаблицаТовары.СтавкаНДС,
			|	СУММА(ТаблицаСерии.Количество),
			|	СУММА(ВЫБОР КОГДА ТаблицаТовары.Количество = 0 ТОГДА 0
			|		ИНАЧЕ ТаблицаСерии.Количество / ТаблицаТовары.Количество КОНЕЦ * ТаблицаТовары.Сумма),
			|	СУММА(ВЫБОР КОГДА ТаблицаТовары.Количество = 0 ТОГДА 0
			|		ИНАЧЕ ТаблицаСерии.Количество / ТаблицаТовары.Количество КОНЕЦ * ТаблицаТовары.СуммаНДС),
			|	СУММА(ВЫБОР КОГДА ТаблицаТовары.Количество = 0 ТОГДА 0
			|		ИНАЧЕ ТаблицаСерии.Количество / ТаблицаТовары.Количество КОНЕЦ * ТаблицаТовары.СуммаСНДС)
			|ИЗ
			|	&ОписаниеДокументаСерии КАК ТаблицаСерии
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|		(ВЫБРАТЬ
			|			ТаблицаТовары.Номенклатура    КАК Номенклатура,
			|			ТаблицаТовары.ХарактеристикаНоменклатуры  КАК Характеристика,
			|			МАКСИМУМ(&ПолеСтавкаНДС)      КАК СтавкаНДС,
			|			СУММА(&ПолеКоличество)        КАК Количество,
			|			СУММА(&ПолеСумма)             КАК Сумма,
			|			СУММА(&ПолеСуммаНДС)          КАК СуммаНДС,
			|			СУММА(&ПолеСуммаСНДС)         КАК СуммаСНДС
			|		ИЗ
			|			&ОписаниеДокументаТовары КАК ТаблицаТовары
			|		ГДЕ
			|			ТаблицаТовары.Ссылка = &ДокументОснование
			|		СГРУППИРОВАТЬ ПО
			|			ТаблицаТовары.Номенклатура,
			|			ТаблицаТовары.ХарактеристикаНоменклатуры) КАК ТаблицаТовары
			|		ПО ТаблицаТовары.Номенклатура = ТаблицаСерии.Номенклатура
			|		И ТаблицаТовары.ХарактеристикаНоменклатуры = ТаблицаСерии.ХарактеристикаНоменклатуры
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
			|		ПО ТаблицаСерии.Номенклатура = СправочникНоменклатура.Ссылка
			|ГДЕ
			|	ТаблицаСерии.Ссылка = &ДокументОснование
			|	И &УсловиеПоВидуПродукции
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаСерии.Номенклатура,
			|	ТаблицаСерии.ХарактеристикаНоменклатуры,
			|	ТаблицаСерии.Серия,
			|	ТаблицаТовары.СтавкаНДС
			|;";
		КонецЕсли;
		
		Если ЭтоПервыйЭлемент Тогда
			ЭтоПервыйЭлемент = Ложь;
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ТоварыСерии", "");
		КонецЕсли;
		
		ОписаниеДокумента = СтрШаблон("Документ.%1", ИмяДокумента);
		Если ЗначениеЗаполнено(ТоварыСерии.Товары) Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&ОписаниеДокументаТовары", СтрШаблон("%1.%2", ОписаниеДокумента,ТоварыСерии.Товары));
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&ОписаниеДокументаТовары", ОписаниеДокумента);
		КонецЕсли;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОписаниеДокументаСерии", СтрШаблон("%1.%2", ОписаниеДокумента,ТоварыСерии.Серии));
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолеСтавкаНДС",          ТоварыСерии.ПолеСтавкаНДС);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолеКоличество",         ТоварыСерии.ПолеКоличество);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолеСуммаНДС",           ТоварыСерии.ПолеСуммаНДС);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолеСуммаСНДС",          ТоварыСерии.ПолеСуммаСНДС);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолеСумма",              ТоварыСерии.ПолеСумма);
		
		ТоварыСерииПоТабличнымЧастям.Добавить(ТекстЗапроса);
		
	КонецЦикла;
	
	
	ТекстЗапроса = СтрСоединить(ТоварыСерииПоТабличнымЧастям, "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|");
	ТекстЗапроса = ТекстЗапроса + "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура        КАК Номенклатура,
	|	Товары.Характеристика      КАК Характеристика,
	|	Товары.Серия               КАК Серия,
	|	Товары.СтавкаНДС           КАК СтавкаНДС,
	|	Товары.Количество          КАК Количество,
	|	Товары.Сумма               КАК Сумма,
	|	Товары.СуммаНДС            КАК СуммаНДС,
	|	Товары.СуммаСНДС           КАК СуммаСНДС
	|	
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	ТоварыСерии КАК Товары
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура,
	|	ОформленныеТовары.Характеристика,
	|	ОформленныеТовары.Серия,
	|	ОформленныеТовары.СтавкаНДС,
	|	-ОформленныеТовары.Количество,
	|	-ОформленныеТовары.Сумма,
	|	-ОформленныеТовары.СуммаНДС,
	|	-ОформленныеТовары.СуммаСНДС
	|ИЗ
	|	Документ.ОтгрузкаТоваровИСМП.Товары КАК ОформленныеТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовИСМП КАК СтатусыДокументовИСМП
	|		ПО СтатусыДокументовИСМП.Документ = ОформленныеТовары.Ссылка
	|ГДЕ
	|	ОформленныеТовары.Ссылка.Проведен
	|	И ОформленныеТовары.Ссылка.ДокументОснование = &ДокументОснование
	|	И ОформленныеТовары.Ссылка <> &ЭтаСсылка
	|	И СтатусыДокументовИСМП.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиОтгрузкиТоваровИСМП.Аннулирован)
	|	И СтатусыДокументовИСМП.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиОтгрузкиТоваровИСМП.ОтклоненКлиентом)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура      КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика    КАК Характеристика,
	|	ТоварыКОформлению.Серия             КАК Серия,
	|	ТоварыКОформлению.СтавкаНДС         КАК СтавкаНДС,
	|	СУММА(ТоварыКОформлению.Количество) КАК Количество,
	|	СУММА(ТоварыКОформлению.Количество) КАК КоличествоУпаковок,
	|	СУММА(ТоварыКОформлению.Сумма)      КАК Сумма,
	|	СУММА(ТоварыКОформлению.СуммаНДС)   КАК СуммаНДС,
	|	СУММА(ТоварыКОформлению.СуммаСНДС)  КАК СуммаСНДС,
	|	ВЫБОР КОГДА СУММА(ТоварыКОформлению.Количество) > 0 И СУММА(ТоварыКОформлению.Сумма) > 0
	|		ТОГДА СУММА(ТоварыКОформлению.Сумма) / СУММА(ТоварыКОформлению.Количество)
	|	ИНАЧЕ 0 КОНЕЦ                       КАК Цена
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	ТоварыКОформлению.СтавкаНДС
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0
	|;
	|";
	
	Если ТабличнаяЧастьШтрихкоды <> Неопределено Тогда
		ТекстЗапроса = ТекстЗапроса + СтрШаблон(
		"ВЫБРАТЬ
		|	Штрихкоды.%3 КАК ШтрихкодУпаковки
		|ИЗ
		|	Документ.%1.%2 КАК Штрихкоды
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровИСМП.ШтрихкодыУпаковок КАК ОформленныеШтрихкоды
		|		ПО ОформленныеШтрихкоды.Ссылка.ДокументОснование = &ДокументОснование
		|		И ОформленныеШтрихкоды.Ссылка <> &ЭтаСсылка
		|		И ОформленныеШтрихкоды.ШтрихкодУпаковки = Штрихкоды.%3
		|		И ОформленныеШтрихкоды.Ссылка.Проведен
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО Штрихкоды.%3.Номенклатура = СправочникНоменклатура.Ссылка
		|ГДЕ
		|	Штрихкоды.Ссылка = &ДокументОснование
		|	И ОформленныеШтрихкоды.ШтрихкодУпаковки ЕСТЬ NULL
		|	И &УсловиеПоВидуПродукции
		|",
		ИмяДокумента,
		ТабличнаяЧастьШтрихкоды.ИмяТаблицыШтрихкодыУпаковок,
		ТабличнаяЧастьШтрихкоды.ИмяКолонкиШтрихкодУпаковки,
		);
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоВидуПродукции", УсловиеВидПродукции);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ЗаполнитьОтгрузкуТоваровИСМПНаОснованииРеализацииТоваровУслуг(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка           КАК ДокументОснование,
	|	ДокументСсылка.Организация      КАК Организация,
	|	ДокументСсылка.Контрагент       КАК Контрагент,
	|	НЕ ДокументСсылка.Проведен      КАК ЕстьОшибкиПроведен,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ОтгрузкаПродажа) КАК Операция,
	|	ДокументСсылка.Номер            КАК НомерПервичногоДокумента,
	|	ДокументСсылка.Дата             КАК ДатаПервичногоДокумента,
	|	ДокументСсылка.Комментарий      КАК Комментарий,
	|	ДокументСсылка.Дата             КАК ДатаОтгрузки
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование
	|");
	
	ЗаполнитьШапкуОтгрузкиТоваровИСМП(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ТабличнаяЧастьШтрихкоды = Новый Структура("ИмяТаблицыШтрихкодыУпаковок, ИмяКолонкиШтрихкодУпаковки", "ШтрихкодыУпаковок", "ШтрихкодУпаковки");
	ЗаполнитьТабличныеЧастиОтгрузкиТоваровИСМП(ДокументОбъект, ДанныеЗаполнения, "РеализацияТоваровУслуг",, ТабличнаяЧастьШтрихкоды);
	
КонецПроцедуры

Процедура ЗаполнитьОтгрузкуТоваровИСМПНаОснованииВозвратаТоваровПоставщику(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка           КАК ДокументОснование,
	|	ДокументСсылка.Организация      КАК Организация,
	|	ДокументСсылка.Контрагент       КАК Контрагент,
	|	НЕ ДокументСсылка.Проведен      КАК ЕстьОшибкиПроведен,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ОтгрузкаПродажа) КАК Операция,
	|	ДокументСсылка.Номер            КАК НомерПервичногоДокумента,
	|	ДокументСсылка.Дата             КАК ДатаПервичногоДокумента,
	|	ДокументСсылка.Комментарий      КАК Комментарий,
	|	ДокументСсылка.Дата             КАК ДатаОтгрузки
	|ИЗ
	|	Документ.ВозвратТоваровПоставщику КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование
	|");
	
	ЗаполнитьШапкуОтгрузкиТоваровИСМП(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ТабличнаяЧастьШтрихкоды = Новый Структура("ИмяТаблицыШтрихкодыУпаковок, ИмяКолонкиШтрихкодУпаковки", "ШтрихкодыУпаковок", "ШтрихкодУпаковки");
	ЗаполнитьТабличныеЧастиОтгрузкиТоваровИСМП(ДокументОбъект, ДанныеЗаполнения, "ВозвратТоваровПоставщику",, ТабличнаяЧастьШтрихкоды);
	
КонецПроцедуры

#КонецОбласти

#Область ПеремаркировкаТоваровИСМП

Процедура ЗаполнитьШапкуПеремаркировкиТоваровИСМП(ДокументОбъект, ДанныеЗаполнения, Запрос)
	
	ЗаполнитьШапкуДокумента(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
КонецПроцедуры

Процедура ЗаполнитьТабличнуюЧастьПеремаркировкиТоваровИСМП(
	ДокументОбъект, ДанныеЗаполнения, ИмяДокумента, Штрихкоды = Неопределено)
	
	Если ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьТабличнуюЧасть") Тогда
		Возврат;
	КонецЕсли;
	
	Если Штрихкоды = Неопределено Тогда
		Штрихкоды = Новый Структура;
		Штрихкоды.Вставить("ИмяТаблицыШтрихкодыУпаковок", "ШтрихкодыУпаковок");
		Штрихкоды.Вставить("ИмяКолонкиШтрихкодУпаковки",  "ШтрихкодУпаковки");
	КонецЕсли;
	
	ДокументОбъект.Товары.Очистить();
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Если ДанныеЗаполнения.ИмяФильтра = "ЗаполнениеСВидомПродукции" Тогда
			ФильтрВидПродукции = ДанныеЗаполнения.ЗначениеФильтра;
		КонецЕсли;
		Основание = ДанныеЗаполнения.Основание;
	Иначе
		Основание          = ДанныеЗаполнения;
		Если ЗначениеЗаполнено(ДокументОбъект.ВидПродукции) Тогда
			ФильтрВидПродукции = ДокументОбъект.ВидПродукции;
		Иначе
			ФильтрВидПродукции = ИнтеграцияИСКлиентСервер.ВидыПродукцииИСМП(Ложь, Ложь);
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаЗаполненияПеремаркировкиТоваровИСМПИзПрикладногоДокумента(ИмяДокумента, ФильтрВидПродукции, Штрихкоды);
	
	Запрос.УстановитьПараметр("ЭтаСсылка",       ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("Основание",       Основание);
	Запрос.УстановитьПараметр("КонечныеСтатусы", Документы.ПеремаркировкаТоваровИСМП.КонечныеСтатусы());
	Запрос.УстановитьПараметр("ВидПродукцииИС",  ФильтрВидПродукции);
	
	Выборка = Запрос.Выполнить();
	ЗаполнитьТабличнуюЧастьДокумента(ДокументОбъект.Товары, Выборка, ДанныеЗаполнения);
	
	Если ТипЗнч(ФильтрВидПродукции) = Тип("Массив") Тогда
		ИнтеграцияИС.ЗаполнитьВидПродукцииПоТабличнойЧасти(ДокументОбъект);
		ОставитьОдинВидПродукции(ДокументОбъект);
	Иначе
		ДокументОбъект.ВидПродукции = ФильтрВидПродукции;
	КонецЕсли;
	
КонецПроцедуры

Функция ТекстЗапросаЗаполненияПеремаркировкиТоваровИСМПИзПрикладногоДокумента(ИмяДокумента, ВидПродукции, ТабличнаяЧастьШтрихкоды)
	
	УсловиеВидПродукции = УсловиеПоВидуПродукции(ВидПродукции);
	
	ТекстЗапроса = СтрШаблон(
	"ВЫБРАТЬ
	|	ШтрихкодыУпаковокТоваров.Ссылка         КАК КодМаркировки,
	|	ШтрихкодыУпаковокТоваров.Номенклатура   КАК Номенклатура,
	|	ШтрихкодыУпаковокТоваров.Характеристика КАК Характеристика,
	|	ШтрихкодыУпаковокТоваров.Номенклатура   КАК НоваяНоменклатура,
	|	ШтрихкодыУпаковокТоваров.Характеристика КАК НоваяХарактеристика
	|ИЗ
	|	Документ.%1.%2 КАК Штрихкоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ШтрихкодыУпаковокТоваров КАК ШтрихкодыУпаковокТоваров
	|		ПО Штрихкоды.%3 = ШтрихкодыУпаковокТоваров.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ШтрихкодыУпаковокТоваров.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПеремаркировкаТоваровИСМП.Товары КАК ОформленныеШтрихкоды
	|		ПО ОформленныеШтрихкоды.Ссылка.ДокументОснование = &Основание
	|		И ОформленныеШтрихкоды.Ссылка <> &ЭтаСсылка
	|		И ОформленныеШтрихкоды.КодМаркировки = ШтрихкодыУпаковокТоваров.Ссылка
	|ГДЕ
	|	Штрихкоды.Ссылка = &Основание
	|	И ОформленныеШтрихкоды.КодМаркировки ЕСТЬ NULL
	|	И %4
	|",
	ИмяДокумента,
	ТабличнаяЧастьШтрихкоды.ИмяТаблицыШтрихкодыУпаковок,
	ТабличнаяЧастьШтрихкоды.ИмяКолонкиШтрихкодУпаковки,
	УсловиеВидПродукции);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ЗаполнитьСписаниеКодовМаркировкиИСМПНаОснованииВозвратаТоваровОтКлиента(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка           КАК ДокументОснование,
	|	ДокументСсылка.Организация      КАК Организация,
	|	НЕ ДокументСсылка.Проведен      КАК ЕстьОшибкиПроведен,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.Перемаркировка) КАК Операция
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование
	|");
	
	ЗаполнитьШапкуПеремаркировкиТоваровИСМП(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ЗаполнитьТабличнуюЧастьПеремаркировкиТоваровИСМП(ДокументОбъект, ДанныеЗаполнения, "ВозвратТоваровОтПокупателя");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ЗаполнениеДобавленныхИзмененныхСтрок

Функция СтруктураДействийПриДобавленииНовойСтрокиПриобретениеТоваровУслуг(Объект)
	
	СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСерверИСУТ.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(Объект);
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ЗаполнитьНоменклатуруПоставщикаПоНоменклатуре", Объект.Контрагент);
	
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Истина));
	СтруктураДействий.Вставить("ОчиститьСуммуВзаиморасчетов");
	
	Возврат СтруктураДействий;
	
КонецФункции

Функция СтруктураДействийПриДобавленииНовойСтрокиВозвратТоваровОтКлиента(Объект)

	СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСерверИСУТ.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(Объект);
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСумму");
	
	Возврат СтруктураДействий;
	
	
КонецФункции

Функция СтруктураДействийПриДобавленииНовойСтроки(ДокументОбъект)

	Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ПоступлениеТоваровУслуг") Тогда
		
		СтруктураДействий = СтруктураДействийПриДобавленииНовойСтрокиПриобретениеТоваровУслуг(ДокументОбъект);
		
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ВозвратТоваровОтПокупателя") Тогда
		
		СтруктураДействий = СтруктураДействийПриДобавленииНовойСтрокиВозвратТоваровОтКлиента(ДокументОбъект);
		
	Иначе
		
		СтруктураДействий = Новый Структура;
		
	КонецЕсли;
	
	Возврат СтруктураДействий;
	
КонецФункции

Процедура ОбработатьСтрокуТабличнойЧасти(СтрокаТабличнойЧасти, СтруктураДействий)

	ТипСтрокиТабличнойЧасти = ТипЗнч(СтрокаТабличнойЧасти);
	
	Если ТипСтрокиТабличнойЧасти = Тип("СтрокаТаблицыЗначений") Тогда
		СтрокаТабличнойЧастиСтруктурой = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(СтрокаТабличнойЧасти);
	ИначеЕсли Метаданные.НайтиПоТипу(ТипСтрокиТабличнойЧасти) <> Неопределено Тогда
		СтрокаТабличнойЧастиСтруктурой = Новый Структура();
		ПодстрокиИмени = СтрРазделить(Метаданные.НайтиПоТипу(ТипСтрокиТабличнойЧасти).ПолноеИмя(), ".");
		
		Если ПодстрокиИмени.Количество() = 4 Тогда
			РеквизитыСтроки = Метаданные.Документы[ПодстрокиИмени[1]].ТабличныеЧасти[ПодстрокиИмени[3]].Реквизиты;
			Для Каждого РеквизитСтроки Из РеквизитыСтроки Цикл
				СтрокаТабличнойЧастиСтруктурой.Вставить(РеквизитСтроки.Имя, СтрокаТабличнойЧасти[РеквизитСтроки.Имя]);
			КонецЦикла;
		Иначе
			Возврат;
		КонецЕсли;
	Иначе
		Возврат;
	КонецЕсли;

	СтрокаТабличнойЧастиСтруктурой.Вставить("ТипНоменклатуры", Перечисления.ТипыНоменклатуры.Товар);
	СтрокаТабличнойЧастиСтруктурой.Вставить("КоличествоУпаковок", СтрокаТабличнойЧастиСтруктурой.Количество);
	ОбработкаТабличнойЧастиСерверИСУТ.ОбработатьСтрокуТЧ(СтрокаТабличнойЧастиСтруктурой, СтруктураДействий, Неопределено);
	ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, СтрокаТабличнойЧастиСтруктурой);
	
КонецПроцедуры

Процедура ОбработатьСтрокуТабличнойЧастиДокумента(СтрокаТабличнойЧасти, ДокументОбъект, Действие)
	
	Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.РеализацияТоваровУслуг") Тогда
		
		ОбработатьСтрокуТабличнойЧастиРеализацияТоваровУслуг(СтрокаТабличнойЧасти, ДокументОбъект, Действие);
		
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ВозвратТоваровПоставщику") Тогда
		
		ОбработатьСтрокуТабличнойЧастиВозвратТоваровПоставщику(СтрокаТабличнойЧасти, ДокументОбъект, Действие);
		
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.КорректировкаРеализации") Тогда
		
		ОбработатьСтрокуТабличнойЧастиКорректировкаРеализации(СтрокаТабличнойЧасти, ДокументОбъект, Действие);
		
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ВозвратТоваровОтПокупателя") Тогда
		
		ОбработатьСтрокуТабличнойЧастиВозвратТоваровОтПокупателя(СтрокаТабличнойЧасти, ДокументОбъект, Действие);
		
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ПоступлениеТоваровУслуг") Тогда
		
		ОбработатьСтрокуТабличнойЧастиПоступлениеТоваровУслуг(СтрокаТабличнойЧасти, ДокументОбъект, Действие);
		
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ЧекККМ") Тогда
		
		ОбработатьСтрокуТабличнойЧастиЧекККМ(СтрокаТабличнойЧасти, ДокументОбъект, Действие);
		
	КонецЕсли;
	
КонецПроцедуры


Процедура ОбработатьСтрокуТабличнойЧастиРеализацияТоваровУслуг(СтрокаТабличнойЧасти, ДокументОбъект, Действие)
	
	Если Действие = "Добавление" Тогда
		
		// Выполнить общие действия для всех документов при изменении номенклатуры.
		ОбработкаТабличныхЧастей.ПриИзмененииНоменклатурыТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект);
		//ПриИзмененииНоменклатурыТоваров//(СтрокаТабличнойЧасти);
		
		// Заполняем реквизиты табличной части.
		ОбработкаТабличныхЧастей.ЗаполнитьСтавкуНДСТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект, "Реализация");
		ОбработкаТабличныхЧастей.ЗаполнитьСпособСписанияОстаткаТоваровТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект);
		
		// Заполнение цены зависит от розничности операции.
		ОбработкаТабличныхЧастей.ЗаполнитьЕдиницуЦенуПродажиТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект, ДокументОбъект.мВалютаРегламентированногоУчета); 
		
		// Рассчитать реквизиты табличной части.
		ОбработкаТабличныхЧастей.РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект);
		ОбработкаТабличныхЧастей.РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект);
		ОбработкаТабличныхЧастей.ЗаполнитьКодТНВЭД(СтрокаТабличнойЧасти);
		
		УчетСерийныхНомеров.ПроверитьСерийныеНомера(СтрокаТабличнойЧасти, ДокументОбъект);
		
		// Ввод состава набора
		УправлениеЗапасами.ДобавитьСоставНабора(СтрокаТабличнойЧасти, ДокументОбъект);
		
	ИначеЕсли Действие = "Изменение" Тогда
		
		// Рассчитать реквизиты табличной части.
		ОбработкаТабличныхЧастей.РассчитатьКоличествоМестТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект);
		
		// Рассчитать реквизиты табличной части.
		ОбработкаТабличныхЧастей.РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект);
		ОбработкаТабличныхЧастей.РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьСтрокуТабличнойЧастиВозвратТоваровПоставщику(СтрокаТабличнойЧасти, ДокументОбъект, Действие)
	
	Если Действие = "Добавление" Тогда
		
		// Выполнить общие действия для всех документов при изменении номенклатуры.
		ОбработкаТабличныхЧастей.ПриИзмененииНоменклатурыТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект);
		
		ОбработкаТабличныхЧастей.ЗаполнитьКодТНВЭД(СтрокаТабличнойЧасти);
		
		// Заполняем реквизиты табличной части.
		ОбработкаТабличныхЧастей.ЗаполнитьСтавкуНДСТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект, "Приобретение"); 
		СтруктураШапкиДокумента = Новый Структура("Контрагент, ТипЦен, ДоговорКонтрагента, ДатаДокумента, ВалютаДокумента, УчитыватьНДС, СуммаВключаетНДС",
		ДокументОбъект.Контрагент, ДокументОбъект.ТипЦен, ДокументОбъект.ДоговорКонтрагента, ДокументОбъект.Дата, ДокументОбъект.ВалютаДокумента, ДокументОбъект.УчитыватьНДС, ДокументОбъект.СуммаВключаетНДС);
		
		ОбработкаТабличныхЧастей.ЗаполнитьЕдиницуЦенуПокупкиТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект, СтруктураШапкиДокумента, ДокументОбъект.мВалютаРегламентированногоУчета); 
		
		СтрокаТабличнойЧасти.Качество = Справочники.Качество.Новый;
		
		// Рассчитываем реквизиты табличной части.
		ОбработкаТабличныхЧастей.РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект);
		ОбработкаТабличныхЧастей.РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект);
		
		УчетСерийныхНомеров.ПроверитьСерийныеНомера(СтрокаТабличнойЧасти, ДокументОбъект);
		
	ИначеЕсли Действие = "Изменение" Тогда
		
		ОбработкаТабличныхЧастей.РассчитатьКоличествоМестТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект);
		ОбработкаТабличныхЧастей.РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект);
		ОбработкаТабличныхЧастей.РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьСтрокуТабличнойЧастиКорректировкаРеализации(СтрокаТабличнойЧасти, ДокументОбъект, Действие)
	
	Если Действие = "Добавление" Тогда
		
		ИмяТабличнойЧасти = "Товары";
		
		// Выполнить общие действия для всех документов при изменении номенклатуры.
		ОбработкаТабличныхЧастей.ПриИзмененииНоменклатурыТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект);
		
		ОбработкаТабличныхЧастей.ЗаполнитьКодТНВЭД(СтрокаТабличнойЧасти);
		
		// Заполняем реквизиты табличной части.
		Если ТипЗнч(СтрокаТабличнойЧасти.Номенклатура) = Тип("СправочникСсылка.Номенклатура") Тогда
			ОбработкаТабличныхЧастей.ЗаполнитьСтавкуНДСТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект, "Реализация");
			ОбработкаТабличныхЧастей.ЗаполнитьЕдиницуЦенуПродажиТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект, ДокументОбъект.мВалютаРегламентированногоУчета); 
		Иначе
			СтрокаТабличнойЧасти.Коэффициент = 1;
		КонецЕсли;
		
		// Рассчитать реквизиты табличной части.
		
		СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Цена * СтрокаТабличнойЧасти.Количество;
		
		СтавкаНДС = УчетНДС.ПолучитьСтавкуНДС(СтрокаТабличнойЧасти.СтавкаНДС);
		СтрокаТабличнойЧасти.СуммаНДС = УчетНДС.РассчитатьСуммуНДС(
		СтрокаТабличнойЧасти.Сумма, ДокументОбъект.УчитыватьНДС, ДокументОбъект.СуммаВключаетНДС, СтавкаНДС);
		
		ОбработкаТабличныхЧастей.ЗаполнитьСпособСписанияОстаткаТоваровТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект);
		
		УчетСерийныхНомеров.ПроверитьСерийныеНомера(СтрокаТабличнойЧасти, ДокументОбъект);
		
		// Ввод состава набора
		Если ТипЗнч(СтрокаТабличнойЧасти.Номенклатура) = Тип("СправочникСсылка.Номенклатура") Тогда
			УправлениеЗапасами.ДобавитьСоставНабора(СтрокаТабличнойЧасти, ДокументОбъект);
		КонецЕсли;
		
	ИначеЕсли Действие = "Изменение" Тогда
		
		Если ТипЗнч(СтрокаТабличнойЧасти.Номенклатура) = Тип("СправочникСсылка.Номенклатура") Тогда // изменен реквизит Количество
			ОбработкаТабличныхЧастей.РассчитатьКоличествоМестТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект);
		КонецЕсли;
		
		// Рассчитать реквизиты табличной части.
		СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Цена * СтрокаТабличнойЧасти.Количество;
		
		СтавкаНДС = УчетНДС.ПолучитьСтавкуНДС(СтрокаТабличнойЧасти.СтавкаНДС);
		СтрокаТабличнойЧасти.СуммаНДС = УчетНДС.РассчитатьСуммуНДС(
		СтрокаТабличнойЧасти.Сумма, ДокументОбъект.УчитыватьНДС, ДокументОбъект.СуммаВключаетНДС, СтавкаНДС);
		
		Если СтрокаТабличнойЧасти.Количество <> СтрокаТабличнойЧасти.КоличествоДоИзменения Тогда
			СтрокаТабличнойЧасти.ОтражатьТоварныйУчет = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьСтрокуТабличнойЧастиВозвратТоваровОтПокупателя(СтрокаТабличнойЧасти, ДокументОбъект, Действие)
	
	Если Действие = "Добавление" Тогда
		
		// Выполнить общие действия для всех документов при изменении номенклатуры.
		ОбработкаТабличныхЧастей.ПриИзмененииНоменклатурыТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект);
		
		// Заполняем реквизиты табличной части.
		ОбработкаТабличныхЧастей.ЗаполнитьСтавкуНДСТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект, "Реализация"); 
		ОбработкаТабличныхЧастей.ЗаполнитьЕдиницуЦенуПродажиТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект, ДокументОбъект.мВалютаРегламентированногоУчета); 
		
		// Рассчитываем реквизиты табличной части.
		ОбработкаТабличныхЧастей.РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект);
		ОбработкаТабличныхЧастей.РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект);
		
		УчетСерийныхНомеров.ПроверитьСерийныеНомера(СтрокаТабличнойЧасти, ДокументОбъект);
		
		// Ввод состава набора
		УправлениеЗапасами.ДобавитьСоставНабора(СтрокаТабличнойЧасти, ДокументОбъект);
		
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьСтрокуТабличнойЧастиПоступлениеТоваровУслуг(СтрокаТабличнойЧасти, ДокументОбъект, Действие)
	
	Если Действие = "Добавление" Тогда
		
		// Выполнить общие действия для всех документов при изменении номенклатуры.
		ОбработкаТабличныхЧастей.ПриИзмененииНоменклатурыТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект);
		
		// Заполняем реквизиты табличной части.
		ОбработкаТабличныхЧастей.ЗаполнитьСтавкуНДСТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект, "Приобретение"); 
		СтруктураШапкиДокумента = Новый Структура("Контрагент, ТипЦен, ДоговорКонтрагента, ДатаДокумента, ВалютаДокумента, УчитыватьНДС, СуммаВключаетНДС",
		ДокументОбъект.Контрагент, ДокументОбъект.ТипЦен, ДокументОбъект.ДоговорКонтрагента, ДокументОбъект.Дата, ДокументОбъект.ВалютаДокумента, ДокументОбъект.УчитыватьНДС, ДокументОбъект.СуммаВключаетНДС);
		ОбработкаТабличныхЧастей.ЗаполнитьЕдиницуЦенуПокупкиТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект, СтруктураШапкиДокумента, ДокументОбъект.мВалютаРегламентированногоУчета);
		
		// Рассчитываем реквизиты табличной части.
		ОбработкаТабличныхЧастей.РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект);
		ОбработкаТабличныхЧастей.РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект);
				
	ИначеЕсли Действие = "Изменение" Тогда
		
		// Рассчитать реквизиты табличной части.
		ОбработкаТабличныхЧастей.РассчитатьКоличествоМестТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект);
		
		// Рассчитать реквизиты табличной части.
		ОбработкаТабличныхЧастей.РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект);
		ОбработкаТабличныхЧастей.РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьСтрокуТабличнойЧастиЧекККМ(СтрокаТабличнойЧасти, ДокументОбъект, Действие)
	
	Если Действие = "Добавление" Тогда
		
		// Выполнить общие действия для всех документов при изменении номенклатуры.
		ОбработкаТабличныхЧастей.ПриИзмененииНоменклатурыТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект);
		//ПриИзмененииНоменклатурыТоваров//(СтрокаТабличнойЧасти);
		
		
		// Заполнение цены зависит от розничности операции.
		ОбработкаТабличныхЧастей.ЗаполнитьЕдиницуЦенуПродажиТабЧасти(
			СтрокаТабличнойЧасти,
			ДокументОбъект,
			ДокументОбъект.мВалютаРегламентированногоУчета,
			ДокументОбъект.мВалютаРегламентированногоУчета,
			,
			Истина);
		
		// Рассчитать реквизиты табличной части.
		ОбработкаТабличныхЧастей.РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект);
		
		УчетСерийныхНомеров.ПроверитьСерийныеНомера(СтрокаТабличнойЧасти, ДокументОбъект);
		
		// Ввод состава набора
		УправлениеЗапасами.ДобавитьСоставНабора(СтрокаТабличнойЧасти, ДокументОбъект);
		
	ИначеЕсли Действие = "Изменение" Тогда
		
		// Рассчитать реквизиты табличной части.
		ОбработкаТабличныхЧастей.РассчитатьКоличествоМестТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект);
		
		// Рассчитать реквизиты табличной части.
		ОбработкаТабличныхЧастей.РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект);
		ОбработкаТабличныхЧастей.РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, ДокументОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьИзменитьКодМаркировкиКегаНаОборудованииРозлива(Форма, Товары, АкцизныеМарки, СтрокаТабличнойЧасти) Экспорт
	
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Номенклатура)
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТабличнойЧасти.Номенклатура, "ВидПродукцииИС") = Перечисления.ВидыПродукцииИС.Пиво Тогда
		
		ПараметрыСканирования = ШтрихкодированиеИС.ПараметрыСканирования(Форма);
		
		ТаблицаТоваров = Товары.Выгрузить();
		ТаблицаТоваров.Колонки.Найти("СерияНоменклатуры").Имя = "Серия";
		Результат = ШтрихкодированиеИСМП.ДобавитьИзменитьКодМаркировкиКегаНаОборудованииРозлива(
			ПараметрыСканирования,
			ТаблицаТоваров,
			АкцизныеМарки);
		
		Если Результат.ЕстьИзменения Тогда
			
			Для Каждого КлючИЗначение Из Результат.ДобавленныеКодыМаркировки Цикл
				НоваяСтрока = АкцизныеМарки.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, КлючИЗначение.Значение);
				НоваяСтрока.АкцизнаяМарка = НоваяСтрока.ШтрихкодУпаковки;
			КонецЦикла;
			Для Каждого КлючИЗначение Из Результат.ИзмененныеКодыМаркировки Цикл
				СтруктураПоиска = Новый Структура("АкцизнаяМарка");
				ЗаполнитьЗначенияСвойств(СтруктураПоиска, КлючИЗначение.Значение);
				ПоискСтрок = АкцизныеМарки.НайтиСтроки(СтруктураПоиска);
				Если ПоискСтрок.Количество() Тогда
					ЗаполнитьЗначенияСвойств(ПоискСтрок[0], КлючИЗначение.Значение);
				КонецЕсли;
			КонецЦикла;
			
			Для Каждого КлючИЗначение Из Результат.УстановленныеСерии Цикл
				КлючИЗначение.Ключ.Серия = КлючИЗначение.Значение;
			КонецЦикла;
			
			ПараметрыИнтеграции = Форма.ПараметрыИнтеграцииФормыПроверкиИСМП();
			ПроверкаИПодборПродукцииИСУТ.ЗаполнитьКешШтрихкодовУпаковок(Форма, ПараметрыИнтеграции);
			ПроверкаИПодборПродукцииИСУТ.ПрименитьКешШтрихкодовУпаковок(Форма, ПараметрыИнтеграции);
			
		КонецЕсли;
	
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецОбласти

Процедура ОпределитьШаблоныКодовМаркировкиСУЗТекстаЗапроса(ТекстЗапроса,
	ПутьКПолюНоменклатура = "Номенклатура",
	ЗаменяемыйПараметр = "&ОпределениеШаблонаКодаМаркировкиСУЗ") Экспорт
	
	ОпределениеШаблонаСУЗ = СтрШаблон("ВЫБОР
	|		КОГДА %1.ВидПродукцииИС = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Табак)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.ТабачнаяПачка)
	|		КОГДА %1.ВидПродукцииИС = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Обувь)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.Обувь)
	|		КОГДА %1.ВидПродукцииИС = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ЛегкаяПромышленность)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.ЛегкаяПромышленность)
	|		КОГДА %1.ВидПродукцииИС = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МолочнаяПродукцияПодконтрольнаяВЕТИС)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.МолочнаяПродукцияПодконтрольнаяВЕТИС)
	|		КОГДА %1.ВидПродукцииИС = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Шины)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.Шины)
	|		КОГДА %1.ВидПродукцииИС = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Фотоаппараты)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.Фотоаппараты)
	|		КОГДА %1.ВидПродукцииИС = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Велосипеды)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.Велосипеды)
	|		КОГДА %1.ВидПродукцииИС = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.КреслаКоляски)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.КреслаКоляски)
	|		КОГДА %1.ВидПродукцииИС = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Духи)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.Духи)
	|		КОГДА %1.ВидПродукцииИС = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.АльтернативныйТабак)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.АльтернативныйТабакПачка)
	|		КОГДА %1.ВидПродукцииИС = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.УпакованнаяВода)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.УпакованнаяВода)
	|		КОГДА %1.ВидПродукцииИС = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МолочнаяПродукцияБезВЕТИС)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.МолочнаяПродукцияБезВЕТИС)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.ПустаяСсылка)
	|	КОНЕЦ", ПутьКПолюНоменклатура);

	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ЗаменяемыйПараметр, ОпределениеШаблонаСУЗ);
	
КонецПроцедуры
