
#Область СлужебныйПрограммныйИнтерфейс

// Устанавливает параметры выбора номенклатуры.
//
// Параметры:
//  ПараметрыВыбора - Массив - параметры выбора элемента формы "Номенклатура".
//  ВидПродукцииИС - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции ИС.
//
Процедура ЗаполнитьПараметрыВыбораНоменклатурыПоВидуПродукции(ПараметрыВыбора, ВидПродукцииИС) Экспорт
	
	Если ЗначениеЗаполнено(ВидПродукцииИС) Тогда 
		ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.ВидПродукцииИС", ВидПродукцииИС));
	КонецЕсли;
	
КонецПроцедуры

#Область Серии

//Подготовливает структуру, массив которых в дальнейшем будет передан в процедуру генерации серий.
//   Дополняется необходимыми полями, специфичными конкретному виду продукции.
//
//Параметры:
//   ВидМаркируемойПродукции - ПеречислениеСсылка.ВидыПродукцииИС - вид маркируемой продукции генерации серии
//Возвращаемое значение:
//   Структура - источник данных генерации серий с обязательными полями:
//    * Номенклатура - ОпределяемыйТип.Номенклатура - Номенклатура, для которой будет генерироваться серия.
//    * Серия        - ОпределяемыйТип.СерияНоменклатуры   - В данное значение будет записана сгенерированная серия.
//    * ЕстьОшибка   - Булево - Будет установлено в Истина, если по каким то причинам серия сгенерирована не будет.
//    * ТекстОшибки  - Строка - причина, по которой серия не генерировалась.
//
Функция СтруктураДанныхДляГенерацииСерии(ВидМаркируемойПродукции) Экспорт
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("Номенклатура", Неопределено);
	СтруктураДанных.Вставить("Серия",        Неопределено);
	СтруктураДанных.Вставить("ЕстьОшибка",   Ложь);
	СтруктураДанных.Вставить("ТекстОшибки",  "");
	
	ДополнитьДанныеДляГенерацииСерииПоВидуПродукции(СтруктураДанных, ВидМаркируемойПродукции);
	
	Возврат СтруктураДанных;

КонецФункции

#КонецОбласти

#Область ПодключаемыеКомандыИС

Процедура УправлениеВидимостьюКомандЗаказаНаЭмиссию(Форма, Команды) Экспорт
	
	Если ПодключаемыеКомандыИСКлиентСервер.УправлениеВидимостьюПоУмолчанию(Форма) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура УправлениеВидимостьюКомандМаркировкиТоваров(Форма, Команды) Экспорт
	
	Если ПодключаемыеКомандыИСКлиентСервер.УправлениеВидимостьюПоУмолчанию(Форма) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура УправлениеВидимостьюКомандВыводаВОборота(Форма, Команды) Экспорт
	
	Если ПодключаемыеКомандыИСКлиентСервер.УправлениеВидимостьюПоУмолчанию(Форма) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура УправлениеВидимостьюКомандСписанияКодовМаркировки(Форма, Команды) Экспорт
	
	Если ПодключаемыеКомандыИСКлиентСервер.УправлениеВидимостьюПоУмолчанию(Форма) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура УправлениеВидимостьюКомандПеремаркировкиТоваров(Форма, Команды) Экспорт
	
	Если ПодключаемыеКомандыИСКлиентСервер.УправлениеВидимостьюПоУмолчанию(Форма) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Серии

Процедура ДополнитьДанныеДляГенерацииСерииПоВидуПродукции(СтруктураДанных, ВидМаркируемойПродукции)

	Если ВидМаркируемойПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Табак") Тогда
		СтруктураДанных.Вставить("МРЦ", 0);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Определяет соответствие переданного документа типу "ПоступлениеТоваровУслуг".
// 
// Параметры:
// * Контекст - ФормаКлиентскогоПриложения, ДокументСсылка - Контекст для определения типа документа.
//
Функция ЭтоДокументПриобретения(Контекст) Экспорт
	
	ЭтоПриобретение = Ложь;
	ТипКонтекста    = ТипЗнч(Контекст);
	
	Если Контекст = Неопределено Тогда
		Возврат ЭтоПриобретение;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст,"ЭтотОбъект") Тогда
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "ДокументОбъект") Тогда
			Если ТипЗнч(Контекст.ДокументОбъект) = Тип("ДокументОбъект.ПоступлениеТоваровУслуг") Тогда 
				ЭтоПриобретение = Истина;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ТипКонтекста = Тип("ФормаКлиентскогоПриложения") Тогда
		Если СтрНачинаетсяС(Контекст.ИмяФормы, "Документ.ПоступлениеТоваровУслуг") Тогда
			ЭтоПриобретение = Истина;
		ИначеЕсли СтрНачинаетсяС(Контекст.ИмяФормы, "ОбщаяФорма.ПроверкаЗаполненияДокументов") Тогда
			Если ТипЗнч(Контекст.Ссылка) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
				ЭтоПриобретение = Истина;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ТипКонтекста = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда 
		ЭтоПриобретение = Истина;
	КонецЕсли;
	
	Возврат ЭтоПриобретение;
	
КонецФункции

// Определяет соответствие переданного документа типу "ЧекККМ".
// 
// Параметры:
// * Контекст - ФормаКлиентскогоПриложения, ДокументСсылка - Контекст для определения типа документа.
//
Функция ЭтоЧекККМ(Контекст) Экспорт
	
	ЭтоЧек       = Ложь;
	ТипКонтекста = ТипЗнч(Контекст);
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст,"ЭтотОбъект") Тогда
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "ДокументОбъект") Тогда
			Если ТипЗнч(Контекст.ДокументОбъект) = Тип("ДокументОбъект.ЧекККМ") Тогда 
				ЭтоЧек = Истина;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ТипКонтекста = Тип("ФормаКлиентскогоПриложения") Тогда
		Если СтрНачинаетсяС(Контекст.ИмяФормы, "Документ.ЧекККМ") Тогда
			ЭтоЧек = Истина;
		ИначеЕсли СтрНачинаетсяС(Контекст.ИмяФормы, "ОбщаяФорма.ПроверкаЗаполненияДокументов") Тогда
			Если ТипЗнч(Контекст.Ссылка) = Тип("ДокументСсылка.ЧекККМ") Тогда
				ЭтоЧек = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ТипКонтекста = Тип("ДокументСсылка.ЧекККМ")
		Или ТипКонтекста = Тип("ДокументОбъект.ЧекККМ") Тогда 
		ЭтоЧек = Истина;
	КонецЕсли;
	
	Возврат ЭтоЧек;
	
КонецФункции

// Определяет соответствие переданного документа типу "ЧекККМКоррекции".
// 
// Параметры:
// * Контекст - ФормаКлиентскогоПриложения, ДокументСсылка - Контекст для определения типа документа.
//
Функция ЭтоЧекККМВозврат(Контекст) Экспорт
	
	ЭтоЧекВозврат = Ложь;
	ТипКонтекста  = ТипЗнч(Контекст);
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст,"ЭтотОбъект") Тогда
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "ДокументОбъект") Тогда
			Если ТипЗнч(Контекст.ДокументОбъект) = Тип("ДокументОбъект.ЧекККМКоррекции") Тогда 
				ЭтоЧекВозврат = Истина;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ТипКонтекста = Тип("ФормаКлиентскогоПриложения") Тогда
		Если СтрНачинаетсяС(Контекст.ИмяФормы, "Документ.ЧекККМКоррекции") Тогда
			ЭтоЧекВозврат = Истина;
		ИначеЕсли СтрНачинаетсяС(Контекст.ИмяФормы, "ОбщаяФорма.ПроверкаЗаполненияДокументов") Тогда
			Если ТипЗнч(Контекст.Ссылка) = Тип("ДокументСсылка.ЧекККМКоррекции") Тогда
				ЭтоЧекВозврат = Истина;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ТипКонтекста = Тип("ДокументСсылка.ЧекККМКоррекции") Тогда 
		ЭтоЧекВозврат = Истина;
	КонецЕсли;
	
	Возврат ЭтоЧекВозврат;
	
КонецФункции

// Определяет соответствие переданного документа проверяемому типу.
// 
// Параметры:
// * Контекст - ФормаКлиентскогоПриложения, ДокументСсылка - Контекст для определения типа документа.
// * Имя      - Строка - имя объекта метаданного документа.
//
Функция ЭтоДокументПоНаименованию(Контекст, Имя) Экспорт
	
	Результат = Ложь;
	
	ТипКонтекста = ТипЗнч(Контекст);
	ТипДокСсылка = Тип(СтрШаблон("ДокументСсылка.%1",Имя));
	ТипДокОбъект = Тип(СтрШаблон("ДокументОбъект.%1",Имя));
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст,"ЭтотОбъект") Тогда
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "ДокументОбъект") Тогда
			Если ТипЗнч(Контекст.ДокументОбъект) = ТипДокОбъект Тогда 
				Результат = Истина;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ТипКонтекста = Тип("ФормаКлиентскогоПриложения") Тогда
		Если СтрНачинаетсяС(Контекст.ИмяФормы, СтрШаблон("Документ.%1",Имя)) Тогда
			Результат = Истина;
		ИначеЕсли СтрНачинаетсяС(Контекст.ИмяФормы, "ОбщаяФорма.ПроверкаЗаполненияДокументов") Тогда
			Если ТипЗнч(Контекст.Ссылка) = ТипДокСсылка Тогда
				Результат = Истина;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ТипКонтекста = ТипДокСсылка Тогда
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти


#Область СлужебныйПрограммныйИнтерфейс

#КонецОбласти

