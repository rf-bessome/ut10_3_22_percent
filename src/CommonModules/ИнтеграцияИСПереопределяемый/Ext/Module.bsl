// Универсальные механизмы интеграции ИС (ЕГАИС, ГИСМ, ВЕТИС, ...)

#Область СлужебныйПрограммныйИнтерфейс

#Область ЗаполнениеИПроверкаЗаполнения

// Заполняет в табличной части служебные реквизиты, например: признак использования характеристик номенклатуры, артикул.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма.
//  ТабличнаяЧасть - ДанныеФормыКоллекция - таблица для заполнения.
Процедура ЗаполнитьСлужебныеРеквизитыВКоллекции(Форма, ТабличнаяЧасть) Экспорт
	
	Если ТипЗнч(ТабличнаяЧасть) = Тип("ТаблицаЗначений") Тогда
		ТаблицаЗначений = ТабличнаяЧасть;
	Иначе 
		ТаблицаЗначений = ТабличнаяЧасть.Выгрузить();
	КонецЕсли;

	Если ТаблицаЗначений.Колонки.Найти("Номенклатура")<>Неопределено Тогда 

		ПараметрыЗаполненияРеквизитов = Новый Структура;
		
		Если ТаблицаЗначений.Колонки.Найти("Артикул")<>Неопределено Тогда 
			ПараметрыЗаполненияРеквизитов.Вставить(
				"ЗаполнитьПризнакАртикул",
				Новый Структура("Номенклатура", "Артикул"));
		КонецЕсли;
		
		Если ТаблицаЗначений.Колонки.Найти("ХарактеристикиИспользуются")<>Неопределено Тогда 
			ПараметрыЗаполненияРеквизитов.Вставить(
				"ЗаполнитьПризнакХарактеристикиИспользуются",
				Новый Структура("Номенклатура", "ХарактеристикиИспользуются"));
		КонецЕсли;
		
		Если ТаблицаЗначений.Колонки.Найти("ЕдиницаИзмерения") <> Неопределено Тогда
			ПараметрыЗаполненияРеквизитов.Вставить(
				"ЗаполнитьПризнакЕдиницаИзмерения",
				Новый Структура("Номенклатура", "ЕдиницаИзмерения"));
		КонецЕсли;
		
		Если ТаблицаЗначений.Колонки.Найти("ТипНоменклатуры") <> Неопределено Тогда
			ПараметрыЗаполненияРеквизитов.Вставить(
				"ЗаполнитьПризнакТипНоменклатуры",
				Новый Структура("Номенклатура", "ТипНоменклатуры"));
		КонецЕсли;
			
		Если ТаблицаЗначений.Колонки.Найти("ТребуетВзвешивания")<> Неопределено Тогда
			ПараметрыЗаполненияРеквизитов.Вставить("ЗаполнитьПризнакТребуетВзвешивания",
				Новый Структура("Номенклатура", "ТребуетВзвешивания"));
		КонецЕсли;
	
		Если ТаблицаЗначений.Колонки.Найти("ПроизвольнаяЕдиницаУчета")<> Неопределено Тогда
			ПараметрыЗаполненияРеквизитов.Вставить("ЗаполнитьПризнакПроизвольнаяЕдиницаУчета",
				Новый Структура("Номенклатура", "ПроизвольнаяЕдиницаУчета"));
		КонецЕсли;
		
		НоменклатураСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(
			ТабличнаяЧасть, ПараметрыЗаполненияРеквизитов);
	КонецЕсли;
	
	Возврат;
	
КонецПроцедуры

// Обрабатывает строку табличной части в соостветствии с переданной структурой действий.
//
// Параметры:
//  СтрокаТабличнойЧасти - Структура, СтрокаТабличнойЧасти, ДокументСтрокаТабличнойЧасти - обрабатываемая строка.
//  СтруктураДействий - структура - структура выполняемых действий.
Процедура ОбработатьСтрокуТабличнойЧасти(СтрокаТабличнойЧасти, СтруктураДействий) Экспорт
	


	ТипСтрокиТабличнойЧасти = ТипЗнч(СтрокаТабличнойЧасти);
	
	Если ТипСтрокиТабличнойЧасти = Тип("Структура") Тогда
		СтрокаТабличнойЧастиСтруктурой = ОбщегоНазначения.СкопироватьРекурсивно(СтрокаТабличнойЧасти, Ложь);
	ИначеЕсли ТипСтрокиТабличнойЧасти = Тип("СтрокаТаблицыЗначений") Тогда
		СтрокаТабличнойЧастиСтруктурой = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(СтрокаТабличнойЧасти);
	ИначеЕсли Метаданные.НайтиПоТипу(ТипСтрокиТабличнойЧасти) <> Неопределено Тогда
		СтрокаТабличнойЧастиСтруктурой = Новый Структура();
		ПодстрокиИмени = СтрРазделить(Метаданные.НайтиПоТипу(ТипСтрокиТабличнойЧасти).ПолноеИмя(), ".");
		
		Если ПодстрокиИмени.Количество() = 4 Тогда
			РеквизитыСтроки = Метаданные.Документы[ПодстрокиИмени[1]].ТабличныеЧасти[ПодстрокиИмени[3]].Реквизиты;
			Для Каждого РеквизитСтроки Из РеквизитыСтроки Цикл
				СтрокаТабличнойЧастиСтруктурой.Вставить(РеквизитСтроки.Имя, СтрокаТабличнойЧасти[РеквизитСтроки.Имя]);
			КонецЦикла;
		Иначе
			Возврат;
		КонецЕсли;
	Иначе
		Возврат;
	КонецЕсли;

	СтрокаТабличнойЧастиСтруктурой.Вставить("ТипНоменклатуры", Перечисления.ТипыНоменклатуры.Товар);
	ОбработкаТабличнойЧастиСерверИСУТ.ОбработатьСтрокуТЧ(СтрокаТабличнойЧастиСтруктурой, СтруктураДействий, Неопределено);
	ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, СтрокаТабличнойЧастиСтруктурой);



	
КонецПроцедуры

// Проверяет заполнение характеристик в таблице значений.
//
// Параметры:
//  ТаблицаТоваров - ТаблицаЗначений - таблица для проверки с колонками: Номенклатура, Характеристика.
//  Отказ - Булево - Истина - характеристики заполнены в требуемых строках, в противном случае - Ложь.
Процедура ПроверитьЗаполнениеХарактеристикВТаблицеЗначений(ТаблицаТоваров, Отказ) Экспорт
	

	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ СтрокиСОшибками
	|ИЗ
	|	&ТаблицаТоваров КАК ТаблицаТоваров
	|ГДЕ
	|	ТаблицаТоваров.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИСТИНА КАК НеЗаполненаХарактеристика






	|ИЗ
	|	СтрокиСОшибками КАК СтрокиСОшибками
	|ГДЕ
	|	ВЫРАЗИТЬ(СтрокиСОшибками.Номенклатура КАК Справочник.Номенклатура).ВестиУчетПоХарактеристикам";

	Запрос.Текст = ТекстЗапроса;

	Запрос.УстановитьПараметр("ТаблицаТоваров", ТаблицаТоваров);

	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл

		Если Выборка.НеЗаполненаХарактеристика Тогда
			Отказ = Истина;
			Прервать;
		КонецЕсли;

	КонецЦикла;


	
КонецПроцедуры

// Необходимо заполнить информацию о сертификатах номенклатуры в виде соответствия из массива структур со свойствами:
//    "ВидСертификации", "НомерСертификации" и "ДатаСертификации". Информацию по сертификатам следует добавить в соответствие
//     "СертификатыНоменклатуры".
// 
// Параметры:
//  Номенклатура - ОпределяемыйТип.Номенклатура - элемент номенклатура.
//  СертификатыНоменклатуры - Соответствие из Массив Структур, Ключ - номенклатура, свойство - структура:
//   * ВидСертификации - ПеречислениеСсылка.ВидыДокументовОбязательнойСертификацииИСМП - Вид сертификата.
//   * НомерСертификации - Строка - Идентификационный номер сертификата.
//   * ДатаСертификации - Дата - Дата начала действия сертификата.
Процедура ПриЗаполненииСертификатовНоменклатуры(СписокНоменклатуры, СертификатыНоменклатуры) Экспорт
	
















































































	Возврат;
	
КонецПроцедуры

// Вызывается расширением формы при необходимости проверки заполнения реквизитов при записи или при проведении документа в форме,
// а также при выполнении метода ПроверитьЗаполнение.
//
// Параметры:
//  ДокументОбъект - ДокументОбъект - проверяемый документ,
//  Отказ - Булево - признак отказа от проведения документа,
//  ПроверяемыеРеквизиты - Массив - массив путей к реквизитам, для которых будет выполнена проверка заполнения,
//  МассивНепроверяемыхРеквизитов - Массив - массив путей к реквизитам, для которых не будет выполнена проверка заполнения.
//
Процедура ОбработкаПроверкиЗаполнения(ДокументОбъект, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов) Экспорт
	
	Если ТипЗнч(ДокументОбъект) = Тип("СправочникОбъект.ШтрихкодыУпаковокТоваров") Тогда
		
		СтруктураКонстант = Новый Структура("ИспользоватьХарактеристикиНоменклатуры");
		мКонстанты = ИнтеграцияЕГАИСУТ.ПолучитьКонстанты(СтруктураКонстант);
		ИспользоватьХарактеристики = мКонстанты.ИспользоватьХарактеристикиНоменклатуры;
		Если Не ИспользоватьХарактеристики Или Не ДокументОбъект.Номенклатура.ВестиУчетПоХарактеристикам Тогда
			МассивНепроверяемыхРеквизитов.Добавить("Характеристика");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПараметрыИспользованияНоменклатуры

// В функции требуется определить право на регистрацию нового штрихкода для текущего пользователя.
//
// Параметры:
//  ДоступРазрешен - Булево - Истина, если есть право на регистрацию штрихкода. Ложь - в противном случае.
//
Процедура ПравоРегистрацииШтрихкодовНоменклатуры(ДоступРазрешен) Экспорт
	
	ДоступРазрешен = ПравоДоступа("Изменение", Метаданные.РегистрыСведений.Штрихкоды);



	
КонецПроцедуры

// В процедуре требуется определить признак использования характеристик 
//   * для номенклатуры, если передана номенклатура,
//   * в программе, если не передана.
//
// Параметры:
//  Использование - Булево - Признак использования характеристик (для номенклатуры или общий)
//  Номенклатура - ОпределяемыйТип.Номенклатура, Неопределено - Ссылка на элемент номенклатуры.
//
Процедура ИспользованиеХарактеристик(Использование, Номенклатура) Экспорт
	
	Если НЕ глЗначениеПеременной("ИспользоватьХарактеристикиНоменклатуры") Тогда

		Использование = Ложь;
	ИначеЕсли Номенклатура = Неопределено Тогда
		Использование = Истина;
	ИначеЕсли НЕ ЗначениеЗаполнено(Номенклатура) Тогда
		Использование = Ложь;
	Иначе
		Использование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ВестиУчетПоХарактеристикам");
	КонецЕсли;


	
КонецПроцедуры

// В процедуре требуется определить признак использования серий
//   * для номенклатуры, если передана номенклатура,
//   * в программе, если не передана.
//
// Параметры:
//  Использование - Булево - Признак использования серий (для номенклатуры или общий)
//  Номенклатура - ОпределяемыйТип.Номенклатура, Неопределено - Ссылка на элемент номенклатуры.
//
Процедура ИспользованиеСерий(Использование, Номенклатура) Экспорт
	
	Если НЕ глЗначениеПеременной("ИспользоватьСерииНоменклатуры") Тогда

		Использование = Ложь;
	ИначеЕсли Номенклатура = Неопределено Тогда
		Использование = Истина;
	ИначеЕсли НЕ ЗначениеЗаполнено(Номенклатура) Тогда
		Использование = Ложь;
	Иначе
		Использование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ВестиУчетПоСериям");

























	КонецЕсли;


	
КонецПроцедуры

// В процедуре требуется определить признак использования упаковок для номенклатуры.
//   * для номенклатуры, если передана номенклатура,
//   * в программе, если не передана.
//
// Параметры:
//  Использование - Булево - Признак использования упаковок (для номенклатуры или общий)
//  Номенклатура - ОпределяемыйТип.Номенклатура, Неопределено - Ссылка на элемент номенклатуры.
//
Процедура ИспользованиеУпаковок(Использование, Номенклатура) Экспорт
	
	Использование = Истина;











	
КонецПроцедуры

// В процедуре требуется определить признак использования видов номенклатуры.
//
// Параметры:
//  Использование - Булево - Признак использования видов номенклатуры в программе.
//
Процедура ИспользованиеВидовНоменклатуры(Использование) Экспорт
	

	Использование = Истина;


	
КонецПроцедуры

Процедура ПриЗаполненииТекстаЗапросаПризнакаМаркируемаяПродукцияПоНоменклатуре(ТекстЗапроса, ИмяТаблицыНоменклатура, ИмяПоляНоменклатура) Экспорт
	

	ТекстЗапроса = СтрШаблон(
		"ВЫБРАТЬ
		|	Товары.%1 КАК Номенклатура,
		|	%3
		|ИЗ %2 КАК Товары",
		ИмяПоляНоменклатура,
		ИмяТаблицыНоменклатура,
		ИнтеграцияИСУТ.ОпределениеПризнакаМаркируемаяПродукцияТекстаЗапроса(
			СтрШаблон("Товары.%1",ИмяПоляНоменклатура)));

	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область ОрганизацииИКонтрагенты

Процедура ПриОпределенииНалоговогоНомераКонтрагента(Контрагент, НалоговыйНомер) Экспорт
	
	Если ЗначениеЗаполнено(Контрагент.РегистрационныйНомер) Тогда
		НалоговыйНомер = Контрагент.РегистрационныйНомер;
	ИначеЕсли ЗначениеЗаполнено(Контрагент.ИНН) Тогда
		НалоговыйНомер = Контрагент.ИНН;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОпределенииСоответствияНалоговыхНомеровКонтрагентам(КоллекцияНалоговыхНомеров, СоответствиеНалоговыхНомеровКонтрагентам) Экспорт 
	
	Если Не ПравоДоступа("Чтение", Метаданные.Справочники.Контрагенты) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Контрагенты.Ссылка               КАК Контрагент,
	|	Контрагенты.ИНН КАК РегистрационныйНомер
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.ИНН В (&КоллекцияНалоговыхНомеров)
	|");
	Запрос.УстановитьПараметр("КоллекцияНалоговыхНомеров", КоллекцияНалоговыхНомеров);

	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СоответствиеНалоговыхНомеровКонтрагентам.Вставить(Выборка.РегистрационныйНомер, Выборка.Контрагент);
	КонецЦикла;
	
КонецПроцедуры

Процедура ИспользованиеПодразделений(Использовать) Экспорт
	
	Использовать = Истина;


	
КонецПроцедуры
	
// В процедуре нужно реализовать заполнение структуры данными из информационной базы.
//
// Параметры:
//  Организация - ОпределяемыйТип.ОрганизацияКонтрагентГосИС - ссылка на собственную организацию или контрагента,
//  Сведения - Структура - структура с реквизитами организации, которые требуется заполнить,
//  ДатаСведений - Дата - дата, на которую требуется получить информацию.
//
Процедура ЗаполнитьСведенияОбОрганизации(Организация, Сведения, ДатаСведений = Неопределено) Экспорт
	

	Если ДатаСведений = Неопределено Тогда
		ДатаСведений = ТекущаяДатаСеанса();
	КонецЕсли;
	
	СведенияОбОрганизации = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Организация, ДатаСведений);
	
	ЗаполнитьЗначенияСвойств(Сведения, СведенияОбОрганизации);
КонецПроцедуры

// В процедуре нужно реализовать заполнение структуры данными ИНН/КПП из информационной базы.
//
// Параметры:
//  Сведения    - Структура - структура с реквизитами организации/контрагента, которые требуется заполнить,
//  Организация - ОпределяемыйТип.ОрганизацияКонтрагентГосИС, ОпределяемыйТип.КонтрагентГосИС - ссылка на 
//                                                                                              собственную организацию или контрагента
//  ТорговыйОбъект - ОпределяемыйТип.ТорговыйОбъектЕГАИС - ссылка на торговый объект для определения КПП.
Процедура ЗаполнитьИННКПППоОрганизацииКонтрагенту(Сведения, Организация, ТорговыйОбъект = Неопределено) Экспорт
	
	СведенияОбОрганизации = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Организация, ТекущаяДатаСеанса());




















































































	
	ЗаполнитьЗначенияСвойств(Сведения, СведенияОбОрганизации);

	
КонецПроцедуры

// Заполняет представление руководителя организации.
//
// Параметры:
//  ПредставлениеРуководителя - (см. ИнтеграцияИС.ДанныеРуководителяОрганизации)
//  Организация - ОпределяемыйТип.ОрганизацияКонтрагентГосИС - ссылка на собственную организацию или контрагента,
//  ДатаСведений - Дата - дата, на которую требуется получить информацию.
Процедура ОпределитьДанныеРуководителяОрганизации(ПредставлениеРуководителя, Организация, ДатаСведений) Экспорт
	
	ПараметрыОтветственногоЛица = Новый Структура();
	ПараметрыОтветственногоЛица.Вставить("Организация", Организация);
	ПараметрыОтветственногоЛица.Вставить("ОтветственноеЛицо", Перечисления.ОтветственныеЛицаОрганизации.Руководитель);
	
	ДанныеОтветственногоЛица = ПолучитьДанныеОтветственногоЛица(ПараметрыОтветственногоЛица);
	
	ПредставлениеРуководителя.Руководитель = Строка(ДанныеОтветственногоЛица.РасшифровкаПодписи);
	ПредставлениеРуководителя.Должность    = Строка(ДанныеОтветственногоЛица.Должность);

КонецПроцедуры

// В процедуре нужно реализовать поиск организации предприятия по ИНН и/или КПП.
//
// Параметры:
//  ИНН - Строка - ИНН искомой организации,
//  КПП - Строка - КПП искомой организации.
//
// Возвращаемое значение:
//  ОпределяемыйТип.ОрганизацияКонтрагентГосИС - найденная организация,
//  Неопределено - организация не найдена.
Процедура ЗаполнитьОрганизациюПоИННКПП(Организация, ИНН, КПП) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Выборка.Ссылка
	|ИЗ
	|	Справочник.Организации КАК Выборка
	|ГДЕ
	|	Выборка.ИНН = &ИНН И
	|	Выборка.КПП = &КПП";
	
	Запрос.УстановитьПараметр("ИНН", ИНН);
	Запрос.УстановитьПараметр("КПП", КПП);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Организация = Выборка.Ссылка;
	КонецЕсли;
	
КонецПроцедуры

// В процедуре нужно реализовать поиск контрагента по ИНН и/или КПП.
//
// Параметры:
//  ИНН - Строка - ИНН искомого контрагента,
//  КПП - Строка - КПП искомого контрагента.
//
// Возвращаемое значение:
//  ОпределяемыйТип.ОрганизацияКонтрагентГосИС, ОпределяемыйТип.КонтрагентГосИС - найденный контрагент,
//  Неопределено - контрагент не найден.
Процедура ЗаполнитьКонтрагентаПоИННКПП(Контрагент, ИНН, КПП) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Выборка.Ссылка
	|ИЗ
	|	Справочник.Контрагенты КАК Выборка
	|ГДЕ
	|	Выборка.ИНН = &ИНН И
	|	Выборка.КПП = &КПП";
	
	Запрос.УстановитьПараметр("ИНН", ИНН);
	Запрос.УстановитьПараметр("КПП", КПП);


	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Контрагент = Выборка.Ссылка;
	КонецЕсли;
	
КонецПроцедуры

// В процедуре необходимо сформировать соответствие по коллекции ИНН. Ключ - ИНН, значение - Контрагент.
//
// Параметры:
//  КоллекцияИНН - Массив - Список ИНН.
//  Соответствие - Соответствие - Соответсвие вида:
//   * ИНН
//   * Контрагент
Процедура ЗаполнитьСоответствиеИННКонтрагентам(КоллекцияИНН, Соответствие) Экспорт

	Если Не ПравоДоступа("Чтение", Метаданные.Справочники.Контрагенты) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Контрагенты.Ссылка КАК Контрагент,
	|	Контрагенты.ИНН    КАК ИНН
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.ИНН В (&КоллекцияИНН)
	|");
	Запрос.УстановитьПараметр("КоллекцияИНН", КоллекцияИНН);

	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Соответствие.Вставить(Выборка.ИНН, Выборка.Контрагент);
	КонецЦикла;


	
КонецПроцедуры

// В процедуре необходимо сформировать соответствие по коллекции ИНН. Ключ - ИНН, значение - Организация.
//
// Параметры:
//  КоллекцияИНН - Массив - Список ИНН.
//  Соответствие - Соответствие - Соответсвие вида:
//   * ИНН
//   * Контрагент
Процедура ЗаполнитьСоответствиеИННОрганизациям(КоллекцияИНН, Соответствие) Экспорт
	


	Если Не ПравоДоступа("Чтение", Метаданные.Справочники.Организации) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Организации.Ссылка КАК Организация,
	|	Организации.ИНН    КАК ИНН
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.ИНН В (&КоллекцияИНН)
	|");
	Запрос.УстановитьПараметр("КоллекцияИНН", КоллекцияИНН);

	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Соответствие.Вставить(Выборка.ИНН, Выборка.Организация);
	КонецЦикла;


	
КонецПроцедуры

Процедура ПриОпределенииИНННалоговыхНомеровКонтрагентов(СоответствиеИННКонтрагентам, Контрагенты) Экспорт
	
	Если Не ПравоДоступа("Чтение", Метаданные.Справочники.Контрагенты) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Контрагенты.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА Контрагенты.РегистрационныйНомер = """"
	|	ТОГДА Контрагенты.ИНН
	|   ИНАЧЕ Контрагенты.РегистрационныйНомер
	|	КОНЕЦ КАК ИНННалоговыйНомер
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.Ссылка В(&Ссылка)");
	Запрос.УстановитьПараметр("Ссылка", Контрагенты);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СоответствиеИННКонтрагентам.Вставить(Выборка.Ссылка, Выборка.ИНННалоговыйНомер);
	КонецЦикла;
	
КонецПроцедуры

// В процедуре нужно реализовать поиск организации в прикладном объекте.
//
// Параметры:
//  Организация - ОпределяемыйТип.Организация - найденная организация
//  Объект      - Произвольный                - объект в котором производится поиск
//
Процедура ПриОпределенииОрганизацииИзПрикладногоОбъекта(Организация, Объект) Экспорт
	
	Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Ссылка, "Организация");
	
КонецПроцедуры

Функция ПолучитьДанныеОтветственногоЛица(Параметры) Экспорт
	
	ДанныеОтветственногоЛица = Новый Структура("Должность,РасшифровкаПодписи");
	
	Если Не Параметры.Свойство("Организация")
		ИЛИ Не Параметры.Свойство("ОтветственноеЛицо") Тогда
		Возврат ДанныеОтветственногоЛица;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Параметры.Организация)
		ИЛИ Не ЗначениеЗаполнено(Параметры.ОтветственноеЛицо) Тогда
		Возврат ДанныеОтветственногоЛица;
	КонецЕсли;
	
	Если Параметры.Свойство("Период") И ЗначениеЗаполнено(Параметры.Период) Тогда
		Период = Параметры.Период;
	Иначе
		Период = ТекущаяДатаСеанса();
	КонецЕсли;
		
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ОтветственныеЛицаОрганизацийСрезПоследних.Должность.Наименование КАК Должность
	|ПОМЕСТИТЬ ОтветственныеЛица
	|ИЗ
	|	РегистрСведений.ОтветственныеЛицаОрганизации.СрезПоследних(
	|			&Период,
	|			СтруктурнаяЕдиница = &Организация
	|				И ОтветственноеЛицо = &ОтветственноеЛицо) КАК ОтветственныеЛицаОрганизацийСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ФИОФизЛицСрезПоследних.Фамилия, """") КАК Фамилия,
	|	ЕСТЬNULL(ФИОФизЛицСрезПоследних.Имя, """") КАК Имя,
	|	ЕСТЬNULL(ФИОФизЛицСрезПоследних.Отчество, """") КАК Отчество,
	|	ОтветственныеЛица.ФизическоеЛицо.Наименование КАК ФизическоеЛицо,
	|	ОтветственныеЛица.Должность КАК Должность
	|ИЗ
	|	ОтветственныеЛица КАК ОтветственныеЛица
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(
	|				,
	|				ФизЛицо В
	|					(ВЫБРАТЬ
	|						ОтветственныеЛица.ФизическоеЛицо
	|					ИЗ
	|						ОтветственныеЛица КАК ОтветственныеЛица)) КАК ФИОФизЛицСрезПоследних
	|		ПО ОтветственныеЛица.ФизическоеЛицо = ФИОФизЛицСрезПоследних.ФизЛицо";
	Запрос.УстановитьПараметр("ОтветственноеЛицо", Параметры.ОтветственноеЛицо);
	Запрос.УстановитьПараметр("Организация"      , Параметры.Организация);
	Запрос.УстановитьПараметр("Период"           , Период);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Если ЗначениеЗаполнено(Выборка.Фамилия) Тогда
			ДанныеОтветственногоЛица.РасшифровкаПодписи = Выборка.Фамилия + " " + Лев(Выборка.Имя, 1) + ". " + Лев(Выборка.Отчество, 1) + ".";
		КонецЕсли;
		ДанныеОтветственногоЛица.Должность = Выборка.Должность;
	КонецЕсли;
	
	Возврат ДанныеОтветственногоЛица;
			
КонецФункции

#КонецОбласти

#Область ПодключаемоеОборудование

// Настраивает подключаемое оборудование в форме,
// устанавливает флаг ИспользоватьПодключаемоеОборудование в форме.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой необходимо настроить подключаемое оборудование,
//  ПрефиксыЭлементовФормы - Строка - строка имен табличных частей, разделенных запятыми, в которых нужно настроить видимость команд.
Процедура НастроитьПодключаемоеОборудование(Форма, ПрефиксыЭлементовФормы = "Товары") Экспорт
	
	ТипыПодключенногоОборудования = Неопределено;
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ПоддерживаемыеТипыПодключаемогоОборудования") Тогда
		ТипыПодключенногоОборудования = Форма.ПоддерживаемыеТипыПодключаемогоОборудования;
	КонецЕсли;

	ИспользоватьПодключаемоеОборудование = ПолучитьФункциональнуюОпцию("ИспользоватьПодключаемоеОборудование");
	ПользовательАвторизовался 			 = ТипЗнч(Пользователи.АвторизованныйПользователь()) = Тип("СправочникСсылка.Пользователи");
	
	Форма.ИспользоватьПодключаемоеОборудование =
		ИспользоватьПодключаемоеОборудование
		И ПользовательАвторизовался
		И ТипыПодключенногоОборудования <> Неопределено;
		
	
	Если ЗначениеЗаполнено(ПрефиксыЭлементовФормы) Тогда
		МассивПрефиксыЭлементовФормы = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ПрефиксыЭлементовФормы, ",");
	Иначе
		МассивПрефиксыЭлементовФормы = Новый Массив;
		МассивПрефиксыЭлементовФормы.Добавить(ПрефиксыЭлементовФормы);
	КонецЕсли;
	
	Для Каждого Префикс Из МассивПрефиксыЭлементовФормы Цикл
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Форма.Элементы,
			Префикс + "ВыгрузитьДанныеВТСД",
			"Видимость",
			Ложь);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ФормированиеПечатныхФорм

// Заполняет структуру параметров возможных получателей печатных форм при отправке по электронной почте.
//
// Параметры:
//  ПараметрыОтправки - Структура - содержит параметры отправки сформированных печатных форм
//  ДанныеОбъектовПечати - Массив,Соответствие - данные о печатаемых объектах
//  КоллекцияПечатныхФорм - ТаблицаЗначений - содержит информацию о формируемых печатных формах.
Процедура ЗаполнитьПараметрыОтправки(ПараметрыОтправки, ДанныеОбъектовПечати, КоллекцияПечатныхФорм)  Экспорт
	



	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область ОбщегоНазначенияУТ

// (См ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании)
Процедура ПроверитьВозможностьВводаНаОсновании(Документ,
	                                           Статус = Неопределено,
	                                           ЕстьОшибкиПроведен = Ложь,
	                                           ЕстьОшибкиСтатус = Ложь,
	                                           МассивДопустимыхСтатусов = Неопределено,
	                                           СоглашениеДоступноВнешнемуПользователю = Неопределено) Экспорт
	
	
	ИнтеграцияИСУТ.ПроверитьВозможностьВводаНаОсновании(
		Документ,
		Статус,
		ЕстьОшибкиПроведен,
		ЕстьОшибкиСтатус,
		МассивДопустимыхСтатусов);



	
КонецПроцедуры

// Возвращает представление объекта.
// 
// Параметры:
//  Представление - Строка - Исходящий параметр со значением представления.
//  ПараметрОбъект - Строка, Объект - Наименование типа объекта либо сам объект.
Процедура УстановитьПредставлениеОбъекта(Представление, ПараметрОбъект) Экспорт
	
	Представление = ИнтеграцияИСУТ.ПредставлениеОбъекта(ПараметрОбъект);



	
КонецПроцедуры

// Заполняет представление строки номенклатуры.
//
// Параметры:
//  Представление  - Строка                                     - представление для заполнения,
//  Номенклатура   - ОпределяемыйТип.Номенклатура               - ссылка на номенклатуру,
//  Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатуры - ссылка на характеристику номенклатуры,
//  Упаковка       - ОпределяемыйТип.Упаковка                   - ссылка на упаковку.
//  Серия          - ОпределяемыйТип.СерияНоменклатуры          - ссылка на серию номенклатуры.
Процедура ЗаполнитьПредставлениеНоменклатуры(Представление, Номенклатура, Характеристика, Упаковка, Серия) Экспорт
	

	Представление = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
		СокрЛП(Номенклатура),
		СокрЛП(Характеристика),
		Упаковка,
		Серия);


	
КонецПроцедуры

#КонецОбласти

#Область ПраваДоступа

// Проверяет наличие права создания контрагента у текущего пользователя.
//
// Параметры:
//  ДоступРазрешен - Булево - Истина, если есть право создания контрагента, Ложь в обратном случае.
//
Процедура ПравоСозданияКонтрагента(ДоступРазрешен) Экспорт
	

	ДоступРазрешен = ПравоДоступа("Добавление", Метаданные.Справочники.Контрагенты);


	
КонецПроцедуры

#КонецОбласти

#Область ВызовыИзБСП

// (См. ОбщийМодуль.ПодключаемыеКомандыПереопределяемый.ПриОпределенииКомандПодключенныхКОбъекту)
//
Процедура ПриОпределенииКомандПодключенныхКОбъекту(НастройкиФормы, Источники, ПодключенныеОтчетыИОбработки, Команды) Экспорт
	
	Если НастройкиФормы.ИмяФормы = "Обработка.ГенерацияШтрихкодовУпаковок.Форма.Форма" Тогда
		Обработки.ГенерацияШтрихкодовУпаковок.ПриОпределенииКомандПодключенныхКОбъекту(Команды);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область МенюОтчеты

// Добавляет команду отчета в список команд.
// 
// Параметры:
//  КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции МенюОтчеты.СоздатьКоллекциюКомандОтчетов.
Процедура ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов) Экспорт
	
	Возврат;
	
КонецПроцедуры

// Добавляет команду отчета в список команд.
// 
// Параметры:
//  КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции МенюОтчеты.СоздатьКоллекциюКомандОтчетов.
//
Процедура ДобавитьКомандуДвиженияДокумента(КомандыОтчетов) Экспорт
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область ДлительныеОперации

//Особенности работы с длительными операциями в конкретном окружении
//
//Параметры:
//   Параметры - Структура - параметры фонового задания.
//   ПараметрыВыполнения - см. ДлительныеОперации.ПараметрыВыполненияВФоне - параметры выполнения фонового задания.
Процедура НастроитьДлительнуюОперацию(Параметры, ПараметрыВыполнения) Экспорт
	
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая()
		И ПараметрыВыполнения.НаименованиеФоновогоЗадания <> "Выполнение обмена с ИС МП" Тогда
		ПараметрыВыполнения.ЗапуститьВФоне = Ложь;
		ПараметрыВыполнения.ЗапуститьНеВФоне = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область КодыТНВЭД

// В данной процедуре требуется переопределить текст запроса, определяющий сопоставление классификатора ТН ВЭД и данных регистра сведений КодыТНВЭДИСМП.
//   Требования к тексту запроса:
//     Если классификатора ТН ВЭД не используется, переопределение также не заполнять.
//     Результат запроса обязательно должен содержать следующие поля:
//   Колонки временной таблицы "ДанныеШтрихкодовУпаковок":
//    * КодТНВЭД   - Строка - код.
//    * ВидПродукции - Перечислениессылка.ВидыПродукцииИС - вид продукции.
//    * НаименованиеПолное - Строка - наименование.
//    * КодТНВЭДСсылка - Произвольный - ссылка на сопоставленный элемент классификатора.
//    * Сопоставлено - Число - 0, если в базе найден соотвествующий элемент классификатора, 1 - если найден.
// Параметры:
//  ТекстЗапроса - Строка - Переопределяемый текст запроса.
Процедура ПриОпределенииТекстаЗапросаСопоставлениеКодовТНВЭД(ТекстЗапроса) Экспорт
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	КодыТНВЭДИСМП.КодТНВЭД           КАК КодТНВЭД,
	|	КодыТНВЭДИСМП.ВидПродукции       КАК ВидПродукции,
	|	КодыТНВЭДИСМП.НаименованиеПолное КАК НаименованиеПолное,
	|
	|	ЕстьNULL(КлассификаторТНВЭД.Ссылка, Неопределено) КАК КодТНВЭДСсылка,
	|	ВЫБОР
	|		КОГДА КлассификаторТНВЭД.Ссылка ЕСТЬ NULL
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Сопоставлено
	|ИЗ
	|	РегистрСведений.КодыТНВЭДИСМП КАК КодыТНВЭДИСМП
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторТНВЭД КАК КлассификаторТНВЭД
	|		ПО КодыТНВЭДИСМП.КодТНВЭД = КлассификаторТНВЭД.Код";
	
КонецПроцедуры

#КонецОбласти

#Область СерииНоменклатуры

// В процедуре необходимо реализовать проверку необходимости выбора серии для номенклатуры.
// 
// Параметры:
//  ДанныеДляРасчетаСерии  - Структура - Данные для расчета серий.
//  ПараметрыУказанияСерий - Структура - Параметры указания серий 
//  ТребуетсяВыбор         - Булево - исходящий, признак необходимости выбора серии
// 	(ПараметрыСканирования.ПараметрыУказанияСерий).
//
Процедура ПриОпределенииНеобходимостиВыбораСерии(ДанныеДляРасчетаСерии, ПараметрыУказанияСерий, ТребуетсяВыбор,СерияДолжнаБытьУказана,КэшированныеЗначения = Неопределено) Экспорт
	
	ИспользованиеСерийНоменклатуры = Константы.ИспользоватьСерииНоменклатуры.Получить();
	Если Не ИспользованиеСерийНоменклатуры Тогда
		ТребуетсяВыбор = Ложь;
		Возврат;
	КонецЕсли;
	
	Если КэшированныеЗначения = Неопределено Тогда 
		КэшированныеЗначения = Новый Структура;
	КонецЕсли;
	
	Если Не КэшированныеЗначения.Свойство("ПризнакУказанияСерий") Тогда
		КэшированныеЗначения.Вставить("ПризнакУказанияСерий", Новый Соответствие);
	КонецЕсли;
	
	ТребуетсяВыбор = КэшированныеЗначения.ПризнакУказанияСерий.Получить(ДанныеДляРасчетаСерии.Номенклатура);
	
	Если ТребуетсяВыбор <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	Номенклатура.Ссылка
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	НЕ Номенклатура.Услуга
	|	И Номенклатура.ВестиУчетПоСериям
	|	И Номенклатура.Ссылка = &Ссылка");
	Запрос.УстановитьПараметр("Ссылка", ДанныеДляРасчетаСерии.Номенклатура);
	
	Результат = Запрос.Выполнить();
	
	ТребуетсяВыбор = Не Результат.Пустой();
	
	КэшированныеЗначения.ПризнакУказанияСерий.Вставить(ДанныеДляРасчетаСерии.Номенклатура, ТребуетсяВыбор);
	
КонецПроцедуры

// Заполняет имена реквизитов, от значений которых зависят параметры указания серий.
//
// Параметры:
//  ИменаРеквизитов - Строка           - Результат заполнения.
//  ТипОбъекта      - ОбъектМетаданных - Тип объекта коллекции объектов метаданных.
Процедура ЗаполнитьИменаРеквизитовДляЗаполненияПараметровУказанияСерий(ИменаРеквизитов, ТипОбъекта) Экспорт
	

	Если ТипОбъекта = Метаданные.Документы.ТТНВходящаяЕГАИС Тогда
		ИменаРеквизитов = "Дата,ДокументОснование,ВидОперации,ТорговыйОбъект";
		
	Иначе
		ИменаРеквизитов = "";








	КонецЕсли;


	
КонецПроцедуры

// Заполняет параметры указания серий для товаров, указанных в объекте.
//
// Параметры:
//  Параметры  - Структура        - выходной, параметры указания серий, состав полей определен в функции
//                                  НоменклатураКлиентСервер.ПараметрыУказанияСерий.
//  ТипОбъекта - ОбъектМетаданных - Тип объекта коллекции объектов метаданных.
//  Объект     - Структура        - Структура значений реквизитов объекта, необходимых для заполнения параметров указания серий.
Процедура ЗаполнитьПараметрыУказанияСерий(Параметры, ТипОбъекта, Объект) Экспорт
	

	Если ТипОбъекта = Метаданные.Документы.ТТНИсходящаяЕГАИС Тогда
		
		Параметры = ИнтеграцияЕГАИСУТ.ПараметрыУказанияСерийТТНИсходящаяЕГАИС(Объект);
		
	ИначеЕсли ТипОбъекта = Метаданные.Документы.ТТНВходящаяЕГАИС Тогда
		
		Параметры = ИнтеграцияЕГАИСУТ.ПараметрыУказанияСерийТТНВходящаяЕГАИС(Объект);
		
	ИначеЕсли ТипОбъекта = Метаданные.Документы.ЧекЕГАИС Тогда
		
		Параметры = ИнтеграцияЕГАИСУТ.ПараметрыУказанияСерийЧекЕГАИС(Объект);
	
	ИначеЕсли ТипОбъекта = Метаданные.Документы.ЧекЕГАИСВозврат Тогда
		
		Параметры = ИнтеграцияЕГАИСУТ.ПараметрыУказанияСерийЧекЕГАИСВозврат(Объект);
		
	ИначеЕсли ТипОбъекта = Метаданные.Документы.АктПостановкиНаБалансЕГАИС Тогда
		
		Параметры = ИнтеграцияЕГАИСУТ.ПараметрыУказанияСерийАктПостановкиНаБалансЕГАИС(Объект);
		
	ИначеЕсли ТипОбъекта = Метаданные.Документы.АктСписанияЕГАИС Тогда
		
		Параметры = ИнтеграцияЕГАИСУТ.ПараметрыУказанияСерийАктСписанияЕГАИС(Объект);
		
	ИначеЕсли ТипОбъекта = Метаданные.Документы.ПередачаВРегистр2ЕГАИС Тогда
		
		Параметры = ИнтеграцияЕГАИСУТ.ПараметрыУказанияСерийПередачаВРегистр2ЕГАИС(Объект);
		
	ИначеЕсли ТипОбъекта = Метаданные.Документы.ВозвратИзРегистра2ЕГАИС Тогда
		
		Параметры = ИнтеграцияЕГАИСУТ.ПараметрыУказанияСерийВозвратИзРегистра2ЕГАИС(Объект);
		
	ИначеЕсли ТипОбъекта = Метаданные.Обработки.СопоставлениеКлассификаторовЕГАИС Тогда
		
		Параметры = ИнтеграцияЕГАИСУТ.ПараметрыУказанияСерийСопоставлениеКлассификаторовЕГАИС(Объект);
		
	ИначеЕсли ТипОбъекта = Метаданные.Справочники.ШтрихкодыУпаковокТоваров Тогда
		
		Параметры = ИнтеграцияЕГАИСУТ.ПараметрыУказанияСерийШтрихкодыУпаковокТоваров(Объект);
		
	ИначеЕсли ТипОбъекта = Метаданные.РегистрыСведений.СоответствиеНоменклатурыЕГАИС Тогда
		
		Параметры = ИнтеграцияЕГАИСУТ.ПараметрыУказанияСерийСоответствиеНоменклатурыЕГАИС(Объект);
		
	//ИСМП	
	ИначеЕсли ТипОбъекта = Метаданные.Документы.МаркировкаТоваровИСМП Тогда 
	
		Параметры = ИнтеграцияИСМПУТ.ПараметрыУказанияСерийМаркировкаТоваровИСМП(Объект);
		
	ИначеЕсли ТипОбъекта = Метаданные.Документы.ВыводИзОборотаИСМП Тогда 
	
		Параметры = ИнтеграцияИСМПУТ.ПараметрыУказанияСерийВыводИзОборотаИСМП(Объект);
		
	ИначеЕсли ТипОбъекта = Метаданные.Документы.ОтгрузкаТоваровИСМП Тогда 
	
		Параметры = ИнтеграцияИСМПУТ.ПараметрыУказанияСерийОтгрузкаТоваровИСМП(Объект);
		
	ИначеЕсли ТипОбъекта = Метаданные.Документы.СписаниеКодовМаркировкиИСМП Тогда 
	
		Параметры = ИнтеграцияИСМПУТ.ПараметрыУказанияСерийСписаниеКодовМаркировки(Объект);
		
	ИначеЕсли ТипОбъекта = Метаданные.Документы.ВозвратВОборотИСМП Тогда 
	
		Параметры = ИнтеграцияИСМПУТ.ПараметрыУказанияСерийВозвратВОборотИСМП(Объект);
		
	ИначеЕсли ТипОбъекта = Метаданные.Документы.ПриемкаТоваровИСМП Тогда 
	
		Параметры = ИнтеграцияИСМПУТ.ПараметрыУказанияСерийПриемкаТоваровИСМП(Объект);
		
	ИначеЕсли ТипОбъекта = Метаданные.Обработки.ВиртуальнаяАгрегацияУпаковокИСМП Тогда 
	
		Параметры = ИнтеграцияИСМПУТ.ПараметрыУказанияСерийВиртуальнаяАгрегацияУпаковокИСМП(Объект);
		
	ИначеЕсли ТипОбъекта = Метаданные.Обработки.ПроверкаИПодборПродукцииИСМП Или ТипОбъекта = Метаданные.Обработки.ПроверкаИПодборТабачнойПродукцииМОТП Тогда 
	
		Параметры = ИнтеграцияИСМПУТ.ПараметрыУказанияСерийПроверкаИПодборПродукцииИСМП(Объект);
		
	ИначеЕсли ТипОбъекта = Метаданные.Документы.УточнениеСведенийОКодахМаркировкиИСМП Тогда
		
		Параметры = ИнтеграцияИСМПУТ.ПараметрыУказанияСерийУточнениеСведенийОКодахМаркировкиИСМП(Объект);
		
	ИначеЕсли ТипОбъекта = Метаданные.Документы.УведомлениеОПланируемомИмпортеЕГАИС Тогда
		
		Параметры = ИнтеграцияЕГАИСУТ.ПараметрыУказанияСерийУведомлениеОПланируемомИмпортеЕГАИС(Объект);
		
	ИначеЕсли ТипОбъекта = Метаданные.Документы.ОтчетОбИмпортеЕГАИС Тогда
		
		Параметры = ИнтеграцияЕГАИСУТ.ПараметрыУказанияСерийОтчетОбИмпортеЕГАИС(Объект);
		
	ИначеЕсли ТипОбъекта = Метаданные.Документы.ОтчетОПроизводствеЕГАИС Тогда
		
		Параметры = ИнтеграцияЕГАИСУТ.ПараметрыУказанияСерийОтчетОПроизводствеЕГАИС(Объект);
		
	ИначеЕсли ТипОбъекта = Метаданные.Документы.СписаниеПартийЗЕРНО Тогда
		
		Параметры = ИнтеграцияЗЕРНОУТ.ПараметрыУказанияСерийСписаниеПартийЗЕРНО(Объект);
		
	ИначеЕсли ТипОбъекта = Метаданные.Документы.ВнесениеСведенийОСобранномУрожаеЗЕРНО Тогда
		
		Параметры = ИнтеграцияЗЕРНОУТ.ПараметрыУказанияСерийВнесениеСведенийОСобранномУрожаеЗЕРНО(Объект);
		
	ИначеЕсли ТипОбъекта = Метаданные.Документы.ЗапросОстатковПартийЗЕРНО Тогда
		
		Параметры = ИнтеграцияЗЕРНОУТ.ПараметрыУказанияСерийЗапросОстатковПартийЗЕРНО(Объект);
		
	ИначеЕсли ТипОбъекта = Метаданные.Документы.ОформлениеСДИЗЗЕРНО Тогда
		
		Параметры = ИнтеграцияЗЕРНОУТ.ПараметрыУказанияСерийОформлениеСДИЗЗЕРНО(Объект);
		
	ИначеЕсли ТипОбъекта = Метаданные.Документы.ПогашениеСДИЗЗЕРНО Тогда
		
		Параметры = ИнтеграцияЗЕРНОУТ.ПараметрыУказанияСерийПогашениеСДИЗЗЕРНО(Объект);
		
	ИначеЕсли ТипОбъекта = Метаданные.Документы.СписаниеПартийЗЕРНО Тогда
		
		Параметры = ИнтеграцияЗЕРНОУТ.ПараметрыУказанияСерийСписаниеПартийЗЕРНО(Объект);
		
	ИначеЕсли ТипОбъекта = Метаданные.Документы.ФормированиеПартийЗЕРНО Тогда
		
		Параметры = ИнтеграцияЗЕРНОУТ.ПараметрыУказанияСерийФормированиеПартийЗЕРНО(Объект);
		
	ИначеЕсли ТипОбъекта = Метаданные.Документы.ФормированиеПартийИзДругихПартийЗЕРНО Тогда
		
		Параметры = ИнтеграцияЗЕРНОУТ.ПараметрыУказанияСерийФормированиеПартийИзДругихПартийЗЕРНО(Объект);
		
	ИначеЕсли ТипОбъекта = Метаданные.Документы.ФормированиеПартийПриПроизводствеЗЕРНО Тогда
		
		Параметры = ИнтеграцияЗЕРНОУТ.ПараметрыУказанияСерийФормированиеПартийПриПроизводствеЗЕРНО(Объект);
		
	ИначеЕсли ТипОбъекта = Метаданные.РегистрыСведений.СоответствиеПартийЗЕРНО Тогда
		
		Параметры = ИнтеграцияЗЕРНОУТ.ПараметрыУказанияСерийСоответствиеПартийЗЕРНО(Объект);
		
	//Общие	
	ИначеЕсли ТипОбъекта = Метаданные.ОбщиеФормы.УточнениеСоставаУпаковкиИС Тогда 
		
		Параметры.ИмяТЧТовары = "ДанныеДляУточнения";
		Параметры.ИмяТЧСерии = "ДанныеДляУточнения";
		
	КонецЕсли;

КонецПроцедуры

// Возвращает параметры указания серий для товаров, указанных в объекте.
//
// Параметры:
//  Параметры - Структура - выходной, особенности указания серий в объекте, состав полей определен в функции
//                          НоменклатураКлиентСервер.ПараметрыУказанияСерий.
//  Объект    - Объект, ДанныеФормыСтруктура - Объект, для формы которого нужно сформировать параметры
//                                             указания серий.
//  МенеджерОбъекта - Менеджер - Менеджер объекта, для формы которого нужно сформировать параметры указания серий.
Процедура ЗаполнитьПараметрыУказанияСерийФормыОбъекта(Параметры, Объект, МенеджерОбъекта) Экспорт
	
	Параметры = НоменклатураСервер.ПараметрыУказанияСерий(Объект, МенеджерОбъекта);
	
КонецПроцедуры

// Извлекает из временного хранилища серии, указанные в форме редактирования серий, помещает эти строки в ТЧ "Серии" объекта,
//   перерасчитывает статусы указания серий строках товарной табличной части.
//
// Параметры:
//  Форма                       - ФормаКлиентскогоПриложения    - Источник вызова.
//  ПараметрыФормыУказанияСерий - Структура           - Состав полей определен в функции НоменклатураСервер.ПараметрыФормыУказанияСерий.
//  КэшированныеЗначения        - Структура           - Сохраненные значения параметров, используемых при обработке строки таблицы.
Процедура ОбработатьУказаниеСерий(Форма, ПараметрыФормыУказанияСерий, КэшированныеЗначения) Экспорт
	
	ПараметрыУказанияСерий = Форма.ПараметрыУказанияСерий;
	Объект = Форма.Объект;

	НоменклатураСервер.ОбработатьУказаниеСерий(Объект, ПараметрыУказанияСерий, ПараметрыФормыУказанияСерий, ,
		КэшированныеЗначения);
	
КонецПроцедуры

// Процедура заполняет статусы указания серий в строках товарной табличной части.
//
// Параметры:
//  Объект                 - ДанныеФормыСтруктура - Основной реквизит формы объекта.
//  ПараметрыУказанияСерий - Структура            - Состав полей определен в функции НоменклатураКлиентСервер.ПараметрыУказанияСерий.

Процедура ЗаполнитьСтатусыУказанияСерий(Объект, ПараметрыУказанияСерий) Экспорт
	
	НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(Объект, ПараметрыУказанияСерий);

КонецПроцедуры

Процедура ЗаполнитьСтатусыУказанияСерийФормы(Форма, ИмяРеквизитаОбъект = "Объект", ИмяРеквизитаПараметрыУказанияСерий = "ПараметрыУказанияСерий") Экспорт
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыУказанияСерийФормы(Менеджер, Форма, ИмяРеквизитаОбъект = "Объект", ИмяРеквизитаПараметрыУказанияСерий = "ПараметрыУказанияСерий") Экспорт
	
КонецПроцедуры

// Процедура пересчитывает статусы указания серий в строках товаров, если это необходимо, переподчиняет строки серий 
// другим строкам таблицы.
//
// Параметры:
//  Форма                      - ФормаКлиентскогоПриложения    - Источник вызова.
//  ПараметрыУказанияСерий     - Структура           - Состав полей определен в функции НоменклатураКлиентСервер.ПараметрыУказанияСерий.
//  ТекущаяСтрокаИдентификатор - Число               - Идентификатор текущей строки товаров в форме объекта.
//  КэшированныеЗначения       - Структура           - Сохраненные значения параметров, используемых при обработке строки таблицы.
Процедура ЗаполнитьСтатусыУказанияСерийПриОкончанииРедактированияСтрокиТЧ(
	Форма,
	ПараметрыУказанияСерий = "",
	ТекущаяСтрокаИдентификатор,
	КэшированныеЗначения) Экспорт
	

	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "Объект") Тогда
		Объект = Форма.Объект;
	Иначе
		Объект = Форма;
	КонецЕсли;
	
	Если (ПараметрыУказанияСерий = "") Тогда
		ПараметрыУказанияСерий = Форма.ПараметрыУказанияСерий;
	КонецЕсли;
	
	НоменклатураСервер.ЗаполнитьСтатусыУказанияСерийПриОкончанииРедактированияСтрокиТЧ(Объект, ПараметрыУказанияСерий,
		ТекущаяСтрокаИдентификатор, КэшированныеЗначения);


	
КонецПроцедуры

// Заполняет текст запроса заполнения статусов указания серий для товаров, указанных в документе.
//
// Параметры:
//  ТекстЗапроса           - Строка           - Текст запроса заполнения статусов указания серий.
//  ТипОбъекта             - ОбъектМетаданных - Тип документа коллекции объектов метаданных.
//  ПараметрыУказанияСерий - Структура        - Состав полей определен в функции НоменклатураКлиентСервер.ПараметрыУказанияСерий.
Процедура ЗаполнитьТекстЗапросаЗаполненияСтатусовУказанияСерий(ТекстЗапроса, ТипОбъекта, ПараметрыУказанияСерий) Экспорт
	
	Если ТипОбъекта = Метаданные.Справочники.ШтрихкодыУпаковокТоваров Тогда
		
		ТекстЗапроса = ИнтеграцияЕГАИСУТ.ТекстЗапросаЗаполненияСтатусовУказанияСерийШтрихкодыУпаковокТоваров(ПараметрыУказанияСерий);
		
	ИначеЕсли ТипОбъекта = Метаданные.РегистрыСведений.СоответствиеНоменклатурыЕГАИС Тогда
		
		ТекстЗапроса = ИнтеграцияЕГАИСУТ.ТекстЗапросаЗаполненияСтатусовУказанияСерийСоответствиеНоменклатурыЕГАИС(ПараметрыУказанияСерий);

	ИначеЕсли ТипОбъекта = Метаданные.Документы.ТТНВходящаяЕГАИС Тогда
		
		ТекстЗапроса = ИнтеграцияЕГАИСУТ.ТекстЗапросаЗаполненияСтатусовУказанияСерийТТНВходящаяЕГАИС(ПараметрыУказанияСерий);
		
	ИначеЕсли ТипОбъекта = Метаданные.Документы.ТТНИсходящаяЕГАИС
		ИЛИ  ТипОбъекта = Метаданные.Документы.ЧекЕГАИС
		ИЛИ ТипОбъекта = Метаданные.Документы.ЧекЕГАИСВозврат
		ИЛИ ТипОбъекта = Метаданные.Документы.АктПостановкиНаБалансЕГАИС
		ИЛИ ТипОбъекта = Метаданные.Документы.АктСписанияЕГАИС
		ИЛИ ТипОбъекта = Метаданные.Документы.ПередачаВРегистр2ЕГАИС
		ИЛИ ТипОбъекта = Метаданные.Документы.ВозвратИзРегистра2ЕГАИС
		ИЛИ ТипОбъекта = Метаданные.Документы.ОстаткиЕГАИС
		ИЛИ ТипОбъекта = Метаданные.Обработки.СопоставлениеКлассификаторовЕГАИС Тогда
		
		ТекстЗапроса = ИнтеграцияЕГАИСУТ.ТекстЗапросаЗаполненияСтатусовУказанияСерий(ПараметрыУказанияСерий);

	ИначеЕсли ТипОбъекта = Метаданные.Обработки.ВиртуальнаяАгрегацияУпаковокИСМП
		ИЛИ ТипОбъекта = Метаданные.Обработки.ПроверкаИПодборПродукцииИСМП
		ИЛИ ТипОбъекта = Метаданные.Обработки.ПроверкаИПодборТабачнойПродукцииМОТП Тогда
		
		ТекстЗапроса = ИнтеграцияИСМПУТ.ТекстЗапросаЗаполненияСтатусовУказанияСерийПроверкаИПодборПродукцииИСМП(ПараметрыУказанияСерий);
		
	ИначеЕсли ТипОбъекта = Метаданные.Документы.МаркировкаТоваровИСМП
		ИЛИ ТипОбъекта = Метаданные.Документы.ВыводИзОборотаИСМП 
		ИЛИ ТипОбъекта = Метаданные.Документы.ОтгрузкаТоваровИСМП 
		ИЛИ ТипОбъекта = Метаданные.Документы.СписаниеКодовМаркировкиИСМП 
		ИЛИ ТипОбъекта = Метаданные.Документы.ПриемкаТоваровИСМП 
		Или ТипОбъекта = Метаданные.Документы.УточнениеСведенийОКодахМаркировкиИСМП Тогда
		
		ТекстЗапроса = ИнтеграцияЕГАИСУТ.ТекстЗапросаЗаполненияСтатусовУказанияСерий(ПараметрыУказанияСерий);
		
	Иначе
		
		//Считать значением по-умолчанию
		ТекстЗапроса = ИнтеграцияЕГАИСУТ.ТекстЗапросаЗаполненияСтатусовУказанияСерий(ПараметрыУказанияСерий);

	КонецЕсли;
	
КонецПроцедуры

// Заполняет текст запроса проверки заполнения серий в документе.
//
// Параметры:
//  ТекстЗапроса           - Строка           - Текст запроса проверки заполнения серий.
//  ТипОбъекта             - ОбъектМетаданных - Тип документа коллекции объектов метаданных.
//  ПараметрыУказанияСерий - Структура        - Состав полей определен в функции НоменклатураКлиентСервер.ПараметрыУказанияСерий.
Процедура ЗаполнитьТекстЗапросаПроверкиЗаполненияСерий(ТекстЗапроса, ТипОбъекта, ПараметрыУказанияСерий) Экспорт
	
	Возврат;
	
КонецПроцедуры

// Процедура формирования движений по регистру "Движения серий товаров".
//
// Параметры:
//  ДополнительныеСвойства - Структура - дополнительные свойства для записи движений 
//  Движения - КоллекцияДвижений - движения по которым двигается документ
//  Отказ - Булево - признак отказа от проведения документа
Процедура ОтразитьДвиженияСерийТоваров(ДополнительныеСвойства, Движения, Отказ) Экспорт

	Таблица= ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаДвиженияСерийТоваров;
	Если Отказ ИЛИ Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	Движения.ДвиженияСерийТоваров.Записывать = Истина;
	Движения.ДвиженияСерийТоваров.Загрузить(Таблица);

КонецПроцедуры

#КонецОбласти

#Область Локализация

Процедура ПриОпределенииМенеджераОбъектаПоПолномуИмени(МенеджерОбъекта, ПолноеИмя) Экспорт
	
	Возврат;
	
КонецПроцедуры

Процедура ПриОпределенииМенеджераОбъектаПоСсылке(МенеджерОбъекта, Ссылка) Экспорт

	Возврат;
	
КонецПроцедуры

Процедура ПередЗаписьюОбъекта(ДокументОбъект, Отказ, РежимЗаписи = Неопределено, РежимПроведения = Неопределено) Экспорт
	
	Если ТипЗнч(ДокументОбъект) = Тип("СправочникОбъект.РеестрПартийЗЕРНО") Тогда 
		Представление =  СтрШаблон("%1, %2, %3, %4", 
			ДокументОбъект.Идентификатор,
			ДокументОбъект.ОКПД2,
			ДокументОбъект.НазначениеПартии,
			ДокументОбъект.ЦельИспользования);
		Если ДокументОбъект.ТребуетсяЗагрузка Тогда
			Представление = СтрШаблон(НСтр("ru = '(не загружено) %1'"), Представление);
		КонецЕсли;
		ДокументОбъект.Наименование = Представление;
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("СправочникОбъект.КлючиАдресовЗЕРНО") Тогда 
		ДокументОбъект.Наименование = ДокументОбъект.Идентификатор;
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("СправочникОбъект.СДИЗЗЕРНО") Тогда 
		Если ДокументОбъект.ТребуетсяЗагрузка Тогда
			ДокументОбъект.Наименование = СтрШаблон(НСтр("ru = '%1 (не загружено)'"), ДокументОбъект.Идентификатор);
		Иначе
			ДокументОбъект.Наименование = Строка(ДокументОбъект.Идентификатор);
		КонецЕсли;
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("СправочникОбъект.РезультатыИсследованийЗЕРНО") Тогда 
		Представление =  СтрШаблон("%1, %2, %3",
			ДокументОбъект.Идентификатор,
			ДокументОбъект.ОКПД2,
			ДокументОбъект.ЦельИспользования);
		Если ДокументОбъект.ТребуетсяЗагрузка Тогда
			Представление = СтрШаблон(НСтр("ru = '(не загружено) %1'"), Представление);
		КонецЕсли;
		ДокументОбъект.Наименование = Представление;
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("СправочникОбъект.ШтрихкодыУпаковокТоваров") Тогда 
		ДокументОбъект.Наименование = ДокументОбъект.ЗначениеШтрихкода;
	КонецЕсли;
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область ДополнительныеПараметры

// В функции требуется определить настройку для использования формы уточнения данных ИС для неизвестных штрихкодов
//
Функция ИспользоватьУточнениеДанных() Экспорт
	
	Возврат Константы.ИспользоватьУточнениеДанных.Получить();
	
КонецФункции

#КонецОбласти

Процедура ДополнитьИсточникиОписанияНоменклатуры(Номенклатура, СоответстиеИсточникаНоменклатуре) Экспорт
	
	Возврат;
	
КонецПроцедуры

Процедура ИспользованиеЧастичноеВыбытие(Используется, ВидПродукцииИС) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидПродукцииИС", ВидПродукцииИС);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	1
	|ИЗ
	|	РегистрСведений.ОписаниеНоменклатурыИС КАК ОписаниеНоменклатурыИС
	|ГДЕ
	|	ОписаниеНоменклатурыИС.ВариантЧастичногоВыбытия <> ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаЧастичногоВыбытияИС.ПустаяСсылка)
	|	И ОписаниеНоменклатурыИС.Номенклатура.ВидПродукцииИС = &ВидПродукцииИС
	|";
	Используется = Не Запрос.Выполнить().Пустой();
	
КонецПроцедуры
	
Процедура ИспользованиеПроизвольныхЕдиницУчета(Используется, ВидПродукцииИС) Экспорт
	
	Запрос = Новый Запрос;
	
	ВидыПродукции = Новый Массив;
	Если ВидПродукцииИС = Неопределено Тогда
		Для Каждого ВидПродукции Из ИнтеграцияИСМПКлиентСерверПовтИсп.УчитываемыеВидыМаркируемойПродукции() Цикл
			ВидыПродукции.Добавить(ВидПродукции);
		КонецЦикла;
	Иначе
		ВидыПродукции.Добавить(ВидПродукцииИС);
	КонецЕсли;
	Запрос.УстановитьПараметр("ВидыПродукции", ВидыПродукции);
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	1
		|ИЗ
		|	Справочник.Номенклатура КАК СправочникНоменклатура
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОписаниеНоменклатурыИС КАК ОписаниеНоменклатурыИС
		|		ПО ОписаниеНоменклатурыИС.Номенклатура = СправочникНоменклатура.Ссылка
		|		И (ОписаниеНоменклатурыИС.КоличествоВПотребительскойУпаковке <> 1
		|			Или ОписаниеНоменклатурыИС.ЕмкостьПотребительскойУпаковки > 1)
		|ГДЕ
		|	Номенклатура.ВидПродукцииИС В(&ВидыПродукции)";
	Используется = Не Запрос.Выполнить().Пустой();
	
КонецПроцедуры
	
Процедура ИспользованиеПродукцииТребующейВзвешивания(Используется) Экспорт
	
	Запрос = Новый Запрос;
	ВидыПродукции = ИнтеграцияИСМПКлиентСерверПовтИсп.УчитываемыеВидыМаркируемойПродукции();
	Запрос.УстановитьПараметр("ВидыПродукции", ВидыПродукции);
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	1
		|ИЗ
		|	Справочник.Номенклатура КАК СправочникНоменклатура
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОписаниеНоменклатурыИС КАК ОписаниеНоменклатурыИС
		|		ПО ОписаниеНоменклатурыИС.Номенклатура = СправочникНоменклатура.Ссылка
		|		И ОписаниеНоменклатурыИС.КоличествоВПотребительскойУпаковке = 0
		|ГДЕ
		|	Номенклатура.ВидПродукцииИС В(&ВидыПродукции)";
	Используется = Не Запрос.Выполнить().Пустой();
	
КонецПроцедуры

Процедура ОбновитьТекстЗапросаСРегистромОписаниеНоменклатурыИС(ТекстЗапроса, ПутьКРегистру, ПутьКПолюНоменклатура) Экспорт
	
	Возврат;
	
КонецПроцедуры

Процедура ПриОпределенииКоэффициентаУпаковкиНоменклатуры(Номенклатура, ПотребительскаяУпаковка, КоличествоВПотребительскойУпаковке) Экспорт
	
	КоличествоВПотребительскойУпаковке = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПотребительскаяУпаковка, "Коэффициент");
	
КонецПроцедуры

Процедура КоэффициентУпаковки(Коэффициент, Упаковка, Номенклатура, КэшированныеЗначения) Экспорт
	
	Если ТипЗнч(Упаковка) = Тип("СправочникСсылка.КлассификаторЕдиницИзмерения") 
		И ЗначениеЗаполнено(Номенклатура) Тогда
		ЕдиницаИзмерения = Справочники.ЕдиницыИзмерения.НайтиПоРеквизиту("ЕдиницаПоКлассификатору", Упаковка,,Номенклатура);
		Коэффициент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЕдиницаИзмерения, "Коэффициент");
	Иначе
		Коэффициент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Упаковка, "Коэффициент");
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбъемУпаковки(Объем, Знач Номенклатура, Знач Упаковка, КэшированныеЗначения) Экспорт
	
	Если Не ЗначениеЗаполнено(Упаковка) Тогда
		Упаковка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ЕдиницаХраненияОстатков");
	КонецЕсли;
	Объем = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Упаковка, "Объем");
	
КонецПроцедуры

Процедура КодОКОПФОрганизации(КодОКОПФ, Организация) Экспорт
	КодОКОПФ = "";
КонецПроцедуры

Процедура ДополнитьТекстЗапросаОписанияНоменклатуры(ТекстЗапроса) Экспорт
КонецПроцедуры

#КонецОбласти

// Вызывается при проведении документа. Выполняется в транзакции записи.
//
// Параметры:
//  ДокументОбъект - ДокументОбъект - проводимый документ,
//  Отказ - Булево - признак отказа от проведения документа,
//  РежимПроведения - РежимПроведенияДокумента - текущий режим проведения.
Процедура ОбработкаПроведения(ДокументОбъект, Отказ, РежимПроведения) Экспорт
	
	Возврат;
	
КонецПроцедуры

Процедура ОтразитьДвижения(ДополнительныеСвойства, Движения, Отказ) Экспорт 
КонецПроцедуры

Процедура ТекстыЗапросовТаблицыДвижения(Запрос, ТекстыЗапроса, Регистры, ДокументСсылка, ДополнительныеСвойства) Экспорт 
КонецПроцедуры

Процедура ПриОпределенииСпискаОрганизацийНедоступныхДляВыбора(СписокОрганизаций) Экспорт
	
	Возврат;
	
КонецПроцедуры

Процедура ПриГенерацииСерий(
		Объект, Товары, ВыделенныеСтроки, ПараметрыУказанияСерий, РезультатЗаполненияСерий) Экспорт
	
	Возврат;
	
КонецПроцедуры

Процедура ИспользоватьОбособленныеПодразделенияВыделенныеНаБаланс(Использовать) Экспорт
	
	Использовать = Истина;
	
КонецПроцедуры

Процедура ПриОпределенииИННКППGLNОрганизации(Результат, Организация, Подразделение) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Организации.ИНН КАК ИНН,
	|	Организации.КПП КАК КПП,
	|	"""" КАК GLN
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.Ссылка = &Организация
	|");
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЕдиницаИзмеренияКилограмм(ЕдиницаИзмеренияКилограмм) Экспорт
	
	Результат = Справочники.КлассификаторЕдиницИзмерения.НайтиПоКоду("166");
	Если Не ЗначениеЗаполнено(Результат) Тогда
		УстановитьПривилегированныйРежим(Истина);
		Объект = Справочники.КлассификаторЕдиницИзмерения.СоздатьЭлемент();
		Объект.Код = "166";
		Объект.Наименование = НСтр("ru = 'кг'");
		Объект.НаименованиеПолное = НСтр("ru = 'Килограмм'");
		Объект.МеждународноеСокращение = "KGM";
		Объект.Записать();
		Результат = Объект.Ссылка;
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	ЕдиницаИзмеренияКилограмм = Результат;
	
КонецПроцедуры

// Процедура заполняет признак использования гос.контрактов по 275ФЗ.
//
// Параметры:
//  Используется - Булево - Признак использования гос.контрактов по 275ФЗ. Значение по умолчанию Ложь.
Процедура ИспользуетсяПоддержкаПлатежейВСоответствииС275ФЗ(Используется) Экспорт
	
	Возврат;
	
КонецПроцедуры

Процедура НастроитьСпособРасчетаСтатусовОформления(РасчетОтдельно) Экспорт
КонецПроцедуры

Процедура КоэффициентВесОбъемУпаковок(ДанныеУпаковок, ВесОбъемУпаковкиВЕдиницеКонстантИС = Истина) Экспорт
	
	ТаблицаДляЗапроса = Новый ТаблицаЗначений;
	ТаблицаДляЗапроса.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаДляЗапроса.Колонки.Добавить("Упаковка",     Новый ОписаниеТипов("СправочникСсылка.ЕдиницыИзмерения"));
	ТаблицаДляЗапроса.Колонки.Добавить("ИндексСтроки", ОбщегоНазначения.ОписаниеТипаЧисло(5));
	
	Для Каждого Строка Из ДанныеУпаковок Цикл
		Если ЗначениеЗаполнено(Строка.Номенклатура) Тогда
			НоваяСтрока = ТаблицаДляЗапроса.Добавить();
			НоваяСтрока.Номенклатура = Строка.Номенклатура;
			НоваяСтрока.Упаковка     = Строка.Упаковка;
			НоваяСтрока.ИндексСтроки = ДанныеУпаковок.Индекс(Строка);
		КонецЕсли;
	КонецЦикла;
	
	Если ТаблицаДляЗапроса.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДанныеУпаковок", ТаблицаДляЗапроса);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеУпаковок.ИндексСтроки,
	|	ДанныеУпаковок.Номенклатура,
	|	ДанныеУпаковок.Упаковка
	|ПОМЕСТИТЬ ДанныеУпаковок
	|ИЗ
	|	&ДанныеУпаковок КАК ДанныеУпаковок
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеУпаковок.ИндексСтроки КАК ИндексСтроки,
	|	ВЫБОР
	|		КОГДА ДанныеУпаковок.Упаковка = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
	|		ТОГДА СправочникНоменклатура.ЕдиницаХраненияОстатков
	|		ИНАЧЕ ДанныеУпаковок.Упаковка
	|	КОНЕЦ КАК Упаковка
	|ПОМЕСТИТЬ Данные
	|ИЗ
	|	ДанныеУпаковок КАК ДанныеУпаковок
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|	ПО ДанныеУпаковок.Номенклатура = СправочникНоменклатура.Ссылка
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Данные.ИндексСтроки,
	|	Данные.Упаковка.Коэффициент КАК Коэффициент,
	|	Данные.Упаковка.Вес КАК ВесЕдиницы,
	|	Данные.Упаковка.Объем КАК ОбъемЕдиницы,
	|	ЛОЖЬ КАК ТипИзмеряемойВеличиныВес,
	|	ЛОЖЬ КАК ТипИзмеряемойВеличиныОбъем
	|ИЗ
	|	Данные КАК Данные";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Строка = ДанныеУпаковок[Выборка.ИндексСтроки];
		Строка.Коэффициент = Выборка.Коэффициент;
		Строка.Вес         = Выборка.ВесЕдиницы;
		Строка.Объем       = Выборка.ОбъемЕдиницы;
	КонецЦикла;
	
КонецПроцедуры

// Возвращает коэффициенты переданной единицы измерения
// 
// Параметры:
//  Упаковка - ОпределяемыйТип.Упаковка, ОпределяемыйТип.ЕдиницаИзмерения - Упаковка или единица измерения
//  Номенклатура - Массив Из ОпределяемыйТип.Номенклатура - Номенклатура
//  КоэффициентыУпаковокНоменклатуры - Соответствие из КлючИЗначение:
//   * Ключ - ОпределяемыйТип.Номенклатура
//   * Значение - структура:
//   * КодОшибки - Число  - Код ошибки получения коэффициента:
//        0 - Нет ошибок;
//        1 - Не заполнена упаковка;
//        2 - В справочнике 'Номенклатура' выключена возможность пересчета количества в соответствующую мерную единицу измерения;
//        3 - Не удалось сопоставить единицу хранения справочника 'Номенклатура' с единицей измерения 'Упаковка'.
//   * Коэффициент - Число  - Коэффициент пересчета единицы измерения из 'Упаковка'.
Процедура ПриОпределенииКоэффициентовУпаковки(Упаковка,  Номенклатура, КоэффициентыУпаковокНоменклатуры) Экспорт
	
	Если ТипЗнч(Упаковка) = Тип("СправочникСсылка.ЕдиницыИзмерения") Тогда 
		Коэффициент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Упаковка, "Коэффициент");
	    КоэффициентыУпаковокНоменклатуры.Вставить(Номенклатура, Новый Структура("КодОшибки,Коэффициент", 0, Коэффициент));
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Упаковка) = Тип("СправочникСсылка.КлассификаторЕдиницИзмерения") Тогда 
		Если ТипЗнч(Номенклатура) = Тип("СправочникСсылка.Номенклатура") Тогда 
		    КоэффициентыУпаковокНоменклатуры.Вставить(Номенклатура, Новый Структура("КодОшибки,Коэффициент", 3, 0));
		Иначе
			Для Каждого Элемент Из Номенклатура Цикл 
		    	КоэффициентыУпаковокНоменклатуры.Вставить(Элемент, Новый Структура("КодОшибки,Коэффициент", 3, 0));
			КонецЦикла;	
		КонецЕсли;
		ОбратныйКоэффициент = 0;
		ТекстЗапроса = "ВЫБРАТЬ Ссылка, %1 КАК Коэффициент ИЗ Справочник.Номенклатура ГДЕ Ссылка В (&Номенклатура)";
		Если Упаковка = ИнтеграцияИСКлиентСерверПовтИсп.ЕдиницаИзмеренияКилограмм() Тогда
			ТекстЗапроса = СтрШаблон(ТекстЗапроса, "ЕдиницаХраненияОстатков.Вес");
		Иначе
			ТекстЗапроса = СтрШаблон(ТекстЗапроса, "ЕдиницаХраненияОстатков.Объем");
		КонецЕсли;
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл 
			Если Выборка.Коэффициент > 0 Тогда
			    КоэффициентыУпаковокНоменклатуры.Вставить(Выборка.Ссылка, Новый Структура("КодОшибки,Коэффициент", 0, 1/Выборка.Коэффициент));
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры
