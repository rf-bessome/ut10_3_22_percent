#Область ПрограммныйИнтерфейс

// Возвращает объект ЗащищенноеСоединениеOpenSSL
// 
// Возвращаемое значение:
// 	ЗащищенноеСоединениеOpenSSL - Описание
Функция ЗащищенноеСоединение() Экспорт
	
	Возврат ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
	
КонецФункции

// Возвращает структуру реквизитов справочника.
//
// Параметры:
//		ИмяСправочника - Строка - имя справочника как оно задано в метаданных
// Возвращаемое значение:
//		Структура - стандартные и обычные реквизиты справочника.
//
Функция РеквизитыСправочника(ИмяСправочника) Экспорт
	
	СтруктураРеквизитов   = Новый Структура;
	МетаданныеСправочника = Метаданные.Справочники[ИмяСправочника];
	
	Для Каждого Реквизит Из МетаданныеСправочника.СтандартныеРеквизиты Цикл
		СтруктураРеквизитов.Вставить(Реквизит.Имя, Реквизит.Синоним);
	КонецЦикла;
	
	Для Каждого Реквизит Из МетаданныеСправочника.Реквизиты Цикл
		СтруктураРеквизитов.Вставить(Реквизит.Имя, Реквизит.Синоним);
	КонецЦикла;
	
	Возврат СтруктураРеквизитов;
	
КонецФункции

// Определяет включено ли ведение учета продукции, подлежащей маркированию.
//
// Параметры:
// Возвращаемое значение:
//  Булево - Истина, если учет ведется.
Функция ВедетсяУчетМаркируемойПродукции() Экспорт
	
	УчитываемыеВидыМаркируемойПродукции = ИнтеграцияИС.УчитываемыеВидыМаркируемойПродукции();
	
	Возврат УчитываемыеВидыМаркируемойПродукции.Количество() > 0;
	
КонецФункции

// Возвращает признак принадлежности переданного в параметре вида продукции к виду продукции ИС МП.
// 
// Параметры:
// 	ВидПродукции              - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции для анализа.
// 	ВключаяТабачнуюПродукцию  - Булево                             - Признак включения табачной продукции.
// 	ВключатьМолочнуюПродукцию - Булево                             - Признак включения молочной продукции.
// 	ВключатьПиво              - Булево                             - Признак включения пивной продукции.
// Возвращаемое значение:
// 	Булево - Принадлежность к виду продукции ИСМП
//
Функция ЭтоПродукцияИСМП(ВидПродукции, ВключаяТабачнуюПродукцию = Ложь, ВключатьМолочнуюПродукцию = Истина, ВключатьПиво = Истина) Экспорт
	
	ВидыПродукцииИСМП = ИнтеграцияИСКлиентСервер.ВидыПродукцииИСМП(ВключаяТабачнуюПродукцию, ВключатьМолочнуюПродукцию, ВключатьПиво);
	
	Возврат ВидыПродукцииИСМП.Найти(ВидПродукции) <> Неопределено;
	
КонецФункции

// Возвращает признак принадлежности переданного в параметре вида продукции к табачной продукции.
// 
// Параметры:
// 	ВидПродукции              - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции для анализа.
// Возвращаемое значение:
// 	Булево - Принадлежность к виду продукции ИСМП
//
Функция ЭтоПродукцияМОТП(ВидПродукции) Экспорт
	
	Возврат ИнтеграцияИСКлиентСервер.ЭтоПродукцияМОТП(ВидПродукции);
	
КонецФункции

Функция GTINНабораИВложенийПринадлежатОднойНоменклатуре(GTINНабора, GTINВложений) Экспорт
	
	EANНабора = РазборКодаМаркировкиИССлужебныйКлиентСерверПовтИсп.ШтрихкодEANИзGTIN(GTINНабора);
	EANВложений = РазборКодаМаркировкиИССлужебныйКлиентСерверПовтИсп.ШтрихкодEANИзGTIN(GTINВложений);
	
	ШтрихкодыEAN = Новый Массив;
	ШтрихкодыEAN.Добавить(EANНабора);
	ШтрихкодыEAN.Добавить(EANВложений);
	
	GTINПотребительскихУпаковок = Новый Соответствие;
	GTINПотребительскихУпаковок.Вставить(EANВложений, Истина);
	
	ИнформацияПоШтрихкодамEAN = ШтрихкодированиеИС.ДанныеПоШтрихкодамEAN(ШтрихкодыEAN, Неопределено, GTINПотребительскихУпаковок);
	
	Наборы = Новый Массив;
	Для Каждого СтрокаТЧ Из ИнформацияПоШтрихкодамEAN Цикл
		Если СтрокаТЧ.ВидУпаковкиИС = Перечисления.ВидыУпаковокИС.Набор Тогда
			Наборы.Добавить(СтрокаТЧ);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрокаТЧ Из ИнформацияПоШтрихкодамEAN Цикл
		
		Если СтрокаТЧ.ВидУпаковкиИС <> Перечисления.ВидыУпаковокИС.Потребительская Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого Набор Из Наборы Цикл
			Если Набор.Номенклатура = СтрокаТЧ.Номенклатура
				И Набор.Характеристика = СтрокаТЧ.Характеристика Тогда
				Возврат Истина;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти