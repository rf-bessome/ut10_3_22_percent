// Универсальные механизмы интеграции ИС (ЕГАИС, ГИСМ, ВЕТИС, ...)

#Область ПрограммныйИнтерфейс

// Возвращяет список типа Перечисления.ВидыПродукцииИС содержащийся в ТЧ.
// 
// Параметры:
// 	Товары - ДанныеФормыКоллекция - ТЧ формы документа.
// Возвращаемое значение:
// 	СписокЗначений - список типа Перечисления.ВидыПродукцииИС содержащийся в ТЧ.
Функция ВидыПродукцииВТоварах(Товары) Экспорт
	
	Результат = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Номенклатура.ВидПродукцииИС КАК ВидПродукцииИС
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка В (&СписокНоменклатуры)";
	
	Запрос.УстановитьПараметр("СписокНоменклатуры", Товары.Выгрузить().ВыгрузитьКолонку("Номенклатура"));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Не ЗначениеЗаполнено(Выборка.ВидПродукцииИС) Тогда
			Продолжить;
		КонецЕсли;
		
		Результат.Добавить(Выборка.ВидПродукцииИС, СинонимПеречисления(Выборка.ВидПродукцииИС));
		
	КонецЦикла;
	
	Если Результат.Количество() = 0 Тогда
		Для Каждого ЗначениеПеречисления Из Перечисления.ВидыПродукцииИС Цикл
			Результат.Добавить(ЗначениеПеречисления, СинонимПеречисления(ЗначениеПеречисления))
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Удаляет акцизные марки по набору полей: номенклатура/характеристика/серия.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма.
//  ДанныеОтбора - Массив (Структура см. ИнтеграцияИСУТКлиентСервер.ПоляДляПоискаМаркированнойПродукции())
//  	- Список отборов для поиска удаляемых данных в ТЧ АкцизныеМарки.
//
Процедура УдалитьАкцизныеМаркиЧека(Форма, ДанныеОтбора, Отказ, ДеревоУпаковок = Неопределено) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если ДеревоУпаковок = Неопределено Тогда
		Ошибки = Неопределено;
	КонецЕсли;
	
	Источник = Форма;
	
	Если ДеревоУпаковок = Неопределено Тогда
		ДеревоУпаковок = ШтрихкодыУпаковок(Источник).ДеревоУпаковок;
	КонецЕсли;
	
	ДанныеУдалены = Ложь;
	Для Каждого СтрокаДерева Из ДеревоУпаковок.Строки Цикл
		Для Каждого СтрокаДанныхОтбора Из ДанныеОтбора Цикл
			
			Если СтрокаДерева.Номенклатура = СтрокаДанныхОтбора.Номенклатура
				И СтрокаДерева.Характеристика = СтрокаДанныхОтбора.ХарактеристикаНоменклатуры
				И СтрокаДерева.Серия = СтрокаДанныхОтбора.СерияНоменклатуры Тогда
					
					СтрокиТЧ = СтрокиТЧАкцизныеМаркиПодходящиеДляШтрихкодаУпаковки(Источник.АкцизныеМарки, СтрокаДерева);
					
					Для Каждого СтрокаТЧ Из СтрокиТЧ Цикл
						
						Если СтрокаТЧ.АкцизнаяМарка.ТипУпаковки = Перечисления.ТипыУпаковок.МаркированныйТовар Тогда
							Источник.АкцизныеМарки.Удалить(СтрокаТЧ);
							ДанныеУдалены = Истина;
						Иначе
							Шаблон = НСтр("ru='Невозможно удалить строку %1 (%2), товар был добавлен в упаковке.'");
							ТекстОшибки = СтрШаблон(Шаблон, СтрокаТЧ.НомерСтроки, СтрокаДанныхОтбора.Номенклатура);
							
							ОшибкаЗафиксирована = Ложь;
							Если Ошибки <> Неопределено Тогда
								Для Каждого СтрокаОшибок Из Ошибки.СписокОшибок Цикл
									Если ТекстОшибки = СтрокаОшибок.ТекстДляОднойОшибки Тогда
										ОшибкаЗафиксирована = Истина;
									КонецЕсли;
								КонецЦикла;
							КонецЕсли;
							
							Если Не ОшибкаЗафиксирована Тогда
								ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
									Ошибки,
									"Объект.Товары[%1].Номенклатура",
									ТекстОшибки,
									"",
									СтрокаТЧ.НомерСтроки-1);
							КонецЕсли;
								
						КонецЕсли;
						
					КонецЦикла;
					
			КонецЕсли;
			
		КонецЦикла;
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
	
	Если Отказ Тогда
		ТекстОшибки = НСтр("ru='Используйте подбор и проверку продукции, доступную по кнопке ""Акцизные марки"".'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
	Если Не (ДанныеУдалены Или Отказ) Тогда
		Для Каждого СтрокаДерева Из ДеревоУпаковок.Строки Цикл
			УдалитьАкцизныеМаркиЧека(Форма, ДанныеОтбора, Отказ, СтрокаДерева);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Функция ШтрихкодыУпаковок(Источник) Экспорт
	
	ТаблицаШтрихкодов = Новый ТаблицаЗначений;
	ТаблицаШтрихкодов.Колонки.Добавить("ШтрихкодУпаковки",             Новый ОписаниеТипов("СправочникСсылка.ШтрихкодыУпаковокТоваров"));
	ТаблицаШтрихкодов.Колонки.Добавить("ШтрихкодРодительскойУпаковки", Новый ОписаниеТипов("СправочникСсылка.ШтрихкодыУпаковокТоваров"));
	ТаблицаШтрихкодов.Колонки.Добавить("ИдентификаторСтроки",          ОпределяемыеТипы().УникальныйИдентификаторИС.Тип);
	ТаблицаШтрихкодов.Колонки.Добавить("АлкогольнаяПродукция",         Новый ОписаниеТипов("СправочникСсылка.КлассификаторАлкогольнойПродукцииЕГАИС"));
	ТаблицаШтрихкодов.Колонки.Добавить("Справка2",                     Новый ОписаниеТипов("СправочникСсылка.Справки2ЕГАИС"));
	ТаблицаШтрихкодов.Колонки.Добавить("ОрганизацияЕГАИС",             Новый ОписаниеТипов("СправочникСсылка.КлассификаторОрганизацийЕГАИС"));
	ТаблицаШтрихкодов.Колонки.Добавить("ЧастичноеВыбытиеКоличество",     ОбщегоНазначения.ОписаниеТипаЧисло(18, 8));
	ТаблицаШтрихкодов.Колонки.Добавить("ЧастичноеВыбытиеВариантУчета",   Новый ОписаниеТипов("ПеречислениеСсылка.ВариантыУчетаЧастичногоВыбытияИС"));
	ТаблицаШтрихкодов.Колонки.Добавить("ЧастичноеВыбытиеНоменклатура",   Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаШтрихкодов.Колонки.Добавить("ЧастичноеВыбытиеХарактеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ТаблицаШтрихкодов.Колонки.Добавить("ВыбытиеБутылки",                 Новый ОписаниеТипов("Булево"));
	
	Для Каждого СтрокаТЧ Из Источник.АкцизныеМарки Цикл
		
		НоваяСтрока = ТаблицаШтрихкодов.Добавить();
		НоваяСтрока.ШтрихкодУпаковки             = СтрокаТЧ.АкцизнаяМарка;
		НоваяСтрока.ШтрихкодРодительскойУпаковки = СтрокаТЧ.ШтрихкодУпаковки;
		
	КонецЦикла;
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстыЗапроса.Добавить(
		"ВЫБРАТЬ
		|	ИсходныеДанные.ОрганизацияЕГАИС             КАК ОрганизацияЕГАИС,
		|	ИсходныеДанные.ИдентификаторСтроки          КАК ИдентификаторСтроки,
		|	ИсходныеДанные.АлкогольнаяПродукция         КАК АлкогольнаяПродукция,
		|	ИсходныеДанные.Справка2                     КАК Справка2,
		|	ИсходныеДанные.ШтрихкодУпаковки             КАК ШтрихкодУпаковки,
		|	ИсходныеДанные.ШтрихкодРодительскойУпаковки КАК ШтрихкодРодительскойУпаковки,
		|	ИсходныеДанные.ЧастичноеВыбытиеКоличество     КАК ЧастичноеВыбытиеКоличество,
		|	ИсходныеДанные.ЧастичноеВыбытиеВариантУчета   КАК ЧастичноеВыбытиеВариантУчета,
		|	ИсходныеДанные.ЧастичноеВыбытиеНоменклатура   КАК ЧастичноеВыбытиеНоменклатура,
		|	ИсходныеДанные.ЧастичноеВыбытиеХарактеристика КАК ЧастичноеВыбытиеХарактеристика,
		|	ИсходныеДанные.ВыбытиеБутылки                 КАК ВыбытиеБутылки
		|ПОМЕСТИТЬ ВТВложенныеШтрихкодыИсходныеДанные
		|ИЗ
		|	&ИсходныеДанные КАК ИсходныеДанные");
	
	ПараметрыФормированияТекстаЗапроса = ШтрихкодированиеЕГАИС.ПараметрыФормированияТекстаЗапросаВложенныхШтрихкодов();
	ПараметрыФормированияТекстаЗапроса.ДокументСсылка                  = Неопределено;
	ПараметрыФормированияТекстаЗапроса.ИспользоватьИдентификаторСтроки = Ложь;
	ТекстыЗапроса.Добавить(
		ШтрихкодированиеЕГАИС.ТекстЗапросаВложенныхШтрихкодовПоДокументу(ПараметрыФормированияТекстаЗапроса),
		"ВложенныеШтрихкоды");
	
	МенеджерВременнойТаблицы = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременнойТаблицы;
	Запрос.УстановитьПараметр("ИсходныеДанные", ТаблицаШтрихкодов);
	Запрос.УстановитьПараметр("ПустыеЗначенияНоменклатуры", ИнтеграцияИС.НезаполненныеЗначенияОпределяемогоТипа("Номенклатура"));
	РезультатыЗапроса = ИнтеграцияИС.ВыполнитьПакетЗапросов(Запрос, ТекстыЗапроса);
	
	ШтрихкодыУпаковок = ШтрихкодированиеЕГАИС.ВложенныеШтрихкодыУпаковокПоВыборкеИМенеджеруВТ(
		РезультатыЗапроса.ВложенныеШтрихкоды.Выбрать(), МенеджерВременнойТаблицы);
	
	Возврат ШтрихкодыУпаковок;
	
КонецФункции

// Получает GTIN для таблицы переданных товаров
//
// Параметры:
//  Товары - ТаблицаЗначений - таблица товаров с колонками
//   * Номенклатура - СправочникСсылка.Номенклатура - номенклатура (маркируемый товар).
//   * Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры - характеристика номенклатуры (маркируемого товара).
//
// Возвращаемое значение:
//   Соответствие - данные возможных GTIN:
//    * Ключ     - Строка    - GTIN всех товаров входящей таблицы
//    * Значение - Структура - пара (номенклатура,характеристика) к которой относится GTIN
//
Функция GTINМаркированныхТоваров(Товары) Экспорт
	
	Результат = Новый Соответствие;
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	Товары.Номенклатура КАК Номенклатура,
	                      |	Товары.Характеристика КАК Характеристика
	                      |ПОМЕСТИТЬ Товары
	                      |ИЗ
	                      |	&Товары КАК Товары
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	Штрихкоды.Штрихкод КАК Штрихкод,
	                      |	Товары.Номенклатура КАК Номенклатура,
	                      |	Товары.Характеристика КАК Характеристика,
	                      |	ЕСТЬNULL(Штрихкоды.ЕдиницаИзмерения.Коэффициент, 1) КАК Коэффициент,
	                      |	Штрихкоды.ЕдиницаИзмерения КАК Упаковка
	                      |ИЗ
	                      |	Товары КАК Товары
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Штрихкоды КАК Штрихкоды
	                      |		ПО Товары.Номенклатура = Штрихкоды.Владелец
	                      |			И (Товары.Характеристика = Штрихкоды.ХарактеристикаНоменклатуры
						  |			ИЛИ Товары.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))");
	
	Если Товары.Колонки.Найти("Характеристика") = Неопределено Тогда
		Товары.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	КонецЕсли;
	Запрос.УстановитьПараметр("Товары", Товары);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если МенеджерОборудованияКлиентСервер.ПроверитьКорректностьGTIN(Выборка.Штрихкод) Тогда
			Результат.Вставить(Выборка.Штрихкод, 
				Новый Структура("Номенклатура,Характеристика,Коэффициент,Упаковка", Выборка.Номенклатура, Выборка.Характеристика, Выборка.Коэффициент, Выборка.Упаковка));
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#Область GTIN

// Дополняет массив текстов запроса получения GTIN для объединения.
//
// Параметры:
//  ТекущиеДанные         - ДанныеФормыЭлементКоллекции - Текущая строка формы.
//  ФормаВладелец         - ФормаКлиентскогоПриложения  - Текущая строка формы.
//  Запрос                - Запрос                      - Запрос, в который можно установить параметры.
//  МассивТекстовЗапросов - Массив из Строка            - Тексты запроса для объединеиня.
Процедура ДополнитьПараметрыЗапросаПриНачалеВыбораGTIN(ТекущиеДанные, ФормаВладелец, ПараметрыЗапроса, МассивТекстовЗапросов) Экспорт
	
	МассивТекстовЗапросов.Добавить("ВЫБРАТЬ
	|	ШтрихкодыНоменклатуры.Штрихкод
	|ИЗ
	|	РегистрСведений.Штрихкоды КАК ШтрихкодыНоменклатуры
	|ГДЕ
	|	ШтрихкодыНоменклатуры.Владелец                     = &Номенклатура
	|	И ШтрихкодыНоменклатуры.ХарактеристикаНоменклатуры = &Характеристика");
	
КонецПроцедуры

#КонецОбласти

#Область ДляОбычныхФорм

Процедура ИнициализацияСвойствМаркируемойПродукции(СвойстваМаркируемойПродукцияГосИС) Экспорт
	
	СвойстваМаркируемойПродукцияГосИС = Новый ТаблицаЗначений;
	СвойстваМаркируемойПродукцияГосИС.Колонки.Добавить("Номенклатура",               Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	СвойстваМаркируемойПродукцияГосИС.Колонки.Добавить("ХарактеристикаНоменклатуры", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	СвойстваМаркируемойПродукцияГосИС.Колонки.Добавить("СерияНоменклатуры",          Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	СвойстваМаркируемойПродукцияГосИС.Колонки.Добавить("МаркируемаяПродукция",       Новый ОписаниеТипов("Булево"));
	СвойстваМаркируемойПродукцияГосИС.Колонки.Добавить("ВидПродукцииИС",             Новый ОписаниеТипов("ПеречислениеСсылка.ВидыПродукцииИС"));
	СвойстваМаркируемойПродукцияГосИС.Колонки.Добавить("ИндексАкцизнойМарки",        Новый ОписаниеТипов("Число"));
	
	СвойстваМаркируемойПродукцияГосИС.Индексы.Добавить("Номенклатура, ХарактеристикаНоменклатуры, СерияНоменклатуры");
	
КонецПроцедуры

Функция ДанныеМаркировкиНоменклатуры(Номенклатура) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Номенклатура
	|ПОМЕСТИТЬ ТаблицаНоменклатура
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка = &Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНоменклатура.Номенклатура,
	|	&ОпределениеВидаПродукции КАК ВидПродукции,
	|	&ОпределениеПризнакаМаркируемаяПродукция
	|ИЗ
	|	ТаблицаНоменклатура КАК ТаблицаНоменклатура";
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	ОпределитьВидПродукцииТекстаЗапроса(Запрос.Текст);
	
	ОпределениеПризнакаМаркируемаяПродукция = ОпределениеПризнакаМаркируемаяПродукцияТекстаЗапроса(
		"ТаблицаНоменклатура.Номенклатура");
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОпределениеПризнакаМаркируемаяПродукция", ОпределениеПризнакаМаркируемаяПродукция);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	ДанныеМаркировки = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(Выборка);
	Если Не ЗначениеЗаполнено(ДанныеМаркировки.МаркируемаяПродукция) Тогда
		ДанныеМаркировки.МаркируемаяПродукция = Ложь;
	КонецЕсли;
	
	Возврат ДанныеМаркировки;
	
КонецФункции

Функция СвойстваМаркируемойПродукцииПоСтрокеТовары(СвойстваМаркируемойПродукцияГосИС, СтрокаТоваров) Экспорт
	
	Отбор = Новый Структура("Номенклатура, ХарактеристикаНоменклатуры");
	Отбор.Номенклатура = СтрокаТоваров.Номенклатура;
	Отбор.ХарактеристикаНоменклатуры = СтрокаТоваров.ХарактеристикаНоменклатуры;
	
	НайденныеСтроки = СвойстваМаркируемойПродукцияГосИС.НайтиСтроки(Отбор);
	Если НайденныеСтроки.Количество() > 0 Тогда
		
		Возврат НайденныеСтроки[0];
		
	Иначе
		
		ДанныеМаркировки = ДанныеМаркировкиНоменклатуры(СтрокаТоваров.Номенклатура);
		
		НоваяСтрока = СвойстваМаркируемойПродукцияГосИС.Добавить();
		НоваяСтрока.Номенклатура               = СтрокаТоваров.Номенклатура;
		НоваяСтрока.ХарактеристикаНоменклатуры = СтрокаТоваров.ХарактеристикаНоменклатуры;
		НоваяСтрока.ВидПродукцииИС             = ДанныеМаркировки.ВидПродукции;
		НоваяСтрока.МаркируемаяПродукция       = ДанныеМаркировки.МаркируемаяПродукция;
		
		Возврат НоваяСтрока;
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ОпределитьВидПродукцииТекстаЗапроса(ТекстЗапроса,
	ПутьКПолюНоменклатура = "Номенклатура",
	ЗаменяемыйПараметр = "&ОпределениеВидаПродукции") Экспорт
	
	ОпределениеВидаПродукции =
	"	Выбор
	|		Когда Номенклатура.АлкогольнаяПродукция И Номенклатура.ВидПродукцииИС <> ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Пиво)
	|			Тогда Значение(Перечисление.ВидыПродукцииИС.Алкогольная)
	|		Иначе Номенклатура.ВидПродукцииИС
	|	Конец ";
	
	ОпределениеВидаПродукции = СтрЗаменить(ОпределениеВидаПродукции, "Номенклатура", ПутьКПолюНоменклатура);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ЗаменяемыйПараметр, ОпределениеВидаПродукции);
	
КонецПроцедуры

Функция ОпределениеПризнакаМаркируемаяПродукцияТекстаЗапроса(ПутьКПолюНоменклатура, Псевдоним = "МаркируемаяПродукция") Экспорт
	
	ОпределениеШаблон =
	"
	|	(%1.АлкогольнаяПродукция 
	|		И ЕСТЬNULL(%1.ВидАлкогольнойПродукцииЕГАИС.Маркируемый, ЛОЖЬ))
	|	ИЛИ (%1.ВидПродукцииИС <> ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПустаяСсылка)
	|		И %1.ВидПродукцииИС <> ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Зерно)
	|		И %1.ВидПродукцииИС <> ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПродуктыПереработкиЗерна)
	|		И %1.ВидПродукцииИС <> ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПодконтрольнаяПродукцияВЕТИС)
	|		И %1.ВидПродукцииИС <> ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Алкогольная)) КАК %2";
	
	Возврат СтрШаблон(ОпределениеШаблон, ПутьКПолюНоменклатура, Псевдоним);
	
КонецФункции

Процедура ОпределитьВидПродукцииТекстаЗапросаДереваУпаковок(ТекстЗапроса, УровнейВЗапросе) Экспорт
	
	Для Уровень = 0 По УровнейВЗапросе Цикл
		
		ОпределениеВидаПродукции = 
		"ВЫБОР
		|	КОГДА ДанныеУпаковок.УпаковкаУровень"+Уровень+".Номенклатура <> НЕОПРЕДЕЛЕНО
		|		ТОГДА ВЫБОР
		|			КОГДА ДанныеУпаковок.УпаковкаУровень"+Уровень+".Номенклатура.АлкогольнаяПродукция
		|				ТОГДА Значение(Перечисление.ВидыПродукцииИС.Алкогольная)
		|				ИНАЧЕ ДанныеУпаковок.УпаковкаУровень"+Уровень+".Номенклатура.ВидПродукцииИС
		|			КОНЕЦ
		|	КОНЕЦ";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОпределениеВидаПродукции" + Уровень , ОпределениеВидаПродукции);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьДанныеПоШтрихкодамEAN(ДанныеПоШтрихкодамEAN) Экспорт
	
	ШтрихкодыEAN = ДанныеПоШтрихкодамEAN.ВыгрузитьКолонку("ШтрихкодEAN");
	ТекстЗапроса = "ВЫБРАТЬ
	|	ШтрихкодыНоменклатуры.Владелец                     КАК Номенклатура,
	|	Представление(ШтрихкодыНоменклатуры.Владелец)      КАК ПредставлениеНоменклатуры,
	|	ШтрихкодыНоменклатуры.ХарактеристикаНоменклатуры   КАК Характеристика,
	|	ШтрихкодыНоменклатуры.СерияНоменклатуры            КАК Серия,
	|	ШтрихкодыНоменклатуры.ЕдиницаИзмерения             КАК Упаковка,
	|	ШтрихкодыНоменклатуры.ЕдиницаИзмерения.Коэффициент КАК Количество,
	|	ШтрихкодыНоменклатуры.Штрихкод                     КАК ШтрихкодEAN,
	|	&ОпределениеВидаПродукции                          КАК ВидПродукции,
	|	&ОпределениеПризнакаМаркируемаяПродукция
	|ИЗ
	|	РегистрСведений.Штрихкоды КАК ШтрихкодыНоменклатуры
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
	|		ПО Номенклатура.Ссылка = ШтрихкодыНоменклатуры.Владелец
	|ГДЕ
	|	ШтрихкодыНоменклатуры.Штрихкод В (&Штрихкоды)";
	
	ОпределитьВидПродукцииТекстаЗапроса(ТекстЗапроса);
	ОпределениеМаркируемаяПродукция = ОпределениеПризнакаМаркируемаяПродукцияТекстаЗапроса("ШтрихкодыНоменклатуры.Владелец");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОпределениеПризнакаМаркируемаяПродукция", ОпределениеМаркируемаяПродукция);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	Запрос.УстановитьПараметр("Штрихкоды", ШтрихкодыEAN);
	
	ДанныеПоШтрихкодамEAN.Очистить();
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ДанныеПоШтрихкодамEAN.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		НоваяСтрока.ШтрихкодEAN = СокрЛП(НоваяСтрока.ШтрихкодEAN);
	КонецЦикла;
	
КонецПроцедуры

// Выделяет из переданного массива штрихкодов упаковок элементы, в составе которых (на любом уровне вложенности, 
//   в т.ч. частично) находится продукция требуемого вида
// 
// Параметры:
//   ШтрихкодыДляПроверки - Массив - проверяемые элементы типа СправочникСсылка.ШтрихкодыУпаковокТоваров.
//   ВидыПродукции - Массив, ПеречислениеСсылка.ВидыПродукцииИС, Неопределено - Вид отбираемой продукции.
// Возвращаемое значение:
//   Массив - Массив - подходящие элементы типа СправочникСсылка.ШтрихкодыУпаковокТоваров.
Функция ШтрихкодыСодержащиеВидыПродукции(Знач ШтрихкодыДляПроверки, Знач ВидыПродукции = Неопределено) Экспорт
	
	Если НЕ ШтрихкодыДляПроверки.Количество() Тогда
		Возврат ШтрихкодыДляПроверки;
	КонецЕсли;
	
	ВидыПродукцииДляПроверки = Новый Массив;
	Если ТипЗнч(ВидыПродукции) = Тип("Массив") Тогда
		ВидыПродукцииДляПроверки = ВидыПродукции;
	ИначеЕсли ТипЗнч(ВидыПродукции) = Тип("ПеречислениеСсылка.ВидыПродукцииИС") Тогда
		ВидыПродукцииДляПроверки.Добавить(ВидыПродукции);
	Иначе 
		Для Каждого ЭлементПеречисления Из Перечисления.ВидыПродукцииИС Цикл 
			ВидыПродукцииДляПроверки.Добавить(ЭлементПеречисления);
		КонецЦикла;
	КонецЕсли;
	
	РезультатПроверки = Новый Соответствие;
	Для Каждого ЭлементМассива Из ШтрихкодыДляПроверки Цикл
		РезультатПроверки.Вставить(ЭлементМассива, Ложь);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидыПродукцииДляПроверки", ВидыПродукцииДляПроверки);
	Запрос.УстановитьПараметр("ПроверятьАлкогольную", ВидыПродукцииДляПроверки.Найти(Перечисления.ВидыПродукцииИС.Алкогольная)<>Неопределено);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ШтрихкодыУпаковок.Ссылка КАК Штрихкод,
	|	ШтрихкодыУпаковок.Номенклатура.ВидПродукцииИС В (&ВидыПродукцииДляПроверки)
	|	ИЛИ (&ПроверятьАлкогольную И ЕСТЬNULL(ШтрихкодыУпаковок.Номенклатура.АлкогольнаяПродукция,ЛОЖЬ))
	|		КАК ПодходящаяПродукция
	|ИЗ
	|	Справочник.ШтрихкодыУпаковокТоваров КАК ШтрихкодыУпаковок
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ШтрихкодыУпаковокТоваров.ВложенныеШтрихкоды КАК ЕстьВложенныеШтрихкоды
	|		ПО ЕстьВложенныеШтрихкоды.Ссылка = ШтрихкодыУпаковок.Ссылка
	|	ГДЕ ШтрихкодыУпаковок.Ссылка В (&ШтрихкодУпаковки)
	|	И ЕстьВложенныеШтрихкоды.Ссылка ЕСТЬ NULL
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ШтрихкодыУпаковок.Ссылка КАК Родитель,
	|	ШтрихкодыУпаковок.Штрихкод
	|ИЗ
	|	Справочник.ШтрихкодыУпаковокТоваров.ВложенныеШтрихкоды КАК ШтрихкодыУпаковок
	|	ГДЕ ШтрихкодыУпаковок.Ссылка В (&ШтрихкодУпаковки)";
	
	
	КешВложенности = Новый Соответствие;
	
	ОбходТаблицы = Истина;
	
	Пока ОбходТаблицы Цикл
		
		Запрос.УстановитьПараметр("ШтрихкодУпаковки", ШтрихкодыДляПроверки);
		МассивРезультатов = Запрос.ВыполнитьПакет();
		СоставУпаковки = МассивРезультатов[0].Выбрать();
		ВложенныеЗаписи = МассивРезультатов[1].Выбрать();
		НуженОбходДочерних = ВложенныеЗаписи.Количество();
	
		Пока СоставУпаковки.Следующий() Цикл
			ИсходныйШтрихкод = КешВложенности.Получить(СоставУпаковки.Штрихкод);
			Если ИсходныйШтрихкод = Неопределено Тогда
				ИсходныйШтрихкод = СоставУпаковки.Штрихкод;
			КонецЕсли;
			
			РезультатПроверки[ИсходныйШтрихкод] = РезультатПроверки[ИсходныйШтрихкод] ИЛИ СоставУпаковки.ПодходящаяПродукция;
		КонецЦикла;
		
		Если НуженОбходДочерних Тогда
			ШтрихкодыДляПроверки = Новый Массив;
			Пока ВложенныеЗаписи.Следующий() Цикл
				ИсходныйШтрихкод = КешВложенности.Получить(ВложенныеЗаписи.Родитель);
				Если ИсходныйШтрихкод = Неопределено Тогда
					ИсходныйШтрихкод = ВложенныеЗаписи.Родитель;
				КонецЕсли;
				КешВложенности.Вставить(ВложенныеЗаписи.Штрихкод, ИсходныйШтрихкод);
				ШтрихкодыДляПроверки.Добавить(ВложенныеЗаписи.Штрихкод);
			КонецЦикла;
		Иначе
			ОбходТаблицы = Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
	Результат = Новый Массив;
	Для Каждого КлючИЗначение Из РезультатПроверки Цикл
		Если КлючИЗначение.Значение Тогда
			Результат.Добавить(КлючИЗначение.Ключ);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура УдалитьСтрокиТоваровПоВидуПродукции(Товары, ВидПродукции) Экспорт
	
	ИмяПоляОтбора = XMLСтрока(ВидПродукции) + "Продукция";
	
	ОтборПоВидуПродукции = Новый Структура(ИмяПоляОтбора, 1);
	УдаляемыеСтроки = Товары.НайтиСтроки(ОтборПоВидуПродукции);
	
	Для Каждого УдаляемаяСтрока Из УдаляемыеСтроки Цикл
		Товары.Удалить(УдаляемаяСтрока);
	КонецЦикла;
	
КонецПроцедуры

Процедура УдалитьСтрокиАкцизныхМарокПоВидуПродукции(АкцизныеМарки, ВидПродукции) Экспорт
	
	СписокАкцизныхМарокНаУдаление = ШтрихкодыСодержащиеВидыПродукции(АкцизныеМарки.Выгрузить().ВыгрузитьКолонку("АкцизнаяМарка"), ВидПродукции);
	
	Для Каждого АкцизнаяМарка Из СписокАкцизныхМарокНаУдаление Цикл
		
		УдаляемыеСтроки = АкцизныеМарки.НайтиСтроки(Новый Структура("АкцизнаяМарка", АкцизнаяМарка));
		Для Каждого УдаляемаяСтрока Из УдаляемыеСтроки Цикл
			АкцизныеМарки.Удалить(УдаляемаяСтрока);
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ОбработатьДанныеШтрихкодаДляОбщейФормы(Форма, ДанныеШтрихкода, ПараметрыСканирования, ВложенныеШтрихкоды) Экспорт
	
	Если ТипЗнч(Форма) <> Тип("ФормаКлиентскогоПриложения") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если СтрНачинаетсяС(Форма.ИмяФормы, "ОбщаяФорма.ПроверкаЗаполненияДокументов") Тогда
		
		Если ДанныеШтрихкода.ТипУпаковки = Перечисления.ТипыУпаковок.МаркированныйТовар Тогда
			
			Возврат ОбработатьДанныеШтрихкодаДляОбщейФормыПроверкиЗаполненияДокументов(Форма, ДанныеШтрихкода, ПараметрыСканирования, ВложенныеШтрихкоды);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Процедура ВыполнитьПроверкуНаОшибкиДанныхПоШтрихкодам(ДанныеПоШтрихкодам, ПараметрыСканирования, ЕстьОшибки) Экспорт
	
	Если ЕстьОшибки Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ПараметрыСканирования.СсылкаНаОбъект) = Тип("ДокументСсылка.ЧекККМ") Тогда
		
		Если ДанныеПоШтрихкодам.ВложенныеШтрихкоды <> Неопределено Тогда
			
			ПроверитьУстановленныеЦеныПоНоменклатуреДляДереваДокументаЧекаККМ(ПараметрыСканирования, ДанныеПоШтрихкодам, ЕстьОшибки);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#Область АдаптацияИсходныйМодульОбщегоНазначенияУТ

// Осуществляет проверку заполненности проверяемых реквизитов.
//
// Параметры:
// Документ           - ДокументСсылка - Документ, на основании которого осуществляется ввод
// Статус             - Статус документ, на основании которого осуществляется ввод
// ЕстьОшибкиПроведен - Булево - Если Истина - документ, на основании которого осуществляется ввод, не проведен
// ЕстьОшибкиСтатус   - Булево - Если Истина - документ, на основании которого осуществляется ввод, имеет некорректный статус
//
Процедура ПроверитьВозможностьВводаНаОсновании(Документ,
	                                           Статус = Неопределено,
	                                           ЕстьОшибкиПроведен = Ложь,
	                                           ЕстьОшибкиСтатус = Ложь,
	                                           МассивДопустимыхСтатусов = Неопределено) Экспорт
	
	Если ЕстьОшибкиПроведен Тогда
		
		ТекстОшибки = НСтр("ru='Документ %Документ% не проведен. Ввод на основании непроведенного документа запрещен.'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", Документ);
	
		ВызватьИсключение ТекстОшибки;
		
	ИначеЕсли ЕстьОшибкиСтатус Тогда
		
		Если Не ЗначениеЗаполнено(МассивДопустимыхСтатусов) Тогда
			ТекстОшибки = НСтр("ru='Документ %Документ% находится в статусе ""%Статус%"". Ввод на основании запрещен.'");
		ИначеЕсли ТипЗнч(МассивДопустимыхСтатусов) = Тип("Массив") Тогда
			
			Если МассивДопустимыхСтатусов.Количество() = 1 Тогда
				ТекстОшибки = НСтр("ru='Документ %Документ% находится в статусе ""%Статус%"". Ввод на основании разрешен только в статусе ""%СтрокаДопустимыхСтатусов%"".'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%СтрокаДопустимыхСтатусов%", МассивДопустимыхСтатусов[0]);
			Иначе
				ТекстОшибки = НСтр("ru='Документ %Документ% находится в статусе ""%Статус%"". Ввод на основании разрешен только в статусах ""%СтрокаДопустимыхСтатусов%"".'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%СтрокаДопустимыхСтатусов%", СтрСоединить(МассивДопустимыхСтатусов, """, """));
			КонецЕсли;
			
		КонецЕсли;
		
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", Документ);
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Статус%",   Статус);
	
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
КонецПроцедуры // ПроверитьВозможностьВводаНаОсновании()

// Возвращает представление переданного объекта.
//
// Параметры:
//	ПараметрОбъект - Строка или ОбъектМетаданных - объект для получения представления
//
// Возвращаемое значение:
//	Строка - представление объекта
//
Функция ПредставлениеОбъекта(ПараметрОбъект) Экспорт
	
	Если ПараметрОбъект = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	МетаданныеОбъекта = ?(ТипЗнч(ПараметрОбъект) = Тип("Строка"), Метаданные.НайтиПоПолномуИмени(ПараметрОбъект), ПараметрОбъект);
	
	Представление = Новый Структура("ПредставлениеОбъекта");
	ЗаполнитьЗначенияСвойств(Представление, МетаданныеОбъекта);
	Если Не ПустаяСтрока(Представление.ПредставлениеОбъекта) Тогда
		Возврат Представление.ПредставлениеОбъекта;
	КонецЕсли;
	
	Возврат МетаданныеОбъекта.Представление();
	
КонецФункции

#КонецОбласти

Процедура СформироватьДанныеДокументаОснования(ПараметрыСканирования, ТаблицаДанных) Экспорт
	
	Если ПараметрыСканирования.Свойство("ДокументЕГАИС")
		И ТипЗнч(ПараметрыСканирования.ДокументОснование) = Тип("ДокументСсылка.ОприходованиеТоваров") Тогда
			
			СформироватьДанныеОснованияОприходованияТоваров(
				ПараметрыСканирования.ДокументОснование, 
				ПараметрыСканирования.ДокументЕГАИС,
				ТаблицаДанных);
			
	Иначе
			
		ИнтеграцияИСМПУТ.СформироватьТаблицуМаркируемойПродукцииДокумента(
			ПараметрыСканирования.ДокументОснование,
			ТаблицаДанных,
			ПараметрыСканирования.ДопустимыеВидыПродукции);
			
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьШтрихкоды(ДанныеПоШтрихкодам, ИмяКолонкиЗаполнения = "Штрихкод") Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Таблица", ДанныеПоШтрихкодам);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Таблица.Номенклатура КАК Номенклатура,
	|	Таблица.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ Входящие
	|ИЗ
	|	&Таблица КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Входящие.Номенклатура                    КАК Номенклатура,
	|	Входящие.Характеристика                  КАК Характеристика,
	|	МАКСИМУМ(ШтрихкодыНоменклатуры.Штрихкод) КАК Штрихкод
	|ИЗ
	|	Входящие КАК Входящие
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Штрихкоды КАК ШтрихкодыНоменклатуры
	|		ПО ШтрихкодыНоменклатуры.Владелец = Входящие.Номенклатура
	|		И ШтрихкодыНоменклатуры.ХарактеристикаНоменклатуры = Входящие.Характеристика
	|СГРУППИРОВАТЬ ПО
	|	Входящие.Номенклатура,
	|	Входящие.Характеристика";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ИсходныеСтроки = ДанныеПоШтрихкодам.НайтиСтроки(Новый Структура("Номенклатура, Характеристика", Выборка.Номенклатура, Выборка.Характеристика));
		Для Каждого ЗаполняемаяСтрока Из ИсходныеСтроки Цикл
			ЗаполняемаяСтрока[ИмяКолонкиЗаполнения] = Выборка.Штрихкод;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОбработатьДанныеШтрихкодаДляОбщейФормыПроверкиЗаполненияДокументов(Форма, ДанныеШтрихкода, ПараметрыСканирования, ВложенныеШтрихкоды)
	
	Результат = ШтрихкодированиеИС.ИнициализироватьРезультатОбработкиШтрихкода(ПараметрыСканирования, ДанныеШтрихкода);
	
	ПараметрыЗаполнения = АкцизныеМаркиЕГАИС.ПараметрыЗаполненияТоваровИАкцизныхМарок(Форма, Истина, ПараметрыСканирования);
	ПараметрыЗаполнения.ЕстьСправка2                   = Ложь;
	ПараметрыЗаполнения.ПараметрыУказанияСерий         = Форма.ПараметрыУказанияСерий;
	ПараметрыЗаполнения.ИмяКолонкиАлкогольнаяПродукция = "НоменклатураЕГАИС";
	ПараметрыЗаполнения.ИмяКолонкиКоличество           = "КоличествоФакт";
	ПараметрыЗаполнения.ИмяКолонкиКоличествоУпаковок   = "КоличествоУпаковокФакт";
	
	ПараметрыПоискаСтатусаУказанияСерии = Новый Структура;
	ПараметрыПоискаСтатусаУказанияСерии.Вставить("Номенклатура",   ДанныеШтрихкода.Номенклатура);
	ПараметрыПоискаСтатусаУказанияСерии.Вставить("Характеристика", ДанныеШтрихкода.Характеристика);
	
	НайденныеСтроки = Форма.Товары.НайтиСтроки(ПараметрыПоискаСтатусаУказанияСерии);
	
	АлкогольнаяПродукция = Неопределено;
	Справка2             = Неопределено;
	Если ДанныеШтрихкода.ВидыПродукции.Количество() > 0
		И ДанныеШтрихкода.ВидыПродукции[0] = Перечисления.ВидыПродукцииИС.Алкогольная Тогда
			
		АлкогольнаяПродукция = ДанныеШтрихкода.АлкогольнаяПродукция;
		Справка2             = ДанныеШтрихкода.Справка2;
	КонецЕсли;
	
	Если НайденныеСтроки.Количество() > 0 Тогда
		
		Если НоменклатураКлиентСервер.ВЭтомСтатусеСерииУказываютсяВТЧТовары(
			НайденныеСтроки[0].СтатусУказанияСерий, ПараметрыЗаполнения.ПараметрыУказанияСерий) Тогда
			
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("Номенклатура",   ДанныеШтрихкода.Номенклатура);
			ПараметрыОтбора.Вставить("Характеристика", ДанныеШтрихкода.Характеристика);
			ПараметрыОтбора.Вставить("Серия",          ДанныеШтрихкода.Серия);
			
		Иначе
			
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("Номенклатура",   ДанныеШтрихкода.Номенклатура);
			ПараметрыОтбора.Вставить("Характеристика", ДанныеШтрихкода.Характеристика);
			ПараметрыОтбора.Вставить("Серия",          Справочники.СерииНоменклатуры.ПустаяСсылка());
			
		КонецЕсли;
		
		РезультатПоиска = АкцизныеМаркиЕГАИС.НайтиСтрокиТоваров(
			Форма.Товары,
			ПараметрыОтбора,
			Неопределено, 
			АлкогольнаяПродукция,
			ПараметрыЗаполнения);
		
		СтрокаТЧ                        = РезультатПоиска.ПолноеСоответствие;
		СтрокиТЧДляУменьшенияКоличества = РезультатПоиска.КЗаполнению;
		
	Иначе
		
		СтрокаТЧ                        = Неопределено;
		СтрокиТЧДляУменьшенияКоличества = Неопределено;
		
	КонецЕсли;
	
	Если СтрокиТЧДляУменьшенияКоличества = Неопределено
		И СтрокаТЧ = Неопределено Тогда
		
		СтрокаТЧ = Форма.Товары.Добавить();
		СтрокаТЧ[ПараметрыЗаполнения.ИмяКолонкиАлкогольнаяПродукция] = АлкогольнаяПродукция;
		СтрокаТЧ.Номенклатура         = ДанныеШтрихкода.Номенклатура;
		СтрокаТЧ.Характеристика       = ДанныеШтрихкода.Характеристика;
		СтрокаТЧ.Серия                = ДанныеШтрихкода.Серия;
		СтрокаТЧ.Серия = Неопределено;
		
		АкцизныеМаркиЕГАИС.ОбработатьДобавленнуюСтроку(СтрокаТЧ, ПараметрыЗаполнения, 1);
		
	ИначеЕсли СтрокиТЧДляУменьшенияКоличества = Неопределено
		И СтрокаТЧ <> Неопределено Тогда
		
		АкцизныеМаркиЕГАИС.ОбработатьИзмененнуюСтроку(СтрокаТЧ, ПараметрыЗаполнения, 1);
		
	Иначе
		
		Для Каждого СтрокаТЧДляУменьшенияКоличества Из СтрокиТЧДляУменьшенияКоличества Цикл
			
			Если СтрокаТЧДляУменьшенияКоличества.Количество <= 1 Тогда
				
				Если СтрокаТЧ = Неопределено Тогда
					
					СтрокаТЧ = СтрокаТЧДляУменьшенияКоличества;
					
					СтрокаТЧ[ПараметрыЗаполнения.ИмяКолонкиАлкогольнаяПродукция] = АлкогольнаяПродукция;
					
				Иначе
					
					Форма.Товары.Удалить(СтрокаТЧДляУменьшенияКоличества);
					
					Индекс = ПараметрыЗаполнения.ИзмененныеСтроки.Найти(СтрокаТЧДляУменьшенияКоличества);
					Если Индекс <> Неопределено Тогда
						ПараметрыЗаполнения.ИзмененныеСтроки.Удалить(Индекс);
					КонецЕсли;
					
				КонецЕсли;
				
				АкцизныеМаркиЕГАИС.ОбработатьИзмененнуюСтроку(СтрокаТЧ, ПараметрыЗаполнения, 1);
				
			Иначе
				
				СтрокаТЧДляУменьшенияКоличества.Количество = СтрокаТЧДляУменьшенияКоличества.Количество - 1;
				ПараметрыЗаполнения.ИзмененныеСтроки.Добавить(СтрокаТЧДляУменьшенияКоличества);
				
				Если СтрокаТЧ = Неопределено Тогда
					
					СтрокаТЧ = Форма.Товары.Вставить(Форма.Товары.Индекс(СтрокаТЧДляУменьшенияКоличества) + 1);
					ЗаполнитьЗначенияСвойств(СтрокаТЧ, СтрокаТЧДляУменьшенияКоличества,,"Количество, КоличествоУпаковок, ИдентификаторСтроки");
					
					СтрокаТЧ[ПараметрыЗаполнения.ИмяКолонкиАлкогольнаяПродукция] = АлкогольнаяПродукция;
					
					АкцизныеМаркиЕГАИС.ОбработатьДобавленнуюСтроку(СтрокаТЧ, ПараметрыЗаполнения, 1);
					
				Иначе
					
					АкцизныеМаркиЕГАИС.ОбработатьИзмененнуюСтроку(СтрокаТЧ, ПараметрыЗаполнения, 1);
					
				КонецЕсли;
				
			КонецЕсли;
			
			Прервать;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СтрокаТЧ.ИдентификаторСтроки) Тогда
		СтрокаТЧ.ИдентификаторСтроки = Строка(Новый УникальныйИдентификатор);
	КонецЕсли;
	
	АкцизныеМаркиСтрокаТЧ = Форма.АкцизныеМарки.Добавить();
	АкцизныеМаркиСтрокаТЧ.ИдентификаторСтроки = СтрокаТЧ.ИдентификаторСтроки;
	АкцизныеМаркиСтрокаТЧ.АкцизнаяМарка       = ДанныеШтрихкода.ШтрихкодУпаковки;
	АкцизныеМаркиСтрокаТЧ.Справка2            = Справка2;
	
	Результат.ИзмененныеСтроки  = ПараметрыЗаполнения.ИзмененныеСтроки;
	Результат.ДобавленныеСтроки = ПараметрыЗаполнения.ДобавленныеСтроки;
	
	Возврат Результат;
	
КонецФункции

Функция СинонимПеречисления(ЗначениеПеречисления)
	
	ИмяПеречисления = ЗначениеПеречисления.Метаданные().Имя;
	ИндексЗначенияПеречисления = Перечисления[ИмяПеречисления].Индекс(ЗначениеПеречисления);
	Возврат Метаданные.Перечисления[ИмяПеречисления].ЗначенияПеречисления[ИндексЗначенияПеречисления].Синоним;
	
КонецФункции

#Область Номенклатура

Процедура ОбъемМаркируемойПродукцииВЛитрах(Таблица) Экспорт
	
	ТаблицаДляЗапроса = Новый ТаблицаЗначений;
	ТаблицаДляЗапроса.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаДляЗапроса.Колонки.Добавить("ИндексСтроки", ОбщегоНазначения.ОписаниеТипаЧисло(5));
	
	Для Каждого Строка Из Таблица Цикл
		НоваяСтрока = ТаблицаДляЗапроса.Добавить();
		НоваяСтрока.Номенклатура = Строка.Номенклатура;
		НоваяСтрока.ИндексСтроки = Таблица.Индекс(Строка);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Таблица", ТаблицаДляЗапроса);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Номенклатура,
	|	Таблица.ИндексСтроки
	|ПОМЕСТИТЬ Таблица
	|ИЗ
	|	&Таблица КАК Таблица
	|;
	|ВЫБРАТЬ
	|	Таблица.Номенклатура,
	|	Таблица.ИндексСтроки,
	|	ЕСТЬNULL(ТаблицаНоменклатура.ОбъемДАЛ * 10, 0) КАК ОбъемВЛитрах
	|ИЗ
	|	Таблица КАК Таблица
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ТаблицаНоменклатура
	|		ПО Таблица.Номенклатура = ТаблицаНоменклатура.Ссылка
	|";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Таблица[Выборка.ИндексСтроки].ОбъемВЛитрах = Выборка.ОбъемВЛитрах;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

Функция СтрокиТЧАкцизныеМаркиПодходящиеДляШтрихкодаУпаковки(ТЧАкцизныеМарки, Знач СтрокаДерева, СтрокиРезультат = Неопределено)
	
	Если СтрокиРезультат <> Неопределено Тогда
		Возврат СтрокиРезультат;
	КонецЕсли;
	
	Найденные = ТЧАкцизныеМарки.НайтиСтроки(Новый Структура("АкцизнаяМарка", СтрокаДерева.ШтрихкодУпаковки));
	
	Если Найденные.Количество() > 0 Тогда
		СтрокиРезультат = Найденные;
		Возврат СтрокиРезультат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СтрокаДерева.Родитель) Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	Возврат СтрокиТЧАкцизныеМаркиПодходящиеДляШтрихкодаУпаковки(ТЧАкцизныеМарки, СтрокаДерева.Родитель, СтрокиРезультат);
	
КонецФункции

#Область ФормированиеДанныхДокументовОснования

Процедура СформироватьДанныеОснованияОприходованияТоваров(ДокументОснование, ДокументЕГАИС, ТаблицаДанных)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "
		|ВЫБРАТЬ
		|	ТаблицаДокументы.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ОформленныеДокументы
		|ИЗ
		|	Документ.АктПостановкиНаБалансЕГАИС КАК ТаблицаДокументы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
		|		ПО (СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка)
		|ГДЕ
		|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
		|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
		|	И ТаблицаДокументы.Проведен
		|	И НЕ СтатусыДокументовЕГАИС.Статус В (&КонечныеСтатусы)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
		|	ВложенныйЗапрос.Характеристика КАК Характеристика,
		|	ВложенныйЗапрос.Серия КАК Серия,
		|	СУММА(ВложенныйЗапрос.Количество) КАК Количество
		|ИЗ
		|	(ВЫБРАТЬ
		|		ТабличнаяЧасть.Номенклатура               КАК Номенклатура,
		|		ТабличнаяЧасть.ХарактеристикаНоменклатуры КАК Характеристика,
		|		ТабличнаяЧасть.СерияНоменклатуры          КАК Серия,
		|		ТабличнаяЧасть.Количество                 КАК Количество
		|	ИЗ
		|		Документ.ОприходованиеТоваров.Товары КАК ТабличнаяЧасть
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|			ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыАлкогольнойПродукции КАК СправочникВидыАлкогольнойПродукции
		|			ПО СправочникНоменклатура.ВидАлкогольнойПродукцииЕГАИС = СправочникВидыАлкогольнойПродукции.Ссылка
		|	ГДЕ
		|		ТабличнаяЧасть.Ссылка = &ДокументОснование
		|		И НЕ СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
		|		И СправочникНоменклатура.АлкогольнаяПродукция
		|		И СправочникВидыАлкогольнойПродукции.Маркируемый
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ОформленныеТовары.Номенклатура,
		|		ОформленныеТовары.Характеристика,
		|		ОформленныеТовары.Серия,
		|		-ОформленныеТовары.Количество
		|	ИЗ
		|		Документ.АктПостановкиНаБалансЕГАИС.Товары КАК ОформленныеТовары
		|	ГДЕ
		|		ОформленныеТовары.Ссылка В
		|				(ВЫБРАТЬ
		|					Т.Ссылка
		|				ИЗ
		|					ОформленныеДокументы КАК Т)) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Номенклатура,
		|	ВложенныйЗапрос.Характеристика,
		|	ВложенныйЗапрос.Серия ";
		
	Запрос.УстановитьПараметр("КонечныеСтатусы",   Документы.АктПостановкиНаБалансЕГАИС.КонечныеСтатусы());
	Запрос.УстановитьПараметр("ЭтаСсылка",         ДокументЕГАИС);
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	
	ТаблицаДанных = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры

#КонецОбласти

Процедура ПроверитьУстановленныеЦеныПоНоменклатуреДляДереваДокументаЧекаККМ(ПараметрыСканирования, ДанныеПоШтрихкодам, ЕстьОшибки)
	
	ЦеныНоменклатуры = Новый Соответствие;
	
	ДеревоУпаковок = ДанныеПоШтрихкодам.ВложенныеШтрихкоды.ДеревоУпаковок;
	ПроверитьУстановленныеРозничныеЦеныПоСтрокамДерева(ПараметрыСканирования, ДеревоУпаковок.Строки, ЦеныНоменклатуры, ЕстьОшибки);
	
	ДанныеПоШтрихкодам.ЕстьОшибкиВДеревеУпаковок = ЕстьОшибки;
	
КонецПроцедуры

Процедура ПроверитьУстановленныеРозничныеЦеныПоСтрокамДерева(ПараметрыСканирования, СтрокиДерева, ЦеныНоменклатуры, ЕстьОшибки)
	
	Склад           = ПараметрыСканирования.ДополнительныеПараметры.ПараметрыЧекаККМ.Склад;
	Дата            = ПараметрыСканирования.ДополнительныеПараметры.ПараметрыЧекаККМ.Дата;
	УсловиеПродаж   = ПараметрыСканирования.ДополнительныеПараметры.ПараметрыЧекаККМ.УсловиеПродаж;
	ТипЦен          = ПараметрыСканирования.ДополнительныеПараметры.ПараметрыЧекаККМ.ТипЦен;
	ВалютаДокумента = ПараметрыСканирования.ДополнительныеПараметры.ПараметрыЧекаККМ.ВалютаДокумента;
	
	СтруктураВалюты    = МодульВалютногоУчета.ПолучитьКурсВалюты(ВалютаДокумента, Дата);
	КурсДокумента      = СтруктураВалюты.Курс;
	КратностьДокумента = СтруктураВалюты.Кратность;
	
	Для Каждого СтрокаДерева Из СтрокиДерева Цикл
		
		ПроверитьУстановленныеРозничныеЦеныПоСтрокамДерева(ПараметрыСканирования, СтрокаДерева.Строки, ЦеныНоменклатуры, ЕстьОшибки);
		
		Если СтрокаДерева.ТипУпаковки <> Перечисления.ТипыУпаковок.МаркированныйТовар Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СтрокаДерева.Номенклатура) Тогда
			Продолжить;
		КонецЕсли;
		
		ЦеныПоХарактеристикам = ЦеныНоменклатуры.Получить(СтрокаДерева.Номенклатура);
		
		Если ЦеныПоХарактеристикам = Неопределено Тогда
			
			Если Склад.ВидСклада = Перечисления.ВидыСкладов.Розничный Тогда
				Цена = УправлениеРозничнойТорговлей.ПолучитьПродажнуюЦену(Дата, СтрокаДерева.Номенклатура,
					СтрокаДерева.Характеристика, Склад, УсловиеПродаж);
			Иначе
				ЕдиницаИзмерения = Неопределено;
				Цена = Ценообразование.ПолучитьЦенуНоменклатуры(СтрокаДерева.Номенклатура, СтрокаДерева.Характеристика, ТипЦен,
					Дата, ЕдиницаИзмерения, ВалютаДокумента, КурсДокумента, КратностьДокумента);
			КонецЕсли;
				
			ЦеныПоХарактеристикам = Новый Соответствие;
			ЦеныПоХарактеристикам.Вставить(СтрокаДерева.Характеристика, Цена);
			
			ЦеныНоменклатуры.Вставить(СтрокаДерева.Номенклатура, ЦеныПоХарактеристикам);
			
		КонецЕсли;
		
		Цена = ЦеныПоХарактеристикам.Получить(СтрокаДерева.Характеристика);
		
		Если Цена > 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаДерева.ТекстОшибки = "Не назначена цена";
		ЕстьОшибки               = Истина;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

Процедура ПроверитьВозможностьФормированияЭлектронныхДокументов(ДокументыМассив, ВыдаватьСообщенияОбОшибке = Истина, СтандартнаяОбработка = Истина) Экспорт
	
	Для Каждого ПроверяемыйДокумент Из ДокументыМассив Цикл
		
		ТипДок = ТипЗнч(ПроверяемыйДокумент);
		Если ТипДок <> Тип("ДокументСсылка.РеализацияТоваровУслуг")
			И ТипДок <> Тип("ДокументСсылка.ВозвратТоваровПоставщику")
			И ТипДок <> Тип("ДокументСсылка.КорректировкаРеализации")Тогда
			Продолжить;
		КонецЕсли;
		
		Отказ = Ложь;
		Источники = Новый Массив();
		Источники.Добавить(ПроверяемыйДокумент);
		ПроверитьЗаполнениеАкцизныхМарокПоСсылкам(Источники, Отказ,, ВыдаватьСообщенияОбОшибке);
		Если Отказ Тогда
			ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ДокументыМассив, ПроверяемыйДокумент);
			СтандартнаяОбработка = Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

//Проверяет соответствие количества маркируемой продукции из ТЧ Товары к количеству акцизных марок ТЧ АкцизныеМарки
//
//Параметры:
//   Источники - ДокументСсылка, Массив Из ДокументСсылка - объекты для проверки (ссылка или ссылки одного типа).
//   Отказ  - Булево - Отказ из вызывающего метода.
//   НаДату - Дата - дата для определения обязательности маркировки.
//          - Неопределено - на текущую дату сеанса.
//  ВыдаватьСообщенияОбОшибке - Булево - Выводить сообщения об ошибках
Процедура ПроверитьЗаполнениеАкцизныхМарокПоСсылкам(Источники, Отказ, НаДату = Неопределено, ВыдаватьСообщенияОбОшибке = Истина) Экспорт
	
	Если ТипЗнч(Источники) <> Тип("Массив") Тогда
		ИсточникиМассив = Новый Массив;
		ИсточникиМассив.Добавить(Источники);
	Иначе
		ИсточникиМассив = ОбщегоНазначения.СкопироватьРекурсивно(Источники);
	КонецЕсли;
	
	Если ИсточникиМассив.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МетаданныеИсточников = ИсточникиМассив[0].Метаданные();
	Менеджер = Документы[МетаданныеИсточников.Имя];
	
	ИмяТаблицыАкцизныеМарки = "ШтрихкодыУпаковок";
	ПолеТаблицыАкцизныеМарки = "ШтрихкодУпаковки";
	
	Реквизиты = Новый Массив;
	Реквизиты.Добавить("ссылка");
	Реквизиты.Добавить("Товары");
	Реквизиты.Добавить(ИмяТаблицыАкцизныеМарки);
	
	ИсточникСоответствие = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(ИсточникиМассив, Реквизиты);
	Для Каждого СтрокаИсточкик Из ИсточникСоответствие Цикл
		СтрокаИсточкик.Значение.Товары = СтрокаИсточкик.Значение.Товары.Выгрузить();
		СтрокаИсточкик.Значение[ИмяТаблицыАкцизныеМарки] = СтрокаИсточкик.Значение[ИмяТаблицыАкцизныеМарки].Выгрузить();
		ПроверитьЗаполнениеАкцизныхМарок(СтрокаИсточкик.Значение, Отказ, НаДату,, Истина);
	КонецЦикла;
	
КонецПроцедуры

//Проверяет соответствие количества маркируемой продукции из ТЧ Товары к количеству акцизных марок ТЧ АкцизныеМарки
//
//Параметры:
//   Объект - ДокументОбъект - форма документа для проверки.
//   Отказ  - Булево - Отказ из вызывающего метода.
//   НаДату - Дата - дата для определения обязательности маркировки.
//          - Неопределено - на текущую дату сеанса.
//   РеквизитыОбъекта  - Неопределено - в этом случае используются значения по-умолчанию
//					   - Структура:
//     *ИмяТаблицыТовары - Строка - (необязательный) имя табличной части объекта с товарным составом
//     *ИмяПоляТовары - Строка - (необязательный) имя поля номенклатуры в табличной части
//	   *ИмяПараметраУказанияСерий - Строка - необязательный ключ
//   ВыдаватьСообщенияОбОшибке - Булево - Выводить сообщения об ошибках
Процедура ПроверитьЗаполнениеАкцизныхМарок(Объект, Отказ, НаДату = Неопределено, РеквизитыОбъекта = Неопределено, ВыдаватьСообщенияОбОшибке = Истина) Экспорт
	
	ИмяТаблицыАкцизныеМарки = "АкцизныеМарки";
	ПолеТаблицыАкцизныеМарки = "АкцизнаяМарка";
	
	ИмяТаблицыТовары = "Товары";
	ИмяПоляТовары = "Номенклатура";
	ИмяПараметраУказанияСерий = "";
	
	Если ТипЗнч(РеквизитыОбъекта) = ТИп("Структура") Тогда
		Если РеквизитыОбъекта.Свойство("ИмяТаблицыТовары") Тогда
			ИмяТаблицыТовары = РеквизитыОбъекта.ИмяТаблицыТовары;
		КонецЕсли; 
		Если РеквизитыОбъекта.Свойство("ИмяПоляТовары") Тогда
			ИмяПоляТовары = РеквизитыОбъекта.ИмяПоляТовары;
		КонецЕсли; 
		Если РеквизитыОбъекта.Свойство("ИмяПараметраУказанияСерий") Тогда
			ИмяПараметраУказанияСерий = РеквизитыОбъекта.ИмяПараметраУказанияСерий;
		КонецЕсли; 
	КонецЕсли; 
	
	Если Не ЕстьРеквизитТабЧастиОбъекта(ПолеТаблицыАкцизныеМарки, Объект.Ссылка.Метаданные(), ИмяТаблицыАкцизныеМарки) Тогда
		ИмяТаблицыАкцизныеМарки = "ШтрихкодыУпаковок";
		ПолеТаблицыАкцизныеМарки = "ШтрихкодУпаковки";
	КонецЕсли;
	
	ОсобенностьУчета = Новый Массив;
	Если НаДату = Неопределено Тогда
		Для Каждого ВидПродукции Из ИнтеграцияИСМП.ВидыПродукцииОбязательнойМаркировки() Цикл
			ОсобенностьУчета.Добавить(ВидПродукции);
		КонецЦикла;
	Иначе
		Для Каждого ВидПродукции Из ИнтеграцияИСМПВызовСервера.УчитываемыеВидыМаркируемойПродукции(НаДату, Ложь) Цикл
			ОсобенностьУчета.Добавить(ВидПродукции);
		КонецЦикла;
	КонецЕсли;
	
	Если Не ОсобенностьУчета.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросМаркируемыхТоваров = Новый Запрос;
	Если ТипЗнч(Объект[ИмяТаблицыТовары]) = Тип("ТаблицаЗначений") Тогда
		ЗапросМаркируемыхТоваров.УстановитьПараметр("Ссылка", Объект[ИмяТаблицыТовары].ВыгрузитьКолонку(ИмяПоляТовары));
	Иначе 
		ЗапросМаркируемыхТоваров.УстановитьПараметр("Ссылка", Объект[ИмяТаблицыТовары].Выгрузить().ВыгрузитьКолонку(ИмяПоляТовары));
	КонецЕсли;
	ЗапросМаркируемыхТоваров.УстановитьПараметр("ОсобенностьУчета", ОсобенностьУчета);
	
	ЗапросМаркируемыхТоваров.Текст = 
	"ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Ссылка,
	|	Номенклатура.ЕдиницаХраненияОстатков КАК ЕдиницаИзмерения
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка В(&Ссылка)
	|	И Номенклатура.ВидПродукцииИС В (&ОсобенностьУчета)";
	ПроверяемыеТовары = ЗапросМаркируемыхТоваров.Выполнить().Выгрузить();
	ПроверяемыеТовары.Индексы.Добавить("Ссылка");
	
	ДанныеЧастичногоВыбытияПоШтрихкодам = Новый Соответствие;
	
	Если ЕстьРеквизитТабЧастиОбъекта("ЧастичноеВыбытиеКоличество", Объект.Ссылка.Метаданные(), ИмяТаблицыАкцизныеМарки) Тогда
		
		Для Каждого СтрокаТаблицы Из Объект[ИмяТаблицыАкцизныеМарки] Цикл 
			
			Если Не ЗначениеЗаполнено(СтрокаТаблицы.ЧастичноеВыбытиеКоличество)
				Или Не ЗначениеЗаполнено(СтрокаТаблицы.ЧастичноеВыбытиеВариантУчета) Тогда
				Продолжить;
			КонецЕсли;
			
			НовыйЭлемент = ШтрихкодированиеИСМП.НовыйЭлементКоллекцииУпаковокДляРаспределенияПоТоварам();
			ЗаполнитьЗначенияСвойств(НовыйЭлемент, СтрокаТаблицы);
			НовыйЭлемент.ШтрихкодУпаковки = СтрокаТаблицы[ПолеТаблицыАкцизныеМарки];
			
			ДанныеЧастичногоВыбытияПоШтрихкодам.Вставить(НовыйЭлемент.ШтрихкодУпаковки, НовыйЭлемент);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ТипЗнч(Объект[ИмяТаблицыАкцизныеМарки]) = Тип("ТаблицаЗначений") Тогда
		ТаблицаУпаковок = Объект[ИмяТаблицыАкцизныеМарки].Скопировать(); // ТаблицаЗначений
	Иначе
		ТаблицаУпаковок = Объект[ИмяТаблицыАкцизныеМарки].Выгрузить(); // Табличная часть
	КонецЕсли;
	ТаблицаУпаковок.Колонки[ПолеТаблицыАкцизныеМарки].Имя = "Штрихкод";
		
	Если ТипЗнч(Объект[ИмяТаблицыТовары]) = Тип("ТаблицаЗначений") Тогда
		ТаблицаТовары = Объект[ИмяТаблицыТовары].Скопировать();
	Иначе
		ТаблицаТовары = Объект[ИмяТаблицыТовары].Выгрузить();
		МассивТипов = Новый Массив;
		МассивТипов.Добавить(ТипЗнч(Объект.Ссылка));
		ТаблицаТовары.Колонки.Добавить("Ссылка", Новый ОписаниеТипов(МассивТипов));
		ТаблицаТовары.ЗаполнитьЗначения(Объект.Ссылка, "Ссылка");
	КонецЕсли;
	ТаблицаТовары.Колонки.ХарактеристикаНоменклатуры.Имя = "Характеристика";
	ПараметрыСканирования = ШтрихкодированиеИС.ПараметрыСканирования(Объект.Ссылка);
	МаркированныеТовары = ЭлектронноеВзаимодействиеИСМП.СодержимоеИКодыОСУ(
		ТаблицаУпаковок, ТаблицаТовары, ПараметрыСканирования);
	МаркированныеТовары.Индексы.Добавить("Номенклатура, Характеристика");
	МаркированныеТовары.Индексы.Добавить("Номенклатура, Характеристика, Серия");
	
	Если ДанныеЧастичногоВыбытияПоШтрихкодам.Количество() Тогда
		
		Для Каждого СтрокаТаблицы Из МаркированныеТовары Цикл
			
			ДанныеЧастичногоВыбытия = ДанныеЧастичногоВыбытияПоШтрихкодам[СтрокаТаблицы.Штрихкод];
			Если ДанныеЧастичногоВыбытия = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаТаблицы.Количество = ДанныеЧастичногоВыбытия.ЧастичноеВыбытиеКоличество;
			
			Если ДанныеЧастичногоВыбытия.ЧастичноеВыбытиеВариантУчета = Перечисления.ВариантыУчетаЧастичногоВыбытияИС.НастроеннаяНоменклатура Тогда
				СтрокаТаблицы.Номенклатура   = ДанныеЧастичногоВыбытия.ЧастичноеВыбытиеНоменклатура;
				СтрокаТаблицы.Характеристика = ДанныеЧастичногоВыбытия.ЧастичноеВыбытиеХарактеристика;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Для Каждого СтрокаТовары Из Объект[ИмяТаблицыТовары] Цикл
		
		ПроверяемСтроку = ПроверяемыеТовары.Найти(СтрокаТовары[ИмяПоляТовары],"Ссылка");
		Если ПроверяемСтроку = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		РеквизитыНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаТовары.Номенклатура, "ВестиУчетПоСериям, ВидПродукцииИС");
		
		ОтборПоПолям = ИнтеграцияИС.ПоляПоискаМаркируемойПродукции(Ложь);
		ЗаполнитьЗначенияСвойств(ОтборПоПолям, СтрокаТовары);
		ОтборПоПолямДокумента = Новый Структура;
		ОтборПоПолямДокумента.Вставить("Номенклатура");
		ОтборПоПолямДокумента.Вставить("ХарактеристикаНоменклатуры");
		ЗаполнитьЗначенияСвойств(ОтборПоПолямДокумента, СтрокаТовары);
		ОтборПоПолям.Характеристика = ОтборПоПолямДокумента.ХарактеристикаНоменклатуры;
		КоличествоМарок = 0;
		СтрокиАкцизныхМарок = МаркированныеТовары.НайтиСтроки(ОтборПоПолям);
		Для Каждого СтрокаМарка Из СтрокиАкцизныхМарок Цикл
			КоличествоМарок = КоличествоМарок + СтрокаМарка.Количество;
			Если ЗначениеЗаполнено(СтрокаМарка.ТекстОшибкиИС) Тогда
				Если ВыдаватьСообщенияОбОшибке Тогда
					Сообщить(СтрокаМарка.ТекстОшибкиИС);
					Отказ = Истина;
				Иначе
					Отказ = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		КоличествоЕдиницТоваров = 0;
		СтрокиТоваров = Объект[ИмяТаблицыТовары].НайтиСтроки(ОтборПоПолямДокумента);
		Для Каждого СтрокаТовар Из СтрокиТоваров Цикл 
			КоличествоЕдиницТоваров = КоличествоЕдиницТоваров + СтрокаТовар.Количество;
		КонецЦикла;
		
		Если КоличествоМарок <> КоличествоЕдиницТоваров Тогда
			
			Если ВыдаватьСообщенияОбОшибке Тогда
				
				ПредставлениеНоменклатуры = ИнтеграцияИС.ПредставлениеНоменклатуры(СтрокаТовары.Номенклатура, СтрокаТовары.ХарактеристикаНоменклатуры);
				
				Шаблон = НСтр("ru = 'Товара ""%1"" по документу - %2 %4, по кодам маркировки - %3 %4.'");
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					Шаблон,
					ПредставлениеНоменклатуры,
					КоличествоЕдиницТоваров,
					КоличествоМарок,
					ПроверяемСтроку.ЕдиницаИзмерения);
					
				Сообщить(ТекстОшибки);
				Отказ = Истина;
			Иначе 
				Отказ = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
		ПроверяемыеТовары.Удалить(ПроверяемСтроку);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ЕстьРеквизитТабЧастиОбъекта(ИмяРеквизита, МетаданныеОбъекта, ИмяТабЧасти) Экспорт

	ТабЧасть = МетаданныеОбъекта.ТабличныеЧасти.Найти(ИмяТабЧасти);

	Если ТабЧасть = Неопределено Тогда // нет такой таб. части
		Возврат Ложь;
	КонецЕсли;
	
	Возврат ТабЧасть.Реквизиты.Найти(ИмяРеквизита) <> Неопределено;
	
КонецФункции
