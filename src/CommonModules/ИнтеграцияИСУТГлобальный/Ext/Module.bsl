/////////////////////////////////////////////////////
//
//  Эмуляция несинхронных вызовов синхронными
//
//  Расхождение для:
//  - ПоказатьЗначение/ПоказатьЗначениеИС - игнорирует обработчик закрытия
//
//  - ОткрытьФормуИС открывает форму модально (если режим открытия "блокировать" что-либо),
//     либо возвращает саму форму (при неблокирующем открытии)
/////////////////////////////////////////////////////

Функция ОписаниеОповещенияИС(ИмяПроцедуры,
	Контекст,
	ДополнительныеПараметры = Неопределено,
	ИмяПроцедурыОбработкиОшибки = Неопределено,
	МодульОбработкиОшибки = Неопределено) Экспорт
	
	Возврат Новый ОписаниеОповещения(ИмяПроцедуры, Контекст, ДополнительныеПараметры);
	
КонецФункции

Функция ОткрытьФормуИС(ИмяФормы,
	Параметры = Неопределено,
	Владелец = Неопределено,
	Уникальность = Неопределено,
	Окно = Неопределено,
	НавигационнаяСсылка = Неопределено,
	ОписаниеОповещенияОЗакрытии = Неопределено,
	РежимОткрытияОкна = Неопределено) Экспорт

	Если ОписаниеОповещенияОЗакрытии <> Неопределено
		ИЛИ РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца Тогда
		
		Если Найти(ИмяФормы, "Обработка.")>0 Тогда 
			Форма = Владелец.ПолучитьФорму(ИмяФормы);
			
			Если Форма <> Неопределено Тогда
			
				Для Каждого Параметр Из Параметры Цикл
					Форма[Параметр.Ключ] = Параметр.Значение;
				КонецЦикла;
				
				Форма.ВладелецФормы = Владелец;
				Если Найти(ИмяФормы, "ПредпросмотрЧека") > 0 Тогда
					Форма.Открыть();
				Иначе
					Форма.ОткрытьМодально();
				КонецЕсли;
			КонецЕсли;
			
		Иначе Результат = ОткрытьФормуМодально(ИмяФормы, Параметры, Владелец);
		КонецЕсли;
	
		Если ОписаниеОповещенияОЗакрытии <> Неопределено Тогда
			ВыполнитьОбработкуОповещенияИС(ОписаниеОповещенияОЗакрытии, Результат);
		КонецЕсли;
		
	Иначе
		
		Результат = ОткрытьФорму(ИмяФормы, Параметры, Владелец);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ПоказатьВопросИС(ОписаниеОповещенияОЗавершении = Неопределено,
	ТекстВопроса,
	Кнопки,
	Таймаут = 0,
	КнопкаПоУмолчанию = Неопределено,
	Заголовок = "",
	КнопкаТаймаута = Неопределено) Экспорт
	
	Результат = Вопрос(ТекстВопроса, Кнопки, Таймаут, КнопкаПоУмолчанию, Заголовок, КнопкаТаймаута);
	
	Если ОписаниеОповещенияОЗавершении <> Неопределено Тогда
		ВыполнитьОбработкуОповещенияИС(ОписаниеОповещенияОЗавершении, Результат);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПоказатьПредупреждениеИС(ОписаниеОповещенияОЗавершении = Неопределено,
	ТекстПредупреждения,
	Таймаут = 0,
	Заголовок = "") Экспорт
	
	Предупреждение(ТекстПредупреждения, Таймаут, Заголовок);
	
	Если ОписаниеОповещенияОЗавершении <> Неопределено Тогда
		ВыполнитьОбработкуОповещенияИС(ОписаниеОповещенияОЗавершении);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПоказатьЗначениеИС(ОписаниеОповещенияОЗавершении = Неопределено, Значение) Экспорт
	
	ОткрытьЗначение(Значение);
	
КонецПроцедуры

Процедура ПоказатьВыборИзСпискаИС(ОписаниеОповещенияОЗакрытии = Неопределено, СписокВыбора, ЭлементФормыВладельца = Неопределено, НачальноеЗначение = Неопределено) Экспорт
	
	Результат=СписокВыбора.ВыбратьЭлемент(, НачальноеЗначение);
	
	Если ОписаниеОповещенияОЗакрытии <> Неопределено Тогда
		ВыполнитьОбработкуОповещенияИС(ОписаниеОповещенияОЗакрытии, Результат);
	КонецЕсли;
		
КонецПроцедуры

Процедура ВыполнитьОбработкуОповещенияИС(ОписаниеОповещения, Результат = Неопределено) Экспорт
	
	Если ТипЗнч(ОписаниеОповещения) = Тип("ОписаниеОповещения") Тогда
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, Результат);	
	ИначеЕсли ОписаниеОповещения <> Неопределено Тогда
		ДополнительныеПараметры = ОписаниеОповещения.ДополнительныеПараметры;
		Если ТипЗнч(ОписаниеОповещения.Контекст) = Тип("Строка") Тогда
			Выполнить ОписаниеОповещения.Контекст+"." + ОписаниеОповещения.ИмяПроцедуры + "(Результат, ДополнительныеПараметры)";
		Иначе
			Выполнить "ОписаниеОповещения.Контекст." + ОписаниеОповещения.ИмяПроцедуры + "(Результат, ДополнительныеПараметры)";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьПолучениеКаталогаВременныхФайловИС(ОписаниеОповещения = Неопределено) Экспорт

	Результат = КаталогВременныхФайлов();
	
	Если ОписаниеОповещения <> Неопределено Тогда
		ДополнительныеПараметры = ОписаниеОповещения.ДополнительныеПараметры;
		Выполнить ОписаниеОповещения.Контекст+"." + ОписаниеОповещения.ИмяПроцедуры + "(Результат, ДополнительныеПараметры)";
	КонецЕсли;

КонецПроцедуры

Процедура НачатьУдалениеФайловИС(ОписаниеОповещения = Неопределено, Путь, Маска = "") Экспорт
	
	УдалитьФайлы(Путь, Маска);
	
	Если ОписаниеОповещения <> Неопределено Тогда
		ДополнительныеПараметры = ОписаниеОповещения.ДополнительныеПараметры;
		Выполнить ОписаниеОповещения.Контекст+"." + ОписаниеОповещения.ИмяПроцедуры + "(ДополнительныеПараметры)";
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьПомещениеФайловИС(ОписаниеОповещения = Неопределено,
	ПомещаемыеФайлы = Неопределено,
	НаборПомещаемыхФайлов = Неопределено,
	Интерактивно = Ложь,
	УникальныйИдентификаторФормы = Неопределено) Экспорт
	
	Результат = ПоместитьФайлы(ПомещаемыеФайлы, НаборПомещаемыхФайлов, Интерактивно, УникальныйИдентификаторФормы);
	
	Если Результат И ОписаниеОповещения <> Неопределено Тогда
		ДополнительныеПараметры = ОписаниеОповещения.ДополнительныеПараметры;
		Выполнить ОписаниеОповещения.Контекст+"." + ОписаниеОповещения.ИмяПроцедуры + "(ПомещаемыеФайлы, ДополнительныеПараметры)";
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьУстановкуВнешнейКомпонентыИС(ОписаниеОповещенияОЗавершении = Неопределено, Местоположение) Экспорт
	
	УстановитьВнешнююКомпоненту(Местоположение);
	
	Если ОписаниеОповещенияОЗавершении <> Неопределено Тогда
		ДополнительныеПараметры = ОписаниеОповещенияОЗавершении.ДополнительныеПараметры;
		Выполнить ОписаниеОповещенияОЗавершении.Контекст+"." + ОписаниеОповещенияОЗавершении.ИмяПроцедуры + "(ДополнительныеПараметры)";
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьПодключениеВнешнейКомпонентыИС(ОписаниеОповещения = Неопределено,
	Местоположение,
	Имя = "",
	Тип = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(Имя) Тогда
		// Вариант с местоположением
		Подключено = ПодключитьВнешнююКомпоненту(Местоположение, Имя, Тип);
	Иначе
		// Вариант с идентификатором объекта
		Подключено = ПодключитьВнешнююКомпоненту(Местоположение);
	КонецЕсли;
	
	Если ОписаниеОповещения <> Неопределено Тогда
		ДополнительныеПараметры = ОписаниеОповещения.ДополнительныеПараметры;
		Выполнить ОписаниеОповещения.Контекст+"." + ОписаниеОповещения.ИмяПроцедуры + "(Подключено, ДополнительныеПараметры)";
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьЗапускПриложенияИС(ОписаниеОповещения = Неопределено,
	СтрокаКоманды,
	ТекущийКаталог = "",
	ДождатьсяЗавершения = Истина) Экспорт

	ДождатьсяЗавершения = Истина;
	КодВозвратаДиалога  = Неопределено;
	
	ЗапуститьПриложение(СтрокаКоманды, ТекущийКаталог, ДождатьсяЗавершения, КодВозвратаДиалога);
	
	Если ОписаниеОповещения <> Неопределено Тогда
		ДополнительныеПараметры = ОписаниеОповещения.ДополнительныеПараметры;
		Выполнить ОписаниеОповещения.Контекст+"." + ОписаниеОповещения.ИмяПроцедуры + "(КодВозвратаДиалога, ДождатьсяЗавершения, ДополнительныеПараметры)";
	КонецЕсли;

КонецПроцедуры

Процедура УстановитьОтключениеБезопасногоРежимаИС(Отключить) Экспорт
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		УстановитьБезопасныйРежим(Не Отключить);
    #КонецЕсли
	
КонецПроцедуры

