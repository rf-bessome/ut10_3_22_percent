// Универсальные механизмы интеграции ИС (ЕГАИС, ГИСМ, ВЕТИС, ...)

#Область СлужебныеПроцедурыИФункции

Функция ЭтоМаркируемаяПродукция(Номенклатура) Экспорт
	
	ДанныеМаркировки = ИнтеграцияИСУТ.ДанныеМаркировкиНоменклатуры(Номенклатура);
	
	Возврат ДанныеМаркировки.МаркируемаяПродукция;
	
КонецФункции

Процедура ОткрытьФормуСканированияМаркиПриНеобходимости(СтрокаТабличнойЧасти, ЭтаФорма, СвойстваМаркируемойПродукции = Неопределено) Экспорт
	
	СтрокаТабличнойЧасти.НеобходимостьВводаАкцизнойМарки = ЭтоМаркируемаяПродукция(СтрокаТабличнойЧасти.Номенклатура);
	
	Если СвойстваМаркируемойПродукции = Неопределено Тогда
		СвойстваМаркируемойПродукции = ИнтеграцияИС.СвойстваМаркируемойПродукции(
			СтрокаТабличнойЧасти.Номенклатура,
			СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры);
	КонецЕсли;
	
	Если СвойстваМаркируемойПродукции.МаркируемаяПродукция
			И ЗначениеЗаполнено(СтрокаТабличнойЧасти.Штрихкод) Тогда
		ДанныеКодаМаркировки = Новый Структура("Штрихкод,Количество", СтрокаТабличнойЧасти.Штрихкод, 1);
		ЭтаФорма.ОбработатьКодМаркировки(ДанныеКодаМаркировки);
	КонецЕсли;
		
КонецПроцедуры

Процедура ОткрытьМодальноФормуСканированияМарки(СтрокаТабличнойЧасти, ЭтаФорма, ЭтотОбъект) Экспорт
	
	Если Не СтрокаТабличнойЧасти.НеобходимостьВводаАкцизнойМарки Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеМаркировки = ИнтеграцияИСУТ.ДанныеМаркировкиНоменклатуры(СтрокаТабличнойЧасти.Номенклатура);
	ОткрытьМодальноФормуСканированияМаркиПоВидуПродукции(ДанныеМаркировки.ВидПродукции, СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры, ЭтаФорма, ЭтотОбъект);
	
КонецПроцедуры

Процедура ОткрытьМодальноФормуСканированияМаркиПоВидуПродукции(ВидПродукции, Номенклатура, Характеристика, ФормаВладелец, ЭтотОбъект)
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("Номенклатура"  , Номенклатура);
	ПараметрыОткрытияФормы.Вставить("Характеристика", Характеристика);
	
	ФормаСканированияМарки = Неопределено;
	
	Если ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Алкогольная") Тогда
		
		ФормаСканированияМарки = ПолучитьФорму("Обработка.РаботаСАкцизнымиМаркамиЕГАИС.Форма.ФормаВводаАкцизнойМарки", ПараметрыОткрытияФормы, ФормаВладелец);
		
	ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Табак") Тогда
		
		ФормаСканированияМарки = ПолучитьФорму("Обработка.ПроверкаИПодборТабачнойПродукцииМОТП.Форма.ФормаВводаКодаМаркировки", ПараметрыОткрытияФормы, ФормаВладелец);
		
	Иначе
		
		ФормаСканированияМарки = ПолучитьФорму("Обработка.ПроверкаИПодборПродукцииИСМП.Форма.ФормаВводаКодаМаркировки", ПараметрыОткрытияФормы, ФормаВладелец);
		
	КонецЕсли;
	
	Если ФормаСканированияМарки <> Неопределено Тогда
		
		СтрокаТабличнойЧасти = ФормаВладелец.ЭлементыФормы.Товары.ТекущиеДанные;
		Если СтрокаТабличнойЧасти = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Результат = ФормаСканированияМарки.ОткрытьМодально();
		Оповещение = Новый ОписаниеОповещения("ПоискПоШтрихкодуЗавершение", ФормаВладелец);
		ВыполнитьОбработкуОповещения(Оповещение, Результат);
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура показывает ввод штрихкода и оповещает в случае успешного ввода
//
// Параметры:
//  ОповещениеУспешногоВвода - ОписаниеОповещения - описание оповещения успешного ввода штрихкода
//  Заголовок                - Строка             - переопределяемый заголовок.
Процедура ПоказатьВводШтрихкода(ОповещениеУспешногоВвода, Количество = Неопределено, Заголовок = "") Экспорт 
	
	Если НЕ ЗначениеЗаполнено(Заголовок) Тогда
		Заголовок = НСтр("ru = 'Введите штрихкод'");
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура(
		"ОповещениеУспешногоВвода, Количество",
		ОповещениеУспешногоВвода,
		Количество);
	Оповещение = Новый ОписаниеОповещения(
		"ПоказатьВводШтрихкодаЗавершение",
		ИнтеграцияИСУТКлиент,
		ДополнительныеПараметры);
	ПоказатьВводЗначения(Оповещение, "", Заголовок);
	
КонецПроцедуры
Процедура ПоказатьВводШтрихкодаЗавершение(Штрихкод, ДополнительныеПараметры) Экспорт
	
	ОповещениеУспешногоВвода = ДополнительныеПараметры.ОповещениеУспешногоВвода;
	Количество = ДополнительныеПараметры.Количество;
	
	Если (Штрихкод <> Неопределено) Тогда
		Если Не ПустаяСтрока(Штрихкод) Тогда
			Если Количество = Неопределено Тогда
				Количество = 1;
			КонецЕсли;
			
			Если СтрДлина(Штрихкод) > 200 Тогда
				ТекстСообщения = НСтр("ru='Длина штрихкода не должна быть больше 200 символов.'");
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
				
				Возврат;
			КонецЕсли;
			
			ВыполнитьОбработкуОповещения(
				ОповещениеУспешногоВвода,
				СтруктураДанныхШтрихкода(Штрихкод, Количество));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция СтруктураДанныхШтрихкода(Штрихкод, Количество)
	
	Возврат Новый Структура("Штрихкод, Количество", Штрихкод, Количество);
	
КонецФункции

#КонецОбласти