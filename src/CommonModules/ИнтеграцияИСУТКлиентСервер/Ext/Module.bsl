// Универсальные механизмы интеграции ИС (ЕГАИС, ГИСМ, ВЕТИС, ...)

#Область ПрограммныйИнтерфейс

Функция ПоляДляПоискаМаркированнойПродукции() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Номенклатура",               ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка"));
	Результат.Вставить("ХарактеристикаНоменклатуры", ПредопределенноеЗначение("Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка"));
	Результат.Вставить("СерияНоменклатуры",          ПредопределенноеЗначение("Справочник.СерииНоменклатуры.ПустаяСсылка"));
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВключитьПоддержкуВидовПродукцииИС(Контекст, ПараметрыСканирования, ВидПродукции) Экспорт
	
	Если ЭтоКонтекстОбъекта(Контекст, "Документ.ЧекККМ") Тогда
		
		ЗаполнитьПараметрыСканированияЧекККМ(Контекст, ПараметрыСканирования, ВидПродукции, Ложь);
		
	ИначеЕсли ЭтоКонтекстОбъекта(Контекст, "Документ.РеализацияТоваровУслуг") Тогда
		
		ЗаполнитьПараметрыСканированияРеализацияТоваровУслуг(Контекст, ПараметрыСканирования, ВидПродукции);
		
	ИначеЕсли ЭтоКонтекстОбъекта(Контекст, "Документ.ВозвратТоваровОтПокупателя") Тогда
		
		ЗаполнитьПараметрыСканированияВозвратТоваровОтКлиента(Контекст, ПараметрыСканирования, ВидПродукции);
		
	ИначеЕсли ЭтоКонтекстОбъекта(Контекст, "Документ.ПоступлениеТоваровУслуг") Тогда
		
		ЗаполнитьПараметрыСканированияПоступлениеТоваровУслуг(Контекст, ПараметрыСканирования, ВидПродукции);
		
	ИначеЕсли ЭтоКонтекстОбъекта(Контекст, "Документ.ВозвратТоваровПоставщику") Тогда
		
		ЗаполнитьПараметрыСканированияВозвратТоваровПоставщику(Контекст, ПараметрыСканирования, ВидПродукции);
		
	ИначеЕсли ЭтоКонтекстОбъекта(Контекст, "Документ.ПеремещениеТоваров") Тогда
		
		ЗаполнитьПараметрыСканированияПеремещениеТоваров(Контекст, ПараметрыСканирования, ВидПродукции);
		
	ИначеЕсли ЭтоКонтекстОбъекта(Контекст, "Документ.КорректировкаРеализации") Тогда
		
		ЗаполнитьПараметрыСканированияКорректировкаРеализации(Контекст, ПараметрыСканирования, ВидПродукции);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ЭтоРеализацияТоваровРозничномуКлиенту(Контекст) Экспорт
	
	Контрагент = ИнтеграцияИСМПСлужебныйКлиентСервер.ЗначениеСвойстваКонтекста(Контекст, "Контрагент");
	
	Возврат РозничныеПродажиВызовСервера.ПартнерЭтоРозничныйКлиент(Контрагент);
	
КонецФункции

#Область АдаптацияИсходныйМодульОбщегоНазначенияУТКлиентСервер

// Устанавливает значение свойства элемента формы, если находит элемент на форме
//
// Параметры
//  ЭлементыФормы - ВсеЭлементыФормы - элементы формы, среди которых содержится искомый элемент.
//  ИмяЭлемента   - Строка - имя искомого элемента.
//  ИмяСвойства   - Строка - имя свойства, для которого будет устанавливаться значение.
//  Значение      - Произвольный - значение, которое будет установлено
//  УстанавливатьДляПодчиненных - Булево - установить аналогичное свойство для подчиненных элементов искомого элемента.
//
Процедура УстановитьСвойствоЭлементаФормы(ЭлементыФормы, ИмяЭлемента, ИмяСвойства, Значение, УстанавливатьДляПодчиненных = Ложь) Экспорт
	
	Элемент = ЭлементыФормы.Найти(ИмяЭлемента);
	Если Элемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НРег(ИмяСвойства) = НРег("ТолькоПросмотр")
	И ТипЗнч(Элемент) = Тип("КнопкаФормы") Тогда
		ИмяСвойстваЭлемента = "Доступность";
		ЗначениеСвойства    = НЕ Значение;
	Иначе
		ИмяСвойстваЭлемента = ИмяСвойства;
		ЗначениеСвойства    = Значение;
	КонецЕсли;
	
	Если НРег(ИмяСвойства) = НРег("РасширеннаяПодсказка") Тогда
		Элемент.РасширеннаяПодсказка.Заголовок = Значение;
	Иначе
		Если Элемент[ИмяСвойстваЭлемента] <> ЗначениеСвойства Тогда
			Элемент[ИмяСвойстваЭлемента] = ЗначениеСвойства;
		КонецЕсли;
	КонецЕсли;
	
	ТипыЭлементовФормыСПодчиненнымиЭлементами = Новый ОписаниеТипов("ФормаКлиентскогоПриложения, ГруппаФормы, ТаблицаФормы");
	
	Если УстанавливатьДляПодчиненных И ТипыЭлементовФормыСПодчиненнымиЭлементами.СодержитТип(ТипЗнч(Элемент)) Тогда
		Для Каждого ПодчиненныйЭлемент Из Элемент.ПодчиненныеЭлементы Цикл
			УстановитьСвойствоЭлементаФормы(ЭлементыФормы, ПодчиненныйЭлемент.Имя, ИмяСвойства, Значение, Истина);
		КонецЦикла;
 	КонецЕсли;
 	
КонецПроцедуры // УстановитьСвойствоЭлементаФорма()

// Устанавливает значение свойства элементов формы, если находит элемент на форме
//
// Параметры
//  ЭлементыФормы  - ВсеЭлементыФормы - элементы формы, среди которых содержится искомый элемент.
//  ИменаЭлементов - Массив, Строка, Структура - имена искомых элементов.
//  ИмяСвойства    - Строка - имя свойства, для которого будет устанавливаться значение.
//  Значение       - Произвольный - значение, которое будет установлено
//  УстанавливатьДляПодчиненных - Булево - установить аналогичное свойство для подчиненных элементов искомых элементов.
//
Процедура УстановитьСвойствоЭлементовФормы(ЭлементыФормы, ИменаЭлементов, ИмяСвойства, Значение, УстанавливатьДляПодчиненных = Ложь) Экспорт
	
	Если ТипЗнч(ИменаЭлементов) = Тип("Массив") Тогда
		
		Для Каждого ИмяЭлемента Из ИменаЭлементов Цикл
			УстановитьСвойствоЭлементаФормы(ЭлементыФормы, ИмяЭлемента, ИмяСвойства, Значение, УстанавливатьДляПодчиненных);
		КонецЦикла
		
	Иначе
		
		Если ТипЗнч(ИменаЭлементов) = Тип("Строка") Тогда
			СтруктураИменЭлементов = Новый Структура(ИменаЭлементов);
		Иначе
			СтруктураИменЭлементов = ИменаЭлементов;
		КонецЕсли;
		
		Для Каждого ИмяЭлемента Из СтруктураИменЭлементов Цикл
			УстановитьСвойствоЭлементаФормы(ЭлементыФормы, ИмяЭлемента.Ключ, ИмяСвойства, Значение, УстанавливатьДляПодчиненных);
		КонецЦикла
		
	КонецЕсли;
	
КонецПроцедуры // УстановитьСвойствоЭлементовФормы()

#КонецОбласти

#КонецОбласти

Процедура ЗаполнитьПараметрыСканированияЧекККМ(Контекст, ПараметрыСканирования, ВидПродукции, ПроверкаКоличества = Неопределено)
	
		
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "ДокументОбъект") Тогда
		ИсточникДанных = Контекст.ДокументОбъект;
	Иначе
		ИсточникДанных = Контекст;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст,"ДокументОбъект") Тогда
		ПараметрыСканирования.КэшМаркируемойПродукции = Контекст.КэшМаркируемойПродукции;
		
		ПараметрыЧекаККМ = Новый Структура;
		
		УсловиеПродаж = ОбщегоНазначения.ПолучитьРеквизитШапки("УсловиеПродаж", Контекст.ДокументОбъект, Контекст.ДокументОбъект.Метаданные());
		ПараметрыЧекаККМ.Вставить("Дата",            Контекст.Дата);
		ПараметрыЧекаККМ.Вставить("Склад",           Контекст.Склад);
		ПараметрыЧекаККМ.Вставить("УсловиеПродаж",   УсловиеПродаж);
		ПараметрыЧекаККМ.Вставить("ТипЦен",          Контекст.ДокументОбъект.ТипЦен);
		ПараметрыЧекаККМ.Вставить("ВалютаДокумента", Контекст.ДокументОбъект.мВалютаРегламентированногоУчета);
		
		ПараметрыСканирования.ДополнительныеПараметры.Вставить("ПараметрыЧекаККМ", ПараметрыЧекаККМ);
		
	КонецЕсли;
	
	#Область ПоддержкаАлкогольнойПродукции
	
	Если ШтрихкодированиеЕГАИСКлиентСервер.ВключитьПоддержкуАлкогольнойПродукции(ПараметрыСканирования, ВидПродукции) Тогда
		
		ПараметрыСканирования.ДоступныеСтатусы.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыАкцизныхМарок.ВНаличии"));
		ПараметрыСканирования.ДоступныеСтатусы.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыАкцизныхМарок.ПустаяСсылка"));
		
		ПараметрыСканирования.КонтрольАкцизныхМарок         = Истина;
		ПараметрыСканирования.Операция                      = ПредопределенноеЗначение("Перечисление.ВидыДокументовЕГАИС.ЧекККМ");
		ПараметрыСканирования.ОперацияКонтроляАкцизныхМарок = "Продажа";
		ПараметрыСканирования.СоздаватьШтрихкодУпаковки     = Истина;
		
		Если ПроверкаКоличества = Истина Тогда
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("Штрихкод");
		Иначе
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("Цена");
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("Штрихкод");
		КонецЕсли;
		
		ПараметрыСканирования.ОрганизацияЕГАИС = ИсточникДанных.ОрганизацияЕГАИС;
		
	КонецЕсли;
	
	#КонецОбласти
	
	#Область ИСМП
	
	ЗаполнитьПараметрыСканирования = Ложь;
	Если Контекст.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Продажа") Тогда 
		ЗаполнитьПараметрыСканирования = ШтрихкодированиеИСМПКлиентСервер.ЗаполнитьПараметрыСканированияДокументаРозничнойПродажи(ПараметрыСканирования, ВидПродукции);
	Иначе
		ЗаполнитьПараметрыСканирования = ШтрихкодированиеИСМПКлиентСервер.ЗаполнитьПараметрыСканированияДокументаРозничногоВозврата(ПараметрыСканирования, ВидПродукции);
	КонецЕсли;
	Если ЗаполнитьПараметрыСканирования Тогда
		
		Реквизиты = Новый Структура("Склад, ДатаДокумента", ИсточникДанных.Склад, Контекст.Дата);	
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ИсточникДанных, "Организация") Тогда
			Реквизиты.Вставить("Организация", ИсточникДанных.Организация);
		КонецЕсли;

		Если ТипЗнч(Контекст) = Тип("ФормаКлиентскогоПриложения") Или ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст,"ЭтотОбъект") Тогда
			Реквизиты.Вставить("Организация", Контекст.Организация);
			Реквизиты.Вставить("Склад",       Контекст.Склад);
		КонецЕсли;
		
		Если Контекст.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Продажа") Тогда 
			ПараметрыСканирования.ОперацияКонтроляАкцизныхМарок = "Продажа";
		Иначе
			ПараметрыСканирования.ОперацияКонтроляАкцизныхМарок = "Возврат";
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ПараметрыСканирования, Реквизиты);
		ЗаполнитьПараметрыРеквизитовЧастичногоВыбытия(ПараметрыСканирования, Контекст);
		ЗаполнитьПараметрыРеквизитовРазрешительнойСистемыГИСМТ(ПараметрыСканирования, Контекст);
		ЗаполнитьККТФФД12ЧекККМ(ПараметрыСканирования, Контекст);
			
	КонецЕсли;
	
	#КонецОбласти
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыСканированияРеализацияТоваровУслуг(Контекст, ПараметрыСканирования, ВидПродукции)
	
	Если ШтрихкодированиеИСМПКлиентСервер.ЗаполнитьПараметрыСканированияДокументаОптовойОтгрузки(ПараметрыСканирования, ВидПродукции) Тогда
		
		ПараметрыСканирования.ИмяКолонкиШтрихкодУпаковки           = "ШтрихкодУпаковки";
		ПараметрыСканирования.ИмяТабличнойЧастиШтрихкодыУпаковок   = "ШтрихкодыУпаковок";
		
		Реквизиты = Новый Структура;
		Реквизиты.Вставить("ДатаДокумента", Контекст.Дата);
		ЗаполнитьЗначенияСвойств(ПараметрыСканирования, Реквизиты);
		
		Если ЭтоРеализацияТоваровРозничномуКлиенту(Контекст) Тогда
			ЗаполнитьПараметрыРеквизитовЧастичногоВыбытия(ПараметрыСканирования, Контекст);
			ЗаполнитьПараметрыРеквизитовРазрешительнойСистемыГИСМТ(ПараметрыСканирования, Контекст);
			ЗаполнитьККТФФД12(ПараметрыСканирования, Контекст);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыСканированияКорректировкаРеализации(Контекст, ПараметрыСканирования, ВидПродукции)
	
	Если ШтрихкодированиеИСМПКлиентСервер.ЗаполнитьПараметрыСканированияДокументаОптовойОтгрузки(ПараметрыСканирования, ВидПродукции) Тогда
	
		ПараметрыСканирования.ИмяКолонкиШтрихкодУпаковки           = "ШтрихкодУпаковки";
		ПараметрыСканирования.ИмяТабличнойЧастиШтрихкодыУпаковок   = "ШтрихкодыУпаковок";
		
		Реквизиты = Новый Структура;
		Реквизиты.Вставить("КонтролироватьОкончаниеСрокаГодности", Ложь);
		Реквизиты.Вставить("ЗапрашиватьДанныеСервисаИСМП",         Ложь);
		Реквизиты.Вставить("ДопустимыПроверкиСеройЗоныМОТП",       Ложь);
		ЗаполнитьЗначенияСвойств(ПараметрыСканирования, Реквизиты);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыСканированияВозвратТоваровОтКлиента(Контекст, ПараметрыСканирования, ВидПродукции)
	
	Если ШтрихкодированиеИСМПКлиентСервер.ЗаполнитьПараметрыСканированияДокументаОптовогоВозврата(ПараметрыСканирования, ВидПродукции) Тогда
	
		ПараметрыСканирования.ИмяКолонкиШтрихкодУпаковки           = "ШтрихкодУпаковки";
		ПараметрыСканирования.ИмяТабличнойЧастиШтрихкодыУпаковок   = "ШтрихкодыУпаковок";
		
		Если ЭтоРеализацияТоваровРозничномуКлиенту(Контекст) Тогда
			ЗаполнитьПараметрыРеквизитовЧастичногоВыбытия(ПараметрыСканирования, Контекст);
			ЗаполнитьККТФФД12(ПараметрыСканирования, Контекст);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыСканированияПоступлениеТоваровУслуг(Контекст, ПараметрыСканирования, ВидПродукции)
	
	Если ШтрихкодированиеИСМПКлиентСервер.ЗаполнитьПараметрыСканированияДокументаЗакупки(ПараметрыСканирования, ВидПродукции) Тогда
		
		ПараметрыСканирования.ИмяКолонкиШтрихкодУпаковки           = "ШтрихкодУпаковки";
		ПараметрыСканирования.ИмяТабличнойЧастиШтрихкодыУпаковок   = "ШтрихкодыУпаковок";
		
		ПараметрыСканирования.СопоставлятьНоменклатуру  = Ложь;
		ПараметрыСканирования.СоздаватьШтрихкодУпаковки = Ложь;
	
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыСканированияВозвратТоваровПоставщику(Контекст, ПараметрыСканирования, ВидПродукции)
	
	ШтрихкодированиеИСМПКлиентСервер.ЗаполнитьПараметрыСканированияДокументаОптовойОтгрузки(ПараметрыСканирования, ВидПродукции);
	
	ПараметрыСканирования.ИмяКолонкиШтрихкодУпаковки           = "ШтрихкодУпаковки";
	ПараметрыСканирования.ИмяТабличнойЧастиШтрихкодыУпаковок   = "ШтрихкодыУпаковок";
		
КонецПроцедуры

Процедура ЗаполнитьПараметрыСканированияПеремещениеТоваров(Контекст, ПараметрыСканирования, ВидПродукции)
	
	ШтрихкодированиеИСМПКлиентСервер.ЗаполнитьПараметрыСканированияДокументаОптовойОтгрузки(ПараметрыСканирования, ВидПродукции);
	
	ПараметрыСканирования.ИмяКолонкиШтрихкодУпаковки           = "ШтрихкодУпаковки";
	ПараметрыСканирования.ИмяТабличнойЧастиШтрихкодыУпаковок   = "ШтрихкодыУпаковок";
		
КонецПроцедуры

Функция ЭтоКонтекстОбъекта(Контекст, Знач ИмяОбъекта) Экспорт
	
	ЭтоСсылка   = Ложь;
	ЭтоФорма    = Ложь;
	Разделитель = ".";
	
	ЭтоСсылка = Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(Контекст));
	
	Если Не ЭтоСсылка Тогда
		
		Если ТипЗнч(Контекст) = Тип("ФормаКлиентскогоПриложения") Тогда
			
			ЭтоФорма       = Истина;
			МассивПодстрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Контекст.ИмяФормы, Разделитель);
			ИмяФормы       = МассивПодстрок[0] + Разделитель + МассивПодстрок[1];
			
		Иначе
			
			ЭтоФорма       = Истина;
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст,"ЭтотОбъект") Тогда 
				ИмяФормы = Контекст.ЭтотОбъект.Метаданные().ПолноеИмя();
			ИначеЕсли ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст,"Ссылка") Тогда
				ИмяФормы = Контекст.Ссылка.Метаданные().ПолноеИмя();
			Иначе 
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		
		МассивПодстрок    = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИмяОбъекта, Разделитель);
		
		Если МассивПодстрок[0] <> "Документ" Тогда
			Возврат Ложь;
		КонецЕсли;
		
		МассивПодстрок[0] = МассивПодстрок[0] + "Ссылка";
		ИмяОбъекта = СтрСоединить(МассивПодстрок, Разделитель);
		
	КонецЕсли;
	
	Возврат ЭтоФорма И ИмяФормы = ИмяОбъекта Или ЭтоСсылка И ТипЗнч(Контекст) = Тип(ИмяОбъекта);
	
КонецФункции

Процедура ЗаполнитьККТФФД12ЧекККМ(ПараметрыСканирования, Контекст)
	
	ПараметрыКассыККМ = Неопределено;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "ПараметрыКассыККМ") Тогда
		ПараметрыКассыККМ = Контекст.ПараметрыКассыККМ;
	КонецЕсли;
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "КассаККМ") И ПараметрыКассыККМ = Неопределено Тогда
		ПараметрыКассыККМ = РозничныеПродажиВызовСервера.ПараметрыКассыККМ(Контекст.КассаККМ);
	КонецЕсли;
	
	Если ПараметрыКассыККМ <> Неопределено Тогда
		
		ИдентификаторУстройства = ПараметрыКассыККМ.ИдентификаторУстройства;
		
		ПараметрыККТ = Неопределено;
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "ПараметрыККТ") Тогда
			ПараметрыККТ = Контекст.ПараметрыККТ;
		КонецЕсли;
		
		Если МенеджерОборудованияВызовСервера.ФискальноеУстройствоПоддерживаетПроверкуКодовМаркировки(ИдентификаторУстройства, ПараметрыККТ) Тогда
			ПараметрыСканирования.ТребуетсяПроверкаСредствамиККТ   = Истина;
			ПараметрыСканирования.ККТФФД12ИСМП                     = ИдентификаторУстройства;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьККТФФД12(ПараметрыСканирования, Контекст)
	
	ПараметрыКассыККМ = Неопределено;

	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "ПараметрыКассыККМ") Тогда
		ПараметрыКассыККМ = Контекст.ПараметрыКассыККМ;
	ИначеЕсли ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "ККМ") Тогда
		ПараметрыКассыККМ = РозничныеПродажиВызовСервера.ПараметрыКассыККМ(Контекст.ККМ);
	КонецЕсли;
	
	Если ПараметрыКассыККМ <> Неопределено Тогда
		ИдентификаторУстройства = ПараметрыКассыККМ.ИдентификаторУстройства;
		
		ПараметрыККТ = Неопределено;
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "ПараметрыККТ") Тогда
			ПараметрыККТ = Контекст.ПараметрыККТ;
		КонецЕсли;
		
		Если МенеджерОборудованияВызовСервера.ФискальноеУстройствоПоддерживаетПроверкуКодовМаркировки(ИдентификаторУстройства) Тогда
			ПараметрыСканирования.ТребуетсяПроверкаСредствамиККТ   = Истина;
			ПараметрыСканирования.ККТФФД12ИСМП                     = ИдентификаторУстройства;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыРеквизитовЧастичногоВыбытия(ПараметрыСканирования, Контекст)
	
	ПараметрыСканирования.ИмяКолонкиЧастичноеВыбытиеКоличество     = "ЧастичноеВыбытиеКоличество";
	ПараметрыСканирования.ИмяКолонкиЧастичноеВыбытиеВариантУчета   = "ЧастичноеВыбытиеВариантУчета";
	ПараметрыСканирования.ИмяКолонкиЧастичноеВыбытиеНоменклатура   = "ЧастичноеВыбытиеНоменклатура";
	ПараметрыСканирования.ИмяКолонкиЧастичноеВыбытиеХарактеристика = "ЧастичноеВыбытиеХарактеристика";
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыРеквизитовРазрешительнойСистемыГИСМТ(ПараметрыСканирования, Контекст)
	
	ПараметрыСканирования.ИмяКолонкиРазрешительныйРежимИдентификаторЗапросаГИСМТ = "РазрешительныйРежимИдентификаторЗапросаГИСМТ";
	ПараметрыСканирования.ИмяКолонкиРазрешительныйРежимДатаЗапросаГИСМТ          = "РазрешительныйРежимДатаЗапросаГИСМТ";
	
КонецПроцедуры

