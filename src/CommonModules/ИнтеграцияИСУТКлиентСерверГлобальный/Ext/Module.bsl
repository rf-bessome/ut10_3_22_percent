/////////////////////////////////////////////////////////////////////////////////////////
//
//  Функции обратной совместимости для эмуляции новых возможностей платформы
//  Удалить лишние при необходимости
//
/////////////////////////////////////////////////////////////////////////////////////////

Функция СтрНайти(Знач Строка,
	Знач ПодстрокаПоиска,
	Знач НаправлениеПоиска = Неопределено,
	Знач НачальнаяПозиция = 0,
	Знач НомерВхождения = 1) Экспорт
	
	Если НаправлениеПоиска = "СКонца" Тогда
		ПеревернутаяСтрокаПоиска = "";
		ПеревернутаяПодстрокаПоиска = "";
		Для й=1 По СтрДлина(Строка)Цикл 
			ПеревернутаяСтрокаПоиска = Лев(Строка,1) + ПеревернутаяСтрокаПоиска;
			Строка = Сред(Строка,2);
		КонецЦикла;
		Для й=1 По СтрДлина(ПодстрокаПоиска)Цикл 
			ПеревернутаяПодстрокаПоиска = Лев(ПодстрокаПоиска,1) + ПеревернутаяПодстрокаПоиска;
			ПодстрокаПоиска = Сред(ПодстрокаПоиска,2);
		КонецЦикла;
		
		Если НачальнаяПозиция > 0 Тогда 
			НачальнаяПозиция = СтрДлина(ПеревернутаяСтрокаПоиска) + 1 - НачальнаяПозиция;
		КонецЕсли;
		
		РезультатПоискаВПеревернутойСтроке = СтрНайти(ПеревернутаяСтрокаПоиска, ПеревернутаяПодстрокаПоиска,,НачальнаяПозиция, НомерВхождения);
		
		Если РезультатПоискаВПеревернутойСтроке = 0 Тогда 
			Возврат 0;
		Иначе 
			Возврат СтрДлина(ПеревернутаяСтрокаПоиска) - СтрДлина(ПеревернутаяПодстрокаПоиска) - РезультатПоискаВПеревернутойСтроке + 2;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПодстрокаПоиска = "" Тогда
		Возврат 1;
	КонецЕсли;
	
	Если НачальнаяПозиция = 0 Тогда 
		НачальнаяПозиция = 1;
	КонецЕсли;
	
	Если НачальнаяПозиция > 1 Тогда
		РезультатПоиска = СтрНайти(Сред(Строка, НачальнаяПозиция), ПодстрокаПоиска, НаправлениеПоиска,,НомерВхождения);
		Если РезультатПоиска = 0 Тогда
			Возврат 0;
		Иначе
			Возврат НачальнаяПозиция - 1 + РезультатПоиска;
		КонецЕсли;
	КонецЕсли;
	
	Если НомерВхождения > 1 Тогда
		Вхождение1 = Найти(Строка, ПодстрокаПоиска);
		Если Вхождение1 > 0 Тогда
			Возврат Вхождение1 + СтрНайти(Сред(Строка,Вхождение1 +1),ПодстрокаПоиска,НаправлениеПоиска,,НомерВхождения - 1);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Найти(Строка, ПодстрокаПоиска);
	
КонецФункции

Функция СтрНачинаетсяС(Строка, ПодстрокаПоиска) Экспорт
	
	Возврат (Найти(Строка, ПодстрокаПоиска)=1);
	
КонецФункции

Функция СтрЗаканчиваетсяНа(Строка, ПодстрокаПоиска) Экспорт
	
	Возврат Прав(Строка, СтрДлина(ПодстрокаПоиска)) = ПодстрокаПоиска;
	
КонецФункции

Функция СтрСоединить(МассивСтрок, Разделитель = "") Экспорт
	
	Возврат СтроковыеФункцииКлиентСервер.ПолучитьСтрокуИзМассиваПодстрок(МассивСтрок, Разделитель);
	
КонецФункции

Функция СтрРазделить(Строка, Разделитель, ВключатьПустые = Ложь) Экспорт

	Возврат СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Строка, Разделитель, Не ВключатьПустые);

КонецФункции // СтрРазделить()

Функция СтрШаблон(Знач СтрокаПодстановки,
	Параметр1 = Неопределено, Параметр2 = Неопределено, Параметр3 = Неопределено,
	Параметр4 = Неопределено, Параметр5 = Неопределено, Параметр6 = Неопределено,
	Параметр7 = Неопределено, Параметр8 = Неопределено, Параметр9 = Неопределено) Экспорт
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СтрокаПодстановки,
		Параметр1, Параметр2, Параметр3, Параметр4, Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
	
	КонецФункции

Функция СтрСравнить(Строка1, Строка2) Экспорт
		
	#Если ТолстыйКлиентОбычноеПриложение ИЛИ Сервер ИЛИ ВнешнееСоединение Тогда
	СравнениеСтрок = Новый СравнениеЗначений;
	
	Возврат СравнениеСтрок.Сравнить(Строка1, Строка2);
	
	#Иначе
		Возврат Неопределено;
	#КонецЕсли
	
КонецФункции

Процедура УстановитьОтключениеБезопасногоРежима(Отключить) Экспорт
	// Не используется
КонецПроцедуры

Функция ОпределяемыеТипы() Экспорт
	#Если НЕ ВебКлиент И НЕ ТонкийКлиент Тогда
		Возврат  ИнтеграцияИСУТПовтИсп.ОпределяемыеТипыУПП();
	#Иначе
		Возврат Неопределено;
	#КонецЕсли
КонецФункции
