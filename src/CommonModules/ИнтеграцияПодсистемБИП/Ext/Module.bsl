///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "Интернет-поддержка пользователей".
// ОбщийМодуль.ИнтеграцияПодсистемБИП.
//
// Серверные процедуры и функции интеграции с БСП, БТС и БИП:
//  - Подписка на события БСП;
//  - Подписка на события БТС;
//  - Обработка событий БСП и БТС в подсистемах БИП;
//  - Определение списка возможных подписок в БИП;
//  - Вызов методов БСП, на которые выполнена подписка.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.БазоваяФункциональность

// Обработка программных событий, возникающих в подсистемах БСП.
// Только для вызовов из библиотеки БСП в БИП.

// Определяет события, на которые подписана эта библиотека.
//
// Параметры:
//  Подписки - Структура - См. ИнтеграцияПодсистемБСП.СобытияБСП.
//
Процедура ПриОпределенииПодписокНаСобытияБСП(Подписки) Экспорт
	
	// БазоваяФункциональность
	Подписки.ПриДобавленииПодсистем = Истина;
	Подписки.ПриДобавленииПараметровРаботыКлиентаПриЗапуске = Истина;
	Подписки.ПриДобавленииПараметровРаботыКлиента = Истина;
	Подписки.ПриДобавленииПереименованийОбъектовМетаданных = Истина;
	Подписки.ПриДобавленииОбработчиковУстановкиПараметровСеанса = Истина;
	
	// ПодключаемыеКоманды
	Подписки.ПриОпределенииВидовПодключаемыхКоманд = Истина;
	Подписки.ПриОпределенииСоставаНастроекПодключаемыхОбъектов = Истина;
	Подписки.ПриОпределенииКомандПодключенныхКОбъекту = Истина;
	
	// Профили безопасности
	Подписки.ПриЗаполненииРазрешенийНаДоступКВнешнимРесурсам = Истина;
	
	// Пользователи
	Подписки.ПриОпределенииНазначенияРолей = Истина;
	
	// РегламентныеЗадания
	Подписки.ПриОпределенииНастроекРегламентныхЗаданий = Истина;
	
	// Текущие дела
	Подписки.ПриОпределенииОбработчиковТекущихДел = Истина;
	Подписки.ПриОпределенииПорядкаРазделовКомандногоИнтерфейса = Истина;
	
	// Управление доступом
	Подписки.ПриЗаполненииВидовОграниченийПравОбъектовМетаданных = Истина;
	Подписки.ПриЗаполненииСписковСОграничениемДоступа = Истина;
	
	// Варианты отчетов
	Подписки.ПриНастройкеВариантовОтчетов = Истина;
	
	// Центр мониторинга
	Подписки.ПриСбореПоказателейСтатистикиКонфигурации = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область БазоваяФункциональность

// См. ПодсистемыКонфигурацииПереопределяемый.ПриДобавленииПодсистем.
//
Процедура ПриДобавленииПодсистем(МодулиПодсистем) Экспорт
	
	МодулиПодсистем.Добавить("ОбновлениеИнформационнойБазыБИП");
	
КонецПроцедуры

// См. ОбщегоНазначенияПереопределяемый.ПриДобавленииПараметровРаботыКлиента.
//
Процедура ПриДобавленииПараметровРаботыКлиента(Параметры) Экспорт
	
	Если Параметры.Свойство("ИнтернетПоддержкаПользователей") Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыИПП = Новый Структура;

	ПараметрыИПП.Вставить("ИмяКонфигурации"          , Метаданные.Имя);
	ПараметрыИПП.Вставить("ИмяПрограммы"             , "Trade");
	ПараметрыИПП.Вставить("ВерсияКонфигурации"       , Метаданные.Версия);
	ПараметрыИПП.Вставить("КодЛокализации"           , ТекущийКодЛокализации());
	ПараметрыИПП.Вставить("ВерсияОбработкиОбновления", СтандартныеПодсистемыСервер.ВерсияБиблиотеки());

	НастройкиСоединения = ИнтернетПоддержкаПользователейСлужебныйПовтИсп.НастройкиСоединенияССерверамиИПП();
	ПараметрыИПП.Вставить("ДоменРасположенияСерверовИПП", НастройкиСоединения.ДоменРасположенияСерверовИПП);

	ПараметрыИПП.Вставить(
		"ДоступноПодключениеИнтернетПоддержки",
		ИнтернетПоддержкаПользователей.ДоступноПодключениеИнтернетПоддержки());
	
	// Добавление параметров подсистем.
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.РаботаСКонтрагентами") Тогда
		МодульРаботаСКонтрагентами = ОбщегоНазначения.ОбщийМодуль("РаботаСКонтрагентами");
		МодульРаботаСКонтрагентами.ПриДобавленииПараметровРаботыКлиента(ПараметрыИПП);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.Новости") Тогда
		МодульОбработкаНовостей = ОбщегоНазначения.ОбщийМодуль("ОбработкаНовостей");
		МодульОбработкаНовостей.ПриДобавленииПараметровРаботыКлиента(ПараметрыИПП);
	КонецЕсли;
	
	Параметры.Вставить("ИнтернетПоддержкаПользователей", ПараметрыИПП);
	
КонецПроцедуры

// См. ОбщегоНазначенияПереопределяемый.ПриДобавленииОбработчиковУстановкиПараметровСеанса.
//
Процедура ПриДобавленииОбработчиковУстановкиПараметровСеанса(Обработчики) Экспорт
	
	Обработчики.Вставить("ПараметрыКлиентаНаСервереБИП", "ИнтернетПоддержкаПользователей.УстановкаПараметровСеанса");
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.Новости") Тогда
		МодульОбработкаНовостей = ОбщегоНазначения.ОбщийМодуль("ОбработкаНовостей");
		МодульОбработкаНовостей.ПриДобавленииОбработчиковУстановкиПараметровСеанса(Обработчики);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область БазоваяФункциональность

// См. ИнтернетПоддержкаПользователейПереопределяемый.ПриОпределенииКодаЯзыкаИнтерфейсаКонфигурации.
//
Процедура ПриОпределенииКодаЯзыкаИнтерфейсаКонфигурации(КодЯзыка, КодЯзыкаВФорматеISO639_1) Экспорт
	
	Если ИнтеграцияПодсистемБИППовтИсп.ПодпискиБСП().ПриОпределенииКодаЯзыкаИнтерфейсаКонфигурации Тогда
		МодульИнтеграцияПодсистемБСП = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияПодсистемБСП");
		МодульИнтеграцияПодсистемБСП.ПриОпределенииКодаЯзыкаИнтерфейсаКонфигурации(КодЯзыка, КодЯзыкаВФорматеISO639_1);
	КонецЕсли;
	
КонецПроцедуры

// См. ИнтернетПоддержкаПользователейПереопределяемый.ПриИзмененииДанныхАутентификацииИнтернетПоддержки.
//
Процедура ПриИзмененииДанныхАутентификацииИнтернетПоддержки(ДанныеПользователя) Экспорт
	
	Если ИнтеграцияПодсистемБИППовтИсп.ПодпискиБСП().ПриИзмененииДанныхАутентификацииИнтернетПоддержки Тогда
		МодульИнтеграцияПодсистемБСП = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияПодсистемБСП");
		МодульИнтеграцияПодсистемБСП.ПриИзмененииДанныхАутентификацииИнтернетПоддержки(ДанныеПользователя);
	КонецЕсли;
	
КонецПроцедуры

// См. ИнтернетПоддержкаПользователейПереопределяемый.ПриОпределенииНомераВерсииПрограммы.
//
Процедура ПриОпределенииНомераВерсииПрограммы(ВерсияПрограммы) Экспорт
	
	Если ИнтеграцияПодсистемБИППовтИсп.ПодпискиБСП().ПриОпределенииНомераВерсииПрограммы Тогда
		МодульИнтеграцияПодсистемБСП = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияПодсистемБСП");
		МодульИнтеграцияПодсистемБСП.ПриОпределенииНомераВерсииПрограммы(ВерсияПрограммы);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСКлассификаторами

// См. РаботаСКлассификаторамиПереопределяемый.ПриДобавленииКлассификаторов.
//
Процедура ПриДобавленииКлассификаторов(Классификаторы) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ДанныеНагрузкиИРентабельности") Тогда
		МодульДанныеНагрузкиИРентабельности = ОбщегоНазначения.ОбщийМодуль("ДанныеНагрузкиИРентабельности");
		МодульДанныеНагрузкиИРентабельности.ПриДобавленииКлассификаторов(Классификаторы);
	КонецЕсли;
	
	Если ИнтеграцияПодсистемБИППовтИсп.ПодпискиБСП().ПриДобавленииКлассификаторов Тогда
		МодульИнтеграцияПодсистемБСП = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияПодсистемБСП");
		МодульИнтеграцияПодсистемБСП.ПриДобавленииКлассификаторов(Классификаторы);
	КонецЕсли;
	
КонецПроцедуры

// См. РаботаСКлассификаторамиПереопределяемый.ПриОпределенииНачальногоНомераВерсииКлассификатора.
//
Процедура ПриОпределенииНачальногоНомераВерсииКлассификатора(Идентификатор, НачальныйНомерВерсии) Экспорт
	
	Если ИнтеграцияПодсистемБИППовтИсп.ПодпискиБСП().ПриОпределенииНачальногоНомераВерсииКлассификатора Тогда
		МодульИнтеграцияПодсистемБСП = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияПодсистемБСП");
		МодульИнтеграцияПодсистемБСП.ПриОпределенииНачальногоНомераВерсииКлассификатора(
			Идентификатор,
			НачальныйНомерВерсии);
	КонецЕсли;
	
КонецПроцедуры

// См. РаботаСКлассификаторамиПереопределяемый.ПриЗагрузкеКлассификатора.
//
Процедура ПриЗагрузкеКлассификатора(Идентификатор, Версия, Адрес, Обработан, ДополнительныеПараметры) Экспорт
	
	Если ИнтеграцияПодсистемБИППовтИсп.ПодпискиБСП().ПриЗагрузкеКлассификатора Тогда
		МодульИнтеграцияПодсистемБСП = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияПодсистемБСП");
		МодульИнтеграцияПодсистемБСП.ПриЗагрузкеКлассификатора(
			Идентификатор,
			Версия,
			Адрес,
			Обработан,
			ДополнительныеПараметры);
	КонецЕсли;
		
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ДанныеНагрузкиИРентабельности") Тогда
		МодульДанныеНагрузкиИРентабельности = ОбщегоНазначения.ОбщийМодуль("ДанныеНагрузкиИРентабельности");
		МодульДанныеНагрузкиИРентабельности.ПриЗагрузкеКлассификатора(Идентификатор, Версия, Адрес, Обработан);
	КонецЕсли;
	
КонецПроцедуры

// См. РаботаСКлассификаторамиВМоделиСервисаПереопределяемый.ПриОбработкеОбластиДанных.
//
Процедура ПриОбработкеОбластиДанных(Идентификатор, Версия, ДополнительныеПараметры) Экспорт
	
	Если ИнтеграцияПодсистемБИППовтИсп.ПодпискиБСП().ПриОбработкеОбластиДанных Тогда
		МодульИнтеграцияПодсистемБСП = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияПодсистемБСП");
		МодульИнтеграцияПодсистемБСП.ПриОбработкеОбластиДанных(
			Идентификатор,
			Версия,
			ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Определяет события, на которые могут подписаться другие библиотеки.
//
// Возвращаемое значение:
//   События - Структура - Ключами свойств структуры являются имена событий, на которые
//             могут быть подписаны библиотеки.
//
Функция СобытияБИП() Экспорт
	
	События = Новый Структура;
	
	// Базовая функциональность БИП
	События.Вставить("ПриОпределенииКодаЯзыкаИнтерфейсаКонфигурации", Ложь);
	События.Вставить("ПриИзмененииДанныхАутентификацииИнтернетПоддержки", Ложь);
	События.Вставить("ПриОпределенииНомераВерсииПрограммы", Ложь);
	
	// Интеграция с платежными системами
	События.Вставить("ПриОпределенииНастроекИнтеграции", Ложь);
	События.Вставить("ПриЗаписиНастроекИнтеграции", Ложь);
	События.Вставить("ПриНастройкеЭлементовФормыИнтеграции", Ложь);
	События.Вставить("ПриЗаполненииФормыИнтеграции", Ложь);
	События.Вставить("ПриФормированииЗаказаНаОплатуСБП", Ложь);
	События.Вставить("ПриФормированииЗаказаНаВозвратСБП", Ложь);
	События.Вставить("ПриЗагрузкеСтатусаОперации", Ложь);
	События.Вставить("ПриОпределенииДоступностиИнтеграцииПоДокументуОперации", Ложь);
	События.Вставить("ПриОпределенииОбъектовСКомандамиСБП", Ложь);
	События.Вставить("ПриОпределенииПараметровИнтеграцииДокументаОперации", Ложь);
	События.Вставить("ПриОпределенииПараметровОтправкиСообщенийCБП", Ложь);
	События.Вставить("ПриФормированииСпискаПолучателейСообщенияСБП", Ложь);
	События.Вставить("ПриОпределенииПредопределенныхШаблоновСообщенийСБППоТипам", Ложь);
	События.Вставить("ПриПроверкеИспользованияШаблоновСообщенийСБП", Ложь);
	События.Вставить("ПриСозданииНаСервереФормыQRКода", Ложь);
	
	// Монитор Портала 1С:ИТС
	События.Вставить("ПриОпределенииОбщихПараметровМонитора", Ложь);
	События.Вставить("ПриСозданииФормыМонитора", Ложь);
	События.Вставить("ПередПолучениемДанныхМонитора", Ложь);
	События.Вставить("ПриПолученииДополнительныхДанныхМонитора", Ложь);
	События.Вставить("ОтобразитьДополнительныеДанныеМонитора", Ложь);
	
	// Онлайн оплаты
	События.Вставить("ПриОпределенииДополнительныхНастроекОнлайнОплаты", Ложь);
	События.Вставить("ПриСозданииФормыОнлайнОплаты", Ложь);
	События.Вставить("ПередНачаломРедактированияДополнительныхНастроекОнлайнОплаты", Ложь);
	События.Вставить("ПередОкончаниемРедактированияДополнительныхНастроекОнлайнОплат", Ложь);
	События.Вставить("СоответствиеРеквизитовОснованийПлатежа", Ложь);
	События.Вставить("ПриОпределенииОснованийПлатежа", Ложь);
	События.Вставить("ПриПроверкеЗаполненияОснованияПлатежа", Ложь);
	События.Вставить("ЗаполнитьРеквизитыОрганизации", Ложь);
	События.Вставить("ИспользуетсяОднаОрганизация", Ложь);
	События.Вставить("ИмяПрикладногоСправочникаОрганизации", Ложь);
	События.Вставить("ЗаполнитьДанныеОснованияПлатежа", Ложь);
	События.Вставить("ЗаполнитьКонтактнуюИнформациюОснованияПлатежа", Ложь);
	События.Вставить("ПриОпределенииПараметровОтправкиСообщенийОнлайнОплат", Ложь);
	События.Вставить("ПриФормированииСпискаПолучателейСообщения", Ложь);
	События.Вставить("ПриЗагрузкеОперацийПоОнлайнОплате", Ложь);
	События.Вставить("ПроверитьИспользованиеШаблоновСообщенийОнлайнОплат", Ложь);
	События.Вставить("ПредопределенныеШаблоныСообщенийОнлайнОплат", Ложь);
	События.Вставить("ПредопределенныеШаблоныСообщенийОнлайнОплатПоТипам", Ложь);
	
	// Подключение сервисов сопровождения
	События.Вставить("ПриОпределенииСервисовСопровождения", Ложь);
	
	// Получение обновления программы
	События.Вставить("ПриОпределенииПараметровПолученияОбновлений", Ложь);
	
	// Работа с классификаторами
	События.Вставить("ПриДобавленииКлассификаторов", Ложь);
	События.Вставить("ПриОпределенииНачальногоНомераВерсииКлассификатора", Ложь);
	События.Вставить("ПриЗагрузкеКлассификатора", Ложь);
	События.Вставить("ПриОбработкеОбластиДанных", Ложь);
	
	// Обмен данными с внешними системами
	События.Вставить("ПриОпределенииИмениПланаОбмена", Ложь);
	События.Вставить("ПриОпределенииДоступныхВнешнихСистем", Ложь);
	События.Вставить("ПередЗагрузкойФайлаОбменаДанными", Ложь);
	
	// Подключение 1С-Такском
	События.Вставить("ИспользоватьСервис1СТакском", Ложь);
	События.Вставить("ЗаполнитьРегистрационныеДанныеОрганизации", Ложь);
	
	// Получение внешних компонент
	События.Вставить("ПриОпределенииИспользуемыхВерсийВнешнихКомпонент", Ложь);
	
	// Сверка взаиморасчетов СБП (c2b)
	События.Вставить("ПриНастройкеСверкиВзаиморасчетов", Ложь);
	События.Вставить("ПриОпределенииОборотов", Ложь);
	События.Вставить("ПриСписанииРасходовКомиссии", Ложь);
	События.Вставить("ПриОпределенииДанныхОпераций", Ложь);
	
	// СПАРК Риски
	События.Вставить("ПриОпределенииСвойствСправочниковКонтрагентов", Ложь);
	События.Вставить("КонтрагентыДляМониторинга", Ложь);
	События.Вставить("ПараметрыОтображенияОтчетов", Ложь);
	События.Вставить("ПриСозданииНаСервереСПАРК", Ложь);
	События.Вставить("ВремяОжиданияФоновогоЗадания", Ложь);
	События.Вставить("ПриФормированииОтчетаНадежностьВходящегоНДС", Ложь);
	События.Вставить("ПриОпределенииИспользуемыхВерсийВнешнихКомпонент", Ложь);
	События.Вставить("ПриФормированииНадежностьДебиторов", Ложь);
	События.Вставить("ПараметрыНачальногоЗаполненияДанных1СПАРКРискиЮридическихЛиц", Ложь);
	События.Вставить("ПараметрыНачальногоЗаполненияДанных1СПАРКРискиИндивидуальныхПредпринимателей", Ложь);
	
	Возврат События;
	
КонецФункции

#КонецОбласти
