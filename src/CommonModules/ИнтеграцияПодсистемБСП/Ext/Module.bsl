///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Определяет события, на которые могут подписаться другие библиотеки.
//
// Возвращаемое значение:
//   Структура - ключами свойств структуры являются имена событий, на которые
//               могут быть подписаны библиотеки.
//
Функция СобытияБСП() Экспорт

	События = Новый Структура;
	
	// БазоваяФункциональность
	События.Вставить("ПриДобавленииПодсистем", Ложь);
	События.Вставить("ПриУстановкеПараметровСеанса", Ложь);
	События.Вставить("ПриДобавленииОбработчиковУстановкиПараметровСеанса", Ложь);
	События.Вставить("ПриДобавленииИсключенийПоискаСсылок", Ложь);
	События.Вставить("ПриДобавленииПереименованийОбъектовМетаданных", Ложь);
	События.Вставить("ПриЗаполненииВсехПараметровРаботыРасширений", Ложь);
	События.Вставить("ПриОчисткеВсехПараметровРаботыРасширений", Ложь);
	События.Вставить("ПриОтправкеДанныхГлавному", Ложь);
	События.Вставить("ПриОтправкеДанныхПодчиненному", Ложь);
	События.Вставить("ПриПолученииДанныхОтГлавного", Ложь);
	События.Вставить("ПриПолученииДанныхОтПодчиненного", Ложь);
	События.Вставить("ПослеПолученияДанных", Ложь);
	События.Вставить("ПриВключенииРазделенияПоОбластямДанных", Ложь);
	События.Вставить("ПриОпределенииПоддерживаемыхВерсийПрограммныхИнтерфейсов", Ложь);
	События.Вставить("ПриДобавленииПараметровРаботыКлиентаПриЗапуске", Ложь);
	События.Вставить("ПриДобавленииПараметровРаботыКлиента", Ложь);
	События.Вставить("ПередЗапускомПрограммы", Ложь);
	
	// ВариантыОтчетов
	События.Вставить("ПриОпределенииНастроекВариантовОтчетов", Ложь);
	События.Вставить("ПриОпределенииРазделовСВариантамиОтчетов", Ложь);
	События.Вставить("ПриНастройкеВариантовОтчетов", Ложь);
	События.Вставить("ПриИзмененииКлючейВариантовОтчетов", Ложь);
	События.Вставить("ПриОпределенииОбъектовСКомандамиОтчетов", Ложь);
	События.Вставить("ПередДобавлениемКомандОтчетов", Ложь);
	События.Вставить("ПриОпределенииПараметровВыбораВариантыОтчетов", Ложь);
	События.Вставить("ПриСозданииНаСервереВариантыОтчетов", Ложь);
	События.Вставить("ПередЗагрузкойВариантаНаСервере", Ложь);
	
	// ГрупповоеИзменениеОбъектов
	События.Вставить("ПриОпределенииОбъектовСРедактируемымиРеквизитами", Ложь);
	
	// ДатыЗапретаИзменений
	События.Вставить("ПриЗаполненииРазделовДатЗапретаИзменения", Ложь);
	События.Вставить("ПриЗаполненииИсточниковДанныхДляПроверкиЗапретаИзменения", Ложь);
	
	// ДополнительныеОтчетыИОбработки
	События.Вставить("ПриУстановкеРежимаПодключенияДополнительногоОтчетаИлиОбработкиВОбластиДанных", Ложь);
	
	// ЗагрузкаДанныхИзФайла
	События.Вставить("ПриОпределенииСправочниковДляЗагрузкиДанных", Ложь);
	
	// ЗапретРедактированияРеквизитовОбъектов
	События.Вставить("ПриОпределенииОбъектовСЗаблокированнымиРеквизитами", Ложь);
	
	// ИнтерфейсOData
	События.Вставить("ПриЗаполненииТиповИсключаемыхИзВыгрузкиЗагрузкиOData", Ложь);
	
	// КонтрольВеденияУчета
	События.Вставить("ПриОпределенииПроверок", Ложь);
	
	// НапоминанияПользователя
	События.Вставить("ПриЗаполненииСпискаРеквизитовИсточникаСДатамиДляНапоминания", Ложь);
	
	// ОбменДанными
	События.Вставить("ПриНастройкеПодчиненногоУзлаРИБ", Ложь);
	
	//ОбновлениеВерсииИБ
	События.Вставить("ПриДобавленииОбработчиковОбновления", Ложь); // Для обратной совместимости с БТС 1.2.2.
	События.Вставить("ПослеОбновленияИнформационнойБазы", Ложь);
	События.Вставить("ПриПолученииПриоритетаОбновления", Ложь);
	
	// Печать
	События.Вставить("ПриПодготовкеСпискаМакетовВФорматеОфисныхДокументовФормируемыхНаСервере", Ложь);
	События.Вставить("ПриОпределенииОбъектовСКомандамиПечати", Ложь);
	События.Вставить("ПередДобавлениемКомандПечати", Ложь);
	События.Вставить("ПриПолученииНастроекСпискаКомандПечати", Ложь);
	События.Вставить("ПриПечати", Ложь);
	События.Вставить("ПередОтправкойПоПочте", Ложь);
	События.Вставить("ПриПолученииПодписейИПечатей", Ложь);
	События.Вставить("ПечатьДокументовПриСозданииНаСервере", Ложь);
	События.Вставить("ПечатьДокументовПриЗагрузкеДанныхИзНастроекНаСервере", Ложь);
	События.Вставить("ПечатьДокументовПриСохраненииДанныхВНастройкахНаСервере", Ложь);
	События.Вставить("ПечатьДокументовПриВыполненииКоманды", Ложь);
	События.Вставить("ПриОпределенииИсточниковДанныхПечати", Ложь);
	События.Вставить("ПриПодготовкеДанныхПечати", Ложь);
	
	// ПодключаемыеКоманды
	События.Вставить("ПриОпределенииВидовПодключаемыхКоманд", Ложь);
	События.Вставить("ПриОпределенииСоставаНастроекПодключаемыхОбъектов", Ложь);
	События.Вставить("ПриОпределенииКомандПодключенныхКОбъекту", Ложь);
	События.Вставить("ПриОпределенииОбъектовСКомандамиСозданияНаОсновании", Ложь);
	События.Вставить("ПередДобавлениемКомандСозданияНаОсновании", Ложь);
	События.Вставить("ПриДобавленииКомандСозданияНаОсновании", Ложь);
	
	// Пользователи
	События.Вставить("ПриОпределенииНастроек", Ложь);
	События.Вставить("ПриОпределенииНазначенияРолей", Ложь);
	События.Вставить("ПриОпределенииДействийВФорме", Ложь);
	События.Вставить("ПриПолученииПрочихНастроек", Ложь);
	События.Вставить("ПриСохраненииПрочихНастроек", Ложь);
	События.Вставить("ПриУдаленииПрочихНастроек", Ложь);
	События.Вставить("ПриЗавершенииОбработкиПользователяИБ", Ложь);
	
	//ПрофилиБезопасности
	События.Вставить("ПриПроверкеВозможностиНастройкиПрофилейБезопасности", Ложь);
	События.Вставить("ПриЗапросеРазрешенийНаИспользованиеВнешнихРесурсов", Ложь);
	События.Вставить("ПриЗапросеСозданияПрофиляБезопасности", Ложь);
	События.Вставить("ПриЗапросеУдаленияПрофиляБезопасности", Ложь);
	События.Вставить("ПриПодключенииВнешнегоМодуля", Ложь);
	События.Вставить("ПриВключенииИспользованияПрофилейБезопасности", Ложь);
	События.Вставить("ПриЗаполненииРазрешенийНаДоступКВнешнимРесурсам", Ложь);
	
	// РаботаСФайлами
	События.Вставить("ПриОпределенииОбъектовИсключенияСинхронизацииФайлов", Ложь);
	
	// РегламентныеЗадания
	События.Вставить("ПриОпределенииНастроекРегламентныхЗаданий", Ложь);
	События.Вставить("ПриЗапретеРаботыСВнешнимиРесурсами", Ложь);
	События.Вставить("ПриРазрешенииРаботыСВнешнимиРесурсами", Ложь);
	
	// Свойства
	События.Вставить("ПриПолученииПредопределенныхНаборовСвойств", Ложь);
	
	// ТекущиеДела
	События.Вставить("ПриОпределенииОбработчиковТекущихДел", Ложь);
	События.Вставить("ПриЗаполненииСпискаТекущихДел", Ложь);
	События.Вставить("ПриОпределенииПорядкаРазделовКомандногоИнтерфейса", Ложь);
	События.Вставить("ПриОтключенииТекущихДел", Ложь);
	События.Вставить("ПриОпределенииНастроекТекущихДел", Ложь);
	События.Вставить("ПриУстановкеОбщихПараметровЗапросов", Ложь);
	События.Вставить("ПриСозданииНаСервереТекущиеДела", Ложь);
	События.Вставить("ПередЗагрузкойДанныхИзНастроекНаСервереТекущиеДела", Ложь);
	
	// УправлениеДоступом
	События.Вставить("ПриЗаполненииВидовДоступа", Ложь);
	События.Вставить("ПриЗаполненииСписковСОграничениемДоступа", Ложь);
	События.Вставить("ПриЗаполненииПоставляемыхПрофилейГруппДоступа", Ложь);
	События.Вставить("ПриЗаполненииЗависимостейПравДоступа", Ложь);
	События.Вставить("ПриЗаполненииВозможныхПравДляНастройкиПравОбъектов", Ложь);
	События.Вставить("ПриЗаполненииИспользованияВидаДоступа", Ложь);
	События.Вставить("ПриЗаполненииВидовОграниченийПравОбъектовМетаданных", Ложь);
	
	// ЦентрМониторинга
	События.Вставить("ПриСбореПоказателейСтатистикиКонфигурации", Ложь);

	Возврат События;

КонецФункции

#Область ОбработчикиСобытийБИП

// Определяет события, на которые подписана эта библиотека.
//
// Параметры:
//  Подписки - Структура - ключами свойств структуры являются имена событий, на которые
//           подписана эта библиотека.
//
Процедура ПриОпределенииПодписокНаСобытияБИП(Подписки) Экспорт
	
	// БазоваяФункциональность
	Подписки.ПриИзмененииДанныхАутентификацииИнтернетПоддержки = Истина;
	
	// РаботаСКлассификаторами
	Подписки.ПриДобавленииКлассификаторов = Истина;
	Подписки.ПриЗагрузкеКлассификатора = Истина;
	Подписки.ПриОбработкеОбластиДанных = Истина;
	
	// ЭлектроннаяПодпись
	Подписки.ПриОпределенииИспользуемыхВерсийВнешнихКомпонент = Истина;
	
КонецПроцедуры

#Область БазоваяФункциональность

// См. ИнтернетПоддержкаПользователейПереопределяемый.ПриИзмененииДанныхАутентификацииИнтернетПоддержки.
Процедура ПриИзмененииДанныхАутентификацииИнтернетПоддержки(ДанныеПользователя) Экспорт

	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Валюты") Тогда
		МодульРаботаСКурсамиВалют = ОбщегоНазначения.ОбщийМодуль("РаботаСКурсамиВалют");
		МодульРаботаСКурсамиВалют.ПриИзмененииДанныхАутентификацииИнтернетПоддержки(ДанныеПользователя);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область РаботаСКлассификаторами

// См. РаботаСКлассификаторамиПереопределяемый.ПриДобавленииКлассификаторов.
Процедура ПриДобавленииКлассификаторов(Классификаторы) Экспорт

	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Банки") И Метаданные.Обработки.Найти(
		"ЗагрузкаКлассификатораБанков") <> Неопределено Тогда
		МодульЗагрузкаКлассификатораБанков = ОбщегоНазначения.ОбщийМодуль("Обработки.ЗагрузкаКлассификатораБанков");
		МодульЗагрузкаКлассификатораБанков.ПриДобавленииКлассификаторов(Классификаторы);
	КонецЕсли;

	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Валюты") И Метаданные.Обработки.Найти(
		"ЗагрузкаКурсовВалют") <> Неопределено Тогда
		МодульЗагрузкаКурсовВалют = ОбщегоНазначения.ОбщийМодуль("Обработки.ЗагрузкаКурсовВалют");
		МодульЗагрузкаКурсовВалют.ПриДобавленииКлассификаторов(Классификаторы);
	КонецЕсли;

	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.КалендарныеГрафики") Тогда
		МодульКалендарныеГрафики = ОбщегоНазначения.ОбщийМодуль("КалендарныеГрафики");
		МодульКалендарныеГрафики.ПриДобавленииКлассификаторов(Классификаторы);
	КонецЕсли;

КонецПроцедуры

// См. РаботаСКлассификаторамиПереопределяемый.ПриЗагрузкеКлассификатора.
Процедура ПриЗагрузкеКлассификатора(Идентификатор, Версия, Адрес, Обработан, ДополнительныеПараметры) Экспорт

	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Банки") Тогда
		МодульЗагрузкаКлассификатораБанков = ОбщегоНазначения.ОбщийМодуль("Обработки.ЗагрузкаКлассификатораБанков");
		МодульЗагрузкаКлассификатораБанков.ПриЗагрузкеКлассификатора(Идентификатор, Версия, Адрес, Обработан,
			ДополнительныеПараметры);
	КонецЕсли;

	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Валюты") Тогда
		МодульЗагрузкаКурсовВалют = ОбщегоНазначения.ОбщийМодуль("Обработки.ЗагрузкаКурсовВалют");
		МодульЗагрузкаКурсовВалют.ПриЗагрузкеКлассификатора(Идентификатор, Версия, Адрес, Обработан,
			ДополнительныеПараметры);
	КонецЕсли;

	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.КалендарныеГрафики") Тогда
		МодульКалендарныеГрафики = ОбщегоНазначения.ОбщийМодуль("КалендарныеГрафики");
		МодульКалендарныеГрафики.ПриЗагрузкеКлассификатора(Идентификатор, Версия, Адрес, Обработан,
			ДополнительныеПараметры);
	КонецЕсли;

КонецПроцедуры

// См. РаботаСКлассификаторамиВМоделиСервисаПереопределяемый.ПриОбработкеОбластиДанных.
Процедура ПриОбработкеОбластиДанных(Идентификатор, Версия, ДополнительныеПараметры) Экспорт

	Если Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.КалендарныеГрафики") Тогда
		Возврат;
	КонецЕсли;

	МодульКалендарныеГрафики = ОбщегоНазначения.ОбщийМодуль("КалендарныеГрафики");
	Если Идентификатор <> МодульКалендарныеГрафики.ИдентификаторКлассификатора() Тогда
		Возврат;
	КонецЕсли;

	Если Не ДополнительныеПараметры.Свойство(Идентификатор) Тогда
		Возврат;
	КонецЕсли;

	ПараметрыОбновления = ДополнительныеПараметры[Идентификатор];

	МодульКалендарныеГрафики.ЗаполнитьДанныеЗависимыеОтПроизводственныхКалендарей(ПараметрыОбновления.ТаблицаИзменений);

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область БазоваяФункциональность

// См. ПодсистемыКонфигурацииПереопределяемый.ПриДобавленииПодсистем.
Процедура ПриДобавленииПодсистем(МодулиПодсистем) Экспорт

	Если ИнтеграцияПодсистемБСППовтИсп.ПодпискиБТС().ПриДобавленииПодсистем Тогда
		МодульИнтеграцияПодсистемБТС = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияПодсистемБТС");
		МодульИнтеграцияПодсистемБТС.ПриДобавленииПодсистем(МодулиПодсистем);
	КонецЕсли;

	Если ИнтеграцияПодсистемБСППовтИсп.ПодпискиБИП().ПриДобавленииПодсистем Тогда
		МодульИнтеграцияПодсистемБИП = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияПодсистемБИП");
		МодульИнтеграцияПодсистемБИП.ПриДобавленииПодсистем(МодулиПодсистем);
	КонецЕсли;

КонецПроцедуры

// См. ОбщегоНазначенияПереопределяемый.ПриДобавленииПараметровРаботыКлиента.
Процедура ПриДобавленииПараметровРаботыКлиента(Параметры) Экспорт

	Если ИнтеграцияПодсистемБСППовтИсп.ПодпискиБТС().ПриДобавленииПараметровРаботыКлиента Тогда
		МодульИнтеграцияПодсистемБТС = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияПодсистемБТС");
		МодульИнтеграцияПодсистемБТС.ПриДобавленииПараметровРаботыКлиента(Параметры);
	КонецЕсли;

	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.АдресныйКлассификатор") Тогда
		МодульАдресныйКлассификаторСлужебный = ОбщегоНазначения.ОбщийМодуль("АдресныйКлассификаторСлужебный");
		МодульАдресныйКлассификаторСлужебный.ПриДобавленииПараметровРаботыКлиента(Параметры);
	КонецЕсли;

	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Взаимодействия") Тогда
		МодульВзаимодействия = ОбщегоНазначения.ОбщийМодуль("Взаимодействия");
		МодульВзаимодействия.ПриДобавленииПараметровРаботыКлиента(Параметры);
	КонецЕсли;

	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаВМоделиСервиса.ОбменДаннымиВМоделиСервиса") Тогда
		МодульОбменДаннымиВМоделиСервиса = ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиВМоделиСервиса");
		МодульОбменДаннымиВМоделиСервиса.ПриДобавленииПараметровРаботыКлиента(Параметры);
	КонецЕсли;

	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ОтправкаSMS") Тогда
		МодульОтправкаSMS = ОбщегоНазначения.ОбщийМодуль("ОтправкаSMS");
		МодульОтправкаSMS.ПриДобавленииПараметровРаботыКлиента(Параметры);
	КонецЕсли;

	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
		МодульПолучениеФайловИзИнтернетаСлужебный = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернетаСлужебный");
		МодульПолучениеФайловИзИнтернетаСлужебный.ПриДобавленииПараметровРаботыКлиента(Параметры);
	КонецЕсли;

	ПользователиСлужебный.ПриДобавленииПараметровРаботыКлиента(Параметры);

	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РезервноеКопированиеИБ") Тогда
		МодульРезервноеКопированиеИБСервер = ОбщегоНазначения.ОбщийМодуль("РезервноеКопированиеИБСервер");
		МодульРезервноеКопированиеИБСервер.ПриДобавленииПараметровРаботыКлиента(Параметры);
	КонецЕсли;

	СтандартныеПодсистемыСервер.ПриДобавленииПараметровРаботыКлиента(Параметры);

	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступомСлужебный = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступомСлужебный");
		МодульУправлениеДоступомСлужебный.ПриДобавленииПараметровРаботыКлиента(Параметры);
	КонецЕсли;

	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ЭлектроннаяПодпись") Тогда
		МодульЭлектроннаяПодписьСлужебный = ОбщегоНазначения.ОбщийМодуль("ЭлектроннаяПодписьСлужебный");
		МодульЭлектроннаяПодписьСлужебный.ПриДобавленииПараметровРаботыКлиента(Параметры);             
	КонецЕсли;

	Если ИнтеграцияПодсистемБСППовтИсп.ПодпискиБИП().ПриДобавленииПараметровРаботыКлиента Тогда
		МодульИнтеграцияПодсистемБИП = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияПодсистемБИП");
		МодульИнтеграцияПодсистемБИП.ПриДобавленииПараметровРаботыКлиента(Параметры);
	КонецЕсли;

	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ЭлектроннаяПодписьСервисаDSS") Тогда
		МодульСервисКриптографииDSSСлужебный = ОбщегоНазначения.ОбщийМодуль("СервисКриптографииDSSСлужебный");
		МодульСервисКриптографииDSSСлужебный.ПриДобавленииПараметровРаботыКлиента(Параметры);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область НапоминанияПользователя

// См. НапоминанияПользователяПереопределяемый.ПриЗаполненииСпискаРеквизитовИсточникаСДатамиДляНапоминания.
Процедура ПриЗаполненииСпискаРеквизитовИсточникаСДатамиДляНапоминания(Источник, РеквизитыСДатами) Экспорт
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти