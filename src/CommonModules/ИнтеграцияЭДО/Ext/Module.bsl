// Возвращает ссылку на основной электронный документ объекта учета.
// 
// Параметры:
//  ОбъектУчета - ОпределяемыеТипы.ОснованияЭлектронныхДокументовЭДО
//
// Возвращаемое значение:
//  ДокументСсылка.ЭлектронныйДокументВходящийЭДО, ДокументСсылка.ЭлектронныйДокументИсходящийЭДО, Неопределено - ссылка на электронный документ.
//  Неопределено - электронный документ не найден.
//
Функция ОсновнойЭлектронныйДокументОбъектаУчета(Знач ОбъектУчета) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент КАК ЭлектронныйДокумент,
	|	ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент.ВидДокумента КАК ВидДокумента,
	|	ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент.Организация КАК Организация
	|ПОМЕСТИТЬ ДанныеДокументовОбъектаУчета
	|ИЗ
	|	РегистрСведений.ОбъектыУчетаДокументовЭДО КАК ОбъектыУчетаДокументовЭДО
	|ГДЕ
	|	ОбъектыУчетаДокументовЭДО.ОбъектУчета = &ОбъектУчета
	|	И ОбъектыУчетаДокументовЭДО.Актуальный";
	
	Запрос.УстановитьПараметр("ОбъектУчета", ОбъектУчета);
	
	ПоляТаблицы = 
		"	ДанныеДокументовОбъектаУчета.ЭлектронныйДокумент КАК ЭлектронныйДокумент";
	ИмяТаблицы = 
		"	ДанныеДокументовОбъектаУчета КАК ДанныеДокументовОбъектаУчета";
	
	ПоляКлючаНастройки = НастройкиЭДО.НовоеОписаниеПолейКлючаНастройкиВнутреннегоЭДО();
	ПоляКлючаНастройки.Организация = "ДанныеДокументовОбъектаУчета.Организация";
	ПоляКлючаНастройки.ВидВнутреннегоДокумента = "ДанныеДокументовОбъектаУчета.ВидДокумента";
	
	ТекстЗапросаСНастройками = НастройкиЭДО.ТекстЗапросаДополнениеНастройкамиВнутреннегоЭДО(ИмяТаблицы, ПоляТаблицы, ПоляКлючаНастройки);
	
	ТекстыЗапросов = Новый Массив;                                                           
	ТекстыЗапросов.Добавить(Запрос.Текст);
	ТекстыЗапросов.Добавить(ТекстЗапросаСНастройками);
	
	Запрос.Текст = СтрСоединить(ТекстыЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Выборка = Запрос.Выполнить().Выгрузить();
	Если Выборка.Количество() Тогда
		ДокументСВидомПоУмолчанию = Выборка.Найти(Истина, "ЭтоОсновнойВидДокумента"); 
		Если ДокументСВидомПоУмолчанию <> Неопределено Тогда
			Возврат ДокументСВидомПоУмолчанию.ЭлектронныйДокумент;
		Иначе
			Возврат Выборка[0].ЭлектронныйДокумент;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции
