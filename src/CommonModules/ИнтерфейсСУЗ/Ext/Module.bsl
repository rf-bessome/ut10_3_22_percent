#Область ПрограммныйИнтерфейс

#Область Формат_V1

Функция СоздатьБизнесЗаказНаЭмиссиюКодовМаркировки_V1(ТелоЗапросаJSON, ПараметрыСУЗ) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса", Неопределено);
	ВозвращаемоеЗначение.Вставить("Операция",                 Перечисления.ВидыОперацийИСМП.ЗаказНаЭмиссиюКодовМаркировки);
	ВозвращаемоеЗначение.Вставить("СтатусОбработки",          Неопределено);
	ВозвращаемоеЗначение.Вставить("ДанныеБизнесЗаказа",       Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",              "");

	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Content-Type",   "application/json; charset=utf-8");
	ЗаголовокHTTP.Вставить("Accept",         "application/json");
	ЗаголовокHTTP.Вставить("clientToken",    ПараметрыСУЗ.Токен);
	
	URLЗапроса = "api/orders";
	РезультатЗапроса = ИнтеграцияИСМП.ОтправитьДанныеВСервис(URLЗапроса, ТелоЗапросаJSON, Неопределено, "POST", ПараметрыСУЗ, ЗаголовокHTTP);
	
	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
			
			Если ДанныеОбработки = Неопределено Тогда
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
				
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			Иначе
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаПринята;
				
				ПроверятьПосле = ТекущаяДатаСеанса() + Окр(ДанныеОбработки.expectedCompleteTimestamp / 1000, 0, РежимОкругления.Окр15как20);
				
				ДанныеБизнесЗаказа = Новый Структура;
				ДанныеБизнесЗаказа.Вставить("ИдентификаторЗаявки", ДанныеОбработки.orderId);
				ДанныеБизнесЗаказа.Вставить("ПроверятьПосле",      ПроверятьПосле);
				
				ВозвращаемоеЗначение.ДанныеБизнесЗаказа = ДанныеБизнесЗаказа;
				
			КонецЕсли;
			
			Возврат ВозвращаемоеЗначение;
			
		ИначеЕсли РезультатОтправкиЗапроса.КодСостояния = 400 Тогда
			
			ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаОтклонена;
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			
		Иначе
			
			ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			
		КонецЕсли;
		
	Иначе
		
		ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
		
		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция СтатусПулаКодовМаркировкиИзБизнесЗаказа_V1(ПараметрыЗапроса, ПараметрыСУЗ) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса", Неопределено);
	ВозвращаемоеЗначение.Вставить("Операция",                 Перечисления.ВидыОперацийИСМП.ПолучениеСтатусаПулаКодовМаркировкиИзСУЗ);
	ВозвращаемоеЗначение.Вставить("СтатусОбработки",          Перечисления.СтатусыОбработкиСообщенийИСМП.ПустаяСсылка());
	ВозвращаемоеЗначение.Вставить("ДанныеПула",               Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",              "");
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Content-Type",   "application/x-www-form-urlencoded;charset=UTF-8");
	ЗаголовокHTTP.Вставить("Accept",         "application/json");
	ЗаголовокHTTP.Вставить("Accept-Charset", "utf-8");
	ЗаголовокHTTP.Вставить("clientToken",    ПараметрыСУЗ.Токен);
	
	URLЗапроса = СтрШаблон(
		"api/poolStatus?orderId=%1&orderLineId=%2",
		КодироватьСтроку(ПараметрыЗапроса.ИдентификаторБизнесЗаказа, СпособКодированияСтроки.КодировкаURL),
		КодироватьСтроку(ПараметрыЗапроса.ИдентификаторСтрокиЗаказа, СпособКодированияСтроки.КодировкаURL));
	
	РезультатЗапроса = ИнтеграцияИСМП.ПолучитьДанныеИзСервиса(URLЗапроса, Неопределено, ПараметрыСУЗ, ЗаголовокHTTP);
	
	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
			
			Если ДанныеОбработки = Неопределено Тогда
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
				
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			Иначе
				
				Статус = ИнтерфейсСУЗСлужебный.СтатусПулаКодовМаркировки_V1(ДанныеОбработки.status);
				
				ДанныеПула = Новый Структура;
				ДанныеПула.Вставить("GTIN",       ДанныеОбработки.gtin);
				ДанныеПула.Вставить("Количество", ДанныеОбработки.quantity);
				ДанныеПула.Вставить("Остаток",    ДанныеОбработки.left);
				ДанныеПула.Вставить("Статус",     Статус);
				
				Если Статус = Перечисления.СтатусыПулаКодовМаркировкиСУЗ.Обрабатывается Тогда
					ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаОбрабатывается;
				ИначеЕсли Статус = Перечисления.СтатусыПулаКодовМаркировкиСУЗ.Ошибка Тогда
					ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаОтклонена;
				Иначе
					ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаВыполнена;
				КонецЕсли;
				
				ВозвращаемоеЗначение.ДанныеПула = ДанныеПула;
				
			КонецЕсли;
			
		Иначе
			
			ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			
		КонецЕсли;
		
	Иначе
		
		ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
		
		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ЗапроситьКодыМаркировкиИзБизнесЗаказа_V1(ПараметрыЗапроса, КоличествоКодовМаркировки, ПараметрыСУЗ) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса", Неопределено);
	ВозвращаемоеЗначение.Вставить("Операция",                 Перечисления.ВидыОперацийИСМП.ПолучениеКодовМаркировкиИзСУЗ);
	ВозвращаемоеЗначение.Вставить("СтатусОбработки",          Неопределено);
	ВозвращаемоеЗначение.Вставить("ДанныеБлока",              Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",              "");
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Content-Type",   "application/x-www-form-urlencoded;charset=UTF-8");
	ЗаголовокHTTP.Вставить("Accept",         "application/json");
	ЗаголовокHTTP.Вставить("Accept-Charset", "utf-8");
	ЗаголовокHTTP.Вставить("clientToken",    ПараметрыСУЗ.Токен);
	
	URLЗапроса = СтрШаблон(
		"api/codes?orderId=%1&orderLineId=%2&quantity=%3",
		КодироватьСтроку(ПараметрыЗапроса.ИдентификаторБизнесЗаказа, СпособКодированияСтроки.КодировкаURL),
		КодироватьСтроку(ПараметрыЗапроса.ИдентификаторСтрокиЗаказа, СпособКодированияСтроки.КодировкаURL),
		Формат(КоличествоКодовМаркировки, "ЧГ=0;"));
	
	РезультатЗапроса = ИнтеграцияИСМП.ПолучитьДанныеИзСервиса(URLЗапроса, Неопределено, ПараметрыСУЗ, ЗаголовокHTTP);
	
	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			// Ожидаемый результат запроса:
			// Массив Из Строка - Массив кодов маркировки
			ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
			Если ДанныеОбработки = Неопределено Тогда
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
				
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			Иначе
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаВыполнена;
				
				ДанныеБлока = Новый Структура;
				ДанныеБлока.Вставить("КодыМаркировки",                    ДанныеОбработки);
				ДанныеБлока.Вставить("ИдентификаторПоследнегоБлокаКодов", 0);
				
				ВозвращаемоеЗначение.ДанныеБлока = ДанныеБлока;
				
			КонецЕсли;
			
		Иначе
			
			ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			
		КонецЕсли;
		
	Иначе
		
		ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
		
		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ПередатьОтчетОбИспользованииКодовМаркировкиБизнесЗаказа_V1(ТелоЗапроса, ПараметрыСУЗ) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса", Неопределено);
	ВозвращаемоеЗначение.Вставить("Операция",                 Перечисления.ВидыОперацийИСМП.ОтчетОбИспользованииКодовМаркировки);
	ВозвращаемоеЗначение.Вставить("СтатусОбработки",          Неопределено);
	ВозвращаемоеЗначение.Вставить("ИдентификаторЗаявки",      Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",              "");
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Content-Type",   "application/json");
	ЗаголовокHTTP.Вставить("Accept",         "application/json");
	ЗаголовокHTTP.Вставить("Accept-Charset", "utf-8");
	ЗаголовокHTTP.Вставить("clientToken",    ПараметрыСУЗ.Токен);
	
	ТелоЗапросаJSON = ИнтерфейсМОТПСлужебный.ОбъектВТекстJSON(ТелоЗапроса);
	
	URLЗапроса = "api/reports";
	РезультатЗапроса = ИнтеграцияИСМП.ОтправитьДанныеВСервис(
		URLЗапроса, ТелоЗапросаJSON, Неопределено, "POST",
		ПараметрыСУЗ, ЗаголовокHTTP);
	
	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
			
			Если ДанныеОбработки = Неопределено Тогда
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
				
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			Иначе
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаПринята;
				
				ВозвращаемоеЗначение.ИдентификаторЗаявки = ДанныеОбработки;
				
			КонецЕсли;
			
		ИначеЕсли РезультатОтправкиЗапроса.КодСостояния = 400 Тогда
			
			ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаОтклонена;
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			
		Иначе
			
			ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			
		КонецЕсли;
		
	Иначе
		
		ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
		
		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ЗапроситьОтчетОбИспользовании_V1(Идентификатор, ПараметрыСУЗ) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",    Неопределено);
	ВозвращаемоеЗначение.Вставить("Операция",                    Неопределено);
	ВозвращаемоеЗначение.Вставить("СтатусОбработки",             Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                 "");
	ВозвращаемоеЗначение.Вставить("ДанныеОтчетаОбИспользовании", Неопределено);
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Content-Type",   "application/json; charset=utf-8");
	ЗаголовокHTTP.Вставить("Accept",         "application/json");
	ЗаголовокHTTP.Вставить("Accept-Charset", "utf-8");
	ЗаголовокHTTP.Вставить("clientToken",    ПараметрыСУЗ.Токен);
	
	URLЗапроса = СтрШаблон("api/usage?productionOrderId=%1", Идентификатор);
	
	РезультатЗапроса = ИнтеграцияИСМП.ПолучитьДанныеИзСервиса(URLЗапроса, Неопределено, ПараметрыСУЗ, ЗаголовокHTTP);
	
	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
			
			Если ДанныеОбработки = Неопределено Тогда
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
				
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			Иначе
				
				ДанныеОтчетаОбИспользовании = Новый ТаблицаЗначений();
				ДанныеОтчетаОбИспользовании.Колонки.Добавить("ИдентификаторЗаказа", Новый ОписаниеТипов("Строка"));
				ДанныеОтчетаОбИспользовании.Колонки.Добавить("ИдентификаторСтроки", Новый ОписаниеТипов("Строка"));
				ДанныеОтчетаОбИспользовании.Колонки.Добавить("КодМаркировки",       Новый ОписаниеТипов("Строка"));
				ДанныеОтчетаОбИспользовании.Колонки.Добавить("Статус",              Новый ОписаниеТипов("ПеречислениеСсылка.СтатусыКодовМаркировкиСУЗ"));
				
				ВозвращаемоеЗначение.ДанныеОтчетаОбИспользовании = Неопределено;
				Для Каждого БизнесЗаказ Из ДанныеОбработки Цикл
					
					Для Каждого Элемент Из БизнесЗаказ.businessOrderReports Цикл
						
						Для Каждого ЭлементСтатуса Из Элемент.codeStatuses  Цикл
							
							СтрокаТЧ = ДанныеОтчетаОбИспользовании.Добавить();
							СтрокаТЧ.ИдентификаторЗаказа = Элемент.orderId;
							СтрокаТЧ.ИдентификаторСтроки = Элемент.orderLineId;
							СтрокаТЧ.КодМаркировки       = ЭлементСтатуса.sntin;
							СтрокаТЧ.Статус              = ИнтерфейсСУЗСлужебный.СтатусКодаМаркировки(ЭлементСтатуса.codeStatus);
							
						КонецЦикла;
						
					КонецЦикла;
					
				КонецЦикла;
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаВыполнена;
				
				ВозвращаемоеЗначение.ДанныеОтчетаОбИспользовании = ДанныеОтчетаОбИспользовании;
				
			КонецЕсли;
			
		Иначе
			
			ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			
		КонецЕсли;
		
	Иначе
		
		ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
		
		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ЗапроситьСтатусБизнесЗаказа_V1(Идентификатор, ПараметрыСУЗ) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",    Неопределено);
	ВозвращаемоеЗначение.Вставить("Операция",                    Неопределено);
	ВозвращаемоеЗначение.Вставить("СтатусОбработки",             Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                 "");
	ВозвращаемоеЗначение.Вставить("ПараметрыСтатуса",            Неопределено);
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Content-Type",   "application/json; charset=utf-8");
	ЗаголовокHTTP.Вставить("Accept",         "application/json");
	ЗаголовокHTTP.Вставить("Accept-Charset", "utf-8");
	ЗаголовокHTTP.Вставить("clientToken",    ПараметрыСУЗ.Токен);
	
	URLЗапроса = СтрШаблон(
		"api/orders?productionOrderId=%1",
		Идентификатор);
	РезультатЗапроса = ИнтеграцияИСМП.ПолучитьДанныеИзСервиса(URLЗапроса, Неопределено, ПараметрыСУЗ, ЗаголовокHTTP);
	
	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
			
			Если ДанныеОбработки = Неопределено Тогда
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
				
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			Иначе
				
//				ДанныеОтчетаОбИспользовании = Новый ТаблицаЗначений();
//				ДанныеОтчетаОбИспользовании.Колонки.Добавить("ИдентификаторЗаказа", Новый ОписаниеТипов("Строка"));
//				ДанныеОтчетаОбИспользовании.Колонки.Добавить("ИдентификаторСтроки", Новый ОписаниеТипов("Строка"));
//				ДанныеОтчетаОбИспользовании.Колонки.Добавить("КодМаркировки",       Новый ОписаниеТипов("Строка"));
//				ДанныеОтчетаОбИспользовании.Колонки.Добавить("Статус",              Новый ОписаниеТипов("ПеречислениеСсылка.СтатусыКодовМаркировкиСУЗ"));
//
//				ВозвращаемоеЗначение.ДанныеОтчетаОбИспользовании = Неопределено;
//				Для Каждого БизнесЗаказ Из ДанныеОбработки Цикл
//
//					Для Каждого Элемент Из БизнесЗаказ.businessOrderReports Цикл
//
//						Для Каждого ЭлементСтатуса Из Элемент.codeStatuses  Цикл
//
//							СтрокаТЧ = ДанныеОтчетаОбИспользовании.Добавить();
//							СтрокаТЧ.ИдентификаторЗаказа = Элемент.orderId;
//							СтрокаТЧ.ИдентификаторСтроки = Элемент.orderLineId;
//							СтрокаТЧ.КодМаркировки       = ЭлементСтатуса.sntin;
//							СтрокаТЧ.Статус              = ИнтерфейсСУЗСлужебный.СтатусКодаМаркировки(ЭлементСтатуса.codeStatus);
//
//						КонецЦикла;
//
//					КонецЦикла;
//
//				КонецЦикла;
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаВыполнена;
				
				ВозвращаемоеЗначение.ПараметрыСтатуса = Неопределено;
				
			КонецЕсли;
			
		Иначе
			
			ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			
		КонецЕсли;
		
	Иначе
		
		ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
		
		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

#КонецОбласти

#Область Формат_V2

Функция СоздатьБизнесЗаказНаЭмиссиюКодовМаркировки_V2(ТелоЗапросаJSON, ВидПродукции, ПараметрыСУЗ, Подпись = Неопределено) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", Ложь);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("Операция",                       Перечисления.ВидыОперацийИСМП.ЗаказНаЭмиссиюКодовМаркировки);
	ВозвращаемоеЗначение.Вставить("СтатусОбработки",                Неопределено);
	ВозвращаемоеЗначение.Вставить("ДанныеБизнесЗаказа",             Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	ВозвращаемоеЗначение.Вставить("Статус",                         Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.ПустаяСсылка());

	ТокенДоступа = ТокенДоступаПоПараметрамСУЗ(ПараметрыСУЗ);
	
	Если ТокенДоступа = Неопределено Тогда
		ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Content-Type", "application/json; charset=utf-8");
	ЗаголовокHTTP.Вставить("Accept",       "application/json");
	ЗаголовокHTTP.Вставить("clientToken",  ТокенДоступа);
	Если Подпись <> Неопределено Тогда
		ЗаголовокHTTP.Вставить(
			"X-Signature", ИнтеграцияИСКлиентСервер.ДвоичныеДанныеBase64(Подпись));
	КонецЕсли;
	
	URLЗапроса = СтрШаблон(
		URLЗапроса("api/v2/extension/orders?omsId=%1", ВидПродукции),
		ПараметрыСУЗ.Идентификатор);

	РезультатЗапроса = ИнтеграцияИСМП.ОтправитьДанныеВСервис(URLЗапроса, ТелоЗапросаJSON, Неопределено, "POST", ПараметрыСУЗ, ЗаголовокHTTP);
	
	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			// Структура - Ожидаемое результат запроса:
			//  * omsId - GUID - Идентификатор СУЗ
			//  * orderId - GUID - Идентификатор заявки
			//  * expectedCompletionTime - Строка - Время в миллисекундах
			ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
			Если ДанныеОбработки = Неопределено Тогда
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
				
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			Иначе
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаПринята;
				ВозвращаемоеЗначение.Статус          = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.СУЗСоздан;

				Если ДанныеОбработки.Свойство("expectedCompleteTimestamp") Тогда
					ОжидаемаяДатаЗавершенияОбработки = ДанныеОбработки.expectedCompleteTimestamp;
				ИначеЕсли ДанныеОбработки.Свойство("expectedCompletionTime") Тогда
					ОжидаемаяДатаЗавершенияОбработки = ДанныеОбработки.expectedCompletionTime;
				Иначе
					ОжидаемаяДатаЗавершенияОбработки = 10000;
				КонецЕсли;
				
				ПроверятьПосле = ТекущаяДатаСеанса() + Окр(ОжидаемаяДатаЗавершенияОбработки / 1000, 0, РежимОкругления.Окр15как20);
				
				ДанныеБизнесЗаказа = Новый Структура;
				ДанныеБизнесЗаказа.Вставить("ИдентификаторЗаявки", ДанныеОбработки.orderId);
				ДанныеБизнесЗаказа.Вставить("ПроверятьПосле",      ПроверятьПосле);
				
				ВозвращаемоеЗначение.ДанныеБизнесЗаказа = ДанныеБизнесЗаказа;
				
			КонецЕсли;
			
			Возврат ВозвращаемоеЗначение;
			
		ИначеЕсли РезультатОтправкиЗапроса.КодСостояния = 400 Тогда
			
			ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаОтклонена;
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			
		Иначе
			
			ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			
		КонецЕсли;
		
	Иначе
		
		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ПередатьОтчетОСписанииКодовМаркировки_V2(ТелоЗапросаJSON, ВидПродукции, ПараметрыСУЗ, Подпись = Неопределено) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", Ложь);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("Операция",                       Перечисления.ВидыОперацийИСМП.СписаниеЭмитированныхКодовМаркировки);
	ВозвращаемоеЗначение.Вставить("СтатусОбработки",                Неопределено);
	ВозвращаемоеЗначение.Вставить("ИдентификаторЗаявки",            Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	
	ТокенДоступа = ТокенДоступаПоПараметрамСУЗ(ПараметрыСУЗ);
	
	Если ТокенДоступа = Неопределено Тогда
		ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Content-Type",   "application/json; charset=utf-8");
	ЗаголовокHTTP.Вставить("Accept",         "application/json");
	ЗаголовокHTTP.Вставить("Accept-Charset", "utf-8");
	ЗаголовокHTTP.Вставить("clientToken",    ТокенДоступа);
	Если ЗначениеЗаполнено(ПараметрыСУЗ.ИмяПользователя) Тогда
		ЗаголовокHTTP.Вставить("userName", ПараметрыСУЗ.ИмяПользователя);
	КонецЕсли;
	Если Подпись <> Неопределено Тогда
		ЗаголовокHTTP.Вставить(
			"X-Signature", ИнтеграцияИСКлиентСервер.ДвоичныеДанныеBase64(Подпись));
	КонецЕсли;
	
	URLЗапроса = СтрШаблон(
		URLЗапроса("api/v2/extension/dropout?omsId=%1", ВидПродукции),
		ПараметрыСУЗ.Идентификатор);

	РезультатЗапроса = ИнтеграцияИСМП.ОтправитьДанныеВСервис(URLЗапроса, ТелоЗапросаJSON, Неопределено, "POST", ПараметрыСУЗ, ЗаголовокHTTP);
	
	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			// Структура - Ожидаемое результат запроса:
			//  * omsId - GUID - Идентификатор СУЗ
			//  * reportId - GUID - Идентификатор отчета
			ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
			Если ДанныеОбработки = Неопределено Тогда
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
				
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			Иначе
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаПринята;
				ВозвращаемоеЗначение.ИдентификаторЗаявки = ДанныеОбработки.reportId;
				
			КонецЕсли;
			
		Иначе
			
			ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаОтклонена;
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			
		КонецЕсли;
		
	Иначе
		
		ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
		
		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ПередатьОтчетОбИспользованииКодовМаркировки_V2(ТелоЗапросаJSON, ЭлементОчереди, ПараметрыСУЗ, Подпись = Неопределено) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", Ложь);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("Операция",                       ЭлементОчереди.Операция);
	ВозвращаемоеЗначение.Вставить("СтатусОбработки",                Неопределено);
	ВозвращаемоеЗначение.Вставить("ИдентификаторЗаявки",            Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	
	ТокенДоступа = ТокенДоступаПоПараметрамСУЗ(ПараметрыСУЗ);
	
	Если ТокенДоступа = Неопределено Тогда
		ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Content-Type",   "application/json; charset=utf-8");
	ЗаголовокHTTP.Вставить("Accept",         "application/json");
	ЗаголовокHTTP.Вставить("Accept-Charset", "utf-8");
	ЗаголовокHTTP.Вставить("clientToken",    ТокенДоступа);
	Если ЗначениеЗаполнено(ПараметрыСУЗ.ИмяПользователя) Тогда
		ЗаголовокHTTP.Вставить("userName", ПараметрыСУЗ.ИмяПользователя);
	КонецЕсли;
	Если Подпись <> Неопределено Тогда
		ЗаголовокHTTP.Вставить(
			"X-Signature", ИнтеграцияИСКлиентСервер.ДвоичныеДанныеBase64(Подпись));
	КонецЕсли;
	
	URLЗапроса = СтрШаблон(
		URLЗапроса_V2("api/v2/extension/utilisation?omsId=%1", ЭлементОчереди.ВидПродукции),
		ПараметрыСУЗ.Идентификатор);
	РезультатЗапроса = ИнтеграцияИСМП.ОтправитьДанныеВСервис(URLЗапроса, ТелоЗапросаJSON, Неопределено, "POST", ПараметрыСУЗ, ЗаголовокHTTP);
	
	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			// Структура - Ожидаемое результат запроса:
			//  * omsId - GUID - Идентификатор СУЗ
			//  * reportId - GUID - Идентификатор отчета
			ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
			Если ДанныеОбработки = Неопределено Тогда
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
				
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			Иначе
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаПринята;
				
				ВозвращаемоеЗначение.ИдентификаторЗаявки = ДанныеОбработки.reportId;
				
			КонецЕсли;
			
		Иначе
			
			ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаОтклонена;
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			
		КонецЕсли;
		
	Иначе
		
		ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
		
		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ПередатьОтчетОбАгрегацииКодовМаркировки_V2(ТелоЗапросаJSON, ЭлементОчереди, ПараметрыСУЗ, Подпись = Неопределено) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", Ложь);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("Операция",                       ЭлементОчереди.Операция);
	ВозвращаемоеЗначение.Вставить("СтатусОбработки",                Неопределено);
	ВозвращаемоеЗначение.Вставить("ИдентификаторЗаявки",            Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	
	ТокенДоступа = ТокенДоступаПоПараметрамСУЗ(ПараметрыСУЗ);
	
	Если ТокенДоступа = Неопределено Тогда
		ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Content-Type",   "application/json; charset=utf-8");
	ЗаголовокHTTP.Вставить("Accept",         "application/json");
	ЗаголовокHTTP.Вставить("Accept-Charset", "utf-8");
	ЗаголовокHTTP.Вставить("clientToken",    ТокенДоступа);
	Если ЗначениеЗаполнено(ПараметрыСУЗ.ИмяПользователя) Тогда
		ЗаголовокHTTP.Вставить("userName", ПараметрыСУЗ.ИмяПользователя);
	КонецЕсли;
	Если Подпись <> Неопределено Тогда
		ЗаголовокHTTP.Вставить(
			"X-Signature", ИнтеграцияИСКлиентСервер.ДвоичныеДанныеBase64(Подпись));
	КонецЕсли;
	
	URLЗапроса = СтрШаблон(
		URLЗапроса_V2("api/v2/extension/aggregation?omsId=%1", ЭлементОчереди.ВидПродукции),
		ПараметрыСУЗ.Идентификатор);
	РезультатЗапроса = ИнтеграцияИСМП.ОтправитьДанныеВСервис(URLЗапроса, ТелоЗапросаJSON, Неопределено, "POST", ПараметрыСУЗ, ЗаголовокHTTP);
	
	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			// Структура - Ожидаемое результат запроса:
			//  * omsId - GUID - Идентификатор СУЗ
			//  * reportId - GUID - Идентификатор отчета
			ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
			Если ДанныеОбработки = Неопределено Тогда

				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;

				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);

			Иначе

				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаПринята;

				ВозвращаемоеЗначение.ИдентификаторЗаявки = ДанныеОбработки.reportId;

			КонецЕсли;

		Иначе

			ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаОтклонена;

			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);

		КонецЕсли;

	Иначе

		ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;

		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);

	КонецЕсли;

	Возврат ВозвращаемоеЗначение;

КонецФункции

Функция ПодтвердитьПоступлениеКИЗ_V3(ТелоЗапросаJSON, ВидПродукции, ПараметрыСУЗ, Подпись = Неопределено) Экспорт

	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", Ложь);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("Операция",                       Перечисления.ВидыОперацийИСМП.ПодтверждениеПоступленияКИЗ);
	ВозвращаемоеЗначение.Вставить("СтатусОбработки",                Неопределено);
	ВозвращаемоеЗначение.Вставить("ИдентификаторЗаявки",            Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");

	ТокенДоступа = ТокенДоступаПоПараметрамСУЗ(ПараметрыСУЗ);

	Если ТокенДоступа = Неопределено Тогда
		ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;

	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Content-Type", "application/json; charset=utf-8");
	ЗаголовокHTTP.Вставить("Accept",       "application/json");
	ЗаголовокHTTP.Вставить("clientToken",  ТокенДоступа);

	Если Подпись <> Неопределено Тогда
		ЗаголовокHTTP.Вставить(
			"X-Signature", ИнтеграцияИСКлиентСервер.ДвоичныеДанныеBase64(Подпись));
	КонецЕсли;

	URLЗапроса = СтрШаблон(
		URLЗапроса("api/v2/extension/acceptance?omsId=%1", ВидПродукции),
		ПараметрыСУЗ.Идентификатор);

	РезультатЗапроса = ИнтеграцияИСМП.ОтправитьДанныеВСервис(URLЗапроса, ТелоЗапросаJSON, Неопределено, "POST", ПараметрыСУЗ, ЗаголовокHTTP);

	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);

	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;

	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда

		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда

			// Структура - Ожидаемый результат запроса:
			//  * reportId - GUID - Идентификатор отчета
			ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
			Если ДанныеОбработки = Неопределено Тогда

				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
				
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			Иначе
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаПринята;
				
				ВозвращаемоеЗначение.ИдентификаторЗаявки = ДанныеОбработки.reportId;
				
			КонецЕсли;
			
		Иначе
			
			ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаОтклонена;
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			
		КонецЕсли;
		
	Иначе
		
		ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
		
		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ЗапроситьИдентификаторСоединения_V2(ТелоЗапросаJSON, ЭлементОчереди, ПараметрыСУЗ, Подпись = Неопределено) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", Ложь);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("Операция",                       ЭлементОчереди.Операция);
	ВозвращаемоеЗначение.Вставить("СтатусОбработки",                Неопределено);
	ВозвращаемоеЗначение.Вставить("ИдентификаторСоединения",        Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Content-Type",   "application/json; charset=utf-8");
	ЗаголовокHTTP.Вставить("Accept",         "application/json");
	ЗаголовокHTTP.Вставить("Accept-Charset", "utf-8");
	Если Подпись <> Неопределено Тогда
		ЗаголовокHTTP.Вставить(
			"X-Signature", ИнтеграцияИСКлиентСервер.ДвоичныеДанныеBase64(Подпись));
	КонецЕсли;
	
	ПараметрыURL = Новый Соответствие();
	ПараметрыURL.Вставить("omsId", ПараметрыСУЗ.Идентификатор);
	
	РежимРаботыСТестовымКонтуромИСМП = ПолучитьФункциональнуюОпцию("РежимРаботыСТестовымКонтуромИСМП");
	
	URLЗапроса = СтрШаблон(
		"api/v2/integration/connection?omsId=%1",
		ПараметрыСУЗ.Идентификатор);
	
	Если Не РежимРаботыСТестовымКонтуромИСМП
		И ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ИнтеграцияСЦРПТ") Тогда
		
		ДанныеБиблиотеки = Новый Структура;
		ДанныеБиблиотеки.Вставить("Идентификатор", "БГосИС");
		ДанныеБиблиотеки.Вставить("Версия",        ИнтеграцияИС.ВерсияБиблиотеки());
		
		МодульИнтеграцияСЦРПТ = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияСЦРПТ");
		РезультатБИП = МодульИнтеграцияСЦРПТ.ИдентификаторСоединения(ТелоЗапросаJSON, ЗаголовокHTTP, ПараметрыURL, ДанныеБиблиотеки);
		
		ЗаголовокHTTP.Вставить("X-RegistrationKey", НСтр("ru = '<данные скрыты>'"));
		
		Если РезультатБИП.ДанныеОтвета <> Неопределено Тогда
			
			// Эмуляция
			HTTPОтветЭмуляция = Новый Структура;
			HTTPОтветЭмуляция.Вставить("Заголовки",    Новый Соответствие());
			HTTPОтветЭмуляция.Вставить("КодСостояния", 200);
			HTTPОтветЭмуляция.Вставить("Тело",         ИнтерфейсМОТПСлужебный.ОбъектВТекстJSON(РезультатБИП.ДанныеОтвета, Истина));
			
			РезультатЗапроса = ИнтеграцияИСМП.ОтправитьДанныеВСервис(URLЗапроса, ТелоЗапросаJSON, Неопределено, "POST", ПараметрыСУЗ, ЗаголовокHTTP, HTTPОтветЭмуляция);
			
			ВозвращаемоеЗначение.РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
			// Эмуляция
			
			// Структура - Ожидаемое результат запроса:
			//  * status - Строка - Статус
			//  * omsConnection - Строка - Идентификатор соединения
			ВозвращаемоеЗначение.СтатусОбработки = ИнтерфейсСУЗСлужебный.СтатусОбработкиЗапросаНаПолучениеИдентификатораСоединения(РезультатБИП.ДанныеОтвета["status"]);
			
			Если ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаВыполнена Тогда
				ВозвращаемоеЗначение.ИдентификаторСоединения = РезультатБИП.ДанныеОтвета["omsConnection"];
			Иначе
				ВозвращаемоеЗначение.ТекстОшибки = РезультатБИП.ДанныеОтвета["rejectionReason"];
			КонецЕсли;
			
		Иначе
			
			Тело = СтрШаблон(
				НСтр("ru = 'Код ошибки: %1
				           |Текст ошибки: %2'"),
				РезультатБИП.КодОшибки,
				РезультатБИП.СообщениеОбОшибке);
			
			// Эмуляция
			HTTPОтветЭмуляция = Новый Структура;
			HTTPОтветЭмуляция.Вставить("Заголовки",    Новый Соответствие());
			HTTPОтветЭмуляция.Вставить("КодСостояния", 500);
			HTTPОтветЭмуляция.Вставить("Тело",         Тело);
			
			РезультатЗапроса = ИнтеграцияИСМП.ОтправитьДанныеВСервис(URLЗапроса, ТелоЗапросаJSON, Неопределено, "POST", ПараметрыСУЗ, ЗаголовокHTTP, HTTPОтветЭмуляция);
			
			ВозвращаемоеЗначение.РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
			// Эмуляция
			
			ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
			
			Если РезультатБИП.КодОшибки = "НеверныйФорматЗапроса"
				Или РезультатБИП.КодОшибки = "НеверныйЛогинИлиПароль"
				Или РезультатБИП.КодОшибки = "ПревышеноКоличествоПопыток"
				Или РезультатБИП.КодОшибки = "ОшибкаПодключения"
				Или РезультатБИП.КодОшибки = "ОшибкаСервиса" Тогда
				
				ВозвращаемоеЗначение.ТекстОшибки = СтрШаблон(
					НСтр("ru='Запрос на получение идентификатора соединения не может быть обработан сервисом интернет-поддержки.
						     |Текст ошибки: %1.'"),
					РезультатБИП.СообщениеОбОшибке);
				
			Иначе
				
				ВозвращаемоеЗначение.ТекстОшибки = СтрШаблон(
					НСтр("ru='Запрос на получение идентификатора соединения не выполнен.
						     |Текст ошибки: %1.'"),
					РезультатБИП.СообщениеОбОшибке);	
				
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли РежимРаботыСТестовымКонтуромИСМП Тогда
		
		ЗаголовокHTTP.Вставить("X-RegistrationKey", "4344d884-7f21-456c-981e-cd68e92391e8");
		
		РезультатЗапроса = ИнтеграцияИСМП.ОтправитьДанныеВСервис(URLЗапроса, ТелоЗапросаJSON, Неопределено, "POST", ПараметрыСУЗ, ЗаголовокHTTP);
		
		РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
		
		ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
		
		Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
			
			Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
				
				// Структура - Ожидаемое результат запроса:
				//  * status - Строка - Статус
				//  * omsConnection - Строка - Идентификатор соединения
				ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
				Если ДанныеОбработки = Неопределено Тогда
					
					ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
					
					ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
						URLЗапроса,
						РезультатОтправкиЗапроса);
					
				Иначе
					
					ВозвращаемоеЗначение.СтатусОбработки = ИнтерфейсСУЗСлужебный.СтатусОбработкиЗапросаНаПолучениеИдентификатораСоединения(ДанныеОбработки.status);
					
					Если ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаВыполнена Тогда
						ВозвращаемоеЗначение.ИдентификаторСоединения = ДанныеОбработки.omsConnection;
					Иначе
						ВозвращаемоеЗначение.ТекстОшибки = ДанныеОбработки.rejectionReason;
					КонецЕсли;
					
				КонецЕсли;
				
			Иначе
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаОтклонена;
				
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			КонецЕсли;
			
		Иначе
			
			ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			
		КонецЕсли;
		
	Иначе
		
		ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
		
		ВозвращаемоеЗначение.ТекстОшибки = НСтр("ru = 'Идентификатор соединения может быть получен только через ЛК СУЗ'");
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ЗапроситьМестаОсуществленияДеятельности(ВидПродукции, ПараметрыСУЗ, ТелоЗапросаJSON) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", Ложь);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("Операция",                       Перечисления.ВидыОперацийИСМП.ЗапросМестОсуществленияДеятельности);
	ВозвращаемоеЗначение.Вставить("СтатусОбработки",                Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");

	ТокенДоступа = ТокенДоступаПоПараметрамСУЗ(ПараметрыСУЗ);

	Если ТокенДоступа = Неопределено Тогда
		ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;

	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Content-Type",   "application/json");
	ЗаголовокHTTP.Вставить("Accept",         "application/json");
	ЗаголовокHTTP.Вставить("Accept-Charset", "utf-8");
	ЗаголовокHTTP.Вставить("clientToken",    ТокенДоступа);
	
	URLЗапроса = СтрШаблон(
		URLЗапроса("api/v2/extension/mod?omsId=%1", ВидПродукции),
		ПараметрыСУЗ.Идентификатор);

	РезультатЗапроса = ИнтеграцияИСМП.ОтправитьДанныеВСервис(URLЗапроса, ТелоЗапросаJSON, Неопределено, "GET", ПараметрыСУЗ, ЗаголовокHTTP);

	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);

	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;

	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда

		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда

			ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
			Если ДанныеОбработки = Неопределено Тогда

				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;

				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);

			Иначе

				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаВыполнена;

			КонецЕсли;

		Иначе

			ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаОтклонена;
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			
		КонецЕсли;
		
	Иначе
		
		ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
		
		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ЗакрытьПодзаказПоGTIN_V2(ПараметрыЗакрытия, ВидПродукции, ПараметрыСУЗ) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", Ложь);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("Операция",                       Перечисления.ВидыОперацийИСМП.ЗакрытиеПодзаказаНаЭмиссиюКодовМаркировкиСУЗ);
	ВозвращаемоеЗначение.Вставить("СтатусОбработки",                Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	ВозвращаемоеЗначение.Вставить("ИдентификаторЗаявки",            Неопределено);

	ТокенДоступа = ТокенДоступаПоПараметрамСУЗ(ПараметрыСУЗ);
	
	Если ТокенДоступа = Неопределено Тогда
		ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Content-Type",   "x-www-form-urlencoded;charset=UTF-8");
	ЗаголовокHTTP.Вставить("Accept",         "application/json");
	ЗаголовокHTTP.Вставить("Accept-Charset", "utf-8");
	ЗаголовокHTTP.Вставить("clientToken",    ТокенДоступа);
	
	Если ПараметрыЗакрытия.Свойство("GTIN") Тогда
		URLЗапроса = СтрШаблон(
			URLЗапроса_V2("api/v2/extension/buffer/close?orderId=%1&gtin=%2&omsId=%3&lastBlockId=%4", ВидПродукции),
			ПараметрыЗакрытия.ИдентификаторБизнесЗаказа,
			ПараметрыЗакрытия.GTIN,
			ПараметрыСУЗ.Идентификатор,
			ПараметрыЗакрытия.ИдентификаторПоследнегоБлокаКодов);
	Иначе
		URLЗапроса = СтрШаблон(
			URLЗапроса_V2("api/v2/extension/buffer/close?orderId=%1&omsId=%2", ВидПродукции),
			ПараметрыЗакрытия.ИдентификаторБизнесЗаказа,
			ПараметрыСУЗ.Идентификатор);
	КонецЕсли;

	РезультатЗапроса = ИнтеграцияИСМП.ОтправитьДанныеВСервис(URLЗапроса, Неопределено, Неопределено, "POST", ПараметрыСУЗ, ЗаголовокHTTP);
	
	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	КодыОшибокЗаказЗакрытИлиОтсутствует = Новый Массив;
	КодыОшибокЗаказЗакрытИлиОтсутствует.Добавить(3010);
	КодыОшибокЗаказЗакрытИлиОтсутствует.Добавить(3360);

	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			// Структура - Ожидаемое результат запроса:
			//  * omsId - GUID - Идентификатор СУЗ
			ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
			Если ДанныеОбработки = Неопределено Тогда
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			Иначе
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаВыполнена;
				
			КонецЕсли;
			
		Иначе
			
			КодыОшибокЗаказЗакрытИлиОтсутствует = Новый Массив;
			КодыОшибокЗаказЗакрытИлиОтсутствует.Добавить(3010);
			КодыОшибокЗаказЗакрытИлиОтсутствует.Добавить(3360);

			ЗаказЗакрытРанееИлиНеНайден = Ложь;
			Если РезультатОтправкиЗапроса.КодСостояния = 400 Тогда
				ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
				Если ДанныеОбработки <> Неопределено
					И ДанныеОбработки.Свойство("globalErrors") Тогда
					Для Каждого СтрокаОшибка Из ДанныеОбработки.globalErrors Цикл
						Если СтрокаОшибка.Свойство("errorCode")
							И КодыОшибокЗаказЗакрытИлиОтсутствует.Найти(СтрокаОшибка.errorCode) <> Неопределено Тогда
							ЗаказЗакрытРанееИлиНеНайден = Истина;
							Прервать;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;

			Если ЗаказЗакрытРанееИлиНеНайден Тогда
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаВыполнена;
			Иначе
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаОтклонена;
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
			КонецЕсли;

		КонецЕсли;

	Иначе

		ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;

		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);

	КонецЕсли;

	Возврат ВозвращаемоеЗначение;

КонецФункции

Функция ЗакрытьЗаказНаЭмиссию_V3(ПараметрыЗакрытия, ВидПродукции, ПараметрыСУЗ, ТелоЗапросаJSON) Экспорт

	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", Ложь);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("Операция",                       Перечисления.ВидыОперацийИСМП.ЗакрытиеЗаказаНаЭмиссиюСУЗ);
	ВозвращаемоеЗначение.Вставить("СтатусОбработки",                Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	ВозвращаемоеЗначение.Вставить("ИдентификаторЗаявки",            Неопределено);

	ТокенДоступа = ТокенДоступаПоПараметрамСУЗ(ПараметрыСУЗ);

	Если ТокенДоступа = Неопределено Тогда
		ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;

	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Content-Type",   "application/json");
	ЗаголовокHTTP.Вставить("Accept",         "application/json");
	ЗаголовокHTTP.Вставить("Accept-Charset", "utf-8");
	ЗаголовокHTTP.Вставить("clientToken",    ТокенДоступа);

	URLЗапроса = СтрШаблон(
		URLЗапроса("api/v2/extension/orders/close?omsId=%1", ВидПродукции),
		ПараметрыСУЗ.Идентификатор);

	РезультатЗапроса = ИнтеграцияИСМП.ОтправитьДанныеВСервис(URLЗапроса, ТелоЗапросаJSON, Неопределено, "POST", ПараметрыСУЗ, ЗаголовокHTTP);

	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);

	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;

	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда

		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда

			// Структура - Ожидаемое результат запроса:
			//  * omsId - GUID - Идентификатор СУЗ
			ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
			Если ДанныеОбработки = Неопределено Тогда

				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;

				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);

			Иначе

				ВозвращаемоеЗначение.ИдентификаторЗаявки = ДанныеОбработки.reportId;
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаПринята;

			КонецЕсли;

		Иначе

			КодыОшибокЗаказЗакрытИлиОтсутствует = Новый Массив;
			КодыОшибокЗаказЗакрытИлиОтсутствует.Добавить(3010);
			КодыОшибокЗаказЗакрытИлиОтсутствует.Добавить(3360);

			ЗаказЗакрытРанееИлиНеНайден = Ложь;
			Если РезультатОтправкиЗапроса.КодСостояния = 400 Тогда
				ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
				Если ДанныеОбработки.Свойство("errorCode")
					И КодыОшибокЗаказЗакрытИлиОтсутствует.Найти(ДанныеОбработки.errorCode) <> Неопределено Тогда
					ЗаказЗакрытРанееИлиНеНайден = Истина;
				КонецЕсли;
			КонецЕсли;

			Если ЗаказЗакрытРанееИлиНеНайден Тогда
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаВыполнена;
			Иначе
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаОтклонена;
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
		
		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ЗапроситьКодыМаркировкиИзБизнесЗаказа_V2(ПараметрыЗапроса, КоличествоКодовМаркировки, ИдентификаторБлокаКодов, ЭлементОчереди, ПараметрыСУЗ) Экспорт
	
	ВидПродукции = ЭлементОчереди.ВидПродукции;
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", Ложь);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("Операция",                       ЭлементОчереди.Операция);
	ВозвращаемоеЗначение.Вставить("СтатусОбработки",                Неопределено);
	ВозвращаемоеЗначение.Вставить("ДанныеБлока",                    Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	
	ТокенДоступа = ТокенДоступаПоПараметрамСУЗ(ПараметрыСУЗ);
	
	Если ТокенДоступа = Неопределено Тогда
		ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Content-Type",   "application/x-www-form-urlencoded;charset=UTF-8");
	ЗаголовокHTTP.Вставить("Accept",         "application/json");
	ЗаголовокHTTP.Вставить("Accept-Charset", "utf-8");
	ЗаголовокHTTP.Вставить("clientToken",    ТокенДоступа);
	
	ПараметрыURL = Новый Массив;
	ПараметрыURL.Добавить(СтрШаблон("omsId=%1",   ПараметрыСУЗ.Идентификатор));
	ПараметрыURL.Добавить(СтрШаблон("orderId=%1", ПараметрыЗапроса.ИдентификаторБизнесЗаказа));

	Если ЭлементОчереди.Операция = Перечисления.ВидыОперацийИСМП.ПовторноеПолучениеКодовМаркировкиИзСУЗ Тогда

		ПараметрыURL.Добавить(СтрШаблон("gtin=%1",    ПараметрыЗапроса.GTIN));
		ПараметрыURL.Добавить(СтрШаблон("blockId=%1", ИдентификаторБлокаКодов));
		
		URLЗапроса = СтрШаблон(
			URLЗапроса("api/v2/extension/codes/retry%1", ВидПродукции),
			ИнтерфейсИСМП.ПараметрыЗапроса(ПараметрыURL));
	Иначе
		
		Если ВидПродукции = Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха Тогда
			ЗаголовокHTTP.Вставить("Content-Type", "application/json");
			Если ЗначениеЗаполнено(ИдентификаторБлокаКодов) Тогда
				ПараметрыURL.Добавить(СтрШаблон("since=%1", Формат(ИдентификаторБлокаКодов, "ЧГ=0;")));
			КонецЕсли;
			URLЗапроса = СтрШаблон(
				URLЗапроса("api/v2/extension/codes%1", ВидПродукции),
				ИнтерфейсИСМП.ПараметрыЗапроса(ПараметрыURL));
		Иначе
			ПараметрыURL.Добавить(СтрШаблон("gtin=%1",     ПараметрыЗапроса.GTIN));
			ПараметрыURL.Добавить(СтрШаблон("quantity=%1", Формат(КоличествоКодовМаркировки, "ЧГ=0;")));
			Если ЗначениеЗаполнено(ИдентификаторБлокаКодов) Тогда
				ПараметрыURL.Добавить(СтрШаблон("lastBlockId=%1", ИдентификаторБлокаКодов));
			КонецЕсли;

			URLЗапроса = СтрШаблон(
				URLЗапроса("api/v2/extension/codes%1", ВидПродукции),
				ИнтерфейсИСМП.ПараметрыЗапроса(ПараметрыURL));
		КонецЕсли;
		
	КонецЕсли;
	
	РезультатЗапроса = ИнтеграцияИСМП.ПолучитьДанныеИзСервиса(URLЗапроса, Неопределено, ПараметрыСУЗ, ЗаголовокHTTP);
	
	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			// Структура - Ожидаемое результат запроса:
			//  * omsId - GUID - Идентификатор СУЗ
			//  * codes - Массиз Из Строка - Полученные коды маркировки
			//  * blockId - Строка - Идентификатор пакета кодов маркировки
			ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
			Если ДанныеОбработки = Неопределено Тогда
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
				
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			Иначе
				
				ДанныеБлока = Новый Структура;
				ДанныеБлока.Вставить("КодыМаркировки",                    Новый Массив());
				ДанныеБлока.Вставить("ИдентификаторПоследнегоБлокаКодов", Неопределено);
				ДанныеБлока.Вставить("ПараметрыКодовМаркировки",          Новый Соответствие());

				Если ВидПродукции = Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха Тогда

					ДанныеБлока.ИдентификаторПоследнегоБлокаКодов = ДанныеОбработки.since;

					Для Каждого СтрокаСведений Из ДанныеОбработки.items Цикл
						Для Каждого ЭлементЗаявки Из СтрокаСведений.products Цикл
							Для Каждого СтрокаКода Из ЭлементЗаявки.codes Цикл

								ПараметрыКодаМаркировки = Новый Структура();
								ПараметрыКодаМаркировки.Вставить("GTIN",               "");
								ПараметрыКодаМаркировки.Вставить("SGTIN",              Неопределено);
								ПараметрыКодаМаркировки.Вставить("ДатаЭмиссии",        ПрочитатьДатуJSON(СтрокаСведений.emitDateTime, ФорматДатыJSON.ISO));
								ПараметрыКодаМаркировки.Вставить("RFIDTID",            СтрокаКода.tid);
								ПараметрыКодаМаркировки.Вставить("ВидКИЗ",             ИнтерфейсСУЗСлужебный.ВидКИЗ(ЭлементЗаявки.identificationType));
								ПараметрыКодаМаркировки.Вставить("РазмерКИЗ",          ИнтерфейсСУЗСлужебный.РазмерКИЗ(ЭлементЗаявки.sizeType));
								ПараметрыКодаМаркировки.Вставить("СпособВводаВОборот", ИнтерфейсСУЗСлужебный.СпособВыпускаВОборот(ЭлементЗаявки.releaseMethodType));
								ПараметрыКодаМаркировки.Вставить("КодТНВЭД",           ЭлементЗаявки.tnvedCode);
								Если СтрокаКода.Свойство("gtin",  ПараметрыКодаМаркировки.GTIN) Тогда
									СтрокаКода.Свойство("serial", ПараметрыКодаМаркировки.SGTIN);
								Иначе
									ПараметрыКодаМаркировки.GTIN = "";
								КонецЕсли;

								ДанныеБлока.КодыМаркировки.Добавить(СтрокаКода.number);
								ДанныеБлока.ПараметрыКодовМаркировки.Вставить(СтрокаКода.number, ПараметрыКодаМаркировки);

							КонецЦикла;
						КонецЦикла;
					КонецЦикла;
				Иначе
					ДанныеБлока.КодыМаркировки                    = ДанныеОбработки.codes;
					ДанныеБлока.ИдентификаторПоследнегоБлокаКодов = ДанныеОбработки.blockId;
				КонецЕсли;
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаВыполнена;
				
				ВозвращаемоеЗначение.ДанныеБлока = ДанныеБлока;
				
			КонецЕсли;
			
		Иначе
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			
			Если СтрНайти(ВозвращаемоеЗначение.ТекстОшибки, "Billing response didn't received in time. Close request sent") Тогда
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаОбрабатывается;
			ИначеЕсли Не ЗначениеЗаполнено(СокрЛП(ВозвращаемоеЗначение.ТекстОшибки)) Тогда
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаОбрабатывается;
			Иначе
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
		
		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ЗапроситьСписокИдентификаторовПакетовБизнесЗаказа_V2(ПараметрыЗапроса, ВидПродукции, ПараметрыСУЗ) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", Ложь);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("Операция",                       Перечисления.ВидыОперацийИСМП.ПолучениеКодовМаркировкиИзСУЗ);
	ВозвращаемоеЗначение.Вставить("СтатусОбработки",                Неопределено);
	ВозвращаемоеЗначение.Вставить("ДанныеБлоков",                   Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	
	ТокенДоступа = ТокенДоступаПоПараметрамСУЗ(ПараметрыСУЗ);
	
	Если ТокенДоступа = Неопределено Тогда
		ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Content-Type",   "application/x-www-form-urlencoded;charset=UTF-8");
	ЗаголовокHTTP.Вставить("Accept",         "application/json");
	ЗаголовокHTTP.Вставить("Accept-Charset", "utf-8");
	ЗаголовокHTTP.Вставить("clientToken",    ТокенДоступа);
	
	ПараметрыURL = Новый Массив;
	ПараметрыURL.Добавить(СтрШаблон("omsId=%1",    ПараметрыСУЗ.Идентификатор));
	ПараметрыURL.Добавить(СтрШаблон("orderId=%1",  ПараметрыЗапроса.ИдентификаторБизнесЗаказа));
	ПараметрыURL.Добавить(СтрШаблон("gtin=%1",     ПараметрыЗапроса.GTIN));
	
	URLЗапроса = СтрШаблон(
		URLЗапроса_V2("api/v2/extension/codes/blocks?%1", ВидПродукции),
		СтрСоединить(ПараметрыURL, "&"));
	
	РезультатЗапроса = ИнтеграцияИСМП.ПолучитьДанныеИзСервиса(URLЗапроса, Неопределено, ПараметрыСУЗ, ЗаголовокHTTP);
	
	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			// Структура - Ожидаемое результат запроса:
			//  * omsId - GUID - Идентификатор СУЗ
			//  * codes - Массиз Из Строка - Полученные коды маркировки
			//  * blockId - Строка - Идентификатор пакета кодов маркировки
			ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
			Если ДанныеОбработки = Неопределено Тогда
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
				
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			Иначе
				
				БлокиКодов = Новый Массив;
				
				Если ДанныеОбработки.Свойство("blocks") Тогда
					Для Каждого СтрокаБлока Из ДанныеОбработки.blocks Цикл
						
						БлокКодов = Новый Структура();
						БлокКодов.Вставить("ИдентификаторБлока", СтрокаБлока.blockId);
						БлокКодов.Вставить("Количество",         СтрокаБлока.quantity);
						БлокКодов.Вставить("ДатаСоздания",       ИнтеграцияИС.ДатаИзСтрокиUNIX(СтрокаБлока.blockDateTime));
						
						БлокиКодов.Добавить(БлокКодов);
						
					КонецЦикла;
				КонецЕсли;
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаВыполнена;
				
				ВозвращаемоеЗначение.ДанныеБлоков = БлокиКодов;
				
			КонецЕсли;
			
		Иначе
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			
			Если СтрНайти(ВозвращаемоеЗначение.ТекстОшибки, "Billing response didn't received in time. Close request sent") Тогда
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаОбрабатывается;
			ИначеЕсли Не ЗначениеЗаполнено(СокрЛП(ВозвращаемоеЗначение.ТекстОшибки)) Тогда
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаОбрабатывается;
			Иначе
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
		
		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция СтатусПулаКодовМаркировкиИзБизнесЗаказа_V2(ПараметрыЗапроса, ВидПродукции, ПараметрыСУЗ) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", Ложь);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("Операция",                       Перечисления.ВидыОперацийИСМП.ПолучениеСтатусаПулаКодовМаркировкиИзСУЗ);
	ВозвращаемоеЗначение.Вставить("СтатусОбработки",                Перечисления.СтатусыОбработкиСообщенийИСМП.ПустаяСсылка());
	ВозвращаемоеЗначение.Вставить("ДанныеПула",                     Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	
	ТокенДоступа = ТокенДоступаПоПараметрамСУЗ(ПараметрыСУЗ);
	
	Если ТокенДоступа = Неопределено Тогда
		ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Content-Type",   "application/x-www-form-urlencoded;charset=UTF-8");
	ЗаголовокHTTP.Вставить("Accept",         "application/json");
	ЗаголовокHTTP.Вставить("Accept-Charset", "utf-8");
	ЗаголовокHTTP.Вставить("clientToken",    ТокенДоступа);
	
	URLЗапроса = СтрШаблон(
		URLЗапроса_V2("api/v2/extension/buffer/status?omsId=%1&orderId=%2&gtin=%3", ВидПродукции),
		ПараметрыСУЗ.Идентификатор,
		ПараметрыЗапроса.ИдентификаторБизнесЗаказа,
		ПараметрыЗапроса.GTIN);
	
	РезультатЗапроса = ИнтеграцияИСМП.ПолучитьДанныеИзСервиса(URLЗапроса, Неопределено, ПараметрыСУЗ, ЗаголовокHTTP);
	
	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			// Структура - Ожидаемое результат запроса:
			//  * availableCodes - Число - Общее количество доступных кодов маркировки для товара в буфере и пулах регистратора
			//  * bufferStatus - Строка - Статус буфера
			//  * gtin - Строка - GTIN
			//  * leftInBuffer - Число - Количество неиспользованных кодов маркировки (локальный буфер)
			//  * omsId - Строка - Уникальный идентификатор СУЗ
			//  * orderId - Строка - Уникальный идентификатор заказа на эмиссию кодов маркировки
			//  * poolInfos - Массив Из Структура - Массив пулов, созданных для буфера:
			//     ** isRegistrarReady - Булево - Готовность РЭ
			//     ** lastRegistrarErrorTimestamp - Число - Метка времени, последней наблюдавшейся ошибки РЭ
			//     ** leftInRegistrar - Число - Оставшееся количество КМ в пуле
			//     ** quantity - Число - Заказанное количество КМ в пуле
			//     ** registrarErrorCount - Число - Количество ошибок РЭ
			//     ** registrarId - Строка - Идентификатор РЭ (номер)
			//     ** rejectionReason - Строка - Причина отказа
			//     ** status - Строка - Статус пула КМ
			//  * poolsExhausted - Булево - Пулы кодов маркировки в регистраторах исчерпаны
			//  * rejectionReason - Строка - Причина отклонения буфера СУЗ-ом
			//  * totalCodes - Число - Заказанное количество кодов маркировки в заказе
			//  * totalPassed - Число - Суммарное количество кодов маркировки полученных из буфера
			//  * unavailableCodes - Строка - Идентификатор пакета кодов маркировки
			ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
			Если ДанныеОбработки = Неопределено Тогда
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
				
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			Иначе
				
				Статус = ИнтерфейсСУЗСлужебный.СтатусПулаКодовМаркировки_V2(ДанныеОбработки.bufferStatus);
				
				ДанныеПула = Новый Структура;
				ДанныеПула.Вставить("GTIN",       ДанныеОбработки.gtin);
				ДанныеПула.Вставить("Количество", ДанныеОбработки.totalCodes);
				ДанныеПула.Вставить("Остаток",    ДанныеОбработки.leftInBuffer);
				ДанныеПула.Вставить("Статус",     Статус);
				
				Если Статус = Перечисления.СтатусыПулаКодовМаркировкиСУЗ.Отклонен Тогда
					
					ВозвращаемоеЗначение.ТекстОшибки = ДанныеОбработки.rejectionReason;
					
				ИначеЕсли Статус = Перечисления.СтатусыПулаКодовМаркировкиСУЗ.Ошибка Тогда
					
					Ошибки = Новый Массив;
					Ошибки.Добавить(ДанныеОбработки.rejectionReason);
					
					Для Каждого ЭлементДанных Из ДанныеОбработки.poolInfos Цикл
						Если Не ЗначениеЗаполнено(ЭлементДанных.rejectionReason) Тогда
							Продолжить;
						КонецЕсли;
						Ошибки.Добавить(
							СтрШаблон(НСтр("ru = 'Регистратор %1: %2'"),
							ЭлементДанных.registrarId, ЭлементДанных.rejectionReason));
					КонецЦикла;
					
					ВозвращаемоеЗначение.ТекстОшибки = СтрСоединить(Ошибки, Символы.ПС);
					
				КонецЕсли;
				
				Если Статус = Перечисления.СтатусыПулаКодовМаркировкиСУЗ.Обрабатывается Тогда
					ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаОбрабатывается;
				ИначеЕсли Статус = Перечисления.СтатусыПулаКодовМаркировкиСУЗ.Ошибка
					Или Статус = Перечисления.СтатусыПулаКодовМаркировкиСУЗ.Отклонен Тогда
					ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаОтклонена;
				Иначе
					ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаВыполнена;
				КонецЕсли;
				
				ВозвращаемоеЗначение.ДанныеПула = ДанныеПула;
				
			КонецЕсли;
			
		Иначе
			
			ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			
		КонецЕсли;
		
	Иначе
		
		ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
		
		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ПроверитьДоступностьСУЗ_V2(ВидПродукции, ПараметрыСУЗ) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", Ложь);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("Доступен",                       Ложь);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	
	ТокенДоступа = ТокенДоступаПоПараметрамСУЗ(ПараметрыСУЗ);
	
	Если ТокенДоступа = Неопределено Тогда
		ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	КодыОшибокОбновленияКлючаСессии = Новый Массив;
	
	Если ЗначениеЗаполнено(ПараметрыСУЗ.ИдентификаторСоединения) Тогда
		КодыОшибокОбновленияКлючаСессии.Добавить(1100);
	КонецЕсли;
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Content-Type", "application/x-www-form-urlencoded;charset=UTF-8");
	ЗаголовокHTTP.Вставить("Accept",       "application/json");
	ЗаголовокHTTP.Вставить("clientToken",  ТокенДоступа);
	
	Если ВидПродукции = Неопределено Тогда
		ВидПродукции = Перечисления.ВидыПродукцииИС.Обувь;
		ПодключенныеВидыПродукции = ИнтеграцияИСМПКлиентСерверПовтИсп.ВидыПродукцииОбязательнойМаркировки(Неопределено);
		Если ПодключенныеВидыПродукции.Количество() Тогда
			ВидПродукции = ПодключенныеВидыПродукции[0];
		КонецЕсли;
	КонецЕсли;
	
	Если ВидПродукции = Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха Тогда
		ЗаголовокHTTP.Вставить("Content-Type", "application/json");
		URLЗапроса = СтрШаблон(
			URLЗапроса_V2("api/v3/extension/ping?omsId=%1", ВидПродукции),
			ПараметрыСУЗ.Идентификатор);
	Иначе
		URLЗапроса = СтрШаблон(
			URLЗапроса_V2("api/v2/extension/ping?omsId=%1", ВидПродукции),
			ПараметрыСУЗ.Идентификатор);
	КонецЕсли;

	РезультатЗапроса = ИнтеграцияИСМП.ПолучитьДанныеИзСервиса(URLЗапроса, Неопределено, ПараметрыСУЗ, ЗаголовокHTTP);
	
	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			// Структура - Ожидаемое результат запроса:
			//  * omsId - GUID - Идентификатор СУЗ
			ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
			Если ДанныеОбработки = Неопределено Тогда
				
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			Иначе
				
				ВозвращаемоеЗначение.Доступен = Истина;
				
			КонецЕсли;
			
			Возврат ВозвращаемоеЗначение;
			
		ИначеЕсли РезультатОтправкиЗапроса.КодСостояния = 400 Тогда
			
			ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
			
			Если ДанныеОбработки <> Неопределено Тогда
				Если ДанныеОбработки.Свойство("globalErrors") Тогда
					Для Каждого СтрокаОшибка Из ДанныеОбработки.globalErrors Цикл
						Если СтрокаОшибка.Свойство("errorCode")
							И КодыОшибокОбновленияКлючаСессии.Найти(СтрокаОшибка.errorCode) <> Неопределено Тогда
							ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
							Прервать;
						КонецЕсли;
					КонецЦикла;
				ИначеЕсли ДанныеОбработки.Свойство("errorCode")
					И КодыОшибокОбновленияКлючаСессии.Найти(ДанныеОбработки.errorCode) <> Неопределено Тогда
					ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
				КонецЕсли;
			КонецЕсли;
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			
		Иначе
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			
		КонецЕсли;
		
	Иначе
		
		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция СтатусЗаказаПоИдентификатору(ВидПродукции, ПараметрыСУЗ, ИдентификаторБизнесЗаказа) Экспорт

	ВозвращаемоеЗначение = СтатусыБизнесЗаказов_V2(ВидПродукции, ПараметрыСУЗ, ИдентификаторБизнесЗаказа);
	ВозвращаемоеЗначение.Вставить("Операция", Перечисления.ВидыОперацийИСМП.ПолучениеСтатусаПулаКодовМаркировкиИзСУЗ);
	ВозвращаемоеЗначение.Вставить("Статус",   Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.ПустаяСсылка());

	Если ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии Тогда
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;

	Если ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаВыполнена Тогда
		Для Каждого СтрокаЗаказа Из ВозвращаемоеЗначение.Список Цикл
			Если СтрокаЗаказа.ИдентификаторЗаявки <> ИдентификаторБизнесЗаказа Тогда
				Продолжить;
			КонецЕсли;
			ВозвращаемоеЗначение.Статус      = СтрокаЗаказа.Статус;
			ВозвращаемоеЗначение.ТекстОшибки = СтрокаЗаказа.ТекстОшибки;
			Если ВозвращаемоеЗначение.Статус = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.Ошибка Тогда
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаОтклонена;
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка Тогда
		ВозвращаемоеЗначение.Статус = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.Ошибка;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ВозвращаемоеЗначение.Статус) Тогда
		Если Не ЗначениеЗаполнено(ВозвращаемоеЗначение.ТекстОшибки) Тогда
			ВозвращаемоеЗначение.ТекстОшибки = СтрШаблон(
					НСтр("ru = 'Заказ %1 не найден в ГИС МТ.'"),
					ИдентификаторБизнесЗаказа);
		КонецЕсли;
		Если ВозвращаемоеЗначение.РезультатОтправкиЗапроса.ОтветПолучен Тогда
			ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(ВозвращаемоеЗначение.РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
			Если ДанныеОбработки <> Неопределено
				И ДанныеОбработки.Свойство("errorCode")
				И ДанныеОбработки.errorCode = 3540 Тогда
				ВозвращаемоеЗначение.КодСостояния = 404;
			КонецЕсли;
		КонецЕсли;
		ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
	КонецЕсли;

	Возврат ВозвращаемоеЗначение;

КонецФункции

Функция СтатусыБизнесЗаказов_V2(ВидПродукции, ПараметрыСУЗ, ИдентификаторЗаказа = Неопределено) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", Ложь);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("СтатусОбработки",                Неопределено);
	ВозвращаемоеЗначение.Вставить("Список",                         Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	ВозвращаемоеЗначение.Вставить("КодСостояния",                   Неопределено);

	ТокенДоступа = ТокенДоступаПоПараметрамСУЗ(ПараметрыСУЗ);
	
	Если ТокенДоступа = Неопределено Тогда
		ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Content-Type", "application/x-www-form-urlencoded;charset=UTF-8");
	ЗаголовокHTTP.Вставить("Accept",       "application/json");
	ЗаголовокHTTP.Вставить("clientToken",  ТокенДоступа);
	
	ПараметрыURL = Новый Массив;
	ПараметрыURL.Добавить(СтрШаблон("omsId=%1", ПараметрыСУЗ.Идентификатор));
	Если ЗначениеЗаполнено(ИдентификаторЗаказа) Тогда
		ПараметрыURL.Добавить(СтрШаблон("orderId=%1", ИдентификаторЗаказа));
	КонецЕсли;

	Если ВидПродукции = Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха Тогда
		ЗаголовокHTTP.Вставить("Content-Type", "application/json");
	КонецЕсли;

	URLЗапроса = СтрШаблон(
		URLЗапроса("api/v2/extension/orders%1", ВидПродукции),
		ИнтерфейсИСМП.ПараметрыЗапроса(ПараметрыURL));

	РезультатЗапроса = ИнтеграцияИСМП.ПолучитьДанныеИзСервиса(URLЗапроса, Неопределено, ПараметрыСУЗ, ЗаголовокHTTP);
	
	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		ВозвращаемоеЗначение.КодСостояния = РезультатОтправкиЗапроса.КодСостояния;

		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			// Структура - Ожидаемое результат запроса:
			//  * omsId - GUID - Идентификатор СУЗ
			//  * orderInfos - Массив Из Структура - Идентификатор отчета:
			//    ** orderId - Строка - Идентификатор заказа на эмиссию
			//    ** orderStatus - Строка - Статус
			//    ** buffers - Массив Из Структура - Массив информации о статусе буферов:
			//       *** isRegistrarReady - Булево - Готовность РЭ
			//       *** lastRegistrarErrorTimestamp - Число - Метка времени, последней наблюдавшейся ошибки РЭ
			//       *** leftInRegistrar - Число - Оставшееся количество КМ в пуле
			//       *** quantity - Число - Заказанное количество КМ в пуле
			//       *** registrarErrorCount - Число - Количество ошибок РЭ
			//       *** registrarId - Строка - Идентификатор РЭ (номер)
			//       *** rejectionReason - Строка - Причина отказа
			//       *** status - Строка - Статус пула КМ
			//    ** createdTimestamp - Число - Время создания заказа
			//    ** declineReason - Строка - Причина отклонения заказа
			ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
			Если ДанныеОбработки = Неопределено Тогда
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
				
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			Иначе
				
				Список = Новый Массив;
				
				Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеОбработки, "orderInfos") Тогда
					Для Каждого ЭлементДанных Из ДанныеОбработки.orderInfos Цикл
						
						Статус = ИнтерфейсСУЗСлужебный.СтатусБизнесЗаказа(ЭлементДанных.orderStatus);
						
						ДанныеПоЗаказуНаЭмиссию = Новый Структура;
						ДанныеПоЗаказуНаЭмиссию.Вставить("ИдентификаторЗаявки", ЭлементДанных.orderId);
						ДанныеПоЗаказуНаЭмиссию.Вставить("Статус",              Статус);
						Если Статус = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.Ошибка Тогда
							Если ЭлементДанных.Свойство("declineReason") Тогда
								ДанныеПоЗаказуНаЭмиссию.Вставить("ТекстОшибки", ЭлементДанных.declineReason);
							ИначеЕсли ЭлементДанных.Свойство("description") Тогда
								ДанныеПоЗаказуНаЭмиссию.Вставить("ТекстОшибки", ЭлементДанных.description);
							Иначе
								ДанныеПоЗаказуНаЭмиссию.Вставить("ТекстОшибки", НСтр("ru = 'Описание ошибки отсутствует.'"));
							КонецЕсли;
						Иначе
							ДанныеПоЗаказуНаЭмиссию.Вставить("ТекстОшибки", "");
						КонецЕсли;
						
						Список.Добавить(ДанныеПоЗаказуНаЭмиссию);
						
					КонецЦикла;
				КонецЕсли;
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаВыполнена;
				ВозвращаемоеЗначение.Список          = Список;
				
			КонецЕсли;
			
			Возврат ВозвращаемоеЗначение;
			
		Иначе
			
			ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			
		КонецЕсли;
		
	Иначе
		
		ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
		
		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция РезультатОбработкиОтчетаПоДаннымКвитанции(ИдентификаторОтчета, ВидПродукции, ИдентификаторСобытия, ПараметрыСУЗ) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", Ложь);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("Операция",                       Перечисления.ВидыОперацийИСМП.ПолучениеРезультатаОбработкиДокумента);
	ВозвращаемоеЗначение.Вставить("СтатусОбработки",                Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	ВозвращаемоеЗначение.Вставить("КодыСОшибками",                  Новый Массив);
	ВозвращаемоеЗначение.Вставить("КодОшибки",                      "");
	
	ТокенДоступа = ТокенДоступаПоПараметрамСУЗ(ПараметрыСУЗ);
	
	Если ТокенДоступа = Неопределено Тогда
		ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Content-Type", "application/json; charset=utf-8");
	ЗаголовокHTTP.Вставить("Accept",       "application/json");
	ЗаголовокHTTP.Вставить("clientToken",  ТокенДоступа);
	
	URLЗапроса = СтрШаблон(
		URLЗапроса_V2("api/v2/extension/receipts/document?omsId=%1&resultDocId=%2&docId=%3", ВидПродукции),
		ПараметрыСУЗ.Идентификатор,
		ИдентификаторОтчета,
		ИдентификаторСобытия);
	РезультатЗапроса = ИнтеграцияИСМП.ПолучитьДанныеИзСервиса(URLЗапроса, Неопределено, ПараметрыСУЗ, ЗаголовокHTTP);
	
	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			// Структура - Ожидаемое результат запроса:
			//  * content - Структура
			//  ** docType - Тип документа.
			//  ** errorReason - Причина ошибки при неудачной операции
			//  ** result - Результат обработки/получения документа. Перечень допустимых значений (См. пункт 11.3.3)
			ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
			Если ДанныеОбработки = Неопределено Тогда
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
				
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			Иначе
				
				СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаОбрабатывается;
				ТекстыОшибок  = Новый Соответствие;
				КодыСОшибками = Новый Массив;
				
				СодержимоеДокумента = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(ДанныеОбработки["content"], Истина);
				
				Если СодержимоеДокумента <> Неопределено
					И СодержимоеДокумента["cisList"] <> Неопределено
					И ТипЗнч(СодержимоеДокумента["cisList"]) = Тип("Массив") Тогда
					
					СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаВыполнена;
					Для Каждого СтрокаКодаМаркировки Из СодержимоеДокумента["cisList"] Цикл
						
						КодМаркировки = СтрокаКодаМаркировки["cis"];
						КодОшибки     = СтрокаКодаМаркировки["code"];
						ТекстОшибки   = СтрокаКодаМаркировки["description"];
						
						ПредставлениеОшибки = СокрЛП(СтрШаблон("%1: %2", КодОшибки, ТекстОшибки));
						Если ЗначениеЗаполнено(ТекстОшибки) И КодОшибки <> 0 Тогда
							КодыМаркировкиПоВидуТекстаОшибки = ТекстыОшибок[ПредставлениеОшибки];
							Если КодыМаркировкиПоВидуТекстаОшибки = Неопределено Тогда
								КодыМаркировкиПоВидуТекстаОшибки = Новый Массив;
							КонецЕсли;
							
							ДанныеРазбора = РазборКодаМаркировкиИССлужебный.РазобратьКодМаркировки(КодМаркировки, ВидПродукции);
							Если ДанныеРазбора <> Неопределено Тогда
								
								ПараметрыНормализации = РазборКодаМаркировкиИССлужебныйКлиентСервер.ПараметрыНормализацииКодаМаркировки();
								ПараметрыНормализации.ИмяСвойстваКодМаркировки = "Штрихкод";
								НормализованныйКодМаркировки = РазборКодаМаркировкиИССлужебныйКлиентСервер.НормализоватьКодМаркировки(
									ДанныеРазбора, ВидПродукции, ПараметрыНормализации);
								КодыМаркировкиПоВидуТекстаОшибки.Добавить(НормализованныйКодМаркировки);
								
								Если ВидПродукции = Перечисления.ВидыПродукцииИС.Табак Тогда
									
									ПараметрыНормализации.ВключатьМРЦ = Ложь;
									НормализованныйКодМаркировкиБезМРЦ = РазборКодаМаркировкиИССлужебныйКлиентСервер.НормализоватьКодМаркировки(
										ДанныеРазбора, ВидПродукции, ПараметрыНормализации);
									Если НормализованныйКодМаркировки <> НормализованныйКодМаркировкиБезМРЦ Тогда
										КодыСОшибками.Добавить(НормализованныйКодМаркировкиБезМРЦ);
									КонецЕсли;
									
								КонецЕсли;
								
							Иначе
								НормализованныйКодМаркировки = СтрЗаменить(КодМаркировки, Символ(29), "<GS>");
								КодыМаркировкиПоВидуТекстаОшибки.Добавить(НормализованныйКодМаркировки);
							КонецЕсли;
							
							ТекстыОшибок[ПредставлениеОшибки] = КодыМаркировкиПоВидуТекстаОшибки;
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЕсли;
				
				ВозвращаемоеЗначение.СтатусОбработки = СтатусОбработки;
				
				Если ТекстыОшибок.Количество() Тогда
					ТекстОшибки = Новый Массив;
					Для Каждого КлючИЗначение Из ТекстыОшибок Цикл
						ТекстОшибки.Добавить(КлючИЗначение.Ключ + " " + СтрСоединить(КлючИЗначение.Значение, ", "));
						Если КлючИЗначение.Значение <> Неопределено Тогда
							ОбщегоНазначенияКлиентСервер.ДополнитьМассив(КодыСОшибками, КлючИЗначение.Значение);
						КонецЕсли;
					КонецЦикла;
					ВозвращаемоеЗначение.ТекстОшибки = СтрСоединить(ТекстОшибки, Символы.ПС);
					ВозвращаемоеЗначение.КодыСОшибками = КодыСОшибками;
				КонецЕсли;
				
			КонецЕсли;
			
			Возврат ВозвращаемоеЗначение;
			
		ИначеЕсли РезультатОтправкиЗапроса.КодСостояния = 400 Тогда
			
			ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаОтклонена;
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
				
			ДанныеJSON = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
			Если ДанныеJSON <> Неопределено
				И ТипЗнч(ДанныеJSON) = Тип("Структура")
				И ДанныеJSON.Свойство("globalErrors")
				И ТипЗнч(ДанныеJSON.globalErrors) = Тип("Массив") Тогда
				
				Для Каждого СтрокаОшибки Из ДанныеJSON.globalErrors Цикл 
					Если ТипЗнч(СтрокаОшибки) = Тип("Структура") Тогда
						СтрокаОшибки.Свойство("errorCode", ВозвращаемоеЗначение.КодОшибки);
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
			КонецЕсли;
			
		Иначе
			
			ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			
		КонецЕсли;
		
	Иначе
		
		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция СтатусКвитанцииДокумента_V2(ИдентификаторОтчета, ВидПродукции, ПараметрыСУЗ) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", Ложь);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("Операция",                       Перечисления.ВидыОперацийИСМП.ПолучениеРезультатаОбработкиДокумента);
	ВозвращаемоеЗначение.Вставить("СтатусОбработки",                Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	ВозвращаемоеЗначение.Вставить("ИдентификаторСобытия",           "");
	ВозвращаемоеЗначение.Вставить("КодОшибки",                      "");
	
	ТокенДоступа = ТокенДоступаПоПараметрамСУЗ(ПараметрыСУЗ);
	
	Если ТокенДоступа = Неопределено Тогда
		ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Content-Type", "application/json; charset=utf-8");
	ЗаголовокHTTP.Вставить("Accept",       "application/json");
	ЗаголовокHTTP.Вставить("clientToken",  ТокенДоступа);
	
	URLЗапроса = СтрШаблон(
		URLЗапроса_V2("api/v2/extension/receipts/receipt?omsId=%1&resultDocId=%2", ВидПродукции),
		ПараметрыСУЗ.Идентификатор,
		ИдентификаторОтчета);
	РезультатЗапроса = ИнтеграцияИСМП.ПолучитьДанныеИзСервиса(URLЗапроса, Неопределено, ПараметрыСУЗ, ЗаголовокHTTP);
	
	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			// Структура - Ожидаемое результат запроса:
			//  * results - массив результатов
			//  ** state - Статус обработки квитанции.
			//  ** description - описание обработки квитанции.
			//  ** operations - массив - Список событий по документу в рамках транзакции.
			//  ** result - Результат обработки/получения документа. Перечень допустимых значений (См. пункт 11.3.3).
			//  *** operationType - Тип события - ожидаемый конечный тип события RNMS_GIS_PROCESSED.
			//  *** docId - Идентификатор связанного с квитанцией документа (документа, сформированного для события) - для запрос результата обработки по кодам.
			ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
			Если ДанныеОбработки = Неопределено Тогда
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
				
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			Иначе
				
				СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаОбрабатывается;
				
				Для Каждого ДанныеКвитанции Из ДанныеОбработки["results"] Цикл
					
					СтатусОбработки = ИнтерфейсСУЗСлужебный.РезультатОбработкиОтчетаСУЗ(ДанныеКвитанции["state"]);
					Если СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка
						Или СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаОбработанаЧастично Тогда	
						
						СобытияПоДокументуВРамкахТранзакции = ДанныеКвитанции["operations"];
						Для Каждого Событие Из СобытияПоДокументуВРамкахТранзакции Цикл
							Если Событие["operationType"] <> "RNMS_GIS_PROCESSED" Тогда
								Продолжить;
							КонецЕсли;
							ВозвращаемоеЗначение.ИдентификаторСобытия = Событие["docId"];
						КонецЦикла;
						ВозвращаемоеЗначение.ТекстОшибки = ДанныеКвитанции["description"];
						
					КонецЕсли;
					
				КонецЦикла;
				
				ВозвращаемоеЗначение.СтатусОбработки = СтатусОбработки;
				
			КонецЕсли;
			
			Возврат ВозвращаемоеЗначение;
			
		ИначеЕсли РезультатОтправкиЗапроса.КодСостояния = 400 Тогда
			
			ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаОтклонена;
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
				
			ДанныеJSON = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
			Если ДанныеJSON <> Неопределено
				И ТипЗнч(ДанныеJSON) = Тип("Структура")
				И ДанныеJSON.Свойство("globalErrors")
				И ТипЗнч(ДанныеJSON.globalErrors) = Тип("Массив") Тогда
				
				Для Каждого СтрокаОшибки Из ДанныеJSON.globalErrors Цикл 
					Если ТипЗнч(СтрокаОшибки) = Тип("Структура") Тогда
						СтрокаОшибки.Свойство("errorCode", ВозвращаемоеЗначение.КодОшибки);
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
			КонецЕсли;
			
		Иначе
			
			ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			
		КонецЕсли;
		
	Иначе
		
		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция СтатусОбработкиОтчета_V2(ИдентификаторОтчета, ВидПродукции, ПараметрыСУЗ, ВидОперации) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", Ложь);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("Операция",                       Перечисления.ВидыОперацийИСМП.ПолучениеРезультатаОбработкиДокумента);
	ВозвращаемоеЗначение.Вставить("СтатусОбработки",                Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	
	ТокенДоступа = ТокенДоступаПоПараметрамСУЗ(ПараметрыСУЗ);
	
	Если ТокенДоступа = Неопределено Тогда
		ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Content-Type", "application/json; charset=utf-8");
	ЗаголовокHTTP.Вставить("Accept",       "application/json");
	ЗаголовокHTTP.Вставить("clientToken",  ТокенДоступа);
	
	Если ВидПродукции = Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха Тогда
		URLЗапроса = СтрШаблон(
			URLЗапроса("api/v2/extension/reports?omsId=%1&reportId=%2", ВидПродукции),
			ПараметрыСУЗ.Идентификатор,
			ИдентификаторОтчета);
	Иначе
		URLЗапроса = СтрШаблон(
			URLЗапроса("api/v2/extension/report/info?omsId=%1&reportId=%2", ВидПродукции),
			ПараметрыСУЗ.Идентификатор,
			ИдентификаторОтчета);
	КонецЕсли;

	РезультатЗапроса = ИнтеграцияИСМП.ПолучитьДанныеИзСервиса(URLЗапроса, Неопределено, ПараметрыСУЗ, ЗаголовокHTTP);
	
	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			// Структура - Ожидаемое результат запроса:
			//  * omsId - GUID - Идентификатор СУЗ
			//  * reportId - GUID - Идентификатор отчета
			//  * reportStatus - Строка - Статус
			//  * errorReason - Строка - Причина отклонения отчета - заполняется, если reportStatus = REJECTED
			// Для ТГ Продукция из натурального меха
			//  * status - Строка - Статус
			//  * errorReason - Строка - Причина отклонения отчета - заполняется, если reportStatus = CHECKED_NOT_OK
			ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
			Если ДанныеОбработки = Неопределено Тогда
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
				
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			Иначе
				
				Если ВидПродукции = Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха Тогда
					Статус = ИнтерфейсСУЗСлужебный.СтатусОбработкиОтчета(ДанныеОбработки.status);
				ИначеЕсли ВидОперации = Перечисления.ВидыОперацийИСМП.ОтчетОВерификацииНанесенныхКМ Тогда
					Статус = ИнтерфейсСУЗСлужебный.СтатусОбработкиОтчетаОНанесении(ДанныеОбработки.reportStatus);
				Иначе
					Статус = ИнтерфейсСУЗСлужебный.СтатусОбработкиОтчета(ДанныеОбработки.reportStatus);
				КонецЕсли;
				
				Если Статус = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаОтклонена Тогда
					Если ДанныеОбработки.Свойство("errorReason") Тогда
						ВозвращаемоеЗначение.ТекстОшибки = ДанныеОбработки.errorReason;
					ИначеЕсли ДанныеОбработки.Свойство("description") Тогда
						ВозвращаемоеЗначение.ТекстОшибки = ДанныеОбработки.description;
					Иначе
						ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
							URLЗапроса,
							РезультатОтправкиЗапроса);
					КонецЕсли;
				КонецЕсли;
				
				ВозвращаемоеЗначение.СтатусОбработки = Статус;
				
			КонецЕсли;
			
			Возврат ВозвращаемоеЗначение;
			
		Иначе
			
			ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			
		КонецЕсли;
		
	Иначе
		
		ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
		
		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ЗапроситьСписокПродукции_V2(ПараметрыЗапроса, ВидПродукции, ПараметрыСУЗ) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", Ложь);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("СтатусОбработки",                Неопределено);
	ВозвращаемоеЗначение.Вставить("СписокПродукции",                Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	
	ТокенДоступа = ТокенДоступаПоПараметрамСУЗ(ПараметрыСУЗ);
	
	Если ТокенДоступа = Неопределено Тогда
		ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Content-Type", "application/json; charset=utf-8");
	ЗаголовокHTTP.Вставить("Accept",       "application/json");
	ЗаголовокHTTP.Вставить("clientToken",  ТокенДоступа);
	
	URLЗапроса = СтрШаблон(
		URLЗапроса_V2("api/v2/extension/product/info?omsId=%1", ВидПродукции),
		ПараметрыСУЗ.Идентификатор);
	РезультатЗапроса = ИнтеграцияИСМП.ПолучитьДанныеИзСервиса(URLЗапроса, Неопределено, ПараметрыСУЗ, ЗаголовокHTTP);
	
	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			// Структура - Ожидаемое результат запроса:
			//  * omsId - GUID - Идентификатор СУЗ
			//  * products - Массив Из Структура:
			//    ** gtin - Строка - GTIN
			//    ** name - Строка - Наименование продукции
			//    ** productAttributes - Структура - Свойства продукции
			ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
			Если ДанныеОбработки = Неопределено Тогда
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
				
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			Иначе
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаВыполнена;
				
				СписокПродукции = Новый Массив;
				Для Каждого ЭлементДанных Из ДанныеОбработки.products Цикл
					
					ДанныеПродукции = Новый Структура;
					ДанныеПродукции.Вставить("GTIN",         ЭлементДанных.gtin);
					ДанныеПродукции.Вставить("Наименование", ЭлементДанных.name);
					ДанныеПродукции.Вставить("Свойства",     ЭлементДанных.productAttributes);
					
					СписокПродукции.Добавить(ДанныеПродукции);
					
				КонецЦикла;
				
				ВозвращаемоеЗначение.СписокПродукции = СписокПродукции;
				
			КонецЕсли;
			
			Возврат ВозвращаемоеЗначение;
			
		Иначе
			
			ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			
		КонецЕсли;
		
	Иначе
		
		ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
		
		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ЗапроситьТокен_V2(ПараметрыЗапроса, ВидПродукции, ПараметрыСУЗ) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", Ложь);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("СтатусОбработки",                Неопределено);
	ВозвращаемоеЗначение.Вставить("Токен",                          Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	
	ТокенДоступа = ТокенДоступаПоПараметрамСУЗ(ПараметрыСУЗ);
	
	Если ТокенДоступа = Неопределено Тогда
		ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Content-Type", "application/json; charset=utf-8");
	ЗаголовокHTTP.Вставить("Accept",       "application/json");
	ЗаголовокHTTP.Вставить("clientToken",  ТокенДоступа);
	
	URLЗапроса = СтрШаблон(
		URLЗапроса_V2("api/v2/extension/token?omsId=%1&username=%2&password=%3", ВидПродукции),
		ПараметрыСУЗ.Идентификатор,
		ПараметрыЗапроса.ИмяПользователя,
		ПараметрыЗапроса.Пароль);
	РезультатЗапроса = ИнтеграцияИСМП.ПолучитьДанныеИзСервиса(URLЗапроса, Неопределено, ПараметрыСУЗ, ЗаголовокHTTP);
	
	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			// Структура - Ожидаемое результат запроса:
			//  * omsId - GUID - Идентификатор СУЗ
			//  * token - Строка - Токен
			ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
			Если ДанныеОбработки = Неопределено Тогда
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
				
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			Иначе
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаВыполнена;
				
				ВозвращаемоеЗначение.Токен = ДанныеОбработки.token;
				
			КонецЕсли;
			
			Возврат ВозвращаемоеЗначение;
			
		Иначе
			
			ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			
		КонецЕсли;
		
	Иначе
		
		ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
		
		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция СписокСервисПровайдеров_V2(ВидПродукции, ПараметрыСУЗ) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", Ложь);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("СтатусОбработки",                Неопределено);
	ВозвращаемоеЗначение.Вставить("Список",                         Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	
	ТокенДоступа = ТокенДоступаПоПараметрамСУЗ(ПараметрыСУЗ);
	
	Если ТокенДоступа = Неопределено Тогда
		ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Content-Type", "application/x-www-form-urlencoded;charset=UTF-8");
	ЗаголовокHTTP.Вставить("Accept",       "application/json");
	ЗаголовокHTTP.Вставить("clientToken",  ТокенДоступа);
	
	URLЗапроса = СтрШаблон(
		URLЗапроса_V2("api/v2/extension/providers?omsId=%1", ВидПродукции),
		ПараметрыСУЗ.Идентификатор);
	РезультатЗапроса = ИнтеграцияИСМП.ПолучитьДанныеИзСервиса(URLЗапроса, Неопределено, ПараметрыСУЗ, ЗаголовокHTTP);
	
	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
			Если ДанныеОбработки = Неопределено Тогда
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
				
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			Иначе
				
				Список = Новый Массив;
				
				Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеОбработки, "providers") Тогда
					Для Каждого ЭлементДанных Из ДанныеОбработки.providers Цикл
						
						Данные = Новый Структура;
						Данные.Вставить("Идентификатор", ЭлементДанных.serviceProviderId);
						Данные.Вставить("Наименование",  ЭлементДанных.Name);
						Данные.Вставить("ИНН",           ЭлементДанных.taxIdentificationNumber);
						Данные.Вставить("Страна",
							УправлениеКонтактнойИнформацией.СтранаМираПоКодуИлиНаименованию(ЭлементДанных.country));
						Данные.Вставить("ВидСервисПровайдера",
							ИнтерфейсСУЗСлужебный.СпособИзготовленияКодовМаркировки(ЭлементДанных.role));
						Данные.Вставить("ВидыПродукции", Новый Массив);
						
						Для Каждого ЭлементВидаПродукции Из ЭлементДанных.productGroups Цикл
							Если ЭлементВидаПродукции = "pharma" Тогда
								Продолжить;
							КонецЕсли;
							Данные.ВидыПродукции.Добавить(ИнтерфейсИСМПСлужебный.ТоварнаяГруппа(ЭлементВидаПродукции));
						КонецЦикла;
						
						Список.Добавить(Данные);
						
					КонецЦикла;
				КонецЕсли;
				
				ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаВыполнена;
				ВозвращаемоеЗначение.Список          = Список;
				
			КонецЕсли;
			
			Возврат ВозвращаемоеЗначение;
			
		Иначе
			
			ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			
		КонецЕсли;
		
	Иначе
		
		ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.Ошибка;
		
		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ВремяОжиданияСледующегоЗапросаСУЗ(НастройкаОбмена) Экспорт
	
	РазрешеннаяДатаСледующегоЗапроса = НастройкаОбмена.ДатаПоследнегоЗапроса + НастройкаОбмена.ИнтервалМеждуЗапросами;
	ВремяОжиданияСледующегоЗапроса = РазрешеннаяДатаСледующегоЗапроса - ТекущаяДатаСеанса();
	Если ВремяОжиданияСледующегоЗапроса < 0 Тогда
		ВремяОжиданияСледующегоЗапроса = 0;
	КонецЕсли;
	
	Возврат ВремяОжиданияСледующегоЗапроса;
	
КонецФункции

Функция ИнтервалПолученияСтатусаОбработкиОтчетаСУЗ() Экспорт

	ВремяОжиданияСледующегоЗапроса = 60;
	Возврат ВремяОжиданияСледующегоЗапроса;

КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТокенДоступаПоПараметрамСУЗ(ПараметрыСУЗ)
	
	Если ЗначениеЗаполнено(ПараметрыСУЗ.ИдентификаторСоединения) Тогда
		ТокенДоступа = ИнтерфейсАвторизацииИСМПСлужебный.ПроверитьОбновитьКлючСессии(
			ИнтерфейсИСМПКлиентСервер.ПараметрыЗапросаКлючаСессииСУЗ(ПараметрыСУЗ));
	Иначе
		ТокенДоступа = ПараметрыСУЗ.Токен;
	КонецЕсли;
	
	Возврат ТокенДоступа;
	
КонецФункции

Функция URLЗапроса(URLЗапроса, ВидПродукции)

	НовыйURLЗапроса = URLЗапроса;
	Если ВидПродукции = Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха Тогда
		НовыйURLЗапроса = СтрЗаменить(НовыйURLЗапроса, "api/v2/extension/", "api/v3/extension/");
	КонецЕсли;

	Возврат URLЗапроса_V2(НовыйURLЗапроса, ВидПродукции);

КонецФункции

Функция URLЗапроса_V2(URLЗапроса, ВидПродукцииИС)
	
	Если ВидПродукцииИС = Перечисления.ВидыПродукцииИС.Табак Тогда
		ВидПродукции = "tobacco";
	ИначеЕсли ВидПродукцииИС = Перечисления.ВидыПродукцииИС.Шины Тогда
		ВидПродукции = "tires";
	ИначеЕсли ВидПродукцииИС = Перечисления.ВидыПродукцииИС.Духи Тогда
		ВидПродукции = "perfum";
	ИначеЕсли ВидПродукцииИС = Перечисления.ВидыПродукцииИС.Фотоаппараты Тогда
		ВидПродукции = "photo";
	ИначеЕсли ВидПродукцииИС = Перечисления.ВидыПродукцииИС.МолочнаяПродукцияПодконтрольнаяВЕТИС
			Или ВидПродукцииИС = Перечисления.ВидыПродукцииИС.МолочнаяПродукцияБезВЕТИС Тогда
		ВидПродукции = "milk";
	ИначеЕсли ВидПродукцииИС = Перечисления.ВидыПродукцииИС.Велосипеды Тогда
		ВидПродукции = "bicycle";
	ИначеЕсли ВидПродукцииИС = Перечисления.ВидыПродукцииИС.КреслаКоляски Тогда
		ВидПродукции = "wheelchairs";
	ИначеЕсли ВидПродукцииИС = Перечисления.ВидыПродукцииИС.ЛегкаяПромышленность Тогда
		ВидПродукции = "lp";
	ИначеЕсли ВидПродукцииИС = Перечисления.ВидыПродукцииИС.Обувь Тогда
		ВидПродукции = "shoes";
	ИначеЕсли ВидПродукцииИС = Перечисления.ВидыПродукцииИС.АльтернативныйТабак Тогда
		ВидПродукции = "otp";
	ИначеЕсли ВидПродукцииИС = Перечисления.ВидыПродукцииИС.УпакованнаяВода Тогда
		ВидПродукции = "water";
	ИначеЕсли ВидПродукцииИС = Перечисления.ВидыПродукцииИС.Пиво Тогда
		ВидПродукции = "beer";
	ИначеЕсли ВидПродукцииИС = Перечисления.ВидыПродукцииИС.НикотиносодержащаяПродукция Тогда
		ВидПродукции = "ncp";
	ИначеЕсли ВидПродукцииИС = Перечисления.ВидыПродукцииИС.БАДы Тогда
		ВидПродукции = "bio";
	ИначеЕсли ВидПродукцииИС = Перечисления.ВидыПродукцииИС.Антисептики Тогда
		ВидПродукции = "antiseptic";
	ИначеЕсли ВидПродукцииИС = Перечисления.ВидыПродукцииИС.МорепродуктыПодконтрольныеВЕТИС Тогда
		ВидПродукции = "seafood";
	ИначеЕсли ВидПродукцииИС = Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха Тогда
		ВидПродукции = "furs";
	ИначеЕсли ВидПродукцииИС = Перечисления.ВидыПродукцииИС.СоковаяПродукция Тогда
		ВидПродукции = "softdrinks";
	ИначеЕсли ВидПродукцииИС = Перечисления.ВидыПродукцииИС.БезалкогольноеПиво Тогда
		ВидПродукции = "nabeer";
	Иначе
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Неизвестный вид продукции: %1'"),
			ВидПродукцииИС);
	КонецЕсли;
	
	Возврат СтрЗаменить(URLЗапроса, "extension", ВидПродукции);
	
КонецФункции

#КонецОбласти