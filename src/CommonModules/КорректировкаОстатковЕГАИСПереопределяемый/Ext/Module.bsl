#Область ПрограммныйИнтерфейс

// Формирует текст запроса на получение учетных остатков алкогольной продукции.
//   Ожидаемые действия:
//   * Создание временной таблицы "Остатки" с колонками
//     ** Номенклатура   - ОпределяемыйТип.Номенклатура
//     ** Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатуры
//     ** Серия          - ОпределяемыйТип.СерияНоменклатуры
//     ** Остаток        - Число
//     ** ОбъемДАЛ       - Число (объем ДАЛ единицы хранения номенклатуры, используется для неупакованной продукции для пересчета)
//   * Установка необходимых параметров запроса (например получение склада из организации ЕГАИС)
//
// Параметры:
//   Запрос            - Запрос - создаваемый запрос,
//   ОрганизацияЕГАИС  - СправочникСсылка.КлассификаторОрганизацийЕГАИС - ссылка сопоставленную собственной организации организацию ЕГАИС,
//   ТолькоМаркируемая - Булево - переключатель маркируемая/вся алкогольная продукция
//
Процедура ПриФормированииЗапросаУчетныхОстатков(Запрос, ОрганизацияЕГАИС, ТолькоМаркируемая) Экспорт
	
	Запрос.УстановитьПараметр("ТорговыйОбъект", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОрганизацияЕГАИС, "ТорговыйОбъект"));
	Запрос.УстановитьПараметр("ВключаяНемаркируемую", НЕ ТолькоМаркируемая);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	|	ТоварыНаСкладахОстатки.ХарактеристикаНоменклатуры КАК Характеристика,
	|	ТоварыНаСкладахОстатки.СерияНоменклатуры КАК Серия,
	|	ТоварыНаСкладахОстатки.КоличествоОстаток КАК Остаток
	|ПОМЕСТИТЬ ДанныеРегистров
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки(
	|			,
	|			Склад = &ТорговыйОбъект
	|				И Номенклатура.АлкогольнаяПродукция
	|				И НЕ Номенклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|				И (&ВключаяНемаркируемую
	|					ИЛИ ЕСТЬNULL(Номенклатура.ВидАлкогольнойПродукцииЕГАИС.Маркируемый, ЛОЖЬ))) КАК ТоварыНаСкладахОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТоварыВРозницеОстатки.Номенклатура,
	|	ТоварыВРозницеОстатки.ХарактеристикаНоменклатуры,
	|	ТоварыВРозницеОстатки.СерияНоменклатуры,
	|	ТоварыВРозницеОстатки.КоличествоОстаток
	|ИЗ
	|	РегистрНакопления.ТоварыВРознице.Остатки(
	|			,
	|			Склад = &ТорговыйОбъект
	|				И Номенклатура.АлкогольнаяПродукция
	|				И НЕ Номенклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|				И (&ВключаяНемаркируемую
	|					ИЛИ ЕСТЬNULL(Номенклатура.ВидАлкогольнойПродукцииЕГАИС.Маркируемый, ЛОЖЬ))) КАК ТоварыВРозницеОстатки
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРегистров.Номенклатура КАК Номенклатура,
	|	ДанныеРегистров.Характеристика КАК Характеристика,
	|	ДанныеРегистров.Серия КАК Серия,
	|	СУММА(ДанныеРегистров.Остаток) КАК Остаток,
	|	МАКСИМУМ(ДанныеРегистров.Номенклатура.ОбъемДАЛ) КАК ОбъемДАЛ
	|ПОМЕСТИТЬ Остатки
	|ИЗ
	|	ДанныеРегистров КАК ДанныеРегистров
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРегистров.Номенклатура,
	|	ДанныеРегистров.Характеристика,
	|	ДанныеРегистров.Серия
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Серия
	|;";
	
	Возврат;
	
КонецПроцедуры

// В процедуре необходимо определить значения переменных ЭтоСклад и ЭтоТорговыйЗал в зависимости от типа торгового объекта.
//
// Параметры:
//  ОрганизацияЕГАИС - ОпределяемыйТип.ТорговыйОбъектЕГАИС - ссылка на собственный торговый объект,
//  ЭтоСклад - Булево - признак того, что торговый объект является складским помещением. Выходной параметр,
//  ЭтоТорговыйЗал - Булево - признак того, что торговый объект является торговым залом. Выходной параметр.
//
Процедура ПриОпределенииТипаТорговогоОбъекта(ОрганизацияЕГАИС, ЭтоСклад, ЭтоТорговыйЗал) Экспорт
	
	Вид = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОрганизацияЕГАИС, Новый Структура("Вид","ТорговыйОбъект.ВидСклада")).Вид;
	
	ЭтоСклад = Вид = Перечисления.ВидыСкладов.Оптовый;
	ЭтоТорговыйЗал = Вид = Перечисления.ВидыСкладов.Розничный;
	
КонецПроцедуры

// Проверяет состояние инвентаризации по складу.
//   Можно вывести текущее состояние (проводится/не проводится) и рекомендации (провести инвентаризацию),
//   активировать доступность отчетов по расхождениям
//
// Параметры:
//   Организация - ОпределяемыйТип.ОрганизацияКонтрагентЕГАИС - ссылка на собственную организацию,
//   ТорговыйОбъект - ОпределяемыйТип.ТорговыйОбъектЕГАИС - ссылка на собственный торговый объект,
//   ТекстПроверкиСклад - Строка, ФорматированнаяСтрока - результат проверки инвентаризации на складе. Выходной параметр,
//   ТекстПроверкиТорговыйЗал - Строка, ФорматированнаяСтрока - результат проверки инвентаризации в торговом зале. Выходной параметр,
//   ОтчетПоРасхождениямСклад - Булево - флаг доступности формирования отчета по излишкам/недостачам (на складе). Выходной параметр,
//   ОтчетПоРасхождениямТорговыйЗал - Булево - флаг доступности ссылка на формирование отчета по излишкам/недостачам (в торговом зале). Выходной параметр.
//
Процедура ПриПроверкеИнвентаризации(Организация, ТорговыйОбъект, ТекстПроверкиСклад, ТекстПроверкиТорговыйЗал, ОтчетПоРасхождениямСклад, ОтчетПоРасхождениямТорговыйЗал) Экспорт
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти
