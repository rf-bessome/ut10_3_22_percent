// @strict-types

#Область СлужебныйПрограммныйИнтерфейс

// Показывает форму создания машиночитаемой доверенности
// 
// Параметры:
//  ОповещениеОЗавершении - ОписаниеОповещения
Процедура СоздатьМЧД(ОповещениеОЗавершении = Неопределено) Экспорт
	
	ПараметрыАутентификации = ЭлектронныеДокументыСлужебныйКлиент.АутентификацияНаСайте();
	Если ПараметрыАутентификации = Неопределено Тогда
		ПоказатьПредупреждение(,
			НСтр("ru = 'Не удалось начать создание доверенности. 
			|Необходимо ввести Логин и Пароль для авторизации на сайте поддержки.'"));	
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму("Справочник.МЧД003.Форма.ЧерновикМЧД", , , , , , ОповещениеОЗавершении);
КонецПроцедуры

// Показывает форму создания машиночитаемой доверенности
// 
// Параметры:
//  ОповещениеОЗавершении - ОписаниеОповещения
Процедура СоздатьМЧДПередоверия(ОповещениеОЗавершении = Неопределено) Экспорт
	
	ПараметрыАутентификации = ЭлектронныеДокументыСлужебныйКлиент.АутентификацияНаСайте();
	Если ПараметрыАутентификации = Неопределено Тогда
		ПоказатьПредупреждение(,
			НСтр("ru = 'Не удалось начать создание доверенности. 
			|Необходимо ввести Логин и Пароль для авторизации на сайте поддержки.'"));	
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму("Справочник.МЧД003.Форма.ЧерновикПередоверияМЧД", , , , , , ОповещениеОЗавершении);
КонецПроцедуры

// Показывает действия при обработке ошибок наличия подписей с невалидной МЧД.
// 
// Параметры:
//  КонтекстДиагностики - См. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
//  ДополнительныеПараметры - Произвольный
//  
Процедура ОткрытьФормуПроверкиПолномочий(КонтекстДиагностики, ДополнительныеПараметры) Экспорт
	
	ДанныеДляПроверки = Новый Соответствие();
	ВсегоЭлементовДляПроверки = 0;
	
	Для Каждого Ошибка Из КонтекстДиагностики.Диагностика.Ошибки Цикл
		
		ВсегоЭлементовДляПроверки = ВсегоЭлементовДляПроверки + Ошибка.ДополнительныеДанные.Количество();
		ДанныеДляПроверки.Вставить(Ошибка, Ошибка.ДополнительныеДанные);
		
	КонецЦикла;
	
	Если ВсегоЭлементовДляПроверки = 1 Тогда
		
		ПараметрыПроверки = МашиночитаемыеДоверенностиКлиентСервер.НовыеПараметрыПроверкиПолномочий();
		ЗаполнитьЗначенияСвойств(ПараметрыПроверки, Ошибка.ДополнительныеДанные[0]);
		ПараметрыПроверки.ЭтоОперацияЭДО = Истина;
		Оповещение = Новый ОписаниеОповещения("ОбработатьРезультатПроверкиПолномочий", МашиночитаемыеДоверенностиКлиент, ДанныеДляПроверки);
		ПроверитьПолномочияДоверенностиВручную(ПараметрыПроверки, Оповещение);
		
	Иначе
		
		ДанныеПроверки = Новый Массив();
		
		Для Каждого Ошибка Из ДанныеДляПроверки Цикл
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ДанныеПроверки, Ошибка.Значение);
		КонецЦикла;
		
		Оповещение = Новый ОписаниеОповещения("ОбработатьРезультатПроверкиПолномочий", МашиночитаемыеДоверенностиКлиент, ДанныеДляПроверки);
		ПроверитьПолномочияДоверенностейВручную(Новый Структура("ДанныеПроверки", ДанныеПроверки), Оповещение);
		
	КонецЕсли;
	
КонецПроцедуры

#Область ОтражениеВУчете
// См. ОбменСКонтрагентамиКлиентПереопределяемый.ПриПодбореУчетногоДокумента
Процедура ПриПодбореУчетногоДокумента(Знач Настройки, Знач ОповещениеОВыборе, СтандартнаяОбработка = Истина) Экспорт

	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура("РежимВыбора, ЗакрыватьПриВыборе", Истина, Истина);
	ПараметрыФормы.Вставить("Контрагент", Настройки.Контрагент);
	
	ОткрытьФорму(Настройки.ИмяОбъектаМетаданных + ".ФормаВыбора", 
		ПараметрыФормы,,,,, ОповещениеОВыборе, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

// Открывает форму результатов проверки подписи 
// 
// Параметры:
// 	ПараметрыФормы - см. ОбщегоНазначенияБЭДКлиент.ОткрытьФормуБЭД.ПараметрыФормы
// 	ПараметрыОткрытия - см. ОбщегоНазначенияБЭДКлиент.ОткрытьФормуБЭД.ПараметрыОткрытияФормы
Процедура ОткрытьРезультатыПроверкиПодписи(ПараметрыФормы = Неопределено, ПараметрыОткрытия = Неопределено) Экспорт
	
	ОткрытьФорму("Обработка.РезультатыПроверкиПодписи.Форма.РезультатыПроверкиПодписи", ПараметрыФормы,
		ПараметрыОткрытия.Владелец,
		ПараметрыОткрытия.Уникальность,
		ПараметрыОткрытия.Окно,
		ПараметрыОткрытия.НавигационнаяСсылка,
		ПараметрыОткрытия.ОписаниеОповещенияОЗакрытии,
		ПараметрыОткрытия.РежимОткрытияОкна);
		
КонецПроцедуры

// Загружает доверенность из файла, помещенного в хранилище.
// Справочник назначение определяется по формату данных файла,
// МЧД версии 003 грузится в справочник МЧД003, файлы прочих форматов
// пытаются загрузится в справочники МашиночитаемыеДоверенностиОрганизаций (в приоритете)
// и МашиночитаемыеДоверенностиКонтрагентов.
// 
// После загрузки выполняется проверка доверенности.
// 
// Параметры:
//  АдресХранилища - Строка - адрес хранилища двоичных данных загружаемого файла
Процедура ЗагрузитьМЧДИзФайла(АдресХранилища) Экспорт
	РезультатЗагрузкиМЧД = МашиночитаемыеДоверенностиВызовСервера.ЗагрузитьМЧДИзФайла(АдресХранилища);
	Если Не ЗначениеЗаполнено(РезультатЗагрузкиМЧД.МЧД) Тогда
		ПоказатьПредупреждениеПриЗагрузкеМЧД(РезультатЗагрузкиМЧД.ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	КонтекстДиагностики = Неопределено;
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("МЧД", РезультатЗагрузкиМЧД.МЧД);
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузитьПерезаполнитьМЧДИзФайлаЗавершение", МашиночитаемыеДоверенностиКлиент,
		ДополнительныеПараметры);
	ПроверитьДанныеМЧД(ОповещениеОЗавершении, РезультатЗагрузкиМЧД, КонтекстДиагностики);
КонецПроцедуры

// Загружает в элемент справочника данные из архива с файлом МЧД и подписью.
// Если доверенности с таким номером нет, то создает новую, иначе перезаполняет существующую.
// Проверяет подпись файла МЧД.
//
// Параметры:
//  АдресСДаннымиФайла - Строка - Адрес во временном хранилище с двоичными данными архива.
//
Процедура ЗагрузитьПерезаполнитьМЧДКонтрагентаИзФайла(АдресСДаннымиФайла) Экспорт
	
	ЗагрузитьПерезаполнитьМЧДИзФайла(АдресСДаннымиФайла, Ложь, Истина);
	
КонецПроцедуры

// Загружает в элемент справочника данные из архива с файлом МЧД и подписью.
// Если доверенности с таким номером нет, то создает новую, иначе перезаполняет существующую.
// Проверяет подпись файла МЧД.
//
// Параметры:
//  АдресСДаннымиФайла - Строка - Адрес во временном хранилище с двоичными данными архива.
//
Процедура ЗагрузитьПерезаполнитьМЧДОрганизацииИзФайла(АдресСДаннымиФайла) Экспорт
	
	ЗагрузитьПерезаполнитьМЧДИзФайла(АдресСДаннымиФайла, Истина);
	
КонецПроцедуры

// Загружает в элемент справочника данные из архива с файлом МЧД и подписью.
// Если доверенности с таким номером нет, то создает новую, иначе перезаполняет существующую.
// Проверяет подпись файла МЧД.
//
// Параметры:
//  АдресСДаннымиФайла - Строка - Адрес во временном хранилище с двоичными данными архива.
//  ЭтоМЧДОрганизации - Булево - Признак загрузки МЧД организации.
//  ЭтоМЧДКонтрагента - Булево - Признак загрузки МЧД контрагента.
//
Процедура ЗагрузитьПерезаполнитьМЧДИзФайла(АдресСДаннымиФайла, ЭтоМЧДОрганизации = Ложь, ЭтоМЧДКонтрагента = Ложь) Экспорт
	
	Если ЭтоМЧДОрганизации Тогда 
		РезультатЗагрузкиМЧД = МашиночитаемыеДоверенностиВызовСервера.ЗагрузитьМЧДОрганизацииИзФайла(АдресСДаннымиФайла);
	ИначеЕсли ЭтоМЧДКонтрагента Тогда
		РезультатЗагрузкиМЧД = МашиночитаемыеДоверенностиВызовСервера.ЗагрузитьМЧДКонтрагентаИзФайла(АдресСДаннымиФайла);
	Иначе	
		РезультатЗагрузкиМЧД = МашиночитаемыеДоверенностиВызовСервера.ЗагрузитьМЧДИзФайла(АдресСДаннымиФайла);
	КонецЕсли;	
		
	Если РезультатЗагрузкиМЧД <> Неопределено И Не ЗначениеЗаполнено(РезультатЗагрузкиМЧД.МЧД) Тогда
		ПоказатьПредупреждениеПриЗагрузкеМЧД(РезультатЗагрузкиМЧД.ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	КонтекстДиагностики = Неопределено;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("МЧД", РезультатЗагрузкиМЧД.МЧД);

	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузитьПерезаполнитьМЧДИзФайлаЗавершение", МашиночитаемыеДоверенностиКлиент,
		ДополнительныеПараметры);
	
	ПроверитьДанныеМЧД(ОповещениеОЗавершении, РезультатЗагрузкиМЧД, КонтекстДиагностики);
	
КонецПроцедуры

// Проверяет данные доверенности и данные подписи доверенности.
//
// Параметры:
//  ОповещениеОЗавершении - ОписаниеОповещения - Оповещение, которое будет вызвано после проверки подписи доверенности.
//  ПараметрыПроверкиМЧД - Структура:
//  * МЧД - СправочникСсылка.МашиночитаемыеДоверенностиКонтрагентов
//        - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
//  * ТребуетсяПроверкаМЧДНаКлиенте - Булево - Флаг устанавливается в Истина, 
//                                             если отсутствует возможность проверить подпись МЧД на сервере 
//                                             (в настройках установлена проверка подписи на клиенте) 
//                                             и необходимо выполнить проверку на клиенте.
//  * ДанныеДляПроверки - см. МашиночитаемыеДоверенностиКлиентСервер.НовыеДанныеДляПроверкиМЧД
//  КонтекстДиагностики - см. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
//
Процедура ПроверитьДанныеМЧД(ОповещениеОЗавершении, ПараметрыПроверкиМЧД, КонтекстДиагностики) Экспорт
	
	ДанныеДляПроверки = ПараметрыПроверкиМЧД.ДанныеДляПроверки;
	
	Если ДанныеДляПроверки = Неопределено
		Или ДанныеДляПроверки.ДанныеДоверенности = Неопределено 
		Или ДанныеДляПроверки.ДанныеПодписи = Неопределено Тогда
		
		ТекстОшибки = Неопределено;
		
		РезультатПроверки = Новый Структура;
		РезультатПроверки.Вставить("Результат", Ложь);
		ПараметрыПроверкиМЧД.Свойство("ТекстОшибки", ТекстОшибки);
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда 			
			РезультатПроверки.Вставить("ТекстОшибки", ТекстОшибки);
		Иначе
			РезультатПроверки.Вставить("ТекстОшибки", НСтр("ru = 'Нет данных файла доверенности и/или подписи.'"));
		КонецЕсли;
		РезультатПроверки.Вставить("КонтекстДиагностики", КонтекстДиагностики);
		
		ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, РезультатПроверки);
		Возврат;
	КонецЕсли;
	
	Если ПараметрыПроверкиМЧД.ТребуетсяПроверкаМЧДНаКлиенте Тогда
		ПроверитьДоверенность(ОповещениеОЗавершении, 
			ПараметрыПроверкиМЧД.ДанныеДляПроверки, КонтекстДиагностики);
	Иначе
		ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

// См. ИнтеграцияЭДОСобытияКлиент.ПослеПерезаполненияОбъектаУчета
Процедура ПослеПерезаполненияОбъектаУчета(ОповещениеОЗавершении, ТипДокумента, ОбъектУчета, КонтекстДиагностики) Экспорт
	
	Если ТипДокумента = ПредопределенноеЗначение("Перечисление.ВидыЭД.МашиночитаемаяДоверенность") Тогда

		ЭтоСсылкаНаДоверенность = 
			МашиночитаемыеДоверенностиКлиентСервер.ЭтоСсылкаНаДоверенность(ОбъектУчета);
			
		Если Не ЭтоСсылкаНаДоверенность Тогда
			Возврат;
		КонецЕсли;
		
		ОбъектыУчета = МашиночитаемыеДоверенностиКлиентСервер.ЗначениеВМассиве(ОбъектУчета);
		ПослеОтраженияПерезаполненияОбъектовУчета(ОповещениеОЗавершении, ТипДокумента, ОбъектыУчета, КонтекстДиагностики);

	Иначе
		ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, КонтекстДиагностики);
	КонецЕсли; 
	
КонецПроцедуры

// См. ИнтеграцияЭДОСобытияКлиент.ПослеОтраженияВУчете
Процедура ПослеОтраженияВУчете(ОповещениеОЗавершении, ТипДокумента, ТаблицаДокументов, КонтекстДиагностики) Экспорт
	
	Если ТипДокумента = ПредопределенноеЗначение("Перечисление.ВидыЭД.МашиночитаемаяДоверенность") Тогда

		Если ТаблицаДокументов.Количество() = 0 Тогда
			ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, КонтекстДиагностики);
			Возврат;
		КонецЕсли;
		
		ОбъектыУчета = Новый Массив;
		
		Для Каждого СтрокаТаблицыДокументов Из ТаблицаДокументов Цикл
			
			ЭтоСсылкаНаДоверенность = 
				МашиночитаемыеДоверенностиКлиентСервер.ЭтоСсылкаНаДоверенность(СтрокаТаблицыДокументов.ДокументОснование);
				
			Если Не ЭтоСсылкаНаДоверенность Тогда
				Продолжить;
			КонецЕсли;
			
			ОбъектыУчета.Добавить(СтрокаТаблицыДокументов.ДокументОснование);

		КонецЦикла;
		
		ПослеОтраженияПерезаполненияОбъектовУчета(ОповещениеОЗавершении, ТипДокумента, ОбъектыУчета, КонтекстДиагностики);
		
	Иначе
		ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, КонтекстДиагностики);
	КонецЕсли; 
	
КонецПроцедуры

// Проверка доверенностей на клиенте после отражения в учете продолжение
//
// Параметры:
//  РезультатПроверки - см. МашиночитаемыеДоверенности.РезультатПроверкиДоверенности
//  Параметры - Структура:
//   * МЧД - СправочникСсылка.МашиночитаемыеДоверенностиКонтрагентов
//         - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
//   * КонтекстДиагностики - См. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
//   * ОповещениеОЗавершении - ОписаниеОповещения - Оповещение, которое будет вызвано после обработки события с параметрами:
//        Результат - см. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
//
Процедура ПослеОтраженияВУчетеЗавершение(РезультатПроверки, Параметры) Экспорт

	МЧД = Параметры.МЧД;
	КонтекстДиагностики = Параметры.КонтекстДиагностики;
	ОповещениеОЗавершении = Параметры.ОповещениеОЗавершении;

	Если ЗначениеЗаполнено(РезультатПроверки) Тогда
		КонтекстДиагностики = РезультатПроверки.КонтекстДиагностики;
		ОбработатьРезультатПроверкиМЧД(РезультатПроверки);
		МашиночитаемыеДоверенностиВызовСервера.ОтразитьРезультатПроверкиМЧД(МЧД, РезультатПроверки.Результат, 
			РезультатПроверки.ТекстОшибки);
	КонецЕсли;

	ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, КонтекстДиагностики);
	
КонецПроцедуры

// Проверяет корректность оформления доверенности.
// 
// Параметры:
//  Оповещение - ОписаниеОповещения - описание процедуры, которая будет вызвана после выполнения
//                                    проверки со следующими параметрами:
//                         * Результат - см. МашиночитаемыеДоверенности.РезультатПроверкиДоверенности
//                         * ДополнительныеПараметры - Произвольный - значение, которое было указано
//                                                      при создании объекта ОписаниеОповещения.
//  ДанныеДляПроверки - см. МашиночитаемыеДоверенностиКлиентСервер.НовыеДанныеДляПроверкиМЧД
//  КонтекстДиагностики - см. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
Процедура ПроверитьДоверенность(Оповещение, ДанныеДляПроверки, КонтекстДиагностики) Экспорт
	
	Контекст = КонтекстПроверкиДоверенности();
	Контекст.ДанныеДоверенности = ДанныеДляПроверки.ДанныеДоверенности;
	Контекст.Оповещение = Оповещение;
	Контекст.КонтекстДиагностики = КонтекстДиагностики;

	СвойстваПодписи = Новый Структура;
	СвойстваПодписи.Вставить("Сертификат", Неопределено);
	СвойстваПодписи.Вставить("ПодписьВерна", Ложь);
	
	РезультатПроверкиПодписи =  Новый Структура;
	РезультатПроверкиПодписи.Вставить("ОписаниеОшибки", "");
	РезультатПроверкиПодписи.Вставить("СвойстваПодписи", СвойстваПодписи);

	Попытка
		МенеджерКриптографии = ЭлектроннаяЦифроваяПодписьКлиент.ПолучитьМенеджерКриптографии();
	Исключение
		ШаблонРезультата = НСтр("ru = '%1
		|%2'");
		ТекстОшибки = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("100");
		РезультатТеста = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								ШаблонРезультата,
								ТекстОшибки,
								КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
								
		МенеджерКриптографии = Неопределено;
		РезультатПроверкиПодписи.СвойстваПодписи.ПодписьВерна = Ложь;
		РезультатПроверкиПодписи.ОписаниеОшибки = РезультатТеста;
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(РезультатТеста);
	КонецПопытки;


	Если МенеджерКриптографии <> Неопределено Тогда
		Попытка
			Сертификат = Неопределено;
			МенеджерКриптографии.ПроверитьПодпись(
				ДанныеДляПроверки.ДанныеДоверенности, ДанныеДляПроверки.ДанныеПодписи, Сертификат);   
				
			Если Сертификат <> Неопределено Тогда
				РезультатПроверкиПодписи.СвойстваПодписи.Сертификат = Сертификат.Выгрузить();	
			КонецЕсли;
			РезультатПроверкиПодписи.СвойстваПодписи.ПодписьВерна = Истина;	
		Исключение
			ШаблонРезультата = НСтр("ru = '%1
			|%2'");
			ТекстСообщения = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("104");
			ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			РезультатТеста = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								ШаблонРезультата,
								ТекстОшибки,
								КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
								
			РезультатПроверкиПодписи.СвойстваПодписи.ПодписьВерна = Ложь;
			РезультатПроверкиПодписи.ОписаниеОшибки = РезультатТеста;
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(РезультатТеста);
		КонецПопытки;
	КонецЕсли;	
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеПроверкиПодписи", МашиночитаемыеДоверенностиКлиент, Контекст);
	ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, РезультатПроверкиПодписи);
	
КонецПроцедуры

// Открывает общую для форму списков машиночитаемых доверенностей организаций и контрагентов.
//
Процедура ОткрытьОбщуюФормуСписковМЧД() Экспорт
	ОткрытьФорму("РегистрСведений.ЖурналМашиночитаемыхДоверенностей.ФормаСписка");
КонецПроцедуры

// Открывает форму списка машиночитаемых доверенностей.
//
// Параметры:
//  Организация - ОпределяемыйТип.Организация - Организация
Процедура ОткрытьСписокМЧД(Организация = Неопределено) Экспорт
	
	ОткрытьФорму("РегистрСведений.ЖурналМашиночитаемыхДоверенностей.Форма.ФормаСписка");
	Оповестить(ИмяОповещенияПоказатьДоверенностиОрганизации(), Организация);
	
КонецПроцедуры


// Оповестить о изменении МЧД.
// 
// Параметры:
//  Доверенности - Массив Из ОпределяемыйТип.МашиночитаемаяДоверенность
//
Процедура ОповеститьОИзмененииМЧД(Доверенности) Экспорт
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("Доверенности", Доверенности);
	
	Оповестить(ИмяОповещенияОИзмененииМЧД(), ПараметрыОповещения);
	
КонецПроцедуры

// Имя оповещения о изменении МЧД.
// 
// Возвращаемое значение:
//  Строка
Функция ИмяОповещенияОИзмененииМЧД() Экспорт
	
	Возврат "ИзмененыМЧД";
	
КонецФункции

// Возвращаемое значение:
//  Строка
Функция ИмяОповещенияПоказатьДоверенностиОрганизации() Экспорт
	Возврат "ПоказатьДоверенностиОрганизации";
КонецФункции

// Возвращает имя события, возникающее при исправлении ошибки проверки полномочий.
// 
// Возвращаемое значение:
//  Строка
Функция ИмяСобытияИсправлениеОшибкиПроверкиПолномочий() Экспорт
	
	Возврат "МашиночитаемыеДоверенности.ВыполненоИсправлениеОшибкиПроверкиПолномочий";
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает имя события, возникающее на форме при регистрации передоверия в ФНС.
// 
// Возвращаемое значение:
//  Строка
Функция ИмяСобытияРегистрацииПередоверия() Экспорт
	
	Возврат "РегистрацияПередоверия";
	
КонецФункции

Процедура ПоказатьПредупреждениеПриЗагрузкеМЧД(ТекстПредупреждения) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ТекстПредупреждения) Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьПредупреждение(, ТекстПредупреждения,, НСтр("ru = 'Доверенность не загружена'"));
	
КонецПроцедуры
// Обрабатывает событие дерева настроек проверки полномочий.
// 
// Параметры:
//  Элемент - ТаблицаФормы
Процедура ДеревоОтбораПриАктивизацииСтроки(Элемент) Экспорт
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные <>  Неопределено Тогда
		ТекущийРодитель = ТекущиеДанные.ПолучитьРодителя();
		РазрешеноУдаление = НЕ Элемент.ТекущиеДанные.ДоступенСписок;
		РазрешеноДобавление = Элемент.ТекущиеДанные.ДоступенСписок
			ИЛИ (ТекущийРодитель <> Неопределено И ТекущийРодитель.ДоступенСписок);
		Элемент.КонтекстноеМеню.ПодчиненныеЭлементы.ДеревоОтбораКонтекстноеМенюДобавить.Доступность = РазрешеноДобавление;
		Элемент.КоманднаяПанель.ПодчиненныеЭлементы.ДеревоОтбораДобавить.Доступность = РазрешеноДобавление;
		Элемент.КонтекстноеМеню.ПодчиненныеЭлементы.ДеревоОтбораКонтекстноеМенюУдалить.Доступность = РазрешеноУдаление;
		Элемент.КоманднаяПанель.ПодчиненныеЭлементы.ДеревоОтбораУдалить.Доступность = РазрешеноУдаление;
		Элемент.КоманднаяПанель.ПодчиненныеЭлементы.ДеревоОтбораИзменить.Доступность =
			НЕ Элемент.ТекущиеДанные.ДоступенСписок;
		Элемент.КонтекстноеМеню.ПодчиненныеЭлементы.ДеревоОтбораКонтекстноеМенюИзменить.Доступность =
			НЕ Элемент.ТекущиеДанные.ДоступенСписок;
	КонецЕсли;
	
КонецПроцедуры

// Обрабатывает событие дерева настроек проверки полномочий.
// 
// Параметры:
//  ДанныеСтроки - ДанныеФормыЭлементДерева
//  Отказ - Булево
//  ИдентификаторСтроки - Число
//  Модифицированность - Булево
Процедура ДеревоОтбораПередУдалением(ДанныеСтроки, Отказ, ИдентификаторСтроки, Модифицированность) Экспорт
	
	Если ДанныеСтроки.ДоступенСписок Тогда
	
		Отказ = Истина;
		Строки = ДанныеСтроки.ПолучитьЭлементы();
		
		Пока Строки.Количество() > 0 Цикл
			Строки.Удалить(0);
			Модифицированность = Истина;
		КонецЦикла;
		
		ИдентификаторСтроки = ДанныеСтроки.ПолучитьИдентификатор();
		Возврат;
	
	КонецЕсли;
	
	Если ДанныеСтроки.Тип = МашиночитаемыеДоверенностиКлиентСервер.Тип_Булево() Тогда
		
		Отказ = Истина;
		
		Если ДанныеСтроки.НачальноеЗначение <> Истина ИЛИ ДанныеСтроки.КонечноеЗначение <> Ложь Тогда
			
			ДанныеСтроки.НачальноеЗначение = Истина;
			ДанныеСтроки.КонечноеЗначение = Ложь;
			Модифицированность = Истина;
			
		КонецЕсли;
		
		ИдентификаторСтроки = ДанныеСтроки.ПолучитьИдентификатор();
	
	ИначеЕсли ДанныеСтроки.Тип = МашиночитаемыеДоверенностиКлиентСервер.Тип_Число_15() Тогда
		
		Отказ = Истина;
		
		Если ДанныеСтроки.НачальноеЗначение + ДанныеСтроки.КонечноеЗначение > 0 Тогда
			
			ДанныеСтроки.НачальноеЗначение = 0;
			ДанныеСтроки.КонечноеЗначение = 0;
			Модифицированность = Истина;
			
		КонецЕсли;
		
		ИдентификаторСтроки = ДанныеСтроки.ПолучитьИдентификатор();
		
	Иначе
		
		Отказ = Истина;
		Модифицированность = Истина;
		КорневойЭлемент = ДанныеСтроки.ПолучитьРодителя();
		КорневойЭлемент.ПолучитьЭлементы().Удалить(ДанныеСтроки);
		ИдентификаторСтроки = КорневойЭлемент.ПолучитьИдентификатор();
		
	КонецЕсли;
	
КонецПроцедуры

// Обрабатывает событие дерева настроек проверки полномочий.
// 
// Параметры:
//  ДанныеСтроки - ДанныеФормыЭлементДерева
//  Отказ - Булево
//  Элемент - ПолеФормы
//  Оповещение - ОписаниеОповещения
Процедура ДеревоОтбораПередНачаломИзменения(ДанныеСтроки, Отказ, Элемент, Оповещение) Экспорт
	
	Если ДанныеСтроки.ДоступенСписок Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если ДанныеСтроки.Тип = МашиночитаемыеДоверенностиКлиентСервер.Тип_Булево()
		ИЛИ ДанныеСтроки.Тип = МашиночитаемыеДоверенностиКлиентСервер.Тип_Число_15() Тогда
		
		Отказ = Истина;
		ПараметрыВвода = Новый Структура("НачальноеЗначение, КонечноеЗначение, Заголовок",
			ДанныеСтроки.НачальноеЗначение, ДанныеСтроки.КонечноеЗначение, ДанныеСтроки.ЗаголовокПоля);
		ОткрытьФорму("Справочник.ПравилаПроверкиПолномочийМЧД.Форма.ВводЗначения",
			ПараметрыВвода, МашиночитаемыеДоверенностиКлиент,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	Иначе
		
		Родитель = ДанныеСтроки.ПолучитьРодителя();
		Если Родитель <> Неопределено И Родитель.ДоступенСписок Тогда
			Элемент.ОграничениеТипа = Родитель.Тип;
			Элемент.РежимВыбораИзСписка = Ложь;
			
			Если ЗначениеЗаполнено(Родитель.СписокВыбора) Тогда
				Элемент.СписокВыбора.ЗагрузитьЗначения(Родитель.СписокВыбора.ВыгрузитьЗначения());
				Элемент.РежимВыбораИзСписка = Истина;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Обрабатывает событие дерева настроек проверки полномочий.
// 
// Параметры:
//  Элемент - ТаблицаФормы
//  ОтменаРедактирования - Булево
//  ДеревоОтбора - ДанныеФормыДерево
Процедура ДеревоОтбораПередОкончаниемРедактирования(Элемент, ОтменаРедактирования, ДеревоОтбора) Экспорт
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	ИдСтроки = ТекущиеДанные.ИдСтроки;
	
	Если ИдСтроки > 0 Тогда
		
		Если ОтменаРедактирования Тогда
			
			СтрокаДерева = ДеревоОтбора.НайтиПоИдентификатору(Элемент.ТекущаяСтрока);
			РодительЭлемента = СтрокаДерева.ПолучитьРодителя();
			ИндексСтроки = РодительЭлемента.ПолучитьЭлементы().Индекс(СтрокаДерева);
			РодительЭлемента.ПолучитьЭлементы().Удалить(ИндексСтроки);
			
		Иначе
			
			ТекущиеДанные.ИдСтроки = 0;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Обрабатывает событие дерева настроек проверки полномочий.
// 
// Параметры:
//  ДеревоОтбора - ТаблицаФормы
//  Отказ - Булево
//  Элемент - ПолеФормы
Процедура ДеревоОтбораПередНачаломДобавления(ДеревоОтбора, Отказ, Элемент) Экспорт
	
	ДанныеСтроки = ДеревоОтбора.ТекущиеДанные;
	Родитель = ДанныеСтроки.ПолучитьРодителя();
	
	Если Родитель = Неопределено И НЕ ДанныеСтроки.ДоступенСписок Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Родитель <> Неопределено И Родитель.ДоступенСписок Тогда
		
		Отказ = Истина;
		СтрокаДерева = Родитель.ПолучитьЭлементы().Добавить();
		СтрокаДерева.ИдСтроки = ДеревоОтбора.ТекущаяСтрока;
		СтрокаДерева.Картинка = ДанныеСтроки.Картинка;
		СтрокаДерева.Тип = Родитель.Тип;
		ДеревоОтбора.ТекущаяСтрока = СтрокаДерева.ПолучитьИдентификатор();
		ДеревоОтбора.ИзменитьСтроку();
		Возврат;
		
	КонецЕсли;
	
	Элемент.ОграничениеТипа = ДанныеСтроки.Тип;
	Элемент.РежимВыбораИзСписка = Ложь;
	Если ЗначениеЗаполнено(ДанныеСтроки.СписокВыбора) Тогда
		Элемент.СписокВыбора.ЗагрузитьЗначения(ДанныеСтроки.СписокВыбора.ВыгрузитьЗначения());
		Элемент.РежимВыбораИзСписка = Истина;
	КонецЕсли;	
	
КонецПроцедуры

// Обработчик события выбора варианта проверки форм МЧД
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
Процедура ОтобразитьВариантПроверки(Форма) Экспорт
	
	Варианты = МашиночитаемыеДоверенностиКлиентСервер.ВариантыПроверки();
	
	Если Форма.ВариантПроверки = Варианты.Настройка Тогда
		
		Форма.Элементы.ГруппаСкрипт.Видимость = Ложь;
		Форма.Элементы.ГруппаНастройка.Видимость = Истина;
		
	ИначеЕсли Форма.ВариантПроверки = Варианты.Скрипт Тогда
		
		Форма.Элементы.ГруппаСкрипт.Видимость = Истина;
		Форма.Элементы.ГруппаНастройка.Видимость = Ложь;
		
	КонецЕсли;
	
	СформироватьЗаголовокВкладкиПолномочия(Форма);
	
КонецПроцедуры

Функция ПредупреждениеОбОтсутствииНастройки() Экспорт
	Возврат НСтр("ru = 'Правила проверки полномочий этой доверенности не настроены. Без этого невозможно ее использование. Настройку можно выполнить позже.'");
КонецФункции

// Формирует заголовок вкладки полномочия.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  ПравилоНастроено - Булево
//  
Процедура СформироватьЗаголовокВкладкиПолномочия(Форма, ПравилоНастроено = Неопределено) Экспорт
	
	ГруппаФормы = Форма.Элементы.Найти("ГруппаПроверкаПолномочий");
	
	Если ГруппаФормы <> Неопределено И ГруппаФормы.Вид = ВидГруппыФормы.Страница Тогда
		
		Если ПравилоНастроено = Неопределено Тогда
			ПравилоНастроено = Ложь;
			Варианты = МашиночитаемыеДоверенностиКлиентСервер.ВариантыПроверки();
			
			Если Форма.ВариантПроверки = Варианты.Скрипт Тогда
				ПравилоНастроено = ЗначениеЗаполнено(Форма.Скрипт);
			ИначеЕсли Форма.ВариантПроверки = Варианты.Настройка Тогда
				ПравилоНастроено = МашиночитаемыеДоверенностиКлиентСервер.ДеревоСодержитНастройки(Форма.ДеревоОтбора);
			КонецЕсли;
		КонецЕсли;
		
		Форма.ЗаголовокГруппаПроверкаПолномочий = ?(ПравилоНастроено, НСтр("ru = 'есть'"), НСтр("ru = 'нет'"));
		
	КонецЕсли;
	
КонецПроцедуры

// Выполняет проверки МЧД после отражения/перезаполнения объектов учета.
// 
// Параметры:
//  ОповещениеОЗавершении - ОписаниеОповещения - Оповещение о завершении
//  ТипДокумента - ПеречислениеСсылка.ТипыДокументовЭДО - Тип документа
//  ОбъектыУчета - Массив из ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО - Объекты учета
//  КонтекстДиагностики - см. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
Процедура ПослеОтраженияПерезаполненияОбъектовУчета(ОповещениеОЗавершении, ТипДокумента, ОбъектыУчета, КонтекстДиагностики)

	Для Каждого ОбъектУчета Из ОбъектыУчета Цикл
		
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("МЧД", ОбъектУчета);
		ПараметрыОповещения.Вставить("КонтекстДиагностики", КонтекстДиагностики);
		ПараметрыОповещения.Вставить("ОповещениеОЗавершении", ОповещениеОЗавершении);

		Оповещение = Новый ОписаниеОповещения("ПослеОтраженияВУчетеЗавершение", МашиночитаемыеДоверенностиКлиент, ПараметрыОповещения);

		ТребуетсяПроверкаМЧДНаКлиенте = Не ЭлектронныеДокументыСлужебныйВызовСервера.ВыполнятьКриптооперацииНаСервере();

		ПараметрыПроверкиМЧД = Новый Структура;
		ПараметрыПроверкиМЧД.Вставить("МЧД", ОбъектУчета);
		ПараметрыПроверкиМЧД.Вставить("ТребуетсяПроверкаМЧДНаКлиенте", ТребуетсяПроверкаМЧДНаКлиенте);

		ДанныеДляПроверки = МашиночитаемыеДоверенностиКлиентСервер.НовыеДанныеДляПроверкиМЧД();
		ДанныеФайлаИПодписи = МашиночитаемыеДоверенностиВызовСервера.ДанныеФайлаДоверенностиИПодписи(ОбъектУчета);
		ДанныеДляПроверки.ДанныеДоверенности = ДанныеФайлаИПодписи.ДанныеФайла;
		ДанныеДляПроверки.ДанныеПодписи = ДанныеФайлаИПодписи.ДанныеПодписи;
		ПараметрыПроверкиМЧД.Вставить("ДанныеДляПроверки", ДанныеДляПроверки);

		ПроверитьДанныеМЧД(Оповещение, ПараметрыПроверкиМЧД, КонтекстДиагностики);
			
	КонецЦикла;
	
КонецПроцедуры

// Настраивает элементы страницы полномочий.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  
Процедура НастроитьСтраницуПолномочий(Форма) Экспорт
	
	Если Форма.Объект.ВариантЗаполненияПолномочий =
		ПредопределенноеЗначение("Перечисление.ВариантыЗаполненияПолномочийМЧД.Текст") Тогда
		
		Форма.Элементы.ГруппаМашиночитаемыеПолномочия.Видимость = Ложь;
		Форма.Элементы.ГруппаТекстовоеПолномочие.Видимость      = Истина;
		
	Иначе
		
		Форма.Элементы.ГруппаМашиночитаемыеПолномочия.Видимость = Истина;
		Форма.Элементы.ГруппаТекстовоеПолномочие.Видимость      = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

#Область ОтправитьПоЭДО

// Открывает новый исходящий электронный документ из формы просмотра доверенности.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения:
//  * Объект - ДанныеФормыСтруктура:
//  ** Ссылка - СправочникСсылка.МашиночитаемыеДоверенностиКонтрагентов
//            - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
Процедура ОтправитьПоЭДОИзФормыПросмотра(Форма) Экспорт
	
	Если Форма.Модифицированность Тогда
		Контекст = Новый Структура("Форма", Форма);
		Оповещение = Новый ОписаниеОповещения("ОтправитьПоЭДОПослеВопросаОЗаписи", МашиночитаемыеДоверенностиКлиент,
			Контекст);
		ТекстВопроса = НСтр("ru = 'Выполнить действие можно только после записи доверенности.
			|Записать?'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Иначе
		ОтправитьПоЭДО(Форма.Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

// Открывает новый исходящий электронный документ из формы просмотра после вопроса о записи модифицированной доверенности.
// 
// Параметры:
//  Ответ - КодВозвратаДиалога
//  Контекст - Структура:
//  * Форма - ФормаКлиентскогоПриложения:
//  ** Объект - ДанныеФормыСтруктура:
//  *** Ссылка - СправочникСсылка.МашиночитаемыеДоверенностиКонтрагентов
//             - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
Процедура ОтправитьПоЭДОПослеВопросаОЗаписи(Ответ, Контекст) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Контекст.Форма.Записать();
	
	ОтправитьПоЭДО(Контекст.Форма.Объект.Ссылка);
	
КонецПроцедуры

// Открывает новый исходящий электронный документ.
// 
// Параметры:
//  Доверенность - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
//               - СправочникСсылка.МашиночитаемыеДоверенностиКонтрагентов
//               - СправочникСсылка.МЧД003
Процедура ОтправитьПоЭДО(Доверенность) Экспорт
	
	ПараметрыСоздания = Новый Структура;
	ПараметрыСоздания.Вставить("ТипЭД", ПредопределенноеЗначение("Перечисление.ТипыЭД.МашиночитаемаяДоверенность"));
	ПараметрыСоздания.Вставить("Ссылка", Доверенность);

	ОткрытьФорму("Документ.ЭлектронныйДокументИсходящий.Форма.ФормаПросмотраЭД", ПараметрыСоздания);
	
КонецПроцедуры

#КонецОбласти

// Получает номер (GUID) для новой машиночитаемой доверенности и записывает в реквизит доверенности
// или поле формы, предполагается использование при открытии формы доверенности, фоновое обновление.
//
// Параметры:
//   ОповещениеОЗавершении - ОписаниеОповещения - описание процедуры, принимающей результат.
//     Результат - структура:
//      * НомерДоверенности - Строка - при ошибке возвращается пустая строка, выводится сообщение об ошибке
//   СсылкаНаДоверенность  - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
//   ФормаДоверенности     - ФормаКлиентскогоПриложения
//
Процедура ПолучитьНомерМЧД(
		ОповещениеОЗавершении = Неопределено,
		СсылкаНаДоверенность = Неопределено,
		ФормаДоверенности = Неопределено) Экспорт
		
	ПараметрыАутентификации = ЭлектронныеДокументыСлужебныйКлиент.АутентификацияНаСайте();
	Если ПараметрыАутентификации = Неопределено Тогда
		ПоказатьПредупреждение(,
			НСтр("ru = 'Не удалось получить новый номер доверенности. 
			|Необходимо ввести Логин и Пароль для авторизации на сайте поддержки.'"));	
		Возврат;
	КонецЕсли;		
	
	Результат = Новый Структура;
	Результат.Вставить("НомерДоверенности", "");
	
	Результат.НомерДоверенности = МашиночитаемыеДоверенностиВызовСервера.ПолучитьНомерМЧД().НомерДоверенности;
	Если ФормаДоверенности <> Неопределено Тогда
		ФормаДоверенности.Объект.НомерДоверенности = Результат.НомерДоверенности;
	КонецЕсли;
	
	Если ОповещениеОЗавершении <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, Результат);
	КонецЕсли;
	
КонецПроцедуры

// Сохраняет форму при заданной форме, выгружает машиночитаемую доверенность, предлагает выбрать/подтвердить выбор
// сертификата, подписывает, отправляет на регистрацию в распределенном реестре ФНС, ожидает регистрации 1-3 минуты,
// получает СтатусВРеестреФНС, обновляет СтатусВРеестреФНС в записи справочника доверенностей,
// в том числе запоминает идентификатор транзакции.
//
// Параметры:
//   ЗарегистрироватьВРеестреФНС - Булево
//   ОповещениеОЗавершении - ОписаниеОповещения - описание процедуры, которая будет вызвана
//                           при завершении со следующими параметрами:
//     * Результат - Структура:
//      ** ИдентификаторТранзакции - Строка - при ошибке отправки на регистрацию выводится сообщение об ошибке
//                                           и возвращается пустая строка, при успехе идентификатор запоминается
//                                           в записи справочника
//      ** СтатусТранзакции        - Строка - "PENDING" - транзакция майнится, "SUCCESS" - транзакция смайнилась,
//                                           "FAILURE" - ошибка при майнинге транзакции, при ошибке получения
//                                           статуса возвращается пустая строка и выводится сообщение об ошибке,
//                                           статус обновляется в записи справочника и на форме, если передана
//      ** ДатаВремяТранзакции     - Дата
//      ** ХешДоверенности         - Строка - хеш доверенности
//      ** НомерДоверенности       - Строка - номер, извлеченный из доверенности
//      ** ИННДоверителя           - Строка - ИНН доверителя, извлеченный из доверенности
//     * ДополнительныеПараметры - Произвольный - значение, которое было указано при создании объекта ОписаниеОповещения.
//   СсылкаНаДоверенность  - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций, СправочникСсылка.МЧД003 -
//   ФормаДоверенности     - ФормаКлиентскогоПриложения
//   ФорматДоверенности - Строка - формат МЧД
//
Процедура ПодписатьЗарегистрироватьМЧД(ЗарегистрироватьВРеестреФНС,
		ОповещениеОЗавершении = Неопределено,
		СсылкаНаДоверенность = Неопределено,
		ФормаДоверенности = Неопределено,
		ФорматДоверенности) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИдентификаторТранзакции", 	"");
	Результат.Вставить("СтатусТранзакции", 			"");
	Результат.Вставить("ДатаВремяТранзакции", 		Неопределено);
	Результат.Вставить("ХешДоверенности", 			"");
	Результат.Вставить("НомерДоверенности", 		"");
	Результат.Вставить("ИННДоверителя", 			"");
	
	ПараметрыАутентификации = ЭлектронныеДокументыСлужебныйКлиент.АутентификацияНаСайте();
	Если ПараметрыАутентификации = Неопределено Тогда
		ПоказатьПредупреждение(,
			НСтр("ru = 'Не удалось подписать и отправить доверенность в реестр. 
			|Необходимо ввести Логин и Пароль для авторизации на сайте поддержки.'"));			
		Если ОповещениеОЗавершении <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, Результат);			
		КонецЕсли;		
		Возврат;
	КонецЕсли;		
	
	Если ФормаДоверенности <> Неопределено И НЕ ФормаДоверенности.Записать() Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не удалось сохранить доверенность'"));
		Если ОповещениеОЗавершении <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, Результат);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	СсылкаНаДоверенностьСУчетомФормы = ?(ФормаДоверенности = Неопределено, СсылкаНаДоверенность,
		ФормаДоверенности.Объект.Ссылка);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеОЗавершении", ОповещениеОЗавершении);
	ДополнительныеПараметры.Вставить("СсылкаНаДоверенность", СсылкаНаДоверенностьСУчетомФормы);
	ДополнительныеПараметры.Вставить("ФормаДоверенности", ФормаДоверенности);
	ДополнительныеПараметры.Вставить("Результат", Результат);
	ДополнительныеПараметры.Вставить("ЗарегистрироватьВРеестреФНС", ЗарегистрироватьВРеестреФНС);
	
	ДополнительныеДанные = Новый Соответствие;
	ДополнительныеДанные.Вставить("ФорматДоверенности", ФорматДоверенности);
	
	НаборДействий = Новый Соответствие;	
	НаборДействий.Вставить(ПредопределенноеЗначение("Перечисление.ОжидаемоеДействиеЭД.Подписать"), Истина);
		
	ДополнительныеПараметры.Вставить("ОповещениеУспешногоЗавершения",
		Новый ОписаниеОповещения("ЗарегистрироватьМЧДПослеПодписи", МашиночитаемыеДоверенностиКлиент, НаборДействий));

	ДвоичныеДанныеДоверенности = МашиночитаемыеДоверенностиВызовСервера.ПолучитьДвоичныеДанныеМЧД(СсылкаНаДоверенностьСУчетомФормы);
	РезультатПодписания = ПодписатьДвоичныеДанныеМЧД(ДвоичныеДанныеДоверенности, СсылкаНаДоверенностьСУчетомФормы);
	
	Если РезультатПодписания <> Неопределено Тогда
		ПослеВыполненияДействийПоЭДО(РезультатПодписания, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает пустой результат подписания.
// 
// Возвращаемое значение:
//  Структура:
// * ПодписьВыполнена - Булево
// * ПодписанныеДанные - ДвоичныеДанные
// 					   - Неопределено
// * Подпись - ДвоичныеДанные
// 			 - Неопределено
// * Сертификат - ДвоичныеДанные
//  			- Неопределено
// * ОписаниеОшибки - Строка
// * МенеджерКриптографии - МенеджерКриптографии
// 						  - Неопределено
// 
Функция РезультатПодписать()
	
	Результат = Новый Структура;
	Результат.Вставить("ПодписьВыполнена", Ложь);
	Результат.Вставить("ПодписанныеДанные", Неопределено);
	Результат.Вставить("Подпись", Неопределено);
	Результат.Вставить("Сертификат", Неопределено);
	Результат.Вставить("ОписаниеОшибки", "");
	Результат.Вставить("МенеджерКриптографии", Неопределено);
	
	Возврат Результат;
	
КонецФункции

// После выполнения действий по ЭДООтзыв.
// 
// Параметры:
//  РезультатПодписания - см. НовыйРезультатПодписания
//  ДополнительныеПараметры - Структура:
//  * ОповещениеОЗавершении - ОписаниеОповещения
//  * СсылкаНаДоверенность - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций,СправочникСсылка.МЧД003
//  * ФормаДоверенности - ФормаКлиентскогоПриложения
//  * Результат - Структура:
//  ** ИдентификаторТранзакции - Строка
//  ** СтатусТранзакции - Строка
//  ** ДатаВремяТранзакции - Неопределено,Дата
//  ** ХешДоверенности - Строка
//  ** НомерДоверенности - Строка
//  ** ИННДоверителя - Строка
//  * ЗарегистрироватьВРеестреФНС - Булево
Процедура ПослеВыполненияДействийПоЭДООтзыв(РезультатПодписания, ДополнительныеПараметры) Экспорт
	
	Если РезультатПодписания.ОшибкиФормирования.Количество() = 0
		И РезультатПодписания.Свойство("ПодписанныеДанные") Тогда
		
		Результат = РезультатПодписать();
		Результат.ПодписьВыполнена = Истина;
		Результат.ПодписанныеДанные = РезультатПодписания.ПодписанныеДанные;
		Результат.Подпись = РезультатПодписания.СвойстваПодписи.Подпись;
		Результат.ОписаниеОшибки = "";
		ДополнительныеПараметры.Вставить("ОтпечатокСертификатаАбонента", РезультатПодписания.СвойстваПодписи.Отпечаток);
		ОтменитьМЧДПослеПодписи(Результат, ДополнительныеПараметры);
	
	Иначе
		
		ПоказатьПредупреждение(, НСтр("ru = 'Подписание отзыва доверенности завершено с ошибками.'"));
		
	КонецЕсли;	

КонецПроцедуры

// Продолжение выполнения действия по ЭДО
//
// Параметры:
//  РезультатПодписания - см. НовыйРезультатПодписания
//  ДополнительныеПараметры - Структура:
//  * ОповещениеОЗавершении - ОписаниеОповещения
//  * СсылкаНаДоверенность - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций,СправочникСсылка.МЧД003
//  * ФормаДоверенности - ФормаКлиентскогоПриложения
//  * Результат - Структура:
//  ** ИдентификаторТранзакции - Строка
//  ** СтатусТранзакции - Строка
//  ** ДатаВремяТранзакции - Неопределено,Дата
//  ** ХешДоверенности - Строка
//  ** НомерДоверенности - Строка
//  ** ИННДоверителя - Строка
//  * ЗарегистрироватьВРеестреФНС - Булево
Процедура ПослеВыполненияДействийПоЭДО(РезультатПодписания, ДополнительныеПараметры) Экспорт
	
	ТекстПредупреждения = НСтр("ru = 'Подписание завершено с ошибками.'");
	
	Если РезультатПодписания = Неопределено Тогда
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	Если РезультатПодписания.ОшибкиФормирования.Количество() = 0
		И РезультатПодписания.Свойство("ПодписанныеДанные") Тогда
		
		Результат = РезультатПодписать();
		Результат.ПодписьВыполнена = Истина;
		Если ТипЗнч(РезультатПодписания.ПодписанныеДанные) = Тип("Строка") И ЭтоАдресВременногоХранилища(РезультатПодписания.ПодписанныеДанные) Тогда
			Результат.ПодписанныеДанные = ПолучитьИзВременногоХранилища(РезультатПодписания.ПодписанныеДанные);
		Иначе
			Результат.ПодписанныеДанные = РезультатПодписания.ПодписанныеДанные;
		КонецЕсли;
		Результат.Подпись = РезультатПодписания.СвойстваПодписи.Подпись;
		Результат.ОписаниеОшибки = "";
		ДополнительныеПараметры.Вставить("СвойстваПодписи", РезультатПодписания.СвойстваПодписи);
		ДополнительныеПараметры.Вставить("ДатаПодписи", РезультатПодписания.СвойстваПодписи.ДатаПодписи);
		ЗарегистрироватьМЧДПослеПодписи(Результат, ДополнительныеПараметры);
	
	Иначе
		
		ПоказатьПредупреждение(, ТекстПредупреждения);
		
	КонецЕсли;	
	
КонецПроцедуры

// Регистрирует МЧД на сервере ФНС
// 
// Параметры:
//  Результат - см. РезультатПодписать
//  ДополнительныеПараметры - Структура
Процедура ЗарегистрироватьМЧДПослеПодписи(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ Результат.ПодписьВыполнена Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон(
			НСтр("ru = 'Не удалось подписать доверенность: %1'"),
			Результат.ОписаниеОшибки));
		Если ДополнительныеПараметры.ОповещениеОЗавершении <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеОЗавершении, ДополнительныеПараметры.Результат);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ФормаДоверенности = ДополнительныеПараметры.ФормаДоверенности;
	
	ТребуетсяПроверкаМЧДНаКлиенте = Ложь;
	Ссылка = ДополнительныеПараметры.СсылкаНаДоверенность;
	
	МашиночитаемыеДоверенностиВызовСервера.ВыполнитьДействияПослеПодписания(Ссылка, Результат.ПодписанныеДанные,
		ДополнительныеПараметры.СвойстваПодписи, ТребуетсяПроверкаМЧДНаКлиенте);
		
	Если ДополнительныеПараметры.ЗарегистрироватьВРеестреФНС Тогда

		Статус = ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ПустаяСсылка");
		ДатаОтправки = Дата(1, 1, 1);
		ИдентификаторТранзакции = "";
	
		РезультатРегистрации = МашиночитаемыеДоверенностиВызовСервера.ЗарегистрироватьМЧД(Ссылка,
			Результат.ПодписанныеДанные, Результат.Подпись);

		Если ЗначениеЗаполнено(РезультатРегистрации.ИдентификаторТранзакции) Тогда
			
			ДополнительныеПараметры.Результат.ИдентификаторТранзакции 	= РезультатРегистрации.ИдентификаторТранзакции;
			ДополнительныеПараметры.Результат.СтатусТранзакции 			= "PENDING";
			ДополнительныеПараметры.Результат.ДатаВремяТранзакции 		= Неопределено;
			ДополнительныеПараметры.Результат.ХешДоверенности 			= РезультатРегистрации.ХешДоверенности;
			ДополнительныеПараметры.Результат.НомерДоверенности 		= РезультатРегистрации.НомерДоверенности;
			ДополнительныеПараметры.Результат.ИННДоверителя 			= РезультатРегистрации.ИННДоверителя;
			
			Статус = ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиВРеестреФНС.Отправлено");
			ДатаОтправки = ОбщегоНазначенияКлиент.ДатаСеанса();
			ИдентификаторТранзакции = РезультатРегистрации.ИдентификаторТранзакции;
			
		Иначе
			
			ТекстСообщения = НСтр("ru = 'Не удалось зарегистрировать доверенность'");
			Если СтрЧислоВхождений(РезультатРегистрации.ТекстОтвета, "В данный момент система не поддерживает работу") Тогда
				ТекстСообщения = ТекстСообщения 
					+ Символы.ПС 
					+ НСтр("ru = 'На данный момент ФНС не поддерживает регистрацию такого типа доверенности'");	
			КонецЕсли;
			Если СтрЧислоВхождений(РезультатРегистрации.ТекстОтвета, "ТипПред, равным '1'") Тогда
				ТекстСообщения = ТекстСообщения + НСтр("ru = ': Уберите из представителей юридических лиц'");	
			КонецЕсли;
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			
		КонецЕсли;
		
		Если ФормаДоверенности <> Неопределено Тогда
			ФормаДоверенности.Прочитать();
			Если ЗначениеЗаполнено(Статус) Тогда
				ФормаДоверенности.Объект.ДатаОтправки = ДатаОтправки;
				ФормаДоверенности.Объект.ДатаОбновленияСтатуса = ФормаДоверенности.Объект.ДатаОтправки;
				ФормаДоверенности.Объект.ИдентификаторТранзакции = ИдентификаторТранзакции;
				ФормаДоверенности.Объект.ОтпечатокСертификата = ДополнительныеПараметры.СвойстваПодписи.Отпечаток;
			Иначе
				ФормаДоверенности.Объект.Подписана = Ложь;
				ФормаДоверенности.Объект.Верна = Ложь;
				ФормаДоверенности.Объект.ДатаПодписания = '00010101';
			КонецЕсли;
			ФормаДоверенности.Объект.СтатусВРеестреФНС = Статус;
			ФормаДоверенности.Записать();
			ФормаДоверенности.РазблокироватьДанныеФормыДляРедактирования();
		КонецЕсли;

		Если Не ЗначениеЗаполнено(Статус) Тогда
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеОЗавершении);
			Возврат;
		КонецЕсли;

	КонецЕсли;
	
	ДанныеДляПроверки = МашиночитаемыеДоверенностиКлиентСервер.НовыеДанныеДляПроверкиМЧД();
	ДанныеДляПроверки.ДанныеДоверенности = Результат.ПодписанныеДанные;
	ДанныеДляПроверки.ДанныеПодписи = ДополнительныеПараметры.СвойстваПодписи.Подпись;
	
	КонтекстДиагностики = Неопределено;
	
	ПараметрыПроверкиМЧД = Новый Структура;
	ПараметрыПроверкиМЧД.Вставить("МЧД", Ссылка);
	ПараметрыПроверкиМЧД.Вставить("ТребуетсяПроверкаМЧДНаКлиенте", ТребуетсяПроверкаМЧДНаКлиенте);
	ПараметрыПроверкиМЧД.Вставить("ДанныеДляПроверки", ДанныеДляПроверки);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗарегистрироватьМЧДПослеПодписиЗавершение", МашиночитаемыеДоверенностиКлиент,
		ДополнительныеПараметры);
	
	ПроверитьДанныеМЧД(ОповещениеОЗавершении, ПараметрыПроверкиМЧД, КонтекстДиагностики);
		
КонецПроцедуры

// Получает частичные (открытые) данные доверенности и обновляет реквизит состояния доверенности, обновляет данные для
// панели состояния в форме, если передана форма.
//
// Параметры:
//   ОповещениеОЗавершении - ОписаниеОповещения - описание процедуры, принимающей результат.
//     Результат - структура:
//      * ХешДоверенности    - Строка - хеш доверенности
//      * НомерДоверенности  - Строка - номер, извлеченный из доверенности
//      * ДатаВыдачи         - Дата - дата начала действия доверенности
//      * ДатаОкончания      - Дата - дата завершения действия доверенности
//      * СтатусДоверенности - Строка - "CREATED" - дата начала действия не наступила, "ACTIVE" - действует,
//                                      "EXPIRED" - истекла, "DECLINED" - отменена (отозвана), "" - не запрашивался
//      * ПубличныйКлюч      - Строка - публичный ключ эмитента доверенности
//   СсылкаНаДоверенность  - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
//   ФормаДоверенности     - ФормаКлиентскогоПриложения
//
Процедура ОбновитьСостояниеВРеестреФНС(
		ОповещениеОЗавершении = Неопределено,
		СсылкаНаДоверенность = Неопределено,
		ФормаДоверенности = Неопределено) Экспорт
	
	// Нереестровая МЧД
	Если ФормаДоверенности.Объект.СтатусВРеестреФНС =
		ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ПустаяСсылка") Тогда
		Возврат
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ХешДоверенности", 		"");
	Результат.Вставить("НомерДоверенности", 	"");
	Результат.Вставить("ДатаВыдачи", 			Неопределено);
	Результат.Вставить("ДатаОкончания", 		Неопределено);
	Результат.Вставить("СтатусДоверенности", 	"");
	Результат.Вставить("ПубличныйКлюч", 		"");
	
	Если ФормаДоверенности <> Неопределено И НЕ ФормаДоверенности.Записать() Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не удалось сохранить доверенность'"));
		Если ОповещениеОЗавершении <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, Результат);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	СостояниеИзменено = Ложь;
	ТокенДоступа = "";
	СтраницаНеНайдена = Ложь;
	Если ФормаДоверенности.Объект.СтатусВРеестреФНС =
		ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиВРеестреФНС.Отправлено") Тогда
		
		СведенияСтатусаТранзакции = МашиночитаемыеДоверенностиВызовСервера.ПолучитьСтатусТранзакцииМЧД(
			ФормаДоверенности.Объект.ИдентификаторТранзакции,
			ТокенДоступа,
			ФормаДоверенности.Объект.НомерДоверенности);
		
		Если СведенияСтатусаТранзакции.СтатусТранзакции = "SUCCESS" Тогда
			ФормаДоверенности.Объект.СтатусВРеестреФНС =
				ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиВРеестреФНС.Зарегистрировано");
			ФормаДоверенности.Объект.ДатаОбновленияСтатуса = ОбщегоНазначенияКлиент.ДатаСеанса();
			ФормаДоверенности.Объект.ИдентификаторТранзакции = "";
			СостояниеИзменено = Истина;
			
		ИначеЕсли СведенияСтатусаТранзакции.СтатусТранзакции = "FAILURE"
			ИЛИ СведенияСтатусаТранзакции.СтатусТранзакции = "ERROR" Тогда
				ФормаДоверенности.Объект.СтатусВРеестреФНС =
				ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ОшибкаРегистрации");
			ФормаДоверенности.Объект.ДатаОбновленияСтатуса = ОбщегоНазначенияКлиент.ДатаСеанса();
			ФормаДоверенности.Объект.ИдентификаторТранзакции = "";
			СостояниеИзменено = Истина;
			Результат.СтатусДоверенности = НСтр("ru ='Ошибка регистрации'");
		ИначеЕсли СведенияСтатусаТранзакции.СтатусТранзакции = 404 Тогда
			СтраницаНеНайдена = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если (ФормаДоверенности.Объект.СтатусВРеестреФНС <>
		ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиВРеестреФНС.Отправлено")
		И ФормаДоверенности.Объект.СтатусВРеестреФНС <>
		ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ОшибкаРегистрации"))
		ИЛИ СтраницаНеНайдена Тогда
		
		СведенияСтатусаДоверенности = МашиночитаемыеДоверенностиВызовСервера.ПолучитьЧастичныеДанныеДоверенностиНаСервереМЧД(
			ФормаДоверенности.Объект.НомерДоверенности,
			ТокенДоступа);
		Результат.ХешДоверенности = СведенияСтатусаДоверенности.ХешДоверенности;
		Результат.НомерДоверенности = СведенияСтатусаДоверенности.НомерДоверенности;
		Результат.ДатаВыдачи = СведенияСтатусаДоверенности.ДатаВыдачи;
		Результат.ДатаОкончания = СведенияСтатусаДоверенности.ДатаОкончания;
		Результат.СтатусДоверенности = СведенияСтатусаДоверенности.СтатусДоверенности;
		Результат.ПубличныйКлюч = СведенияСтатусаДоверенности.ПубличныйКлюч;
		
		Если СведенияСтатусаДоверенности.СтатусДоверенности = "CREATED"
			И ФормаДоверенности.Объект.СтатусВРеестреФНС <>
			ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ДатаНачалаДействияНеНаступила") Тогда
			
			ФормаДоверенности.Объект.СтатусВРеестреФНС =
				ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ДатаНачалаДействияНеНаступила");
			ФормаДоверенности.Объект.ДатаОбновленияСтатуса = ОбщегоНазначенияКлиент.ДатаСеанса();
			СостояниеИзменено = Истина;
			
		ИначеЕсли СведенияСтатусаДоверенности.СтатусДоверенности = "ACTIVE"
			И ФормаДоверенности.Объект.СтатусВРеестреФНС <>
			ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиВРеестреФНС.Зарегистрировано")
			И ФормаДоверенности.Объект.СтатусВРеестреФНС <>
			ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ОтправленоЗаявлениеНаОтзыв")
			И ФормаДоверенности.Объект.СтатусВРеестреФНС <>
			ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ОшибкаОтзыва") Тогда
			
			ФормаДоверенности.Объект.СтатусВРеестреФНС =
				ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиВРеестреФНС.Зарегистрировано");
			ФормаДоверенности.Объект.ДатаОбновленияСтатуса = ОбщегоНазначенияКлиент.ДатаСеанса();
			СостояниеИзменено = Истина;
			
		ИначеЕсли СведенияСтатусаДоверенности.СтатусДоверенности = "EXPIRED"
			И ФормаДоверенности.Объект.СтатусВРеестреФНС <>
			ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ИстекСрокДействия") Тогда
			
			ФормаДоверенности.Объект.СтатусВРеестреФНС =
				ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ИстекСрокДействия");
			ФормаДоверенности.Объект.ДатаОбновленияСтатуса = ОбщегоНазначенияКлиент.ДатаСеанса();
			СостояниеИзменено = Истина;
			
		ИначеЕсли СведенияСтатусаДоверенности.СтатусДоверенности = "DECLINED"
			Или СведенияСтатусаДоверенности.СтатусДоверенности = "REVOKED" Тогда
			ДоверительИНН = ?(ЗначениеЗаполнено(ФормаДоверенности.Объект.ДоверительЮЛ_ИНН), 
				ФормаДоверенности.Объект.ДоверительЮЛ_ИНН, ФормаДоверенности.Объект.ДоверительФЛ_ИНН);
			ДатаОтзыва = МашиночитаемыеДоверенностиВызовСервера.ДатаОтзываДоверенности(
				ФормаДоверенности.Объект.НомерДоверенности, ДоверительИНН, ТокенДоступа);
			Если Не ЗначениеЗаполнено(ДатаОтзыва)
				И Не ЗначениеЗаполнено(ФормаДоверенности.Объект.ДатаОтзыва) Тогда
				ДатаОтзыва = ОбщегоНазначенияКлиент.ДатаСеанса();
			КонецЕсли;
			Если ФормаДоверенности.Объект.СтатусВРеестреФНС <>
				ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиВРеестреФНС.Отозвано")
				Или ФормаДоверенности.Объект.ДатаОтзыва <> ДатаОтзыва Тогда
				ФормаДоверенности.Объект.СтатусВРеестреФНС =
					ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиВРеестреФНС.Отозвано");
				ФормаДоверенности.Объект.ДатаОбновленияСтатуса = ОбщегоНазначенияКлиент.ДатаСеанса();
				ФормаДоверенности.Объект.ДатаОтзыва = ДатаОтзыва;
				ФормаДоверенности.Объект.Отозвана = Истина;
				СостояниеИзменено = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ФормаДоверенности <> Неопределено И СостояниеИзменено И НЕ ФормаДоверенности.Записать() Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не удалось сохранить доверенность'"));
		Если ОповещениеОЗавершении <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, Результат);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если ОповещениеОЗавершении <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, Результат);
	КонецЕсли;
	
КонецПроцедуры

// Запрашивает номер и ИНН доверенности, если не переданы, и отправляет запрос на получение полных данных
// машиночитаемой доверенности, при успехе создает и заполняет полученными данными запись справочника машиночитаемых
// доверенностей, ожидает обработки запроса до 1-3 минуты.
//
// Параметры:
//   ОповещениеОЗавершении - ОписаниеОповещения - описание процедуры, принимающей результат.
//     Результат - структура:
//      * СсылкаНаДоверенность - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций - задана при успехе
//      * СтатусПолучения      - "PENDING" - операция выполняется, идет запрос данных с узла ФНС, повторить попытку
//                               позже, возвращается, если за 3 минуты не удалось получить данные доверенности,
//                               при ошибке получения статуса возвращается пустая строка и выводится сообщение
//                               об ошибке
//   ФормаВладелец         - ФормаКлиентскогоПриложения
//   ОбновлятьСуществующий - Булево - Истина, если необходимо обновить существующий в базе элемент
//   ЭтоМЧДОрганизации - Булево
//
Процедура ПолучитьДанныеМЧД(ОповещениеОЗавершении,
	ФормаВладелец = Неопределено,
	ОбновлятьСуществующий = Неопределено,
	ЭтоМЧДОрганизации = Ложь) Экспорт
	
	ПараметрыАутентификации = ЭлектронныеДокументыСлужебныйКлиент.АутентификацияНаСайте();
	Если ПараметрыАутентификации = Неопределено Тогда
		ПоказатьПредупреждение(,
			НСтр("ru = 'Не удалось получить данные доверенности. 
			|Необходимо ввести Логин и Пароль для авторизации на сайте поддержки.'"));	
		Возврат;
	КонецЕсли;		

	Результат = Новый Структура;
	Результат.Вставить("СсылкаНаДоверенность", Неопределено);
	Результат.Вставить("СтатусПолучения", "");
	Результат.Вставить("ОткрытьФормуДляОбновления", Ложь);

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеОЗавершении", ОповещениеОЗавершении);
	ДополнительныеПараметры.Вставить("Результат", Результат);
	ДополнительныеПараметры.Вставить("ОбновлятьСуществующий", ОбновлятьСуществующий);
	ДополнительныеПараметры.Вставить("МЧД", Неопределено);
	ДополнительныеПараметры.Вставить("ЭтоМЧДОрганизации", ЭтоМЧДОрганизации);
	ОписаниеОповещения = Новый ОписаниеОповещения("ПолучитьДанныеМЧДПослеВводаРеквизитов", МашиночитаемыеДоверенностиКлиент,
		ДополнительныеПараметры);
	ОткрытьФорму("Справочник.МЧД003.Форма.ЗагрузкаИзРеестраФНС", , , , , ,
		ОписаниеОповещения);
		
КонецПроцедуры

// Получает из РР данные МЧД.
// 
// Параметры:
//  СтруктураДанных - Структура, Неопределено, КодВозвратаДиалога
//  ДополнительныеПараметры - Структура
Процедура ПолучитьДанныеМЧДПослеВводаРеквизитов(
		СтруктураДанных,
		ДополнительныеПараметры) Экспорт
	
	Если СтруктураДанных = Неопределено Или СтруктураДанных = КодВозвратаДиалога.Отмена Тогда
		Если ДополнительныеПараметры.ОповещениеОЗавершении <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеОЗавершении, ДополнительныеПараметры.Результат);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ДанныеДоверенности = МашиночитаемыеДоверенностиВызовСервера.ПолучитьСведенияДоверенностиНаСервереМЧД(
		СтруктураДанных.НомерДоверенности,
		СтруктураДанных.ИННДоверителя);
		
	Если ДанныеДоверенности.Ошибка Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ДанныеДоверенности.ТекстОшибки);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеОЗавершении, ДополнительныеПараметры.Результат);
		Возврат;
			
	КонецЕсли;
	
	ПолныеДанные = ДанныеДоверенности.ПолныеДанные;
	
	ДополнительныеПараметры.Результат.СтатусПолучения = ПолныеДанные.СтатусПолучения;
	
	Если ПолныеДанные.СтатусПолучения = "PENDING" Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ПолучитьДанныеМЧДПослеПоказаПредупреждения",
			МашиночитаемыеДоверенностиКлиент, ДополнительныеПараметры);
		ПоказатьПредупреждение(ОписаниеОповещения,
			НСтр("ru = 'Запрос данных доверенности отправлен успешно, повторите попытку загрузки через несколько минут'"));
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПолныеДанные.ДанныеВыгрузки) Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеОЗавершении, ДополнительныеПараметры.Результат);
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.ОбновлятьСуществующий <> Неопределено Тогда
		ОбновлятьСуществующий = ДополнительныеПараметры.ОбновлятьСуществующий;
	Иначе
		ОбновлятьСуществующий = Истина;
	КонецЕсли;
	
	ДанныеДляЗагрузки = МашиночитаемыеДоверенностиКлиентСервер.НовыеДанныеДляЗагрузкиМЧД();
	ДанныеДляЗагрузки.ДанныеДоверенности = ПолныеДанные.ДанныеВыгрузки;
	ДанныеДляЗагрузки.ДанныеПодписи = ПолныеДанные.ДанныеПодписи;
	ДанныеДляЗагрузки.ДанныеПодписиЗаявленияНаОтмену = ПолныеДанные.ДанныеПодписиЗаявленияНаОтмену;
	ДанныеДляЗагрузки.ДанныеПодписей.Добавить(ПолныеДанные.ДанныеПодписи);
	ТребуетсяПроверкаМЧДНаКлиенте = Ложь;
	
	СтатусМЧД = ДанныеДоверенности.ЧастичныеДанные.СтатусДоверенности;
	
	СтатусВРеестреФНС = МашиночитаемыеДоверенностиКлиентСервер.СтатусВРеестреФНС(СтатусМЧД);
	
	ДополнительныеСведения = МашиночитаемыеДоверенностиКлиентСервер.ДополнительныеСведенияПоЗагрузкеМЧД();
	ДополнительныеСведения.СтатусВРеестреФНС = СтатусВРеестреФНС;
	
	Если СтруктураДанных.Свойство("ВладелецДоверенности") Тогда
		Если СтруктураДанных.ВладелецДоверенности = "Организация" Тогда
			ДополнительныеСведения.ЭтоМЧДОрганизации = Истина;
		ИначеЕсли СтруктураДанных.ВладелецДоверенности = "Контрагент" Тогда
			ДополнительныеСведения.ЭтоМЧДКонтрагента = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ДополнительныеПараметры.Свойство("ЭтоМЧДОрганизации") Тогда
		ДополнительныеСведения.ЭтоМЧДОрганизации = ДополнительныеПараметры.ЭтоМЧДОрганизации;
	КонецЕсли;
		
	РезультатЗагрузки =
		МашиночитаемыеДоверенностиВызовСервера.ЗагрузитьМЧД(ДанныеДляЗагрузки, ТребуетсяПроверкаМЧДНаКлиенте, 
			ОбновлятьСуществующий, ДополнительныеСведения);
		
	ДополнительныеПараметры.Результат.СсылкаНаДоверенность = РезультатЗагрузки.МЧД;
	Если РезультатЗагрузки.Свойство("ОткрытьФормуДляОбновления") Тогда
		ДополнительныеПараметры.Результат.ОткрытьФормуДляОбновления = РезультатЗагрузки.ОткрытьФормуДляОбновления;
	КонецЕсли;
	
	КонтекстДиагностики = Неопределено;
	ДополнительныеПараметры.МЧД = РезультатЗагрузки.МЧД;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПолучитьДанныеМЧДПослеВводаРеквизитовЗавершение", МашиночитаемыеДоверенностиКлиент,
		ДополнительныеПараметры);
		
	ПроверитьДанныеМЧД(ОповещениеОЗавершении, РезультатЗагрузки, КонтекстДиагностики);
	
КонецПроцедуры

// Завершение получения данных МЧД и подписи после ввода реквизитов на форме
//
// Параметры:
//  РезультатПроверки - см. МашиночитаемыеДоверенности.РезультатПроверкиДоверенности
//  ДополнительныеПараметры - Структура
//
Процедура ПолучитьДанныеМЧДПослеВводаРеквизитовЗавершение(РезультатПроверки, ДополнительныеПараметры) Экспорт

	МЧД = ДополнительныеПараметры.МЧД;

	Если ЗначениеЗаполнено(РезультатПроверки) Тогда
		
		Если РезультатПроверки.Свойство("ТекстОшибки") 
			И ЗначениеЗаполнено(РезультатПроверки.ТекстОшибки) Тогда
			ДополнительныеПараметры.Результат.Вставить("ТекстОшибки", РезультатПроверки.ТекстОшибки);
		КонецЕсли;		
		
		КонтекстДиагностики = РезультатПроверки.КонтекстДиагностики;
		ОбработатьРезультатПроверкиМЧД(РезультатПроверки);
		МашиночитаемыеДоверенностиВызовСервера.ОтразитьРезультатПроверкиМЧД(МЧД, РезультатПроверки.Результат, 
			РезультатПроверки.ТекстОшибки);
	Иначе
		КонтекстДиагностики = Неопределено;
	КонецЕсли;
	
	МашиночитаемыеДоверенностиВызовСервера.ЗагрузитьРодительскиеДоверенности(МЧД);

	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеОЗавершении, ДополнительныеПараметры.Результат); 
	
КонецПроцедуры 

// Завершение процедуры регистрации МЧД после подписи 
//
// Параметры:
//  РезультатПроверки - см. МашиночитаемыеДоверенности.РезультатПроверкиДоверенности
//  ДополнительныеПараметры - Структура:
//   * МЧД - СправочникСсылка.МашиночитаемыеДоверенностиКонтрагентов
//         - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
//   * КонтекстДиагностики - См. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
//
Процедура ЗарегистрироватьМЧДПослеПодписиЗавершение(РезультатПроверки, ДополнительныеПараметры) Экспорт

	МЧД = ДополнительныеПараметры.СсылкаНаДоверенность;
	
	Если ЗначениеЗаполнено(РезультатПроверки) Тогда
		КонтекстДиагностики = РезультатПроверки.КонтекстДиагностики;
		ОбработатьРезультатПроверкиМЧД(РезультатПроверки);
		МашиночитаемыеДоверенностиВызовСервера.ОтразитьРезультатПроверкиМЧД(МЧД, РезультатПроверки.Результат, 
			РезультатПроверки.ТекстОшибки);
	Иначе
		КонтекстДиагностики = Неопределено;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеОЗавершении, ДополнительныеПараметры.Результат); 
	
КонецПроцедуры

Процедура ПолучитьДанныеМЧДПослеПоказаПредупреждения(
		ДополнительныеПараметры) Экспорт
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеОЗавершении, ДополнительныеПараметры.Результат);
	
КонецПроцедуры

// Запрашивает текст причины отмены (отзыва), выгружает заявление на отмену машиночитаемой доверенности, предлагает
// выбрать/подтвердить выбор сертификата, подписывает и отправляет заявление на регистрацию в распределенном реестре
// ФНС, ожидает 1-3 минуты, получает статус транзакции заявления, обновляет статус в записи справочника доверенностей,
// в том числе запоминает идентификатор транзакции.
//
// Параметры:
//   ОповещениеОЗавершении - ОписаниеОповещения - описание процедуры, принимающей результат.
//     Результат - структура:
//      * ИдентификаторТранзакции - Строка - при ошибке отправки на регистрацию выводится сообщение об ошибке
//                                           и возвращается пустая строка
//      * СтатусТранзакции        - Строка - "PENDING" - транзакция майнится, "SUCCESS" - транзакция смайнилась,
//                                           "FAILURE" - ошибка при майнинге транзакции, при ошибке получения
//                                           статуса возвращается пустая строка и выводится сообщение об ошибке
//      * ДатаВремяТранзакции     - Дата
//   СсылкаНаДоверенность  - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
//   ФормаДоверенности     - ФормаКлиентскогоПриложения
//
Процедура ОтменитьМЧД(
		ОповещениеОЗавершении = Неопределено,
		СсылкаНаДоверенность = Неопределено,
		ФормаДоверенности = Неопределено) Экспорт
		
	ПараметрыАутентификации = ЭлектронныеДокументыСлужебныйКлиент.АутентификацияНаСайте();
	Если ПараметрыАутентификации = Неопределено Тогда
		ПоказатьПредупреждение(,
			НСтр("ru = 'Не удалось отозвать доверенность в реестре. 
			|Необходимо ввести Логин и Пароль для авторизации на сайте поддержки.'"));	
		Возврат;
	КонецЕсли;		
	
	Результат = Новый Структура;
	Результат.Вставить("ИдентификаторТранзакции", 	"");
	Результат.Вставить("СтатусТранзакции", 			"");
	Результат.Вставить("ДатаВремяТранзакции", 		Неопределено);
	
	Если ФормаДоверенности <> Неопределено И НЕ ФормаДоверенности.Записать() Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не удалось сохранить доверенность'"));
		Если ОповещениеОЗавершении <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, Результат);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	СсылкаНаДоверенностьСУчетомФормы = ?(ФормаДоверенности = Неопределено, СсылкаНаДоверенность,
		ФормаДоверенности.Объект.Ссылка);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеОЗавершении", 	ОповещениеОЗавершении);
	ДополнительныеПараметры.Вставить("СсылкаНаДоверенность", 	СсылкаНаДоверенностьСУчетомФормы);
	ДополнительныеПараметры.Вставить("ФормаДоверенности", 		ФормаДоверенности);
	ДополнительныеПараметры.Вставить("Результат", 				Результат);

	НаборДействий = Новый Соответствие;	
	НаборДействий.Вставить(ПредопределенноеЗначение("Перечисление.ОжидаемоеДействиеЭД.Подписать"), Истина);
		
	ДополнительныеПараметры.Вставить("ОповещениеУспешногоЗавершения",
		Новый ОписаниеОповещения("ОтменитьМЧДПослеПодписи", МашиночитаемыеДоверенностиКлиент, НаборДействий));
		
	НомерДоверенности = "";
	Если ФормаДоверенности = Неопределено Тогда
		Доверенности = МашиночитаемыеДоверенностиКлиентСервер.ЗначениеВМассиве(СсылкаНаДоверенность);
		НомераДоверенностей = МашиночитаемыеДоверенностиВызовСервера.ПолучитьНомераДоверенностей(Доверенности);
		НомерДоверенности = НомераДоверенностей[СсылкаНаДоверенность];
	Иначе
		НомерДоверенности = ФормаДоверенности.Объект.НомерДоверенности;
	КонецЕсли;
	РезультатВыгрузки = МашиночитаемыеДоверенностиВызовСервера.ВыгрузитьЗаявлениеНаОтменуМЧД(НомерДоверенности, "Отзыв");
	ДополнительныеПараметры.Вставить("РезультатВыгрузки", РезультатВыгрузки);
	ДанныеXMLВыгрузки = ПолучитьДвоичныеДанныеИзСтроки(ДополнительныеПараметры.РезультатВыгрузки.Содержимое, "windows-1251");
	ДополнительныеПараметры.Вставить("ДанныеXMLВыгрузки", ДанныеXMLВыгрузки);

	Оповещение = Новый ОписаниеОповещения("ПослеВыполненияДействийПоЭДООтзыв", МашиночитаемыеДоверенностиКлиент, ДополнительныеПараметры);
	
	РезультатПодписания = ПодписатьДвоичныеДанныеМЧД(ДанныеXMLВыгрузки, СсылкаНаДоверенностьСУчетомФормы);
	
	Если РезультатПодписания <> Неопределено Тогда
		ПослеВыполненияДействийПоЭДООтзыв(РезультатПодписания, ДополнительныеПараметры);
	КонецЕсли;	
		
КонецПроцедуры

Процедура ОтменитьМЧДПослеПодписи(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ Результат.ПодписьВыполнена Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон(
			НСтр("ru = 'Не удалось подписать заявление на отмену доверенности: %1'"),
			Результат.ОписаниеОшибки));
		Если ДополнительныеПараметры.ОповещениеОЗавершении <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеОЗавершении, ДополнительныеПараметры.Результат);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ФормаДоверенности = ДополнительныеПараметры.ФормаДоверенности;
	
	ИмяФайлаЗаявленияНаОтзыв = "revoke.xml";
	РезультатРегистрации = МашиночитаемыеДоверенностиВызовСервера.ОтменитьМЧД(
		ИмяФайлаЗаявленияНаОтзыв,
		Результат.ПодписанныеДанные,
		Результат.Подпись, 
		Результат.Сертификат, ,
		?(ФормаДоверенности = Неопределено, "", ФормаДоверенности.Объект.НомерДоверенности),
		ДополнительныеПараметры.СсылкаНаДоверенность);
	
	СтатусТранзакции = ?(ЗначениеЗаполнено(РезультатРегистрации.ИдентификаторТранзакции), "PENDING", "");
	
	ДополнительныеПараметры.Результат.ИдентификаторТранзакции 	= РезультатРегистрации.ИдентификаторТранзакции;
	ДополнительныеПараметры.Результат.СтатусТранзакции 			= СтатусТранзакции;
	ДополнительныеПараметры.Результат.ДатаВремяТранзакции 		= Неопределено;
	
	Если ФормаДоверенности <> Неопределено И ЗначениеЗаполнено(РезультатРегистрации.ИдентификаторТранзакции) Тогда
		ЭтоМЧД003 = ТипЗнч(ДополнительныеПараметры.СсылкаНаДоверенность) = Тип("СправочникСсылка.МЧД003");
		Если Не ЭтоМЧД003 Тогда
			ФормаДоверенности.Прочитать();
			ФормаДоверенности.Объект.СтатусВРеестреФНС =
				ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ОтправленоЗаявлениеНаОтзыв");
			ФормаДоверенности.Объект.ДатаОтправкиЗаявленияНаОтзыв = ОбщегоНазначенияКлиент.ДатаСеанса();
			ФормаДоверенности.Объект.ДатаОбновленияСтатуса = ФормаДоверенности.Объект.ДатаОтправкиЗаявленияНаОтзыв;
			ФормаДоверенности.Объект.ИдентификаторТранзакции = РезультатРегистрации.ИдентификаторТранзакции;
			ФормаДоверенности.Объект.ИмяФайлаЗаявленияНаОтзыв = ИмяФайлаЗаявленияНаОтзыв;
			ФормаДоверенности.Объект.ОтпечатокСертификата = ДополнительныеПараметры.ОтпечатокСертификатаАбонента;
			ФормаДоверенности.Записать();
		Иначе
			ФормаДоверенности.Прочитать();
			ФормаДоверенности.Объект.СтатусВРеестреФНС =
				ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ОтправленоЗаявлениеНаОтзыв");
			ФормаДоверенности.Объект.ДатаОтправкиЗаявленияНаОтзыв = ОбщегоНазначенияКлиент.ДатаСеанса();
			ФормаДоверенности.Объект.ДатаОбновленияСтатуса = ФормаДоверенности.Объект.ДатаОтправкиЗаявленияНаОтзыв;
			ФормаДоверенности.Объект.ИдентификаторТранзакции = РезультатРегистрации.ИдентификаторТранзакции;
			ФормаДоверенности.Объект.ИмяФайлаЗаявленияНаОтзыв = ИмяФайлаЗаявленияНаОтзыв;
			ФормаДоверенности.Записать();
		КонецЕсли;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеОЗавершении, ДополнительныеПараметры.Результат);
	
КонецПроцедуры

// Завершение загрузки в элемент справочника данных из архива с файлом МЧД и подписью.
//
// Параметры:
//  РезультатПроверки - см. МашиночитаемыеДоверенности.РезультатПроверкиДоверенности
//  ДополнительныеПараметры - Структура:
//   * МЧД - СправочникСсылка.МашиночитаемыеДоверенностиКонтрагентов
//         - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
//
Процедура ЗагрузитьПерезаполнитьМЧДИзФайлаЗавершение(РезультатПроверки, ДополнительныеПараметры) Экспорт

	МЧД = ДополнительныеПараметры.МЧД;

	Если ЗначениеЗаполнено(РезультатПроверки) Тогда
		КонтекстДиагностики = РезультатПроверки.КонтекстДиагностики;
		ОбработатьРезультатПроверкиМЧД(РезультатПроверки);
		МашиночитаемыеДоверенностиВызовСервера.ОтразитьРезультатПроверкиМЧД(МЧД, РезультатПроверки.Результат, 
			РезультатПроверки.ТекстОшибки);
	Иначе
		КонтекстДиагностики = Неопределено;
	КонецЕсли;
	
	ОповеститьОбИзменении(МЧД);
	ПоказатьЗначение(Неопределено, МЧД);
	
КонецПроцедуры

// Обработка результата проверки МЧД.
//
// Параметры:
//  РезультатПроверки - см. МашиночитаемыеДоверенности.РезультатПроверкиДоверенности
//
Процедура ОбработатьРезультатПроверкиМЧД(РезультатПроверки)
	
	Если Не РезультатПроверки.Результат Тогда

	КонецЕсли;
		
КонецПроцедуры

#Область ПроверкаДоверенности

// Обрабатывает результат ручной проверки полномочий МЧД.
// 
// Параметры:
//  Результаты - Неопределено 
//  		  - Массив из Структура:
//  * ПодписанныйОбъект - СправочникСсылка.СообщениеЭДОПрисоединенныеФайлы
//  * ХешПодписи - Строка
//  * ПолномочияПодтверждены - Булево
//  Ошибки - Соответствие из КлючИЗначение:
//	* Ключ - см. ОбработкаНеисправностейБЭДКлиентСервер.НоваяОшибка
//	* Значение - Массив из Структура:
//	  ** ПодписанныйОбъект - СправочникСсылка.СообщениеЭДОПрисоединенныеФайлы
//	  ** ХешПодписи - Строка
//
// Возвращаемое значение:
// 	Булево
Функция ОбработатьРезультатПроверкиПолномочий(Результаты) Экспорт
	
	Если НЕ (ТипЗнч(Результаты) = Тип("Массив") И Результаты.Количество() > 0) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Результат = Результаты[0];
	
	ПолномочияПодтверждены = Результат.ПолномочияПодтверждены;
	
	ПроверкаПолномочий = МашиночитаемыеДоверенностиКлиентСервер.НовыйРезультатПроверки();
	ПроверкаПолномочий.Выполнено = Истина;
	ПроверкаПолномочий.Успех = ПолномочияПодтверждены;
	ПроверкаПолномочий.РучнаяПроверка = Истина;
	Если НЕ ПолномочияПодтверждены Тогда
		ПроверкаПолномочий.Ошибка =
			МашиночитаемыеДоверенностиКлиентСервер.ТекстНеобходимостиРучнойПроверкиПолномочий();
	КонецЕсли;
	
	ДанныеПроверки = МашиночитаемыеДоверенностиКлиентСервер.НовыеДанныеПроверкиПолномочий();
	ДанныеПроверки.ПодписанныйОбъект = Результат.ПодписанныйОбъект;
	ДанныеПроверки.ХешПодписи = Результат.ХешПодписи;
	ДанныеПроверки.РезультатПроверки = ПроверкаПолномочий;
	ДанныеПроверки.РучнаяПроверка = Истина;
	
	ВыполненныеПроверки = МашиночитаемыеДоверенностиКлиентСервер.ЗначениеВМассиве(ДанныеПроверки);
	
	РезультатЗаписиПроверкиПолномочий = МашиночитаемыеДоверенностиВызовСервера.ЗаписатьРезультатПроверкиПолномочий(ВыполненныеПроверки);
	
	Если РезультатЗаписиПроверкиПолномочий.Успех Тогда
		Возврат Истина;
	ИначеЕсли НЕ ПустаяСтрока(РезультатЗаписиПроверкиПолномочий.ТекстОшибки) Тогда
		
		ТекстОшибки = НСтр("ru = 'Ошибка при записи результатов проверки полномочий:'");
		ТекстОшибки = ТекстОшибки + РезультатЗаписиПроверкиПолномочий.ТекстОшибки;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Открывает форму проверки полномочий доверенностей
// 
// Параметры:
//  ПараметрыПроверки - Массив из см. МашиночитаемыеДоверенностиКлиентСервер.НовыеПараметрыПроверкиПолномочий
//  ОповещениеОЗавершении - ОписаниеОповещения
//    						
Процедура ПроверитьПолномочияДоверенностейВручную(ПараметрыПроверки, ОповещениеОЗавершении)
			
КонецПроцедуры

// Открывает форму проверки полномочий доверенности
// 
// Параметры:
//  ПараметрыПроверки - см. МашиночитаемыеДоверенностиКлиентСервер.НовыеПараметрыПроверкиПолномочий
//  ОповещениеОЗавершении - ОписаниеОповещения
//    						
Процедура ПроверитьПолномочияДоверенностиВручную(ПараметрыПроверки, ОповещениеОЗавершении) Экспорт
	
	ОткрытьФорму("Обработка.РезультатыПроверкиПодписи.Форма.ПроверкаПолномочийДоверенности",
		ПараметрыПроверки,,,,, ОповещениеОЗавершении, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

// Продолжение процедуры ПроверитьДоверенность.
// 
// Параметры:
//  Результат - см. КриптографияБЭДКлиентСервер.НовыйРезультатПроверкиПодписи
//  Контекст - см. КонтекстПроверкиДоверенности
Процедура ПослеПроверкиПодписи(Результат, Контекст) Экспорт
	
	Результат = МашиночитаемыеДоверенностиВызовСервера.РезультатПроверкиДоверенности(Контекст.ДанныеДоверенности,
		Результат, Контекст.КонтекстДиагностики);
		
	ВыполнитьОбработкуОповещения(Контекст.Оповещение, Результат);
	
КонецПроцедуры

// Возвращает контекст проверки доверенности.
// 
// Возвращаемое значение:
//  Структура:
// * ДанныеДоверенности - ДвоичныеДанные
//                      - Неопределено
// * Оповещение - ОписаниеОповещения
// * КонтекстДиагностики - см. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
Функция КонтекстПроверкиДоверенности()
	
	Контекст = Новый Структура;
	Контекст.Вставить("ДанныеДоверенности", Неопределено);
	Контекст.Вставить("Оповещение", Неопределено);
	Контекст.Вставить("КонтекстДиагностики", Неопределено);
	
	Возврат Контекст;
	
КонецФункции

#КонецОбласти

#Область ПроверкаВводаТекстовогоОписанияПолномочия

Функция ПроверитьТекстНаСоответствиеПравиламОбработки(Текст) Экспорт
	
	КоличествоСимволов = СтрДлина(Текст);

	КоличествоПереносовСтроки = СтрЧислоВхождений(Текст, Символы.ПС) * 2;
	ВсегоСимволов = КоличествоСимволов + КоличествоПереносовСтроки;

	Если ВсегоСимволов >= 1000 Тогда

		РазницаСимволов = ВсегоСимволов - 1000;
		
		ИтоговыйТекст = Лев(Текст, 1000 - РазницаСимволов);
		Текст = ИтоговыйТекст;
		
		Возврат Ложь;
		
	Иначе
		
		Возврат Истина;
		
	КонецЕсли;
	
КонецФункции

Функция КомментарийОшибкиПроверкиТекстаНаСоответствиеПравиламОбработки() Экспорт

	Возврат НСтр("ru = 'В одной строке полномочий для корректной регистрации МЧД может быть не более одной тысячи символов. 
		|Проверьте и при необходимости добавьте новую строку и продолжите ввод.'");

КонецФункции

#КонецОбласти

#Область Подписание

// Возвращаемое значение:
//  Структура:
//  * ПодписанныеДоверенности - см. НовыеПодписанныеДоверенности
//  * КонтекстДиагностики - см. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
Функция НовыйРезультатПодписания()
	Результат = Новый Структура;
	Результат.Вставить("ПодписанныеДоверенности", НовыеПодписанныеДоверенности());
	Результат.Вставить("КонтекстДиагностики", Неопределено);
	Возврат Результат;
КонецФункции

// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//  * Ключ - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций,СправочникСсылка.МЧД003
//  * Значение - см. НовыеСвойстваПодписаннойДоверенности
Функция НовыеПодписанныеДоверенности()
	Возврат Новый Соответствие;
КонецФункции

// Возвращаемое значение:
//  Структура:
//  * Данные - Неопределено,ДвоичныеДанные
//  * СвойстваПодписи - см. КриптографияБЭДКлиентСервер.НовыеСвойстваПодписи
Функция НовыеСвойстваПодписаннойДоверенности()
	Свойства = Новый Структура;
	Свойства.Вставить("Данные", Неопределено);
	Свойства.Вставить("СвойстваПодписи", Новый Структура);
	Возврат Свойства;
КонецФункции

// Возвращаемое значение:
//  Структура -  Новый контекст подписания:
//  * ОповещениеОЗавершении - см. Подписать.ОповещениеОЗавершении
//  * Доверенности - см. Подписать.Доверенности
//  * КонтекстДиагностики - см. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
//  * ПаролиСертификатов - см. КриптографияБЭДКлиент.НовыеПаролиСертификатов
//  * ДанныеДляПодписания - см. МашиночитаемыеДоверенностиВызовСервера.ДанныеДляПодписания
//  * ИндексНабораДанных - Число
//  * ПодписанныеДоверенности - см. НовыеПодписанныеДоверенности
Функция НовыйКонтекстПодписания()
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеОЗавершении", Неопределено);
	Контекст.Вставить("Доверенности", Новый Массив);
	Контекст.Вставить("КонтекстДиагностики", Неопределено);
	Контекст.Вставить("ПаролиСертификатов", Неопределено);
	Контекст.Вставить("ДанныеДляПодписания", Новый Массив);
	Контекст.Вставить("ИндексНабораДанных", 0);
	Контекст.Вставить("ПодписанныеДоверенности", НовыеПодписанныеДоверенности());
	Возврат Контекст;
КонецФункции

#КонецОбласти

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// Адаптация

// Только для внутреннего использования
Процедура ОформитьГруппуСостоянияИСтатусыМЧД(Форма, Подписана, Верна, Отозвана, ДатаОтзыва, СтатусВРеестреФНС) Экспорт
	
	Элементы = Форма.Элементы;
	
	Если МашиночитаемыеДоверенностиКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "Объект") Тогда
		Если Не ЗначениеЗаполнено(Форма.Объект.Ссылка) Тогда
			Элементы.ГруппаСостоянияИСтатусы.Видимость = Ложь;
			Возврат;
		Иначе
			Элементы.ГруппаСостоянияИСтатусы.Видимость = Истина;		
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ГруппаНеверна.Видимость = Ложь;
	#Если ТонкийКлиент Тогда
		ЦветаФона = МашиночитаемыеДоверенностиВызовСервера.ЦветаФона();
	#Иначе
		ЦветаФона = Новый Структура();
		ЦветаФона.Вставить("ВниманиеМЧД", ЦветаСтиля.ЦветФонаВниманиеМЧД);
		ЦветаФона.Вставить("ДействительнаяМЧД", ЦветаСтиля.ЦветФонаДействительнаяМЧД);
		ЦветаФона.Вставить("НедействительнаяМЧД", ЦветаСтиля.ЦветФонаНедействительнаяМЧД);
	#КонецЕсли
	
	Элементы.ГруппаПометкаОтозвана.Видимость = Отозвана;
	Если Отозвана И (ДатаОтзыва > ОбщегоНазначенияКлиент.ДатаСеанса()) Тогда
		ТекстЗаголовка = МашиночитаемыеДоверенностиКлиентСервер.ЗаголовокБудетОтозвана();
		Элементы.ДекорацияПометкаОтозвана.Заголовок = ТекстЗаголовка + " " + Формат(ДатаОтзыва, "ДЛФ=Д");
		Элементы.ДекорацияПометкаОтозванаКартинка.Картинка = БиблиотекаКартинок.ОформлениеЗнакВоcклицательныйЗнакБЭД;
	ИначеЕсли Отозвана И (ДатаОтзыва <= ОбщегоНазначенияКлиент.ДатаСеанса()) Тогда
		Элементы.ГруппаСостоянияИСтатусы.ЦветФона = ЦветаФона.ВниманиеМЧД;
	КонецЕсли;
	
	Если Не Подписана Тогда
		Элементы.ДекорацияПодписана.Заголовок = НСтр("ru = 'Требуется подписать'");
		Элементы.ГруппаСостоянияИСтатусы.ЦветФона = ЦветаФона.НедействительнаяМЧД;
	Иначе
		Элементы.ДекорацияПодписана.Заголовок = НСтр("ru = 'Подписана'");
		Элементы.ГруппаСостоянияИСтатусы.ЦветФона = ЦветаФона.ДействительнаяМЧД;		
	КонецЕсли;
	
	Если (Не Верна) И Подписана Тогда
		Элементы.ГруппаСостоянияИСтатусы.ЦветФона = ЦветаФона.НедействительнаяМЧД;
		Элементы.ГруппаНеверна.Видимость = Истина;
		Элементы.ДекорацияНеверна.Заголовок = НСтр("ru = 'Неверна'");
		Элементы.ДекорацияВернаКартинка.Картинка = БиблиотекаКартинок.ОформлениеЗнакКрестБЭД;
	КонецЕсли;
	
	Если Верна Тогда
		Элементы.ДекорацияНеверна.Заголовок = НСтр("ru = 'Верна'");
		Элементы.ГруппаНеверна.Видимость = Истина;
		Элементы.ДекорацияВернаКартинка.Картинка = БиблиотекаКартинок.ОформлениеЗнакФлажокБЭД;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтатусВРеестреФНС) Тогда
		ТекстЗаголовка = НСтр("ru = 'В реестре ФНС: %1'");
		Элементы.ГруппаСтатусВРеестреФНС.Видимость = Истина;
		Элементы.ДекорацияСтатусВРеестреФНС.Заголовок = СтрШаблон(ТекстЗаголовка, СтатусВРеестреФНС);
	Иначе
		Элементы.ГруппаСтатусВРеестреФНС.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Только для внутреннего использования
Функция ПодписатьДвоичныеДанныеМЧД(ДвоичныеДанные, МЧД) Экспорт
	
	ФлагИспользованияЭЦП = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьЗначениеФункциональнойОпции(
		"ИспользоватьЭлектронныеЦифровыеПодписи");
	
	Если НЕ ФлагИспользованияЭЦП Тогда
		ТекстСообщения = ЭлектронныеДокументыСлужебныйВызовСервера.ТекстСообщенияОНеобходимостиНастройкиСистемы("ПодписаниеЭД");
		ВидОперации = НСтр("ru = 'Подписание доверенности'");
		ЭлектронныеДокументыСлужебныйВызовСервера.ОбработатьИсключениеПоЭДНаСервере(ВидОперации, ТекстСообщения, ТекстСообщения);
		Возврат Неопределено;
	КонецЕсли;  		
	
	ВыполнятьКриптооперацииНаСервере = ЭлектронныеДокументыСлужебныйВызовСервера.ВыполнятьКриптооперацииНаСервере();

	Попытка
		Если ВыполнятьКриптооперацииНаСервере Тогда		
			МассивСтруктурСертификатов = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьМассивСтруктурСертификатов(Истина);
		Иначе													
			МассивСтруктурСертификатов = ЭлектроннаяЦифроваяПодписьКлиент.ПолучитьМассивСтруктурСертификатов(Истина);
		КонецЕсли;
	Исключение
		
		МассивСтруктурСертификатовСервер = Новый Массив;
		Если ВыполнятьКриптооперацииНаСервере Тогда	
			ТекстСообщения = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("115");
		Иначе
			ТекстСообщения = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("105");
		КонецЕсли;
		ЭлектронныеДокументыСлужебныйВызовСервера.ОбработатьИсключениеПоЭДНаСервере(ВидОперации, ТекстСообщения, ТекстСообщения);
		Возврат Неопределено;
	КонецПопытки;
	
	ДоступныеСертификатыПользователя = МашиночитаемыеДоверенностиВызовСервера.ПолучитьСертификатыДляПодписанияМЧД(МЧД, МассивСтруктурСертификатов);
	
	Если ДоступныеСертификатыПользователя.Количество() = 0 Тогда
		ПодробныйТекстОшибки = НСтр("ru = 'В процессе подписания доверенности не найден ни один из доступных сертификатов ЭП.'");
		ТекстОшибки = НСтр("ru = 'Не найден доступный сертификат для подписания.'");
		ВидОперации = НСтр("ru = 'Подписание доверенности'");
		ЭлектронныеДокументыСлужебныйВызовСервера.ОбработатьИсключениеПоЭДНаСервере(ВидОперации, ТекстСообщения, ПодробныйТекстОшибки);
	КонецЕсли;
	
	Соответствие = Новый Соответствие;
	
	Для Каждого ДанныеСертификата Из ДоступныеСертификатыПользователя Цикл
		Соответствие.Вставить(ДанныеСертификата.Ссылка, ДанныеСертификата);
	КонецЦикла;
	
	ВидОперации = НСтр("ru = 'Подписание МЧД'");
	Если ЭлектронныеДокументыСлужебныйКлиент.ПарольКСертификатуПолучен(Соответствие, ВидОперации, МЧД)
		И Соответствие.Количество() > 0 Тогда
		Для Каждого КлючИЗначение Из Соответствие Цикл
			ПараметрыСертификата = КлючИЗначение.Значение;
			Прервать;
		КонецЦикла;
	Иначе
		Возврат Неопределено;
	КонецЕсли;	
	
	
	Если ВыполнятьКриптооперацииНаСервере Тогда
		
		ДанныеПодписи = ЭлектронныеДокументыСлужебныйВызовСервера.ПодписатьДвоичныеДанныеОпределеннымСертификатом(
															ДвоичныеДанные,
															ПараметрыСертификата);
	Иначе													
	
		ДанныеПодписи = ЭлектронныеДокументыСлужебныйКлиент.ПодписатьДвоичныеДанныеОпределеннымСертификатом(
															ДвоичныеДанные,
															ПараметрыСертификата);
	КонецЕсли;
															
	
	Если ДанныеПодписи = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;

	РезультатПодписания = Новый Структура;
	РезультатПодписания.Вставить("ПодписанныеДанные", ДвоичныеДанные);
	РезультатПодписания.Вставить("ОшибкиФормирования", Новый Массив);
	
	СвойстваПодписи = Новый Структура;
	СвойстваПодписи.Вставить("ДатаПодписи", ДанныеПодписи.ДатаПодписи);
	СвойстваПодписи.Вставить("Подпись", ДанныеПодписи.НоваяПодписьДвоичныеДанные);
	СвойстваПодписи.Вставить("Отпечаток", ДанныеПодписи.Отпечаток);//Base64
	
	РезультатПодписания.Вставить("СвойстваПодписи", СвойстваПодписи);	
	
	Возврат РезультатПодписания; 
	
КонецФункции

Процедура ПроверитьДоверенностиИзСодержимогоПроизвольногоЭД() Экспорт
		
КонецПроцедуры
