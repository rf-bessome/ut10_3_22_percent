
#Область СлужебныйПрограммныйИнтерфейс

#Область ПроверкаДоверенности

// Формирует пустую структуру данных проверки полномочий.
// 
// Возвращаемое значение:
//  Структура:
// * ПодписанныйОбъект - СправочникСсылка.СообщениеЭДОПрисоединенныеФайлы
// * ХешПодписи - Строка
// * РезультатПроверки - см. НовыйРезультатПроверки
// * РучнаяПроверка - Булево
// * НомерРодительскойДоверенности - Строка
// 
Функция НовыеДанныеПроверкиПолномочий() Экспорт
	
	ДанныеПроверки = Новый Структура();
	ДанныеПроверки.Вставить("ПодписанныйОбъект",
		ПредопределенноеЗначение("Справочник.ЭДПрисоединенныеФайлы.ПустаяСсылка"));
	ДанныеПроверки.Вставить("ХешПодписи", "");
	ДанныеПроверки.Вставить("РезультатПроверки", НовыйРезультатПроверки());
	ДанныеПроверки.Вставить("РучнаяПроверка", Ложь);
	ДанныеПроверки.Вставить("НомерРодительскойДоверенности", "");
	
	Возврат ДанныеПроверки;
	
КонецФункции

// Возвращает новый результат проверки.
// 
// Возвращаемое значение:
//  Структура - Новый результат проверки:
// * Выполнено - Булево
// * ДатаПроверки - Дата
// * Успех - Булево
// * Ошибка - Строка
// * НомерДоверенности - Строка
// * РучнаяПроверка - Булево
// * ПредставлениеПользователя - Строка - Представление пользователя от лица которого была выполнена ручная проверка.
// * ИдентификаторПользователя - Строка
//
Функция НовыйРезультатПроверки() Экспорт
	
	РезультатПроверки = Новый Структура;
	РезультатПроверки.Вставить("Выполнено", Ложь);
	РезультатПроверки.Вставить("ДатаПроверки", '00010101');
	РезультатПроверки.Вставить("Успех", Ложь);
	РезультатПроверки.Вставить("Ошибка", "");
	РезультатПроверки.Вставить("НомерДоверенности", "");
	РезультатПроверки.Вставить("РучнаяПроверка", Ложь);
	РезультатПроверки.Вставить("ПредставлениеПользователя", "");
	РезультатПроверки.Вставить("ИдентификаторПользователя", "");
	
	Возврат РезультатПроверки;
	
КонецФункции

// Инициализирует структуру для проверки МЧД.
// 
// Возвращаемое значение:
//  Структура:
// * ДанныеДоверенности - ДвоичныеДанные
// * ДанныеПодписи - ДвоичныеДанные
//
Функция НовыеДанныеДляПроверкиМЧД() Экспорт
	
	ДанныеДляПроверки = Новый Структура;
	ДанныеДляПроверки.Вставить("ДанныеДоверенности", ПустыеДвоичныеДанные());
	ДанныеДляПроверки.Вставить("ДанныеПодписи", ПустыеДвоичныеДанные());
	
	Возврат ДанныеДляПроверки;
	
КонецФункции

// Возвращает вид ошибки, для документа имеющего подписи с невалидными МЧД
// 
// Возвращаемое значение:
// 	См. ОбработкаНеисправностейБЭДКлиентСервер.НовоеОписаниеВидаОшибки
// 	
Функция ВидОшибкиЕстьПодписиСНепровереннымиПолномочиямиМЧД() Экспорт
	
	ВидОшибки = НовоеОписаниеВидаОшибки();
	ВидОшибки.Идентификатор = "ЕстьПодписиСНепровереннымиПолномочиямиМЧД";
	ВидОшибки.ЗаголовокПроблемы = НСтр("ru = 'Документ содержит подписи с
		|непроверенными полномочиями'");
	ВидОшибки.ОписаниеРешения =
		НСтр("ru = '<a href = ""Выполните"">Выполните</a> ручную проверку полномочий'");
	ВидОшибки.ОбработчикиНажатия.Вставить("Выполните",
		"МашиночитаемыеДоверенностиКлиент.ОткрытьФормуПроверкиПолномочий");
	ВидОшибки.ВыполнятьОбработчикАвтоматически = Истина;
	ВидОшибки.АвтоматическиВыполняемыйОбработчик = "МашиночитаемыеДоверенностиКлиент.ОткрытьФормуПроверкиПолномочий";
	
	Возврат ВидОшибки;
	
КонецФункции

// Возвращает вид ошибки для документа, имеющего подписи с непроверенными МЧД, когда доверенность не найдена.
// 
// Возвращаемое значение:
// 	См. ОбработкаНеисправностейБЭДКлиентСервер.НовоеОписаниеВидаОшибки
Функция ВидОшибкиЕстьПодписиСМЧДДоверенностьНеНайдена() Экспорт
	
	ВидОшибки = НовоеОписаниеВидаОшибки();
	ВидОшибки.Идентификатор = "ЕстьПодписиСНепровереннымиПолномочиямиМЧДДоверенностьНеНайдена";
	ВидОшибки.ЗаголовокПроблемы = НСтр("ru = 'Для автоматической проверки подписи требуется электронная доверенность'");
	ВидОшибки.ОписаниеПроблемы = НСтр("ru = 'Найти подходящую доверенность автоматически не получилось'");
	ВидОшибки.ОписаниеРешения =
		НСтр("ru = 'Получите от контрагента подходящую доверенность, загрузите ее в программу и проверьте подпись. 
	    |Или проверьте самостоятельно правильность подписания каждого документа в отдельности для принятия решения о доверии к подписи контрагента.'"); 
	
	Возврат ВидОшибки;
	
КонецФункции

// Возвращает статус проверки МЧД выполненной вручную
// 
// Параметры:
//  РезультатПроверкиПоМЧД - см. МашиночитаемыеДоверенности.НовыйРезультатПроверкиПодписи
// 
// Возвращаемое значение:
// 	Булево
//
Функция ПроверкаДоверенностиВыполненаВручную(РезультатПроверкиПоМЧД) Экспорт
	Возврат ЗначениеЗаполнено(РезультатПроверкиПоМЧД) 
		И РезультатПроверкиПоМЧД.ПодписьВерна 
		И РезультатПроверкиПоМЧД.ПроверкаВыполнена
		И ЗначениеЗаполнено(РезультатПроверкиПоМЧД.ПротоколПроверки)
		И РезультатПроверкиПоМЧД.ПротоколПроверки.ПроверкаПодписиДокумента.Свойство("РучнаяПроверка")
		И РезультатПроверкиПоМЧД.ПротоколПроверки.ПроверкаПодписиДокумента.РучнаяПроверка;
КонецФункции
	
// Возвращает результат проверки доверенности из протокола проверки
// 
// Параметры:
//  Проверка - см. НовыйПротоколПроверкиМЧД
// 
// Возвращаемое значение:
//  Булево
//  
Функция ДоверенностьПроверенаУспешно(Проверка) Экспорт
	
	Протокол = НовыйПротоколПроверкиМЧД();
	ЗаполнитьЗначенияСвойств(Протокол, Проверка);
	
	Для Каждого ИмяИЗначениеПоля Из Протокол Цикл
		
		ИмяПоля = ИмяИЗначениеПоля.Ключ;
		Если ИмяПоля = "Выполнена" Или ИмяПоля = "ОшибкаВыполнения" Тогда
			// Служебные поля
			Продолжить;
		КонецЕсли;
		
		РезультатПроверки = ИмяИЗначениеПоля.Значение;
		Если РезультатПроверки = Протокол.ПроверкаОператором Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(РезультатПроверки)
			И Не РезультатПроверки.Успех Тогда
			Возврат Ложь;
		КонецЕсли;
		
	КонецЦикла;

	Возврат Истина;
	
КонецФункции

// Новый протокол проверки МЧД.
// 
// Возвращаемое значение:
//   Структура:
// 	   * Выполнена - Булево
// 	   * ОшибкаВыполнения - Строка
// 	   * ПроверкаПодписиМЧД - Неопределено - если проверка не требуется.
// 	   						- см. НовыйРезультатПроверки
// 	   * ПроверкаОтзываМЧД	- Неопределено - если проверка не требуется.
// 							- см. НовыйРезультатПроверки
//	   * ПроверкаОператором - Неопределено - если проверка не требуется.
//							- см. НовыйРезультатПроверки
// 	   * СопоставлениеПредставителя - Неопределено - если проверка не требуется.
//									- см. НовыйРезультатПроверки
//	   * СопоставлениеДоверителя	- Неопределено - если проверка не требуется.
//									- см. НовыйРезультатПроверки
// 	   * ПроверкаПериодаДействия 	- Неопределено - если проверка не требуется.
//									- см. НовыйРезультатПроверки
// 	   * ПроверкаСтатусаВРеестреФНС	- Неопределено - если проверка не требуется.
// 							 		- см. НовыйРезультатПроверки
// 	   * ПроверкаПолномочий			- Неопределено - если проверка не требуется.
//									- см. НовыйРезультатПроверки
//	   * РодительскиеДанныеПолучены									 - Неопределено - если проверка не требуется.
//																	 - см. НовыйРезультатПроверки
//	   * РодительскаяДоверенностьДействительнаВРеестре				 - Неопределено - если проверка не требуется.
//																	 - см. НовыйРезультатПроверки
//	   * РодительскаяДоверенностьСоответствуетОрганизацииВДокументе - Неопределено - если проверка не требуется.
//																	 - см. НовыйРезультатПроверки
//	   * РодительскийПредставительСоответствуютДоверителюПередоверия - Неопределено - если проверка не требуется.
//																	 - см. НовыйРезультатПроверки
//	   * РодительскийПериодДействияСоответствуетДатеДокумента		 - Неопределено - если проверка не требуется.
//																	 - см. НовыйРезультатПроверки
//	   * РодительскаяДоверенностьМоглаПередоверять					 - Неопределено - если проверка не требуется.
//																	 - см. НовыйРезультатПроверки
//	   * РодительскиеПолномочияСоответствуютПолномочиямПередоверия	 - Неопределено - если проверка не требуется.
//																	 - см. НовыйРезультатПроверки
Функция НовыйПротоколПроверкиМЧД() Экспорт
	
	ПротоколПроверкиМЧД = Новый Структура;
	
	ПротоколПроверкиМЧД.Вставить("Выполнена", Ложь);
	ПротоколПроверкиМЧД.Вставить("ОшибкаВыполнения", "");
	ПротоколПроверкиМЧД.Вставить("ПроверкаПодписиМЧД", НовыйРезультатПроверки());
	ПротоколПроверкиМЧД.Вставить("ПроверкаОтзываМЧД", НовыйРезультатПроверки());
	ПротоколПроверкиМЧД.Вставить("ПроверкаОператором", НовыйРезультатПроверки());
	ПротоколПроверкиМЧД.Вставить("СопоставлениеПредставителя", НовыйРезультатПроверки());
	ПротоколПроверкиМЧД.Вставить("СопоставлениеДоверителя", НовыйРезультатПроверки());
	ПротоколПроверкиМЧД.Вставить("ПроверкаПериодаДействия", НовыйРезультатПроверки());
	ПротоколПроверкиМЧД.Вставить("ПроверкаСтатусаВРеестреФНС", НовыйРезультатПроверки());
	ПротоколПроверкиМЧД.Вставить("ПроверкаПолномочий", НовыйРезультатПроверки());
	
	ПротоколПроверкиМЧД.Вставить("РодительскиеДанныеПолучены", НовыйРезультатПроверки());
	ПротоколПроверкиМЧД.Вставить("РодительскаяДоверенностьДействительнаВРеестре", НовыйРезультатПроверки());
	ПротоколПроверкиМЧД.Вставить("РодительскаяДоверенностьСоответствуетОрганизацииВДокументе", НовыйРезультатПроверки());
	ПротоколПроверкиМЧД.Вставить("РодительскийПредставительСоответствуютДоверителюПередоверия", НовыйРезультатПроверки());
	ПротоколПроверкиМЧД.Вставить("РодительскийПериодДействияСоответствуетДатеДокумента", НовыйРезультатПроверки());
	ПротоколПроверкиМЧД.Вставить("РодительскаяДоверенностьМоглаПередоверять", НовыйРезультатПроверки());
	ПротоколПроверкиМЧД.Вставить("РодительскиеПолномочияСоответствуютПолномочиямПередоверия", НовыйРезультатПроверки());
	
	Возврат ПротоколПроверкиМЧД;
	
КонецФункции	

#КонецОбласти

#Область РаботаСРеестром

// Возвращает структуру, предназаначенную для хранения результатов проверки МЧД в реестре ФНС.
// 
// Возвращаемое значение:
// - Структура:
//    * СтраницаНеНайдена - Булево
//    * СостояниеИзменено - Булево
//    * СтатусВРеестреФНС - ПеречислениеСсылка.СтатусыМашиночитаемойДоверенностиВРеестреФНС
//    * НомерДоверенности - ОпределяемыйТип.ЕдиныйРегистрационныйНомерДоверенности
//    * ДатаПроверки - Дата 
//    * ДатаОтзыва - Дата
//
Функция РезультатПроверкиВРеестреФНС() Экспорт

	Результат = Новый Структура;
	Результат.Вставить("СтраницаНеНайдена", Ложь);
	Результат.Вставить("СостояниеИзменено", Ложь);
	Результат.Вставить("СтатусВРеестреФНС", ПредопределенноеЗначение(
		"Перечисление.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ПустаяСсылка"));
	Результат.Вставить("НомерДоверенности", "");
	Результат.Вставить("ДатаПроверки", '00010101');
	Результат.Вставить("ДатаОтзыва", '00010101');
	Возврат Результат;

КонецФункции

// Возвращает структуру, предназаначенную для проверки МЧД в реестре ФНС.
// 
// Возвращаемое значение:
//  Структура:
//  * НомерДоверенности - ОпределяемыйТип.ЕдиныйРегистрационныйНомерДоверенности
//  * СтатусВРеестреФНС - ПеречислениеСсылка.СтатусыМашиночитаемойДоверенностиВРеестреФНС
//  * ИдентификаторТранзакции - Строка
//  * ДоверительЮЛ_ИНН - Строка
//  * ДоверительФЛ_ИНН - Строка
//  * ДатаОтзыва - Дата
//
Функция ДанныеДляПроверкиВРеестреФНС() Экспорт

	ДанныеДоверенности = Новый Структура;
	ДанныеДоверенности.Вставить("НомерДоверенности", "");
	ДанныеДоверенности.Вставить("СтатусВРеестреФНС", ПредопределенноеЗначение(
		"Перечисление.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ПустаяСсылка"));
	ДанныеДоверенности.Вставить("ИдентификаторТранзакции", "");
	ДанныеДоверенности.Вставить("ДоверительЮЛ_ИНН", "");
	ДанныеДоверенности.Вставить("ДоверительФЛ_ИНН", "");
	ДанныеДоверенности.Вставить("ДатаОтзыва", '00010101');
	Возврат ДанныеДоверенности;

КонецФункции

#КонецОбласти

// Новые сведенеия о представителе МЧД.
// 
// Возвращаемое значение:
//  Структура - Новые сведенеия о представителе МЧД:
// * ТипПредставителя - Строка
// * ДокументВид - Строка
// * ДокументСерия - Строка
// * ДокументНомер - Строка
// * ДокументДатаВыдачи - Дата
// * ДокументКемВыдан - Строка
// * ДокументКодПодразделения - Строка
// * ФЛ_ИНН - Строка
// * ФЛ_СНИЛС - Строка
// * ФЛ_Удостоверение - Строка
// * ФЛ_Гражданство - СправочникСсылка.СтраныМира
// * ФЛ_ДатаРождения - Дата
// * Представитель - Строка
// * ПредставлениеПредставителя - Строка
// * ФЛ_Фамилия - Строка
// * ФЛ_Имя - Строка
// * ФЛ_Имя - Строка
// * ФЛ_Отчество - Строка 
// * ФЛ_ОГРН - Строка
// * ЮЛ_Наименование - Строка
// * ЮЛ_ИНН - Строка
// * ЮЛ_КПП - Строка
// * ЮЛ_ОГРН - Строка
//
Функция НовыеСведенияОПредставителеМЧД() Экспорт
	
	СтруктураДанных = Новый Структура;
	
	СтруктураДанных.Вставить("ТипПредставителя", "");
	
	СтруктураДанных.Вставить("ДокументВид", "");
	СтруктураДанных.Вставить("ДокументСерия", "");
	СтруктураДанных.Вставить("ДокументНомер", "");
	СтруктураДанных.Вставить("ДокументДатаВыдачи", Дата(1, 1, 1));
	СтруктураДанных.Вставить("ДокументКемВыдан", "");
	СтруктураДанных.Вставить("ДокументКодПодразделения", "");
	
	СтруктураДанных.Вставить("ФЛ_ИНН", "");	
	СтруктураДанных.Вставить("ФЛ_СНИЛС", "");
	СтруктураДанных.Вставить("ФЛ_Удостоверение", "");
	СтруктураДанных.Вставить("ФЛ_Гражданство", Неопределено);
	СтруктураДанных.Вставить("ФЛ_ДатаРождения", Дата(1, 1, 1));
	СтруктураДанных.Вставить("Представитель", "");
	СтруктураДанных.Вставить("ПредставлениеПредставителя", "");
	
	СтруктураДанных.Вставить("ФЛ_Фамилия", "");
	СтруктураДанных.Вставить("ФЛ_Имя", "");
	СтруктураДанных.Вставить("ФЛ_Отчество", "");
	
	СтруктураДанных.Вставить("ФЛ_ОГРН", "");
	
	СтруктураДанных.Вставить("ЮЛ_Наименование", "");
	СтруктураДанных.Вставить("ЮЛ_ИНН", "");
	СтруктураДанных.Вставить("ЮЛ_КПП", "");
	СтруктураДанных.Вставить("ЮЛ_ОГРН",	"");

	Возврат СтруктураДанных;

КонецФункции

Функция ЗаголовокБудетОтозвана() Экспорт
	Возврат НСтр("ru = 'Будет отозвана'");
КонецФункции

// Возвращает структуру способов отражения в учете ЭД Машиночитаемая доверенность
// 
// Возвращаемое значение:
//  Структура:
//  * МашиночитаемыеДоверенностиКонтрагентов - Строка
//  * МашиночитаемыеДоверенностиОрганизаций - Строка
//  * МЧД003 - Строка
Функция СпособыОтраженияВУчете() Экспорт
	
	Способы = Новый Структура;
	Способы.Вставить("МашиночитаемыеДоверенностиКонтрагентов", НСтр("ru = 'Машиночитаемая доверенность контрагента'"));
	Способы.Вставить("МашиночитаемыеДоверенностиОрганизаций", НСтр("ru = 'Машиночитаемая доверенность организации'"));
	Возврат Способы;
	
КонецФункции

// Возвращает структуру способов отражения в учете ЭД МЧД003
// 
// Возвращаемое значение:
//  Структура:
//  * МЧД003 - Строка
Функция СпособыОтраженияВУчетеМЧД003() Экспорт
	
	
	СписокОпераций = Новый СписокЗначений;
	СписокОпераций.Добавить("МЧД003", НСтр("ru = 'Машиночитаемая доверенность единой формы'"), Истина);
	
	Возврат СписокОпераций;
	
КонецФункции


// Коллекция видов документов
// 
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение- где:
//   * Ключ - Строка - код вида документа, 
//   * Значение - Строка - наименование вида документа.
//
Функция ВидыДокументовФизическихЛиц() Экспорт
	
	ВидыДокументов = Новый Соответствие();
	
	ВидыДокументов.Вставить("07", НСтр("ru = 'Военный билет'"));
	ВидыДокументов.Вставить("10", НСтр("ru = 'Паспорт иностранного гражданина'"));
	ВидыДокументов.Вставить("11", НСтр("ru = 'Свидетельство о рассмотрении ходатайства о признании лица беженцем на территории Российской Федерации по существу'"));
	ВидыДокументов.Вставить("12", НСтр("ru = 'Вид на жительство в Российской Федерации'"));
	ВидыДокументов.Вставить("13", НСтр("ru = 'Удостоверение беженца'"));
	ВидыДокументов.Вставить("15", НСтр("ru = 'Разрешение на временное проживание в Российской Федерации'"));
	ВидыДокументов.Вставить("19", НСтр("ru = 'Свидетельство о предоставлении временного убежища на территории Российской Федерации'"));
	ВидыДокументов.Вставить("21", НСтр("ru = 'Паспорт гражданина Российской Федерации'"));
	ВидыДокументов.Вставить("24", НСтр("ru = 'Удостоверение личности военнослужащего Российской Федерации'"));
	
	Возврат ВидыДокументов;
	
КонецФункции

// Возвращает СтатусВРеестреФНС МЧД, полученной из реестра
//
// Параметры:
//  СтатусДоверенности - Строка - "CREATED" - дата начала действия не наступила, "ACTIVE" - действует,
//                                "EXPIRED" - истекла, "REVOKED/DECLINED" - отменена (отозвана), "" - не запрашивался
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СтатусыМашиночитаемойДоверенностиВРеестреФНС
//
Функция СтатусВРеестреФНС(СтатусДоверенности) Экспорт
	Если СтатусДоверенности = "ACTIVE" Тогда
		Возврат ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиВРеестреФНС.Зарегистрировано");
	ИначеЕсли СтатусДоверенности = "CREATED" Тогда
		Возврат ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ДатаНачалаДействияНеНаступила");
	ИначеЕсли СтатусДоверенности = "EXPIRED" Тогда
		Возврат ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ИстекСрокДействия");
	ИначеЕсли СтатусДоверенности = "REVOKED" ИЛИ СтатусДоверенности = "DECLINED" Тогда
		Возврат ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиВРеестреФНС.Отозвано");
	Иначе
		Возврат ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ПустаяСсылка");
	КонецЕсли;
КонецФункции

// Возвращает признак, что передана ссылка на доверенность.
// 
// Параметры:
//  Ссылка - ЛюбаяСсылка
// 
// Возвращаемое значение:
//  Булево - Истина, если передана ссылка на машиночитаемую доверенность.
Функция ЭтоСсылкаНаДоверенность(Ссылка) Экспорт
	
	Результат = Ложь;
	
	ТипСсылки = ТипЗнч(Ссылка);
	Если ТипСсылки = Тип("СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций")
		ИЛИ ТипСсылки = Тип("СправочникСсылка.МашиночитаемыеДоверенностиКонтрагентов")
		ИЛИ ТипСсылки = Тип("СправочникСсылка.МЧД003") Тогда
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Формирует набор дополнительных сведений по загрузке МЧД из реестра
//
// Возвращаемое значение:
// Структура:
//  * СтатусВРеестреФНС - ПеречислениеСсылка.СтатусыМашиночитаемойДоверенностиВРеестреФНС
//  * ДатаЗагрузки - Дата
//  * ЭтоМЧДОрганизации - Булево
Функция ДополнительныеСведенияПоЗагрузкеМЧД() Экспорт
	
	ДополнительныеСведения = Новый Структура();
	ДополнительныеСведения.Вставить("СтатусВРеестреФНС", 
		ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ПустаяСсылка"));
	ДополнительныеСведения.Вставить("ДатаЗагрузки", Дата(1, 1, 1));
	ДополнительныеСведения.Вставить("ЭтоМЧДОрганизации", Ложь);
	ДополнительныеСведения.Вставить("ЭтоМЧДКонтрагента", Ложь);
	
	Возврат ДополнительныеСведения
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Формирует набор дерева правил проверки полномочий МЧД.
// 
// Возвращаемое значение:
//  Структура - Набор картинок:
// * Документ - Число
// * Реквизит - Число
// * Ресурс - Число
// * Справочник - Число
Функция НаборКартинок() Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("Документ", 0);
	Результат.Вставить("Реквизит", 1);
	Результат.Вставить("Ресурс", 2);
	Результат.Вставить("Справочник", 3);
	
	Возврат Результат;
	
КонецФункции

Функция Тип_Число_15() Экспорт
	Возврат Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 0, ДопустимыйЗнак.Неотрицательный));
КонецФункции

Функция Тип_Булево() Экспорт
	Возврат Новый ОписаниеТипов("Булево");
КонецФункции

// Проверяет наличие заполненных настроек в дереве
// 
// Параметры:
//  ДеревоОтбора - ДанныеФормыДерево
// 
// Возвращаемое значение:
//  Булево
Функция ДеревоСодержитНастройки(ДеревоОтбора) Экспорт
	
	СтрокиДерева = ДеревоОтбора.ПолучитьЭлементы();
	
	Для Каждого СтрокаДерева Из СтрокиДерева Цикл
		
		Если СтрокаДерева.ДоступенСписок Тогда
			
			ЭлементыДерева = СтрокаДерева.ПолучитьЭлементы();
			
			Для Каждого Элемент Из ЭлементыДерева Цикл
				Если ЗначениеЗаполнено(Элемент.Данные) Тогда
					Возврат Истина;
				КонецЕсли;
			КонецЦикла;
			
		Иначе
			
			Если СтрокаДерева.Тип = Тип_Булево()
				И СтрокаДерева.НачальноеЗначение = СтрокаДерева.КонечноеЗначение Тогда
				Возврат Истина;
			КонецЕсли;
			
			Если СтрокаДерева.Тип = Тип_Число_15()
				И СтрокаДерева.НачальноеЗначение + СтрокаДерева.КонечноеЗначение > 0 Тогда
				Возврат Истина;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

// Проверяет настройку правила проверки полномочий МЧД
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
// 
// Возвращаемое значение:
//  Булево
Функция ПравилоНастроено(Форма) Экспорт
	
	Варианты = ВариантыПроверки();
	ВариантПроверки = Форма.ВариантПроверки;
	
	Если ВариантПроверки = Варианты.Скрипт Тогда
		Возврат ЗначениеЗаполнено(Форма.Скрипт);
	ИначеЕсли ВариантПроверки = Варианты.Настройка Тогда
		Возврат ДеревоСодержитНастройки(Форма.ДеревоОтбора);
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Задает варианты проверки.
// 
// Возвращаемое значение:
//  Структура - Варианты проверки:
// * Настройка - Строка
// * Отбор - Строка
// * Скрипт - Строка
Функция ВариантыПроверки() Экспорт
	
	Варианты = Новый Структура();
	Варианты.Вставить("Настройка", НСтр("ru = 'Упрощенная настройка'"));
	Варианты.Вставить("Скрипт", НСтр("ru = 'Скрипт на языке 1С:Предприятия'"));
	
	Возврат Варианты;
	
КонецФункции

// Возвращает строковое представление неограниченных полномочий.
// 
// Возвращаемое значение:
//  Строка
Функция ПредставлениеНеограниченныхПолномочий() Экспорт
	Возврат НСтр("ru = 'Все полномочия'");
КонецФункции

// Проверяет текст на наличие полных полномочий.
// 
// Параметры:
//  ТекстПолномочий - Строка - Текст полномочий
// 
// Возвращаемое значение:
//  Булево
//  
Функция ЭтоТекстПолныхПолномочий(ТекстПолномочий) Экспорт
	
	ТекстыПолныхПолномочий = Новый Массив;
	ТекстыПолныхПолномочий.Добавить(ПредставлениеНеограниченныхПолномочий());
	ТекстыПолныхПолномочий.Добавить(НСтр("ru = 'Полные полномочия'"));
	
	Возврат ТекстыПолныхПолномочий.Найти(СокрЛП(ТекстПолномочий)) <> Неопределено;
	
КонецФункции

// Проверяет таблицу полномочий на наличие полных полномочий.
// 
// Параметры:
//  Полномочия - ДанныеФормыКоллекция:
//  * Описание - Строка
// 
// Возвращаемое значение:
//  Булево
//  
Функция ЭтоПолныеПолномочия(Полномочия) Экспорт
		
	Если Полномочия.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
		
	Для Каждого СтрокаПолномочий Из Полномочия Цикл
		Если Не ЭтоТекстПолныхПолномочий(СтрокаПолномочий.Описание) Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

Функция ТекстНеобходимостиРучнойПроверкиПолномочий() Экспорт
	Возврат НСтр("ru = 'Правила проверки полномочий не настроены. Настройте или проверьте полномочия вручную.'");
КонецФункции

// Возвращает новые параметры проверки полномочий доверенности.
// 
// Возвращаемое значение:
//  Структура:
// 	  * ПодписанныйОбъект - СправочникСсылка.СообщениеЭДОПрисоединенныеФайлы
//    * ХешПодписи - Строка
//    * ЭтоОперацияЭДО - Булево
//    * НоваяДоверенность - СправочникСсылка.МашиночитаемыеДоверенностиКонтрагентов
//    * ТекстОшибки - Строка
//
Функция НовыеПараметрыПроверкиПолномочий() Экспорт
	
	Результат = Новый Структура();
	
	Результат.Вставить("ПодписанныйОбъект",
		ПредопределенноеЗначение("Справочник.ЭДПрисоединенныеФайлы.ПустаяСсылка"));
	Результат.Вставить("ХешПодписи", "");
	Результат.Вставить("ЭтоОперацияЭДО", Ложь);
	Результат.Вставить("НоваяДоверенность",
		ПредопределенноеЗначение("Справочник.МашиночитаемыеДоверенностиКонтрагентов.ПустаяСсылка"));
	Результат.Вставить("ТекстОшибки", "");
	
	Возврат Результат;
	
КонецФункции

// Инициализирует структуру для загрузки МЧД в информационную базу.
// 
// Возвращаемое значение:
//  Структура:
// * ДанныеДоверенности - ДвоичныеДанные
// * ДанныеПодписи - ДвоичныеДанные
// * ДанныеПодписиЗаявленияНаОтмену - ДвоичныеДанные
// * ДанныеПодписей - Массив Из ДвоичныеДанные - Количество подписей соответствует количеству доверителей в доверенности
Функция НовыеДанныеДляЗагрузкиМЧД() Экспорт
	
	ДанныеДляПроверки = Новый Структура;
	ДанныеДляПроверки.Вставить("ДанныеДоверенности", Неопределено);
	ДанныеДляПроверки.Вставить("ДанныеПодписи", Неопределено);
	ДанныеДляПроверки.Вставить("ДанныеПодписиЗаявленияНаОтмену", Неопределено);
	ДанныеДляПроверки.Вставить("ДанныеПодписей", Новый Массив);
	
	Возврат ДанныеДляПроверки;
	
КонецФункции

// Формирует представление полномочия для элементов справочников:
// СправочникСсылка.МашиночитаемыеДоверенностиКонтрагентов
// СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
// 
// Параметры:
//  Код        - Строка
//  Содержание - Строка
//  Описание   - Строка
// 
// Возвращаемое значение:
//  Строка
//
Функция СформироватьПредставлениеПолномочия(Код = "", Содержание = "", Описание = "") Экспорт
	
	Если ЗначениеЗаполнено(Код) Тогда
		
		ШаблонОписания = ("%1; %2"); 
		ПредставлениеПолномочия = СтрШаблон(ШаблонОписания, Код, Содержание);
		
	Иначе
		
		ПредставлениеПолномочия = Описание;
		
	КонецЕсли;
	
	Возврат ПредставлениеПолномочия;
	
КонецФункции

// Параметры:
//  МЧД - СправочникОбъект.МашиночитаемыеДоверенностиОрганизаций
//      - СправочникОбъект.МашиночитаемыеДоверенностиКонтрагентов
//      - СправочникОбъект.МЧД003
//      - ДанныеФормыСтруктура - созданная из объектов указанных выше.
//      
// Возвращаемое значение:
//  Булево - если Истина, то настройка доступна, иначе - недоступна.
//
Функция ДоступнаИнтерактивнаяНастройкаПравилПроверки(МЧД) Экспорт
	
	Возврат МЧД.ПолномочияОграничены И МЧД.ВариантЗаполненияПолномочий =
		ПредопределенноеЗначение("Перечисление.ВариантыЗаполненияПолномочийМЧД.Текст");
КонецФункции

// Возвращает идентификатор пространства имен универсального формата МЧД версии 003
// 
// Возвращаемое значение:
//  Строка
Функция ФорматМЧД_003() Экспорт
	Возврат "urn://x-artefacts/EMCHD_1";
КонецФункции

// Параметры:
//  СведенияМЧД - СправочникОбъект.МЧД003
//              - Структура:
//              	* ДатаПрекращения - Дата
// Возвращаемое значение:
//  Булево
//
Функция ДоверенностьОтозвана(СведенияМЧД) Экспорт
	Возврат СведенияМЧД.ДатаПрекращения > Дата(1, 1, 1);
КонецФункции

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// Адаптация

Функция ЗначениеВМассиве(Знач Значение) Экспорт
	
	Результат = Новый Массив;
	Результат.Добавить(Значение);
	Возврат Результат;
	
КонецФункции

// Возвращает пустой набор действий по ЭДО.
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//  * Ключ - ПеречислениеСсылка.ДействияПоЭДО
//  * Значение - Булево
//
Функция НовыйНаборДействийПоЭДО()
	Возврат Новый Соответствие;
КонецФункции

Функция НовыеОбъектыДействийПоЭДО() Экспорт
	ОбъектыДействий = Новый Структура;
	ОбъектыДействий.Вставить("ОбъектыУчета", Новый Массив);
	ОбъектыДействий.Вставить("ЭлектронныеДокументы", Новый Массив);
	ОбъектыДействий.Вставить("Сообщения", Новый Массив);
	ОбъектыДействий.Вставить("ПакетыДокументов", Новый Массив);
	ОбъектыДействий.Вставить("ИдентификаторыОрганизаций", Новый Массив);
	ОбъектыДействий.Вставить("МЧД", Новый Массив);
	Возврат ОбъектыДействий;
КонецФункции

Функция НовыеПараметрыВыполненияДействийПоЭДО() Экспорт
	
	ВыбранныеСертификаты = Новый Массив; // Массив из СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
	
	ПараметрыВыполнения = Новый Структура;
	ПараметрыВыполнения.Вставить("НаборДействий", НовыйНаборДействийПоЭДО());
	ПараметрыВыполнения.Вставить("ОбъектыДействий", НовыеОбъектыДействийПоЭДО());
	ПараметрыВыполнения.Вставить("ДополнительныеПараметрыДействий", Новый Соответствие);
	ПараметрыВыполнения.Вставить("КлючиНастроекОбъектов", Новый Соответствие);
	ПараметрыВыполнения.Вставить("НастройкиОбъектов", Новый Соответствие);
	ПараметрыВыполнения.Вставить("МаршрутыПодписанияОбъектов", Новый Соответствие);
	ПараметрыВыполнения.Вставить("ПодписантыОбъектов", Новый Соответствие);
	ПараметрыВыполнения.Вставить("ДополнительныеДанныеОбъектов", Новый Соответствие);
	ПараметрыВыполнения.Вставить("ОтпечаткиСертификатов", Неопределено);
	ПараметрыВыполнения.Вставить("ВыбранныеСертификаты", ВыбранныеСертификаты);
	ПараметрыВыполнения.Вставить("ИдентификаторыПечатныхФормОбъектов", Новый Соответствие);
	
	Возврат ПараметрыВыполнения;
	
КонецФункции

#Область ФорматированнаяСтрока

Функция СформироватьФорматированнуюСтроку(ШаблонСтроки, ЭлементыСтиля,
		Знач Параметр1, Знач Параметр2, Знач Параметр3, Знач Параметр4, Знач Параметр5) Экспорт
	
	HTMLСтрока = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСтроки, Параметр1, Параметр2, Параметр3, Параметр4, Параметр5);
	
	НаборСтрок = Новый Массив;
	
	ТекущийШрифт  = Неопределено;
	ТекущийЦвет   = Неопределено;
	ТекущийФон    = Неопределено;
	ТекущаяСсылка = Неопределено;
	
	ФрагментыСтроки = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(HTMLСтрока, "<", Истина);
	Для каждого Фрагмент Из ФрагментыСтроки Цикл
		
		ТелоСтроки = "";
		ПозицияТэг = Найти(Фрагмент, ">");
		
		Если ПозицияТэг = 0 Тогда
			ТелоСтроки = Фрагмент;
			
		ИначеЕсли Найти(Фрагмент, "/")=1 Тогда
			ТелоСтроки = Сред(Фрагмент, ПозицияТэг + 1);
			
			ТекущийШрифт  = Неопределено;
			ТекущийЦвет   = Неопределено;
			ТекущийФон    = Неопределено;
			ТекущаяСсылка = Неопределено;
			
		Иначе
			
			ОписаниеТэга = Лев(Фрагмент, ПозицияТэг - 1);
			ПервыйПробел = Найти(ОписаниеТэга, " ");
			
			Если ПервыйПробел > 0 Тогда
				ИмяТега = СокрЛП(Лев(ОписаниеТэга, ПервыйПробел));
				
				ОписаниеАтрибутов = Сред(ОписаниеТэга, ПервыйПробел + 1);
				ОписаниеАтрибутов = СтрЗаменить(ОписаниеАтрибутов , """", "'");
				
				ПозицияРавно = Найти(ОписаниеАтрибутов, "=");
				Пока ПозицияРавно > 0 Цикл
					
					ИмяАтрибута = СокрЛП(Лев(ОписаниеАтрибутов, ПозицияРавно - 1));
					ПозицияПерваяКавычка = Найти(Сред(ОписаниеАтрибутов, ПозицияРавно + 1), "'") + ПозицияРавно;
					Если ПозицияПерваяКавычка = 0 Тогда
						ПозицияПерваяКавычка = ПозицияРавно;
					КонецЕсли;
					ПозицияВтораяКавычка = Найти(Сред(ОписаниеАтрибутов, ПозицияПерваяКавычка + 1), "'") + ПозицияПерваяКавычка;
					Если ПозицияВтораяКавычка = 0 Тогда
						ПозицияВтораяКавычка = СтрДлина(ОписаниеАтрибутов) + 1;
					КонецЕсли;
					ЗначениеАтрибута  = СокрЛП(Сред(ОписаниеАтрибутов, ПозицияПерваяКавычка + 1,  ПозицияВтораяКавычка - ПозицияПерваяКавычка - 1));
					
					Если ИмяАтрибута = "style" Тогда
						УстановитьСтилиПоЗначениюАтрибута(ЗначениеАтрибута, ЭлементыСтиля, ТекущийФон, ТекущийЦвет, ТекущийШрифт);
					ИначеЕсли ИмяАтрибута = "href" И ИмяТега = "a" Тогда
						ТекущаяСсылка = ЗначениеАтрибута;
					ИначеЕсли ИмяАтрибута = "src" И ИмяТега = "img" Тогда
						НаборСтрок.Добавить(Новый ФорматированнаяСтрока(БиблиотекаКартинок[ЗначениеАтрибута], ТекущийШрифт, ТекущийЦвет, ТекущийФон, ТекущаяСсылка));
					КонецЕсли;
					
					ОписаниеАтрибутов = Сред(ОписаниеАтрибутов, ПозицияВтораяКавычка + 1);
					ПозицияРавно = Найти(ОписаниеАтрибутов, "=");
					
				КонецЦикла;
			Иначе
				ИмяТега = ОписаниеТэга;
			КонецЕсли;
			
			Если ИмяТега = "b" Тогда
				ТекущийШрифт = ЭлементыСтиля["ВажнаяНадписьШрифт"];
			КонецЕсли;
			
			ТелоСтроки = Сред(Фрагмент, ПозицияТэг + 1);
			
		КонецЕсли;
		
		ТелоСтроки = СтрЗаменить(ТелоСтроки, "&lt;", "<");
		Если СтрДлина(ТелоСтроки) > 0 Тогда
			НаборСтрок.Добавить(Новый ФорматированнаяСтрока(ТелоСтроки, ТекущийШрифт, ТекущийЦвет, ТекущийФон, ТекущаяСсылка));
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Новый ФорматированнаяСтрока(НаборСтрок);	// АПК:1356 - можно использовать составную форматированную строку,
														// так как массив строк формируется из переданного в функцию текста.

КонецФункции

Процедура УстановитьСтилиПоЗначениюАтрибута(Знач ОписаниеСтиля, ЭлементыСтиля, ТекущийФон, ТекущийЦвет, ТекущийШрифт)
	
	Стили = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ОписаниеСтиля, ";");
	
	Для каждого Стиль Из Стили Цикл
		
		ЗначенияСтиля = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(стиль, ":");
		ИмяСтиля      = СокрЛП(ЗначенияСтиля[0]);
		ЗначениеСтиля = СокрЛП(ЗначенияСтиля[1]);
		
		Если ИмяСтиля = "color"  Тогда
			ТекущийЦвет = ЭлементыСтиля[ЗначениеСтиля];
		ИначеЕсли ИмяСтиля = "background-color" Тогда
			ТекущийФон = ЭлементыСтиля[ЗначениеСтиля];
		ИначеЕсли ИмяСтиля = "font" Тогда
			ТекущийШрифт = ЭлементыСтиля[ЗначениеСтиля];
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

// Возвращает статусы ошибок.
// 
// Возвращаемое значение:
// 	Структура - Описание:
// * Обычная - Строка -ошибки с таким статусом выделены желтой картинкой в форме вывода ошибок
// * Важная - Строка - ошибки с таким статусом выделены красной картинкой в форме вывода ошибок
Функция СтатусыОшибок() Экспорт
	
	Статусы = Новый Структура;
	Статусы.Вставить("Обычная", "Обычная");
	Статусы.Вставить("Важная", "Важная");
	
	Возврат Статусы;
	
КонецФункции

// Возвращает описание вида ошибки.
// Виды ошибок можно наследовать, переопределяя свойства. Необходимо обязательно изменить идентификатор 
// переопределяемого вида ошибки.
// 
// Возвращаемое значение:
//  Структура:
//    * Идентификатор     - Строка - идентификатор вида ошибки формата "ИмяПодсистемы_НаименованиеОшибки",
//                      пример: "ОбменСКонтрагентами_СертификатПросрочен". Ошибки с одинаковыми
//                      идентификаторами группируются при отображении, поэтому каждый вид ошибки должен иметь
//                      свой уникальный идентификатор.
//    * ЗаголовокПроблемы - Строка - краткая суть проблемы, выводится крупным шрифтом.
//    * ОписаниеПроблемы  - Строка - детальное описание проблемы.
//    * ОписаниеРешения   - ФорматированнаяСтрока, Строка - описание решения проблемы, может содержать гиперссылки,
//                           при нажатии на которые выполняются обработчики, заданные в ключе ОбработчикиНажатия.
//                           Строка может содержать теги см. СтроковыеФункции.ФорматированнаяСтрока.ШаблонСтроки.
//    * ВыводитьСсылкуНаСписокОшибок - Булево - если истина - в форму вывода ошибок под описанием проблемы будет
//                           добавлена гиперссылка, при нажатии на которую откроется форма со списком ошибок
//                           по текущему виду ошибки.
//    * ОбработчикиНажатия - Соответствие из КлючИЗначение:
//        ** Ключ - Строка - фраза-гиперссылка из параметра ОписаниеРешения, при нажатии на которую будет 
//                           вызван обработчик.
//        ** Значение - Строка - содержит описание клиентской процедуры в формате ИмяОбщегоМодуля.ИмяПроцедуры,
//                           которая будет вызвана при нажатии на гиперссылку. Процедура будет вызвана со 
//                           следующими параметрами:
//                             ** КонтекстДиагностики - см. НовыйКонтекстДиагностики
//                             ** ДополнительныеПараметры - Произвольный - значение, которое указано в
//                                                          параметре ПараметрыОбработчиков для соответствующего ключа.
//    * ПараметрыОбработчиков - Соответствие из КлючИЗначение - содержит параметры, с которыми будут вызваны процедуры,
//                              описанные в параметре ОбработчикиНажатия:
//       ** Ключ - Строка - должен совпадать с одним из ключей параметра ОбработчикиНажатия.
//       ** Значение - Произвольный - значение, с которым будет вызван обработчик из параметра ОбработчикиНажатия для
//                                    соответствующего ключа.
//    * ВыполнятьОбработчикАвтоматически - Булево - если значение параметра - Истина и в коллекции накопленных
//                           ошибок находятся ошибки только одного вида, будет выполнен обработчик, указанный в
//                           параметре АвтоматическиВыполняемыйОбработчик без открытия формы вывода ошибок.
//    * АвтоматическиВыполняемыйОбработчик - Строка - обработчик, который будет выполнен при значении параметра
//                            ВыполнятьОбработчикПриЕдинственномВидеОшибок - Истина.
//    * Статус - см. СтатусыОшибок
//    * ЭтоВидОшибки - Булево - (служебной поле) для определение типа структуры (структура является видом ошибки).
Функция НовоеОписаниеВидаОшибки() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("Идентификатор", "");
	Параметры.Вставить("ЗаголовокПроблемы", "");
	Параметры.Вставить("ОписаниеПроблемы", "");
	Параметры.Вставить("ОписаниеРешения", "");
	Параметры.Вставить("ВыводитьСсылкуНаСписокОшибок", Истина);
	Параметры.Вставить("ОбработчикиНажатия", Новый Соответствие);
	Параметры.Вставить("ПараметрыОбработчиков", Новый Соответствие);
	Параметры.Вставить("ВыполнятьОбработчикАвтоматически", Ложь);
	Параметры.Вставить("АвтоматическиВыполняемыйОбработчик", "");
	Параметры.Вставить("Статус", СтатусыОшибок().Важная);
	Параметры.Вставить("ЭтоВидОшибки", Истина);
	
	Параметры.ОбработчикиНажатия.Вставить("ОбработкаНеисправностейБЭДОткрытьСписокОшибок",
		Неопределено);
	
	Возврат Параметры;
	
КонецФункции

Функция ФИОСтрокой(ФИОИлиФамилия, Имя = "", Отчество = "") Экспорт
	ФИО = Новый Структура("Фамилия, Имя, Отчество", "", "", "");
	Если ТипЗнч(ФИОИлиФамилия) = Тип("Строка") Тогда
		ФИО.Фамилия = ФИОИлиФамилия;
		ФИО.Имя = Имя;
		ФИО.Отчество = Отчество;
	Иначе
		ЗаполнитьЗначенияСвойств(ФИО, ФИОИлиФамилия);
	КонецЕсли;
	Шаблон = "[Фамилия] [Имя] [Отчество]";
	Если ПустаяСтрока(СокрЛП(ФИО.Отчество)) Тогда
		Шаблон = "[Фамилия] [Имя]";
	КонецЕсли;
	Возврат СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(Шаблон, ФИО);
КонецФункции

Функция СвойствоСтруктуры(Структура, Ключ, ЗначениеПоУмолчанию = Неопределено) Экспорт
	
	Если Структура = Неопределено Тогда
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;
	
	Результат = ЗначениеПоУмолчанию;
	Если Структура.Свойство(Ключ, Результат) Тогда
		Возврат Результат;
	Иначе
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;
	
КонецФункции

Функция ЕстьРеквизитИлиСвойствоОбъекта(Объект, ИмяРеквизита) Экспорт
	
	КлючУникальности   = Новый УникальныйИдентификатор;
	СтруктураРеквизита = Новый Структура(ИмяРеквизита, КлючУникальности);
	ЗаполнитьЗначенияСвойств(СтруктураРеквизита, Объект);
	
	Возврат СтруктураРеквизита[ИмяРеквизита] <> КлючУникальности;
	
КонецФункции

// Возвращает описание подписи.
// 
// Возвращаемое значение:
//  Структура:
//  * Подпись - ДвоичныеДанные
//  * Сертификат - ДвоичныеДанные
//  * Отпечаток - Строка
//  * КомуВыданСертификат - Строка
//  * УстановившийПодпись - СправочникСсылка.Пользователи
//  * ДатаПроверкиПодписи - Дата
//  * ПодписьВерна - Булево
//  * ИмяФайлаПодписи - Строка
//  * ДатаПодписи - Дата
//  * Комментарий - Строка
//  * ТипПодписи - ПеречислениеСсылка.ТипыЭлектроннойПодписи
//  * СрокДействияПоследнейМеткиВремени - Дата - срок действия сертификата, которым подписана
//                                    последняя метка времени (или пустая дата, если нет метки времени)
//  * ПорядковыйНомер - Число - идентификатор подписи, по которому можно упорядочивать их в списке.
//                      Не заполнен, если подпись не связана с объектом.
//  * ПодписанныйОбъект - ОпределяемыйТип.ПодписанныйОбъект - идентификатор подписи, по которому можно
//                        упорядочивать их в списке. Не заполнен, если подпись не связана с объектом.
//  * ОшибкаПриАвтоматическомПродлении - Булево - служебный, заполняется регламентным заданием.
//  * ОписаниеСертификата - Структура - свойство, требуемое для сертификатов, которые
//                          не могут быть переданы в метод платформы СертификатКриптографии, со свойствами:
//  ** СерийныйНомер - Строка - серийный номер сертификата, как у объекта платформы СертификатКриптографии.
//  ** КемВыдан      - Строка - как возвращает функция ПредставлениеИздателя.
//  ** КомуВыдан     - Строка - как возвращает функция ПредставлениеСубъекта.
//  ** ДатаНачала    - Строка - дата сертификата, как у объекта платформы СертификатКриптографии в формате "ДЛФ=D".
//  ** ДатаОкончания - Строка - дата сертификата, как у объекта платформы СертификатКриптографии в формате "ДЛФ=D".
//  * ДатаПодписиИзМетки - Дата, Неопределено - самый ранний штамп времени: CADES-T, если его нет, но есть другие штампы,
//               дата заполняется только с помощью менеджера криптографии.
//  * ТипПодписи  - ПеречислениеСсылка.ТипыПодписиКриптографии
Функция НовыеСвойстваПодписи() Экспорт
	
	СвойстваПодписи = Новый Структура;
	СвойстваПодписи.Вставить("Подпись");
	СвойстваПодписи.Вставить("Сертификат");
	СвойстваПодписи.Вставить("Отпечаток", "");
	СвойстваПодписи.Вставить("КомуВыданСертификат", "");
	СвойстваПодписи.Вставить("ДатаПодписи", Дата(1, 1, 1));
	СвойстваПодписи.Вставить("ИмяФайлаПодписи", "");
	СвойстваПодписи.Вставить("ПодписьВерна", Ложь);
	СвойстваПодписи.Вставить("ДатаПроверкиПодписи", Дата(1, 1, 1));
	СвойстваПодписи.Вставить("УстановившийПодпись", Неопределено);
	СвойстваПодписи.Вставить("Комментарий", "");
	СвойстваПодписи.Вставить("ТипПодписи");
	СвойстваПодписи.Вставить("СрокДействияПоследнейМеткиВремени");
	СвойстваПодписи.Вставить("ПорядковыйНомер");
	СвойстваПодписи.Вставить("ПодписанныйОбъект");
	СвойстваПодписи.Вставить("ОшибкаПриАвтоматическомПродлении");
	СвойстваПодписи.Вставить("ОписаниеСертификата");
	СвойстваПодписи.Вставить("ДатаПодписиИзМетки", Дата(1, 1, 1));
	СвойстваПодписи.Вставить("ТипПодписи", Неопределено);
	
	Возврат СвойстваПодписи;
	
КонецФункции

// Приводит двоичные данные (некодированные и base64 двоичные данные) к некодированному значению.
// 
// Параметры:
//  ДвоичныеДанные - ДвоичныеДанные
// 
// Возвращаемое значение:
//  ДвоичныеДанные
Функция ДекодированныеДвоичныеДанные(ДвоичныеДанные) Экспорт
	ДекодированныеДанные = ПолучитьДвоичныеДанныеИзBase64ДвоичныхДанных(ДвоичныеДанные);
	Если ЗначениеЗаполнено(ДекодированныеДанные) Тогда
		Возврат ДекодированныеДанные;
	Иначе
		Возврат ДвоичныеДанные;
	КонецЕсли;
КонецФункции

// Возвращает структуру видов сертификатов электронной подписи.
// 
// Возвращаемое значение:
//  Структура:
//  * СертификатДолжностногоЛицаГосОрганаУЦ - Строка 
//  * СертификатЮЛ - Строка
//  * СертификатИП - Строка
//  * СертификатФЛ - Строка
//
Функция ВидыСертификатовЭлектроннойПодписи() Экспорт
	
	ВидыСертификатов = Новый Структура;

	ВидыСертификатов.Вставить("СертификатДолжностногоЛицаГосОрганаУЦ", 
		НСтр("ru = 'Сертификат должностного лица гос. органа, являющегося удостоверяющим центром'"));
	ВидыСертификатов.Вставить("СертификатЮЛ", НСтр("ru = 'Сертификат юридического лица'"));
	ВидыСертификатов.Вставить("СертификатИП", НСтр("ru = 'Сертификат индивидуального предпринимателя'"));
	ВидыСертификатов.Вставить("СертификатФЛ", НСтр("ru = 'Сертификат физического лица'"));
	
	Возврат ВидыСертификатов;
	
КонецФункции

// Возвращает пустые двоичные данные.
// 
// Возвращаемое значение:
//  ДвоичныеДанные
Функция ПустыеДвоичныеДанные()
	
	Поток = Новый ПотокВПамяти();
	Возврат Поток.ЗакрытьИПолучитьДвоичныеДанные();
	
КонецФункции