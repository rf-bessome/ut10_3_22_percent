#Область ПрограммныйИнтерфейс    

#Область Системные

// Функция, вызываемая перед началом работы системы.
//
Функция ПередНачаломРаботыСистемы() Экспорт
	
	Если глПодключаемоеОборудование = Неопределено Тогда
		глПодключаемоеОборудование = Новый Структура();
		глПодключаемоеОборудование.Вставить("ДрайвераПодключаемогоОборудования", Новый Соответствие());
		глПодключаемоеОборудование.Вставить("ПараметрыПодключенияПО"           , Новый Массив());
		глПодключаемоеОборудование.Вставить("ПоследнийСлипЧек"                 , "");
		глПодключаемоеОборудование.Вставить("ЗавершениеРаботыСистемы"          , Ложь);
		глПодключаемоеОборудование.Вставить("ДанныеОчереди"                    , Неопределено);
		глПодключаемоеОборудование.Вставить("ФискальныеУстройства"             , Новый Массив());
		глПодключаемоеОборудование.Вставить("ПроверкиКодаМаркировки"           , Новый Массив());
	КонецЕсли;
	
КонецФункции

// Функция, вызываемая при начале работы системы.
//
Функция ПриНачалеРаботыСистемы() Экспорт
	
#Если Не ВебКлиент Тогда
	ПереустановитьПомеченныеДрайверы();
#КонецЕсли

КонецФункции

// Функция, вызываемая при начале работы системы.
// Выполняет подготовку данных механизма.
// 
Функция ПередЗавершениемРаботыСистемы() Экспорт
	
	НачатьОтключениеВсегоОборудования();
	
КонецФункции

// Производит получение события от устройства.
// 
// Параметры:
// 	ОписаниеСобытия - Структура - содержит параметры:
// 	*Источник - Строка - источник события.
// 	ОписаниеОшибки - Строка - Описание
// Возвращаемое значение:
// 	Неопределено, Структура - Описание:
// * Источник - Строка -
// * Параметр - Структура- 
// * ИмяСобытия - Строка -  
Функция ПолучитьСобытиеОтУстройства(ОписаниеСобытия, ОписаниеОшибки = "") Экспорт
	
	Результат = Неопределено;
	
	// Поиск обработчика события
	Для Каждого Подключение Из глПодключаемоеОборудование.ПараметрыПодключенияПО Цикл
						  
		Если Подключение.ИсточникСобытия = ОписаниеСобытия.Источник
		 Или (ПустаяСтрока(Подключение.ИсточникСобытия) И Подключение.ИменаСобытий <> Неопределено) Тогда
		   
		   // Ищем среди подключенного оборудования устройство с полученным событием.
		    Событие = Подключение.ИменаСобытий.Найти(ОписаниеСобытия.Событие);
		    Если Событие <> Неопределено Тогда
		    	
		    	Если Подключение.ОбъектДрайвера = Неопределено Тогда
		    		// Сообщить об ошибке, что не удалось загрузить драйвер.
		    		ОписаниеОшибки = НСтр("ru='""%Наименование%"": Не удалось загрузить драйвер устройства.
		    									|Проверьте, что драйвер корректно установлен и зарегистрирован в системе.'");
		    		ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%Наименование%", Подключение.Наименование);
		    		Продолжить;
		    	КонецЕсли;
		    	
		    	ВходныеПараметры  = Новый Массив();
		    	ВходныеПараметры.Добавить(ОписаниеСобытия.Событие);
		    	ВходныеПараметры.Добавить(ОписаниеСобытия.Данные);
		    	ВыходныеПараметры = Неопределено;
		    	
		    	// Обрабатываем сообщение
		    	РезультатОбработки = ВыполнитьКоманду(Подключение.Ссылка, "ОбработатьСобытие", ВходныеПараметры, ВыходныеПараметры);
		    	Если РезультатОбработки Тогда
		    		// Оповещаем 
		    		Результат = Новый Структура();
		    		Результат.Вставить("ИмяСобытия", ВыходныеПараметры[0]);
		    		Результат.Вставить("Параметр",   ВыходныеПараметры[1]);
		    		Результат.Вставить("Источник",   "ПодключаемоеОборудование");
		    	КонецЕсли;
		    	
		    	// Оповещаем драйвер о завершении обработки события.
		    	ВходныеПараметры.Очистить();
		    	ВходныеПараметры.Добавить(РезультатОбработки);
		    	ВыполнитьКоманду(Подключение.Ссылка, "ЗавершитьОбработкуСобытия", ВходныеПараметры, ВыходныеПараметры);
		    КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Производит обработку данных события, полученных от клиента.
// 
// Параметры:
//  ОписаниеСобытия - Структура:
// 	*Источник - Строка - источник события.
// 	ОписаниеОшибки - Строка - Описание
// Возвращаемое значение:
// 	Булево - Описание
Функция ОбработатьСобытиеОтУстройства(ОписаниеСобытия, ОписаниеОшибки = "") Экспорт

	Результат = Истина;
	
	// Поиск обработчика события
	
	ПараметрыПодключенияПО = глПодключаемоеОборудование.ПараметрыПодключенияПО; //Массив Из Структура, где: *Параметры - Структура - .
	
	Для Каждого Подключение Из ПараметрыПодключенияПО Цикл
						  
		Если Подключение.ИсточникСобытия = ОписаниеСобытия.Источник
		 Или (ПустаяСтрока(Подключение.ИсточникСобытия) И Подключение.ИменаСобытий <> Неопределено) Тогда
		   
		   // Ищем среди подключенного оборудования устройство с полученным событием.
		    Событие = Подключение.ИменаСобытий.Найти(ОписаниеСобытия.Событие);
		    Если Событие <> Неопределено Тогда
				
				ОбработчикДрайвераМодуль = Подключение.ОбработчикДрайвераМодуль;
				ОбъектДрайвера = Подключение.ОбъектДрайвера;
				
				ВыходныеПараметры = Новый Массив();
				
				// Обрабатываем сообщение
				Результат = ОбработчикДрайвераМодуль.ОбработатьСобытие(ОбъектДрайвера, Подключение.Параметры, Подключение.ПараметрыПодключения, ОписаниеСобытия.Событие, ОписаниеСобытия.Данные, ВыходныеПараметры);
				// Обрабатываем сообщение
				Если Результат Тогда
					// Оповещаем 
					Оповестить(ВыходныеПараметры[0], ВыходныеПараметры[1], "ПодключаемоеОборудование");
				КонецЕсли;
				ОбработчикДрайвераМодуль.ЗавершитьОбработкуСобытия(ОбъектДрайвера, Подключение.Параметры, Подключение.ПараметрыПодключения, ВыходныеПараметры);
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Параметры подключения устройства.
// 
// Параметры:
//  ДанныеУстройства - Неопределено, Произвольный, Структура - Данные устройства
// 
// Возвращаемое значение:
//  Структура - Параметры подключения:
//   * Наименование - Строка.
//   * ТипОборудования - ПеречислениеСсылка.ТипыПодключаемогоОборудования.
//   * ПодключениеИзМакета - Булево.
//   * ПодключениеЛокальноПоИдентификатору - Булево. 
//   * ИдентификаторОбъекта - СправочникСсылка.ПодключаемоеОборудование.
//   * ВерсияДрайвера - Строка.
//   * ИмяМакетаДрайвера - Строка.
//   * РевизияИнтерфейса - Строка.
//   * ШиринаСтроки - Строка.
//   * ПечатьКвитанцийНаТерминале - Булево. 
//   * КороткиеСлипЧеки - Булево.
//   * ВыдачиНаличныхДенежныхСредств - Булево. 
//   * ОплатаЭлектроннымиСертификатами - Булево.
//   * СтолбцовНаДисплее - Число.
//   * СтрокНаДисплее - Число. 
//   * ДисплейОтображаетТекст - Булево.
//   * ДисплейОтображаетГрафику - Булево.
//   * ДисплейОтображаетQRКод - Булево.
//   * ИдентификаторУстройства - УникальныйИдентификатор.
//   * ОбъектДрайвера - Неопределено -
//   * Параметры - Неопределено -
//   * ОбработчикДрайвера - Неопределено -
//   * Клиенты - Массив из СправочникСсылка.РабочиеМеста -
//   * Ссылка - СправочникСсылка.ПодключаемоеОборудование.
//   * СетевоеОборудование - Булево. 
//   * КоличествоПодключенных - Число -
//   * ИменаСобытий - Массив Из Строка -
//   * ИДУстройства - Строка.
//
Функция ПараметрыПодключенияУстройства(ДанныеУстройства) Экспорт
	
	Подключение = МенеджерОборудованияКлиентСервер.ПараметрыПодключенияДрайвера(ДанныеУстройства);
	Подключение.Вставить("Клиенты", Новый Массив());
	Подключение.Вставить("Ссылка"                  , ДанныеУстройства.Ссылка);
	Подключение.Вставить("Параметры"               , ДанныеУстройства.Параметры);
	Подключение.Вставить("СетевоеОборудование"     , ДанныеУстройства.СетевоеОборудование);
	Подключение.Вставить("КоличествоПодключенных"  , 0);
	Подключение.Вставить("ИменаСобытий"            , Новый Массив());
	Подключение.Вставить("ИДУстройства"            , "");
#Если ВебКлиент Тогда
	Подключение.ОбработчикДрайвера = ПодключаемоеОборудованиеДрайверКлиент; 
#Иначе
	Подключение.ОбработчикДрайвера = ПодключаемоеОборудованиеДрайверСинхронноКлиент; 
#КонецЕсли
	Возврат Подключение;
	
КонецФункции

// Возвращает текущую дату, приведенную к часовому поясу сеанса.
// Предназначена для использования вместо функции ТекущаяДата().
//
// Возвращаемое значение:
//  Дата.
//
Функция ДатаСеанса() Экспорт
	
	ДатаСеанса = МенеджерОборудованияКлиентПереопределяемый.ДатаСеанса();
	Если ЗначениеЗаполнено(ДатаСеанса) Тогда
		Возврат ДатаСеанса;
	Иначе 
		Возврат МенеджерОборудованияВызовСервера.ДатаСеанса();
	КонецЕсли;
	
КонецФункции

// Функция возвращает подключено ли оборудование.
//
//  Параметры:
// 	ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - .
//
// Возвращаемое значение:
//  Булево.
Функция УстройствоПодключенно(ИдентификаторУстройства) Экспорт
	
	Для Каждого Подключение Из глПодключаемоеОборудование.ПараметрыПодключенияПО Цикл
		Если Подключение.Ссылка = ИдентификаторУстройства Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

// Производит подключение доступного оборудования по списку типов ПО
//
Функция ПодключитьОборудованиеПоТипу(ИдентификаторКлиента, ТипыПО, ОписаниеОшибки = "") Экспорт

	СтруктураТиповПО = Новый Структура();
	Если ТипЗнч(ТипыПО) = Тип("Массив") Тогда
		Для каждого ТипПО Из ТипыПО Цикл
			СтруктураТиповПО.Вставить(ТипПО);
		КонецЦикла;
	Иначе
		СтруктураТиповПО.Вставить(ТипыПО);
	КонецЕсли;

	Возврат ПодключитьОборудование(ИдентификаторКлиента, СтруктураТиповПО, , ОписаниеОшибки);

КонецФункции

// Сохраняет пользовательские настройки подключаемого оборудования.
//
Процедура СохранитьПользовательскиеНастройкиПодключаемогоОборудования(СписокНастроек) Экспорт
	
	МенеджерОборудованияВызовСервера.СохранитьПользовательскиеНастройкиПодключаемогоОборудования(СписокНастроек);
	
КонецПроцедуры

// Возвращает глобальные переменные подключаемого оборудования
//
// Возвращаемое значение:
//  Структура:
//   * ПараметрыПодключенияПО - Массив из Структура
//   * ДрайверыОборудования - Соответствие 
//   * ПоследнийСлипЧек - Строка
//   * ЗавершениеРаботыСистемы - Булево
//   * ДанныеОчереди - Неопределено
//   * ФискальныеУстройства - Массив из Структура
//   * ПроверкиКодаМаркировки - Массив из Структура
//
Функция ПодключаемоеОборудование() Экспорт
	
	Если ПараметрыПриложения = Неопределено Тогда
		ПараметрыПриложения = Новый Соответствие();
	КонецЕсли;
	
	ПодключаемоеОборудование = ПараметрыПриложения.Получить("БПО.ПодключаемоеОборудование");
	
	Если ПодключаемоеОборудование = Неопределено Тогда
		ПодключаемоеОборудование = Новый Структура();
		ПодключаемоеОборудование.Вставить("ПараметрыПодключенияПО" , Новый Массив());
		ПодключаемоеОборудование.Вставить("ДрайверыОборудования"   , Новый Соответствие());
		ПодключаемоеОборудование.Вставить("ПоследнийСлипЧек"       , "");
		ПодключаемоеОборудование.Вставить("ЗавершениеРаботыСистемы", Ложь);
		ПодключаемоеОборудование.Вставить("ДанныеОчереди"          , Неопределено);
		ПодключаемоеОборудование.Вставить("ФискальныеУстройства"   , Новый Массив());
		ПодключаемоеОборудование.Вставить("ПроверкиКодаМаркировки" , Новый Массив());
		ПараметрыПриложения.Вставить("БПО.ПодключаемоеОборудование", ПодключаемоеОборудование);
	КонецЕсли;
	
	Возврат ПодключаемоеОборудование;
	
КонецФункции

// Возвращает значения параметров, необходимых для работы клиентского кода конфигурации
// без дополнительных серверных вызовов.
// 
// Возвращаемое значение:
//   ФиксированнаяСтруктура:
//    * ИдентификаторКлиента - Строка - (входящий) идентификатор рабочего места клиента
//    * ИменаМакетовДляПереустановки - Массив из Строка - Имена макетов для переустановки внешних компонент
//    * ИдентификаторОбсужденияРаспределеннойФискализации - ИдентификаторОбсужденияСистемыВзаимодействия -
//
Функция ПараметрыРаботыКлиентаПриЗапуске() Экспорт
	
	Если ИспользуетсяБСП() Тогда
		МодульСтандартныеПодсистемыКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СтандартныеПодсистемыКлиент");
		ПараметрыРаботыКлиента = МодульСтандартныеПодсистемыКлиент.ПараметрыРаботыКлиентаПриЗапуске();
		Если ПараметрыРаботыКлиента.Свойство("ИдентификаторКлиентаБПО") Тогда
			Возврат ПараметрыРаботыКлиента;
		КонецЕсли;
	КонецЕсли;
	
	Возврат МенеджерОборудованияКлиентПовтИсп.ПараметрыРаботыКлиентаПриЗапуске();
	
КонецФункции

// Выполняет обработку внешнего события, вызывается из глобального модуля
//
// Параметры:
//  Источник - Строка.
//  Событие - Строка.
//  Данные - Строка.
Процедура ОбработкаВнешнегоСобытия(Источник, Событие, Данные) Экспорт

	ОписаниеОшибки  = "";
	// Подготовить данные
	ОписаниеСобытия = Новый Структура();
	ОписаниеСобытия.Вставить("Источник", Источник);
	ОписаниеСобытия.Вставить("Событие",  Событие);
	ОписаниеСобытия.Вставить("Данные",   Данные);
	// Передать на обработку данные.
	Результат = МенеджерОборудованияКлиент.ОбработатьСобытиеОтУстройства(ОписаниеСобытия, ОписаниеОшибки);
	Если Не Результат Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru='При обработке внешнего события от устройства произошла ошибка.'") + Символы.ПС + ОписаниеОшибки);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РабочееМесто

// Выполняет обновление имени компьютера в параметре сеанса "РабочееМестоКлиента".
//
// Возвращаемое значение:
//  Булево - .
Функция ОбновитьРабочееМестоКлиента() Экспорт
	
	Результат = Истина;
	
	РабочееМесто = МенеджерОборудованияКлиентПовтИсп.ПолучитьРабочееМестоКлиента();
	
	Если Не ЗначениеЗаполнено(РабочееМесто) Тогда
		МассивРабочихМест = МенеджерОборудованияКлиентПовтИсп.НайтиРабочиеМестаПоИД(МенеджерОборудованияКлиентСервер.ИдентификаторКлиентаДляРабочегоМеста());
		Если МассивРабочихМест.Количество() = 0 Тогда
			Параметры = Новый Структура;
			Параметры.Вставить("ИмяКомпьютера");
			Параметры.Вставить("ИдентификаторКлиента");
			
			#Если Не ВебКлиент Тогда
				Параметры.ИмяКомпьютера = ИмяКомпьютера();
			#КонецЕсли
			
			Параметры.ИдентификаторКлиента = МенеджерОборудованияКлиентСервер.ИдентификаторКлиентаДляРабочегоМеста();
			РабочееМесто = МенеджерОборудованияВызовСервера.СоздатьРабочееМестоКлиента(Параметры);
		Иначе
			РабочееМесто = МассивРабочихМест[0];
		КонецЕсли;
		
	КонецЕсли;
	
	Если Результат И РабочееМесто <> МенеджерОборудованияКлиентПовтИсп.ПолучитьРабочееМестоКлиента() Тогда
		МенеджерОборудованияВызовСервера.УстановитьРабочееМестоКлиента(РабочееМесто);
		Оповестить("ИзмененоРабочееМестоТекущегоСеанса", РабочееМесто);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Процедура для выбора рабочего места текущего сеанса.
//
Процедура ВыбратьРМТекущегоСеанса(ПараметрКоманды, ПараметрыВыполненияКоманды) Экспорт
	
	Оповещение = ОписаниеОповещенияИС("ПредложитьВыборРабочегоМестаЗавершение", МенеджерОборудованияКлиент);
	ПредложитьВыборРабочегоМеста(Оповещение);
	
КонецПроцедуры

// Функция предоставляет диалог выбора рабочего места.
// 
Процедура ПредложитьВыборРабочегоМеста(ОбработкаОповещения, ИдентификаторКлиента = "") Экспорт

	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ИдентификаторКлиента", ИдентификаторКлиента);
	
	ОткрытьФормуИС("Справочник.ПодключаемоеОборудование.Форма.ФормаВыбораРабочегоМеста", ПараметрыФормы,,,  ,, ОбработкаОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

// Завершение выбора рабочего места.
//
Процедура ПредложитьВыборРабочегоМестаЗавершение(Результат, Параметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") И Результат.Свойство("РабочееМесто") Тогда 
		УстановитьРабочееМесто(Результат.РабочееМесто);
	КонецЕсли;
		
КонецПроцедуры

// Функция устанавливает рабочее место.
// 
Процедура УстановитьРабочееМесто(РабочееМесто) Экспорт
	
	МенеджерОборудованияВызовСервера.УстановитьРабочееМестоКлиента(РабочееМесто);
	Оповестить("ИзмененоРабочееМестоТекущегоСеанса", РабочееМесто);
	
КонецПроцедуры

// Открытие формы списка рабочих мест.
//
Процедура ОткрытьРабочиеМеста(ПараметрКоманды, ПараметрыВыполненияКоманды) Экспорт
	
	ОбновитьРабочееМестоКлиента();
	ПараметрыФормы = Новый Структура();
	ОткрытьФормуИС("Справочник.РабочиеМеста.ФормаСписка", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно);
	
КонецПроцедуры

#КонецОбласти

#Область ОбщиеМетодыДрайвера

// Поиск по идентификатору подключенного ранее устройства. 
//
//  Параметры:
// 	СписокПодключений - Массив из Структура - содержит:
// 	*Ссылка - СправочникСсылка.ПодключаемоеОборудование - .
// 	Идентификатор - СправочникСсылка.ПодключаемоеОборудование - .
// Возвращаемое значение:
// 	СправочникСсылка.ПодключаемоеОборудование - Описание.
Функция ПолучитьПодключенноеУстройство(СписокПодключений, Идентификатор) Экспорт
	
	ПодключенноеУстройство = Неопределено;
	
	Для Каждого Подключение Из СписокПодключений Цикл
		Если Подключение.Ссылка = Идентификатор Тогда
			ПодключенноеУстройство = Подключение;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПодключенноеУстройство;
	
КонецФункции

// Поиск по типу подключенные ранее устройства.
//
// Параметры:
// 	СписокПодключений - Массив из Структура - где:
// 	*Ссылка - СправочникСсылка.ПодключаемоеОборудование - .
// 	ТипПО - ПеречислениеСсылка.ТипыПодключаемогоОборудования - Описание
// 	Идентификатор - Неопределено - Описание
// Возвращаемое значение:
// 	Массив Из Структура - где:
// 	*Ссылка - СправочникСсылка.ПодключаемоеОборудование - .
Функция ПолучитьПодключенныеУстройства(СписокПодключений, ТипПО, Идентификатор = Неопределено) Экспорт
	
	ПодключенныеУстройства = Новый Массив();
	Для Каждого Подключение Из СписокПодключений Цикл
		Если Подключение.ТипОборудования = ТипПО Тогда
			Если (Идентификатор <> Неопределено) Тогда
				Если (Подключение.Ссылка = Идентификатор) Тогда
					ПодключенныеУстройства.Добавить(Подключение);
					Прервать;
				КонецЕсли;
			Иначе
				ПодключенныеУстройства.Добавить(Подключение);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПодключенныеУстройства;
	
КонецФункции

// Поиск по типу подключенные ранее устройства.
//
// Параметры:
//  ИдентификаторОбъекта - СправочникСсылка.ПодключаемоеОборудование - экземпляр подключаемого оборудования.
//
// Возвращаемое значение:
//  Массив.
//
Функция ПодключенныеУстройстваПоИдентификаторуОбъекта(ИдентификаторОбъекта) Экспорт
	
	ПодключаемоеОборудование = ПодключаемоеОборудование();
	СписокПодключений = ПодключаемоеОборудование.ПараметрыПодключенияПО;
	
	ПодключенныеУстройства = Новый Массив();
	Для Каждого Подключение Из СписокПодключений Цикл
		Если Подключение.ИдентификаторОбъекта = ИдентификаторОбъекта Тогда
			ПодключенныеУстройства.Добавить(Подключение);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПодключенныеУстройства;
	
КонецФункции

// Начать выполнение дополнительной команды к драйверу, не требующую предварительного подключения устройства в системе.
//
Процедура НачатьВыполнениеДополнительнойКоманды(ОповещениеПриЗавершении, Команда, ВходныеПараметры, Идентификатор, Параметры) Экспорт
	
	// Поиск подключенного устройства.
	ПодключенноеУстройство = ПолучитьПодключенноеУстройство(глПодключаемоеОборудование.ПараметрыПодключенияПО, Идентификатор);
	                                                       
	Если ПодключенноеУстройство = Неопределено Тогда
		ДанныеОборудования = МенеджерОборудованияВызовСервера.ПолучитьДанныеУстройства(Идентификатор);
		ПараметрыКоманды = Новый Структура();
		ПараметрыКоманды.Вставить("Команда"           , Команда);
		ПараметрыКоманды.Вставить("ВходныеПараметры"  , ВходныеПараметры);
		ПараметрыКоманды.Вставить("Параметры"         , Параметры);
		ПараметрыКоманды.Вставить("ДанныеОборудования", ДанныеОборудования);
		ПараметрыКоманды.Вставить("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
		Оповещение = Новый ОписаниеОповещения("НачатьВыполнениеДополнительнойКоманды_Завершение", МенеджерОборудованияКлиент, ПараметрыКоманды);
		НачатьПолучениеОбъектаДрайвера(Оповещение, ДанныеОборудования);
	Иначе
		// Сообщить об ошибке, что устройство подключено.
		ТекстОшибки = НСтр("ru='Устройство подключено, операция невозможна. Для отключения устройства требуется закрыть все формы приложения для текущей информационной базы.'");
		ВыходныеПараметры = Новый Массив();    
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(ТекстОшибки);
		ВыходныеПараметры.Добавить(НСтр("ru='Установлен'"));
		РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ТекстОшибки, Идентификатор); 
		РезультатВыполнения.ВыходныеПараметры = ВыходныеПараметры;
		ВыполнитьОбработкуОповещенияИС(ОповещениеПриЗавершении, РезультатВыполнения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПодключениеОтключенияОборудования

// Производит подключение доступного оборудования по списку типов ПО
//
Процедура НачатьПодключениеОборудованиеПоТипу(ОповещениеПриПодключении, ИдентификаторКлиента, ТипыПО) Экспорт
	
	СтруктураТиповПО = Новый Структура();
	Если ТипЗнч(ТипыПО) = Тип("Массив") Тогда
		Для Каждого ТипПО Из ТипыПО Цикл
			СтруктураТиповПО.Вставить(ТипПО);
		КонецЦикла;
	Иначе
		СтруктураТиповПО.Вставить(ТипыПО);
	КонецЕсли;
	
	НачатьПодключениеОборудования(ОповещениеПриПодключении, ИдентификаторКлиента, СтруктураТиповПО);
	 
 КонецПроцедуры

// Начать подключать одиночный экземпляр устройства определяемый идентификатором.
//
Процедура НачатьПодключениеОборудованиеПоИдентификатору(ОповещениеПриПодключении, ИдентификаторКлиента, ИдентификаторУстройства) Экспорт
	
	НачатьПодключениеОборудования(ОповещениеПриПодключении, ИдентификаторКлиента, , ИдентификаторУстройства);
	
КонецПроцедуры

// Начать подключения устройства.
//
// Параметры:
//  ОповещениеПриПодключении - ОписаниеОповещения - событие описания оповещения.
//  ИдентификаторКлиента - УникальныйИдентификатор - уникальный идентификатор клиента.
//  ТипыПО - Структура, Массив, Строка - тип оборудования для выбора устройства.
//  ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - подключаемое устройство.
// 
Процедура НачатьПодключениеОборудования(ОповещениеПриПодключении, ИдентификаторКлиента, ТипыПО = Неопределено, ИдентификаторУстройства = Неопределено) Экспорт
	   
	ОбъектДрайвера = Неопределено;
	
	Результат = ОбновитьРабочееМестоКлиента();
	Если Не Результат Тогда
		Если ОповещениеПриПодключении <> Неопределено Тогда
			ОписаниеОшибки = НСтр("ru='Предварительно необходимо выбрать рабочее место подключаемого оборудования текущего сеанса.'");
			РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Результат, ОписаниеОшибки);
			ВыполнитьОбработкуОповещенияИС(ОповещениеПриПодключении, РезультатВыполнения);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	СписокОборудования = МенеджерОборудованияКлиентПовтИсп.ОборудованиеПоПараметрам(ТипыПО, ИдентификаторУстройства);
	
	Если СписокОборудования.Количество() > 0 Тогда
		Для Каждого Устройство Из СписокОборудования Цикл
			
			// Проверим, не подключено ли устройство ранее.
			ПодключенноеУстройство = ПолучитьПодключенноеУстройство(глПодключаемоеОборудование.ПараметрыПодключенияПО, Устройство.Ссылка);
			
			Если ПодключенноеУстройство = Неопределено Тогда // Если устройство не было подключено ранее.
				
				ОбработчикДрайвераМодуль = МенеджерОборудованияКлиентПовтИсп.ПолучитьОбработчикДрайвера(Устройство.ОбработчикДрайвера, Не Устройство.ВСоставеКонфигурации, Устройство.ТипОборудованияИмя);
				
				НовоеПодключение = Новый Структура();
				НовоеПодключение.Вставить("Клиенты"                 , Новый Массив());
				НовоеПодключение.Клиенты.Добавить(ИдентификаторКлиента);
				НовоеПодключение.Вставить("Ссылка"                  , Устройство.Ссылка);
				НовоеПодключение.Вставить("ИдентификаторУстройства" , Устройство.ИдентификаторУстройства);
				НовоеПодключение.Вставить("ОбработчикДрайвера"      , Устройство.ОбработчикДрайвера);
				НовоеПодключение.Вставить("Наименование"            , Устройство.Наименование);
				НовоеПодключение.Вставить("ТипОборудования"         , Устройство.ТипОборудования);
				НовоеПодключение.Вставить("ТипОборудованияИмя"      , Устройство.ТипОборудованияИмя);
				НовоеПодключение.Вставить("ДрайверОборудования"     , Устройство.ДрайверОборудования);
				НовоеПодключение.Вставить("ВСоставеКонфигурации"    , Устройство.ВСоставеКонфигурации);
				НовоеПодключение.Вставить("ИдентификаторОбъекта"    , Устройство.ИдентификаторОбъекта);
				НовоеПодключение.Вставить("ИмяМакетаДрайвера"       , Устройство.ИмяМакетаДрайвера);
				НовоеПодключение.Вставить("ИмяФайлаДрайвера"        , Устройство.ИмяФайлаДрайвера);
				НовоеПодключение.Вставить("РабочееМесто"            , Устройство.РабочееМесто);
				НовоеПодключение.Вставить("ИмяКомпьютера"           , Устройство.ИмяКомпьютера);
				НовоеПодключение.Вставить("Параметры"               , Устройство.Параметры);
				НовоеПодключение.Вставить("КоличествоПодключенных"  , 1);
				НовоеПодключение.Вставить("ПараметрыПодключения"    , Новый Структура());
				НовоеПодключение.Вставить("ОбъектДрайвера"          , Неопределено);
				НовоеПодключение.Вставить("ОбработчикДрайвераМодуль", ОбработчикДрайвераМодуль);
				НовоеПодключение.ПараметрыПодключения.Вставить("ТипОборудования"     , Устройство.ТипОборудованияИмя);
				НовоеПодключение.ПараметрыПодключения.Вставить("ПараметрыРегистрации", Устройство.ПараметрыРегистрации);
				НовоеПодключение.ПараметрыПодключения.Вставить("ИдентификаторУстройства", Устройство.Ссылка);
				
				Если ОбработчикДрайвераМодуль = Неопределено Тогда
					// Сообщить об ошибке, что не удалось подключить обработчик.
					Если ОповещениеПриПодключении <> Неопределено Тогда
						ОписаниеОшибки = НСтр("ru='Не удалось подключить обработчик драйвера.'");
						РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки);
						ВыполнитьОбработкуОповещенияИС(ОповещениеПриПодключении, РезультатВыполнения);
					КонецЕсли;
					Продолжить;
				Иначе
					
					// Разделение на асинхронные и синхронные вызовы.
					Если ОбработчикДрайвераМодуль.ПоддержкаАсинхронногоРежима() Тогда
						// Асинхронные вызовы
						ПараметрыКоманды = Новый Структура("НовоеПодключение, ОповещениеПриПодключении, ОбработчикДрайвераМодуль", НовоеПодключение, ОповещениеПриПодключении, ОбработчикДрайвераМодуль);
						Оповещение = ОписаниеОповещенияИС("НачатьПодключениеОборудования_ПолучениеОбъектаДрайвераЗавершение", МенеджерОборудованияКлиент, ПараметрыКоманды);
						НачатьПолучениеОбъектаДрайвера(Оповещение, Устройство);
					Иначе
						// Синхронные
						ОбъектДрайвера = ПолучитьОбъектДрайвера(Устройство);
						Если ОбъектДрайвера = Неопределено Тогда
							Если ОповещениеПриПодключении <> Неопределено Тогда
								// Сообщить об ошибке, что не удалось загрузить драйвер.
								ОписаниеОшибки = НСтр("ru='%Наименование%: Не удалось загрузить драйвер устройства.
													|Проверьте, что драйвер корректно установлен и зарегистрирован в системе.'");
								ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%Наименование%",НовоеПодключение.Наименование);
								РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки);
								ВыполнитьОбработкуОповещенияИС(ОповещениеПриПодключении, РезультатВыполнения);
							КонецЕсли;
							Продолжить;
						Иначе
							ВыходныеПараметры = Неопределено;
							Результат = ОбработчикДрайвераМодуль.ПодключитьУстройство(ОбъектДрайвера, НовоеПодключение.Параметры, НовоеПодключение.ПараметрыПодключения, ВыходныеПараметры);
							
							Если Результат Тогда
								
								Если ВыходныеПараметры.Количество() >= 2 Тогда
									НовоеПодключение.Вставить("ИсточникСобытия", ВыходныеПараметры[0]);
									НовоеПодключение.Вставить("ИменаСобытий",    ВыходныеПараметры[1]);
								Иначе
									НовоеПодключение.Вставить("ИсточникСобытия", "");
									НовоеПодключение.Вставить("ИменаСобытий",    Неопределено);
								КонецЕсли;
								НовоеПодключение.ОбъектДрайвера = ОбъектДрайвера;
								МассивПараметровПодключения = глПодключаемоеОборудование.ПараметрыПодключенияПО; //Массив - 
								МассивПараметровПодключения.Добавить(НовоеПодключение);
								
								Если ОповещениеПриПодключении <> Неопределено Тогда
									ОписаниеОшибки = НСтр("ru='Ошибок нет.'");
									РезультатВыполнения = Новый Структура("Результат, ОписаниеОшибки, ПараметрыПодключения", Истина, ОписаниеОшибки, НовоеПодключение.ПараметрыПодключения);
									ВыполнитьОбработкуОповещенияИС(ОповещениеПриПодключении, РезультатВыполнения);
								КонецЕсли;
								
							Иначе
								// Сообщим пользователю о том, что не удалось подключить устройство.
								Если ОповещениеПриПодключении <> Неопределено Тогда
									ОписаниеОшибки = НСтр("ru='Не удалось подключить устройство ""%Наименование%"": %ОписаниеОшибки% (%КодОшибки%)'");
									ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%Наименование%"  , НовоеПодключение.Наименование);
									ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%ОписаниеОшибки%", ВыходныеПараметры[1]);
									ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%КодОшибки%"     , ВыходныеПараметры[0]);
									РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки);
									ВыполнитьОбработкуОповещенияИС(ОповещениеПриПодключении, РезультатВыполнения);
								КонецЕсли;
							КонецЕсли;
						КонецЕсли;
						
					КонецЕсли;
					
				КонецЕсли;
			
			Иначе // Устройство было подключено ранее.
				// Увеличим количество пользователей данного соединения.
				МассивКлиентов = ПодключенноеУстройство.Клиенты; //Массив - 
				МассивКлиентов.Добавить(ИдентификаторКлиента);
				ПодключенноеУстройство.КоличествоПодключенных = ПодключенноеУстройство.КоличествоПодключенных + 1;
				Если ОповещениеПриПодключении <> Неопределено Тогда
					ОписаниеОшибки = НСтр("ru='Ошибок нет.'");
					РезультатВыполнения = Новый Структура("Результат, ОписаниеОшибки, ПараметрыПодключения", Истина, ОписаниеОшибки, ПодключенноеУстройство.ПараметрыПодключения);
					ВыполнитьОбработкуОповещенияИС(ОповещениеПриПодключении, РезультатВыполнения);
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли ИдентификаторУстройства <> Неопределено И ОповещениеПриПодключении <> Неопределено Тогда
		ОписаниеОшибки = 
			НСтр("ru='Устройство отсутствует на текущем рабочем месте и не может использоваться для подключения. Укажите другое устройство.'");
		РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки);
		ВыполнитьОбработкуОповещенияИС(ОповещениеПриПодключении, РезультатВыполнения);
	ИначеЕсли ТипыПО = Неопределено И ИдентификаторУстройства = Неопределено Тогда
		ОписаниеОшибки =  НСтр("ru='Нет доступного оборудования для подключения.'");
		РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки);
		ВыполнитьОбработкуОповещенияИС(ОповещениеПриПодключении, РезультатВыполнения);
	КонецЕсли;

КонецПроцедуры

// НачинаетПодключениеОборудования.
// 
// Параметры:
// 	РезультатВыполнения - Структура - результат выполнения операции.
// 	Параметры - Структура - где:
// 	*НовоеПодключение - Структура - где:
// 	**Наименование - Строка - .
// 	
Процедура НачатьПодключениеОборудования_Завершение(РезультатВыполнения, Параметры) Экспорт
	
	Если РезультатВыполнения.Результат Тогда
		Если РезультатВыполнения.ВыходныеПараметры.Количество() >= 2 Тогда
			Параметры.НовоеПодключение.Вставить("ИсточникСобытия", Параметры.ВыходныеПараметры[0]);
			Параметры.НовоеПодключение.Вставить("ИменаСобытий",    Параметры.ВыходныеПараметры[1]);
		Иначе
			Параметры.НовоеПодключение.Вставить("ИсточникСобытия", "");
			Параметры.НовоеПодключение.Вставить("ИменаСобытий",    Неопределено);
		КонецЕсли;
		
		ПараметрыПодключенияПО = глПодключаемоеОборудование.ПараметрыПодключенияПО; // Массив -
		ПараметрыПодключенияПО.Добавить(Параметры.НовоеПодключение);
		
		Если Параметры.ОповещениеПриПодключении <> Неопределено Тогда
			ОписаниеОшибки = НСтр("ru='Ошибок нет.'");
			РезультатВыполнения = Новый Структура("Результат, ОписаниеОшибки, ПараметрыПодключения", Истина, ОписаниеОшибки, Параметры.НовоеПодключение.ПараметрыПодключения);
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриПодключении, РезультатВыполнения);
		КонецЕсли;
	Иначе
		// Сообщим пользователю о том, что не удалось подключить устройство.
		Если Параметры.ОповещениеПриПодключении <> Неопределено Тогда
			ОписаниеОшибки = НСтр("ru='Не удалось подключить устройство ""%Наименование%"": %ОписаниеОшибки%'");
			ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%Наименование%"  , Параметры.НовоеПодключение.Наименование);
			ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%ОписаниеОшибки%", РезультатВыполнения.ВыходныеПараметры[1]);
			РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки);
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриПодключении, РезультатВыполнения);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Начать отключение устройств по типу оборудования.
//
Процедура НачатьОтключениеОборудованиеПоТипу(ОповещениеПриОтключении, ИдентификаторКлиента, ТипыПО) Экспорт
	
	НачатьОтключениеОборудование(ОповещениеПриОтключении, ИдентификаторКлиента, ТипыПО, );
	
КонецПроцедуры

// Начать отключать устройства определенное идентификатором.
//
Процедура НачатьОтключениеОборудованиеПоИдентификатору(ОповещениеПриОтключении, ИдентификаторКлиента, ИдентификаторУстройства) Экспорт
	
	НачатьОтключениеОборудование(ОповещениеПриОтключении, ИдентификаторКлиента, , ИдентификаторУстройства);
	
КонецПроцедуры

// Функция подключает устройства по типу оборудования.
// 
Процедура НачатьОтключениеОборудование(ОповещениеПриОтключении, ИдентификаторКлиента, ТипыПО = Неопределено, ИдентификаторУстройства = Неопределено)
	
	Если глПодключаемоеОборудование.ПараметрыПодключенияПО <> Неопределено Тогда
		КоличествоУстройств = глПодключаемоеОборудование.ПараметрыПодключенияПО.Количество();
		Для Индекс = 1 По КоличествоУстройств Цикл
			
			ПодключенноеУстройство = глПодключаемоеОборудование.ПараметрыПодключенияПО[КоличествоУстройств - Индекс]; //СправочникСсылка.ПодключаемоеОборудование - .
			КлиентПодключения = ПодключенноеУстройство.Клиенты.Найти(ИдентификаторКлиента);
			
			Если КлиентПодключения <> Неопределено  И (ТипыПО = Неопределено Или ТипыПО.Найти(ПодключенноеУстройство.ТипОборудованияИмя) <> Неопределено)
			   И (ИдентификаторУстройства = Неопределено  Или ПодключенноеУстройство.Ссылка = ИдентификаторУстройства) Тогда
				
				Если ПодключенноеУстройство.КоличествоПодключенных = 1 Тогда
					
					ОбработчикДрайвераМодуль = ПодключенноеУстройство.ОбработчикДрайвераМодуль;
					ОбъектДрайвера = ПодключенноеУстройство.ОбъектДрайвера;
					
					Если ОбработчикДрайвераМодуль = Неопределено Тогда
						// Сообщить об ошибке, что не удалось подключить обработчик.
						Если ОповещениеПриОтключении <> Неопределено Тогда
							ОписаниеОшибки = НСтр("ru='Не удалось подключить обработчик драйвера.'");
							РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки);
							ВыполнитьОбработкуОповещенияИС(ОповещениеПриОтключении, РезультатВыполнения);
						КонецЕсли;
					Иначе
						
						Если ОбъектДрайвера = Неопределено Тогда
							Если ОповещениеПриОтключении <> Неопределено Тогда
								// Сообщить об ошибке, что не удалось загрузить драйвер.
								ОписаниеОшибки = НСтр("ru='%Наименование%: Не удалось загрузить драйвер устройства.
													|Проверьте, что драйвер корректно установлен и зарегистрирован в системе.'");
								ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%Наименование%", ПодключенноеУстройство.Наименование);
								РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки);
								ВыполнитьОбработкуОповещенияИС(ОповещениеПриОтключении, РезультатВыполнения);
							КонецЕсли;
						Иначе
							
							ВыходныеПараметры = Неопределено;
							           
							// Разделение на асинхронные и синхронные вызовы.
							Если ОбработчикДрайвераМодуль.ПоддержкаАсинхронногоРежима() Тогда
								
								ПараметрыКоманды = Новый Структура("ПодключенноеУстройство, ОповещениеПриОтключении", ПодключенноеУстройство, ОповещениеПриОтключении);
								Оповещение = ОписаниеОповещенияИС("НачатьОтключениеОборудование_Завершение", МенеджерОборудованияКлиент, ПараметрыКоманды);
								ОбработчикДрайвераМодуль.НачатьОтключениеУстройства(Оповещение, ОбъектДрайвера, 
									ПодключенноеУстройство.Параметры, ПодключенноеУстройство.ПараметрыПодключения, ВыходныеПараметры);
							Иначе
								Результат = ОбработчикДрайвераМодуль.ОтключитьУстройство(ОбъектДрайвера, ПодключенноеУстройство.Параметры, ПодключенноеУстройство.ПараметрыПодключения, ВыходныеПараметры);
								Если НЕ Результат Тогда
									// Сообщим пользователю о том, что не удалось подключить устройство.
									Если ОповещениеПриОтключении <> Неопределено Тогда
										ОписаниеОшибки = НСтр("ru='При отключении устройства ""%Наименование%"" произошла ошибка: %ОписаниеОшибки%'");
										ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%Наименование%"  , ПодключенноеУстройство.Наименование);
										ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%ОписаниеОшибки%", ВыходныеПараметры[1]);
										РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки);
										ВыполнитьОбработкуОповещенияИС(ОповещениеПриОтключении, РезультатВыполнения);
									КонецЕсли;
								Иначе
									ПодключенноеУстройство.КоличествоПодключенных = 0;
								КонецЕсли;
								
								НомерСтрокиМассива = глПодключаемоеОборудование.ПараметрыПодключенияПО.Найти(ПодключенноеУстройство);
								Если НомерСтрокиМассива <> Неопределено Тогда
									глПодключаемоеОборудование.ПараметрыПодключенияПО.Удалить(НомерСтрокиМассива);
								КонецЕсли;
								
								Если ОповещениеПриОтключении <> Неопределено Тогда
									ОписаниеОшибки = НСтр("ru='Ошибок нет.'");
									РезультатВыполнения = Новый Структура("Результат, ОписаниеОшибки", Истина, ОписаниеОшибки);
									ВыполнитьОбработкуОповещенияИС(ОповещениеПриОтключении, РезультатВыполнения);
								КонецЕсли;
							КонецЕсли;
							
						КонецЕсли;
					КонецЕсли;
				Иначе
					ПодключенноеУстройство.КоличествоПодключенных = ПодключенноеУстройство.КоличествоПодключенных - 1;
					ПодключенноеУстройство.Клиенты.Удалить(КлиентПодключения);
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры  

// Отключает оборудование.
// 
// Параметры:
// 	РезультатВыполнения - Структура - структура результата выполнения операции.
// 	Параметры - Структура - где:
// 	*ПодключенноеУстройство - СправочникСсылка.ПодключаемоеОборудование - .
Процедура НачатьОтключениеОборудование_Завершение(РезультатВыполнения, Параметры) Экспорт
	
	Если РезультатВыполнения.Результат Тогда
		
		Параметры.ПодключенноеУстройство.КоличествоПодключенных = 0;
		
		НомерСтрокиМассива = глПодключаемоеОборудование.ПараметрыПодключенияПО.Найти(Параметры.ПодключенноеУстройство);
		Если НомерСтрокиМассива <> Неопределено Тогда
			глПодключаемоеОборудование.ПараметрыПодключенияПО.Удалить(НомерСтрокиМассива);
		КонецЕсли;
		Если Параметры.ОповещениеПриОтключении <> Неопределено Тогда
			ОписаниеОшибки = НСтр("ru='Ошибок нет.'");
			РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Истина, ОписаниеОшибки);
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриОтключении, РезультатВыполнения);
		КонецЕсли;
	Иначе
		// Сообщим пользователю о том, что не удалось подключить устройство.
		Если Параметры.ОповещениеПриОтключении <> Неопределено Тогда
			ОписаниеОшибки = НСтр("ru='При отключении устройства ""%Наименование%"" произошла ошибка.'");
			ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%Наименование%"  , Параметры.ПодключенноеУстройство.Наименование);
			РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки);
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриОтключении, РезультатВыполнения);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Производит принудительное отключение всего подключенного оборудования,
// независимо от числа ссылок на подключение.
Процедура НачатьОтключениеВсегоОборудования(ОповещениеПриОтключении = Неопределено) Экспорт
	
	КонечныйРезультат = Истина;
	Результат         = Истина;
	
	ПодключенноеОборудование = глПодключаемоеОборудование.ПараметрыПодключенияПО; //Массив Из Структура, где: *Параметры - Структура - .
	
	Для Каждого ПодключенноеУстройство Из ПодключенноеОборудование Цикл
		
		Если ПодключенноеУстройство.ОбъектДрайвера = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ВыходныеПараметры = Неопределено;
		ОбработчикДрайвераМодуль = ПодключенноеУстройство.ОбработчикДрайвераМодуль;
		Если ОбработчикДрайвераМодуль.ПоддержкаАсинхронногоРежима() Тогда
			Оповещение = ОписаниеОповещенияИС("НачатьОтключениеВсегоОборудованияЗавершение", МенеджерОборудованияКлиент);
			ОбработчикДрайвераМодуль.НачатьОтключениеУстройства(Оповещение, ПодключенноеУстройство.ОбъектДрайвера, ПодключенноеУстройство.Параметры, 
				ПодключенноеУстройство.ПараметрыПодключения, ВыходныеПараметры);
			ПодключенноеУстройство.КоличествоПодключенных = 0;
			Результат = Истина;
		Иначе
			Результат = ОбработчикДрайвераМодуль.ОтключитьУстройство(ПодключенноеУстройство.ОбъектДрайвера, ПодключенноеУстройство.Параметры, 
				ПодключенноеУстройство.ПараметрыПодключения, ВыходныеПараметры);
			Если Результат Тогда
				ПодключенноеУстройство.КоличествоПодключенных = 0;
			КонецЕсли;
		КонецЕсли;
		КонечныйРезультат = КонечныйРезультат И Результат;
		
	КонецЦикла;
	
	КоличествоПодключенныхКлиентов = 0;
	Для Каждого ПодключенноеУстройство Из глПодключаемоеОборудование.ПараметрыПодключенияПО Цикл
		КоличествоПодключенныхКлиентов = КоличествоПодключенныхКлиентов + ПодключенноеУстройство.КоличествоПодключенных;
	КонецЦикла;
	
	глПодключаемоеОборудование.ПараметрыПодключенияПО.Очистить();
	
КонецПроцедуры

Процедура НачатьОтключениеВсегоОборудованияЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если НЕ РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр("ru='При отключении оборудования произошла ошибка: ""%ОписаниеОшибки%"".'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%" , РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);;
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьПодключениеОборудования_ПолучениеОбъектаДрайвераЗавершение(ОбъектДрайвера, Параметры) Экспорт
	
	Если ОбъектДрайвера = Неопределено Тогда
		
		Если Параметры.ОповещениеПриПодключении <> Неопределено Тогда
			// Сообщить об ошибке, что не удалось загрузить драйвер.
			ОписаниеОшибки = НСтр("ru='%Наименование%: Не удалось загрузить драйвер устройства.
										|Проверьте, что драйвер корректно установлен и зарегистрирован в системе.'");
			ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%Наименование%", Параметры.НовоеПодключение.Наименование);
			РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки);
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриПодключении, РезультатВыполнения);
		КонецЕсли;
		
	Иначе
		Параметры.НовоеПодключение.ОбъектДрайвера = ОбъектДрайвера;
		Оповещение = ОписаниеОповещенияИС("НачатьПодключениеОборудования_Завершение", МенеджерОборудованияКлиент, Параметры);
		Параметры.ОбработчикДрайвераМодуль.НачатьПодключениеУстройства(Оповещение, ОбъектДрайвера, 
			 Параметры.НовоеПодключение.Параметры,  Параметры.НовоеПодключение.ПараметрыПодключения, Параметры);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииПодключенияОтключенияОборудованияВФорме

// Начать подключение необходимых типов оборудования при открытии формы.
//
// Параметры:
//	ОповещениеПриПодключении - ОписаниеОповещения -.
//	Форма - ФормаКлиентскогоПриложения
//	ПоддерживаемыеТипыПодключаемогоОборудования - Строка
//		Содержит перечень типов подключаемого оборудования, разделенных запятыми.
//
Процедура НачатьПодключениеОборудованиеПриОткрытииФормы(ОповещениеПриПодключении, Форма, ПоддерживаемыеТипыПодключаемогоОборудования) Экспорт
	
	Форма.ПоддерживаемыеТипыПодключаемогоОборудования = ПоддерживаемыеТипыПодключаемогоОборудования;
	
	Если Форма.ИспользоватьПодключаемоеОборудование Тогда
			
	Если ОповещениеПриПодключении = Неопределено Тогда
		ОповещениеПриПодключении = ОписаниеОповещенияИС("ПодключитьОборудованиеЗавершениеПоУмолчанию", МенеджерОборудованияКлиент);
	КонецЕсли;
		НачатьПодключениеОборудованиеПоТипу(ОповещениеПриПодключении, Форма.УникальныйИдентификатор,
											ПреобразоватьСписокСтрокойВМассив(Форма.ПоддерживаемыеТипыПодключаемогоОборудования));
	КонецЕсли;
	
КонецПроцедуры

// Обработчик оповещения по умолчанию
//
Процедура ПодключитьОборудованиеЗавершениеПоУмолчанию(РезультатВыполнения, Параметры) Экспорт
	
	Если НЕ РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр("ru='При подключении оборудования произошла ошибка:""%ОписаниеОшибки%"".'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%" , РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);;
	КонецЕсли;
	
КонецПроцедуры

// Отключает все подключенное оборудование, соответствующее указанному типу ПО
//
Функция ОтключитьОборудованиеПоТипу(ИдентификаторКлиента, ТипыПО, ОписаниеОшибки = "") Экспорт

	ОповещениеПриОтключении = ОписаниеОповещенияИС("ОтключитьОборудованиеЗавершениеПоУмолчанию", МенеджерОборудованияКлиент);
	
	НачатьОтключениеОборудованиеПоТипу(ОповещениеПриОтключении, ИдентификаторКлиента, ТипыПО);
	
	Возврат Неопределено;

КонецФункции

// Начать отключать оборудование по типу при закрытии формы.
//
Процедура НачатьОтключениеОборудованиеПриЗакрытииФормы(ОповещениеПриОтключении, Форма) Экспорт
	
	Если ОповещениеПриОтключении = Неопределено Тогда
		ОповещениеПриОтключении = ОписаниеОповещенияИС("ОтключитьОборудованиеЗавершениеПоУмолчанию", МенеджерОборудованияКлиент);
	КонецЕсли;
	
	НачатьОтключениеОборудованиеПоТипу(ОповещениеПриОтключении, Форма.УникальныйИдентификатор, ПреобразоватьСписокСтрокойВМассив(Форма.ПоддерживаемыеТипыПодключаемогоОборудования));
	
КонецПроцедуры

Процедура ОтключитьОборудованиеЗавершениеПоУмолчанию(РезультатВыполнения, Параметры) Экспорт
	
	Если НЕ РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр("ru='При отключении оборудования произошла ошибка: ""%ОписаниеОшибки%"".'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%" , РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ВыполнениеКоманды

// Заполнить параметры операции подписи чека.
// 
Процедура ЗаполнитьПараметрыОперацииПодписиЧека(ПараметрыОперации, НомерСмены = Неопределено, 
	НомерЧека = Неопределено, ЗаводскойНомерФН = Неопределено) Экспорт; 
	
	ПараметрыОперации.Вставить("ИНН"       , Неопределено);
	ПараметрыОперации.Вставить("КПП"       , Неопределено);
	ПараметрыОперации.Вставить("НомерСмены", НомерСмены);
	ПараметрыОперации.Вставить("НомерЧека" , НомерЧека);
	ПараметрыОперации.Вставить("НомерКассы"      , Неопределено);
	ПараметрыОперации.Вставить("ЗаводскойНомерФН", ЗаводскойНомерФН);
	ПараметрыОперации.Вставить("НаименованиеОрганизации", Неопределено);
	
КонецПроцедуры

// Начать выполнение команды ответственному обработчику драйвера
//
Процедура НачатьВыполнениеКоманды(ОповещениеПриЗавершении, Идентификатор, Команда, ВходныеПараметры) Экспорт
	
	// Поиск подключенного устройства.
	ПодключенноеУстройство = ПолучитьПодключенноеУстройство(глПодключаемоеОборудование.ПараметрыПодключенияПО, Идентификатор);
	
	Если ПодключенноеУстройство <> Неопределено Тогда
		
		ОбработчикДрайвераМодуль = ПодключенноеУстройство.ОбработчикДрайвераМодуль;
		ОбъектДрайвера = ПодключенноеУстройство.ОбъектДрайвера;
		Если ОбработчикДрайвераМодуль = Неопределено Или ОбъектДрайвера = Неопределено Тогда
			// Сообщить об ошибке, что не удалось загрузить драйвер.
			ТекстОшибки = НСтр("ru='Не удалось подключить обработчик драйвера или загрузить драйвер.'");
			РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании();
			РезультатВыполнения.Результат = Ложь;
			РезультатВыполнения.ОписаниеОшибки = ТекстОшибки; 
			ВыполнитьОбработкуОповещенияИС(ОповещениеПриЗавершении, РезультатВыполнения);
		Иначе
			Если ОбработчикДрайвераМодуль.ПоддержкаАсинхронногоРежима() Тогда
				ОбработчикДрайвераМодуль.НачатьВыполнениеКоманды(ОповещениеПриЗавершении, Команда, ВходныеПараметры,
					 ОбъектДрайвера, ПодключенноеУстройство.Параметры, ПодключенноеУстройство.ПараметрыПодключения);
			Иначе
				ТекстОшибки = "";
				ВыходныеПараметры = Неопределено;
				Результат = ОбработчикДрайвераМодуль.ВыполнитьКоманду(Команда, ВходныеПараметры,
					ВыходныеПараметры,  ОбъектДрайвера, ПодключенноеУстройство.Параметры, ПодключенноеУстройство.ПараметрыПодключения);
				Если Не Результат Тогда
					Если ВыходныеПараметры.Количество() >= 2 Тогда
						ТекстОшибки = ВыходныеПараметры[1];
					КонецЕсли;
					ВыходныеПараметры.Добавить(НСтр("ru='Установлен'"));
				КонецЕсли;
				РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Результат, ТекстОшибки, Идентификатор);
				РезультатВыполнения.ВыходныеПараметры = ВыходныеПараметры;
				РезультатВыполнения.Вставить("ОбработчикДрайвераМодуль", ОбработчикДрайвераМодуль);
				РезультатВыполнения.Вставить("ОбъектДрайвера"          , ОбъектДрайвера);
				РезультатВыполнения.Вставить("ПодключенноеУстройство"  , ПодключенноеУстройство);
				ВыполнитьОбработкуОповещенияИС(ОповещениеПриЗавершении, РезультатВыполнения);
			КонецЕсли;
		КонецЕсли;
	Иначе
		// Сообщить об ошибке, что устройство не подключено.
		ТекстОшибки = НСтр("ru='Устройство не подключено. Перед выполнением операции устройство должно быть подключено.'");
		РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании();
		РезультатВыполнения.Результат = Ложь;
		РезультатВыполнения.ОписаниеОшибки = ТекстОшибки; 
		ВыполнитьОбработкуОповещенияИС(ОповещениеПриЗавершении, РезультатВыполнения);
	КонецЕсли;
	
КонецПроцедуры

// Адаптация для работы с использованием ТорговоеОборудование
Процедура НачатьВыполнениеКомандыТО(ОповещениеПриЗавершении, Идентификатор, Команда, ВходныеПараметры) Экспорт
	
	ОбработкаОбслуживания = Неопределено;
	ОбъектДрайвера = Неопределено;
	
	Результат = ПолучитьСерверТО().ПолучитьОбъектДрайвера(Идентификатор, ОбработкаОбслуживания, ОбъектДрайвера);
	
	Если Результат = ПредопределенноеЗначение("Перечисление.ТООшибкиОбщие.ПустаяСсылка") Тогда
		
		Результат = ОбработкаОбслуживания.ПолучитьПараметрыККТ(ОбъектДрайвера);
		Если Результат = ПредопределенноеЗначение("Перечисление.ТООшибкиОбщие.ПустаяСсылка") Тогда
			ПараметрыККТ = ОбъектДрайвера.ВыходныеПараметры;
			
			ТекстОшибки = "";
			ВыходныеПараметры = Неопределено;
			
			ПараметрыПодключения = Новый Структура;
			ПараметрыПодключения.Вставить("ПараметрыРегистрации", ПараметрыККТ);
			ПараметрыПодключения.Вставить("ТипОборудования", "ККТ");
			
			Если Команда = "RequestKM" Тогда
				Результат = ОбработкаОбслуживания.ЗапросКМ(ОбъектДрайвера, ПараметрыККТ, ВходныеПараметры, ВыходныеПараметры, Идентификатор);
			ИначеЕсли Команда = "GetProcessingKMResult" Тогда
				Результат = ОбработкаОбслуживания.ПолучитьРезультатыЗапросаКМ(ОбъектДрайвера, ПараметрыККТ, ВходныеПараметры, ВыходныеПараметры, Идентификатор);
			ИначеЕсли Команда = "ConfirmKM" Тогда
				Результат = ОбработкаОбслуживания.ПодтвердитьКМ(ОбъектДрайвера, ПараметрыККТ, ВходныеПараметры, ВыходныеПараметры, Идентификатор);
			ИначеЕсли Команда = "CloseSessionRegistrationKM" Тогда
				Результат = ОбработкаОбслуживания.ЗакрытьСессиюРегистрацииКМ(ОбъектДрайвера, ПараметрыККТ, ВходныеПараметры, ВыходныеПараметры, Идентификатор);
			КонецЕсли;
	
			Если Результат Тогда
				РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Результат, ТекстОшибки, Идентификатор);
				РезультатВыполнения.ВыходныеПараметры = ВыходныеПараметры;
				РезультатВыполнения.Вставить("ОбъектДрайвера"          , ОбъектДрайвера);
				ВыполнитьОбработкуОповещенияИС(ОповещениеПриЗавершении, РезультатВыполнения);
			Иначе
				//ТекстОшибки = ОбъектДрайвера.ОписаниеОшибки;
				РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании();
				РезультатВыполнения.Результат = Ложь;
				Если ВыходныеПараметры.Количество() >= 2 Тогда
					ТекстОшибки = ВыходныеПараметры[1];
				КонецЕсли;
				РезультатВыполнения.ОписаниеОшибки = ТекстОшибки;
				ВыходныеПараметры.Добавить(НСтр("ru='Установлен'"));
				ВыполнитьОбработкуОповещенияИС(ОповещениеПриЗавершении, РезультатВыполнения);
			КонецЕсли;
		Иначе
			// Сообщить об ошибке, что не удалось загрузить драйвер.
			ТекстОшибки = НСтр("ru='Не удалось подключить обработчик драйвера или загрузить драйвер.'");
			РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании();
			РезультатВыполнения.Результат = Ложь;
			РезультатВыполнения.ОписаниеОшибки = ТекстОшибки; 
			ВыполнитьОбработкуОповещенияИС(ОповещениеПриЗавершении, РезультатВыполнения);
		КонецЕсли;
	Иначе
		// Сообщить об ошибке, что устройство не подключено.
		ТекстОшибки = НСтр("ru='Устройство не подключено. Перед выполнением операции устройство должно быть подключено.'");
		РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании();
		РезультатВыполнения.Результат = Ложь;
		РезультатВыполнения.ОписаниеОшибки = ТекстОшибки; 
		ВыполнитьОбработкуОповещенияИС(ОповещениеПриЗавершении, РезультатВыполнения);
	КонецЕсли;
		
КонецПроцедуры

Процедура НачатьВыполнениеКоманды_ПодключениеЗавершение(РезультатПодключения, Параметры) Экспорт
	
	Если РезультатПодключения.Результат Тогда  
		ВыполняемаяКоманда = Параметры.ВыполняемаяКоманда;
		Если (ВыполняемаяКоманда = "CheckFiscalization") И (Параметры.ОповещениеПослеОткрытииЧека <> Неопределено) Тогда
			ОписаниеОповещения = ОписаниеОповещенияИС("НачатьВыполнениеКоманды_ВыполнитьКомандуЗавершение", МенеджерОборудованияКлиент, Параметры);
			НачатьВыполнениеКомандыПечатиЧека(ОписаниеОповещения, Параметры.ИдентификаторУстройства, ВыполняемаяКоманда, Параметры, Параметры.ВходныеПараметры);
		Иначе
			ОписаниеОповещения = ОписаниеОповещенияИС("НачатьВыполнениеКоманды_ВыполнитьКомандуЗавершение", МенеджерОборудованияКлиент, Параметры);
			НачатьВыполнениеКоманды(ОписаниеОповещения, Параметры.ИдентификаторУстройства, ВыполняемаяКоманда, Параметры.ВходныеПараметры);
		КонецЕсли;
	Иначе
		Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
			ТекстСообщения = НСтр("ru='При подключении оборудования произошла ошибка: %ДополнительноеОписание%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", РезультатПодключения.ОписаниеОшибки);
			РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ТекстСообщения);
			РезультатОперации.ИдентификаторУстройства = Параметры.ИдентификаторУстройства;
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Интерфейсные
 
// Выполнить настройку оборудования.
// 
Процедура ВыполнитьНастройкуОборудования(Идентификатор, ОповещениеПриЗавершении = Неопределено) Экспорт

	ДанныеУстройства = МенеджерОборудованияКлиентПовтИсп.ПолучитьДанныеУстройства(Идентификатор);
	ПараметрыФормы = Новый Структура("ПараметрыОборудования", ДанныеУстройства.Параметры);
	ПараметрыФормы.Вставить("Идентификатор", Идентификатор);
	ПараметрыФормы.Вставить("ДрайверОборудования", ДанныеУстройства.ДрайверОборудования);
	
	ФормаНастройки = "ФормаНастройкиУниверсальныйДрайвер";
	
	ОбработчикДрайвера = МенеджерОборудованияКлиентПовтИсп.ПолучитьОбработчикДрайвера(ДанныеУстройства.ОбработчикДрайвера, Не ДанныеУстройства.ВСоставеКонфигурации, ДанныеУстройства.ТипОборудованияИмя);
		
	Если Не ОбработчикДрайвера = ПодключаемоеОборудованиеУниверсальныйДрайверКлиент 
		И Не ОбработчикДрайвера = ПодключаемоеОборудованиеУниверсальныйДрайверАсинхронноКлиент Тогда
			ФормаНастройки = СтрЗаменить(ДанныеУстройства.ОбработчикДрайвераИмя, "Обработчик", "ФормаНастройки");
	КонецЕсли;
		
	Если НЕ ПустаяСтрока(ФормаНастройки) Тогда
		ПараметрыКоманды = Новый Структура("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
		Обработчик = Новый ОписаниеОповещения("ВыполнитьНастройкуОборудования_Завершение", МенеджерОборудованияКлиент, ПараметрыКоманды);
		ОткрытьФормуИС("ОбщаяФорма." + ФормаНастройки, ПараметрыФормы,,,  ,, Обработчик, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru='Произошла ошибка инициализации формы настройки драйвера.'")); 
	КонецЕсли;
	
КонецПроцедуры

// Завершение настройки оборудования.
// 
// Параметры:
// 	Результат - Структура - где:
//  * Идентификатор - СправочникСсылка.ПодключаемоеОборудование - экземпляр подключаемого оборудования.
// 	* ПараметрыОборудования - Структура - структура параметров.
// 	Параметры - Структура - структура параметров выполнения операции.
//
Процедура ВыполнитьНастройкуОборудования_Завершение(Результат, Параметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		
		РезультатЗавершения = Ложь;
		Если Результат.Свойство("Идентификатор") И Результат.Свойство("ПараметрыОборудования") Тогда
			РезультатЗавершения = МенеджерОборудованияВызовСервера.СохранитьПараметрыУстройства(Результат.Идентификатор, Результат.ПараметрыОборудования);
		КонецЕсли;
		
		Если РезультатЗавершения Тогда 
			ОбновитьПовторноИспользуемыеЗначения();
		Иначе
			СообщениеОбОшибке = НСтр("ru='Не удалось сохранить параметры устройства.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(СообщениеОбОшибке);
		КонецЕсли;
		
		Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатЗавершения);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Открытие формы подключаемого Оборудования.
//
Процедура ОткрытьПодключаемоеОборудование(ПараметрКоманды, ПараметрыВыполненияКоманды) Экспорт
	
	ОбновитьРабочееМестоКлиента();
	
	ПараметрыФормы = Новый Структура();
	ОткрытьФормуИС("Справочник.ПодключаемоеОборудование.ФормаСписка", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно);
	
КонецПроцедуры

// Открытие формы драйверов оборудования.
//
Процедура ОткрытьДрайверыОборудования(ПараметрКоманды, ПараметрыВыполненияКоманды) Экспорт
	
	ОбновитьРабочееМестоКлиента();
	
	ПараметрыФормы = Новый Структура();
	ОткрытьФормуИС("Справочник.ДрайверыОборудования.ФормаСписка", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно);
	
КонецПроцедуры

// Открытие формы списка платежных операций
//
// Параметры:
//  ПараметрКоманды - Произвольный - источник, в котором реализована команда
//  ПараметрыВыполненияКоманды - ПараметрыВыполненияКоманды
//
Процедура ОткрытьПлатежныеОперации(ПараметрКоманды, ПараметрыВыполненияКоманды) Экспорт
	
	ПараметрыФормы = Новый Структура();
	ОткрытьФорму("РегистрСведений.ПлатежныеОперации.ФормаСписка", 
		ПараметрыФормы, 
		ПараметрыВыполненияКоманды.Источник, 
		ПараметрыВыполненияКоманды.Уникальность, 
		ПараметрыВыполненияКоманды.Окно);
	
КонецПроцедуры

// Открытие формы списка фискальных операций
//
// Параметры:
//  ПараметрКоманды - Произвольный - источник, в котором реализована команда
//  ПараметрыВыполненияКоманды - ПараметрыВыполненияКоманды
//
Процедура ОткрытьФискальныеОперации(ПараметрКоманды, ПараметрыВыполненияКоманды) Экспорт
	
	ПараметрыФормы = Новый Структура();
	ОткрытьФорму("РегистрСведений.ФискальныеОперации.ФормаСписка", 
		ПараметрыФормы, 
		ПараметрыВыполненияКоманды.Источник, 
		ПараметрыВыполненияКоманды.Уникальность, 
		ПараметрыВыполненияКоманды.Окно);
	
КонецПроцедуры

// Открытие формы списка операций очереди чеков
//
// Параметры:
//  ПараметрКоманды - Произвольный - источник, в котором реализована команда
//  ПараметрыВыполненияКоманды - ПараметрыВыполненияКоманды
//
Процедура ОткрытьОчередьЧеков(ПараметрКоманды, ПараметрыВыполненияКоманды) Экспорт
	
	ПараметрыФормы = Новый Структура();
	ОткрытьФорму("РегистрСведений.ОчередьЧековККТ.ФормаСписка", 
		ПараметрыФормы, 
		ПараметрыВыполненияКоманды.Источник, 
		ПараметрыВыполненияКоманды.Уникальность, 
		ПараметрыВыполненияКоманды.Окно);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Функция ПреобразоватьДанныеСоСканераВМассив(Параметр) Экспорт
	
	МенеджерОборудованияКлиентПереопределяемый.ОбработатьСобытие();
	
	Данные = Новый Массив;
	Данные.Добавить(ПреобразоватьДанныеСоСканераВСтруктуру(Параметр));
	
	Возврат Данные;
	
КонецФункции

Функция ПреобразоватьДанныеСоСканераВСтруктуру(Параметр) Экспорт
	
	МенеджерОборудованияКлиентПереопределяемый.ОбработатьСобытие();
	
	Если Параметр[1] = Неопределено Тогда
		Данные = Новый Структура("Штрихкод, Количество", Параметр[0], 1);    // Достаем штрихкод из основных данных
	Иначе
		Данные = Новый Структура("Штрихкод, Количество", Параметр[1][1], 1); // Достаем штрихкод из дополнительных данных
	КонецЕсли;
	
	Возврат Данные;
	
КонецФункции

#КонецОбласти

#Область РаспределеннаяФискализации

Процедура ОбработкаОповещенияПодключенияСообщенийФискализации(ДополнительныеПараметры) Экспорт
	
	Оповестить("ПодключенияСообщенийФискализации");
	
КонецПроцедуры

Процедура ОбработкаОповещенияСообщенийФискализации(Сообщение, ДополнительныеПараметры) ЭКспорт
	
	Если СтрНачинаетсяС(Сообщение.Текст, НСтр("ru='Фискализация чека'")) Тогда
		Оповестить("ФискализацияЧека", Сообщение.Текст);
		ФискализацияЧековВОчереди();
	КонецЕсли;
	
КонецПроцедуры

Процедура ФискализацияЧековВОчереди_ПослеОткрытияЧека(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	
	МенеджерОборудованияКлиентПереопределяемый.ФискализацияЧековВОчередиПослеОткрытияЧека(ПараметрыВыполнения, ДополнительныеПараметры);
	
	Если ПараметрыВыполнения.Свойство("ДанныеОтправленыВЕГАИС") Тогда
	
		Возврат;
	
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ПараметрыВыполнения.ОповещениеПродолжения, ПараметрыВыполнения);
	
КонецПроцедуры

Процедура ФискализацияЧековВОчереди_ПослеОшибкиПечатиЧека(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	
	МенеджерОборудованияКлиентПереопределяемый.ФискализацияЧековВОчередиПослеОшибкиПечатиЧека(ПараметрыВыполнения, ДополнительныеПараметры);
	
КонецПроцедуры

// Подписка на событие
// 
// Параметры:
// 	РезультатВыполнения - Структура - 
// 	ОбщиеПараметры - Структура - 
Процедура ФискализацияЧековВОчереди_Завершение(РезультатВыполнения, ОбщиеПараметры) Экспорт
	
	Если РезультатВыполнения.Результат Тогда
	    СтатусЧека = ПредопределенноеЗначение("Перечисление.СтатусЧекаККТВОчереди.Фискализирован");
		ТекстСообщения = "";
	Иначе
	    СтатусЧека = ПредопределенноеЗначение("Перечисление.СтатусЧекаККТВОчереди.Ошибка");
		ТекстСообщения = РезультатВыполнения.ОписаниеОшибки;
	КонецЕсли;
	
	Если РезультатВыполнения.Свойство("ДополнительныеПараметры") Тогда
		ОбщиеПараметры.Вставить("ДополнительныеПараметры", РезультатВыполнения.ДополнительныеПараметры);
	КонецЕсли;
	
	МенеджерОборудованияВызовСервера.ЗаписатьСтатусЧекаВОчереди(ОбщиеПараметры, СтатусЧека, Неопределено, ТекстСообщения);
	
	Оповестить("ФискализированЧек");
	
	ФискализацияЧековВОчереди_Начало();
	
КонецПроцедуры

// Начать фискализацию чеков в очереди
// 
// Параметры:
//  Параметры - см. ПараметрыФискализацииОчередиЧеков
Процедура ФискализацияЧековВОчереди_Начало(Параметры = Неопределено) Экспорт
	
	ДанныеОчереди =  глПодключаемоеОборудование.ДанныеОчереди;
		
	Если ДанныеОчереди.НомерЧека < ДанныеОчереди.ЧекиВОчереди.Количество() Тогда
		
		ЧекВОчереди = ДанныеОчереди.ЧекиВОчереди[ДанныеОчереди.НомерЧека];
		ДанныеОчереди.НомерЧека = ДанныеОчереди.НомерЧека + 1;
		
		Если ЧекВОчереди.ДанныеЧека <> Неопределено Тогда
			ОбщиеПараметры = ЧекВОчереди.ДанныеЧека;
			СтатусДокументаИзменен = МенеджерОборудованияВызовСервераПереопределяемый.ПроверитьСтатусДокументаОснования(ОбщиеПараметры);
			Если СтатусДокументаИзменен Тогда
				ЧекВОчереди.СтатусЧека = ПредопределенноеЗначение("Перечисление.СтатусЧекаККТВОчереди.Фискализирован");
			КонецЕсли;
		Иначе
			ФискализацияЧековВОчереди_Начало();
			Возврат;
		КонецЕсли;
		
		Если ЧекВОчереди.СтатусЧека = ПредопределенноеЗначение("Перечисление.СтатусЧекаККТВОчереди.Новый") 
			Или ЧекВОчереди.СтатусЧека = ПредопределенноеЗначение("Перечисление.СтатусЧекаККТВОчереди.Ошибка") Тогда
			
			ОбщиеПараметры.Кассир = ДанныеОчереди.Кассир;
			ОбщиеПараметры.КассирИНН = ДанныеОчереди.КассирИНН;
			
			ИдентификаторУстройстваККТ = ПолучитьДоступноеККТДляФискализации(ОбщиеПараметры, ДанныеОчереди.СписокУстройств);
			
			Если НЕ ЗначениеЗаполнено(ИдентификаторУстройстваККТ) Тогда
				ТекстСообщения = НСтр("ru='Нет доступных ККТ для фискализации чека.'");
				СтатусЧека = ПредопределенноеЗначение("Перечисление.СтатусЧекаККТВОчереди.Ошибка");
				МенеджерОборудованияВызовСервера.ЗаписатьСтатусЧекаВОчереди(ОбщиеПараметры, СтатусЧека, Неопределено, ТекстСообщения);
			Иначе
				СтатусЧека = ПредопределенноеЗначение("Перечисление.СтатусЧекаККТВОчереди.Фискализируется");
				МенеджерОборудованияВызовСервера.ЗаписатьСтатусЧекаВОчереди(ОбщиеПараметры, СтатусЧека, ИдентификаторУстройстваККТ);
				
				ПослеОткрытияЧека = Новый ОписаниеОповещения("ФискализацияЧековВОчереди_ПослеОткрытияЧека", МенеджерОборудованияКлиент, ОбщиеПараметры);
				ПослеОшибкиПечатиЧека = Новый ОписаниеОповещения("ФискализацияЧековВОчереди_ПослеОшибкиПечатиЧека", МенеджерОборудованияКлиент, ОбщиеПараметры);
				
				ОписаниеОповещения = Новый ОписаниеОповещения("ФискализацияЧековВОчереди_Завершение", МенеджерОборудованияКлиент, ОбщиеПараметры);
				УникальныйИдентификатор = Новый УникальныйИдентификатор;
				МенеджерОборудованияКлиент.НачатьФискализациюЧекаНаФискальномУстройстве(ОписаниеОповещения, УникальныйИдентификатор, ОбщиеПараметры, ИдентификаторУстройстваККТ, , 
					ПослеОткрытияЧека, ПослеОшибкиПечатиЧека);
				Возврат;
			КонецЕсли;
			
		Иначе
			ФискализацияЧековВОчереди_Начало(Параметры);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает структуру параметров для выполнения фискализации очереди чеков
// 
// Возвращаемое значение:
//  Структура:
//   * Форма - ФормаКлиентскогоПриложения
//   * КассаККМ - ОпределяемыйТип.КассаБПО
Функция ПараметрыФискализацииОчередиЧеков() Экспорт
	
	Параметры = Новый Структура();
	Параметры.Вставить("Форма", Неопределено);
	Параметры.Вставить("КассаККМ", Неопределено);
	Возврат Параметры;
	
КонецФункции

// Выполнить фискализацию чеков в очереди
//
// Параметры:
//  РазрешенаАвтоматическаяФискализация - Булево
//  Параметры - см. ПараметрыФискализацииОчередиЧеков.
Процедура ФискализацияЧековВОчереди(РазрешенаАвтоматическаяФискализация = Истина, Параметры = Неопределено) Экспорт
	
	ПоддерживаемыеТипыВО = Новый Массив();
	ПоддерживаемыеТипыВО.Добавить("ПринтерЧеков");
	ПоддерживаемыеТипыВО.Добавить("ККТ");
	СписокУстройств = МенеджерОборудованияВызовСервера.ОборудованиеПоПараметрам(ПоддерживаемыеТипыВО,,, РазрешенаАвтоматическаяФискализация);
	Если СписокУстройств.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	КассаККМ = Неопределено;
	
	МодульОборудованиеЧекопечатающиеУстройстваВызовСервера = ОбщегоНазначенияКлиент.ОбщийМодуль("ОборудованиеЧекопечатающиеУстройстваВызовСервера");
	ЧекиВОчереди = МодульОборудованиеЧекопечатающиеУстройстваВызовСервера.ЧекиВОчередиНаФискализацию(КассаККМ);
	Если ЧекиВОчереди.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Кассир = "";
	ВыполненаСтандартнаяОбработка = Истина; 
	МенеджерОборудованияКлиентСерверПереопределяемый.ОбработкаЗаполненияИмяКассира(Кассир, ВыполненаСтандартнаяОбработка); 
	Кассир = ?(Не ВыполненаСтандартнаяОбработка, Кассир, НСтр("ru='Администратор'")); 
	
	КассирИНН = "";
	ВыполненаСтандартнаяОбработка = Истина;
	МенеджерОборудованияКлиентСерверПереопределяемый.ОбработкаЗаполненияИННКассира(КассирИНН, ВыполненаСтандартнаяОбработка); 
	КассирИНН = ?(Не ВыполненаСтандартнаяОбработка, КассирИНН, ""); 
	
	ДанныеОчереди = Новый Структура();
	ДанныеОчереди.Вставить("Кассир"         , Кассир);
	ДанныеОчереди.Вставить("КассирИНН"      , КассирИНН);
	ДанныеОчереди.Вставить("ЧекиВОчереди"   , ЧекиВОчереди);
	ДанныеОчереди.Вставить("СписокУстройств", СписокУстройств);
	ДанныеОчереди.Вставить("НомерЧека"      , 0);
	глПодключаемоеОборудование.ДанныеОчереди = ДанныеОчереди;
	
	ФискализацияЧековВОчереди_Начало();
	
КонецПроцедуры

#КонецОбласти

#Область УпрощениеНастройкиПараметров

// Завершает асинхронное заполнение ключевых параметров оборудования по идентификатору
//
// Параметры:
//  Результат - Структура
//  Контекст - Структура
Процедура НачатьПолучениеКлючевыхПараметровЗавершение(Результат, Контекст) Экспорт
	
	ОповещениеЗавершения = Контекст.ОповещениеЗавершения;

	Если Результат.Результат Тогда
	
		Идентификатор      = Контекст.Идентификатор;
		КлючевыеПараметры  = Контекст.КлючевыеПараметры;
		ОписаниеИнтерфейса = Результат.ОписаниеДрайвера.ПараметрыДрайвера;
		
		ПрочитатьАтрибутыКлючевыхПараметров(ОписаниеИнтерфейса, КлючевыеПараметры);
	
		ПодключаемоеОборудование = ПодключаемоеОборудование();
		Если Не ПодключаемоеОборудование.Свойство("СобственныеПараметрыОборудования") Тогда
			ПодключаемоеОборудование.Вставить("СобственныеПараметрыОборудования", Новый Соответствие());
		КонецЕсли;
		ПодключаемоеОборудование.СобственныеПараметрыОборудования.Вставить(Идентификатор, КлючевыеПараметры);
		
		// добавить общие параметры
		Если Не КлючевыеПараметры.Свойство("LogEnabled") Тогда
			ДобавитьПараметрЛогДрайвераВключен(КлючевыеПараметры, Результат.ОписаниеДрайвера.ЛогДрайвераВключен);
		КонецЕсли;
		Если Не КлючевыеПараметры.Свойство("LogPath") Тогда
			ДобавитьПараметрЛогДрайвераПутьКФайлу(КлючевыеПараметры, Результат.ОписаниеДрайвера.ЛогДрайвераПутьКФайлу);
		КонецЕсли;
		
		
		ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Истина);
	Иначе
		ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Ложь);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает атрибуты параметра оборудования по стандартному имени параметра, имя проверяется по словарю возможных вариантов
//
// Параметры:
//  Идентификатор - СправочникСсылка.ПодключаемоеОборудование - идентификатор оборудования
//  ИмяПараметра - Строка - стандартное имя параметра оборудования
//
// Возвращаемое значение:
//  Структура - см. НовыйАтрибутыПараметра
//
Функция АтрибутыПараметра(Идентификатор, ИмяПараметра) Экспорт

	ПодключаемоеОборудование = ПодключаемоеОборудование();
	Если Не ПодключаемоеОборудование.Свойство("СобственныеПараметрыОборудования") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	КлючевыеПараметры = ПодключаемоеОборудование.СобственныеПараметрыОборудования.Получить(Идентификатор);
	Если КлючевыеПараметры = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	АтрибутыПараметра = Неопределено;
	Если Не КлючевыеПараметры.Свойство(ИмяПараметра, АтрибутыПараметра) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат АтрибутыПараметра;

КонецФункции

// Возвращает Истина если параметр с именем ИмяПараметра существует, имя проверяется по словарю возможных вариантов
//
// Параметры:
//  Идентификатор - СправочникСсылка.ПодключаемоеОборудование - идентификатор оборудования
//  ИмяПараметра - Строка - стандартное имя параметра оборудования
//
// Возвращаемое значение:
//  Булево - 
Функция ПараметрСуществует(Идентификатор, ИмяПараметра) Экспорт

	АтрибутыПараметра = АтрибутыПараметра(Идентификатор, ИмяПараметра);
	Возврат АтрибутыПараметра <> Неопределено;

КонецФункции

// Возвращает тип параметра оборудования по стандартному имени параметра, имя проверяется по словарю возможных вариантов
//
// Параметры:
//  Идентификатор - СправочникСсылка.ПодключаемоеОборудование - идентификатор оборудования
//  ИмяПараметра - Строка - стандартное имя параметра оборудования
//
// Возвращаемое значение:
//   Тип - Тип - Тип значения параметра (Число, Строка, Булево)
Функция ТипПараметра(Идентификатор, ИмяПараметра) Экспорт
	
	АтрибутыПараметра = АтрибутыПараметра(Идентификатор, ИмяПараметра);
	Если АтрибутыПараметра = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат АтрибутыПараметра.Тип;

КонецФункции

#КонецОбласти

#Область УстаревшиеПроцедурыИФункции

// Устарела: больше не используется.
// Выводит текст в панель состояния.
//
Процедура СостояниеПроцесса(ТекстСообщения, Прогресс = Неопределено, Пояснение = Неопределено) Экспорт
	
#Если НЕ МобильныйКлиент Тогда
	Состояние(ТекстСообщения, Прогресс);
#КонецЕсли 
	
КонецПроцедуры

#Область ВыбратьУстройство

// Устарела: следует использовать ВыбратьУстройство.
// Процедура выбора устройства из доступных, привязанных к текущему рабочему месту.
//
Процедура ПредложитьВыбратьУстройство(ОповещениеВыбора, ТипОборудования, ТекстЗаголовкаВыбора, 
	СообщениеНеПодключен = "", СообщениеНеВыбран = "", БезСообщений = Ложь, ТекстСообщения = "") Экспорт
	
	Если Не ОбновитьРабочееМестоКлиента() Тогда
		ТекстСообщения = НСтр("ru='Предварительно необходимо выбрать рабочее место подключаемого оборудования текущего сеанса.'");
		Если Не БезСообщений Тогда
		      ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);;
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	СписокДоступныхУстройств = МенеджерОборудованияВызовСервера.ОборудованиеПоПараметрам(ТипОборудования);
	
	Если СписокДоступныхУстройств.Количество() = 0 Тогда
		Если Не ПустаяСтрока(СообщениеНеПодключен) Тогда
			Если БезСообщений Тогда
				ТекстСообщения = СообщениеНеПодключен;
			Иначе
				ОбщегоНазначенияКлиент.СообщитьПользователю(СообщениеНеПодключен);
			КонецЕсли;
		КонецЕсли;
		ВыполнитьОбработкуОповещенияИС(ОповещениеВыбора, Неопределено);
	Иначе
		СписокУстройств = Новый СписокЗначений();
		Для Каждого Устройства Из СписокДоступныхУстройств Цикл
			СписокУстройств.Добавить(Устройства.Ссылка, Устройства.Наименование);
		КонецЦикла;
		Если СписокУстройств.Количество() = 1 Тогда
			Идентификатор = СписокУстройств[0].Значение;
			ВыполнитьОбработкуОповещенияИС(ОповещениеВыбора, Идентификатор); 
		Иначе
			Контекст = Новый Структура;
			Контекст.Вставить("СледующееОповещение", ОповещениеВыбора);
			Контекст.Вставить("СообщениеНеВыбран"  , ?(ПустаяСтрока(СообщениеНеВыбран), СообщениеНеПодключен, СообщениеНеВыбран));
			Контекст.Вставить("БезСообщений"       , БезСообщений);
			ОписаниеОповещения = ОписаниеОповещенияИС("ПредложитьВыбратьУстройствоЗавершение", МенеджерОборудованияКлиент, Контекст);
			
			Если СписокУстройств.ВыбратьЭлемент(ТекстЗаголовкаВыбора) <> Неопределено Тогда
				ВыполнитьОбработкуОповещенияИС(ОписаниеОповещения);
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Устарела: больше не используется.
// Завершение выбора устройства
// 
// Параметры:
//  Результат - Структура
//  Параметры - Структура
// 
Процедура ПредложитьВыбратьУстройствоЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено И НЕ Параметры.БезСообщений И Не ПустаяСтрока(Параметры.СообщениеНеВыбран) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Параметры.СообщениеНеВыбран);
	КонецЕсли;
	
	Если Параметры.СледующееОповещение <> Неопределено Тогда
		Идентификатор = ?(Результат = Неопределено, Неопределено, Результат.Значение);
		ВыполнитьОбработкуОповещенияИС(Параметры.СледующееОповещение, Идентификатор);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ВыборКодаВидаНоменклатурнойКлассификации

// Устарела: больше не используется.
// Процедура открывает форму выбора кода вида номенклатурной классификации
//
// Параметры:
//  ОповещениеПриВыборе - ОписаниеОповещения - событие описания оповещения.
//
Процедура НачатьВыборКодаВидаНоменклатурнойКлассификации(ОповещениеПриВыборе) Экспорт
	
	ПараметрыКоманды = Новый Структура("ОповещениеПриВыборе", ОповещениеПриВыборе);
	Обработчик = Новый ОписаниеОповещения("НачатьВыборКодаВидаНоменклатурнойКлассификации_Завершение", МенеджерОборудованияКлиент, ПараметрыКоманды);
	ОткрытьФорму("ОбщаяФорма.КодВидаНоменклатурнойКлассификации", ,,,  ,, Обработчик, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

// Устарела: больше не используется.
// Завершение выбора 
// 
// Параметры:
//  Результат - Структура
//  Параметры - Структура
// 
Процедура НачатьВыборКодаВидаНоменклатурнойКлассификации_Завершение(Результат, Параметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
			Если Параметры.ОповещениеПриВыборе <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(Параметры.ОповещениеПриВыборе, Результат);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ТСД

// Устарела: следует использовать ОборудованиеТерминалыСбораДанныхКлиент.НачатьЗагрузкуДанныеИзТСД.
// Начать загрузку данных из терминала сбора данных.
//
Процедура НачатьЗагрузкуДанныеИзТСД(ОповещениеПриЗавершении, УникальныйИдентификатор, СворачиватьДанные = Ложь) Экспорт
	
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеПриЗавершении" , ОповещениеПриЗавершении);
	Контекст.Вставить("УникальныйИдентификатор" , УникальныйИдентификатор);
	Контекст.Вставить("СворачиватьДанные"       , СворачиватьДанные);
	
	Оповещение = ОписаниеОповещенияИС("НачатьЗагрузкуДанныеИзТСД_ВыбратьУстройствоЗавершение", МенеджерОборудованияКлиент, Контекст);
	ПредложитьВыбратьУстройство(Оповещение, "ТерминалСбораДанных", НСтр("ru='Выберите терминал сбора данных'"), НСтр("ru='Терминал сбора данных не подключен.'"), ,Истина);
		
КонецПроцедуры

Процедура НачатьЗагрузкуДанныеИзТСД_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Параметры) Экспорт
	
	Если ИдентификаторУстройства = Неопределено Тогда
		ОписаниеОшибки = НСтр("ru='Терминал сбора данных не подключен.'");
		Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
			РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки);
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Параметры.Вставить("ИдентификаторУстройства", ИдентификаторУстройства);
	Оповещение = ОписаниеОповещенияИС("НачатьЗагрузкуДанныеИзТСД_ПодключениеЗавершение", МенеджерОборудованияКлиент, Параметры);
	НачатьПодключениеОборудованиеПоИдентификатору(Оповещение, Параметры.УникальныйИдентификатор, ИдентификаторУстройства);
	
КонецПроцедуры

Процедура НачатьЗагрузкуДанныеИзТСД_ПодключениеЗавершение(РезультатПодключения, Параметры) Экспорт
	
	Если РезультатПодключения.Результат Тогда
		ВходныеПараметры  = Неопределено;
		ОписаниеОповещения = ОписаниеОповещенияИС("НачатьЗагрузкуДанныеИзТСД_ВыполнитьКомандуЗавершение", МенеджерОборудованияКлиент, Параметры);
		НачатьВыполнениеКоманды(ОписаниеОповещения, Параметры.ИдентификаторУстройства, "DownloadDocument", ВходныеПараметры);
	Иначе
		Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
			ОписаниеОшибки = РезультатПодключения.ОписаниеОшибки;
			РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки, Параметры.ИдентификаторУстройства);
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьЗагрузкуДанныеИзТСД_ВыполнитьКомандуЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если РезультатВыполнения.Результат Тогда
		
		ВыходныеПараметры = РезультатВыполнения.ВыходныеПараметры;
		ТаблицаШтрихкодов = ВыходныеПараметры[0];
		АлкогольнаяПродукция = (ВыходныеПараметры.Количество() > 1) И ВыходныеПараметры[1];
		МассивЗагрузкиИзТСД = Новый Массив;       
		
		Если Параметры.СворачиватьДанные И НЕ АлкогольнаяПродукция Тогда
			
			СоответствиеДанных = Новый Соответствие;
			Для Индекс = 0 По ТаблицаШтрихкодов.Количество() - 1 Цикл
				Штрихкод   = ТаблицаШтрихкодов[Индекс].Штрихкод;
				Количество = Число(?(ПустаяСтрока(ТаблицаШтрихкодов[Индекс].Количество), 0, ТаблицаШтрихкодов[Индекс].Количество));
				ЭлементСоответствия = СоответствиеДанных.Получить(Штрихкод);
				Если ЭлементСоответствия = Неопределено Тогда
					СтруктураШтрихкода = Новый Структура("Штрихкод, Количество", Штрихкод, Количество);
					ИндексЗагрузки = МассивЗагрузкиИзТСД.Количество(); // Вычитать 1 не нужно, т.к. добавление идет после.
					МассивЗагрузкиИзТСД.Добавить(СтруктураШтрихкода);
					СоответствиеДанных.Вставить(Штрихкод, ИндексЗагрузки);
				Иначе
					СтруктураШтрихкода = МассивЗагрузкиИзТСД[ЭлементСоответствия];
					СтруктураШтрихкода.Количество = СтруктураШтрихкода.Количество + Количество;
				КонецЕсли;
			КонецЦикла;
		Иначе
			МассивЗагрузкиИзТСД = ТаблицаШтрихкодов; 
		КонецЕсли;
		
		Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
			РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(Истина, ,Параметры.ИдентификаторУстройства);
			РезультатОперации.Вставить("ТаблицаТоваров"      , МассивЗагрузкиИзТСД);
			РезультатОперации.Вставить("АлкогольнаяПродукция", АлкогольнаяПродукция);
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
		КонецЕсли;
		
	Иначе
		Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
			ОписаниеОшибки = НСтр("ru='При загрузке данных из терминала сбора данных произошла ошибка.
								  |%ОписаниеОшибки%'");
			ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%ОписаниеОшибки%", РезультатВыполнения.ВыходныеПараметры[1]);
			РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(РезультатВыполнения.Результат, ОписаниеОшибки, Параметры.ИдентификаторУстройства);  
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
		КонецЕсли;
	КонецЕсли;
	
	НачатьОтключениеОборудованиеПоИдентификатору(Неопределено, Параметры.УникальныйИдентификатор, Параметры.ИдентификаторУстройства);
	
КонецПроцедуры

// Устарела: следует использовать ОборудованиеТерминалыСбораДанныхКлиент.НачатьВыгрузкуДанныеВТСД.
// Начать выгрузку данных в терминал сбора данных.
//
Процедура НачатьВыгрузкуДанныеВТСД(ОповещениеПриЗавершении, УникальныйИдентификатор, ТаблицаВыгрузкиТоваров, ПолнаяВыгрузка = Истина) Экспорт
	
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеПриЗавершении" , ОповещениеПриЗавершении);
	Контекст.Вставить("УникальныйИдентификатор" , УникальныйИдентификатор);
	Контекст.Вставить("ТаблицаВыгрузкиТоваров"  , ТаблицаВыгрузкиТоваров);
	Контекст.Вставить("ПолнаяВыгрузка"          , ПолнаяВыгрузка);
	
	Оповещение = ОписаниеОповещенияИС("НачатьВыгрузкуДанныеВТСД_ВыбратьУстройствоЗавершение", МенеджерОборудованияКлиент, Контекст);
	ПредложитьВыбратьУстройство(Оповещение, "ТерминалСбораДанных", НСтр("ru='Выберите терминал сбора данных'"), НСтр("ru='Терминал сбора данных не подключен.'"), ,Истина);
	
КонецПроцедуры                          

Процедура НачатьВыгрузкуДанныеВТСД_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Параметры) Экспорт
	
	Если ИдентификаторУстройства = Неопределено Тогда
		ОписаниеОшибки = НСтр("ru='Терминал сбора данных не подключен.'");
		Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
			РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки);
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Параметры.Вставить("ИдентификаторУстройства", ИдентификаторУстройства);
	Оповещение = ОписаниеОповещенияИС("НачатьВыгрузкуДанныеВТСД_ПодключениеЗавершение", МенеджерОборудованияКлиент, Параметры);
	НачатьПодключениеОборудованиеПоИдентификатору(Оповещение, Параметры.УникальныйИдентификатор, ИдентификаторУстройства);
	
КонецПроцедуры

// Начинает выгрузку данных в ТСД.
// 
// Параметры:
// 	РезультатПодключения - Структура - структура результата подключения.
// 	Параметры - Структура - где:
// 	*ТаблицаВыгрузкиТоваров - ТаблицаЗначений - где:
// 	**Наименование - Строка - .
Процедура НачатьВыгрузкуДанныеВТСД_ПодключениеЗавершение(РезультатПодключения, Параметры) Экспорт
	
	Если РезультатПодключения.Результат Тогда
		
		ВходныеПараметры  = Новый Массив();
		ВходныеПараметры.Добавить("Items");
		ВходныеПараметры.Добавить(Параметры.ТаблицаВыгрузкиТоваров);
		ВходныеПараметры.Добавить(Параметры.ПолнаяВыгрузка);
		
		Оповещение = ОписаниеОповещенияИС("НачатьВыгрузкуДанныеВТСД_ВыполнитьКомандуЗавершение", МенеджерОборудованияКлиент, Параметры);
		НачатьВыполнениеКоманды(Оповещение, Параметры.ИдентификаторУстройства, "UploadDirectory", ВходныеПараметры);
		
	Иначе
		Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
			ОписаниеОшибки = РезультатПодключения.ОписаниеОшибки;
			РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки, Параметры.ИдентификаторУстройства);
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьВыгрузкуДанныеВТСД_ВыполнитьКомандуЗавершение(РезультатВыполнения, Параметры) Экспорт
		
	Если РезультатВыполнения.Результат Тогда
		ОписаниеОшибки = ""; 
	Иначе
		ОписаниеОшибки = НСтр("ru='При выгрузке данных в терминал сбора данных произошла ошибка: %ОписаниеОшибки%'");
		ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%ОписаниеОшибки%", РезультатВыполнения.ВыходныеПараметры[1]);
	КонецЕсли;
	
	Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
		РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(РезультатВыполнения.Результат, ОписаниеОшибки, Параметры.ИдентификаторУстройства); 
		ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
	КонецЕсли;
	
	НачатьОтключениеОборудованиеПоИдентификатору(Неопределено, Параметры.УникальныйИдентификатор, Параметры.ИдентификаторУстройства);
	
КонецПроцедуры

// Устарела: следует использовать ОборудованиеТерминалыСбораДанныхКлиент.НачатьОчисткуДанныеВТСД.
// Начать очистку данных в терминале сбора данных.
//
Процедура НачатьОчисткуДанныеВТСД(ОповещениеПриЗавершении, УникальныйИдентификатор, ИдентификаторУстройства = Неопределено) Экспорт
	
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеПриЗавершении" , ОповещениеПриЗавершении);
	Контекст.Вставить("УникальныйИдентификатор" , УникальныйИдентификатор);
	
	Если ИдентификаторУстройства = Неопределено Тогда
		Оповещение = ОписаниеОповещенияИС("НачатьОчисткуДанныеВТСД_ВыбратьУстройствоЗавершение", МенеджерОборудованияКлиент, Контекст);
		ПредложитьВыбратьУстройство(Оповещение, "ТерминалСбораДанных",
			НСтр("ru='Выберите терминал сбора данных'"), НСтр("ru='Терминал сбора данных не подключен.'"), ,Истина);
	Иначе
		НачатьОчисткуДанныеВТСД_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Контекст);
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьОчисткуДанныеВТСД_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Параметры) Экспорт
	
	Если ИдентификаторУстройства = Неопределено Тогда
		ОписаниеОшибки = НСтр("ru='Терминал сбора данных не подключен.'");
		Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
			РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки); 
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Параметры.Вставить("ИдентификаторУстройства", ИдентификаторУстройства);
	ОписаниеОповещения = ОписаниеОповещенияИС("НачатьОчисткуДанныеВТСД_ПодключениеЗавершение", МенеджерОборудованияКлиент, Параметры);
	НачатьПодключениеОборудованиеПоИдентификатору(ОписаниеОповещения, Параметры.УникальныйИдентификатор, ИдентификаторУстройства);
	
КонецПроцедуры

Процедура НачатьОчисткуДанныеВТСД_ПодключениеЗавершение(РезультатПодключения, Параметры) Экспорт
	
	Если РезультатПодключения.Результат Тогда
		ВходныеПараметры  = Неопределено;
		ОписаниеОповещения = ОписаниеОповещенияИС("НачатьОчисткуДанныеВТСД_ВыполнитьКомандуЗавершение", МенеджерОборудованияКлиент, Параметры);
		НачатьВыполнениеКоманды(ОписаниеОповещения, Параметры.ИдентификаторУстройства, "ClearTable", ВходныеПараметры);
	Иначе
		Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
			ОписаниеОшибки = РезультатПодключения.ОписаниеОшибки;
			РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки, Параметры.ИдентификаторУстройства);
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьОчисткуДанныеВТСД_ВыполнитьКомандуЗавершение(РезультатВыполнения, Параметры) Экспорт
		
	Если РезультатВыполнения.Результат Тогда
		ОписаниеОшибки = ""; 
	Иначе
		ОписаниеОшибки = НСтр("ru='При очистке данных в терминале сбора данных произошла ошибка: %ОписаниеОшибки%'");
		ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%ОписаниеОшибки%", РезультатВыполнения.ВыходныеПараметры[1]);
	КонецЕсли;
	
	Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
		РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(РезультатВыполнения.Результат, ОписаниеОшибки, Параметры.ИдентификаторУстройства); 
		ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
	КонецЕсли;
	
	НачатьОтключениеОборудованиеПоИдентификатору(Неопределено, Параметры.УникальныйИдентификатор, Параметры.ИдентификаторУстройства);
	
КонецПроцедуры

#КонецОбласти

#Область ЭлектронныеВесы

// Получает вес с электронных весов.
// УникальныйИдентификатор - идентификатор формы.
// ОповещениеПриЗавершении - оповещение при завершении взвешивании и передачи веса.
//
Процедура НачатьПолученияВесаСЭлектронныхВесов(ОповещениеПриЗавершении, УникальныйИдентификатор, ИдентификаторУстройства = Неопределено, ОтображатьСообщения = Ложь) Экспорт
	
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
	Контекст.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
	Контекст.Вставить("ОтображатьСообщения"    , ОтображатьСообщения);
	
	Если ИдентификаторУстройства = Неопределено Тогда
		Оповещение = ОписаниеОповещенияИС("НачатьПолученияВесаСЭлектронныхВесов_ВыбратьУстройствоЗавершение", МенеджерОборудованияКлиент, Контекст);
		ПредложитьВыбратьУстройство(Оповещение, "ЭлектронныеВесы",
			НСтр("ru='Выберите электронные весы'"), 
			НСтр("ru='Электронные весы не подключены.'"), 
			НСтр("ru='Электронные весы не выбраны.'"),
			Истина);
	Иначе
		НачатьПолученияВесаСЭлектронныхВесов_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Контекст);
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьПолученияВесаСЭлектронныхВесов_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Параметры) Экспорт
	
	Если ИдентификаторУстройства = Неопределено Тогда
		ОписаниеОшибки = НСтр("ru='Электронные весы не выбраны.'");
		Если Параметры.ОтображатьСообщения Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(ОписаниеОшибки);
		КонецЕсли;
		Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
			РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании();
			РезультатОперации.Результат = Ложь;
			РезультатОперации.ОписаниеОшибки = ОписаниеОшибки;
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Параметры.Вставить("ИдентификаторУстройства" , ИдентификаторУстройства);
	Оповещение = ОписаниеОповещенияИС("НачатьПолученияВесаСЭлектронныхВесов_ПодключениеЗавершение", МенеджерОборудованияКлиент, Параметры);
	НачатьПодключениеОборудованиеПоИдентификатору(Оповещение, Параметры.УникальныйИдентификатор, ИдентификаторУстройства);
	
КонецПроцедуры

Процедура НачатьПолученияВесаСЭлектронныхВесов_ПодключениеЗавершение(РезультатПодключения, Параметры) Экспорт
	
	Если РезультатПодключения.Результат Тогда  
		ВходныеПараметры  = Неопределено;
		Оповещение = ОписаниеОповещенияИС("НачатьПолученияВесаСЭлектронныхВесов_ВыполнитьКомандуЗавершение", МенеджерОборудованияКлиент, Параметры);
		НачатьВыполнениеКоманды(Оповещение, Параметры.ИдентификаторУстройства, "ПолучитьВес", ВходныеПараметры);
	Иначе
		// Ошибка подключения весов
		ОписаниеОшибки = РезультатПодключения.ОписаниеОшибки;
		Если Параметры.ОтображатьСообщения Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(ОписаниеОшибки);
		КонецЕсли;
		Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
			РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки, Параметры.ИдентификаторУстройства);
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьПолученияВесаСЭлектронныхВесов_ВыполнитьКомандуЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если РезультатВыполнения.Результат Тогда
		Вес = РезультатВыполнения.ВыходныеПараметры[0]; // Вес получен
		ОписаниеОшибки = "";
	Иначе
		Вес = Неопределено;
		ОписаниеОшибки = НСтр("ru='При использовании электронных весов произошла ошибка: %ДополнительноеОписание%'");
		ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%ДополнительноеОписание%", РезультатВыполнения.ВыходныеПараметры[1]);
		Если Параметры.ОтображатьСообщения Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(ОписаниеОшибки);
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
		РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(РезультатВыполнения.Результат, ОписаниеОшибки, Параметры.ИдентификаторУстройства);
		РезультатОперации.Вставить("Вес", Вес);
		ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
	КонецЕсли;
	
	НачатьОтключениеОборудованиеПоИдентификатору(Неопределено, Параметры.УникальныйИдентификатор, Параметры.ИдентификаторУстройства);
	
КонецПроцедуры

// Устарела: следует использовать ОборудованиеВесовоеОборудованиеКлиент.НачатьУстановкуВесаТарыЭлектронныхВесов.
// Начать установку веса тары на электронных весах.
// УникальныйИдентификатор - идентификатор формы.
// ОповещениеПриЗавершении - оповещение при завершении взвешивании и передачи веса.
//
Процедура НачатьУстановкуВесаТары(ОповещениеПриЗавершении, УникальныйИдентификатор, ИдентификаторУстройства = Неопределено, Вес = Неопределено) Экспорт
	
	ВходныеПараметры = Новый Массив();
	ВходныеПараметры.Добавить(Вес);
	
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
	Контекст.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
	Контекст.Вставить("ВходныеПараметры"       , ВходныеПараметры);
	
	Если ИдентификаторУстройства = Неопределено Тогда
		Оповещение = ОписаниеОповещенияИС("НачатьУстановкуВесаТары_ВыбратьУстройствоЗавершение", МенеджерОборудованияКлиент, Контекст);
		ПредложитьВыбратьУстройство(Оповещение, "ЭлектронныеВесы",
			НСтр("ru='Выберите электронные весы'"), 
			НСтр("ru='Электронные весы не подключены.'"), 
			НСтр("ru='Электронные весы не выбраны.'"),
			Истина);
	Иначе
		НачатьУстановкуВесаТары_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Контекст);
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьУстановкуВесаТары_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Параметры) Экспорт
	
	Если ИдентификаторУстройства = Неопределено Тогда
		ОписаниеОшибки = НСтр("ru='Электронные весы не выбраны.'");
		Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
			РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании();
			РезультатОперации.Результат = Ложь;
			РезультатОперации.ОписаниеОшибки = ОписаниеОшибки;
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Параметры.Вставить("ИдентификаторУстройства" , ИдентификаторУстройства);
	Оповещение = ОписаниеОповещенияИС("НачатьУстановкуВесаТары_ПодключениеЗавершение", МенеджерОборудованияКлиент, Параметры);
	НачатьПодключениеОборудованиеПоИдентификатору(Оповещение, Параметры.УникальныйИдентификатор, ИдентификаторУстройства);
	
КонецПроцедуры

Процедура НачатьУстановкуВесаТары_ПодключениеЗавершение(РезультатПодключения, Параметры) Экспорт
	
	Если РезультатПодключения.Результат Тогда  
		ВходныеПараметры  = Параметры.ВходныеПараметры;
		Оповещение = ОписаниеОповещенияИС("НачатьУстановкуВесаТары_ВыполнитьКомандуЗавершение", МенеджерОборудованияКлиент, Параметры);
		НачатьВыполнениеКоманды(Оповещение, Параметры.ИдентификаторУстройства, "Тарировать", ВходныеПараметры);
	Иначе
		// Ошибка подключения весов
		Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
			ОписаниеОшибки = РезультатПодключения.ОписаниеОшибки;
			РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки, Параметры.ИдентификаторУстройства);
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьУстановкуВесаТары_ВыполнитьКомандуЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если НЕ РезультатВыполнения.Результат Тогда
		ОписаниеОшибки = НСтр("ru='При использовании электронных весов произошла ошибка: %ДополнительноеОписание%'");
		ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%ДополнительноеОписание%", РезультатВыполнения.ВыходныеПараметры[1]);
	КонецЕсли;
	
	Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
		РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(РезультатВыполнения.Результат, ОписаниеОшибки, Параметры.ИдентификаторУстройства);
		ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
	КонецЕсли;
	
	НачатьОтключениеОборудованиеПоИдентификатору(Неопределено, Параметры.УникальныйИдентификатор, Параметры.ИдентификаторУстройства);
	
КонецПроцедуры

#КонецОбласти

#Область ККТ

// Устарела: следует использовать ОборудованиеЧекопечатающиеУстройстваКлиент.НачатьОткрытиеСессииРегистрацииКМ.
// Осуществляет открытие сессии регистрации КМ.
//
// Параметры:
//  ОповещениеПриЗавершении - ОписаниеОповещения - обработчик результата.
//  УникальныйИдентификатор - УникальныйИдентификатор - Идентификатор формы.
//  Параметры - Структура - Cодержит параметры выполнения операции.
//  ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - Устройство.
//
Процедура НачатьОткрытиеСессииРегистрацииКМ(ОповещениеПриЗавершении, УникальныйИдентификатор, Параметры = Неопределено, ИдентификаторУстройства = Неопределено) Экспорт
	
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеПриЗавершении" , ОповещениеПриЗавершении);
	Контекст.Вставить("УникальныйИдентификатор" , УникальныйИдентификатор);
	Контекст.Вставить("ВходныеПараметры"        , Параметры);
	Контекст.Вставить("ВыполняемаяКоманда"      , "OpenSessionRegistrationKM");
	
	Если ИдентификаторУстройства = Неопределено Тогда
		
		ПоддерживаемыеТипыВО = Новый Массив();
		ПоддерживаемыеТипыВО.Добавить("ККТ");
		ОписаниеОповещения = Новый ОписаниеОповещения("НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение", МенеджерОборудованияКлиент, Контекст);
		ПредложитьВыбратьУстройство(ОписаниеОповещения, ПоддерживаемыеТипыВО,
			НСтр("ru='Выберите ККТ'"), 
			НСтр("ru='ККТ не подключено.'"), 
			НСтр("ru='ККТ не выбрано.'"), 
			Истина);
	Иначе
		НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Контекст);
	КонецЕсли;
	
КонецПроцедуры

// Устарела: следует использовать ОборудованиеЧекопечатающиеУстройстваКлиент.НачатьЗакрытииСессииРегистрацииКМ.
// Осуществляет закрытии сессии регистрации КМ.
//
// Параметры:
//  ОповещениеПриЗавершении - ОписаниеОповещения - обработчик результата.
//  УникальныйИдентификатор - УникальныйИдентификатор - Идентификатор формы.
//  Параметры - Структура - Cодержит параметры выполнения операции.
//  ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - Устройство.
//
Процедура НачатьЗакрытииСессииРегистрацииКМ(ОповещениеПриЗавершении, УникальныйИдентификатор, Параметры = Неопределено, ИдентификаторУстройства = Неопределено) Экспорт
	
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеПриЗавершении" , ОповещениеПриЗавершении);
	Контекст.Вставить("УникальныйИдентификатор" , УникальныйИдентификатор);
	Контекст.Вставить("ВходныеПараметры"        , Параметры);
	Контекст.Вставить("ВыполняемаяКоманда"      , "CloseSessionRegistrationKM");
	
	Если ИдентификаторУстройства = Неопределено Тогда
		ВыводСообщений.ВывестиСообщениеВОкноСообщений(НСтр("ru='ККТ не подключено.'"),ПредопределенноеЗначение("Перечисление.ВидыСообщений.Ошибка"),,,Истина);
	Иначе
		НачатьВыполнениеКомандыТО(ОповещениеПриЗавершении, ИдентификаторУстройства, "CloseSessionRegistrationKM", Параметры);
	КонецЕсли;
	
КонецПроцедуры

// Устарела: следует использовать ОборудованиеЧекопечатающиеУстройстваКлиент.НачатьЗапросКМ.
// Осуществляет запрос КМ.
//
// Параметры:
//  ОповещениеПриЗавершении - ОписаниеОповещения - обработчик результата.
//  УникальныйИдентификатор - УникальныйИдентификатор - Идентификатор формы.
//  Параметры - Структура - Cодержит параметры выполнения операции.
//  ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - Устройство.
//
Процедура НачатьЗапросКМ(ОповещениеПриЗавершении, УникальныйИдентификатор, Параметры, ИдентификаторУстройства = Неопределено) Экспорт
	
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеПриЗавершении" , ОповещениеПриЗавершении);
	Контекст.Вставить("УникальныйИдентификатор" , УникальныйИдентификатор);
	Контекст.Вставить("ВходныеПараметры"        , Параметры);
	Контекст.Вставить("ВыполняемаяКоманда"      , "RequestKM");
	
	Если ИдентификаторУстройства = Неопределено Тогда
		
		ВыводСообщений.ВывестиСообщениеВОкноСообщений(НСтр("ru='ККТ не подключено.'"),ПредопределенноеЗначение("Перечисление.ВидыСообщений.Ошибка"),,,Истина);
		
	Иначе
		НачатьВыполнениеКомандыТО(ОповещениеПриЗавершении, ИдентификаторУстройства, "RequestKM", Параметры);
	КонецЕсли;
	
КонецПроцедуры

// Начать получения результатов запроса КМ.
//
// Параметры:
//  ОповещениеПриЗавершении - ОписаниеОповещения - обработчик результата.
//  УникальныйИдентификатор - УникальныйИдентификатор - Идентификатор формы.
//  Параметры - Структура - Cодержит параметры выполнения операции.
//  ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - Устройство.
//
Процедура НачатьПолученияРезультатовЗапросаКМ(ОповещениеПриЗавершении, УникальныйИдентификатор, Параметры, ИдентификаторУстройства = Неопределено) Экспорт
	
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеПриЗавершении" , ОповещениеПриЗавершении);
	Контекст.Вставить("УникальныйИдентификатор" , УникальныйИдентификатор);
	Контекст.Вставить("ВходныеПараметры"        , Параметры);
	Контекст.Вставить("ВыполняемаяКоманда"      , "GetProcessingKMResult");
	
	Если ИдентификаторУстройства = Неопределено Тогда
		
		ВыводСообщений.ВывестиСообщениеВОкноСообщений(НСтр("ru='ККТ не подключено.'"),ПредопределенноеЗначение("Перечисление.ВидыСообщений.Ошибка"),,,Истина);

	Иначе
		НачатьВыполнениеКомандыТО(ОповещениеПриЗавершении, ИдентификаторУстройства, "GetProcessingKMResult", Параметры);
	КонецЕсли;
	
КонецПроцедуры

// Устарела: следует использовать ОборудованиеЧекопечатающиеУстройстваКлиент.НачатьПодтверждениеКМ.
// Начать подтверждение КМ.
//
// Параметры:
//  ОповещениеПриЗавершении - ОписаниеОповещения - обработчик результата.
//  УникальныйИдентификатор - УникальныйИдентификатор - Идентификатор формы.
//  Параметры - Структура - Cодержит параметры выполнения операции.
//  ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - Устройство.
//
Процедура НачатьПодтверждениеКМ(ОповещениеПриЗавершении, УникальныйИдентификатор, Параметры, ИдентификаторУстройства = Неопределено) Экспорт
	
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеПриЗавершении" , ОповещениеПриЗавершении);
	Контекст.Вставить("УникальныйИдентификатор" , УникальныйИдентификатор);
	Контекст.Вставить("ВходныеПараметры"        , Параметры);
	Контекст.Вставить("ВыполняемаяКоманда"      , "ConfirmKM");
	
	Если ИдентификаторУстройства = Неопределено Тогда
		
		ВыводСообщений.ВывестиСообщениеВОкноСообщений(НСтр("ru='ККТ не подключено.'"),ПредопределенноеЗначение("Перечисление.ВидыСообщений.Ошибка"),,,Истина);
		
	Иначе
		НачатьВыполнениеКомандыТО(ОповещениеПриЗавершении, ИдентификаторУстройства, "ConfirmKM", Параметры);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ВспомогательныеПроцедурыИФункции

// Заполняет структуру выполнения операции на Оборудовании.
// 
Функция ПараметрыВыполненияОперацииНаОборудовании(Результат = Ложь, ОписаниеОшибки = Неопределено, ИдентификаторУстройства = Неопределено) Экспорт; 
	
	РезультатВыполнения = Новый Структура();
	РезультатВыполнения.Вставить("Результат"              , Результат);
	РезультатВыполнения.Вставить("ОписаниеОшибки"         , ОписаниеОшибки);
	РезультатВыполнения.Вставить("ИдентификаторУстройства", ИдентификаторУстройства);
	РезультатВыполнения.Вставить("ВыходныеПараметры"      , Неопределено);
	Возврат РезультатВыполнения;
	
КонецФункции

// Функция начинает выбор файла драйвера для последующей загрузки.
//
Процедура НачатьВыборФайлаДрайвера(ОповещениеПриВыборе) Экспорт 
	
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
	ДиалогОткрытияФайла.Заголовок = НСтр("ru='Выберите файл драйвера'");
	ДиалогОткрытияФайла.Фильтр = НСтр("ru='Файл драйвера'") + "(*.zip)|*.zip";  
	
	Параметры = Новый Структура("СледующееОповещение", ОповещениеПриВыборе);
	Оповещение = ОписаниеОповещенияИС("ВыборФайлаДрайвераЗавершение", МенеджерОборудованияКлиент, Параметры);
	
	Если ДиалогОткрытияФайла.Выбрать() Тогда 
		ВыполнитьОбработкуОповещенияИС(Оповещение,ДиалогОткрытияФайла);
	КонецЕсли;
	
КонецПроцедуры

// Завершение выбора файла драйвера.
//
Процедура ВыборФайлаДрайвераЗавершение(ВыбранныеФайлы, Параметры) Экспорт
	
	Если ТипЗнч(ВыбранныеФайлы) = Тип("Массив") И ВыбранныеФайлы.Количество() > 0  Тогда
		Если Параметры.СледующееОповещение <> Неопределено Тогда
			ВыполнитьОбработкуОповещенияИС(Параметры.СледующееОповещение, ВыбранныеФайлы[0]);
		КонецЕсли
	КонецЕсли;
	
КонецПроцедуры

// Завершение выбора файла
//
Процедура НачатьВыборФайлаРасширенияЗавершение(Установлено, ДополнительныеПараметры) Экспорт
	
	Если Установлено Тогда
		Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла[ДополнительныеПараметры.РежимДиалогаВыбораФайла]);
		Диалог.МножественныйВыбор = Ложь;
		Диалог.ПолноеИмяФайла = ДополнительныеПараметры.ИмяФайла;
		Если Диалог.Выбрать() Тогда
			ВыполнитьОбработкуОповещенияИС(ДополнительныеПараметры.ОповещениеПриВыборе,Диалог);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
 
// Функция начинает выбор файла.
//
Процедура НачатьВыборФайла(ОповещениеПриВыборе, Знач ИмяФайла, РежимДиалогаВыбораФайла = "Открытие") Экспорт
	
	ПараметрыКоманды = Новый Структура("ОповещениеПриВыборе, ИмяФайла, РежимДиалогаВыбораФайла", ОповещениеПриВыборе, ИмяФайла, РежимДиалогаВыбораФайла);
	Оповещение = ОписаниеОповещенияИС("НачатьВыборФайлаРасширенияЗавершение", МенеджерОборудованияКлиент, ПараметрыКоманды);
	ПроверитьДоступностьРасширенияРаботыСФайлами(Оповещение);
	
КонецПроцедуры

// Процедура формирует задержку указанной длительности.
//
// Параметры:
//  Время - Число - Длительность задержки в секундах.
//        - Длительность задержки в секундах.
//
Процедура Пауза(Время) Экспорт

	ВремяЗавершения = ТекущаяДата() + Время;
	Пока ТекущаяДата() < ВремяЗавершения Цикл
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииПодключениеОборудованияСинхронноУстарели

// Функция читает корневой элемент XML.
//
// Параметры:
//  СтрокаXML - Строка - XML строка.
//
// Возвращаемое значение:
//  Структура:
//   * ЭлементXML - Строка.
//
Функция ПрочитатьКорневойЭлементXML(СтрокаXML) Экспорт
	
	Результат = Новый Структура();
	Если Не ПустаяСтрока(СтрокаXML) Тогда
		ЧтениеXML = Новый ЧтениеXML; 
		ЧтениеXML.УстановитьСтроку(СтрокаXML);
		ЧтениеXML.ПерейтиКСодержимому();
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			Если ЧтениеXML.КоличествоАтрибутов() > 0 Тогда
				Пока ЧтениеXML.ПрочитатьАтрибут() Цикл
					Результат.Вставить(ЧтениеXML.Имя, ЧтениеXML.Значение);
				КонецЦикла
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Подключает одиночный экземпляр устройства определяемый идентификатором.
//
// Возвращаемое значение:
//  Булево - .
Функция ПодключитьОборудованиеПоИдентификатору(ИдентификаторКлиента, ИдентификаторУстройства, ОписаниеОшибки = "") Экспорт
	
	Возврат ПодключитьОборудование(ИдентификаторКлиента, , ИдентификаторУстройства, ОписаниеОшибки);
	
КонецФункции

// Функция подключает устройства по типу оборудования.
// Возвращает результат выполнения функции.
//
// Возвращаемое значение:
//  Булево - .
Функция ПодключитьОборудование(ИдентификаторКлиента, ТипыПО = Неопределено, ИдентификаторУстройства = Неопределено, ОписаниеОшибки = "") Экспорт
	   
	КонечныйРезультат = Истина;
	Результат         = Истина;
	
	ОбъектДрайвера    = Неопределено;
	ОписаниеОшибки    = "";

	Результат = ОбновитьРабочееМестоКлиента();
	Если Не Результат Тогда
		ОписаниеОшибки = НСтр("ru='Предварительно необходимо выбрать рабочее место подключаемого оборудования текущего сеанса.'");
		Возврат Ложь;
	КонецЕсли;
	
	СписокОборудования = МенеджерОборудованияВызовСервера.ОборудованиеПоПараметрам(ТипыПО, ИдентификаторУстройства);
	
	Если СписокОборудования.Количество() > 0 Тогда
		Для Каждого Устройство Из СписокОборудования Цикл
			
			// Проверим, не подключено ли устройство ранее.
			ПодключенноеУстройство = ПолучитьПодключенноеУстройство(глПодключаемоеОборудование.ПараметрыПодключенияПО, Устройство.Ссылка);
			
			Если ПодключенноеУстройство = Неопределено Тогда // Если устройство не было подключено ранее.
				ОбъектДрайвера = ПолучитьОбъектДрайвера(Устройство);
				Если ОбъектДрайвера = Неопределено Тогда
					// Сообщить об ошибке, что не удалось загрузить драйвер.
					ОписаниеОшибки = ОписаниеОшибки + ?(ПустаяСтрока(ОписаниеОшибки), "", Символы.ПС)
								   + НСтр("ru='%Наименование%: Не удалось загрузить драйвер устройства.
									 |Проверьте, что драйвер корректно установлен и зарегистрирован в системе.'");
					ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%Наименование%", Устройство.Наименование);
					КонечныйРезультат = Ложь;
					Продолжить;
				КонецЕсли;
				
				ОбработчикДрайвераМодуль = МенеджерОборудованияКлиентПовтИсп.ПолучитьОбработчикДрайвера(Устройство.ОбработчикДрайвера, Не Устройство.ВСоставеКонфигурации, Устройство.ТипОборудованияИмя);
				
				НовоеПодключение = Новый Структура();
				НовоеПодключение.Вставить("Клиенты"               , Новый Массив());
				НовоеПодключение.Клиенты.Добавить(ИдентификаторКлиента);
				НовоеПодключение.Вставить("Ссылка"                  , Устройство.Ссылка);
				НовоеПодключение.Вставить("ИдентификаторУстройства" , Устройство.ИдентификаторУстройства);
				НовоеПодключение.Вставить("ОбработчикДрайвера"      , Устройство.ОбработчикДрайвера);
				НовоеПодключение.Вставить("Наименование"            , Устройство.Наименование);
				НовоеПодключение.Вставить("ТипОборудования"         , Устройство.ТипОборудования);
				НовоеПодключение.Вставить("ТипОборудованияИмя"      , Устройство.ТипОборудованияИмя);
				НовоеПодключение.Вставить("ДрайверОборудования"     , Устройство.ДрайверОборудования);
				НовоеПодключение.Вставить("ВСоставеКонфигурации"    , Устройство.ВСоставеКонфигурации);
				НовоеПодключение.Вставить("ИдентификаторОбъекта"    , Устройство.ИдентификаторОбъекта);
				НовоеПодключение.Вставить("ИмяМакетаДрайвера"       , Устройство.ИмяМакетаДрайвера);
				НовоеПодключение.Вставить("ИмяФайлаДрайвера"        , Устройство.ИмяФайлаДрайвера);
				НовоеПодключение.Вставить("РабочееМесто"            , Устройство.РабочееМесто);
				НовоеПодключение.Вставить("ИмяКомпьютера"           , Устройство.ИмяКомпьютера);
				НовоеПодключение.Вставить("Параметры"               , Устройство.Параметры);
				НовоеПодключение.Вставить("ПараметрыРегистрации"    , Устройство.ПараметрыРегистрации);
				НовоеПодключение.Вставить("КоличествоПодключенных"  , 1);
				НовоеПодключение.Вставить("ОбъектДрайвера"          , Неопределено);
				НовоеПодключение.Вставить("ОбработчикДрайвераМодуль", ОбработчикДрайвераМодуль);
				НовоеПодключение.Вставить("ПараметрыПодключения"    , Новый Структура());
				НовоеПодключение.ПараметрыПодключения.Вставить("ТипОборудования", Устройство.ТипОборудованияИмя);
				НовоеПодключение.ПараметрыПодключения.Вставить("ПараметрыРегистрации"   , Устройство.ПараметрыРегистрации);
				НовоеПодключение.ПараметрыПодключения.Вставить("ИдентификаторУстройства", Устройство.Ссылка);

				ВыходныеПараметры = Неопределено;
				   
				Если ОбработчикДрайвераМодуль = Неопределено Тогда
					// Сообщить об ошибке, что не удалось загрузить драйвер.
					ОписаниеОшибки = ОписаниеОшибки +  НСтр("ru='Не удалось подключить обработчик драйвера.'");
					КонечныйРезультат = Ложь;
					Продолжить;
				Иначе
					#Если ВебКлиент Тогда
					// Принудительно заменяем асинхронный обработчик на синхронный, так как работам в синхронном режиме.
					Если ОбработчикДрайвераМодуль = ПодключаемоеОборудованиеУниверсальныйДрайверАсинхронноКлиент Тогда
						ОбработчикДрайвераМодуль = ПодключаемоеОборудованиеУниверсальныйДрайверКлиент;
					КонецЕсли;
					#КонецЕсли
					Результат = ОбработчикДрайвераМодуль.ПодключитьУстройство(ОбъектДрайвера, НовоеПодключение.Параметры, 
						НовоеПодключение.ПараметрыПодключения, ВыходныеПараметры);
				КонецЕсли;
				
				Если Результат Тогда
					Если ВыходныеПараметры.Количество() >= 2 Тогда
						НовоеПодключение.Вставить("ИсточникСобытия", ВыходныеПараметры[0]);
						НовоеПодключение.Вставить("ИменаСобытий",    ВыходныеПараметры[1]);
					Иначе
						НовоеПодключение.Вставить("ИсточникСобытия", "");
						НовоеПодключение.Вставить("ИменаСобытий",    Неопределено);
					КонецЕсли;
					НовоеПодключение.Вставить("ОбъектДрайвера"          , ОбъектДрайвера);
					НовоеПодключение.Вставить("ОбработчикДрайвераМодуль", ОбработчикДрайвераМодуль);
					МассивПараметровПодключения = глПодключаемоеОборудование.ПараметрыПодключенияПО; //Массив - 
					МассивПараметровПодключения.Добавить(НовоеПодключение);
				Иначе
					// Сообщим пользователю о том, что не удалось подключить устройство.
					ОписаниеОшибки = ОписаниеОшибки + ?(ПустаяСтрока(ОписаниеОшибки), "", Символы.ПС)
								   + НСтр("ru='Не удалось подключить устройство ""%Наименование%"": %ОписаниеОшибки% (%КодОшибки%)'");
					ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%Наименование%"  , Устройство.Наименование);
					ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%ОписаниеОшибки%", ВыходныеПараметры[1]);
					ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%КодОшибки%"     , ВыходныеПараметры[0]);
				КонецЕсли;
			Иначе // Устройство было подключено ранее.
				// Увеличим количество пользователей данного соединения.
				МассивКлиентов = ПодключенноеУстройство.Клиенты; //Массив - 
				МассивКлиентов.Добавить(ИдентификаторКлиента);
				ПодключенноеУстройство.КоличествоПодключенных = ПодключенноеУстройство.КоличествоПодключенных + 1;
			КонецЕсли;
			
			КонечныйРезультат = КонечныйРезультат И Результат;
		КонецЦикла;
		
	ИначеЕсли ИдентификаторУстройства <> Неопределено Тогда
		ОписаниеОшибки = НСтр("ru='Выбранное устройство не может использоваться для подключения.
		|Укажите другое устройство.'");
		КонечныйРезультат = Ложь;
	ИначеЕсли ТипыПО = Неопределено И ИдентификаторУстройства = Неопределено Тогда
		ОписаниеОшибки = НСтр("ru='Нет доступного оборудования для подключения.'");
		КонечныйРезультат = Ложь;
	КонецЕсли;
	
	Возврат КонечныйРезультат;
	
КонецФункции

// Отключает устройство, определенное идентификатором.
//
// Возвращаемое значение:
//  Булево - .
Функция ОтключитьОборудованиеПоИдентификатору(ИдентификаторКлиента, ИдентификаторУстройства, ОписаниеОшибки = "") Экспорт
	
	Возврат ОтключитьОборудование(ИдентификаторКлиента, , ИдентификаторУстройства, ОписаниеОшибки);
	
КонецФункции

// Функция подключает устройства по типу оборудования.
// 
// Возвращаемое значение:
//  Булево - . 
Функция ОтключитьОборудование(ИдентификаторКлиента, ТипыПО = Неопределено, ИдентификаторУстройства = Неопределено, ОписаниеОшибки = "") Экспорт
	
	КонечныйРезультат = Истина;
	Результат         = Истина;
	
	Если глПодключаемоеОборудование.ПараметрыПодключенияПО <> Неопределено Тогда
		КоличествоУстройств = глПодключаемоеОборудование.ПараметрыПодключенияПО.Количество();
		Для Индекс = 1 По КоличествоУстройств Цикл
			
			ПодключенноеУстройство = глПодключаемоеОборудование.ПараметрыПодключенияПО[КоличествоУстройств - Индекс]; //СправочникСсылка.ПодключаемоеОборудование - 
			КлиентПодключения = ПодключенноеУстройство.Клиенты.Найти(ИдентификаторКлиента);
			
			Если КлиентПодключения <> Неопределено  И (ТипыПО = Неопределено Или ТипыПО.Найти(ПодключенноеУстройство.ТипОборудованияИмя) <> Неопределено)
			   И (ИдентификаторУстройства = Неопределено  Или ПодключенноеУстройство.Ссылка = ИдентификаторУстройства)Тогда
				 
				 Если ПодключенноеУстройство.КоличествоПодключенных = 1 Тогда
					 
					Если ПодключенноеУстройство.ОбъектДрайвера = Неопределено Тогда
						// Сообщить об ошибке, что не удалось загрузить драйвер.
						ОписаниеОшибки = НСтр("ru='""%Наименование%"": Не удалось загрузить драйвер устройства.
													|Проверьте, что драйвер корректно установлен и зарегистрирован в системе.'");
						ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%Наименование%", ПодключенноеУстройство.Наименование);
						КонечныйРезультат = Ложь;
						Продолжить;
					КонецЕсли;
					
					ОбработчикДрайвераМодуль = ПодключенноеУстройство.ОбработчикДрайвераМодуль;
					#Если ВебКлиент Тогда
					// Принудительно заменяем асинхронный обработчик на синхронный, так как работам в синхронном режиме.
					Если ОбработчикДрайвераМодуль = ПодключаемоеОборудованиеУниверсальныйДрайверАсинхронноКлиент Тогда
						ОбработчикДрайвераМодуль = ПодключаемоеОборудованиеУниверсальныйДрайверКлиент;
					КонецЕсли;
					#КонецЕсли
					
					ВыходныеПараметры = Неопределено;
					Результат = ОбработчикДрайвераМодуль.ОтключитьУстройство(ПодключенноеУстройство.ОбъектДрайвера, ПодключенноеУстройство.Параметры, ПодключенноеУстройство.ПараметрыПодключения, ВыходныеПараметры);
					Если НЕ Результат Тогда
						ОписаниеОшибки = ОписаниеОшибки + ?(ПустаяСтрока(ОписаниеОшибки), "", Символы.ПС)
									   + НСтр("ru='При отключении устройства ""%Наименование%"" произошла ошибка: %ОписаниеОшибки% (%КодОшибки%)'");
						ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%Наименование%", ПодключенноеУстройство.Наименование);
						ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%ОписаниеОшибки%", ВыходныеПараметры[1]);
						ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%КодОшибки%", ВыходныеПараметры[0]);
					Иначе
						ПодключенноеУстройство.КоличествоПодключенных = 0;
					КонецЕсли;
					
					НомерСтрокиМассива = глПодключаемоеОборудование.ПараметрыПодключенияПО.Найти(ПодключенноеУстройство);
					Если НомерСтрокиМассива <> Неопределено Тогда
						глПодключаемоеОборудование.ПараметрыПодключенияПО.Удалить(НомерСтрокиМассива);
					КонецЕсли;
				Иначе
					ПодключенноеУстройство.КоличествоПодключенных = ПодключенноеУстройство.КоличествоПодключенных - 1;
					ПодключенноеУстройство.Клиенты.Удалить(КлиентПодключения);
				КонецЕсли;
			КонецЕсли;
			
			КонечныйРезультат = КонечныйРезультат И Результат;
		КонецЦикла;
	КонецЕсли;
	
	Возврат КонечныйРезультат;
	
КонецФункции  

Функция ВыполнитьКоманду(Идентификатор, Команда, ВходныеПараметры, ВыходныеПараметры, Таймаут = -1) Экспорт
	
	Результат = Ложь;
	
	// Поиск подключенного устройства.
	ПодключенноеУстройство = ПолучитьПодключенноеУстройство(глПодключаемоеОборудование.ПараметрыПодключенияПО, Идентификатор);
	
	Если ПодключенноеУстройство <> Неопределено Тогда
		
		ОбъектДрайвера = ПодключенноеУстройство.ОбъектДрайвера;
		Если ОбъектДрайвера = Неопределено Тогда
			// Сообщить об ошибке, что не удалось загрузить драйвер.
			ОписаниеОшибки = НСтр("ru='""%Наименование%"": Не удалось загрузить драйвер устройства.
										|Проверьте, что драйвер корректно установлен и зарегистрирован в системе.'");
			ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%Наименование%", ПодключенноеУстройство.Наименование);
			ВыходныеПараметры = Новый Массив();
			ВыходныеПараметры.Добавить(999);
			ВыходныеПараметры.Добавить(ОписаниеОшибки);
		Иначе
			Параметры            = ПодключенноеУстройство.Параметры;
			ПараметрыПодключения = ПодключенноеУстройство.ПараметрыПодключения;
				// Вызов метода выполнения команды у обработчика.
			Результат = ПодключаемоеОборудованиеУниверсальныйДрайверКлиент.ВыполнитьКоманду(Команда, ВходныеПараметры, ВыходныеПараметры, ОбъектДрайвера, Параметры, ПараметрыПодключения); 
		КонецЕсли;
	Иначе
		// Сообщить об ошибке, что устройство не подключено.
		ВыходныеПараметры = Новый Массив();
		ТекстСообщения = НСтр("ru='Устройство не подключено. Перед выполнением операции устройство должно быть подключено.'");
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(ТекстСообщения);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ПроцедурыИФункцииДляРаботыСПодключаемымОборудованием

// Выполняет дополнительную команду к драйверу, не требующую предварительного подключения устройства в системе.
// 
//  Параметры:
// 	Команда - Строка - наименование команды.
// 	ВходныеПараметры - Массив - массив входных параметров.
// 	ВыходныеПараметры - Массив - массив выходных параметров.
// 	Идентификатор - СправочникСсылка.ПодключаемоеОборудование - .
// 	Параметры - Структура - параметры выполнения операции.
// Возвращаемое значение:
// 	Булево - Описание
Функция ВыполнитьДополнительнуюКоманду(Команда, ВходныеПараметры, ВыходныеПараметры, Идентификатор, Параметры) Экспорт
	
	Результат = Ложь;
	
	// Поиск подключенного устройства.
	ПодключенноеУстройство = ПолучитьПодключенноеУстройство(глПодключаемоеОборудование.ПараметрыПодключенияПО, Идентификатор);
	
	Если ПодключенноеУстройство = Неопределено Тогда
		
		ДанныеОборудования = МенеджерОборудованияВызовСервера.ПолучитьДанныеУстройства(Идентификатор);
		
		времПараметрыПодключения = Новый Структура();
		времПараметрыПодключения.Вставить("ТипОборудования", ДанныеОборудования.ТипОборудованияИмя);
		
		ОбъектДрайвера = ПолучитьОбъектДрайвера(ДанныеОборудования);
		Если ОбъектДрайвера = Неопределено Тогда
			// Сообщить об ошибке, что не удалось загрузить драйвер.
			ТекстСообщения = НСтр("ru='Не удалось загрузить драйвер устройства.
										|Проверьте, что драйвер корректно установлен и зарегистрирован в системе.'");
			ВыходныеПараметры = Новый Массив();
			ВыходныеПараметры.Добавить(999);
			ВыходныеПараметры.Добавить(ТекстСообщения);
			ВыходныеПараметры.Добавить(НСтр("ru='Не установлен'"));
			
		Иначе
			ОбработчикДрайвераМодуль = МенеджерОборудованияКлиентПовтИсп.ПолучитьОбработчикДрайвера(ДанныеОборудования.ОбработчикДрайвера, Не ДанныеОборудования.ВСоставеКонфигурации, ДанныеОборудования.ТипОборудованияИмя);
			Если ОбработчикДрайвераМодуль = Неопределено Тогда
				// Сообщить об ошибке, что не удалось загрузить драйвер.
				ТекстСообщения = НСтр("ru='Не удалось подключить обработчик драйвера.'");
				ВыходныеПараметры = Новый Массив();
				ВыходныеПараметры.Добавить(999);
				ВыходныеПараметры.Добавить(ТекстСообщения);
				ВыходныеПараметры.Добавить(НСтр("ru='Не установлен'"));
			Иначе
				#Если ВебКлиент Тогда
				// Принудительно заменяем асинхронный обработчик на синхронный, так как работам в синхронном режиме.
				Если ОбработчикДрайвераМодуль = ПодключаемоеОборудованиеУниверсальныйДрайверАсинхронноКлиент Тогда
					ОбработчикДрайвераМодуль = ПодключаемоеОборудованиеУниверсальныйДрайверКлиент;
				КонецЕсли;
				#КонецЕсли
				
				Результат = ОбработчикДрайвераМодуль.ВыполнитьКоманду(Команда, ВходныеПараметры, ВыходныеПараметры, ОбъектДрайвера, Параметры, времПараметрыПодключения);
				Если Не Результат Тогда
					Если ВыходныеПараметры = Неопределено Тогда
						ВыходныеПараметры = Новый Массив();
					КонецЕсли;
					ВыходныеПараметры.Добавить(НСтр("ru='Установлен'"));
				КонецЕсли;
			КонецЕсли
		КонецЕсли;
	Иначе
		// Сообщить об ошибке, что устройство подключено.
		ТекстСообщения = НСтр("ru='Устройство подключено, настройки не могут быть изменены. Для отключения устройства требуется закрыть все формы приложения для текущей информационной базы.'");
		ВыходныеПараметры = Новый Массив();
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(ТекстСообщения);
		ВыходныеПараметры.Добавить(НСтр("ru='Установлен'"));
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Выполняет команду обработчика драйвера, не требующую предварительного подключения устройства в системе.
//
// Возвращаемое значение:
//  Булево - .
Функция ВыполнитьКомандуОбработчикаДрайвера(Команда, ВходныеПараметры, ВыходныеПараметры, Идентификатор, Параметры, ПоддержкаАсинхронногоРежима) Экспорт
	
	Результат = Ложь;
	
	Если Идентификатор <> Неопределено Тогда
		ДанныеОборудования = МенеджерОборудованияВызовСервера.ПолучитьДанныеУстройства(Идентификатор);
		ОбработчикДрайвераМодуль = МенеджерОборудованияКлиентПовтИсп.ПолучитьОбработчикДрайвера(ДанныеОборудования.ОбработчикДрайвера, Не ДанныеОборудования.ВСоставеКонфигурации, ДанныеОборудования.ТипОборудованияИмя);
		Если ОбработчикДрайвераМодуль <> Неопределено Тогда
			Результат = ОбработчикДрайвераМодуль.ВыполнитьКоманду(Команда, ВходныеПараметры, ВыходныеПараметры, Неопределено, Параметры, Неопределено);
			ПоддержкаАсинхронногоРежима = ОбработчикДрайвераМодуль.ПоддержкаАсинхронногоРежима();
		КонецЕсли;
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

#КонецОбласти  

#Область ПроцедурыИФункцииДляРаботыСДрайвером

// Проверяет установлен ли драйвер.
//
// Возвращаемое значение:
//  СправочникСсылка.ДрайверыОборудования.
Функция ДрайверУстановлен(Идентификатор) Экспорт
	
	ДанныеОборудования = МенеджерОборудованияВызовСервера.ПолучитьДанныеУстройства(Идентификатор);
	ОбъектДрайвера = ПолучитьОбъектДрайвера(ДанныеОборудования);
	
	Возврат ОбъектДрайвера <> Неопределено;
	
КонецФункции

#Если Не ВебКлиент Тогда

// Установить или переустановить драйверы помеченные флагами.
//
Процедура ПереустановитьПомеченныеДрайверы() Экспорт
	
	МассивРабочихМест = МенеджерОборудованияКлиентПовтИсп.НайтиРабочиеМестаПоИД(МенеджерОборудованияКлиентСервер.ИдентификаторКлиентаДляРабочегоМеста());
	
	Если МассивРабочихМест.Количество() = 0 Тогда
		РабочееМесто = Неопределено
	Иначе
		РабочееМесто = МассивРабочихМест[0];
	КонецЕсли;
	
	// Переустановить драйверы помеченные флагом для переустановки.
	Если ЗначениеЗаполнено(РабочееМесто) Тогда
		СписокОборудования = МенеджерОборудованияВызовСервера.ДрайвераДляПереустановки(РабочееМесто);
		Для Каждого Оборудование Из СписокОборудования Цикл
			Если ЗначениеЗаполнено(Оборудование.ДанныеДрайвера) И Оборудование.ДанныеДрайвера.ВСоставеКонфигурации И НЕ Оборудование.ДанныеДрайвера.ПоставляетсяДистрибутивом Тогда
				НачатьУстановкуВнешнейКомпонентыИС(, "ОбщийМакет." + Оборудование.ДанныеДрайвера.ИмяМакетаДрайвера);
			КонецЕсли;
			МенеджерОборудованияВызовСервера.УстановитьПризнакПереустановкиДрайвера(РабочееМесто, Оборудование.ДрайверОборудования, Ложь); 
		КонецЦикла;
	КонецЕсли;
	

	// Установить драйверы помеченные флагом для установки.
	Если ЗначениеЗаполнено(РабочееМесто) Тогда
		СписокОборудования = МенеджерОборудованияВызовСервера.ДрайвераДляУстановки(РабочееМесто);
		Для Каждого Оборудование Из СписокОборудования Цикл
			Если Оборудование.ДанныеДрайвера.ВСоставеКонфигурации И НЕ Оборудование.ДанныеДрайвера.ПоставляетсяДистрибутивом Тогда
				ОбъектДрайвера = ПолучитьОбъектДрайвера(Оборудование.ДанныеДрайвера);
				Если ОбъектДрайвера = Неопределено Тогда
					НачатьУстановкуВнешнейКомпонентыИС(, "ОбщийМакет." + Оборудование.ДанныеДрайвера.ИмяМакетаДрайвера);
				Иначе
					ОтключитьОбъектДрайвера(Оборудование.ДанныеДрайвера);
				КонецЕсли;
			КонецЕсли;
			МенеджерОборудованияВызовСервера.УстановитьПризнакУстановкиДрайвера(РабочееМесто, Оборудование.ДрайверОборудования, Ложь); 
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьУстановкуДрайвераИзДистрибутиваЗавершение(Результат, Параметры) Экспорт
	
	Если Параметры.Свойство("ВременныйФайл") Тогда
		НачатьУдалениеФайловИС(, Параметры.ВременныйФайл);
	КонецЕсли;
	Если Параметры.Свойство("КаталогИнсталляции") Тогда
		НачатьУдалениеФайловИС(, Параметры.КаталогИнсталляции);
	КонецЕсли;
	
	Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
		ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, Результат = 0);
	КонецЕсли;
	
КонецПроцедуры

// Начать установку драйвера из дистрибутива поставщика из макета.
//
Процедура НачатьУстановкуДрайвераИзДистрибутиваВМакете(ОповещениеПриЗавершении, ИмяМакета, ИмяФайла) Экспорт
	
	Результат = Ложь;
	// Получение макета с сервера
	СсылкаНаФайл = МенеджерОборудованияВызовСервера.ПолучитьМакетССервера(ИмяМакета);
	ИмяФайлаВрем = ?(ПустаяСтрока(ИмяФайла), "setup.exe", ИмяФайла);
	
	// НачатьПолучениеКаталогаВременныхФайлов 
	ВременныйКаталог   = КаталогВременныхФайлов();
	ВременныйФайл      = ВременныйКаталог + "Model.zip";
	КаталогИнсталляции = ВременныйКаталог + "Model\";
	
	// Распаковка архива дистрибутива во временный каталог.
	Результат = ПолучитьФайл(СсылкаНаФайл, ВременныйФайл, Ложь);
	
	ФайлАрхива = Новый ЧтениеZipФайла();
	ФайлАрхива.Открыть(ВременныйФайл);
	
	Если ФайлАрхива.Элементы.Найти(ИмяФайлаВрем) <> Неопределено Тогда
		// Распаковка дистрибутива
		ФайлАрхива.ИзвлечьВсе(КаталогИнсталляции);
		ФайлАрхива.Закрыть();
		// Запуск инсталлятора
		Параметры = Новый Структура("КаталогИнсталляции, ВременныйФайл, ОповещениеПриЗавершении", КаталогИнсталляции, ВременныйФайл, ОповещениеПриЗавершении);
		Оповещение = ОписаниеОповещенияИС("НачатьУстановкуДрайвераИзДистрибутиваЗавершение", МенеджерОборудованияКлиент, Параметры);
		НачатьЗапускПриложенияИС(Оповещение, КаталогИнсталляции + ИмяФайлаВрем, КаталогИнсталляции, Истина);
	Иначе
		ТекстОшибки = НСтр("ru='Ошибка установки драйвера из дистрибутива в макете.
							|Файл ""%Файл%"" в макете не найден.'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Файл%", ИмяФайлаВрем);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки); 
		НачатьУдалениеФайловИС(, ВременныйФайл);
		Если ОповещениеПриЗавершении <> Неопределено Тогда
			ВыполнитьОбработкуОповещенияИС(ОповещениеПриЗавершении, Ложь);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьУстановкуДрайвераИзДистрибутиваИзБазыЗавершение(Результат, Параметры) Экспорт
	
	НачатьУдалениеФайловИС(, Параметры.ВременныйКаталог + "Model\");
	НачатьУдалениеФайловИС(, Параметры.ВременныйКаталог + "Model.zip");
	
	Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
		ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, Результат = 0);
	КонецЕсли;
	
КонецПроцедуры

// Начать установку драйвера из дистрибутива поставщика из базы.
//
Процедура НачатьУстановкуДрайвераИзДистрибутиваИзБазы(ОповещениеПриЗавершении, ДанныеДрайвера) Экспорт
	
	Результат = Ложь;
	
	ВременныйКаталог   = КаталогВременныхФайлов();
	ИмяФайлаВрем       = ВременныйКаталог + ДанныеДрайвера.ИмяФайлаДрайвера;
	КаталогИнсталляции = ВременныйКаталог + "Model\";
	
	ПолучитьФайл(ПолучитьНавигационнуюСсылку(ДанныеДрайвера.ДрайверОборудования, "ЗагруженныйДрайвер"), ИмяФайлаВрем, Ложь);
	ВременныйФайл = Новый Файл(ИмяФайлаВрем);
	
	Если ВРег(ВременныйФайл.Расширение) = ".ZIP" Тогда
		// Распаковка дистрибутива
		ФайлАрхива = Новый ЧтениеZipФайла();
		ФайлАрхива.Открыть(ВременныйФайл.ПолноеИмя);
		
		ИмяФайлаУстанавливаемый = "";
		Если ФайлАрхива.Элементы.Найти(ВременныйФайл.ИмяБезРасширения  + ".EXE") <> Неопределено Тогда
			ИмяФайлаУстанавливаемый = ВременныйФайл.ИмяБезРасширения  + ".EXE";
		ИначеЕсли ФайлАрхива.Элементы.Найти("setup.exe") <> Неопределено Тогда
			ИмяФайлаУстанавливаемый = "setup.exe";
		КонецЕсли;
		
		Если НЕ ПустаяСтрока(ИмяФайлаУстанавливаемый) Тогда
			// Распаковка дистрибутива
			ФайлАрхива.ИзвлечьВсе(КаталогИнсталляции);
			ФайлАрхива.Закрыть();
			// Запуск инсталлятора
			Параметры = Новый Структура("КаталогИнсталляции, ВременныйФайл, ОповещениеПриЗавершении", КаталогИнсталляции, ИмяФайлаВрем, ОповещениеПриЗавершении);
			Оповещение = ОписаниеОповещенияИС("НачатьУстановкуДрайвераИзДистрибутиваЗавершение", МенеджерОборудованияКлиент, Параметры);
			НачатьЗапускПриложенияИС(Оповещение, КаталогИнсталляции + ИмяФайлаУстанавливаемый, КаталогИнсталляции, Истина);
		Иначе
			ФайлАрхива.Закрыть();
			ТекстОшибки = НСтр("ru='Ошибка установки драйвера из дистрибутива в архиве.
						|Необходимый файл в архиве не найден.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки); 
			НачатьУдалениеФайловИС(, ИмяФайлаВрем);
		КонецЕсли;
	Иначе
		// Запуск инсталлятора
		Параметры = Новый Структура("ВременныйФайл, ОповещениеПриЗавершении", ИмяФайлаВрем, ОповещениеПриЗавершении);
		Оповещение = ОписаниеОповещенияИС("НачатьУстановкуДрайвераИзДистрибутиваЗавершение", МенеджерОборудованияКлиент, Параметры);
		НачатьЗапускПриложенияИС(Оповещение, ВременныйКаталог + ИмяФайлаВрем, ВременныйКаталог, Истина);
	КонецЕсли;
	
КонецПроцедуры

#Иначе

Процедура НачатьУстановкуРасширенияРаботыСФайламиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		НачатьУстановкуРасширенияРаботыСФайлами();
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьПодключениеРасширенияРаботыСФайламиЗавершение(Подключено, ДополнительныеПараметры) Экспорт
	
	Если Не Подключено И ДополнительныеПараметры.ПредлагатьУстановку Тогда
		Оповещение = ОписаниеОповещенияИС("НачатьУстановкуРасширенияРаботыСФайламиЗавершение", МенеджерОборудованияКлиент, ДополнительныеПараметры);
		ТекстСообщения = НСтр("ru='Для продолжении работы необходимо установить расширение для веб-клиента ""1С:Предприятие"". Установить?'");
		ПоказатьВопросИС(Оповещение, ТекстСообщения, РежимДиалогаВопрос.ДаНет); 
	КонецЕсли;
	
	Если ДополнительныеПараметры.ОповещениеПриЗавершении <> Неопределено Тогда
		ВыполнитьОбработкуОповещенияИС(ДополнительныеПараметры.ОповещениеПриЗавершении, Подключено);
	КонецЕсли
	
КонецПроцедуры

#КонецЕсли

// Проверить доступность расширения работы с Файлами.
// 
Процедура ПроверитьДоступностьРасширенияРаботыСФайлами(ОповещениеПриЗавершении, ПредлагатьУстановку = Истина) Экспорт
	
#Если Не ВебКлиент Тогда
	// В тонком и толстом клиентах расширение подключено всегда.
	ВыполнитьОбработкуОповещенияИС(ОповещениеПриЗавершении, Истина);
	Возврат;
#Иначе
	ДополнительныеПараметры = Новый Структура("ОповещениеПриЗавершении, ПредлагатьУстановку", ОповещениеПриЗавершении, ПредлагатьУстановку);
	Оповещение = ОписаниеОповещенияИС("НачатьПодключениеРасширенияРаботыСФайламиЗавершение", МенеджерОборудованияКлиент, ДополнительныеПараметры);
	НачатьПодключениеРасширенияРаботыСФайлами(Оповещение);
#КонецЕсли
	
КонецПроцедуры

// Отключение объекта драйвера.
//
Процедура ОтключитьОбъектДрайвера(ДанныеДрайвера) Экспорт

	НомерСтрокиМассива = глПодключаемоеОборудование.ПараметрыПодключенияПО.Найти(ДанныеДрайвера.ДрайверОборудования);
	Если НомерСтрокиМассива <> Неопределено Тогда
		глПодключаемоеОборудование.ПараметрыПодключенияПО.Удалить(НомерСтрокиМассива);
	КонецЕсли;
	
КонецПроцедуры

// Получение объекта драйвера
//
Функция ПолучитьОбъектДрайвера(ДанныеДрайвера, ТекстОшибки = Неопределено)
	
	ОбъектДрайвера = Неопределено;
	
	Для Каждого ДрайверПО Из глПодключаемоеОборудование.ДрайвераПодключаемогоОборудования Цикл
		Если ДрайверПО.Ключ = ДанныеДрайвера.ДрайверОборудования  Тогда
			ОбъектДрайвера = ДрайверПО.Значение;
			Прервать;
		КонецЕсли;
	КонецЦикла;   
	
	Если ОбъектДрайвера = Неопределено Тогда
		Попытка
			
			ProgID = ДанныеДрайвера.ИдентификаторОбъекта;
			Если ПустаяСтрока(ProgID) Тогда
				ОбъектДрайвера = ""; // Драйвер не требуется
			Иначе
				ProgID1 = ?(Найти(ProgID, "|") > 0, Сред(ProgID, 1, Найти(ProgID, "|")-1), ProgID); 
				ProgID2 = ?(Найти(ProgID, "|") > 0, Сред(ProgID, Найти(ProgID, "|")+1), ProgID); 
				Если ДанныеДрайвера.ПоставляетсяДистрибутивом Тогда
					ПодключитьВнешнююКомпоненту(ProgID1);
				Иначе
					ИмяОбъекта = Сред(ProgID1, Найти(ProgID1, ".") + 1); 
					Префикс = Сред(ProgID1, 1, Найти(ProgID1, ".")); 
					ProgID2 = Префикс + СтрЗаменить(ИмяОбъекта, ".", "_") + "." + ИмяОбъекта;
					Если ДанныеДрайвера.ВСоставеКонфигурации Тогда
						Результат = ПодключитьВнешнююКомпоненту("ОбщийМакет." + ДанныеДрайвера.ИмяМакетаДрайвера, СтрЗаменить(ИмяОбъекта, ".", "_"));
					Иначе
						СсылкаНаДрайвер = ПолучитьНавигационнуюСсылку(ДанныеДрайвера.ДрайверОборудования, "ЗагруженныйДрайвер");
						Результат = ПодключитьВнешнююКомпоненту(СсылкаНаДрайвер, СтрЗаменить(ИмяОбъекта, ".", "_"));
					КонецЕсли;
				КонецЕсли;
				ОбъектДрайвера = Новый (ProgID2);
			КонецЕсли;
				
		Исключение
			Инфо = ИнформацияОбОшибке();
			ТекстОшибки = Инфо.Описание;
		КонецПопытки;
		
		Если ОбъектДрайвера <> Неопределено Тогда
			глПодключаемоеОборудование.ДрайвераПодключаемогоОборудования.Вставить(ДанныеДрайвера.ДрайверОборудования, ОбъектДрайвера);
			ОбъектДрайвера = глПодключаемоеОборудование.ДрайвераПодключаемогоОборудования[ДанныеДрайвера.ДрайверОборудования];
		КонецЕсли;
		
	КонецЕсли;   
		
	Возврат ОбъектДрайвера;
	
КонецФункции

Процедура НачатьПолучениеОбъектаДрайвераЗавершение(Подключено, ДополнительныеПараметры) Экспорт
	
	ОбъектДрайвера = Неопределено;
	
	Если Подключено Тогда 
		Попытка
			ОбъектДрайвера = Новый (ДополнительныеПараметры.ProgID);
			Если ОбъектДрайвера <> Неопределено Тогда
				глПодключаемоеОборудование.ДрайвераПодключаемогоОборудования.Вставить(ДополнительныеПараметры.ДрайверОборудования, ОбъектДрайвера);
				ОбъектДрайвера = глПодключаемоеОборудование.ДрайвераПодключаемогоОборудования[ДополнительныеПараметры.ДрайверОборудования];
			КонецЕсли;
			ВыполнитьОбработкуОповещенияИС(ДополнительныеПараметры.ОповещениеПриЗавершении, ОбъектДрайвера);
			Возврат;
		Исключение
			ОбъектДрайвера = Неопределено;
		КонецПопытки;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещенияИС(ДополнительныеПараметры.ОповещениеПриЗавершении, Неопределено);
	
КонецПроцедуры

// Начать получение объекта драйвера.
//
Процедура НачатьПолучениеОбъектаДрайвера(ОповещениеПриЗавершении, ДанныеДрайвера) Экспорт
	
	ОбъектДрайвера = Неопределено;
	
	Для Каждого ДрайверПО Из глПодключаемоеОборудование.ДрайвераПодключаемогоОборудования Цикл
		Если ДрайверПО.Ключ = ДанныеДрайвера.ДрайверОборудования  Тогда
			ОбъектДрайвера = ДрайверПО.Значение;
			ВыполнитьОбработкуОповещенияИС(ОповещениеПриЗавершении, ОбъектДрайвера);
			Возврат;
		КонецЕсли;
	КонецЦикла;   
	
	Если ОбъектДрайвера = Неопределено Тогда
			ProgID = ДанныеДрайвера.ИдентификаторОбъекта;
			Если ПустаяСтрока(ProgID) Тогда
				ОбъектДрайвера = ""; // Драйвер не требуется
				ВыполнитьОбработкуОповещенияИС(ОповещениеПриЗавершении, ОбъектДрайвера);
			Иначе
				ProgID1 = ?(Найти(ProgID, "|") > 0, Сред(ProgID, 1, Найти(ProgID, "|")-1), ProgID); 
				ProgID2 = ?(Найти(ProgID, "|") > 0, Сред(ProgID, Найти(ProgID, "|")+1), ProgID); 
				
				Если ДанныеДрайвера.ПоставляетсяДистрибутивом Тогда
					Параметры = Новый Структура("ProgID, ОповещениеПриЗавершении, ДрайверОборудования", ProgID2, ОповещениеПриЗавершении, ДанныеДрайвера.ДрайверОборудования);
					Оповещение = ОписаниеОповещенияИС("НачатьПолучениеОбъектаДрайвераЗавершение", МенеджерОборудованияКлиент, Параметры);
					НачатьПодключениеВнешнейКомпоненты(Оповещение, ProgID1);
				Иначе
					ИмяОбъекта = Сред(ProgID1, Найти(ProgID1, ".") + 1); 
					Префикс = Сред(ProgID1, 1, Найти(ProgID1, ".")); 
					ProgID2 = Префикс + СтрЗаменить(ИмяОбъекта, ".", "_") + "." + ИмяОбъекта;
					
					Параметры = Новый Структура("ProgID, ОповещениеПриЗавершении, ДрайверОборудования", ProgID2, ОповещениеПриЗавершении, ДанныеДрайвера.ДрайверОборудования);
					Оповещение = Новый ОписаниеОповещения("НачатьПолучениеОбъектаДрайвераЗавершение", МенеджерОборудованияКлиент, Параметры);
					Если ДанныеДрайвера.ВСоставеКонфигурации Тогда
						НачатьПодключениеВнешнейКомпоненты(Оповещение, "ОбщийМакет." + ДанныеДрайвера.ИмяМакетаДрайвера, СтрЗаменить(ИмяОбъекта, ".", "_"));
					Иначе
						СсылкаНаДрайвер = ПолучитьНавигационнуюСсылку(ДанныеДрайвера.ДрайверОборудования, "ЗагруженныйДрайвер");
						НачатьПодключениеВнешнейКомпоненты(Оповещение, СсылкаНаДрайвер, СтрЗаменить(ИмяОбъекта, ".", "_"));
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
	КонецЕсли;   
	
КонецПроцедуры

// Установить драйвер оборудования.
//
Процедура УстановитьДрайвер(Идентификатор, ОповещениеИзДистрибутиваПриЗавершении = Неопределено, ОповещениеИзАрхиваПриЗавершении = Неопределено) Экспорт
	
	ДанныеДрайвера = МенеджерОборудованияВызовСервера.ПолучитьДанныеДрайвера(Идентификатор);
	
	Попытка  
		Если ДанныеДрайвера.ВСоставеКонфигурации Тогда
			
			Если ДанныеДрайвера.ПоставляетсяДистрибутивом Тогда
				ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru='Данный драйвер не может быть установлен из дистрибутива.'")); 
			Иначе
				НачатьУстановкуВнешнейКомпонентыИС(ОповещениеИзАрхиваПриЗавершении, "ОбщийМакет." + ДанныеДрайвера.ИмяМакетаДрайвера);
			КонецЕсли;
			
		Иначе
			
			Если ДанныеДрайвера.ПоставляетсяДистрибутивом Тогда
				ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru='Данный драйвер не может быть установлен и использован.'")); 
			Иначе
				СсылкаНаДрайвер = ПолучитьНавигационнуюСсылку(ДанныеДрайвера.ДрайверОборудования, "ЗагруженныйДрайвер");
				НачатьУстановкуВнешнейКомпонентыИС(ОповещениеИзАрхиваПриЗавершении, СсылкаНаДрайвер);
			КонецЕсли;
			
		КонецЕсли;
		
	Исключение
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru='Произошла ошибка при установке драйвера.'")); 
	КонецПопытки;  
		
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииДляРаботыСПодключаемымОборудованием

// Завершение выполнение дополнительной команды к драйверу.
// Не требует предварительного подключения устройства в системе.
//
// Параметры:
// 	ОбъектДрайвера - СправочникСсылка.ДрайверыОборудования - .
// 	ПараметрыКоманды - Структура - где:
// 	*Параметры - Структура - .
Процедура НачатьВыполнениеДополнительнойКоманды_Завершение(ОбъектДрайвера, ПараметрыКоманды) Экспорт
	
	Если ОбъектДрайвера = Неопределено Или ПустаяСтрока(ОбъектДрайвера) Тогда
		// Сообщить об ошибке, что не удалось загрузить драйвер.
		ТекстОшибки = НСтр("ru='Не удалось загрузить драйвер устройства.
								|Проверьте, что драйвер корректно установлен и зарегистрирован в системе.'");
		ВыходныеПараметры = Новый Массив();
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(ТекстОшибки);
		ВыходныеПараметры.Добавить(НСтр("ru='Не установлен'"));
		РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ТекстОшибки); 
		РезультатВыполнения.ВыходныеПараметры = ВыходныеПараметры;
		ВыполнитьОбработкуОповещенияИС(ПараметрыКоманды.ОповещениеПриЗавершении, РезультатВыполнения);
	Иначе
		
		ДанныеОборудования = ПараметрыКоманды.ДанныеОборудования; 
		ОбработчикДрайвераМодуль = МенеджерОборудованияКлиентПовтИсп.ПолучитьОбработчикДрайвера(ДанныеОборудования.ОбработчикДрайвера, Не ДанныеОборудования.ВСоставеКонфигурации, ДанныеОборудования.ТипОборудованияИмя);
		
		Если ОбработчикДрайвераМодуль = Неопределено Тогда
			// Сообщить об ошибке, что не удалось подключить обработчик драйвера.
			ТекстОшибки = НСтр("ru='Не удалось подключить обработчик драйвера.'");
			ВыходныеПараметры = Новый Массив();
			ВыходныеПараметры.Добавить(999);
			ВыходныеПараметры.Добавить(ТекстОшибки);
			ВыходныеПараметры.Добавить(НСтр("ru='Не установлен'"));
			РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ТекстОшибки); 
			РезультатВыполнения.ВыходныеПараметры = ВыходныеПараметры;
			ВыполнитьОбработкуОповещенияИС(ПараметрыКоманды.ОповещениеПриЗавершении, РезультатВыполнения);
		Иначе
			времПараметрыПодключения = Новый Структура();
			времПараметрыПодключения.Вставить("ТипОборудования", ПараметрыКоманды.ДанныеОборудования.ТипОборудованияИмя);
			
			Если ОбработчикДрайвераМодуль.ПоддержкаАсинхронногоРежима() Тогда
				ОбработчикДрайвераМодуль.НачатьВыполнениеКоманды(ПараметрыКоманды.ОповещениеПриЗавершении, ПараметрыКоманды.Команда, ПараметрыКоманды.ВходныеПараметры,
					ОбъектДрайвера, ПараметрыКоманды.Параметры, времПараметрыПодключения);
			Иначе
				ТекстОшибки = "";
				ВыходныеПараметры = Неопределено;
				Результат = ОбработчикДрайвераМодуль.ВыполнитьКоманду(ПараметрыКоманды.Команда, ПараметрыКоманды.ВходныеПараметры,
					ВыходныеПараметры, ОбъектДрайвера, ПараметрыКоманды.Параметры, времПараметрыПодключения);
				Если Не Результат Тогда
					Если ВыходныеПараметры.Количество() >= 2 Тогда
						ТекстОшибки = ВыходныеПараметры[1];
					КонецЕсли;
					ВыходныеПараметры.Добавить(НСтр("ru='Установлен'"));
				КонецЕсли;
				РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Результат); 
				РезультатВыполнения.ВыходныеПараметры = ВыходныеПараметры;
				ВыполнитьОбработкуОповещенияИС(ПараметрыКоманды.ОповещениеПриЗавершении, РезультатВыполнения);
			КонецЕсли;
		КонецЕсли
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииРаботыСФискальнымиУстройствами

Процедура НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Параметры) Экспорт
	
	Если ИдентификаторУстройства = Неопределено Тогда
		Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
			ОписаниеОшибки = НСтр("ru='Фискальное устройство не выбрано или не подключено.'");
			РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки);
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
		КонецЕсли;
	Иначе
		// Возвращаем сохраненные ранее параметры регистрации устройства. 
		Если Параметры.ВыполняемаяКоманда = "GetDataKKT"  И (Параметры.Свойство("ИспользоватьСохраненныеЗначения") И Параметры.ИспользоватьСохраненныеЗначения) Тогда
			ПараметрыРегистрации = МенеджерОборудованияВызовСервера.ПолучитьПараметрыРегистрацииУстройства(ИдентификаторУстройства);
			Если ПараметрыРегистрации.ЕстьДанные Тогда
				РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Истина);
				РезультатВыполнения.Вставить("ВыходныеПараметры", ПараметрыРегистрации);
				ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатВыполнения);
				Возврат;
			КонецЕсли
		КонецЕсли;
		
		Параметры.Вставить("ИдентификаторУстройства", ИдентификаторУстройства);
		
		ТребуетсяПроверкаАктивностиСмены = Параметры.Свойство("ПроверкаАктивностиСмены") И Параметры.ПроверкаАктивностиСмены;
		Если ТребуетсяПроверкаАктивностиСмены Тогда
			
			СтатусПоследнейСмены = МенеджерОборудованияВызовСервера.ПолучитьСтатусПоследнейСмены(ИдентификаторУстройства);
			Если Не СтатусПоследнейСмены.Активна = Истина Тогда
				ОписаниеОшибки = НСтр("ru='Кассовая смена не открыта или истекла.'");
				РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки);
				ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
				Возврат;
			Иначе
				Если Параметры.ВыполняемаяКоманда = "CheckFiscalization" Тогда
					Параметры.ВходныеПараметры.НомерЧека = СтатусПоследнейСмены.ТекущийНомерЧека;
					Параметры.ВходныеПараметры = МенеджерОборудованияВызовСервера.СформироватьШаблонЧека(Параметры.ВходныеПараметры, , ИдентификаторУстройства);
					ОсновныеПараметры = Параметры.ВходныеПараметры;
					// Проверка обязательности реквизитов 
					ОписаниеОшибки = "";
					Если НЕ МенеджерОборудованияВызовСервера.ВыполненаПроверкаОбязательностиИПравильностиЗаполненияТэгов(ОсновныеПараметры, ИдентификаторУстройства, ОписаниеОшибки) Тогда
						РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки);
						ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
						Возврат;
					КонецЕсли;
					Отказ = Ложь;
					ИсправленыОсновныеПараметры = Ложь;
					МенеджерОборудованияВызовСервера.ПривестиДанныеКТребуемомуФормату(ОсновныеПараметры, Отказ, ОписаниеОшибки, ИсправленыОсновныеПараметры);
					Если Отказ Тогда
						РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки);
						ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
						Возврат;
					ИначеЕсли ИсправленыОсновныеПараметры Тогда
						Параметры.ВходныеПараметры = ОсновныеПараметры;
					КонецЕсли;
					Параметры.ВходныеПараметры.СистемаНалогообложения = ОсновныеПараметры.СистемаНалогообложения;
					
				КонецЕсли;
				Параметры.Вставить("КассоваяСмена", СтатусПоследнейСмены.КассоваяСмена);
				
			КонецЕсли;
		КонецЕсли;
		
		Если Параметры.ВыполняемаяКоманда = "OpenShift" Тогда
			ТребуетсяЗакрытиеСмены = МенеджерОборудованияВызовСервера.ТребуетсяЗакрытиеСмены(ИдентификаторУстройства);
			Если ТребуетсяЗакрытиеСмены Тогда
				ОповещениеПриЗавершении = ОписаниеОповещенияИС("НачатьВыполнениеКоманды_ПослеПроверкиСтатуса", МенеджерОборудованияКлиент, Параметры);
				МенеджерОборудованияКлиент.НачатьПолучениеТекущегоСостоянияФискальногоУстройства(ОповещениеПриЗавершении, Параметры.УникальныйИдентификатор, ИдентификаторУстройства);
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		Сессия = СессияПроверкиКодовМаркировки(Параметры.ИдентификаторУстройства);
		Если Сессия = Неопределено Тогда
			ОписаниеОповещения = Новый ОписаниеОповещения("НачатьВыполнениеКоманды_ПодключениеЗавершение", МенеджерОборудованияКлиент, Параметры);
			НачатьПодключениеОборудованиеПоИдентификатору(ОписаниеОповещения, Параметры.УникальныйИдентификатор, ИдентификаторУстройства);
		Иначе
			РезультатПодключения = Новый Структура("Результат", Истина);
			НачатьВыполнениеКоманды_ПодключениеЗавершение(РезультатПодключения, Параметры)
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьВыполнениеКоманды_ПослеПроверкиСтатуса(Результат, Параметры) Экспорт
	
	Если Результат.Результат Тогда
		
		СтатусСмены = Результат.ВыходныеПараметры[2];
		Если МенеджерОборудованияКлиентСервер.ТребуетсяЗакрытиеСмены(СтатусСмены) Тогда
			ТекстВопроса = НСтр("ru = 'На фискальном устройстве открыта кассовая смена, данные о которой отсутствуют в системе.
									|Выполнить закрытие смены?'");
			ОписаниеОповещения = ОписаниеОповещенияИС("НачатьВыполнениеКоманды_ПослеПодтвержденияЗакрытияСмены", МенеджерОборудованияКлиент, Параметры);
			ПоказатьВопросИС(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Иначе
			ОписаниеОповещения = ОписаниеОповещенияИС("НачатьВыполнениеКоманды_ПодключениеЗавершение", МенеджерОборудованияКлиент, Параметры);
			НачатьПодключениеОборудованиеПоИдентификатору(ОписаниеОповещения, Параметры.УникальныйИдентификатор, Параметры.ИдентификаторУстройства);
		КонецЕсли;
		
	Иначе
		ОписаниеОповещения = ОписаниеОповещенияИС("НачатьВыполнениеКоманды_ПодключениеЗавершение", МенеджерОборудованияКлиент, Параметры);
		НачатьПодключениеОборудованиеПоИдентификатору(ОписаниеОповещения, Параметры.УникальныйИдентификатор, Параметры.ИдентификаторУстройства);
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьВыполнениеКоманды_ПослеПодтвержденияЗакрытияСмены(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		НоваяКассоваяСмена = МенеджерОборудованияВызовСервера.НоваяКассоваяСмена(Параметры.ИдентификаторУстройства);
		ОповещениеВыполнения = ОписаниеОповещенияИС("НачатьВыполнениеКоманды_ПослеЗакрытияСмены", МенеджерОборудованияКлиент, Параметры);
		МенеджерОборудованияКлиент.НачатьЗакрытиеСменыНаФискальномУстройстве(ОповещениеВыполнения, Параметры.УникальныйИдентификатор, Параметры.ВходныеПараметры, Параметры.ИдентификаторУстройства,, НоваяКассоваяСмена);
		
	Иначе
		ТекстСообщения = НСтр("ru = 'На фискальном устройстве смена не закрыта'");
		РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ТекстСообщения);
		ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьВыполнениеКоманды_ПослеЗакрытияСмены(Результат, Параметры) Экспорт
	
	Если Результат.Результат Тогда
		ОписаниеОповещения = ОписаниеОповещенияИС("НачатьВыполнениеКоманды_ПодключениеЗавершение", МенеджерОборудованияКлиент, Параметры);
		НачатьПодключениеОборудованиеПоИдентификатору(ОписаниеОповещения, Параметры.УникальныйИдентификатор, Параметры.ИдентификаторУстройства);
	КонецЕсли;
	
КонецПроцедуры

// Описание процедуры выполнения команды.
// 
// Параметры:
// 	РезультатВыполнения - Структура - содержит параметры:
// 	*ПодключенноеУстройство - СправочникСсылка.ПодключаемоеОборудование - .
// 	*ДополнительныеПараметры - Структура - структура дополнительных параметров.
// 	Параметры - Структура - содержит параметры:
// 	*ДополнительныеПараметры - Структура - структура дополнительных параметров.
//
Процедура НачатьВыполнениеКоманды_ВыполнитьКомандуЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если РезультатВыполнения.Результат Тогда
		
		ТекстСообщения = НСтр("ru='Ошибок нет.'");
		
		Если МенеджерОборудованияКлиентСервер.ТребуетсяВызовСобытияПослеВыполненияКомандыФискальнымУстройством(Параметры) Тогда
			
			СтруктураПараметров = Новый Структура();
			
			Если (РезультатВыполнения.ВыходныеПараметры.Количество() > 4) И РезультатВыполнения.ВыходныеПараметры[4] <> Неопределено Тогда
				СтруктураПараметров.Вставить("ФискальныеДанныеСтруктура", РезультатВыполнения.ВыходныеПараметры[4]);
			КонецЕсли;
			Если (РезультатВыполнения.ВыходныеПараметры.Количество() > 5) И РезультатВыполнения.ВыходныеПараметры[5] <> Неопределено Тогда
				СтруктураПараметров.Вставить("ФискальныеДанныеXMLСтрока", РезультатВыполнения.ВыходныеПараметры[5]);
			КонецЕсли;
			СтруктураПараметров.Вставить("ИдентификаторУстройства", Параметры.ИдентификаторУстройства);
			Если Параметры.Свойство("КассоваяСмена") Тогда
				СтруктураПараметров.Вставить("КассоваяСмена", Параметры.КассоваяСмена);
			КонецЕсли;
			СтруктураПараметров.Вставить("ВыполняемаяКоманда", Параметры.ВыполняемаяКоманда);
			Если Параметры.Свойство("ДополнительныеПараметры") Тогда
				СтруктураПараметров.Вставить("ДополнительныеПараметры", Параметры.ДополнительныеПараметры);
			КонецЕсли;
			
			МенеджерОборудованияВызовСервера.ПослеВыполненияКомандыФискальнымУстройством(СтруктураПараметров);
			
		КонецЕсли;
		
		Если Параметры.ВыполняемаяКоманда = "CheckFiscalization" И МенеджерОборудованияКлиентПовтИсп.ИспользуетсяПодсистемыФискальныхУстройств() Тогда
			
			МенеджерОборудованияВызовСервера.ИнкрементироватьТекущийНомерЧекаККТ(Параметры.ИдентификаторУстройства, Параметры.КассоваяСмена);
			
			ВремВходныеПараметры = Параметры.ВходныеПараметры; 
			ВремВыходныеПараметры = Неопределено;
			Если ВремВходныеПараметры.НефискальныеДокументы.Количество() > 0 И РезультатВыполнения.Свойство("ОбработчикДрайвераМодуль") Тогда
				ОбработчикДрайвераМодуль = РезультатВыполнения.ОбработчикДрайвераМодуль;
				ОбъектДрайвера           = РезультатВыполнения.ОбъектДрайвера;
				ПодключенноеУстройство   = РезультатВыполнения.ПодключенноеУстройство;
				ПоддержкаАсинхронногоРежима = ОбработчикДрайвераМодуль.ПоддержкаАсинхронногоРежима();
				
				ВходныеПараметрыОперации = Новый Массив();
				ВходныеПараметрыОперации.Добавить(ВремВходныеПараметры.НефискальныеДокументы);
				
				Если ПоддержкаАсинхронногоРежима Тогда
					ОбработчикДрайвераМодуль.НачатьВыполнениеКоманды(Неопределено, "PrintTextDocument", ВходныеПараметрыОперации, 
						ОбъектДрайвера, ПодключенноеУстройство.Параметры, ПодключенноеУстройство.ПараметрыПодключения);
				Иначе
					ОбработчикДрайвераМодуль.ВыполнитьКоманду("PrintTextDocument", ВходныеПараметрыОперации, ВремВыходныеПараметры,
						ОбъектДрайвера, ПодключенноеУстройство.Параметры, ПодключенноеУстройство.ПараметрыПодключения);
				КонецЕсли;
				
				МенеджерОборудованияКлиентПереопределяемый.НачатьРассылкуНефискальныхДокументов(Параметры);
				
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		ТекстСообщения = НСтр("ru='При выполнении операции произошла ошибка: %ДополнительноеОписание%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", РезультатВыполнения.ВыходныеПараметры[1]);
	КонецЕсли;
	
	Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
		РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(РезультатВыполнения.Результат, ТекстСообщения, Параметры.ИдентификаторУстройства);
		РезультатОперации.Вставить("ВыходныеПараметры", РезультатВыполнения.ВыходныеПараметры);
		
		Если РезультатВыполнения.Свойство("ДополнительныеПараметры") Тогда
			РезультатОперации.Вставить("ДополнительныеПараметры", РезультатВыполнения.ДополнительныеПараметры);
		КонецЕсли;
		
		ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
	КонецЕсли;
	
	Сессия = СессияПроверкиКодовМаркировки(Параметры.ИдентификаторУстройства);
	Если Сессия = Неопределено Тогда
		НачатьОтключениеОборудованиеПоИдентификатору(Неопределено, Параметры.УникальныйИдентификатор, Параметры.ИдентификаторУстройства);
	КонецЕсли;
	
КонецПроцедуры

// Начать формирование отчета о текущем состоянии расчетов на фискальном устройстве.
// УникальныйИдентификатор - идентификатор формы.
// ИдентификаторУстройства - идентификатор устройства, если неопределенно - будет предложен выбор.
// ОповещениеПриЗавершении - оповещение при завершении завершении.
//
Процедура НачатьФормированиеОтчетаОТекущемСостоянииРасчетов(ОповещениеПриЗавершении, УникальныйИдентификатор, ПараметрыОперации, ИдентификаторУстройства = Неопределено, ТипОборудования = Неопределено) Экспорт
	
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеПриЗавершении" , ОповещениеПриЗавершении);
	Контекст.Вставить("УникальныйИдентификатор" , УникальныйИдентификатор);
	Контекст.Вставить("ВходныеПараметры"        , ПараметрыОперации);
	Контекст.Вставить("ВыполняемаяКоманда"      , "ReportCurrentStatusOfSettlements");
	
	Если ТипОборудования = Неопределено Тогда
		ПоддерживаемыеТипыВО = Новый Массив();
		ПоддерживаемыеТипыВО.Добавить("ФискальныйРегистратор");
		ПоддерживаемыеТипыВО.Добавить("ПринтерЧеков");
		ПоддерживаемыеТипыВО.Добавить("ККТ");
	Иначе
		ПоддерживаемыеТипыВО = ТипОборудования;
	КонецЕсли;
	
	Если ИдентификаторУстройства = Неопределено Тогда
		ОписаниеОповещения = ОписаниеОповещенияИС("НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение", МенеджерОборудованияКлиент, Контекст);
		ПредложитьВыбратьУстройство(ОписаниеОповещения, ПоддерживаемыеТипыВО,
			НСтр("ru='Выберите фискальное устройство'"),
			НСтр("ru='Фискальное устройство не подключено.'"), 
			НСтр("ru='Фискальное устройство не выбрано.'"),
			Истина);
	Иначе
		НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Контекст);
	КонецЕсли;
	
КонецПроцедуры

// Начать формирование отчета отчет без гашения  на фискальном устройстве.
// УникальныйИдентификатор - идентификатор формы.
// ИдентификаторУстройства - идентификатор устройства, если неопределенно - будет предложен выбор.
// ОповещениеПриЗавершении - оповещение при завершении завершении.
//
Процедура НачатьФормированиеОтчетаБезГашения(ОповещениеПриЗавершении, УникальныйИдентификатор, ПараметрыОперации, ИдентификаторУстройства = Неопределено, ТипОборудования = Неопределено) Экспорт
	                     
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеПриЗавершении" , ОповещениеПриЗавершении);
	Контекст.Вставить("УникальныйИдентификатор" , УникальныйИдентификатор);
	Контекст.Вставить("ВходныеПараметры"        , ПараметрыОперации);
	Контекст.Вставить("ВыполняемаяКоманда"      , "PrintXReport");
	
	Если ТипОборудования = Неопределено Тогда
		ПоддерживаемыеТипыВО = Новый Массив();
		ПоддерживаемыеТипыВО.Добавить("ФискальныйРегистратор");
		ПоддерживаемыеТипыВО.Добавить("ПринтерЧеков");
		ПоддерживаемыеТипыВО.Добавить("ККТ");
	Иначе
		ПоддерживаемыеТипыВО = ТипОборудования;
	КонецЕсли;
	
	Если ИдентификаторУстройства = Неопределено Тогда
		ОписаниеОповещения = ОписаниеОповещенияИС("НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение", МенеджерОборудованияКлиент, Контекст);
		ПредложитьВыбратьУстройство(ОписаниеОповещения, ПоддерживаемыеТипыВО,
			НСтр("ru='Выберите фискальное устройство'"),
			НСтр("ru='Фискальное устройство не подключено.'"), 
			НСтр("ru='Фискальное устройство не выбрано.'"),
			Истина);
	Иначе
		НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Контекст);
	КонецЕсли;
	
КонецПроцедуры

// Начать открытие смены на фискальном устройстве.
// УникальныйИдентификатор - идентификатор формы.
// ИдентификаторУстройства - идентификатор устройства, если неопределенно - будет предложен выбор.
// ОповещениеПриЗавершении - оповещение при завершении завершении.
//
Процедура НачатьОткрытиеСменыНаФискальномУстройстве(ОповещениеПриЗавершении, УникальныйИдентификатор, ПараметрыОперации, ИдентификаторУстройства = Неопределено, ТипОборудования = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
	Контекст.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
	Контекст.Вставить("ВходныеПараметры"       , ПараметрыОперации);
	Контекст.Вставить("ВыполняемаяКоманда"     , "OpenShift");
	Контекст.Вставить("ДополнительныеПараметры", ДополнительныеПараметры);
	Контекст.Вставить("КассаККМ"               , ПараметрыОперации.КассаККМ);
	
	Если ТипОборудования = Неопределено Тогда
		ПоддерживаемыеТипыВО = Новый Массив();
		ПоддерживаемыеТипыВО.Добавить("ФискальныйРегистратор");
		ПоддерживаемыеТипыВО.Добавить("ПринтерЧеков");
		ПоддерживаемыеТипыВО.Добавить("ККТ");
	Иначе
		ПоддерживаемыеТипыВО = ТипОборудования;
	КонецЕсли;
	
	Если ИдентификаторУстройства = Неопределено Тогда
		ОписаниеОповещения = ОписаниеОповещенияИС("НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение", МенеджерОборудованияКлиент, Контекст);
		ПредложитьВыбратьУстройство(ОписаниеОповещения, ПоддерживаемыеТипыВО,
			НСтр("ru='Выберите фискальное устройство'"), 
			НСтр("ru='Фискальное устройство не подключено.'"), 
			НСтр("ru='Фискальное устройство не выбрано.'"), 
			Истина);
	Иначе
		НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Контекст);
	КонецЕсли;
	
КонецПроцедуры

// Начать закрытие смены на фискальном устройстве.
// УникальныйИдентификатор - идентификатор формы.
// ИдентификаторУстройства - идентификатор устройства, если неопределенно - будет предложен выбор.
// ОповещениеПриЗавершении - оповещение при завершении завершении.
//
Процедура НачатьЗакрытиеСменыНаФискальномУстройстве(ОповещениеПриЗавершении, УникальныйИдентификатор, ПараметрыОперации, ИдентификаторУстройства = Неопределено, ТипОборудования = Неопределено, КассоваяСмена = Неопределено) Экспорт
	
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеПриЗавершении" , ОповещениеПриЗавершении);
	Контекст.Вставить("УникальныйИдентификатор" , УникальныйИдентификатор);
	Контекст.Вставить("ВходныеПараметры"        , ПараметрыОперации);
	Контекст.Вставить("ВыполняемаяКоманда"      , "CloseShift");
	
	Если ТипОборудования = Неопределено Тогда
		ПоддерживаемыеТипыВО = Новый Массив();
		ПоддерживаемыеТипыВО.Добавить("ФискальныйРегистратор");
		ПоддерживаемыеТипыВО.Добавить("ПринтерЧеков");
		ПоддерживаемыеТипыВО.Добавить("ККТ");
	Иначе
		ПоддерживаемыеТипыВО = ТипОборудования;
	КонецЕсли;
	
	Если КассоваяСмена = Неопределено Тогда
		
		Если ИдентификаторУстройства = Неопределено Тогда
			
			СтруктураОтбора = Новый Структура();
			СтруктураОтбора.Вставить("Статус", ПредопределенноеЗначение("Перечисление.СтатусыКассовойСмены.Открыта"));
			
			СтруктураРезультата = Новый Структура();
			СтруктураРезультата.Вставить("КассоваяСмена", "Ссылка");
			СтруктураРезультата.Вставить("ИдентификаторУстройства", "ФискальноеУстройство");
			
			ПараметрыВыбора = Новый Структура();
			ПараметрыВыбора.Вставить("СтруктураПараметрыОтбора", СтруктураОтбора);
			ПараметрыВыбора.Вставить("СтруктураРезультата", СтруктураРезультата);
			ПараметрыВыбора.Вставить("Заголовок", НСтр("ru = 'Выберите кассовую смену для закрытия'"));
			
			ОписаниеОповещения = ОписаниеОповещенияИС("НачатьВыполнениеКоманды_ВыбратьСменуЗавершение", МенеджерОборудованияКлиент, Контекст);
			
			ИмяФормы = "Документ.КассоваяСмена.ФормаВыбора";
			ОткрытьФормуИС(ИмяФормы, ПараметрыВыбора,,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
		Иначе
			
			ОписаниеПоследнейСмены = МенеджерОборудованияВызовСервера.ОписаниеПоследнейКассовойСмены(ИдентификаторУстройства);
			Если ОписаниеПоследнейСмены = Неопределено Тогда
				Если ОповещениеПриЗавершении <> Неопределено Тогда
					ТекстСообщения = НСтр("ru = 'Нет кассовых смен для устройства'") + Символы.НПП + Строка(ИдентификаторУстройства);
					РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ТекстСообщения, ИдентификаторУстройства);
					ВыполнитьОбработкуОповещенияИС(ОповещениеПриЗавершении, РезультатОперации);
				КонецЕсли;
			Иначе
				Если ОписаниеПоследнейСмены.Статус = ПредопределенноеЗначение("Перечисление.СтатусыКассовойСмены.Открыта") Тогда
					Контекст.Вставить("КассоваяСмена", ОписаниеПоследнейСмены.КассоваяСмена);
					СтруктураПараметровСмены = Новый Структура();
					СтруктураПараметровСмены.Вставить("КассоваяСмена", ОписаниеПоследнейСмены.КассоваяСмена);
					СтруктураПараметровСмены.Вставить("ИдентификаторУстройства", ИдентификаторУстройства);
					НачатьВыполнениеКоманды_ВыбратьСменуЗавершение(СтруктураПараметровСмены, Контекст);
				Иначе
					Если ОповещениеПриЗавершении <> Неопределено Тогда
						ТекстСообщения = НСтр("ru = 'Кассовая смена была закрыта ранее'");
						РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ТекстСообщения, ИдентификаторУстройства);
						ВыполнитьОбработкуОповещенияИС(ОповещениеПриЗавершении, РезультатОперации);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
	Иначе
		Контекст.Вставить("КассоваяСмена", КассоваяСмена);
		СтруктураПараметровСмены = Новый Структура();
		СтруктураПараметровСмены.Вставить("КассоваяСмена", КассоваяСмена);
		СтруктураПараметровСмены.Вставить("ИдентификаторУстройства", ИдентификаторУстройства);
		НачатьВыполнениеКоманды_ВыбратьСменуЗавершение(СтруктураПараметровСмены, Контекст);
	КонецЕсли;
	
КонецПроцедуры

// Производить инкассацию на фискальном устройстве.
// УникальныйИдентификатор - идентификатор формы.
// ИдентификаторУстройства - идентификатор устройства, если неопределенно - будет предложен выбор.
// ОповещениеПриЗавершении - оповещение при завершении завершении.
//
Процедура НачатьИнкассациюНаФискальномУстройстве(ОповещениеПриЗавершении, УникальныйИдентификатор, ПараметрыОперации, ИдентификаторУстройства = Неопределено, ТипОборудования = Неопределено) Экспорт
	 
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеПриЗавершении" , ОповещениеПриЗавершении);
	Контекст.Вставить("УникальныйИдентификатор" , УникальныйИдентификатор);
	Контекст.Вставить("ВходныеПараметры"        , ПараметрыОперации);
	Контекст.Вставить("ВыполняемаяКоманда"      , "Encash");
	Контекст.Вставить("ПроверкаАктивностиСмены" , Истина); 
	
	Если ТипОборудования = Неопределено Тогда
		ПоддерживаемыеТипыВО = Новый Массив();
		ПоддерживаемыеТипыВО.Добавить("ФискальныйРегистратор");
		ПоддерживаемыеТипыВО.Добавить("ПринтерЧеков");
		ПоддерживаемыеТипыВО.Добавить("ККТ");
	Иначе
		ПоддерживаемыеТипыВО = ТипОборудования;
	КонецЕсли;
	
	Если ИдентификаторУстройства = Неопределено Тогда
		ОписаниеОповещения = ОписаниеОповещенияИС("НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение", МенеджерОборудованияКлиент, Контекст);
		ПредложитьВыбратьУстройство(ОписаниеОповещения, ПоддерживаемыеТипыВО,
			НСтр("ru='Выберите фискальное устройство'"), 
			НСтр("ru='Фискальное устройство не подключено.'"), 
			НСтр("ru='Фискальное устройство не выбрано.'"),
			Истина);
	Иначе
		НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Контекст);
	КонецЕсли;
	
КонецПроцедуры

// Процедура получает ширину строки в символах.
//  
Процедура НачатьПолучениеШириныСтрокиПечатающегоУстройства(ОповещениеПриЗавершении, УникальныйИдентификатор, ИдентификаторУстройства = Неопределено, ТипОборудования = Неопределено) Экспорт
	
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеПриЗавершении" , ОповещениеПриЗавершении);
	Контекст.Вставить("УникальныйИдентификатор" , УникальныйИдентификатор);
	Контекст.Вставить("ВходныеПараметры"        , Неопределено);
	Контекст.Вставить("ВыполняемаяКоманда"      , "GetLineLength");
	
	Если ТипОборудования = Неопределено Тогда
		ПоддерживаемыеТипыВО = Новый Массив();
		ПоддерживаемыеТипыВО.Добавить("ФискальныйРегистратор");
		ПоддерживаемыеТипыВО.Добавить("ПринтерЧеков");
		ПоддерживаемыеТипыВО.Добавить("ККТ");
	Иначе
		ПоддерживаемыеТипыВО = ТипОборудования;
	КонецЕсли;
	
	Если ИдентификаторУстройства = Неопределено Тогда
		ОписаниеОповещения = ОписаниеОповещенияИС("НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение", МенеджерОборудованияКлиент, Контекст);
		ПредложитьВыбратьУстройство(ОписаниеОповещения, ПоддерживаемыеТипыВО,
			НСтр("ru='Выберите фискальное устройство'"), 
			НСтр("ru='Фискальное устройство не подключено.'"), 
			НСтр("ru='Фискальное устройство не выбрано.'"),
			Истина);
	Иначе
		НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Контекст);
	КонецЕсли;
	
КонецПроцедуры

// Производить печать чека на фискальном устройстве.
// УникальныйИдентификатор - идентификатор формы.
// ИдентификаторУстройства - идентификатор устройства, если неопределенно - будет предложен выбор.
// ОповещениеПриЗавершении - оповещение при завершении завершении.
//
Процедура НачатьФискализациюЧекаНаФискальномУстройстве(ОповещениеПриЗавершении, УникальныйИдентификатор, ПараметрыОперации, ИдентификаторУстройства = Неопределено,
	ТипОборудования = Неопределено, ОповещениеПослеОткрытииЧека = Неопределено, ОповещениеПриОшибкеПечатиЧека = Неопределено) Экспорт
	
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеПриЗавершении"    , ОповещениеПриЗавершении);
	Контекст.Вставить("УникальныйИдентификатор"    , УникальныйИдентификатор);
	Контекст.Вставить("УникальныйИдентификатор"    , УникальныйИдентификатор);
	Контекст.Вставить("ВходныеПараметры"           , ПараметрыОперации);
	Контекст.Вставить("ВыполняемаяКоманда"         , "CheckFiscalization");
	Контекст.Вставить("ОповещениеПослеОткрытииЧека", ОповещениеПослеОткрытииЧека);
	Контекст.Вставить("ОповещениеПриОшибке"        , ОповещениеПриОшибкеПечатиЧека); 
	Контекст.Вставить("ПроверкаАктивностиСмены"    , Истина); 
	
	Если ИдентификаторУстройства = Неопределено Тогда
		
		Если ТипОборудования = Неопределено Тогда
			ПоддерживаемыеТипыВО = Новый Массив();
			ПоддерживаемыеТипыВО.Добавить("ФискальныйРегистратор");
			ПоддерживаемыеТипыВО.Добавить("ПринтерЧеков");
			ПоддерживаемыеТипыВО.Добавить("ККТ");
		Иначе
			ПоддерживаемыеТипыВО = ТипОборудования;
		КонецЕсли;
		
		ОписаниеОповещения = ОписаниеОповещенияИС("НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение", МенеджерОборудованияКлиент, Контекст);
		ПредложитьВыбратьУстройство(ОписаниеОповещения, ПоддерживаемыеТипыВО,
			НСтр("ru='Выберите фискальное устройство'"),
			НСтр("ru='Фискальное устройство не подключено.'"), 
			НСтр("ru='Фискальное устройство не выбрано.'"),
			Истина);
	Иначе
		НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Контекст);
	КонецЕсли;
	
КонецПроцедуры

// Производить печать чека на фискальном устройстве пакетом.
// УникальныйИдентификатор - идентификатор формы.
// ИдентификаторУстройства - идентификатор устройства, если неопределенно - будет предложен выбор.
// ОповещениеПриЗавершении - оповещение при завершении завершении.
//
Процедура НачатьФискализациюЧекаНаФискальномУстройствеПакетом(ОповещениеПриЗавершении, УникальныйИдентификатор, ПараметрыОперации, ИдентификаторУстройства = Неопределено, ТипОборудования = Неопределено) Экспорт
	                     
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеПриЗавершении" , ОповещениеПриЗавершении);
	Контекст.Вставить("УникальныйИдентификатор" , УникальныйИдентификатор);
	Контекст.Вставить("ВходныеПараметры"        , ПараметрыОперации);
	Контекст.Вставить("ВыполняемаяКоманда"      , "CheckFiscalizationPacket");
	
	Если ТипОборудования = Неопределено Тогда
		ПоддерживаемыеТипыВО = Новый Массив();
		ПоддерживаемыеТипыВО.Добавить("ФискальныйРегистратор");
		ПоддерживаемыеТипыВО.Добавить("ПринтерЧеков");
		ПоддерживаемыеТипыВО.Добавить("ККТ");
	Иначе
		ПоддерживаемыеТипыВО = ТипОборудования;
	КонецЕсли;
	
	Если ИдентификаторУстройства = Неопределено Тогда
		ОписаниеОповещения = ОписаниеОповещенияИС("НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение", МенеджерОборудованияКлиент, Контекст);
		ПредложитьВыбратьУстройство(ОписаниеОповещения, ПоддерживаемыеТипыВО,
			НСтр("ru='Выберите фискальное устройство'"),
			НСтр("ru='Фискальное устройство не подключено.'"), 
			НСтр("ru='Фискальное устройство не выбрано.'"),
			Истина);
	Иначе
		НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Контекст);
	КонецЕсли;
	
КонецПроцедуры

// Производить печать текста на фискальном устройстве.
// УникальныйИдентификатор - идентификатор формы.
// ИдентификаторУстройства - идентификатор устройства, если неопределенно - будет предложен выбор.
// ОповещениеПриЗавершении - оповещение при завершении завершении.
//
Процедура НачатьПечатьТекста(ОповещениеПриЗавершении, УникальныйИдентификатор, Текст, ИдентификаторУстройства = Неопределено, ТипОборудования = Неопределено) Экспорт
	
	ВходныеПараметры = Новый Массив();
	ВходныеПараметры.Добавить(Текст);
	
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеПриЗавершении" , ОповещениеПриЗавершении);
	Контекст.Вставить("УникальныйИдентификатор" , УникальныйИдентификатор);
	Контекст.Вставить("ВходныеПараметры"        , ВходныеПараметры);
	Контекст.Вставить("ВыполняемаяКоманда"      , "PrintText");
	
	Если ИдентификаторУстройства = Неопределено Тогда
		
		Если ТипОборудования = Неопределено Тогда
			ПоддерживаемыеТипыВО = Новый Массив();
			ПоддерживаемыеТипыВО.Добавить("ФискальныйРегистратор");
			ПоддерживаемыеТипыВО.Добавить("ПринтерЧеков");
			ПоддерживаемыеТипыВО.Добавить("ККТ");
		Иначе
			ПоддерживаемыеТипыВО = ТипОборудования;
		КонецЕсли;
		
		ОписаниеОповещения = ОписаниеОповещенияИС("НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение", МенеджерОборудованияКлиент, Контекст);
		ПредложитьВыбратьУстройство(ОписаниеОповещения, ПоддерживаемыеТипыВО,
			НСтр("ru='Выберите печатающее устройство'"), 
			НСтр("ru='Печатающее устройство не подключено.'"), 
			НСтр("ru='Печатающее устройство не выбрано.'"), 
			Истина);
	Иначе
		НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Контекст);
	КонецЕсли;
	
КонецПроцедуры

// Производит печать текстового документа на фискальном устройстве.
// УникальныйИдентификатор - идентификатор формы.
// ИдентификаторУстройства - идентификатор устройства, если неопределенно - будет предложен выбор.
// ОповещениеПриЗавершении - оповещение при завершении завершении.
//
Процедура НачатьПечатьТекстовыхДокументов(ОповещениеПриЗавершении, УникальныйИдентификатор, НефискальныеДокументы, ИдентификаторУстройства = Неопределено, ТипОборудования = Неопределено) Экспорт
	
	ВходныеПараметры = Новый Массив();
	ВходныеПараметры.Добавить(НефискальныеДокументы);
	
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеПриЗавершении" , ОповещениеПриЗавершении);
	Контекст.Вставить("УникальныйИдентификатор" , УникальныйИдентификатор);
	Контекст.Вставить("ВходныеПараметры"        , ВходныеПараметры);
	Контекст.Вставить("ВыполняемаяКоманда"      , "PrintTextDocument");
	
	Если ИдентификаторУстройства = Неопределено Тогда
		
		Если ТипОборудования = Неопределено Тогда
			ПоддерживаемыеТипыВО = Новый Массив();
			ПоддерживаемыеТипыВО.Добавить("ФискальныйРегистратор");
			ПоддерживаемыеТипыВО.Добавить("ПринтерЧеков");
			ПоддерживаемыеТипыВО.Добавить("ККТ");
		Иначе
			ПоддерживаемыеТипыВО = ТипОборудования;
		КонецЕсли;
		
		ОписаниеОповещения = ОписаниеОповещенияИС("НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение", МенеджерОборудованияКлиент, Контекст);
		ПредложитьВыбратьУстройство(ОписаниеОповещения, ПоддерживаемыеТипыВО,
			НСтр("ru='Выберите печатающее устройство'"), 
			НСтр("ru='Печатающее устройство не подключено.'"), 
			НСтр("ru='Печатающее устройство не выбрано.'"), 
			Истина);
	Иначе
		НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Контекст);
	КонецЕсли;
	
КонецПроцедуры

// Производить формирование чека коррекции на фискальном устройстве.
// УникальныйИдентификатор - идентификатор формы.
// ИдентификаторУстройства - идентификатор устройства, если неопределенно - будет предложен выбор.
// ОповещениеПриЗавершении - оповещение при завершении завершении.
//
Процедура НачатьФормированиеЧекаКоррекцииНаФискальномУстройстве(ОповещениеПриЗавершении, УникальныйИдентификатор, ПараметрыОперации, ИдентификаторУстройства = Неопределено, ТипОборудования = Неопределено) Экспорт
	
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеПриЗавершении" , ОповещениеПриЗавершении);
	Контекст.Вставить("УникальныйИдентификатор" , УникальныйИдентификатор);
	Контекст.Вставить("ВходныеПараметры"        , ПараметрыОперации);
	Контекст.Вставить("ВыполняемаяКоманда"      , "PrintReceiptCorrection");
	Контекст.Вставить("ПроверкаАктивностиСмены" , Истина); 
	
	Если ИдентификаторУстройства = Неопределено Тогда
		
		Если ТипОборудования = Неопределено Тогда
			ПоддерживаемыеТипыВО = Новый Массив();
			ПоддерживаемыеТипыВО.Добавить("ККТ");
		Иначе
			ПоддерживаемыеТипыВО = ТипОборудования;
		КонецЕсли;
		
		ОписаниеОповещения = ОписаниеОповещенияИС("НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение", МенеджерОборудованияКлиент, Контекст);
		ПредложитьВыбратьУстройство(ОписаниеОповещения, ПоддерживаемыеТипыВО,
			НСтр("ru='Выберите фискальное устройство'"),
			НСтр("ru='Фискальное устройство не подключено.'"), 
			НСтр("ru='Фискальное устройство не выбрано.'"),
			Истина);
	Иначе
		НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Контекст);
	КонецЕсли;
	
КонецПроцедуры

// Осуществляет открытие денежного ящика.
// Параметры:
//  ОповещениеПриЗавершении - ОписаниеОповещения - обработчик результата.
//  УникальныйИдентификатор - УникальныйИдентификатор - Идентификатор формы.
//  Параметры - Структура - Cодержит параметры выполнения операции.
//  ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - Устройство.
//  ТипОборудования - ПеречислениеСсылка.ТипыПодключаемогоОборудования - тип оборудования.
//
Процедура НачатьОткрытиеДенежногоЯщика(ОповещениеПриЗавершении, УникальныйИдентификатор, ИдентификаторУстройства = Неопределено, ТипОборудования = Неопределено) Экспорт
	
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеПриЗавершении" , ОповещениеПриЗавершении);
	Контекст.Вставить("УникальныйИдентификатор" , УникальныйИдентификатор);
	Контекст.Вставить("ВходныеПараметры"        , Неопределено);
	Контекст.Вставить("ВыполняемаяКоманда"      , "OpenCashDrawer");
	
	Если ИдентификаторУстройства = Неопределено Тогда
		
		Если ТипОборудования = Неопределено Тогда
			ПоддерживаемыеТипыВО = Новый Массив();
			ПоддерживаемыеТипыВО.Добавить("ФискальныйРегистратор");
			ПоддерживаемыеТипыВО.Добавить("ПринтерЧеков");
			ПоддерживаемыеТипыВО.Добавить("ККТ");
		Иначе
			ПоддерживаемыеТипыВО = ТипОборудования;
		КонецЕсли;
		
		ОписаниеОповещения = ОписаниеОповещенияИС("НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение", МенеджерОборудованияКлиент, Контекст);
		ПредложитьВыбратьУстройство(ОписаниеОповещения, ПоддерживаемыеТипыВО,
			НСтр("ru='Выберите фискальное устройство'"), 
			НСтр("ru='Фискальное устройство не подключено.'"), 
			НСтр("ru='Фискальное устройство не выбрано.'"),
			Истина);
	Иначе
		НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Контекст);
	КонецЕсли;
	
КонецПроцедуры

// Осуществляет печать копии чека.
//  ОповещениеПриЗавершении - ОписаниеОповещения - обработчик результата.
// УникальныйИдентификатор - идентификатор формы.
// ИдентификаторУстройства - идентификатор устройства, если неопределенно - будет предложен выбор.
//  ТипОборудования - ПеречислениеСсылка.ТипыПодключаемогоОборудования - .
//
Процедура НачатьПечатьКопииЧека(ОповещениеПриЗавершении, УникальныйИдентификатор, Параметры, ИдентификаторУстройства = Неопределено, ТипОборудования = Неопределено) Экспорт
	
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеПриЗавершении" , ОповещениеПриЗавершении);
	Контекст.Вставить("УникальныйИдентификатор" , УникальныйИдентификатор);
	Контекст.Вставить("ВходныеПараметры"        , Параметры);
	Контекст.Вставить("ВыполняемаяКоманда"      , "PrintCheckCopy");
	
	Если ИдентификаторУстройства = Неопределено Тогда
		
		Если ТипОборудования = Неопределено Тогда
			ПоддерживаемыеТипыВО = Новый Массив();
			ПоддерживаемыеТипыВО.Добавить("ФискальныйРегистратор");
			ПоддерживаемыеТипыВО.Добавить("ПринтерЧеков");
			ПоддерживаемыеТипыВО.Добавить("ККТ");
		Иначе
			ПоддерживаемыеТипыВО = ТипОборудования;
		КонецЕсли;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение", МенеджерОборудованияКлиент, Контекст);
		ПредложитьВыбратьУстройство(ОписаниеОповещения, ПоддерживаемыеТипыВО,
			НСтр("ru='Выберите печатающее устройство'"), 
			НСтр("ru='Печатающее устройство не подключено.'"), 
			НСтр("ru='Печатающее устройство не выбрано.'"), 
			Истина);
	Иначе
		НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Контекст);
	КонецЕсли;
	
КонецПроцедуры

// Производить аннулирование чека на фискальном устройстве.
// УникальныйИдентификатор - идентификатор формы.
// ИдентификаторУстройства - идентификатор устройства, если неопределенно - будет предложен выбор.
// ОповещениеПриЗавершении - оповещение при завершении завершении.
//
Процедура НачатьАннулированиеЧекаНаФискальномУстройстве(ОповещениеПриЗавершении, УникальныйИдентификатор, ПараметрыОперации, ИдентификаторУстройства = Неопределено, ТипОборудования = Неопределено) Экспорт
	
	ТипРасчета = МенеджерОборудованияКлиентСервер.КодРасчетаДенежнымиСредствами(ПараметрыОперации.ТипРасчета);
	ТипЧека =  ?(ТипРасчета = 1, Ложь, Истина);

	ВходныеПараметры = Новый Массив();
	ВходныеПараметры.Добавить(ТипЧека);
	ВходныеПараметры.Добавить(ПараметрыОперации.Фискальный);
	
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеПриЗавершении" , ОповещениеПриЗавершении);
	Контекст.Вставить("УникальныйИдентификатор" , УникальныйИдентификатор);
	Контекст.Вставить("УникальныйИдентификатор" , УникальныйИдентификатор);
	Контекст.Вставить("ВходныеПараметры"        , ВходныеПараметры);
	Контекст.Вставить("ВыполняемаяКоманда"      , "AnnulCheck");
	
	Если ИдентификаторУстройства = Неопределено Тогда
		
		Если ТипОборудования = Неопределено Тогда
			ПоддерживаемыеТипыВО = Новый Массив();
			ПоддерживаемыеТипыВО.Добавить("ФискальныйРегистратор");
			ПоддерживаемыеТипыВО.Добавить("ПринтерЧеков");
			ПоддерживаемыеТипыВО.Добавить("ККТ");
		Иначе
			ПоддерживаемыеТипыВО = ТипОборудования;
		КонецЕсли;
		
		ОписаниеОповещения = ОписаниеОповещенияИС("НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение", МенеджерОборудованияКлиент, Контекст);
		ПредложитьВыбратьУстройство(ОписаниеОповещения, ПоддерживаемыеТипыВО,
			НСтр("ru='Выберите фискальное устройство'"),
			НСтр("ru='Фискальное устройство не подключено.'"), 
			НСтр("ru='Фискальное устройство не выбрано.'"),
			Истина);
	Иначе
		НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Контекст);
	КонецЕсли;
	
КонецПроцедуры

// Производить получение параметров фискального устройства.
// ОповещениеПриЗавершении - оповещение при завершении завершении.
// УникальныйИдентификатор - идентификатор формы.
// ИдентификаторУстройства - идентификатор устройства, если неопределенно - будет предложен выбор.
//
Процедура НачатьПолучениеПараметровФискальногоУстройства(ОповещениеПриЗавершении, УникальныйИдентификатор, ИдентификаторУстройства = Неопределено, ИспользоватьСохраненныеЗначения = Истина) Экспорт
	
	ВходныеПараметры = Новый Структура();
	ВходныеПараметры.Вставить("ИспользоватьСохраненныеЗначения", ИспользоватьСохраненныеЗначения);
	
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеПриЗавершении" , ОповещениеПриЗавершении);
	Контекст.Вставить("УникальныйИдентификатор" , УникальныйИдентификатор);
	Контекст.Вставить("ВходныеПараметры"        , ВходныеПараметры);
	Контекст.Вставить("ВыполняемаяКоманда"      , "GetDataKKT");
	Контекст.Вставить("ИспользоватьСохраненныеЗначения", ИспользоватьСохраненныеЗначения); 
	
	ПоддерживаемыеТипыВО = Новый Массив();
	ПоддерживаемыеТипыВО.Добавить("ККТ");
	
	Если ИдентификаторУстройства = Неопределено Тогда
		ОписаниеОповещения = ОписаниеОповещенияИС("НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение", МенеджерОборудованияКлиент, Контекст);
		ПредложитьВыбратьУстройство(ОписаниеОповещения, ПоддерживаемыеТипыВО,
			НСтр("ru='Выберите фискальное устройство'"), 
			НСтр("ru='Фискальное устройство не подключено.'"), 
			НСтр("ru='Фискальное устройство не выбрано.'"),
			Истина);
	Иначе
		НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Контекст);
	КонецЕсли;
	
КонецПроцедуры

// Получение текущего состояния фискального устройства.
// ОповещениеПриЗавершении - оповещение при завершении завершении.
// УникальныйИдентификатор - идентификатор формы.
// ИдентификаторУстройства - идентификатор устройства, если неопределенно - будет предложен выбор.
//
Процедура НачатьПолучениеТекущегоСостоянияФискальногоУстройства(ОповещениеПриЗавершении, УникальныйИдентификатор, ИдентификаторУстройства = Неопределено, ТипОборудования = Неопределено) Экспорт
	
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеПриЗавершении" , ОповещениеПриЗавершении);
	Контекст.Вставить("УникальныйИдентификатор" , УникальныйИдентификатор);
	Контекст.Вставить("ВходныеПараметры"        , Новый Структура());
	Контекст.Вставить("ВыполняемаяКоманда"      , "GetCurrentStatus");
	
	Если ТипОборудования = Неопределено Тогда
		ПоддерживаемыеТипыВО = Новый Массив();
		ПоддерживаемыеТипыВО.Добавить("ФискальныйРегистратор");
		ПоддерживаемыеТипыВО.Добавить("ПринтерЧеков");
		ПоддерживаемыеТипыВО.Добавить("ККТ");
	Иначе
		ПоддерживаемыеТипыВО = ТипОборудования;
	КонецЕсли;
	
	Если ИдентификаторУстройства = Неопределено Тогда
		ОписаниеОповещения = ОписаниеОповещенияИС("НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение", МенеджерОборудованияКлиент, Контекст);
		ПредложитьВыбратьУстройство(ОписаниеОповещения, ПоддерживаемыеТипыВО,
			НСтр("ru='Выберите фискальное устройство'"), 
			НСтр("ru='Фискальное устройство не подключено.'"), 
			НСтр("ru='Фискальное устройство не выбрано.'"),
			Истина);
	Иначе
		НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Контекст);
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьОперациюФНДляФискальногоУстройства(ОповещениеПриЗавершении, УникальныйИдентификатор, ПараметрыОперации, ИдентификаторУстройства = Неопределено, ТипОборудования = Неопределено) Экспорт
	
	ВходныеПараметры = Новый Массив();
	ВходныеПараметры.Добавить(ПараметрыОперации);
	
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеПриЗавершении" , ОповещениеПриЗавершении);
	Контекст.Вставить("УникальныйИдентификатор" , УникальныйИдентификатор);
	Контекст.Вставить("ВходныеПараметры"        , ВходныеПараметры);
	Контекст.Вставить("ВыполняемаяКоманда"      , "OperationFN");
	
	Если ТипОборудования = Неопределено Тогда
		ПоддерживаемыеТипыВО = Новый Массив();
		ПоддерживаемыеТипыВО.Добавить("ФискальныйРегистратор");
		ПоддерживаемыеТипыВО.Добавить("ПринтерЧеков");
		ПоддерживаемыеТипыВО.Добавить("ККТ");
	Иначе
		ПоддерживаемыеТипыВО = ТипОборудования;
	КонецЕсли;
	
	Если ИдентификаторУстройства = Неопределено Тогда
		ОписаниеОповещения = ОписаниеОповещенияИС("НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение", МенеджерОборудованияКлиент, Контекст);
		ПредложитьВыбратьУстройство(ОписаниеОповещения, ПоддерживаемыеТипыВО,
			НСтр("ru='Выберите фискальное устройство'"), 
			НСтр("ru='Фискальное устройство не подключено.'"), 
			НСтр("ru='Фискальное устройство не выбрано.'"),
			Истина);
	Иначе
		НачатьВыполнениеКоманды_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Контекст);
	КонецЕсли;
КонецПроцедуры

// Функция формирует текст нефискального чека по шаблону.
//
// Возвращаемое значение:
//  Строка - .
Функция СформироватьТекстНефискальногоЧека(ШиринаСтроки, ТипДокумента, ВходныеПараметры, ДополнительныйТекст = Неопределено, ФорматФФД = "1.1") Экспорт
	
	ОбщиеПараметры = Новый Структура("ТипЧека");
	
	Если ВходныеПараметры <> Неопределено Тогда
		ОбщиеПараметры = ВходныеПараметры;
	КонецЕсли;
	
	Текст = МенеджерОборудованияКлиентСервер.СформироватьТекстНефискальногоДокумента(ТипДокумента, ОбщиеПараметры, ШиринаСтроки, ДополнительныйТекст, ФорматФФД);
	
	Если ОбщиеПараметры.Свойство("КопийЧека") И ОбщиеПараметры.КопийЧека > 1 Тогда
		Эталон = Текст;
		Для Копия = 2 По ОбщиеПараметры.КопийЧека Цикл
			Текст = Текст + "[отрезка]" + Символы.ПС + Эталон;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Текст;
	
КонецФункции

// Начать выполнение команды печати чека ответственному обработчику драйвера
//
Процедура НачатьВыполнениеКомандыПечатиЧека(ОповещениеПриЗавершении, Идентификатор, Команда, Параметры, ВходныеПараметры) Экспорт
	
	// Поиск подключенного устройства.
	ПодключенноеУстройство = ПолучитьПодключенноеУстройство(глПодключаемоеОборудование.ПараметрыПодключенияПО, Идентификатор);
	
	Если ПодключенноеУстройство <> Неопределено Тогда
		
		ОбработчикДрайвераМодуль = ПодключенноеУстройство.ОбработчикДрайвераМодуль;
		ОбъектДрайвера = ПодключенноеУстройство.ОбъектДрайвера;
		
		Если ОбработчикДрайвераМодуль = Неопределено Или ОбъектДрайвера = Неопределено Тогда
			// Сообщить об ошибке, что не удалось загрузить драйвер.
			ТекстОшибки = НСтр("ru='Не удалось подключить обработчик драйвера или загрузить драйвер.'");
			ВыходныеПараметры = Новый Массив();
			ВыходныеПараметры.Добавить(999);
			ВыходныеПараметры.Добавить(ТекстОшибки);
			ВыходныеПараметры.Добавить(НСтр("ru='Не установлен'"));
			
			РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании();
			РезультатВыполнения.Результат = Ложь;
			РезультатВыполнения.ОписаниеОшибки = ТекстОшибки; 
			РезультатВыполнения.ВыходныеПараметры = ВыходныеПараметры;
			ВыполнитьОбработкуОповещенияИС(ОповещениеПриЗавершении, РезультатВыполнения);
		Иначе
			
			ПоддержкаАсинхронногоРежима = ОбработчикДрайвераМодуль.ПоддержкаАсинхронногоРежима();
			
			ПараметрыВыполнения = Новый Структура();
			ПараметрыВыполнения.Вставить("ВходныеПараметры", ВходныеПараметры);
			ПараметрыВыполнения.Вставить("Команда"         , Команда);
			ПараметрыВыполнения.Вставить("Параметры"       , Параметры);
			ПараметрыВыполнения.Вставить("ОписаниеОшибки"  , Неопределено); 
			ПараметрыВыполнения.Вставить("ПродолжитьПечать", Истина);
			ПараметрыВыполнения.Вставить("ОповещениеПриЗавершении" , ОповещениеПриЗавершении);
			ПараметрыВыполнения.Вставить("ОповещениеПриОшибке"     , Параметры.ОповещениеПриОшибке);
			ПараметрыВыполнения.Вставить("ОбработчикДрайвераМодуль", ОбработчикДрайвераМодуль);
			ПараметрыВыполнения.Вставить("ОбъектДрайвера", ОбъектДрайвера);
			ПараметрыВыполнения.Вставить("ПодключенноеУстройство", ПодключенноеУстройство);
			ПараметрыВыполнения.Вставить("ПоддержкаАсинхронногоРежима", ПоддержкаАсинхронногоРежима);
			
			ВыходныеПараметры = Неопределено;
			ОбщиеПараметры = ВходныеПараметры;
			
			Если ПоддержкаАсинхронногоРежима Тогда
				Если ПодключенноеУстройство.ТипОборудованияИмя = "ПринтерЧеков" Тогда
					ВыходныеПараметры = Новый Массив();
					ВыходныеПараметры.Добавить(ОбщиеПараметры.НомерСмены);
					ВыходныеПараметры.Добавить(ОбщиеПараметры.НомерЧека);
					РезультатВыполнения = Новый Структура("Результат, ОписаниеОшибки, ВыходныеПараметры", Истина, , ВыходныеПараметры);
					НачатьВыполнениеКомандыПечатиЧека_ЗавершениеОткрытиеЧека(РезультатВыполнения, ПараметрыВыполнения);
				ИначеЕсли ПодключенноеУстройство.ТипОборудованияИмя = "ККТ" Тогда
					Оповещение = ОписаниеОповещенияИС("НачатьВыполнениеКомандыПечатиЧека_ЗавершениеОткрытиеЧека", МенеджерОборудованияКлиент, ПараметрыВыполнения);
					ОбработчикДрайвераМодуль.НачатьВыполнениеКоманды(Оповещение, "GetCurrentStatus", ОбщиеПараметры,
						 ОбъектДрайвера, ПодключенноеУстройство.Параметры, ПодключенноеУстройство.ПараметрыПодключения);
				Иначе
					Оповещение = ОписаниеОповещенияИС("НачатьВыполнениеКомандыПечатиЧека_ЗавершениеОткрытиеЧека", МенеджерОборудованияКлиент, ПараметрыВыполнения);
					ОбработчикДрайвераМодуль.НачатьВыполнениеКоманды(Оповещение, "OpenCheck", ОбщиеПараметры,
						 ОбъектДрайвера, ПодключенноеУстройство.Параметры, ПодключенноеУстройство.ПараметрыПодключения);
				КонецЕсли;
			Иначе
				ЗаводскойНомерФН = Неопределено;
				Результат = Истина;
				
				Если ПодключенноеУстройство.ТипОборудованияИмя = "ПринтерЧеков" Тогда
					ВыходныеПараметры = Новый Массив();
					ВыходныеПараметры.Добавить(ОбщиеПараметры.НомерСмены);
					ВыходныеПараметры.Добавить(ОбщиеПараметры.НомерЧека);
				ИначеЕсли ПодключенноеУстройство.ТипОборудованияИмя = "ККТ" Тогда
					ВремВходныеПараметры = Новый Массив();
					// Получение текущего состояние ККТ с ФН.
					РезультатКоманды = ОбработчикДрайвераМодуль.ВыполнитьКоманду("GetCurrentStatus", ВходныеПараметры, ВыходныеПараметры,
						ОбъектДрайвера, ПодключенноеУстройство.Параметры, ПодключенноеУстройство.ПараметрыПодключения);
					Если РезультатКоманды Тогда
						// Увеличиваем инкремент номера последнего чека.
						Если ПустаяСтрока(ВыходныеПараметры[1]) Тогда
							ВыходныеПараметры[1] = 1
						Иначе
							ВыходныеПараметры[1] = Число(ВыходныеПараметры[1]) + 1;
						КонецЕсли;
						ЗаводскойНомерФН = ВыходныеПараметры[5];
					Иначе
						ВыходныеПараметры = Новый Массив();
						ВыходныеПараметры.Добавить(ОбщиеПараметры.НомерСмены);
						ВыходныеПараметры.Добавить(ОбщиеПараметры.НомерЧека);
					КонецЕсли;
				Иначе
					ТипРасчета = МенеджерОборудованияКлиентСервер.КодРасчетаДенежнымиСредствами(ОбщиеПараметры.ТипРасчета);
					ТипЧека = ?(ТипРасчета = 1, Ложь, Истина);
					ВремВходныеПараметры = Новый Массив();
					ВремВходныеПараметры.Добавить(ТипЧека);
					ВремВходныеПараметры.Добавить(Истина);
					Результат = ОбработчикДрайвераМодуль.ВыполнитьКоманду("OpenCheck", ВремВходныеПараметры, ВыходныеПараметры,
						ОбъектДрайвера, ПодключенноеУстройство.Параметры, ПодключенноеУстройство.ПараметрыПодключения);
				КонецЕсли;
				
				Если Не Результат Тогда
					ТекстОшибки = ?(ВыходныеПараметры.Количество() >= 2, ВыходныеПараметры[1], "");
					ВыходныеПараметры.Добавить(НСтр("ru='Установлен'"));
					РезультатВыполнения = Новый Структура("Результат, ОписаниеОшибки, ВыходныеПараметры", Результат, ТекстОшибки, ВыходныеПараметры);
					ВыполнитьОбработкуОповещенияИС(ОповещениеПриЗавершении, РезультатВыполнения);
				Иначе
					Оповещение = ОписаниеОповещенияИС("НачатьВыполнениеКомандыПечатиЧека_ЗавершениеПечати", МенеджерОборудованияКлиент, Параметры);
					ЗаполнитьПараметрыОперацииПодписиЧека(ПараметрыВыполнения, ВыходныеПараметры[0], ВыходныеПараметры[1], ЗаводскойНомерФН); 
					ПараметрыВыполнения.Вставить("ОповещениеПродолжения", Оповещение);
					ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПослеОткрытииЧека, ПараметрыВыполнения);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		// Сообщить об ошибке, что устройство не подключено.
		ТекстОшибки = НСтр("ru='Устройство не подключено. Перед выполнением операции устройство должно быть подключено.'");
		ВыходныеПараметры = Новый Массив();
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(ТекстОшибки);
		ВыходныеПараметры.Добавить(НСтр("ru='Установлен'"));
		РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании();
		РезультатВыполнения.Результат = Ложь;
		РезультатВыполнения.ОписаниеОшибки = ТекстОшибки; 
		РезультатВыполнения.ВыходныеПараметры = ВыходныеПараметры;
		ВыполнитьОбработкуОповещенияИС(ОповещениеПриЗавершении, РезультатВыполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьВыполнениеКомандыПечатиЧека_ЗавершениеОтмены(РезультатВыполнения, Параметры) Экспорт
	
	ВыходныеПараметры = Новый Массив();
	ВыходныеПараметры.Добавить(999);
	ВыходныеПараметры.Добавить(Параметры.ОписаниеОшибки);
	
	РезультатВыполнения = Новый Структура("Результат, ОписаниеОшибки, ВыходныеПараметры", Ложь, Параметры.ОписаниеОшибки, ВыходныеПараметры);
	ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатВыполнения);
	
КонецПроцедуры

Процедура НачатьВыполнениеКомандыПечатиЧека_ЗавершениеПроцессаПечатиСлипЧека(РезультатВыполнения, Параметры) Экспорт
	
	Если НЕ РезультатВыполнения.Результат Тогда
		Если Параметры.ОповещениеПриОшибке <> Неопределено Тогда
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриОшибке, Параметры);
		КонецЕсли;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, Параметры.РезультатВыполнения);
	
КонецПроцедуры

// Начинает выполнение печати чека.
// 
// Параметры:
// 	РезультатВыполнения - Структура - структура результата выполнения операции.
// 	Параметры - Структура - где:
// 	*ПодключенноеУстройство - Структура - где:
// 	**Параметры - Структура - .
Процедура НачатьВыполнениеКомандыПечатиЧека_ЗавершениеПроцессаПечати(РезультатВыполнения, Параметры) Экспорт
	
	Если РезультатВыполнения.Результат Тогда
		Если Параметры.Свойство("ПодписьЧека") И НЕ ПустаяСтрока(Параметры.ПодписьЧека) Тогда 
			ПодключенноеУстройство = Параметры.ПодключенноеУстройство;
			ШиринаСтроки = ?(ПодключенноеУстройство.ПараметрыПодключения.Свойство("ШиринаСтроки"), ПодключенноеУстройство.ПараметрыПодключения.ШиринаСтроки, 32); 
			
			ВремВходныеПараметры = Новый Массив();
			ВремВходныеПараметры.Добавить(СформироватьШаблонИнфоКвитанции(Параметры, ШиринаСтроки));
			ВремВыходныеПараметры = Неопределено;  
			Параметры.Вставить("РезультатВыполнения", РезультатВыполнения);
			
			Оповещение = ОписаниеОповещенияИС("НачатьВыполнениеКомандыПечатиЧека_ЗавершениеПроцессаПечатиСлипЧека", МенеджерОборудованияКлиент, Параметры);
			Параметры.ОбработчикДрайвераМодуль.НачатьВыполнениеКоманды(Оповещение, "PrintText", ВремВходныеПараметры, 
				Параметры.ОбъектДрайвера, ПодключенноеУстройство.Параметры, ПодключенноеУстройство.ПараметрыПодключения);
		Иначе
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатВыполнения);
		КонецЕсли;
	Иначе
		Если Параметры.ОповещениеПриОшибке <> Неопределено Тогда
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриОшибке, Параметры);
		КонецЕсли;
		ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатВыполнения);
	КонецЕсли;
	
КонецПроцедуры

// Завершение печати.
// 
// Параметры:
// 	ПараметрыВыполнения - Структура - где:
// 	*ПодключенноеУстройство - Структура - где:
// 	**Параметры - Структура - .
// 	Параметры - Структура - .
Процедура НачатьВыполнениеКомандыПечатиЧека_ЗавершениеПечати(ПараметрыВыполнения, Параметры) Экспорт
	
	ОбработчикДрайвераМодуль = ПараметрыВыполнения.ОбработчикДрайвераМодуль;
	ОбъектДрайвера           = ПараметрыВыполнения.ОбъектДрайвера;
	ПодключенноеУстройство   = ПараметрыВыполнения.ПодключенноеУстройство;
	ВыходныеПараметры = Неопределено;
	
	Если ПараметрыВыполнения.ПродолжитьПечать Тогда 
		
		ОбщиеПараметры = ПараметрыВыполнения.ВходныеПараметры;
		
		ДополнительныеПараметры = Новый Структура();
		ЗаполнитьПараметрыОперацииПодписиЧека(ДополнительныеПараметры);
		ЗаполнитьЗначенияСвойств(ДополнительныеПараметры, ПараметрыВыполнения);
		ДополнительныеПараметры.Вставить("АдресЧека"  , ?(ПараметрыВыполнения.Свойство("АдресЧека"), ПараметрыВыполнения.АдресЧека, Неопределено));
		ДополнительныеПараметры.Вставить("ПодписьЧека", ?(ПараметрыВыполнения.Свойство("ПодписьЧека"), ПараметрыВыполнения.ПодписьЧека, Неопределено));
		ДополнительныеПараметры.Вставить("НомерКассы" , ?(ОбщиеПараметры.Свойство("НомерКассы"), ОбщиеПараметры.НомерКассы, Неопределено));
		ОбщиеПараметры.Вставить("ПодписьЧека", ДополнительныеПараметры);
		
		Если ПараметрыВыполнения.ПоддержкаАсинхронногоРежима Тогда
			
			Оповещение = ОписаниеОповещенияИС("НачатьВыполнениеКомандыПечатиЧека_ЗавершениеПроцессаПечати", МенеджерОборудованияКлиент, ПараметрыВыполнения);
			ОбработчикДрайвераМодуль.НачатьВыполнениеКоманды(Оповещение, ПараметрыВыполнения.Команда, ПараметрыВыполнения.ВходныеПараметры, 
				ОбъектДрайвера, ПодключенноеУстройство.Параметры, ПодключенноеУстройство.ПараметрыПодключения);
				
		Иначе
			Результат = ОбработчикДрайвераМодуль.ВыполнитьКоманду(ПараметрыВыполнения.Команда, ПараметрыВыполнения.ВходныеПараметры,
					ВыходныеПараметры, ОбъектДрайвера, ПодключенноеУстройство.Параметры, ПодключенноеУстройство.ПараметрыПодключения);
			Если Не Результат Тогда
				Если ВыходныеПараметры.Количество() >= 2 Тогда
					ТекстОшибки = ВыходныеПараметры[1];
				КонецЕсли;
				ВыходныеПараметры.Добавить(НСтр("ru='Установлен'"));
				
				Если ПараметрыВыполнения.ОповещениеПриОшибке <> Неопределено Тогда
					ВыполнитьОбработкуОповещенияИС(ПараметрыВыполнения.ОповещениеПриОшибке, ПараметрыВыполнения);
				КонецЕсли;
			КонецЕсли;
			
			Если Результат И НЕ ПустаяСтрока(ДополнительныеПараметры.ПодписьЧека) Тогда
				
				ШиринаСтроки = ?(ПодключенноеУстройство.ПараметрыПодключения.Свойство("ШиринаСтроки"), ПодключенноеУстройство.ПараметрыПодключения.ШиринаСтроки, 32); 
				ВремВходныеПараметры = Новый Массив();
				ВремВходныеПараметры.Добавить(СформироватьШаблонИнфоКвитанции(ДополнительныеПараметры, ШиринаСтроки));
				
				ВремВыходныеПараметры = Неопределено;
				ДополнительныйРезультат = ОбработчикДрайвераМодуль.ВыполнитьКоманду("PrintText", ВремВходныеПараметры,
					ВремВыходныеПараметры, ОбъектДрайвера, ПодключенноеУстройство.Параметры, ПодключенноеУстройство.ПараметрыПодключения);
			КонецЕсли;
			
			РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Результат, ТекстОшибки);
			РезультатВыполнения.ВыходныеПараметры = ВыходныеПараметры;
			РезультатВыполнения.Вставить("ОбработчикДрайвераМодуль", ОбработчикДрайвераМодуль);
			РезультатВыполнения.Вставить("ОбъектДрайвера"          , ОбъектДрайвера);
			РезультатВыполнения.Вставить("ПодключенноеУстройство"  , ПодключенноеУстройство);
			РезультатВыполнения.Вставить("ДополнительныеПараметры", ДополнительныеПараметры);
			ВыполнитьОбработкуОповещенияИС(ПараметрыВыполнения.ОповещениеПриЗавершении, РезультатВыполнения);
		КонецЕсли;
		
	Иначе
		Если ПараметрыВыполнения.ПоддержкаАсинхронногоРежима Тогда
			Оповещение = ОписаниеОповещенияИС("НачатьВыполнениеКомандыПечатиЧека_ЗавершениеОтмены", МенеджерОборудованияКлиент, ПараметрыВыполнения);
			ОбработчикДрайвераМодуль.НачатьВыполнениеКоманды(Оповещение, "CancelCheck", Неопределено,
				ОбъектДрайвера, ПодключенноеУстройство.Параметры, ПодключенноеУстройство.ПараметрыПодключения);
		Иначе
			ОбработчикДрайвераМодуль.ВыполнитьКоманду("CancelCheck", Неопределено, Неопределено,
				ОбъектДрайвера, ПодключенноеУстройство.Параметры, ПодключенноеУстройство.ПараметрыПодключения);
				
			ВыходныеПараметры = Новый Массив();
			ВыходныеПараметры.Добавить(999);
			ВыходныеПараметры.Добавить(ПараметрыВыполнения.ОписаниеОшибки);
		
			РезультатВыполнения = Новый Структура("Результат, ОписаниеОшибки, ВыходныеПараметры", Ложь, ПараметрыВыполнения.ОписаниеОшибки, ВыходныеПараметры);
			ВыполнитьОбработкуОповещенияИС(ПараметрыВыполнения.ОповещениеПриЗавершении, РезультатВыполнения);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьВыполнениеКомандыПечатиЧека_ЗавершениеОткрытиеЧека(РезультатОперации, ПараметрыВыполнения) Экспорт
	
	ВыходныеПараметры = РезультатОперации.ВыходныеПараметры;
	
	Если Не РезультатОперации.Результат Тогда
		Если ВыходныеПараметры.Количество() >= 2 Тогда
			ТекстОшибки = ВыходныеПараметры[1];
		КонецЕсли;
		ВыходныеПараметры.Добавить(НСтр("ru='Установлен'"));
		РезультатВыполнения = Новый Структура("Результат, ОписаниеОшибки, ВыходныеПараметры", РезультатОперации.Результат, ТекстОшибки, ВыходныеПараметры);
		ВыполнитьОбработкуОповещенияИС(ПараметрыВыполнения.ОповещениеПриЗавершении, РезультатВыполнения);
	Иначе
		Оповещение = ОписаниеОповещенияИС("НачатьВыполнениеКомандыПечатиЧека_ЗавершениеПечати", МенеджерОборудованияКлиент, ПараметрыВыполнения.Параметры);
		ЗаводскойНомерФН = ?(ВыходныеПараметры.Количество() > 5, ВыходныеПараметры[5], Неопределено);  
		ЗаполнитьПараметрыОперацииПодписиЧека(ПараметрыВыполнения, ВыходныеПараметры[0], ВыходныеПараметры[1], ЗаводскойНомерФН);
		ПараметрыВыполнения.Вставить("ОповещениеПродолжения", Оповещение);
		ВыполнитьОбработкуОповещенияИС(ПараметрыВыполнения.Параметры.ОповещениеПослеОткрытииЧека, ПараметрыВыполнения);
	КонецЕсли;
	
КонецПроцедуры

// Вспомогательная функция - осуществляет печать чека по шаблону.
//
//  Параметры:
// 	ОбщийМодульОборудования - Строка - .
// 	ОбъектДрайвера - СправочникСсылка.ДрайверыОборудования - .
// 	Параметры - Структура - .
// 	ПараметрыПодключения - Структура - .
// 	ВходныеПараметры - Структура - где:
// 	*ПозицииЧека - Массив из Структура - где: 
// 	**Наименование - Строка - .
// 	ВыходныеПараметры - Структура - .
// Возвращаемое значение:
// 	Булево - Описание
//
Функция ПечатьЧекаПоШаблону(ОбщийМодульОборудования, ОбъектДрайвера, Параметры, ПараметрыПодключения, ВходныеПараметры, ВыходныеПараметры) Экспорт
	
	Результат  = Истина;
	
	ПозицииЧека  = ВходныеПараметры.ПозицииЧека;
	ТаблицаОплат = ВходныеПараметры.ТаблицаОплат;
	
	Если ВходныеПараметры.Свойство("ПодписьЧека")  Тогда
		// Печать чека производиться в 2 этапа. Чека был открыт ранее.
		РасширенныеПараметры = ВходныеПараметры.ПодписьЧека; 
		// Заполнение выходных параметров.
		ВыходныеПараметры.Очистить();
		ВыходныеПараметры.Добавить(РасширенныеПараметры.НомерСмены);
		ВыходныеПараметры.Добавить(РасширенныеПараметры.НомерЧека);
		ВыходныеПараметры.Добавить(0); // Номер документа
		ВыходныеПараметры.Добавить(МенеджерОборудованияКлиентПереопределяемый.ДатаСеанса());
		ВыходныеПараметры.Добавить(Неопределено);
		ВыходныеПараметры.Добавить(Неопределено);
		ВыходныеПараметры.Добавить(Неопределено);
		ВыходныеПараметры.Добавить(Неопределено);
	Иначе
		ТипРасчета = МенеджерОборудованияКлиентСервер.КодРасчетаДенежнымиСредствами(ВходныеПараметры.ТипРасчета);
		ТипЧека =  ?(ТипРасчета = 1, Ложь, Истина);
		// Открываем чек
		Результат = ОбщийМодульОборудования.ОткрытьЧек(ОбъектДрайвера, Параметры, ПараметрыПодключения, ТипЧека, Истина, ВыходныеПараметры);
	КонецЕсли;
	
	// Печатаем строки чека   
	Если Результат Тогда
		
		ОшибкаПриПечати = Ложь;
		// Печатаем позиции чека
		Для ИндексМассива = 0 По ПозицииЧека.Количество() - 1 Цикл
			
			ПозицияЧека = ПозицииЧека[ИндексМассива];
			Если ПозицияЧека.Свойство("ФискальнаяСтрока") Тогда
				Наименование  = ?(ПозицияЧека.Свойство("Наименование") , ПозицияЧека.Наименование, "");
				Количество    = ?(ПозицияЧека.Свойство("Количество")   , ПозицияЧека.Количество  , 1);
				Цена          = ?(ПозицияЧека.Свойство("Цена")         , ПозицияЧека.Цена        , 0);
				Сумма         = ?(ПозицияЧека.Свойство("Сумма")        , ПозицияЧека.Сумма       , 0);
				НомерСекции   = ?(ПозицияЧека.Свойство("НомерСекции")  , ПозицияЧека.НомерСекции , 0);
				СтавкаНДС     = ?(ПозицияЧека.Свойство("СтавкаНДС")    , ПозицияЧека.СтавкаНДС   , 0);
				Если НЕ ОбщийМодульОборудования.НапечататьФискальнуюСтроку(ОбъектДрайвера, Параметры, ПараметрыПодключения,
									Наименование, Количество, Цена, Сумма, НомерСекции, СтавкаНДС, ВыходныеПараметры) Тогда
					ОшибкаПриПечати = Истина;   
					Прервать;
				КонецЕсли;
			ИначеЕсли ПозицияЧека.Свойство("ТекстоваяСтрока") Тогда
				Текст = ?(ПозицияЧека.Свойство("Текст"), ПозицияЧека.Текст, "");
				Если НЕ ОбщийМодульОборудования.НапечататьНефискальнуюСтроку(ОбъектДрайвера, Параметры, ПараметрыПодключения,
											Текст, ВыходныеПараметры) Тогда
					ОшибкаПриПечати = Истина;   
					Прервать;
				КонецЕсли;
			ИначеЕсли ПозицияЧека.Свойство("Штрихкод") Тогда
				ВремВыходныеПараметры = Новый Массив();
				ТипШтрихКодаЗнач = ?(ПозицияЧека.Свойство("ТипШтрихКода"), ПозицияЧека.ТипШтрихКода, "");
				ШтрихКод     = ?(ПозицияЧека.Свойство("ШтрихКод")    , ПозицияЧека.ШтрихКод    , "");
				Если НЕ ОбщийМодульОборудования.ПечатьШтрихкода(ОбъектДрайвера, Параметры, ПараметрыПодключения,
											ТипШтрихКодаЗнач, ШтрихКод, ВремВыходныеПараметры) Тогда
					Текст = НСтр("ru='<Штрихкод %ТипШтрихКода% не распечатан>'");
					Текст = СтрЗаменить(Текст, "%ТипШтрихКода%", ТипШтрихКодаЗнач);
					Если НЕ ОбщийМодульОборудования.НапечататьНефискальнуюСтроку(ОбъектДрайвера, Параметры, ПараметрыПодключения,
						Текст, ВремВыходныеПараметры) Тогда
						ОшибкаПриПечати = Истина;   
						Прервать;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
			
		Если НЕ ОшибкаПриПечати Тогда
			// Закрываем чек
			ТаблицаОплатЧека = Новый Массив();
			Если ТаблицаОплат <> Неопределено Тогда
				
				СуммаНаличными       = 0;
				СуммаЭлектронными    = 0;
				СуммаПостоплатой     = 0;
				СуммаПредоплатой     = 0;
				СуммаПредоставлением = 0;
				
				Для ИндексОплаты = 0 По ТаблицаОплат.Количество() - 1 Цикл
					Если ТаблицаОплат[ИндексОплаты].ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплатыККТ.Электронно") Тогда
						СуммаЭлектронными = СуммаЭлектронными + ТаблицаОплат[ИндексОплаты].Сумма;
					ИначеЕсли ТаблицаОплат[ИндексОплаты].ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплатыККТ.Предоплата") Тогда
						СуммаПредоплатой = СуммаПредоплатой + ТаблицаОплат[ИндексОплаты].Сумма;
					ИначеЕсли ТаблицаОплат[ИндексОплаты].ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплатыККТ.Постоплата") Тогда
						СуммаПостоплатой = СуммаПостоплатой + ТаблицаОплат[ИндексОплаты].Сумма;
					ИначеЕсли ТаблицаОплат[ИндексОплаты].ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплатыККТ.ВстречноеПредоставление") Тогда
						СуммаПредоставлением = СуммаПредоставлением + ТаблицаОплат[ИндексОплаты].Сумма;
					Иначе
						СуммаНаличными = СуммаНаличными + ТаблицаОплат[ИндексОплаты].Сумма;
					КонецЕсли;
				КонецЦикла;
				
				СтрокаОплаты = Новый СписокЗначений();
				СтрокаОплаты.Добавить(0);
				СтрокаОплаты.Добавить(СуммаНаличными);
				ТаблицаОплатЧека.Добавить(СтрокаОплаты);
				
				СтрокаОплаты = Новый СписокЗначений();
				СтрокаОплаты.Добавить(1);
				СтрокаОплаты.Добавить(СуммаЭлектронными + СуммаПредоставлением);
				ТаблицаОплатЧека.Добавить(СтрокаОплаты);
				
				СтрокаОплаты = Новый СписокЗначений();
				СтрокаОплаты.Добавить(2);
				СтрокаОплаты.Добавить(СуммаПостоплатой);
				ТаблицаОплатЧека.Добавить(СтрокаОплаты);
				
				СтрокаОплаты = Новый СписокЗначений();
				СтрокаОплаты.Добавить(3);
				СтрокаОплаты.Добавить(СуммаПредоплатой);
				ТаблицаОплатЧека.Добавить(СтрокаОплаты);
				
			КонецЕсли;
			Результат = ОбщийМодульОборудования.ЗакрытьЧек(ОбъектДрайвера, Параметры, ПараметрыПодключения, ТаблицаОплатЧека, ВыходныеПараметры);
		Иначе
			Результат = Ложь;
		КонецЕсли;
			
		КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция СформироватьШаблонИнфоКвитанции(Параметры, ШиринаСтроки, ПечатьШтрихкода = Истина) Экспорт
	
	Текст = МенеджерОборудованияКлиентСервер.ВыстроитьПоля(Параметры.НаименованиеОрганизации, , ШиринаСтроки) + Символы.ПС;
	
	АдресЧека = ?(Параметры.Свойство("АдресЧека"), Параметры.АдресЧека, Неопределено);
	ПодписьЧека = ?(Параметры.Свойство("ПодписьЧека"), Параметры.ПодписьЧека, Неопределено);
	
	ОрганизацияИНН = ?(НЕ ПустаяСтрока(Параметры.ИНН), НСтр("ru='ИНН:'") + Параметры.ИНН, "");
	ОрганизацияКПП = ?(НЕ ПустаяСтрока(Параметры.КПП), НСтр("ru='КПП:'") + Параметры.КПП, "");
	Если Не ПустаяСтрока(ОрганизацияИНН) Или НЕ ПустаяСтрока(ОрганизацияКПП) Тогда
		Текст = Текст + МенеджерОборудованияКлиентСервер.ВыстроитьПоля(ОрганизацияИНН, ОрганизацияКПП, ШиринаСтроки) + Символы.ПС;
	КонецЕсли;  
	
	НомерКассы = ?(НЕ ПустаяСтрока(Параметры.НомерКассы), НСтр("ru='КАССА:'") + Параметры.НомерКассы, "");
	НомерСмены = ?(НЕ ПустаяСтрока(Параметры.НомерСмены), НСтр("ru='СМЕНА:'") + Формат(Параметры.НомерСмены, "ЧГ=0"), 0);
	
	Если Не ПустаяСтрока(НомерКассы) Или НЕ ПустаяСтрока(НомерСмены) Тогда
		Текст = Текст + МенеджерОборудованияКлиентСервер.ВыстроитьПоля(НомерКассы, НомерСмены, ШиринаСтроки) + Символы.ПС;
	КонецЕсли;
	
	НомерЧека = ?(НЕ ПустаяСтрока(Параметры.НомерЧека), НСтр("ru='ЧЕК:'") + Параметры.НомерЧека, "");
	ДатаВремя = НСтр("ru='ДАТА:'") + Формат(ТекущаяДата(), "ДФ=""дд.ММ.гггг ЧЧ:мм""");
	Если Не ПустаяСтрока(НомерЧека) Или НЕ ПустаяСтрока(ДатаВремя) Тогда
		Текст = Текст + МенеджерОборудованияКлиентСервер.ВыстроитьПоля(НомерЧека, ДатаВремя, ШиринаСтроки) + Символы.ПС;
	КонецЕсли;
	
	Текст = Текст + " " + Символы.ПС;
	
	Если ПечатьШтрихкода Тогда
		Текст = Текст + "|ШтрихКод|QR|" + АдресЧека + Символы.ПС;
	КонецЕсли;
	
	Текст = Текст + " " + Символы.ПС;
	
	СтрокаТекста = МенеджерОборудованияКлиентСервер.ПостроитьПолеПереносом(АдресЧека, ШиринаСтроки);
	Для НомерСтроки = 1 По СтрЧислоСтрок(СтрокаТекста) Цикл
		Текст = Текст + СтрПолучитьСтроку(СтрокаТекста, НомерСтроки) + Символы.ПС;
	КонецЦикла;
	
	Текст = Текст + " " + Символы.ПС;
	
	СтрокаТекста = МенеджерОборудованияКлиентСервер.ПостроитьПолеПереносом(ПодписьЧека, ШиринаСтроки);
	Для НомерСтроки = 1 По СтрЧислоСтрок(СтрокаТекста) Цикл
		Текст = Текст + СтрПолучитьСтроку(СтрокаТекста, НомерСтроки) + Символы.ПС;
	КонецЦикла;
	
	Возврат Текст;
	
КонецФункции

#Область МаркировкаНаККТ

// Функция возвращает поддерживает ли фискальное устройство.
//
// Возвращаемое значение:
//  Булево - .
Функция ФискальноеУстройствоПоддерживаетПроверкуКодовМаркировки(ИдентификаторУстройства) Экспорт
	
	Возврат МенеджерОборудованияКлиентПовтИсп.ФискальноеУстройствоПоддерживаетПроверкуКодовМаркировки(ИдентификаторУстройства);
	
КонецФункции

// Функция возвращает для фискального устройства версию ФФД.
//
// Возвращаемое значение:
//  Булево - .
Функция ФискальноеУстройствоПоддерживаетВерсиюФФД(ИдентификаторУстройства) Экспорт
	
	Возврат МенеджерОборудованияКлиентПовтИсп.ФискальноеУстройствоПоддерживаетВерсиюФФД(ИдентификаторУстройства)
	
КонецФункции

#КонецОбласти

// Заполнить атрибуты идентификации чека для отсылки покупателю
//
Функция ЗаполнитьАтрибутыИдентификацииЧека() Экспорт
	
	АтрибутыЧека = Новый Структура();
	АтрибутыЧека.Вставить("РегистрационныйНомерККТ");
	АтрибутыЧека.Вставить("ЗаводскойНомерФН");
	АтрибутыЧека.Вставить("НомерЧекаФН");
	АтрибутыЧека.Вставить("ФискальныйПризнак");
	АтрибутыЧека.Вставить("СуммаЧека");
	АтрибутыЧека.Вставить("АдресСайтаПроверки");
	АтрибутыЧека.Вставить("ДатаСеанса");
	
	Возврат АтрибутыЧека;
	
КонецФункции 

// Получить шаблон атрибутов чека для отсылки.
// 
Функция ПолучитьШаблонАтрибутовЧекаДляОтсылки(АтрибутыЧека) Экспорт 
	
	ТекстСообщения  = НСтр("ru='ККТ№:'") + АтрибутыЧека.РегистрационныйНомерККТ + ";" + Символы.НПП 
		+ НСтр("ru='СУММА:'") + Формат(АтрибутыЧека.СуммаЧека, "ЧРД=.;ЧЦ=12;ЧДЦ=2;ЧН=0.00;ЧГ=0") + ";" + Символы.НПП 
		+ Формат(АтрибутыЧека.ДатаСеанса, "ДФ:""дд.ММ.гггг ЧЧ:мм""") + ";" + Символы.НПП 
		+ НСтр("ru='ФН№:'") + АтрибутыЧека.ЗаводскойНомерФН + ";" + Символы.НПП 
		+ НСтр("ru='ФД№:'") + АтрибутыЧека.НомерЧекаФН + ";" + Символы.НПП 
		+ НСтр("ru='ФПД:'") + АтрибутыЧека.ФискальныйПризнак + ";" + Символы.НПП 
		+ НСтр("ru='САЙТ:'") + АтрибутыЧека.АдресСайтаПроверки;
		
	Возврат ТекстСообщения;
	
КонецФункции 

// Функция формирует шаблон чека.
Функция СформироватьШаблонЧека(ВходныеПараметры, ДополнительныйТекст = Неопределено, ТипОборудования = "") Экспорт
	
	СтандартнаяОбработка = Истина;
	
	ШаблонЧека = МенеджерОборудованияКлиентПереопределяемый.СформироватьШаблонЧека(ВходныеПараметры, ДополнительныйТекст, СтандартнаяОбработка, ТипОборудования); 
	Если СтандартнаяОбработка Тогда
		Возврат ВходныеПараметры;
	Иначе
		Возврат ШаблонЧека;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область ПроцедурыИФункцииРаботыСЭквайрингом

// Заполняет структуру результата подключения эквайрингового терминала.
// 
// Возвращаемое значение:
// Структура.
Функция ПараметрыРезультатаПодключенияЭквайринговогоТерминала(Результат = Ложь, ОписаниеОшибки = Неопределено) Экспорт; 
	
	РезультатОперации = Новый Структура();
	РезультатОперации.Вставить("Результат", Результат);
	РезультатОперации.Вставить("ОписаниеОшибки", ОписаниеОшибки);
	РезультатОперации.Вставить("ИдентификаторУстройстваЭТ" , Неопределено);
	РезультатОперации.Вставить("ИдентификаторУстройстваПУ" , Неопределено);
	РезультатОперации.Вставить("ПечатьКвитанцийНаТерминале", Ложь);
	Возврат РезультатОперации;
	
КонецФункции

// Начать подключение эквайрингово терминала.
// Возвращает структуру:
//   Результат - Результат операции.
//   ОписаниеОшибки - Описание ошибки (Для Результат = Ложь).
//   ИдентификаторПодключенногоЭТ - Идентификатор эквайринговогой терминала.
//   ИдентификаторПодключенногоПУ - Идентификатор фискального регистратора.
//   ПечатьКвитанцийНаТерминале   - Печать квитанций на терминале, если Истина то ИдентификаторПодключенногоПУ = Неопределено.
//
Процедура НачатьПодключенияЭквайринговогоТерминала(ОповещениеПриЗавершении, УникальныйИдентификатор, ИдентификаторУстройстваЭТ = Неопределено, ИдентификаторУстройстваПУ = Неопределено) Экспорт
	
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеПриЗавершении"  , ОповещениеПриЗавершении);
	Контекст.Вставить("УникальныйИдентификатор"  , УникальныйИдентификатор);
	Контекст.Вставить("ИдентификаторУстройстваЭТ", ИдентификаторУстройстваЭТ);
	Контекст.Вставить("ИдентификаторУстройстваПУ", ИдентификаторУстройстваПУ);
	
	Если Контекст.ИдентификаторУстройстваЭТ = Неопределено Тогда
		ОписаниеОповещения = ОписаниеОповещенияИС("НачатьПодключенияЭквайринговогоТерминала_ВыбратьУстройствоЗавершение", МенеджерОборудованияКлиент, Контекст);
		ПредложитьВыбратьУстройство(ОписаниеОповещения, "ЭквайринговыйТерминал",
			НСтр("ru='Выберите эквайринговый терминал'"), 
			НСтр("ru='Эквайринговый терминал не подключен.'"),
			НСтр("ru='Эквайринговый терминал не выбран.'"), 
			Истина);
	Иначе
		НачатьПодключенияЭквайринговогоТерминала_ВыбратьУстройствоЗавершение(Контекст.ИдентификаторУстройстваЭТ, Контекст);
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьПодключенияЭквайринговогоТерминала_ВыбратьУстройствоЗавершение(ИдентификаторУстройстваЭТ, Параметры) Экспорт
	
	Если ИдентификаторУстройстваЭТ = Неопределено Тогда
		Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
			ОписаниеОшибки = НСтр("ru='Эквайринговый терминал не выбран.'");
			РезультатОперации = ПараметрыРезультатаПодключенияЭквайринговогоТерминала(Ложь, ОписаниеОшибки);
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
		КонецЕсли;
	Иначе
		Параметры.Вставить("ИдентификаторУстройстваЭТ" , ИдентификаторУстройстваЭТ);
		ОписаниеОповещения = ОписаниеОповещенияИС("НачатьПодключенияЭквайринговогоТерминала_ПодключениеЗавершение", МенеджерОборудованияКлиент, Параметры);
		НачатьПодключениеОборудованиеПоИдентификатору(ОписаниеОповещения, Параметры.УникальныйИдентификатор, ИдентификаторУстройстваЭТ);
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьПодключенияЭквайринговогоТерминала_ПодключениеЗавершение(РезультатПодключения, Параметры) Экспорт
	
	Если РезультатПодключения.Результат Тогда  
		ВходныеПараметры  = Неопределено;
		ОписаниеОповещения = ОписаниеОповещенияИС("НачатьПодключенияЭквайринговогоТерминала_ПечатьНаТерминалеЗавершение", МенеджерОборудованияКлиент, Параметры);
		НачатьВыполнениеКоманды(ОписаниеОповещения, Параметры.ИдентификаторУстройстваЭТ, "PrintSlipOnTerminal", ВходныеПараметры);
	Иначе
		// Ошибка подключения.
		Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
			ТекстСообщения = НСтр("ru='Эквайринговая операция не была произведена: %ДополнительноеОписание%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", РезультатПодключения.ОписаниеОшибки);
			РезультатОперации = ПараметрыРезультатаПодключенияЭквайринговогоТерминала(Ложь, ТекстСообщения);
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьПодключенияЭквайринговогоТерминала_ПечатьНаТерминалеЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если РезультатВыполнения.Результат Тогда
		ПечатьКвитанцийНаТерминале = РезультатВыполнения.ВыходныеПараметры[0]; 
		Параметры.Вставить("ПечатьКвитанцийНаТерминале", ПечатьКвитанцийНаТерминале);
		Если ПечатьКвитанцийНаТерминале Тогда
			Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
				РезультатОперации = ПараметрыРезультатаПодключенияЭквайринговогоТерминала(Истина);
				РезультатОперации.ИдентификаторУстройстваЭТ  = Параметры.ИдентификаторУстройстваЭТ;
				РезультатОперации.ПечатьКвитанцийНаТерминале = ПечатьКвитанцийНаТерминале;
				ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
			КонецЕсли;
		Иначе
			Если Параметры.ИдентификаторУстройстваПУ = Неопределено Тогда
				ПоддерживаемыеТипыВО = Новый Массив();
				ПоддерживаемыеТипыВО.Добавить("ФискальныйРегистратор");
				ПоддерживаемыеТипыВО.Добавить("ПринтерЧеков");
				ПоддерживаемыеТипыВО.Добавить("ККТ");
				ОписаниеОповещения = ОписаниеОповещенияИС("НачатьПодключенияЭквайринговогоТерминала_ВыбратьПечатающегоУстройстваЗавершение", МенеджерОборудованияКлиент, Параметры);
				ПредложитьВыбратьУстройство(ОписаниеОповещения, ПоддерживаемыеТипыВО,
					НСтр("ru='Выберите печатающее устройство'"), НСтр("ru='Печатающее устройство не подключено.'"), НСтр("ru='Печатающее устройство не выбрано.'"), Истина);
			Иначе
				НачатьПодключенияЭквайринговогоТерминала_ВыбратьПечатающегоУстройстваЗавершение(Параметры.ИдентификаторУстройстваПУ, Параметры);
			КонецЕсли;
		КонецЕсли;
	Иначе
		НачатьОтключениеОборудованиеПоИдентификатору(Неопределено, Параметры.УникальныйИдентификатор, Параметры.ИдентификаторУстройстваЭТ);
		Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
			ТекстСообщения = НСтр("ru='При использовании терминала произошла ошибка: %ДополнительноеОписание%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", РезультатВыполнения.ВыходныеПараметры[1]);
			РезультатОперации = ПараметрыРезультатаПодключенияЭквайринговогоТерминала(Ложь, ТекстСообщения);
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьПодключенияЭквайринговогоТерминала_ВыбратьПечатающегоУстройстваЗавершение(ИдентификаторУстройстваПУ, Параметры) Экспорт
	
	Если ИдентификаторУстройстваПУ = Неопределено Тогда
		НачатьОтключениеОборудованиеПоИдентификатору(Неопределено, Параметры.УникальныйИдентификатор, Параметры.ИдентификаторУстройстваЭТ);
		Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
			ОписаниеОшибки = НСтр("ru='Печатающее устройство не выбрано.'");
			РезультатОперации = ПараметрыРезультатаПодключенияЭквайринговогоТерминала(Ложь, ОписаниеОшибки);
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
		КонецЕсли;
	Иначе
		Параметры.ИдентификаторУстройстваПУ = ИдентификаторУстройстваПУ;
		ОписаниеОповещения = ОписаниеОповещенияИС("НачатьПодключенияЭквайринговогоТерминала_ПечатающееУстройствоПодключениеЗавершение", МенеджерОборудованияКлиент, Параметры);
		НачатьПодключениеОборудованиеПоИдентификатору(ОписаниеОповещения, Параметры.УникальныйИдентификатор, ИдентификаторУстройстваПУ);
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьПодключенияЭквайринговогоТерминала_ПечатающееУстройствоПодключениеЗавершение(РезультатПодключения, Параметры) Экспорт
	
	Если РезультатПодключения.Результат Тогда  
		Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
			РезультатОперации = ПараметрыРезультатаПодключенияЭквайринговогоТерминала(Истина);
			РезультатОперации.ИдентификаторУстройстваЭТ  = Параметры.ИдентификаторУстройстваЭТ;
			РезультатОперации.ИдентификаторУстройстваПУ  = Параметры.ИдентификаторУстройстваПУ;
			РезультатОперации.ПечатьКвитанцийНаТерминале = Параметры.ПечатьКвитанцийНаТерминале;
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
		КонецЕсли;
	Иначе
		НачатьОтключениеОборудованиеПоИдентификатору(Неопределено, Параметры.УникальныйИдентификатор, Параметры.ИдентификаторУстройстваЭТ);
		Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
			ТекстСообщения = НСтр("ru='При подключении печатающего устройство произошла ошибка: %ДополнительноеОписание%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", РезультатПодключения.ОписаниеОшибки);
			РезультатОперации = ПараметрыРезультатаПодключенияЭквайринговогоТерминала(Ложь, ТекстСообщения);
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Отключение подключенного эквайрингово терминала. 
// Если эквайринговый терминал не поддерживает печать квитанций на терминале, для печати подключается печатающее устройство.
// Данной процедурой оно тоже будет отключено.
// Входящие параметры:  
//  ПараметрыОперации - Структура со следующими атрибутами.
//    ИдентификаторУстройстваЭТ - Идентификатор подключенного эквайринговогой терминала.
//    ИдентификаторУстройстваПУ - Идентификатор подключенного фискального регистратора.
//    ПечатьКвитанцийНаТерминале   - Печать квитанций на терминале, если True то ИдентификаторПодключенногоПУ = Неопределено.
//  УникальныйИдентификатор - идентификатор формы.
//
Процедура НачатьОтключениеЭквайринговогоТерминала(ОповещениеПриЗавершении, УникальныйИдентификатор, ПараметрыОперации) Экспорт
	
	Если Не ПараметрыОперации.ПечатьКвитанцийНаТерминале И Не ПараметрыОперации.ИдентификаторУстройстваПУ = Неопределено Тогда
		НачатьОтключениеОборудованиеПоИдентификатору(Неопределено, УникальныйИдентификатор, ПараметрыОперации.ИдентификаторУстройстваПУ);
	КонецЕсли;
	
	Если Не ПараметрыОперации.ИдентификаторУстройстваЭТ = Неопределено Тогда
		НачатьОтключениеОборудованиеПоИдентификатору(Неопределено, УникальныйИдентификатор, ПараметрыОперации.ИдентификаторУстройстваЭТ);
	КонецЕсли;
	
	Если ОповещениеПриЗавершении <> Неопределено Тогда
		РезультатОперации = ПараметрыРезультатаПодключенияЭквайринговогоТерминала(Истина);
		ВыполнитьОбработкуОповещенияИС(ОповещениеПриЗавершении, РезультатОперации);
	КонецЕсли;
	
КонецПроцедуры

// Выполнить сверку итогов на эквайринговом терминале.
// Если эквайринговый терминал не поддерживает печать квитанций на терминале, для печати подключается печатающее устройство.
//
Процедура НачатьВыполнениеСверкиИтоговНаЭквайринговомТерминале(ОповещениеПриЗавершении, УникальныйИдентификатор, ИдентификаторУстройстваЭТ = Неопределено, ИдентификаторУстройстваПУ = Неопределено) Экспорт
	
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеПриЗавершении" , ОповещениеПриЗавершении);
	Контекст.Вставить("УникальныйИдентификатор" , УникальныйИдентификатор);
	
	Оповещение = ОписаниеОповещенияИС("НачатьВыполнениеСверкиИтоговНаЭквайринговомТерминале_ПодключениеЗавершение", МенеджерОборудованияКлиент, Контекст);
	НачатьПодключенияЭквайринговогоТерминала(Оповещение, УникальныйИдентификатор, ИдентификаторУстройстваЭТ, ИдентификаторУстройстваПУ);
	
КонецПроцедуры

Процедура НачатьВыполнениеСверкиИтоговНаЭквайринговомТерминале_ПодключениеЗавершение(РезультатПодключения, Параметры) Экспорт
	
	Если РезультатПодключения.Результат Тогда  
		Параметры.Вставить("ПечатьКвитанцийНаТерминале", РезультатПодключения.ПечатьКвитанцийНаТерминале);
		Параметры.Вставить("ИдентификаторУстройстваЭТ" , РезультатПодключения.ИдентификаторУстройстваЭТ);
		Параметры.Вставить("ИдентификаторУстройстваПУ" , РезультатПодключения.ИдентификаторУстройстваПУ);
		
		ВходныеПараметры  = Неопределено;
		Оповещение = ОписаниеОповещенияИС("НачатьВыполнениеСверкиИтоговНаЭквайринговомТерминале_ВыполнениеКомандыЗавершение", МенеджерОборудованияКлиент, Параметры);
		НачатьВыполнениеКоманды(Оповещение, РезультатПодключения.ИдентификаторУстройстваЭТ, "Settlement", ВходныеПараметры);
	Иначе
		// Ошибка подключения.
		Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
			ТекстСообщения = НСтр("ru='При подключении терминала произошла ошибка: %ДополнительноеОписание%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", РезультатПодключения.ОписаниеОшибки);
			РезультатОперации = ПараметрыРезультатаПодключенияЭквайринговогоТерминала(Ложь, ТекстСообщения);
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьВыполнениеСверкиИтоговНаЭквайринговомТерминале_ВыполнениеКомандыЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если РезультатВыполнения.Результат Тогда
		
		ТекстСлипЧека = РезультатВыполнения.ВыходныеПараметры[0][1];
		
		Если Не ПустаяСтрока(ТекстСлипЧека) Тогда
			глПодключаемоеОборудование.Вставить("ПоследнийСлипЧек", ТекстСлипЧека);
		КонецЕсли;
		
		Если Не Параметры.ПечатьКвитанцийНаТерминале И Не Параметры.ИдентификаторУстройстваПУ = Неопределено И Не ПустаяСтрока(ТекстСлипЧека) Тогда
			Оповещение = ОписаниеОповещенияИС("НачатьВыполнениеСверкиИтоговНаЭквайринговомТерминале_ПечатьЗавершение", МенеджерОборудованияКлиент, Параметры);
			НачатьПечатьТекста(Оповещение, Параметры.УникальныйИдентификатор, ТекстСлипЧека, Параметры.ИдентификаторУстройстваПУ);
		Иначе
			НачатьОтключениеЭквайринговогоТерминала(, Параметры.УникальныйИдентификатор, Параметры);
			Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
				РезультатОперации = ПараметрыРезультатаПодключенияЭквайринговогоТерминала(Истина);
				ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		НачатьОтключениеЭквайринговогоТерминала(, Параметры.УникальныйИдентификатор, Параметры);
		Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
			ТекстСообщения = НСтр("ru='При использовании терминала произошла ошибка: %ДополнительноеОписание%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", РезультатВыполнения.ВыходныеПараметры[1]);
			РезультатОперации = ПараметрыРезультатаПодключенияЭквайринговогоТерминала(Ложь, ТекстСообщения);
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьВыполнениеСверкиИтоговНаЭквайринговомТерминале_ПечатьЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	НачатьОтключениеЭквайринговогоТерминала(, Параметры.УникальныйИдентификатор, Параметры);
	
	Если РезультатВыполнения.Результат Тогда  
		Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
			РезультатОперации = ПараметрыРезультатаПодключенияЭквайринговогоТерминала();
			РезультатОперации.Результат = Истина;
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
		КонецЕсли;
	Иначе
		Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
			ТекстСообщения = НСтр("ru='При печати слип чека возникла ошибка: %ДополнительноеОписание%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", РезультатВыполнения.ОписаниеОшибки);
			РезультатОперации = ПараметрыРезультатаПодключенияЭквайринговогоТерминала(Ложь, ТекстСообщения);
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Заполняет структуру параметров выполнения эквайринговой операции.
//
// Возвращаемое значение:
//  Структура - .
Функция ПараметрыВыполненияЭквайринговойОперации() Экспорт;
	
	Результат = Новый Структура();
	Результат.Вставить("ТипТранзакции"      , Неопределено);
	Результат.Вставить("СуммаОперации"      , 0);
	Результат.Вставить("НомерКарты"         , Неопределено);
	Результат.Вставить("НомерЧека"          , Неопределено);
	Результат.Вставить("НомерЧекаЭТ"        , Неопределено);
	Результат.Вставить("СсылочныйНомер"     , Неопределено);
	Результат.Вставить("ТекстСлипЧека"      , Неопределено);
	Результат.Вставить("КодАвторизации"     , Неопределено);
	Возврат Результат;
	
КонецФункции

// Выполнить операции на эквайринговом терминале.
// Если эквайринговый терминал не поддерживает печать квитанций на терминале, для печати подключается печатающее устройство.
//
Процедура НачатьВыполнениеОперацииНаЭквайринговомТерминале(ОповещениеПриЗавершении, УникальныйИдентификатор, ИдентификаторУстройстваЭТ = Неопределено, ИдентификаторУстройстваПУ = Неопределено, ПараметрыОперации = Неопределено) Экспорт
	
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеПриЗавершении" , ОповещениеПриЗавершении);
	Контекст.Вставить("УникальныйИдентификатор" , УникальныйИдентификатор);
	Контекст.Вставить("ПараметрыОперации"       , ПараметрыОперации);
	
	Оповещение = ОписаниеОповещенияИС("НачатьВыполнениеОперацииНаЭквайринговомТерминале_ПодключениеЗавершение", МенеджерОборудованияКлиент, Контекст);
	НачатьПодключенияЭквайринговогоТерминала(Оповещение, УникальныйИдентификатор, ИдентификаторУстройстваЭТ, ИдентификаторУстройстваПУ);
	
КонецПроцедуры

Процедура НачатьВыполнениеОперацииНаЭквайринговомТерминале_ПодключениеЗавершение(РезультатПодключения, Параметры) Экспорт
	
	Если РезультатПодключения.Результат Тогда  
		Параметры.Вставить("ПечатьКвитанцийНаТерминале", РезультатПодключения.ПечатьКвитанцийНаТерминале);
		Параметры.Вставить("ИдентификаторУстройстваЭТ" , РезультатПодключения.ИдентификаторУстройстваЭТ);
		Параметры.Вставить("ИдентификаторУстройстваПУ" , РезультатПодключения.ИдентификаторУстройстваПУ);
		
		ПараметрыОперации = Параметры.ПараметрыОперации;
		
		ТипТранзакции  = Параметры.ПараметрыОперации.ТипТранзакции;
		СуммаОперации  = Параметры.ПараметрыОперации.СуммаОперации;
		НомерКарты     = ?(ПараметрыОперации.Свойство("НомерКарты")    , ПараметрыОперации.НомерКарты, "");
		НомерЧека      = ?(ПараметрыОперации.Свойство("НомерЧека")     , ПараметрыОперации.НомерЧека, "");
		НомерЧекаЭТ    = ?(ПараметрыОперации.Свойство("НомерЧекаЭТ")   , ПараметрыОперации.НомерЧекаЭТ, "");
		СсылочныйНомер = ?(ПараметрыОперации.Свойство("СсылочныйНомер"), ПараметрыОперации.СсылочныйНомер, "");   
		
		СсылочныйНомер = ?(ПустаяСтрока(СсылочныйНомер), "", СсылочныйНомер);
		НомерЧека = ?(ПустаяСтрока(НомерЧека), "", НомерЧека);
		НомерЧекаЭТ = ?(ПустаяСтрока(НомерЧекаЭТ), "", НомерЧекаЭТ);
		
		Если ТипТранзакции = "AuthorizeCompletion" Или ТипТранзакции = "AuthorizeVoidPreSales" Тогда
			Параметры.Вставить("БезВозвращаемыхПараметров", Истина);
		КонецЕсли;
		
		ВходныеПараметры  = Новый Массив();
		ВходныеПараметры.Добавить(СуммаОперации);
		
		Если ТипТранзакции = "AuthorizeSales" Тогда
			ВходныеПараметры.Добавить(НомерКарты);
			ВходныеПараметры.Добавить(НомерЧека);
			ВходныеПараметры.Добавить(СсылочныйНомер);
		ИначеЕсли ТипТранзакции = "AuthorizeVoid" Или ТипТранзакции = "EmergencyVoid" Тогда
			ВходныеПараметры.Добавить(СсылочныйНомер);
			ВходныеПараметры.Добавить(НомерЧекаЭТ);
			ВходныеПараметры.Добавить(НомерКарты);
		ИначеЕсли ТипТранзакции = "AuthorizeRefund" Тогда
			ВходныеПараметры.Добавить(НомерКарты);
			ВходныеПараметры.Добавить(СсылочныйНомер);
			ВходныеПараметры.Добавить(НомерЧекаЭТ);
		ИначеЕсли ТипТранзакции = "AuthorizePreSales" Тогда
			ВходныеПараметры.Добавить(НомерКарты);
			ВходныеПараметры.Добавить(НомерЧека);
			ВходныеПараметры.Добавить(СсылочныйНомер);
		Иначе
			ВходныеПараметры.Добавить(НомерКарты);
			ВходныеПараметры.Добавить(СсылочныйНомер);
			ВходныеПараметры.Добавить(НомерЧекаЭТ);
		КонецЕсли;
		
		Оповещение = ОписаниеОповещенияИС("НачатьВыполнениеОперацииНаЭквайринговомТерминале_ВыполнениеКомандыЗавершение", МенеджерОборудованияКлиент, Параметры);
		НачатьВыполнениеКоманды(Оповещение, РезультатПодключения.ИдентификаторУстройстваЭТ, ТипТранзакции, ВходныеПараметры);
		
	Иначе
		// Ошибка подключения.
		Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
			ТекстСообщения = НСтр("ru='Эквайринговая операция не была произведена: %ДополнительноеОписание%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", РезультатПодключения.ОписаниеОшибки);
			РезультатОперации = ПараметрыРезультатаПодключенияЭквайринговогоТерминала(Ложь, ТекстСообщения);
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьВыполнениеОперацииНаЭквайринговомТерминале_ВыполнениеКомандыЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если РезультатВыполнения.Результат Тогда
		// Эквайринговая операция произведена успешно.
		ПараметрыОперации = Параметры.ПараметрыОперации;
		Если Параметры.Свойство("БезВозвращаемыхПараметров") Или РезультатВыполнения.ВыходныеПараметры.Количество()= 1 Тогда
			ПараметрыОперации.НомерКарты     = "";
			ПараметрыОперации.СсылочныйНомер = "";
			ПараметрыОперации.НомерЧекаЭТ    = "";
			ПараметрыОперации.ТекстСлипЧека  = РезультатВыполнения.ВыходныеПараметры[0][1];
		Иначе
			ПараметрыОперации.НомерКарты     = РезультатВыполнения.ВыходныеПараметры[0];
			ПараметрыОперации.СсылочныйНомер = РезультатВыполнения.ВыходныеПараметры[1];
			ПараметрыОперации.НомерЧекаЭТ    = РезультатВыполнения.ВыходныеПараметры[2];
			ПараметрыОперации.ТекстСлипЧека  = РезультатВыполнения.ВыходныеПараметры[3][1];
			Если РезультатВыполнения.ВыходныеПараметры.Количество() > 4 Тогда
				ПараметрыОперации.КодАвторизации = РезультатВыполнения.ВыходныеПараметры[4];
			КонецЕсли;
		КонецЕсли;
	
		Если Не ПустаяСтрока(ПараметрыОперации.ТекстСлипЧека) Тогда
			глПодключаемоеОборудование.Вставить("ПоследнийСлипЧек", ПараметрыОперации.ТекстСлипЧека);
		КонецЕсли;
		
		Если Не Параметры.ПечатьКвитанцийНаТерминале И Не Параметры.ИдентификаторУстройстваПУ = Неопределено И Не ПустаяСтрока(ПараметрыОперации.ТекстСлипЧека) Тогда
			// Печатаем слипчек на подключаемом печатающем устройстве.
			Оповещение = ОписаниеОповещенияИС("НачатьВыполнениеОперацииНаЭквайринговомТерминале_ПечатьЗавершение", МенеджерОборудованияКлиент, Параметры);
			НачатьПечатьТекста(Оповещение, Параметры.УникальныйИдентификатор, ПараметрыОперации.ТекстСлипЧека, Параметры.ИдентификаторУстройстваПУ);
		Иначе
			НачатьОтключениеЭквайринговогоТерминала(, Параметры.УникальныйИдентификатор, Параметры);
			Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
				РезультатОперации = Параметры.ПараметрыОперации;
				РезультатОперации.Вставить("Результат", Истина);
				РезультатОперации.Вставить("ОписаниеОшибки");
				ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		// Эквайринговая операция не была произведена.
		НачатьОтключениеЭквайринговогоТерминала(, Параметры.УникальныйИдентификатор, Параметры);
		Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
			ТекстСообщения = НСтр("ru='Эквайринговая операция не была произведена: %ДополнительноеОписание%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", РезультатВыполнения.ВыходныеПараметры[1]);
			РезультатОперации = ПараметрыРезультатаПодключенияЭквайринговогоТерминала(Ложь, ТекстСообщения);
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьВыполнениеОперацииНаЭквайринговомТерминале_ПослеЗакрытияВопроса(Результат, РезультатВыполнения) Экспорт
	
	Параметры = РезультатВыполнения.Параметры; 
	Если Результат = КодВозвратаДиалога.Отмена Тогда
		Параметры.Вставить("ОписаниеОшибки", РезультатВыполнения.ОписаниеОшибки);
		
		РезультатОперации = Параметры.ПараметрыОперации;
		ВходныеПараметры = Новый Массив();
		ВходныеПараметры.Добавить(РезультатОперации.СуммаОперации);
		ВходныеПараметры.Добавить(РезультатОперации.СсылочныйНомер);
		ВходныеПараметры.Добавить(РезультатОперации.НомерЧека);
		// Слип чек не распечатан, необходимо выполнить аварийную отмену операции.
		Оповещение = ОписаниеОповещенияИС("НачатьВыполнениеОперацииНаЭквайринговомТерминале_ОшибкаПечатиЗавершение", МенеджерОборудованияКлиент, Параметры);
		НачатьВыполнениеКоманды(Оповещение, Параметры.ИдентификаторУстройстваЭТ, "EmergencyVoid", ВходныеПараметры);
	Иначе
		ПараметрыОперации = Параметры.ПараметрыОперации;
		Оповещение = ОписаниеОповещенияИС("НачатьВыполнениеОперацииНаЭквайринговомТерминале_ПечатьЗавершение", МенеджерОборудованияКлиент, Параметры);
		НачатьПечатьТекста(Оповещение, Параметры.УникальныйИдентификатор, ПараметрыОперации.ТекстСлипЧека, Параметры.ИдентификаторУстройстваПУ);
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьВыполнениеОперацииНаЭквайринговомТерминале_ПечатьЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если РезультатВыполнения.Результат Тогда  
		// Слип чек распечатан успешно.
		НачатьОтключениеЭквайринговогоТерминала(, Параметры.УникальныйИдентификатор, Параметры);
		Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
			РезультатОперации = Параметры.ПараметрыОперации;
			РезультатОперации.Вставить("Результат", Истина);
			РезультатОперации.Вставить("ОписаниеОшибки");
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
		КонецЕсли;
	Иначе
		РезультатВыполнения.Вставить("Параметры", Параметры);
		Режим = РежимДиалогаВопрос.ПовторитьОтмена;
		СообщениеНаЭкране = РезультатВыполнения.ОписаниеОшибки  + Символы.ПС +
			НСтр("ru = 'Повторить печать слип-чека?'") + Символы.ПС +
			НСтр("ru = 'ВНИМАНИЕ:'") + Символы.ПС +
			НСтр("ru = 'При отмене печати слип-чека выполнится аварийная отмена операции на экваринговом терминале.'") + Символы.ПС +
			НСтр("ru = 'Подготовьте банковскую карту покупателя.'");
		Оповещение = ОписаниеОповещенияИС("НачатьВыполнениеОперацииНаЭквайринговомТерминале_ПослеЗакрытияВопроса", МенеджерОборудованияКлиент, РезультатВыполнения);
		ПоказатьВопросИС(Оповещение, СообщениеНаЭкране, Режим, ,, НСтр("ru = 'При печати слип-чека произошла ошибка'"));
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьВыполнениеОперацииНаЭквайринговомТерминале_ОшибкаПечатиЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	НачатьОтключениеЭквайринговогоТерминала(, Параметры.УникальныйИдентификатор, Параметры);
	
	Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
		Если РезультатВыполнения.Результат Тогда 
			// Слип чек не распечатан, аварийная отмена операции произведена успешно.
			ТекстСообщения = НСтр("ru='Эквайринговая операция отменена - ошибка печати слип чека: %ДополнительноеОписание%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", Параметры.ОписаниеОшибки);
		Иначе
			// Ошибка при аварийной отмене операции.
			ТекстСообщения = НСтр("ru='Ошибка отмены операции транзакции. Обратитесь в банк. %ДополнительноеОписание%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", РезультатВыполнения.ОписаниеОшибки);
		КонецЕсли;
		РезультатОперации = ПараметрыРезультатаПодключенияЭквайринговогоТерминала(Ложь, ТекстСообщения);
		ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииРаботыСДисплеямиПокупателя

// Начать вывод тестовых строк на подключенные дисплеи покупателя.
//
Процедура НачатьВыводИнформацииНаДисплейПокупателя(ОповещениеПриЗавершении, УникальныйИдентификатор, ИдентификаторУстройства = Неопределено, СтрокиТекста = Неопределено) Экспорт
	
	ПодключенныеУстройства = ПолучитьПодключенныеУстройства(глПодключаемоеОборудование.ПараметрыПодключенияПО, 
		ПредопределенноеЗначение("Перечисление.ТипыПодключаемогоОборудования.ДисплейПокупателя"), ИдентификаторУстройства);
	
	Если ПодключенныеУстройства.Количество() > 0 Тогда
		Для Каждого Устройство Из ПодключенныеУстройства Цикл
			Контекст = Новый Структура;
			Контекст.Вставить("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
			Контекст.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
			Контекст.Вставить("ИдентификаторУстройства", Устройство.Ссылка);
			Оповещение = ОписаниеОповещенияИС("НачатьОперациюНаДисплейПокупателя_ВыполнитьКомандуЗавершение", МенеджерОборудованияКлиент, Контекст);
			
			ВходныеПараметры  = Новый Массив();
			ВходныеПараметры.Добавить(СтрокиТекста);
			НачатьВыполнениеКоманды(Оповещение, Устройство.Ссылка, "DisplayText", ВходныеПараметры);
		КонецЦикла
	ИначеЕсли ИдентификаторУстройства <> Неопределено И ОповещениеПриЗавершении <> Неопределено Тогда
		ОписаниеОшибки = НСтр("ru='Нет подключенных дисплеев покупателя.'");
		РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки, ИдентификаторУстройства);
		ВыполнитьОбработкуОповещенияИС(ОповещениеПриЗавершении, РезультатОперации);
	КонецЕсли;
	
КонецПроцедуры

// Начать очистку подключенных дисплеев покупателя.
//
Процедура НачатьОчисткуДисплеяПокупателя(ОповещениеПриЗавершении, УникальныйИдентификатор, ИдентификаторУстройства = Неопределено) Экспорт
	
	ПодключенныеУстройства = ПолучитьПодключенныеУстройства(глПодключаемоеОборудование.ПараметрыПодключенияПО, 
		ПредопределенноеЗначение("Перечисление.ТипыПодключаемогоОборудования.ДисплейПокупателя"), ИдентификаторУстройства);
	
	Если ПодключенныеУстройства.Количество() > 0 Тогда
		Для Каждого Устройство Из ПодключенныеУстройства Цикл
			Контекст = Новый Структура;
			Контекст.Вставить("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
			Контекст.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
			Контекст.Вставить("ИдентификаторУстройства", Устройство.Ссылка);
			Оповещение = ОписаниеОповещенияИС("НачатьОперациюНаДисплейПокупателя_ВыполнитьКомандуЗавершение", МенеджерОборудованияКлиент, Контекст);
			ВходныеПараметры  = Неопределено;
			НачатьВыполнениеКоманды(Оповещение, Устройство.Ссылка, "ClearText", ВходныеПараметры);
		КонецЦикла
	ИначеЕсли ИдентификаторУстройства <> Неопределено И ОповещениеПриЗавершении <> Неопределено Тогда
		ОписаниеОшибки = НСтр("ru='Нет подключенных дисплеев покупателя.'");
		РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки, ИдентификаторУстройства); 
		ВыполнитьОбработкуОповещенияИС(ОповещениеПриЗавершении, РезультатОперации);
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьОперациюНаДисплейПокупателя_ВыполнитьКомандуЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если РезультатВыполнения.Результат Тогда
		ОписаниеОшибки = "";
	Иначе
		ОписаниеОшибки = НСтр("ru='При использовании дисплея покупателя произошла ошибка: %ДополнительноеОписание%'");
		ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%ДополнительноеОписание%", РезультатВыполнения.ВыходныеПараметры[1]);
	КонецЕсли;
	
	Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
		РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(РезультатВыполнения.Результат, ОписаниеОшибки, Параметры.ИдентификаторУстройства);
		ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииРаботыСОборудованиемПринтеромЭтикеток

// Начать печать этикеток.
//
Процедура НачатьПечатьЭтикеток(ОповещениеПриЗавершенииПечати, УникальныйИдентификатор, ОписаниеЭтикеткиВXML, ДанныеДляПечати, ИдентификаторУстройства = Неопределено) Экспорт
	
	Контекст = Новый Структура;
	Контекст.Вставить("СледующееОповещение"     , ОповещениеПриЗавершенииПечати);
	Контекст.Вставить("УникальныйИдентификатор" , УникальныйИдентификатор);
	Контекст.Вставить("ОписаниеЭтикеткиВXML"    , ОписаниеЭтикеткиВXML);
	Контекст.Вставить("ДанныеДляПечати"         , ДанныеДляПечати);
	
	Если ИдентификаторУстройства = Неопределено Тогда	
		ОписаниеОповещения = ОписаниеОповещенияИС("НачатьПечатьЭтикеток_ВыбратьУстройствоЗавершение", МенеджерОборудованияКлиент, Контекст);
		ПредложитьВыбратьУстройство(ОписаниеОповещения, "ПринтерЭтикеток",
			НСтр("ru='Выберите принтер этикеток'"), 
			НСтр("ru='Принтер этикеток не подключен.'"), 
			НСтр("ru='Принтер этикеток не выбран.'"), 
			Истина);
	Иначе
		НачатьПечатьЭтикеток_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Контекст);
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьПечатьЭтикеток_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Параметры) Экспорт
	
	Если ИдентификаторУстройства = Неопределено Тогда
		Если Параметры.СледующееОповещение <> Неопределено Тогда
			ОписаниеОшибки = НСтр("ru='Принтер этикеток не выбран.'");
			Результат = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки);
			ВыполнитьОбработкуОповещенияИС(Параметры.СледующееОповещение, Результат);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Параметры.Вставить("ИдентификаторУстройства", ИдентификаторУстройства);
	ОписаниеОповещения = ОписаниеОповещенияИС("НачатьПечатьЭтикеток_ПодключениеЗавершение", МенеджерОборудованияКлиент, Параметры);
	НачатьПодключениеОборудованиеПоИдентификатору(ОписаниеОповещения, Параметры.УникальныйИдентификатор, ИдентификаторУстройства);
	
КонецПроцедуры

Процедура НачатьПечатьЭтикеток_ПодключениеЗавершение(РезультатПодключения, Параметры) Экспорт
	
	Если РезультатПодключения.Результат Тогда
		
		СообщениеОбОшибке = "";
		ОписаниеЭтикеткиВСтруктуре = Неопределено;
		Результат = МенеджерОборудованияВызовСервера.ПолучитьОписаниеМакета(Параметры.ОписаниеЭтикеткиВXML, СообщениеОбОшибке, ОписаниеЭтикеткиВСтруктуре);
		
		Если Не Результат Тогда
			Если Параметры.СледующееОповещение <> Неопределено Тогда
				ОписаниеОшибки = НСтр("ru='При обработке макета этикетки произошла ошибка.
									  |%ОписаниеОшибки%
									  |Печать прервана.'");
				ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%ОписаниеОшибки%", СообщениеОбОшибке);
				РезультатВыполнения = Новый Структура("Результат, ОписаниеОшибки, ИдентификаторУстройства", Ложь, ОписаниеОшибки, Параметры.ИдентификаторУстройства);
				ВыполнитьОбработкуОповещенияИС(Параметры.СледующееОповещение, РезультатВыполнения);
			КонецЕсли;
		Иначе
			ВходныеПараметры = Новый Массив();
			ВходныеПараметры.Добавить(ОписаниеЭтикеткиВСтруктуре);
			ВходныеПараметры.Добавить(Параметры.ДанныеДляПечати);
			ОписаниеОповещения = ОписаниеОповещенияИС("НачатьПечатьЭтикеток_ВыполнитьКомандуЗавершение", МенеджерОборудованияКлиент, Параметры);
			НачатьВыполнениеКоманды(ОписаниеОповещения, Параметры.ИдентификаторУстройства, "ПечатьЭтикеток", ВходныеПараметры);
		КонецЕсли;
		
	ИначеЕсли Параметры.СледующееОповещение <> Неопределено Тогда
		ОписаниеОшибки = НСтр("ru='При подключении устройства произошла ошибка.
							  |%ОписаниеОшибки%'");
		ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%ОписаниеОшибки%", РезультатПодключения.ОписаниеОшибки);
		РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки, Параметры.ИдентификаторУстройства); 
		ВыполнитьОбработкуОповещенияИС(Параметры.СледующееОповещение, РезультатВыполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьПечатьЭтикеток_ВыполнитьКомандуЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если РезультатВыполнения.Результат Тогда
		ОписаниеОшибки = НСтр("ru='Ошибок нет.'");
	Иначе
		ОписаниеОшибки = НСтр("ru='При работе с принтером этикеток произошла ошибка.
								  |%ОписаниеОшибки%'");
		ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%ОписаниеОшибки%", РезультатВыполнения.ВыходныеПараметры[1]);
	КонецЕсли;
		
	Если Параметры.СледующееОповещение <> Неопределено Тогда
		Результат = ПараметрыВыполненияОперацииНаОборудовании(РезультатВыполнения.Результат, ОписаниеОшибки, Параметры.ИдентификаторУстройства); 
		ВыполнитьОбработкуОповещенияИС(Параметры.СледующееОповещение, Результат);
	КонецЕсли;
	
	НачатьОтключениеОборудованиеПоИдентификатору(Неопределено, Параметры.УникальныйИдентификатор, Параметры.ИдентификаторУстройства);
	
КонецПроцедуры

// Начать инициализацию принтера этикеток.
//
Процедура НачатьИнициализацияПринтераЭтикеток(ОповещениеПриЗавершенииПечати, УникальныйИдентификатор, ИдентификаторУстройства = Неопределено) Экспорт
	
	Контекст = Новый Структура;
	Контекст.Вставить("СледующееОповещение"     , ОповещениеПриЗавершенииПечати);
	Контекст.Вставить("УникальныйИдентификатор" , УникальныйИдентификатор);
	
	Если ИдентификаторУстройства = Неопределено Тогда	
		ОписаниеОповещения = ОписаниеОповещенияИС("НачатьИнициализацияПринтераЭтикеток_ВыбратьУстройствоЗавершение", МенеджерОборудованияКлиент, Контекст);
		ПредложитьВыбратьУстройство(ОписаниеОповещения, "ПринтерЭтикеток",
			НСтр("ru='Выберите принтер этикеток'"), 
			НСтр("ru='Принтер этикеток не подключен.'"), 
			НСтр("ru='Принтер этикеток не выбран.'"), 
			Истина);
	Иначе
		НачатьИнициализацияПринтераЭтикеток_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Контекст);
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьИнициализацияПринтераЭтикеток_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Параметры) Экспорт
	
	Если ИдентификаторУстройства = Неопределено Тогда
		Если Параметры.СледующееОповещение <> Неопределено Тогда
			ОписаниеОшибки = НСтр("ru='Принтер этикеток не выбран.'");
			Результат = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки);
			ВыполнитьОбработкуОповещенияИС(Параметры.СледующееОповещение, Результат);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Параметры.Вставить("ИдентификаторУстройства", ИдентификаторУстройства);
	ОписаниеОповещения = ОписаниеОповещенияИС("НачатьИнициализацияПринтераЭтикеток_ПодключениеЗавершение", МенеджерОборудованияКлиент, Параметры);
	НачатьПодключениеОборудованиеПоИдентификатору(ОписаниеОповещения, Параметры.УникальныйИдентификатор, ИдентификаторУстройства);
	
КонецПроцедуры

Процедура НачатьИнициализацияПринтераЭтикеток_ПодключениеЗавершение(РезультатПодключения, Параметры) Экспорт
	
	Если РезультатПодключения.Результат Тогда
		ВходныеПараметры = Новый Массив();
		ОписаниеОповещения = ОписаниеОповещенияИС("НачатьИнициализацияПринтераЭтикеток_ВыполнитьКомандуЗавершение", МенеджерОборудованияКлиент, Параметры);
		НачатьВыполнениеКоманды(ОписаниеОповещения, Параметры.ИдентификаторУстройства, "ИнициализацияПринтера", ВходныеПараметры);
	ИначеЕсли Параметры.СледующееОповещение <> Неопределено Тогда
		ОписаниеОшибки = НСтр("ru='При подключении устройства произошла ошибка.
							  |%ОписаниеОшибки%'");
		ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%ОписаниеОшибки%", РезультатПодключения.ОписаниеОшибки);
		РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки, Параметры.ИдентификаторУстройства); 
		ВыполнитьОбработкуОповещенияИС(Параметры.СледующееОповещение, РезультатВыполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьИнициализацияПринтераЭтикеток_ВыполнитьКомандуЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если РезультатВыполнения.Результат Тогда
		ОписаниеОшибки = НСтр("ru='Ошибок нет.'");
	Иначе
		ОписаниеОшибки = НСтр("ru='При работе с принтером этикеток произошла ошибка.
								  |%ОписаниеОшибки%'");
		ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%ОписаниеОшибки%", РезультатВыполнения.ВыходныеПараметры[1]);
	КонецЕсли;
		
	Если Параметры.СледующееОповещение <> Неопределено Тогда
		Результат = ПараметрыВыполненияОперацииНаОборудовании(РезультатВыполнения.Результат, ОписаниеОшибки, Параметры.ИдентификаторУстройства); 
		ВыполнитьОбработкуОповещенияИС(Параметры.СледующееОповещение, Результат);
	КонецЕсли;
	
	НачатьОтключениеОборудованиеПоИдентификатору(Неопределено, Параметры.УникальныйИдентификатор, Параметры.ИдентификаторУстройства);
	
КонецПроцедуры

// Начать редактирование макета.
//
Процедура НачатьРедактированиеМакета(ОповещениеПриЗавершенииРедактирования, ОписаниеЭтикеткиВXML, АдресХранилищаСКД) Экспорт
	
	Контекст = Новый Структура;
	Контекст.Вставить("СледующееОповещение", ОповещениеПриЗавершенииРедактирования);
	Контекст.Вставить("XMLОписаниеМакета"  , ОписаниеЭтикеткиВXML);
	ОповещениеПриЗавершении = ОписаниеОповещенияИС("НачатьРедактированиеМакета_Завершение", МенеджерОборудованияКлиент, Контекст);
	
	ИмяФормы = "ОбщаяФорма.РедакторЭтикетокФормаРедактированияМакета";
	Попытка
		ОткрытьФормуИС(ИмяФормы, Новый Структура("АдресХранилищаСКД", АдресХранилищаСКД),,,,, ОповещениеПриЗавершении);
	Исключение
		ТекстСообщения = НСтр("ru='Использование редактора этикеток невозможно! Функциональность не поддерживается.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);;
	КонецПопытки;
	
КонецПроцедуры

Процедура НачатьРедактированиеМакета_Завершение(Результат, ДополнительныеПараметры) Экспорт
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("РазмерМакета",      ?(Результат <> Неопределено, ДополнительныеПараметры.РазмерМакета, Неопределено));
	ДопПараметры.Вставить("XMLОписаниеМакета", ?(Результат <> Неопределено, ДополнительныеПараметры.XMLОписаниеМакета, Неопределено));
	ДопПараметры.Вставить("Поля",              ?(Результат <> Неопределено, ДополнительныеПараметры.Поля, Неопределено));
	
	ОписаниеОповещения = ОписаниеОповещенияИС(ДополнительныеПараметры.СледующееОповещение.ИмяПроцедуры, ДополнительныеПараметры.СледующееОповещение.Модуль, ДопПараметры);
	ВыполнитьОбработкуОповещенияИС(ОписаниеОповещения, ?(Результат <> Неопределено, Результат, КодВозвратаДиалога.Отмена));
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииРаботыCВесамиСПечатьюЭтикеток

// Начать выгрузку данных в терминал сбора данных.
//
Процедура НачатьВыгрузкуДанныеВВесыСПечатьюЭтикеток(ОповещениеПриЗавершении, УникальныйИдентификатор, ТаблицаВыгрузкиТоваров, ИдентификаторУстройства = Неопределено, ЧастичнаяВыгрузка = Ложь, ОтображатьСообщения = Ложь) Экспорт
	
	Если ТаблицаВыгрузкиТоваров.Количество() = 0 Тогда
		
		ТекстСообщения = НСтр("ru='Нет данных для выгрузки.'");
		Если ОтображатьСообщения Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);;
		КонецЕсли;
		
		Если ОповещениеПриЗавершении <> Неопределено Тогда
			РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании();
			РезультатОперации.Результат = Ложь;
			РезультатОперации.ОписаниеОшибки = ТекстСообщения;
			РезультатОперации.ИдентификаторУстройства = ИдентификаторУстройства;
			ВыполнитьОбработкуОповещенияИС(ОповещениеПриЗавершении, РезультатОперации);
		КонецЕсли;
		Возврат;
	КонецЕсли;

	Контекст = Новый Структура; 
	Контекст.Вставить("СледующееОповещение"     , ОповещениеПриЗавершении);
	Контекст.Вставить("УникальныйИдентификатор" , УникальныйИдентификатор);
	Контекст.Вставить("ТаблицаВыгрузкиТоваров"  , ТаблицаВыгрузкиТоваров);
	Контекст.Вставить("ЧастичнаяВыгрузка"       , ЧастичнаяВыгрузка);
	Контекст.Вставить("ОтображатьСообщения"     , ОтображатьСообщения);
	
	Если ИдентификаторУстройства = Неопределено Тогда
		ОписаниеОповещения = ОписаниеОповещенияИС("НачатьВыгрузкуДанныеВВесыСПечатьюЭтикеток_ВыбратьУстройствоЗавершение", МенеджерОборудованияКлиент, Контекст);
		ПредложитьВыбратьУстройство(ОписаниеОповещения, "ВесыСПечатьюЭтикеток",
			НСтр("ru='Выберите весы с печатью этикеток'"),
			НСтр("ru='Весы с печатью этикеток не подключены.'"), 
			НСтр("ru='Весы с печатью этикеток не выбраны.'"), 
			Истина);
	Иначе
		НачатьВыгрузкуДанныеВВесыСПечатьюЭтикеток_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Контекст);
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьВыгрузкуДанныеВВесыСПечатьюЭтикеток_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Параметры) Экспорт
	
	Если ИдентификаторУстройства = Неопределено Тогда
		Если Параметры.СледующееОповещение <> Неопределено Тогда
			ТекстСообщения = НСтр("ru='Весы с печатью этикеток не выбраны.'");
			Результат = Новый Структура("Результат, ОписаниеОшибки, ИдентификаторУстройства", Ложь, ТекстСообщения, Неопределено);
			ВыполнитьОбработкуОповещенияИС(Параметры.СледующееОповещение, Результат);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Параметры.Вставить("ИдентификаторУстройства", ИдентификаторУстройства);
#Если ВебКлиент Тогда
	ОписаниеОповещения = ОписаниеОповещенияИС("НачатьВыгрузкуДанныеВВесыСПечатьюЭтикеток_ФайловоеРасширениеЗавершение", МенеджерОборудованияКлиент, Параметры);
	ПроверитьДоступностьРасширенияРаботыСФайлами(ОписаниеОповещения, Истина);
#Иначе
	// В тонком и толстом клиентах расширение подключено всегда.
	НачатьВыгрузкуДанныеВВесыСПечатьюЭтикеток_ФайловоеРасширениеЗавершение(Истина, Параметры);
#КонецЕсли
	
КонецПроцедуры

Процедура НачатьВыгрузкуДанныеВВесыСПечатьюЭтикеток_ФайловоеРасширениеЗавершение(Подключено, Параметры) Экспорт
	
	Если Подключено Тогда
		ОписаниеОповещения = ОписаниеОповещенияИС("НачатьВыгрузкуДанныеВВесыСПечатьюЭтикеток_ПодключениеЗавершение", МенеджерОборудованияКлиент, Параметры);
		НачатьПодключениеОборудованиеПоИдентификатору(ОписаниеОповещения, Параметры.УникальныйИдентификатор, Параметры.ИдентификаторУстройства);
	Иначе
		ТекстСообщения = НСтр("ru='Данная операция не доступна без установленного расширения для веб-клиента ""1С:Предприятие"".'");
		Если Параметры.ОтображатьСообщения Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		Если Параметры.СледующееОповещение <> Неопределено Тогда
			РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ТекстСообщения, Параметры.ИдентификаторУстройства);
			ВыполнитьОбработкуОповещенияИС(Параметры.СледующееОповещение, РезультатОперации);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьВыгрузкуДанныеВВесыСПечатьюЭтикеток_ПодключениеЗавершение(РезультатПодключения, Параметры) Экспорт
	
	Если РезультатПодключения.Результат Тогда
		
		ВходныеПараметры  = Новый Массив;
		ВходныеПараметры.Добавить(Параметры.ТаблицаВыгрузкиТоваров);
		ВходныеПараметры.Добавить(Параметры.ЧастичнаяВыгрузка); // Частичная выгрузка.
		
		ОписаниеОповещения = ОписаниеОповещенияИС("НачатьВыгрузкуДанныеВВесыСПечатьюЭтикеток_ВыполнитьКомандуЗавершение", МенеджерОборудованияКлиент, Параметры);
		НачатьВыполнениеКоманды(ОписаниеОповещения, Параметры.ИдентификаторУстройства, "ВыгрузитьТовары", ВходныеПараметры);
		
	Иначе
		ТекстСообщения = НСтр("ru='При подключении устройства произошла ошибка.
								  |%ОписаниеОшибки%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%", РезультатПодключения.ОписаниеОшибки);
		
		Если Параметры.ОтображатьСообщения Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		
		Если Параметры.СледующееОповещение <> Неопределено Тогда
			РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ТекстСообщения, Параметры.ИдентификаторУстройства);
			ВыполнитьОбработкуОповещенияИС(Параметры.СледующееОповещение, РезультатОперации);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьВыгрузкуДанныеВВесыСПечатьюЭтикеток_ВыполнитьКомандуЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	ТекстСообщения = НСтр("ru='Ошибок нет.'");
	
	Если НЕ РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр("ru='При выгрузке данных в оборудование произошла ошибка.
								  |%ОписаниеОшибки%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%", РезультатВыполнения.ВыходныеПараметры[1]);
		Если Параметры.ОтображатьСообщения Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);;
		КонецЕсли;
	КонецЕсли;
		
	Если Параметры.СледующееОповещение <> Неопределено Тогда
		РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(РезультатВыполнения.Результат, ТекстСообщения, Параметры.ИдентификаторУстройства);
		ВыполнитьОбработкуОповещенияИС(Параметры.СледующееОповещение, РезультатОперации);
	КонецЕсли;
	
	НачатьОтключениеОборудованиеПоИдентификатору(Неопределено, Параметры.УникальныйИдентификатор, Параметры.ИдентификаторУстройства);
	
КонецПроцедуры

// Начать очистку товаров в весах с печатью этикеток.
//
Процедура НачатьОчисткуТоваровВВесахСПечатьюЭтикеток(ОповещениеПриЗавершении, УникальныйИдентификатор, ИдентификаторУстройства = Неопределено, ОтображатьСообщения = Ложь) Экспорт
	
	Контекст = Новый Структура;
	Контекст.Вставить("СледующееОповещение"     , ОповещениеПриЗавершении);
	Контекст.Вставить("УникальныйИдентификатор" , УникальныйИдентификатор);
	Контекст.Вставить("ОтображатьСообщения"     , ОтображатьСообщения);
	
	Если ИдентификаторУстройства = Неопределено Тогда
		ОписаниеОповещения = ОписаниеОповещенияИС("НачатьОчисткуТоваровВВесахСПечатьюЭтикеток_ВыбратьУстройствоЗавершение", МенеджерОборудованияКлиент, Контекст);
		ПредложитьВыбратьУстройство(ОписаниеОповещения, "ВесыСПечатьюЭтикеток",
			НСтр("ru='Выберите весы с печатью этикеток'"), 
			НСтр("ru='Весы с печатью этикеток не подключены.'"),
			НСтр("ru='Весы с печатью этикеток не выбраны.'"), 
			Истина);
	Иначе
		НачатьОчисткуТоваровВВесахСПечатьюЭтикеток_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Контекст);
	КонецЕсли;
		
КонецПроцедуры

Процедура НачатьОчисткуТоваровВВесахСПечатьюЭтикеток_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Параметры) Экспорт
	
	Если ИдентификаторУстройства = Неопределено Тогда
		Если Параметры.СледующееОповещение <> Неопределено Тогда
			ОписаниеОшибки = НСтр("ru='Весы с печатью этикеток не выбраны.'");
			РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки, Неопределено);
			ВыполнитьОбработкуОповещенияИС(Параметры.СледующееОповещение, РезультатОперации);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Параметры.Вставить("ИдентификаторУстройства", ИдентификаторУстройства);
	ОписаниеОповещения = ОписаниеОповещенияИС("НачатьОчисткуТоваровВВесахСПечатьюЭтикеток_ПодключениеЗавершение", МенеджерОборудованияКлиент, Параметры);
	НачатьПодключениеОборудованиеПоИдентификатору(ОписаниеОповещения, Параметры.УникальныйИдентификатор, ИдентификаторУстройства);
	
КонецПроцедуры

Процедура НачатьОчисткуТоваровВВесахСПечатьюЭтикеток_ПодключениеЗавершение(РезультатПодключения, Параметры) Экспорт
	
	ТекстСообщения = НСтр("ru='Ошибок нет.'");
	
	Если РезультатПодключения.Результат Тогда
		ВходныеПараметры  = Неопределено;
		ОписаниеОповещения = ОписаниеОповещенияИС("НачатьОчисткуТоваровВВесахСПечатьюЭтикеток_ВыполнитьКомандуЗавершение", МенеджерОборудованияКлиент, Параметры);
		НачатьВыполнениеКоманды(ОписаниеОповещения, Параметры.ИдентификаторУстройства, "ОчиститьБазу", ВходныеПараметры);
	Иначе
		ОписаниеОшибки = НСтр("ru='При подключении устройства произошла ошибка. |%ОписаниеОшибки%'");
		ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%ОписаниеОшибки%", РезультатПодключения.ОписаниеОшибки);
		
		Если Параметры.ОтображатьСообщения Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
	
		Если Параметры.СледующееОповещение <> Неопределено Тогда
			РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки, Параметры.ИдентификаторУстройства);
			ВыполнитьОбработкуОповещенияИС(Параметры.СледующееОповещение, РезультатОперации);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьОчисткуТоваровВВесахСПечатьюЭтикеток_ВыполнитьКомандуЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если РезультатВыполнения.Результат Тогда
		ОписаниеОшибки = НСтр("ru='Ошибок нет.'");
	Иначе
		ОписаниеОшибки = НСтр("ru='При очистке данных в оборудование произошла ошибка. |%ОписаниеОшибки%'");
		ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%ОписаниеОшибки%", РезультатВыполнения.ВыходныеПараметры[1]);
	КонецЕсли;
	
	Если Параметры.ОтображатьСообщения Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ОписаниеОшибки);
	КонецЕсли;
	
	Если Параметры.СледующееОповещение <> Неопределено Тогда
		РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(РезультатВыполнения.Результат, ОписаниеОшибки, Параметры.ИдентификаторУстройства);
		ВыполнитьОбработкуОповещенияИС(Параметры.СледующееОповещение, РезультатОперации);
	КонецЕсли;
	
	НачатьОтключениеОборудованиеПоИдентификатору(Неопределено, Параметры.УникальныйИдентификатор, Параметры.ИдентификаторУстройства);
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииРаботыСОборудованиемRFID

// Заполняет структуру параметров записи метки RFID.
// 
// Возвращаемое значение:
// Структура - .
Функция ПараметрыЗаписиМеткиRFID() Экспорт; 
	
	Результат = Новый Структура();
	Результат.Вставить("TID"       , Неопределено);
	Результат.Вставить("EPC"       , Неопределено);
	Результат.Вставить("Данные"    , Неопределено);
	Результат.Вставить("БанкПамяти", "EPC");
	Возврат Результат;
	
КонецФункции

// Начать открытие сессии считывателя RFID.
//
Процедура НачатьОткрытиеСессииСчитывателяRFID(ОповещениеПриЗавершении, УникальныйИдентификатор, ИдентификаторУстройства = Неопределено) Экспорт
	
	ПодключенныеУстройства = ПолучитьПодключенныеУстройства(глПодключаемоеОборудование.ПараметрыПодключенияПО, 
		ПредопределенноеЗначение("Перечисление.ТипыПодключаемогоОборудования.СчитывательRFID"), ИдентификаторУстройства);
	
	Если ПодключенныеУстройства.Количество() > 0 Тогда
		Для Каждого Устройство Из ПодключенныеУстройства Цикл
			Контекст = Новый Структура;
			Контекст.Вставить("ОповещениеПриЗавершении" , ОповещениеПриЗавершении);
			Контекст.Вставить("УникальныйИдентификатор" , УникальныйИдентификатор);
			Контекст.Вставить("ИдентификаторУстройства" , Устройство.Ссылка);
			Оповещение = ОписаниеОповещенияИС("НачатьОткрытиеСессииСчитывателяRFID_ВыполнитьКомандуЗавершение", МенеджерОборудованияКлиент, Контекст);
			ВходныеПараметры  = Новый Массив();
			НачатьВыполнениеКоманды(Оповещение, Устройство.Ссылка, "OpenSessionRFID", ВходныеПараметры);
		КонецЦикла
	ИначеЕсли  ОповещениеПриЗавершении <> Неопределено Тогда
		ОписаниеОшибки = НСтр("ru='Нет подключенных RFID считывателей.'");
		РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки);
		ВыполнитьОбработкуОповещенияИС(ОповещениеПриЗавершении, РезультатОперации);
	КонецЕсли;
		
КонецПроцедуры

Процедура НачатьОткрытиеСессииСчитывателяRFID_ВыполнитьКомандуЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если РезультатВыполнения.Результат Тогда
		ОписаниеОшибки = "";
	Иначе
		ОписаниеОшибки = НСтр("ru='При работе со считывателем RFID произошла ошибка: %ДополнительноеОписание%'");
		ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%ДополнительноеОписание%", РезультатВыполнения.ВыходныеПараметры[1]);
	КонецЕсли;
	
	Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
		РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(РезультатВыполнения.Результат, ОписаниеОшибки, Параметры.ИдентификаторУстройства);
		ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
	КонецЕсли;
	
КонецПроцедуры

// Начать закрытие сессии считывателя RFID.
//
Процедура НачатьЗакрытиеСессииСчитывателяRFID(ОповещениеПриЗавершении, УникальныйИдентификатор, ИдентификаторУстройства = Неопределено) Экспорт
	
	ПодключенныеУстройства = ПолучитьПодключенныеУстройства(глПодключаемоеОборудование.ПараметрыПодключенияПО, 
		ПредопределенноеЗначение("Перечисление.ТипыПодключаемогоОборудования.СчитывательRFID"), ИдентификаторУстройства);
	
	Если ПодключенныеУстройства.Количество() > 0 Тогда
		Для Каждого Устройство Из ПодключенныеУстройства Цикл
			Контекст = Новый Структура;
			Контекст.Вставить("ОповещениеПриЗавершении" , ОповещениеПриЗавершении);
			Контекст.Вставить("УникальныйИдентификатор" , УникальныйИдентификатор);
			Контекст.Вставить("ИдентификаторУстройства" , Устройство.Ссылка);
			Оповещение = ОписаниеОповещенияИС("НачатьЗакрытиеСессииСчитывателяRFID_ВыполнитьКомандуЗавершение", МенеджерОборудованияКлиент, Контекст);
			
			ВходныеПараметры  = Новый Массив();
			НачатьВыполнениеКоманды(Оповещение, Устройство.Ссылка, "CloseSessionRFID", ВходныеПараметры);
		КонецЦикла
	ИначеЕсли  ОповещениеПриЗавершении <> Неопределено Тогда
		ОписаниеОшибки = НСтр("ru='Нет подключенных RFID считывателей.'");
		РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки);
		ВыполнитьОбработкуОповещенияИС(ОповещениеПриЗавершении, РезультатОперации);
	КонецЕсли;
		
КонецПроцедуры

Процедура НачатьЗакрытиеСессииСчитывателяRFID_ВыполнитьКомандуЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если РезультатВыполнения.Результат Тогда
		ОписаниеОшибки = "";
	Иначе
		ОписаниеОшибки = НСтр("ru='При работе со считывателем RFID произошла ошибка: %ДополнительноеОписание%'");
		ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%ДополнительноеОписание%", РезультатВыполнения.ВыходныеПараметры[1]);
	КонецЕсли;
	
	Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
		РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(РезультатВыполнения.Результат, ОписаниеОшибки, Параметры.ИдентификаторУстройства);
		ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьЗаписьДанныхВМеткуRFID(ОповещениеПриЗавершении, УникальныйИдентификатор, ИдентификаторУстройства, ПараметрыЗаписи, Таймаут = 0) Экспорт 
	
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеПриЗавершении" , ОповещениеПриЗавершении);
	Контекст.Вставить("УникальныйИдентификатор" , УникальныйИдентификатор);
	Контекст.Вставить("ПараметрыЗаписи"         , ПараметрыЗаписи);
	Контекст.Вставить("Таймаут"                 , Таймаут);
	
	Если ИдентификаторУстройства = Неопределено Тогда
		ОписаниеОповещения = ОписаниеОповещенияИС("НачатьЗаписьДанныхВМеткуRFID_ВыбратьУстройствоЗавершение", МенеджерОборудованияКлиент, Контекст);
		ПредложитьВыбратьУстройство(ОписаниеОповещения, "СчитывательRFID",
			НСтр("ru='Выберите RFID считыватель'"), НСтр("ru='Нет подключенных RFID считывателей.'"));
	Иначе
		НачатьЗаписьДанныхВМеткуRFID_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Контекст);
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьЗаписьДанныхВМеткуRFID_ВыбратьУстройствоЗавершение(ИдентификаторУстройства, Параметры) Экспорт
	
	Если ИдентификаторУстройства = Неопределено Тогда
		ОписаниеОшибки = НСтр("ru='RFID считыватель  не подключен.'");
		Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
			РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании();
			РезультатОперации.Результат = Ложь;
			РезультатОперации.ОписаниеОшибки = ОписаниеОшибки;
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Параметры.Вставить("ИдентификаторУстройства", ИдентификаторУстройства);
	
	ВходныеПараметры  = Новый Массив();
	ВходныеПараметры.Добавить(Параметры.ПараметрыЗаписи);
	ВходныеПараметры.Добавить(Параметры.Таймаут);
	
	Оповещение = ОписаниеОповещенияИС("НачатьЗаписьДанныхВМеткуRFID_ВыполнитьКомандуЗавершение", МенеджерОборудованияКлиент, Параметры);
	НачатьВыполнениеКоманды(Оповещение, ИдентификаторУстройства, "SaveDataTagRFID", ВходныеПараметры);
	
КонецПроцедуры

Процедура НачатьЗаписьДанныхВМеткуRFID_ВыполнитьКомандуЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	ОписаниеОшибки = ""; 
	Если НЕ РезультатВыполнения.Результат Тогда
		ОписаниеОшибки = НСтр("ru='При работе со считывателем RFID произошла ошибка: %ОписаниеОшибки%'");
		ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%ОписаниеОшибки%", РезультатВыполнения.ВыходныеПараметры[1]);
	КонецЕсли;
	
	Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
		РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(РезультатВыполнения.Результат, ОписаниеОшибки, Параметры.ИдентификаторУстройства); 
		ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыРаботыСФайлами

// Процедура получения содержания текстовых файлов.
// Параметры:
//  ИменаФайлов  - Строка, Массив - имя файла или массив с именами файлов.
//  ОписаниеОповещенияПриЗавершении  - ОписаниеОповещения - вызывается после завершения чтения файлов.
//  Кодировка - КодировкаТекста - кодировка при чтении текстового файла, по умолчанию КодировкаТекста.UTF8.
//
Процедура ПолучитьСодержаниеТекстовыхФайлов(ИменаФайлов, ОписаниеОповещенияПриЗавершении, Кодировка = Неопределено) Экспорт
	
	ПомещаемыеФайлы = МенеджерОборудованияВызовСервера.ПолучитьПомещаемыеФайлы(ИменаФайлов, Кодировка); //Массив из ОписаниеПередаваемогоФайла
	
	#Если ВебКлиент Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ОписаниеОповещенияПриЗавершении", ОписаниеОповещенияПриЗавершении);
		ДополнительныеПараметры.Вставить("Кодировка", Кодировка);
		
		ОповещениеНачатьПомещениеФайла = ОписаниеОповещенияИС("ПолучитьСодержаниеТекстовыхФайловЗавершение", МенеджерОборудованияКлиент, ДополнительныеПараметры);
		НачатьПомещениеФайлов(ОповещениеНачатьПомещениеФайла, ПомещаемыеФайлы,, Ложь);
		
	#Иначе
		
		РезультатЧтенияФайлов = МенеджерОборудованияВызовСервера.ПолучитьСодержаниеТекстовыхФайлов(ПомещаемыеФайлы, Кодировка);
		ВыполнитьОбработкуОповещенияИС(ОписаниеОповещенияПриЗавершении, РезультатЧтенияФайлов);
		
	#КонецЕсли
	
КонецПроцедуры

// Процедура оповещения получения содержания текстовых файлов.
// Вызывается из процедуры ПолучитьСодержаниеТекстовыхФайлов.
//
Процедура ПолучитьСодержаниеТекстовыхФайловЗавершение(ПомещенныеФайлы, ДополнительныеПараметры) Экспорт
	
	РезультатЧтенияФайлов = Новый Структура;
	РезультатЧтенияФайлов.Вставить("СодержаниеФайлов", Неопределено);
	РезультатЧтенияФайлов.Вставить("Успешно", Ложь);
	РезультатЧтенияФайлов.Вставить("ТекстОшибки", "");
	
	Если ПомещенныеФайлы = Неопределено Тогда
		РезультатЧтенияФайлов.ТекстОшибки = НСтр("ru = 'Неизвестная ошибка при передаче файлов на сервер.'");
	Иначе
		Если Не ПомещенныеФайлы.Количество() = 0 Тогда
			РезультатИзвлеченияТекста = МенеджерОборудованияВызовСервера.ПолучитьСодержаниеТекстовыхФайловИзХранилища(
				ПомещенныеФайлы, ДополнительныеПараметры.Кодировка);
			ЗаполнитьЗначенияСвойств(РезультатЧтенияФайлов, РезультатИзвлеченияТекста);
		КонецЕсли;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещенияИС(ДополнительныеПараметры.ОписаниеОповещенияПриЗавершении, РезультатЧтенияФайлов);
	
КонецПроцедуры

Процедура ПредложитьВыбратьСмену(ОповещениеВыбора, ТекстЗаголовкаВыбора,
	СообщениеНеПодключен = "", СообщениеНеВыбран = "", БезСообщений = Ложь, ТекстСообщения = "") Экспорт
	
	Если Не ОбновитьРабочееМестоКлиента() Тогда
		ТекстСообщения = НСтр("ru='Предварительно необходимо выбрать рабочее место подключаемого оборудования текущего сеанса.'");
		Если Не БезСообщений Тогда
		      ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);;
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	СтруктураПараметрыОтбора = Новый Структура();
	СтруктураПараметрыОтбора.Вставить("Статус", ПредопределенноеЗначение("Перечисление.СтатусыКассовойСмены.Открыта"));
	
	СтруктураРезультата = Новый Структура();
	СтруктураРезультата.Вставить("КассоваяСмена", "Ссылка");
	СтруктураРезультата.Вставить("ИдентификаторУстройства", "ФискальноеУстройство");
	
	Контекст = Новый Структура;
	Контекст.Вставить("СледующееОповещение" , ОповещениеВыбора);
	Контекст.Вставить("СообщениеНеВыбран"   , ?(ПустаяСтрока(СообщениеНеВыбран), СообщениеНеПодключен, СообщениеНеВыбран));
	Контекст.Вставить("БезСообщений"        , БезСообщений);
	Контекст.Вставить("ТекстЗаголовкаВыбора", ТекстЗаголовкаВыбора);
	
	ПараметрыОткрытия = Новый Структура();
	ПараметрыОткрытия.Вставить("БезСообщений"        , БезСообщений);
	ПараметрыОткрытия.Вставить("ТекстЗаголовкаВыбора", ТекстЗаголовкаВыбора);
	ПараметрыОткрытия.Вставить("СтруктураПараметрыОтбора", СтруктураПараметрыОтбора);
	ПараметрыОткрытия.Вставить("СтруктураРезультата", СтруктураРезультата);
	
	ОписаниеОповещения = ОписаниеОповещенияИС("ПредложитьВыбратьСменуЗавершение", МенеджерОборудованияКлиент, Контекст);
	ИмяФормы = "Документ.КассоваяСмена.ФормаВыбора";
	ОткрытьФормуИС(ИмяФормы, ПараметрыОткрытия,,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Процедура ПредложитьВыбратьСменуЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено И НЕ Параметры.БезСообщений И Не ПустаяСтрока(Параметры.СообщениеНеВыбран) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Параметры.СообщениеНеВыбран);
	КонецЕсли;
	
	Если Параметры.СледующееОповещение <> Неопределено Тогда
		ВыполнитьОбработкуОповещенияИС(Параметры.СледующееОповещение, Результат);
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьВыполнениеКоманды_ВыбратьСменуЗавершение(СтруктураПараметровСмены, Параметры) Экспорт
	
	Если СтруктураПараметровСмены = Неопределено Тогда
		Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
			ОписаниеОшибки = НСтр("ru='Кассовая смена не выбрана'");
			РезультатОперации = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки);
			ВыполнитьОбработкуОповещенияИС(Параметры.ОповещениеПриЗавершении, РезультатОперации);
		КонецЕсли;
	Иначе
		Параметры.Вставить("КассоваяСмена", СтруктураПараметровСмены.КассоваяСмена);
		Параметры.Вставить("ИдентификаторУстройства", СтруктураПараметровСмены.ИдентификаторУстройства);
		ОписаниеОповещения = ОписаниеОповещенияИС("НачатьВыполнениеКоманды_ПодключениеЗавершение", МенеджерОборудованияКлиент, Параметры);
		НачатьПодключениеОборудованиеПоИдентификатору(ОписаниеОповещения, Параметры.УникальныйИдентификатор, СтруктураПараметровСмены.ИдентификаторУстройства);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти  

#Область СлужебныйПрограммныйИнтерфейс

#Область ККТ

// Функция возвращает идентификатор открытой сессии для фискального устройства.
// 
// Возвращаемое значение:
// УникальныйИдентификатор - .
Функция СессияПроверкиКодовМаркировки(ИдентификаторУстройства) Экспорт
	
	ИдентификаторСессии = Неопределено;
	ФискальныеУстройства = глПодключаемоеОборудование.ФискальныеУстройства;
	
	Для Каждого ФискальноеУстройство Из ФискальныеУстройства Цикл
		Если ФискальноеУстройство.ИдентификаторУстройства = ИдентификаторУстройства Тогда
			ИдентификаторСессии = ФискальноеУстройство.ИдентификаторСессии;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ИдентификаторСессии;
	
КонецФункции

// Процедура сохраняет идентификатор открытой сессии для фискального устройства.
//
Процедура УстановитьСессиюПроверкиКодовМаркировки(ИдентификаторУстройства, ИдентификаторСессии = Неопределено, ИдентификаторЗапроса = Неопределено) Экспорт
	
	ФискальныеУстройства = глПодключаемоеОборудование.ФискальныеУстройства;
	
	Для Каждого ФискальноеУстройство Из ФискальныеУстройства Цикл
		Если ФискальноеУстройство.ИдентификаторУстройства = ИдентификаторУстройства Тогда
			ФискальноеУстройство.ИдентификаторСессии = ИдентификаторСессии;
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	ФискальноеУстройство = Новый Структура();
	ФискальноеУстройство.Вставить("ИдентификаторУстройства", ИдентификаторУстройства);
	ФискальноеУстройство.Вставить("ИдентификаторСессии" , ИдентификаторСессии);
	ФискальноеУстройство.Вставить("ИдентификаторЗапроса", ИдентификаторЗапроса);
	ФискальныеУстройства.Добавить(ФискальноеУстройство);
	
КонецПроцедуры

// Функция возвращает проверяет ли устройство код маркировки.
// 
// Возвращаемое значение:
// Структура - .
Функция ПроверкаКодаМаркировки(ИдентификаторУстройства) Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("ИдентификаторСессии");
	Результат.Вставить("ИдентификаторЗапроса");
	
	ФискальныеУстройства = глПодключаемоеОборудование.ФискальныеУстройства;
	
	Для Каждого ФискальноеУстройство Из ФискальныеУстройства Цикл
		Если ФискальноеУстройство.ИдентификаторУстройства = ИдентификаторУстройства Тогда
			Результат.ИдентификаторСессии  = ФискальноеУстройство.ИдентификаторСессии;
			Результат.ИдентификаторЗапроса = ФискальноеУстройство.ИдентификаторЗапроса;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Процедура сохраняет статус проверки кода каркировки.
//
Процедура УстановитьПроверкуКодаМаркировки(ИдентификаторУстройства, ИдентификаторЗапроса = Неопределено) Экспорт
	
	ФискальныеУстройства = глПодключаемоеОборудование.ФискальныеУстройства;
	
	Для Каждого ФискальноеУстройство Из ФискальныеУстройства Цикл
		Если ФискальноеУстройство.ИдентификаторУстройства = ИдентификаторУстройства Тогда
			   ФискальноеУстройство.ИдентификаторЗапроса = ИдентификаторЗапроса;
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция РезультатПроверкиКодаМаркировки(ИдентификаторУстройства, ИдентификаторСессии, ПараметрыЗапросаКМ) Экспорт
	
	РезультатПроверки  = Неопределено;
	ПроверкиКодаМаркировки = глПодключаемоеОборудование.ПроверкиКодаМаркировки;
	
	Для Каждого ПроверкаКодаМаркировки Из ПроверкиКодаМаркировки Цикл
		Если ПроверкаКодаМаркировки.ИдентификаторУстройства = ИдентификаторУстройства
			И ПроверкаКодаМаркировки.ИдентификаторСессии = ИдентификаторСессии
			И ПроверкаКодаМаркировки.ПараметрыЗапросаКМ.КонтрольнаяМарка = ПараметрыЗапросаКМ.КонтрольнаяМарка  
			И ПроверкаКодаМаркировки.ПараметрыЗапросаКМ.Количество = ПараметрыЗапросаКМ.Количество
			И ПроверкаКодаМаркировки.ПараметрыЗапросаКМ.ПланируемыйСтатусТовара = ПараметрыЗапросаКМ.ПланируемыйСтатусТовара Тогда
				РезультатПроверки = ПроверкаКодаМаркировки.РезультатПроверки;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат РезультатПроверки;
	
КонецФункции

Процедура УстановитьРезультатПроверкиКодаМаркировки(ИдентификаторУстройства, ИдентификаторСессии, ПараметрыЗапросаКМ, РезультатПроверки, ИдентификаторЗапроса = Неопределено) Экспорт
	
	ФискальныеУстройства = глПодключаемоеОборудование.ФискальныеУстройства;
	Для Каждого ФискальноеУстройство Из ФискальныеУстройства Цикл
		Если ФискальноеУстройство.ИдентификаторУстройства = ИдентификаторУстройства Тогда
			   ФискальноеУстройство.ИдентификаторЗапроса = ИдентификаторЗапроса;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ПроверкиКодаМаркировки = глПодключаемоеОборудование.ПроверкиКодаМаркировки;
	
	Для Каждого ПроверкаКодаМаркировки Из ПроверкиКодаМаркировки Цикл
		Если ПроверкаКодаМаркировки.ИдентификаторУстройства = ИдентификаторУстройства
			И ПроверкаКодаМаркировки.ИдентификаторСессии = ИдентификаторСессии
			И ПроверкаКодаМаркировки.ПараметрыЗапросаКМ.КонтрольнаяМарка = ПараметрыЗапросаКМ.КонтрольнаяМарка  
			И ПроверкаКодаМаркировки.ПараметрыЗапросаКМ.Количество = ПараметрыЗапросаКМ.Количество
			И ПроверкаКодаМаркировки.ПараметрыЗапросаКМ.ПланируемыйСтатусТовара = ПараметрыЗапросаКМ.ПланируемыйСтатусТовара Тогда
				ПроверкаКодаМаркировки.РезультатПроверки = РезультатПроверки;
			Возврат;
		КонецЕсли;
	КонецЦикла;                             
	
	ПроверкаКодаМаркировки = Новый Структура();
	ПроверкаКодаМаркировки.Вставить("ИдентификаторУстройства", ИдентификаторУстройства);
	ПроверкаКодаМаркировки.Вставить("ИдентификаторСессии", ИдентификаторСессии);
	ПроверкаКодаМаркировки.Вставить("ПараметрыЗапросаКМ" , ПараметрыЗапросаКМ);
	ПроверкаКодаМаркировки.Вставить("РезультатПроверки"  , РезультатПроверки);  
	
	ПроверкиКодаМаркировки.Добавить(ПроверкаКодаМаркировки);
	
КонецПроцедуры

#КонецОбласти  

// Функция возвращает Истина, если внедрена Библиотека стандартных подсистем
//
// Возвращаемое значение:
//  Булево.
Функция ИспользуетсяБСП() Экспорт
	// СтандартныеПодсистемы.Пользователи - одна из обязательных подсистем БСП при внедрении
	Возврат ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.Пользователи");
КонецФункции

// Выполняет рекурсивно установку компоненты
// 
// Параметры:
//  Результат - Структура - результат установки компоненты:
//   * Установлено    - Булево - признак установки.
//   * ОписаниеОшибки - Строка - краткое описание ошибки. При отмене пользователем пустая строка.
//  Контекст - Структура
Процедура УстановитьКомпонентуИзМакета(Результат, Контекст) Экспорт
	
	Если Не Результат.Установлено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(СтрШаблон(НСтр("ru = 'Ошибка установки компоненты: %1'"), Результат.ОписаниеОшибки));
	КонецЕсли;
	
	МакетыДляПереустановки = Контекст.МакетыДляПереустановки;
	Если МакетыДляПереустановки.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	ПоследнийИндекс = МакетыДляПереустановки.ВГраница();
	ТекущийМакет    = МакетыДляПереустановки[ПоследнийИндекс];
	МакетыДляПереустановки.Удалить(ПоследнийИндекс);

	ОповещениеУстановки = Новый ОписаниеОповещения("УстановитьКомпонентуИзМакета", МенеджерОборудованияКлиент, Контекст);
	ОбщегоНазначенияКлиент.УстановитьКомпонентуИзМакета(ОповещениеУстановки, ТекущийМакет);
	
КонецПроцедуры

#Область ПанельАдминистрирования

// Обработчик команды формы.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма, в которой выполняется команда.
//   Команда - КомандаФормы - выполняемая команда.
//   Источник - ТаблицаФормы
//            - ДанныеФормыСтруктура - объект или список формы с полем "Ссылка".
//
Процедура НачатьВыполнениеКомандыПанелиАдминистрирования(Форма, Команда, Источник) Экспорт
	ИмяКоманды = Команда.Имя;
	АдресНастроек = Форма.ПараметрыПодключаемыхКоманд.АдресТаблицыКоманд;
	ОписаниеКоманды = МенеджерОборудованияКлиентПовтИсп.ОписаниеКомандыПанелиАдминистрирования(ИмяКоманды, АдресНастроек);
	
	ПараметрыВыполнения = ПараметрыВыполненияКоманды();
	ПараметрыВыполнения.ОписаниеКоманды = Новый Структура(ОписаниеКоманды);
	ПараметрыВыполнения.Форма           = Форма;
	ПараметрыВыполнения.Источник        = Источник;
	ПараметрыВыполнения.ЭтоФормаОбъекта = ТипЗнч(Источник) = Тип("ДанныеФормыСтруктура");
	
	ПараметрыВыполнения.ВызовСервераЧерезОбработкуОповещения = Истина;
	
	ПродолжитьВыполнениеКоманды(ПараметрыВыполнения);
КонецПроцедуры

#КонецОбласти

Функция ДопустимаУстановкаКомпоненты(ИмяМакета) Экспорт
	#Если ВебКлиент Тогда
	Если ИспользуетсяБСП() Тогда
		Реквизиты = МенеджерОборудованияВызовСервера.СовместимостьВнешнейКомпонентыИзФайла(ИмяМакета);
		Если ЗначениеЗаполнено(Реквизиты) Тогда
			Результат = ТекущийКлиентПоддерживаетсяКомпонентой(Реквизиты);
			Возврат Результат = Истина; // Результат может содержать Неопределено
		Иначе
			Возврат Ложь;
		КонецЕсли;
	Иначе
		Возврат Истина;
	КонецЕсли;
	#Иначе
	Возврат Истина;
	#КонецЕсли
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Преобразовать список строкой в массив.
//
// Возвращаемое значение:
//  Массив - .
Функция ПреобразоватьСписокСтрокойВМассив(Источник) Экспорт
	
	ПромежуточнаяСтруктура = Новый Структура(Источник);
	Приемник = Новый Массив;
	
	Для Каждого КлючИЗначение Из ПромежуточнаяСтруктура Цикл
		Приемник.Добавить(КлючИЗначение.Ключ);
	КонецЦикла;
	
	Возврат Приемник;
	
КонецФункции

// Возвращает структуру результат установки компоненты
Функция РезультатУстановкиКомпоненты()
	
	Результат = Новый Структура;
	Результат.Вставить("Установлено", Ложь);
	Результат.Вставить("ОписаниеОшибки", "");
	
	Возврат Результат;
	
КонецФункции

#Область ВыполнениеКоманды

// Завершение выполнение команды
// 
// Параметры:
//  РезультатОперации - Структура
//  ПараметрыВыполнениеКоманды - Структура
//
Процедура ЗавершениеВыполнениеКоманды(РезультатОперации, ПараметрыВыполнениеКоманды);
	
	ВыполнитьОбработкуОповещения(ПараметрыВыполнениеКоманды.ОповещениеПриЗавершении, РезультатОперации); 
	
КонецПроцедуры       

// Завершает проверку результата операции
// 
// Параметры:
//  Результат - см. СообщенияВСлужбуТехническойПоддержкиБПОКлиент.РезультатОперации
//  ПараметрыВыполнениеКоманды - см. ПараметрыПроверкиРезультатаОперации     
//
Процедура ЗавершениеВыполнениеКоманды_ПроверкаРезультата(Результат, ПараметрыВыполнениеКоманды) Экспорт
	
	Если ПараметрыВыполнениеКоманды.ОповещениеПриЗавершении <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ПараметрыВыполнениеКоманды.ОповещениеПриЗавершении, ПараметрыВыполнениеКоманды.РезультатВыполненияОперации);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РаспределеннаяФискализации

Функция ПолучитьДоступноеККТДляФискализации(РеквизитыЧека, СписокУстройств)
	
	ИдентификаторУстройстваККТ = Неопределено;
	СтандартнаяОбработка = Истина;
	
	МенеджерОборудованияКлиентПереопределяемый.ДоступноеККТДляФискализации(РеквизитыЧека, СписокУстройств, ИдентификаторУстройстваККТ, СтандартнаяОбработка);
	
	Если НЕ СтандартнаяОбработка Тогда
		Возврат ИдентификаторУстройстваККТ;
	КонецЕсли;
	
	Для Каждого Устройство Из СписокУстройств Цикл
		УстройствоОрганизацияИНН = Устройство.ПараметрыРегистрации.ОрганизацияИНН; 
		УстройствоКодыНалогообложения = СтрРазделить(Устройство.ПараметрыРегистрации.КодыСистемыНалогообложения, ",");
		ОрганизацияИНН = РеквизитыЧека.ОрганизацияИНН;
		
		Если Устройство.ИспользуетсяФН36 И РеквизитыЧека.СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.ТипыСистемНалогообложенияККТ.ОСН") Тогда
			СистемаНалогообложения = МенеджерОборудованияКлиентСервер.КодСистемыНалогообложенияККТ(ПредопределенноеЗначение("Перечисление.ТипыСистемНалогообложенияККТ.ЕНВД"));
		Иначе
			СистемаНалогообложения = МенеджерОборудованияКлиентСервер.КодСистемыНалогообложенияККТ(РеквизитыЧека.СистемаНалогообложения);
		КонецЕсли;
		
		Если УстройствоКодыНалогообложения.Найти(Строка(СистемаНалогообложения)) <> Неопределено 
			И УстройствоОрганизацияИНН = ОрганизацияИНН Тогда
				ИдентификаторУстройстваККТ = Устройство.Ссылка;
				Прервать; 
		КонецЕсли;
	КонецЦикла;
	
	Возврат ИдентификаторУстройстваККТ;    
	
КонецФункции

#КонецОбласти  

#Область УпрощениеНастройкиПараметров

// Возвращает словарь реквизитов и возможных вариантов имен параметров производителя драйверов
//
// Возвращаемое значение:
//  см. МенеджерОборудованияКлиентПовтИсп.СловарьКлючевыхСвойств
Функция СловарьКлючевыхСвойств() 
	
	Возврат МенеджерОборудованияКлиентПовтИсп.СловарьКлючевыхСвойств();
	
КонецФункции

// Возвращает имя параметра по имени параметра указанного в файле XML
//
// Параметры:
//  ИмяПараметраXML - Строка - имя параметра оборудования заданное в файле XML
//
// Возвращаемое значение:
//  Строка, Неопределено - если имя параметра найдено в словаре, возвращает имя стандартного параметра
Функция ИмяКлючевогоПараметра(ИмяПараметраXML, Преобразование = Неопределено)
	
	Словарь = СловарьКлючевыхСвойств();
	
	Для Каждого КлючЗначение Из Словарь Цикл
		Если КлючЗначение.Значение.Свойство(ИмяПараметраXML, Преобразование) Тогда
			Возврат КлючЗначение.Ключ;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

// Возвращает структуру атрибуты параметра
//
// Возвращаемое значение:
//  Структура:
//   * Стандартный - Булево - реквизит предусмотрен стандартами БПО
//   * ИмяПараметраXML - Строка - имя параметра как оно задано у производителя оборудования
//   * ИмяПараметра - Строка - имя стандартного параметра
//   * ИмяРеквизита - Строка - имя реквизита, под которым хранится значение данного параметра в справочнике
//   * ТолькоЧтение - Булево - параметр только для чтения
//   * Тип - Тип - Тип значения параметра (Число, Строка, Булево)
//   * Заголовок - Строка - заголовок параметра
//   * ЗначениеПоУмолчанию - Число, Строка, Булево - значение параметра по умолчанию
//   * Описание - Строка - описание параметра
//   * СтрокаФорматирования - Строка - строка форматирования параметра
//   * СписокВыбора - СписокЗначений - возможные варианты значения параметра
// 
Функция НовыйАтрибутыПараметра()
	
	Результат = Новый Структура();
	Результат.Вставить("Стандартный",          Ложь);
	Результат.Вставить("ЗначениеСтандартного", "");
	Результат.Вставить("ИмяПараметраXML",      "");
	Результат.Вставить("ИмяПараметра",         "");
	Результат.Вставить("ИмяРеквизита",         "");
	Результат.Вставить("ТолькоЧтение",         Ложь);
	Результат.Вставить("Тип",                  Тип("Число"));
	Результат.Вставить("Заголовок",            "");
	Результат.Вставить("ЗначениеПоУмолчанию",  "");
	Результат.Вставить("Описание",             "");
	Результат.Вставить("СтрокаФорматирования", "");
	Результат.Вставить("СписокВыбора",         Новый СписокЗначений());
	Результат.Вставить("Преобразование",       "");
	
	Возврат Результат;
КонецФункции

// Возвращает структуру атрибуты параметра
//
// Параметры:
//  ЧтениеXML - ЧтениеXML - открытый для чтения объект XML
//
// Возвращаемое значение:
//  Структура - см. НовыйАтрибутыПараметра
Функция ПрочитатьАтрибутыПараметра(ЧтениеXML)
	
	ИмяПараметраXML =  ЧтениеXML.ЗначениеАтрибута("Name");
	Преобразование  = Неопределено;
	ИмяПараметра    = ИмяКлючевогоПараметра(ИмяПараметраXML, Преобразование);
	Если ИмяПараметра = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ЗначениеАтрибута = ВРег(ЧтениеXML.ЗначениеАтрибута("ReadOnly"));
	ТолькоЧтение     = (ЗначениеАтрибута = "TRUE" Или ЗначениеАтрибута = "ИСТИНА");
	
	ЗначениеАтрибута = ВРег(ЧтениеXML.ЗначениеАтрибута("TypeValue"));
	ПараметрТип      = ?(НЕ ПустаяСтрока(ЗначениеАтрибута), ЗначениеАтрибута, "STRING");
	
	Результат = НовыйАтрибутыПараметра();
	Результат.ИмяПараметраXML = ИмяПараметраXML;
	Результат.ИмяПараметра    = ИмяПараметра;
	Результат.Тип             = Тип(ПараметрТип);
	Результат.ТолькоЧтение    = ТолькоЧтение;
	Результат.ИмяРеквизита    = ?(ТолькоЧтение, "R_", "P_") + ИмяПараметраXML;
	Результат.Заголовок       = ЧтениеXML.ЗначениеАтрибута("Caption");
	Результат.Описание        = ЧтениеXML.ЗначениеАтрибута("Description");
	Результат.Преобразование  = Преобразование;
	Результат.ЗначениеПоУмолчанию  = ЧтениеXML.ЗначениеАтрибута("DefaultValue");
	Результат.СтрокаФорматирования = ЧтениеXML.ЗначениеАтрибута("FieldFormat");
	
	Возврат Результат;
КонецФункции

// Заполняет список выбора в атрибутах параметра из XML
//
// Параметры:
//  АтрибутыПараметра - Структура - в которую нужно занести список выбора
//  ЧтениеXML - ЧтениеXML - открытый для чтения объект XML
Процедура ПрочитатьСписокВыбора(АтрибутыПараметра, ЧтениеXML)
	
	#Если Не ВебКлиент Тогда
	СписокВыбора = Новый СписокЗначений();
	АтрибутыПараметра.Вставить("СписокВыбора", СписокВыбора);
	
	Пока ЧтениеXML.Прочитать() Цикл
		Если ЧтениеXML.Имя = "ChoiceList" И ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда  
			Прервать;
		ИначеЕсли ЧтениеXML.Имя = "Item" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда  
			Значение = ЧтениеXML.ЗначениеАтрибута("Value");
		ИначеЕсли ЧтениеXML.Имя = "#text" И ЧтениеXML.ТипУзла = ТипУзлаXML.Текст Тогда  
			Представление = ЧтениеXML.Значение;
		ИначеЕсли ЧтениеXML.Имя = "Item" И ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда  
			СписокВыбора.Добавить(Значение, Представление);
		КонецЕсли;
	КонецЦикла;
	#КонецЕсли

КонецПроцедуры

// Заполнить структуру стандартных параметров по описанию интерфейса
//
// Параметры:
//  ОписаниеИнтерфейса - Строка - строка XML в которой находится описание интерфейса
//  КлючевыеПараметры - Структура - структура содержащая найденные ключевые параметры и их атрибуты
Процедура ПрочитатьАтрибутыКлючевыхПараметров(ОписаниеИнтерфейса, КлючевыеПараметры)
	
	#Если Не ВебКлиент Тогда
	ЧтениеXML = Новый ЧтениеXML; 
	ЧтениеXML.УстановитьСтроку(ОписаниеИнтерфейса);
	ЧтениеXML.ПерейтиКСодержимому();
	
	АтрибутыПараметра = Неопределено;
	Если ЧтениеXML.Имя = "Settings" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
		Пока ЧтениеXML.Прочитать() Цикл  
			Если ЧтениеXML.Имя = "ChoiceList" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда  
				Если АтрибутыПараметра <> Неопределено Тогда
					ПрочитатьСписокВыбора(АтрибутыПараметра, ЧтениеXML);
				КонецЕсли;
			ИначеЕсли ЧтениеXML.Имя = "Parameter" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда  
				АтрибутыПараметра = ПрочитатьАтрибутыПараметра(ЧтениеXML);
				Если АтрибутыПараметра <> Неопределено Тогда
					КлючевыеПараметры.Вставить(АтрибутыПараметра.ИмяПараметра, АтрибутыПараметра);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	#КонецЕсли
	
КонецПроцедуры

Процедура ДобавитьПараметрЛогДрайвераВключен(КлючевыеПараметры, Значение)
	
	АтрибутыПараметра = НовыйАтрибутыПараметра();
	АтрибутыПараметра.Стандартный     = Истина;
	АтрибутыПараметра.ИмяПараметраXML = "";
	АтрибутыПараметра.ИмяПараметра    = "LogEnabled";
	АтрибутыПараметра.Тип             = Тип("Булево");
	АтрибутыПараметра.ТолькоЧтение    = Истина;
	АтрибутыПараметра.ИмяРеквизита    = "";
	АтрибутыПараметра.Заголовок       = НСтр("ru = 'Включен лог'");
	АтрибутыПараметра.Описание        = "";
	АтрибутыПараметра.Преобразование  = "";
	АтрибутыПараметра.ЗначениеСтандартного = Значение;
	АтрибутыПараметра.ЗначениеПоУмолчанию  = Ложь;
	АтрибутыПараметра.СтрокаФорматирования = "";
	
	КлючевыеПараметры.Вставить(АтрибутыПараметра.ИмяПараметра, АтрибутыПараметра);

КонецПроцедуры

Процедура ДобавитьПараметрЛогДрайвераПутьКФайлу(КлючевыеПараметры, Значение)
	
	АтрибутыПараметра = НовыйАтрибутыПараметра();
	АтрибутыПараметра.Стандартный     = Истина;
	АтрибутыПараметра.ИмяПараметраXML = "";
	АтрибутыПараметра.ИмяПараметра    = "LogPath";
	АтрибутыПараметра.Тип             = Тип("Строка");
	АтрибутыПараметра.ТолькоЧтение    = Истина;
	АтрибутыПараметра.ИмяРеквизита    = "";
	АтрибутыПараметра.Заголовок       = НСтр("ru = 'Путь к log-файлу'");
	АтрибутыПараметра.Описание        = "";
	АтрибутыПараметра.Преобразование  = "";
	АтрибутыПараметра.ЗначениеСтандартного = Значение;
	АтрибутыПараметра.ЗначениеПоУмолчанию  = "";
	АтрибутыПараметра.СтрокаФорматирования = "";
	
	КлючевыеПараметры.Вставить(АтрибутыПараметра.ИмяПараметра, АтрибутыПараметра);

КонецПроцедуры

#КонецОбласти

#Область ПанельАдминистрирования

// Свойства второго параметра обработчика подключаемой команды, выполняемой на клиенте.
//
// Возвращаемое значение:
//  Структура:
//   * ОписаниеКоманды - Структура - состав свойств совпадает с колонками таблицы значений параметра Команды процедуры
///                                  ПодключаемыеКомандыПереопределяемый.ПриОпределенииКомандПодключенныхКОбъекту.
//                                   Ключевые свойства:
//      ** Идентификатор  - Строка - идентификатор команды.
//      ** Представление  - Строка - представление команды в форме.
//      ** Имя            - Строка - имя команды в форме.
//      ** ДополнительныеПараметры - Структура - дополнительные свойства, состав которых определяется видом 
//                                   конкретной команды.
//   * Форма - ФормаКлиентскогоПриложения - форма, из которой вызвана команда.
//           - РасширениеУправляемойФормыДляДокумента
//   * ЭтоФормаОбъекта - Булево - Истина, если команда вызвана из формы объекта.
//   * Источник - ТаблицаФормы
//              - ДанныеФормыСтруктура - объект или список формы с полем "Ссылка":
//     ** Ссылка - ЛюбаяСсылка
//
Функция ПараметрыВыполненияКоманды()
	
	Результат = МенеджерОборудованияКлиентСервер.ПараметрыВыполненияКомандыПанелиАдминистрирования();

	Результат.Вставить("Уникальность");
	Результат.Вставить("Окно");
	
	// Служебные параметры.
	Результат.Вставить("ТребуетсяЗапись", Ложь);
	Результат.Вставить("ТребуетсяПроведение", Ложь);
	Результат.Вставить("ТребуетсяРаботаСФайлами", Ложь);
	Результат.Вставить("МассивСсылок", Новый Массив);
	Результат.Вставить("ВызовСервераЧерезОбработкуОповещения", Ложь);
	Результат.Вставить("НепроведенныеДокументы", Новый Массив);
	Возврат Результат;
	
КонецФункции

// Выполняет команду, подключенную к форме.
//
// Параметры:
//  ПараметрыВыполнения - см. ПараметрыВыполненияКоманды
// 
Процедура ПродолжитьВыполнениеКоманды(ПараметрыВыполнения)
	
	ОписаниеКоманды = ПараметрыВыполнения.ОписаниеКоманды;
	
	// Выполнение команды.
	ПараметрКоманды = Неопределено;
	// Выполнение команды.
	Если ОписаниеКоманды.Серверная Тогда
		Результат = Новый Структура;
		
		СерверныйКонтекст = Новый Структура;
		СерверныйКонтекст.Вставить("ПараметрКоманды", ПараметрКоманды);
		СерверныйКонтекст.Вставить("ИмяКомандыВФорме", ОписаниеКоманды.ИмяВФорме);
		СерверныйКонтекст.Вставить("Результат", Результат);
		
		Если ПараметрыВыполнения.ВызовСервераЧерезОбработкуОповещения Тогда
			ОписаниеОповещения = Новый ОписаниеОповещения("Подключаемый_ПродолжитьВыполнениеКомандыНаСервере", ПараметрыВыполнения.Форма);
			ВыполнитьОбработкуОповещения(ОписаниеОповещения, СерверныйКонтекст);
			Результат = СерверныйКонтекст.Результат;
		Иначе
			ПараметрыВыполнения.Форма.Подключаемый_ВыполнитьКомандуНаСервере(СерверныйКонтекст, Результат);
		КонецЕсли;
		Если ЗначениеЗаполнено(Результат.Текст) Тогда
			ПоказатьПредупреждение(, Результат.Текст);
		КонецЕсли;
	Иначе
		Если ЗначениеЗаполнено(ОписаниеКоманды.Обработчик) Тогда
			Если СтрНачинаетсяС(ОписаниеКоманды.Обработчик, "e1cib") Тогда
				
				// Нужно проверять по подсистеме БазоваяФункциональность, но эта подсистема частично загружена в БПО
				Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.Пользователи") Тогда
					МодульФайловаяСистемаКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ФайловаяСистемаКлиент");
					МодульФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(ОписаниеКоманды.Обработчик);
				Иначе
					Если МенеджерОборудованияКлиентСервер.СтрокаЗапускаБезопасная(ОписаниеКоманды.Обработчик) Тогда
						ПерейтиПоНавигационнойСсылке(ОписаниеКоманды.Обработчик); // АПК:534 произведена проверка запуска
					КонецЕсли;
				КонецЕсли;
				
			ИначеЕсли СтрНайти(ОписаниеКоманды.Обработчик, "://")>0 Тогда
				
				// Нужно проверять по подсистеме БазоваяФункциональность, но эта подсистема частично загружена в БПО
				Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.Пользователи") Тогда
					МодульФайловаяСистемаКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ФайловаяСистемаКлиент");
					МодульФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(ОписаниеКоманды.Обработчик);
				Иначе
					Если МенеджерОборудованияКлиентСервер.СтрокаЗапускаБезопасная(ОписаниеКоманды.Обработчик) Тогда
						ПерейтиПоНавигационнойСсылке(ОписаниеКоманды.Обработчик); // АПК:534 произведена проверка запуска
					КонецЕсли;
				КонецЕсли;
				
			Иначе
				МассивПодстрок = СтрРазделить(ОписаниеКоманды.Обработчик, ".");
				Если МассивПодстрок.Количество() = 1 Тогда
					ПараметрыФормы = ПараметрыФормы(ПараметрыВыполнения, ПараметрКоманды);
					// АПК:65-выкл ПолучитьФорму используется для вызова обработчика описания оповещения
					КлиентскийМодуль = ПолучитьФорму(ОписаниеКоманды.ИмяФормы, ПараметрыФормы, ПараметрыВыполнения.Форма, Истина);
					// АПК:65-вкл
					ИмяПроцедуры = ОписаниеКоманды.Обработчик;
				Иначе
					КлиентскийМодуль = ОбщегоНазначенияКлиент.ОбщийМодуль(МассивПодстрок[0]);
					ИмяПроцедуры = МассивПодстрок[1];
				КонецЕсли;
				ПараметрыВыполнения.Уникальность = ПараметрыВыполнения.Форма.УникальныйИдентификатор;
				Обработчик = Новый ОписаниеОповещения(ИмяПроцедуры, КлиентскийМодуль, ПараметрыВыполнения);
				ВыполнитьОбработкуОповещения(Обработчик, ПараметрКоманды);
			КонецЕсли;
		ИначеЕсли ЗначениеЗаполнено(ОписаниеКоманды.ИмяФормы) Тогда
			ПараметрыФормы = ПараметрыФормы(ПараметрыВыполнения, ПараметрКоманды);
			ОткрытьФорму(ОписаниеКоманды.ИмяФормы, ПараметрыФормы, ПараметрыВыполнения.Форма, Истина);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// Формирует параметры формы подключенного объекта в контексте выполняемой команды.
Функция ПараметрыФормы(Контекст, ПараметрКоманды)
	Результат = Контекст.ОписаниеКоманды.ПараметрыФормы;
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Результат = Новый Структура;
	КонецЕсли;
	Контекст.ОписаниеКоманды.Удалить("ПараметрыФормы");
	Результат.Вставить("ОписаниеКоманды", Контекст.ОписаниеКоманды);
	Если ПустаяСтрока(Контекст.ОписаниеКоманды.ИмяПараметраФормы) Тогда
		Результат.Вставить("ПараметрКоманды", ПараметрКоманды);
	Иначе
		МассивИмен = СтрРазделить(Контекст.ОписаниеКоманды.ИмяПараметраФормы, ".", Ложь);
		Узел = Результат;
		ВГраница = МассивИмен.ВГраница();
		Для Индекс = 0 По ВГраница-1 Цикл
			Имя = СокрЛП(МассивИмен[Индекс]);
			Если Не Узел.Свойство(Имя) Или ТипЗнч(Узел[Имя]) <> Тип("Структура") Тогда
				Узел.Вставить(Имя, Новый Структура);
			КонецЕсли;
			Узел = Узел[Имя];
		КонецЦикла;
		Узел.Вставить(МассивИмен[ВГраница], ПараметрКоманды);
	КонецЕсли;
	Возврат Результат;
КонецФункции

#КонецОбласти

Функция ТекущийКлиентПоддерживаетсяКомпонентой(Реквизиты)
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	
	Браузер = Неопределено;
#Если ВебКлиент Тогда
	Строка = СистемнаяИнформация.ИнформацияПрограммыПросмотра;
	
	Если СтрНайти(Строка, "Chrome/") > 0 Тогда
		Браузер = "Chrome";
	ИначеЕсли СтрНайти(Строка, "MSIE") > 0 Тогда
		Браузер = "MSIE";
	ИначеЕсли СтрНайти(Строка, "Safari/") > 0 Тогда
		Браузер = "Safari";
	ИначеЕсли СтрНайти(Строка, "Firefox/") > 0 Тогда
		Браузер = "Firefox";
	КонецЕсли;
#КонецЕсли
	
	Если СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Linux_x86 Тогда
		
		Если Браузер = Неопределено Тогда
			Возврат Реквизиты.Linux_x86;
		КонецЕсли;
		
		Если Браузер = "Firefox" Тогда
			Возврат Реквизиты.Linux_x86_Firefox;
		КонецЕсли;
		
		Если Браузер = "Chrome" Тогда
			Возврат Реквизиты.Linux_x86_Chrome;
		КонецЕсли;
			
	ИначеЕсли СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Linux_x86_64 Тогда
		
		Если Браузер = Неопределено Тогда
			Возврат Реквизиты.Linux_x86_64;
		КонецЕсли;
		
		Если Браузер = "Firefox" Тогда
			Возврат Реквизиты.Linux_x86_64_Firefox;
		КонецЕсли;
		
		Если Браузер = "Chrome" Тогда
			Возврат Реквизиты.Linux_x86_64_Chrome;
		КонецЕсли;
		
	ИначеЕсли СистемнаяИнформация.ТипПлатформы = ТипПлатформы.MacOS_x86_64 Тогда
		
		Если Браузер = Неопределено Тогда
			Возврат Реквизиты.MacOS_x86_64;
		КонецЕсли;
		
		Если Браузер = "Safari" Тогда
			Возврат Реквизиты.MacOS_x86_64_Safari;
		КонецЕсли;
		
		Если Браузер = "Firefox" Тогда
			Возврат Реквизиты.MacOS_x86_64_Firefox;
		КонецЕсли;
		
		Если Браузер = "Chrome" Тогда
			Возврат Реквизиты.MacOS_x86_64_Chrome;
		КонецЕсли;
		
	ИначеЕсли СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86 Тогда
		
		Если Браузер = Неопределено Тогда
			Возврат Реквизиты.Windows_x86;
		КонецЕсли;
		
		Если Браузер = "Firefox" Тогда
			Возврат Реквизиты.Windows_x86_Firefox;
		КонецЕсли;
		
		Если Браузер = "Chrome" Тогда
			Возврат Реквизиты.Windows_x86_Chrome;
		КонецЕсли;
		
		Если Браузер = "MSIE" Тогда
			Возврат Реквизиты.Windows_x86_MSIE;
		КонецЕсли;
		
	ИначеЕсли СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86_64 Тогда
		
		Если Браузер = Неопределено Тогда
			Возврат Реквизиты.Windows_x86_64;
		КонецЕсли;
		
		Если Браузер = "Firefox" Тогда
			Возврат Реквизиты.Windows_x86_Firefox;
		КонецЕсли;
		
		Если Браузер = "Chrome" Тогда
			Возврат Реквизиты.Windows_x86_Chrome;
		КонецЕсли;
		
		Если Браузер = "MSIE" Тогда
			Возврат Реквизиты.Windows_x86_64_MSIE;
		КонецЕсли;
	
	ИначеЕсли СистемнаяИнформация.ТипПлатформы = ТипПлатформы.MacOS_x86 Тогда
		// В браузере может быть неправильно определен тип платформы.
	
		Если Браузер = "Firefox" Тогда
			Возврат Реквизиты.MacOS_x86_64_Firefox;
		КонецЕсли;
		
		Если Браузер = "Chrome" Тогда
			Возврат Реквизиты.MacOS_x86_64_Chrome;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти  
