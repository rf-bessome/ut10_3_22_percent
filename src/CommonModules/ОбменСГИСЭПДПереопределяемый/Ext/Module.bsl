//@skip-check module-empty-method
Процедура ОбработкаЗаполненияЭТрН(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли; 
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		ЗаполнитьЗначенияСвойств(ДокументОбъект, ДанныеЗаполненияЭТрН(ДанныеЗаполнения));
		
		НоваяСтрока = ДокументОбъект.ДокументыОснования.Добавить();
		НоваяСтрока.ДокументОснование = ДанныеЗаполнения;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ДокументОбъект, ДанныеЗаполнения);
	КонецЕсли; 
	
КонецПроцедуры

//@skip-check module-empty-method
Процедура ОбработкаЗаполненияЭСВ(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
КонецПроцедуры

//@skip-check module-empty-method
Процедура ОбработкаЗаполненияЭЗН(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
КонецПроцедуры

//@skip-check module-empty-method
Процедура ОбработкаЗаполненияЭЗЗ(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
КонецПроцедуры

//@skip-check module-empty-method
Процедура ОбработкаЗаполненияЭПЛ(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
КонецПроцедуры

// Обработчик события получения входящего титула ЭПД.
//
// Параметры:
//   ДокументСсылка - ДокументСсылка.ЭлектроннаяТранспортнаяНакладная -
//   ПолученныйТитул - ПеречислениеСсылка.ТипыЭлементовВерсииЭД - полученный титул документа.
//   ЭтоИсправление - Булево - признак исправления титула
//
//@skip-check module-empty-method
Процедура СобытиеПолучениеТитулаЭПД(ДокументСсылка, ПолученныйТитул, ЭтоИсправление) Экспорт
КонецПроцедуры

Функция ИмяПечатнойФормыЭТрН(ДатаДокумента) Экспорт

	Возврат "Документ.ЭлектроннаяТранспортнаяНакладная.ПФ_MXL_ТранспортнаяНакладная2116"
		
КонецФункции

Функция ВидФактическийАдресКонтрагента() Экспорт
	
	Возврат ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ФактАдресКонтрагента");
	
КонецФункции

Функция ВидФактическийАдресОрганизации() Экспорт
	
	Возврат ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ФактАдресОрганизации");
	
КонецФункции

// Контактная информация объекта.
//
// Параметры:
//   Объект - СправочникСсылка.Организации,
//			СправочникСсылка.Контрагенты - объект для получения информации.
//   Дата - Дата - дата контактной информации.
//   Адреса - Массив Из Структура:
// 	   * Значение - Строка
// 	   * Представление - Строка
//
Процедура КонтактнаяИнформацияОбъекта(Объект, Дата, Адреса) Экспорт
	
	
	
КонецПроцедуры

Функция ИмяТипаБанковскиеСчетаОрганизации() Экспорт
	
	Возврат "СправочникСсылка.БанковскиеСчета";
	
КонецФункции

Процедура ПолучитьДанныеФизЛица(ФизЛицо, СведенияОбУчастнике) Экспорт

	

КонецПроцедуры

#Область СлужебныеПроцедурыИФункции

 Процедура ДобавитьДокументОснование(ТранспортнаяНакладнаяОбъект, ДокументОснованиеСсылка)
	
	Если ДокументОснованиеСсылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	НайденныйДокумент = ТранспортнаяНакладнаяОбъект.ДокументыОснования.Найти(ДокументОснованиеСсылка, "ДокументОснование");
	Если НайденныйДокумент = Неопределено Тогда
		НоваяСтрокаОснований = ТранспортнаяНакладнаяОбъект.ДокументыОснования.Добавить();
		НоваяСтрокаОснований.ДокументОснование = ДокументОснованиеСсылка;
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьПредставлениеАдресаПоСтруктуре(КонтактнаяИнформация) Экспорт
	
	Если Не КонтактнаяИнформация.Свойство("Регион") Тогда
		КонтактнаяИнформация.Вставить("Регион");
	КонецЕсли;
	Если Не КонтактнаяИнформация.Свойство("Квартира") Тогда
		КонтактнаяИнформация.Вставить("Квартира");
	КонецЕсли;
	
	Возврат УправлениеКонтактнойИнформацией.ПолучитьПредставлениеАдресаПоСтруктуре(КонтактнаяИнформация);

КонецФункции

Функция ДокументооборотНастроен(СсылкаНаВладельца, Организация, Контрагент) Экспорт

	Если НЕ ЗначениеЗаполнено(Организация) ИЛИ НЕ ЗначениеЗаполнено(Контрагент) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ПравоДоступа("Чтение",Метаданные.РегистрыСведений.СостоянияЭД) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПараметрыЭД = Неопределено;
	
	Возврат ЭлектронныеДокументыСлужебныйВызовСервера.ОпределитьДействующуюНастройкуЭДО(СсылкаНаВладельца, ПараметрыЭД, Перечисления.ВидыЭД.ЭТрН);
	
КонецФункции

Функция НастроенОбменЭПД(СсылкаНаВладельца, Организация, Грузополучатель, Перевозчик) Экспорт
	
	Возврат ДокументооборотНастроен(СсылкаНаВладельца, Организация, Грузополучатель)
			И ДокументооборотНастроен(СсылкаНаВладельца, Организация, Перевозчик);
	
КонецФункции

#КонецОбласти

#Область ЗаполнениеЭТрН

Функция ДанныеЗаполненияЭТрН(Основание) Экспорт
	ДанныеЗаполнения = Новый Структура;
	
	Если НЕ ЗначениеЗаполнено(Основание) 
		ИЛИ ТипЗнч(Основание) <> Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		
		Возврат ДанныеЗаполнения;
	КонецЕсли; 
		
	СписокРеквизитов = "Организация, Контрагент, Грузоотправитель, Грузополучатель, 
		|Перевозчик, Дата, Номер, АдресДоставки, 
		|Организация";
	
	РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Основание, СписокРеквизитов);
		
	ДанныеЗаполнения.Вставить("ДокументыОснования", Новый Массив);
	ДанныеЗаполнения.ДокументыОснования.Добавить(Новый Структура("ДокументОснование", Основание));
		
	ДанныеЗаполнения.Вставить("Организация", РеквизитыОснования.Организация);
	
	ДанныеЗаполнения.Вставить("ТитулГрузоотправителяЗаказДата", РеквизитыОснования.Дата);
	
	ДанныеЗаполнения.Вставить("СсылкаТитулГрузоотправителяГрузоотправитель", ?(ЗначениеЗаполнено(РеквизитыОснования.Грузоотправитель), 
		РеквизитыОснования.Грузоотправитель, РеквизитыОснования.Организация));
	
	ДанныеЗаполнения.Вставить("СсылкаТитулГрузоотправителяГрузополучатель", ?(ЗначениеЗаполнено(РеквизитыОснования.Грузополучатель), 
		РеквизитыОснования.Грузополучатель, РеквизитыОснования.Контрагент));
		
	ДанныеЗаполнения.Вставить("СсылкаТитулГрузоотправителяПеревозчик", РеквизитыОснования.Перевозчик);
	
	ОбменСГИСЭПДКлиентСервер.ЗаполнитьРеквизитыУчастника(
		ДанныеЗаполнения.СсылкаТитулГрузоотправителяГрузоотправитель, "ТитулГрузоотправителяГрузоотправитель", ДанныеЗаполнения);
		
	ОбменСГИСЭПДКлиентСервер.ЗаполнитьРеквизитыУчастника(
		ДанныеЗаполнения.СсылкаТитулГрузоотправителяГрузополучатель, "ТитулГрузоотправителяГрузополучатель", ДанныеЗаполнения);
		
	ОбменСГИСЭПДКлиентСервер.ЗаполнитьРеквизитыУчастника(
		ДанныеЗаполнения.СсылкаТитулГрузоотправителяПеревозчик, "ТитулГрузоотправителяПеревозчик", ДанныеЗаполнения);
	
	ДанныеЗаполненияАдрес(ДанныеЗаполнения, РеквизитыОснования.АдресДоставки, "ТитулГрузоотправителяГрузополучательАдресДоставки");
		
	Возврат ДанныеЗаполнения;

КонецФункции

Функция ДанныеЗаполненияАдрес(ДанныеЗаполнения, Адрес, Префикс)
	
	Если НЕ ЗначениеЗаполнено(Адрес) Тогда
		Возврат ДанныеЗаполнения;
	КонецЕсли; 

	СтруктураАдреса = РаботаСАдресами.СведенияОбАдресе(Адрес);
	
	Если ТипЗнч(СтруктураАдреса) = Тип("Массив") Тогда
		СтруктураАдреса = СтруктураАдреса[0];
	КонецЕсли;
	
	Если СтруктураАдреса.ТипАдреса = "ВСвободнойФорме" Тогда
		ДанныеЗаполнения.Вставить(Префикс + "Представление",   СтруктураАдреса.Представление);
		ДанныеЗаполнения.Вставить(Префикс + "КодСтраны",       СтруктураАдреса.КодСтраны);
	Иначе	
		ДанныеЗаполнения.Вставить(Префикс + "Индекс",          СтруктураАдреса.Индекс);
		ДанныеЗаполнения.Вставить(Префикс + "КодРегиона",      СтруктураАдреса.КодРегиона);
		ДанныеЗаполнения.Вставить(Префикс + "Город",           СтруктураАдреса.Город 
			+ ?(СтруктураАдреса.ГородТипКраткий = "", "", " ") + СтруктураАдреса.ГородТипКраткий); 
		ДанныеЗаполнения.Вставить(Префикс + "Район",           СтруктураАдреса.Район 
			+ ?(СтруктураАдреса.РайонТипКраткий = "", "", " ") + СтруктураАдреса.РайонТипКраткий);
		ДанныеЗаполнения.Вставить(Префикс + "НаселенныйПункт", СтруктураАдреса.НаселенныйПункт 
			+ ?(СтруктураАдреса.НаселенныйПунктТипКраткий = "", "", " ") + СтруктураАдреса.НаселенныйПунктТипКраткий);
		ДанныеЗаполнения.Вставить(Префикс + "Улица",           СтруктураАдреса.Улица 
			+ ?(СтруктураАдреса.УлицаТипКраткий = "", "", " ") + СтруктураАдреса.УлицаТипКраткий);
		ДанныеЗаполнения.Вставить(Префикс + "Дом",             СтруктураАдреса.Здание.Номер);
		Если СтруктураАдреса.Корпуса.Количество() > 0 Тогда
			ДанныеЗаполнения.Вставить(Префикс + "Корпус",      СтруктураАдреса.Корпуса[0].Номер);
		КонецЕсли;
	КонецЕсли;

	Возврат ДанныеЗаполнения;
КонецФункции

#КонецОбласти

#Область СостояниеЭТрН
Функция РеквизитыЭТрН(Основание) Экспорт
	
	РеквизитыФормы = Новый Структура;
	РеквизитыФормы.Вставить("ЭлектроннаяТранспортнаяНакладная", Документы.ЭлектроннаяТранспортнаяНакладная.ПустаяСсылка());
	РеквизитыФормы.Вставить("СостояниеЭТрН",                    ""); 
	
	Если ЗначениеЗаполнено(Основание) Тогда
		Запрос = Новый Запрос;
		
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЭлектроннаяТранспортнаяНакладнаяДокументыОснования.Ссылка КАК ЭлектроннаяТранспортнаяНакладная,
		|	ЭлектроннаяТранспортнаяНакладная.Дата КАК Дата
		|ИЗ
		|	Документ.ЭлектроннаяТранспортнаяНакладная.ДокументыОснования КАК ЭлектроннаяТранспортнаяНакладнаяДокументыОснования
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭлектроннаяТранспортнаяНакладная КАК ЭлектроннаяТранспортнаяНакладная
		|		ПО ЭлектроннаяТранспортнаяНакладнаяДокументыОснования.Ссылка = ЭлектроннаяТранспортнаяНакладная.Ссылка 
		|ГДЕ
		|	ЭлектроннаяТранспортнаяНакладнаяДокументыОснования.ДокументОснование = &ДокументОснование
		|	И НЕ ЭлектроннаяТранспортнаяНакладная.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата УБЫВ";
		
		Запрос.УстановитьПараметр("ДокументОснование", Основание);
		
		УстановитьПривилегированныйРежим(Истина);
		Выборка = Запрос.Выполнить().Выбрать();
		УстановитьПривилегированныйРежим(Ложь);
		Если Выборка.Следующий() Тогда
			РеквизитыФормы.ЭлектроннаяТранспортнаяНакладная = Выборка.ЭлектроннаяТранспортнаяНакладная;
		КонецЕсли;
	КонецЕсли; 
	
	РеквизитыФормы.СостояниеЭТрН = СостояниеЭТрН(РеквизитыФормы.ЭлектроннаяТранспортнаяНакладная, Основание);
	
	Возврат РеквизитыФормы;
	
КонецФункции

Функция СостояниеЭТрН(ЭлектроннаяТранспортнаяНакладная, Основание) Экспорт 
	
	СостояниеЭТрН = НСтр("ru = 'Не выбран'");
	Если ЗначениеЗаполнено(ЭлектроннаяТранспортнаяНакладная) Тогда
		РеквизитыОбъекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ЭлектроннаяТранспортнаяНакладная, 
			"ТитулГрузоотправителяТранспортнаяНакладнаяНомер, ТитулГрузоотправителяТранспортнаяНакладнаяДата, ТекущийШаг");
			
		СостояниеЭТрН = СтрШаблон("Электронная транспортная накладная №%1 от %2 (%3)", 
			ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(РеквизитыОбъекта.ТитулГрузоотправителяТранспортнаяНакладнаяНомер), 
			ФОрмат(РеквизитыОбъекта.ТитулГрузоотправителяТранспортнаяНакладнаяДата, "ДФ=dd.MM.yyyy"), РеквизитыОбъекта.ТекущийШаг);
	ИначеЕсли ПравоДоступа("Изменение", Метаданные.Документы.ЭлектроннаяТранспортнаяНакладная) Тогда
		Если ТипЗнч(Основание) = Тип("ДокументСсылка.РеализацияТоваровУслуг")  Тогда
			СостояниеЭТрН = НСтр("ru = 'Электронная транспортная накладная: создать документ'");
		Иначе
			СостояниеЭТрН = НСтр("ru = 'Электронная транспортная накладная: выбрать документ'");
		КонецЕсли;
	КонецЕсли;
	
	Возврат СостояниеЭТрН;
КонецФункции

#КонецОбласти

