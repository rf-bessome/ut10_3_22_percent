Функция ПрисоединенныйФайл(Ссылка) Экспорт
	
	Возврат ЭлектронныеДокументыСлужебный.ПрисоединенныйФайл(Ссылка);
	
КонецФункции

Процедура ЗаполнитьНоменклатуруИБВДеревеДокумента(Знач ЭлектронныйДокумент, ДеревоДокумента) Экспорт
	
	Если ТипЗнч(ЭлектронныйДокумент) = Тип("Структура") Тогда
		Контрагент = ЭлектронныйДокумент.Контрагент;
	Иначе
		Контрагент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭлектронныйДокумент, "Контрагент");
	КонецЕсли;
	Владелец = Контрагент;
	
	ИменаТаблицТоваров = Новый Массив;
	ИменаТаблицТоваров.Добавить("СведенияОТоварах");
	ИменаТаблицТоваров.Добавить("ТаблицаТоваров");
	ИменаТаблицТоваров.Добавить("ТаблицаУслуг");
	ИменаТаблицТоваров.Добавить("РезультатыПриемки");
	
	Товары = Неопределено;
	Для Каждого ИмяТаблицы Из ИменаТаблицТоваров Цикл
		
		Товары = ДеревоДокумента.Строки.Найти(ИмяТаблицы, "ПолныйПуть");
		Если Товары <> Неопределено Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если Товары = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НаСопоставление = Новый Массив;
	ИдентификаторВСтрокеТоваров = Новый Соответствие;
	
	Для Каждого СтрокаТовара Из Товары.Строки Цикл
		
		Сопоставление = ИзвлечьСопоставлениеНоменклатурыИзДереваДокумента(СтрокаТовара);
		Если Сопоставление = Неопределено Тогда
			Продолжить;
		ИначеЕсли Не Сопоставление.Свойство("Идентификатор") Тогда
			Продолжить;
		КонецЕсли;
		// Проверим, что номенклатура уже заполнена.
		Если Сопоставление.Свойство("НоменклатураИБ") Тогда
			Если  ЗначениеЗаполнено(Сопоставление.НоменклатураИБ) Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		// Если не заполнено, то добавим данные к сопоставлению.
		ИдентификаторВСтрокеТоваров.Вставить(СтрокаТовара, Сопоставление.Идентификатор);
		НоменклатураКонтрагента = НоваяНоменклатураКонтрагента();
		ЗаполнитьЗначенияСвойств(НоменклатураКонтрагента, Сопоставление);
		НоменклатураКонтрагента.Владелец = Владелец;
		НаСопоставление.Добавить(НоменклатураКонтрагента);
		
	КонецЦикла;
	
	Отбор = Новый Структура("НоменклатураКонтрагента", НаСопоставление);
	Соответствие = НайтиСоответствиеНоменклатуры(Отбор, Истина);
	
	Для Каждого Элемент Из ИдентификаторВСтрокеТоваров Цикл
		
		СтрокаТовара = Элемент.Ключ;
		Идентификатор = Элемент.Значение;
		ПутьКСопоставлению = СтрокаТовара.ПолныйПуть + ".Сопоставление";
		Для Каждого ЭлементСоответствия Из Соответствие Цикл
			Если Идентификатор = ЭлементСоответствия.НоменклатураКонтрагента.Идентификатор Тогда
				ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
					СтрокаТовара, ПутьКСопоставлению + ".НоменклатураИБ", ЭлементСоответствия.НоменклатураИБ.Номенклатура);
				ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
					СтрокаТовара, ПутьКСопоставлению + ".ХарактеристикаИБ", ЭлементСоответствия.НоменклатураИБ.Характеристика);
				ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
					СтрокаТовара, ПутьКСопоставлению + ".УпаковкаИБ", ЭлементСоответствия.НоменклатураИБ.Упаковка);
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;	

КонецПроцедуры

// Извлекает данные для сопоставления номенклатуры из строки товара дерева документа.
//
// Параметры:
//  СтрокаТовара - СтрокаДереваЗначений - строка товара дерева документа, из которой нужно извлечь данные.
//
// Возвращаемое значение:
//  Структура - информация для сопоставления товаров.
//
Функция ИзвлечьСопоставлениеНоменклатурыИзДереваДокумента(Знач СтрокаТовара)
	
	ПутьКСопоставлению = СтрокаТовара.ПолныйПуть + ".Сопоставление";
	
	ЕстьСопоставление = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(
		СтрокаТовара, ПутьКСопоставлению, Ложь);
		
	Если ЕстьСопоставление = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Сопоставление = Новый Структура;
	Сопоставление.Вставить("Идентификатор");
	Сопоставление.Вставить("Наименование");
	Сопоставление.Вставить("Характеристика");
	Сопоставление.Вставить("ЕдиницаИзмерения");
	Сопоставление.Вставить("ЕдиницаИзмеренияКод");
	Сопоставление.Вставить("Артикул");
	Сопоставление.Вставить("СтавкаНДС");
	Сопоставление.Вставить("Штрихкод");
	Сопоставление.Вставить("ШтрихкодКомбинации");
	Сопоставление.Вставить("ШтрихкодыНоменклатуры");
	Сопоставление.Вставить("НоменклатураИБ");
	Сопоставление.Вставить("ХарактеристикаИБ");
	Сопоставление.Вставить("УпаковкаИБ");
	
	Для Каждого Элемент Из Сопоставление Цикл
		
		Значение = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(
			СтрокаТовара, ПутьКСопоставлению + "." + Элемент.Ключ, Ложь);
		Если ЗначениеЗаполнено(Значение) Тогда
			Сопоставление.Вставить(Элемент.Ключ, Значение);
		Иначе
			Сопоставление.Удалить(Элемент.Ключ);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Сопоставление;
	
КонецФункции

// См. ОбменСКонтрагентамиКлиентСервер.НоваяНоменклатураКонтрагента.
Функция НоваяНоменклатураКонтрагента(Знач Владелец = Неопределено, Знач Идентификатор = Неопределено) Экспорт
	
	НоменклатураКонтрагента = Новый Структура;
	НоменклатураКонтрагента.Вставить("Владелец"           , Владелец);
	НоменклатураКонтрагента.Вставить("Идентификатор"      , ?(ЗначениеЗаполнено(Идентификатор), Идентификатор, ""));
	НоменклатураКонтрагента.Вставить("Наименование"       , "");
	НоменклатураКонтрагента.Вставить("Характеристика"     , "");
	НоменклатураКонтрагента.Вставить("ЕдиницаИзмерения"   , "");
	НоменклатураКонтрагента.Вставить("ЕдиницаИзмеренияКод", "");
	НоменклатураКонтрагента.Вставить("Артикул"            , "");
	НоменклатураКонтрагента.Вставить("СтавкаНДС"          , "");
	НоменклатураКонтрагента.Вставить("ШтрихкодКомбинации"                , "");
	НоменклатураКонтрагента.Вставить("ШтрихкодыНоменклатуры"             , "");
	НоменклатураКонтрагента.Вставить("ИдентификаторНоменклатурыСервиса"  , "");
	НоменклатураКонтрагента.Вставить("ИдентификаторХарактеристикиСервиса", "");
	НоменклатураКонтрагента.Вставить("ИдентификаторНоменклатуры"         , "");
	НоменклатураКонтрагента.Вставить("ИдентификаторХарактеристики"       , "");
	НоменклатураКонтрагента.Вставить("ИдентификаторУпаковки"             , "");
	НоменклатураКонтрагента.Вставить("ИсторияИдентификаторов", Новый Массив);
	
	Возврат НоменклатураКонтрагента;
	
КонецФункции

Функция НайтиСоответствиеНоменклатуры(Знач Отбор = Неопределено, Знач ТолькоСопоставленные = Ложь) Экспорт
	
	Если Отбор = Неопределено Тогда
		Отбор = Новый Структура;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Владелец КАК Владелец,
	|	Таблица.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ втТаблицаНоменклатурыКонтрагентов
	|ИЗ
	|	&ТаблицаНоменклатурыКонтрагентов КАК Таблица
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Владелец,
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.Номенклатура КАК Номенклатура,
	|	Таблица.Характеристика КАК Характеристика,
	|	Таблица.Упаковка КАК Упаковка
	|ПОМЕСТИТЬ втТаблицаНоменклатурыИБ
	|ИЗ
	|	&ТаблицаНоменклатурыИБ КАК Таблица
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Упаковка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Сопоставление.Контрагент КАК Владелец,
	|	Сопоставление.Идентификатор КАК Идентификатор,
	|	Сопоставление.НаименованиеНоменклатурыКонтрагента КАК Наименование,
	|	Сопоставление.АртикулНоменклатурыКонтрагента КАК Артикул,
	|	Сопоставление.Номенклатура КАК Номенклатура,
	|	Сопоставление.ХарактеристикаНоменклатуры КАК Характеристика,
	|	Сопоставление.ШтрихкодНоменклатурыКонтрагента КАК ШтрихкодыНоменклатуры
	|ИЗ
	|	РегистрСведений.НоменклатураКонтрагентов КАК Сопоставление
	|ГДЕ
	|	&УсловиеПоНоменклатуреКонтрагентов
	|	И &УсловиеПоНоменклатуреИБ
	|	И &УсловиеПоВладельцу";
	
	Если Отбор.Свойство("НоменклатураКонтрагента") Тогда
		
		Таблица = НоваяТаблицаНоменклатурыКонтрагентов(Отбор.НоменклатураКонтрагента);
		Запрос.УстановитьПараметр("ТаблицаНоменклатурыКонтрагентов", Таблица);
		
		ТекстЗамены = "(Сопоставление.Контрагент,Сопоставление.Идентификатор) В (ВЫБРАТЬ вт.Владелец,вт.Идентификатор ИЗ втТаблицаНоменклатурыКонтрагентов КАК вт)";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоНоменклатуреКонтрагентов", ТекстЗамены);
		
	Иначе
		
		Таблица = НоваяТаблицаНоменклатурыКонтрагентов();
		Запрос.УстановитьПараметр("ТаблицаНоменклатурыКонтрагентов", Таблица);
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоНоменклатуреКонтрагентов", "ИСТИНА");
		
	КонецЕсли;
	
	Если Отбор.Свойство("НоменклатураИБ") Тогда
		
		Таблица = НоваяТаблицаНоменклатурыИнформационнойБазы(Отбор.НоменклатураИБ);
		Запрос.УстановитьПараметр("ТаблицаНоменклатурыИБ", Таблица);
		
		ТекстЗамены = "(Сопоставление.Номенклатура,Сопоставление.Характеристика,Сопоставление.Упаковка) В (ВЫБРАТЬ вт.Номенклатура,вт.Характеристика,вт.Упаковка ИЗ втТаблицаНоменклатурыИБ КАК вт)";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоНоменклатуреИБ", ТекстЗамены);
		
	Иначе
		
		Таблица = НоваяТаблицаНоменклатурыИнформационнойБазы();
		Запрос.УстановитьПараметр("ТаблицаНоменклатурыИБ", Таблица);
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоНоменклатуреИБ", "ИСТИНА");
		
	КонецЕсли;
	
	Если Отбор.Свойство("Владелец") Тогда
		
		Запрос.УстановитьПараметр("Владелец", Отбор.Владелец);
		
		ТекстЗамены = "Сопоставление.Контрагент = &Владелец";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоВладельцу", ТекстЗамены);
		
	Иначе
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоВладельцу", "ИСТИНА");
		
	КонецЕсли;
	
	Соответствие = Новый Массив;
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Пока Выборка.Следующий() Цикл
		
		Если ТолькоСопоставленные И Не ЗначениеЗаполнено(Выборка.Номенклатура) Тогда
			Продолжить;
		КонецЕсли;
		
		НоменклатураКонтрагента = НоваяНоменклатураКонтрагента();
		НоменклатураИБ = НоваяНоменклатураИнформационнойБазы();
		
		ЗаполнитьЗначенияСвойств(НоменклатураКонтрагента, Выборка);
		
		ЗаполнитьЗначенияСвойств(НоменклатураИБ, Выборка);
		
		ЭлементСоответствия = Новый Структура("НоменклатураКонтрагента,НоменклатураИБ",
			НоменклатураКонтрагента, НоменклатураИБ);
			
		Соответствие.Добавить(ЭлементСоответствия);
		
	КонецЦикла;
	
	Возврат Соответствие;
	
КонецФункции

Функция НоваяНоменклатураИнформационнойБазы(Знач Номенклатура = Неопределено, Знач Характеристика = Неопределено, Знач Упаковка = Неопределено) Экспорт
	
	НоменклатураИБ = Новый Структура;
	НоменклатураИБ.Вставить("Номенклатура"  , Номенклатура);
	НоменклатураИБ.Вставить("Характеристика", Характеристика);
	НоменклатураИБ.Вставить("Упаковка"      , Упаковка);
	
	Возврат НоменклатураИБ;
	
КонецФункции

Функция НоваяТаблицаНоменклатурыИнформационнойБазы(Знач НаборНоменклатурыИБ = Неопределено)
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	Таблица.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	Таблица.Колонки.Добавить("Упаковка", Новый ОписаниеТипов("СправочникСсылка.ЕдиницыИзмерения"));
	
	Если ЗначениеЗаполнено(НаборНоменклатурыИБ) Тогда
		
		Если ТипЗнч(НаборНоменклатурыИБ) <> Тип("Массив") Тогда
			НаборНоменклатурыИБ = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(НаборНоменклатурыИБ);
		КонецЕсли;
		Для Каждого НоменклатураИБ Из НаборНоменклатурыИБ Цикл
			
			Запись = Таблица.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, НоменклатураИБ);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Таблица;
	
КонецФункции

// Возвращает таблицу значений с колонками, описывающими номенклатуру контрагента.
// Таблица может быть инициализирована массивом структур с аналогичным составом свойств.
//
// Параметры
//  НаборНоменклатурыКонтрагентов - Массив, Структура - структура или массив структур, описывающих номенклатуру контрагента, 
//                                                      для инициализации таблицы.
//                                                      См. ОбменСКонтрагентамиСлужебныйКлиентСервер.НоваяНоменклатураИнформационнойБазы.
//                                - Неопределено - возвращаемая таблица не будет содержать строк.
//
// Возвращаемое значение:
//  ТаблицаЗначений - таблица с типизированными колонками, описывающими номенклатуру контрагента.
//                    Состав колонок аналогичен свойствам структуры, возвращаемой методом 
//                    ОбменСКонтрагентамиСлужебныйКлиентСервер.НоваяНоменклатураКонтрагента.
//
Функция НоваяТаблицаНоменклатурыКонтрагентов(Знач НаборНоменклатурыКонтрагентов = Неопределено)
	
	СтруктураТаблицы = НоваяНоменклатураКонтрагента();
	
	Таблица = Новый ТаблицаЗначений;
	
	СкопироватьКолонкиСтруктурыСопоставленияБЭДВТаблицу(СтруктураТаблицы, Таблица);
	
	Если ЗначениеЗаполнено(НаборНоменклатурыКонтрагентов) Тогда
		
		Если ТипЗнч(НаборНоменклатурыКонтрагентов) <> Тип("Массив") Тогда
			НаборНоменклатурыКонтрагентов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(НаборНоменклатурыКонтрагентов);
		КонецЕсли;
		Для Каждого НоменклатураКонтрагента Из НаборНоменклатурыКонтрагентов Цикл
			
			Запись = Таблица.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, НоменклатураКонтрагента);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Таблица;
	
КонецФункции

Процедура СкопироватьКолонкиСтруктурыСопоставленияБЭДВТаблицу(СтруктураТаблицы, Таблица)
	
	ТипСтрока300 = Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(300));
	
	Для Каждого Элемент Из СтруктураТаблицы Цикл
		
		ИмяКолонки = Элемент.Ключ;
		ТипКолонки = ТипСтрока300;
		Если Элемент.Ключ = "Владелец" Тогда
			ТипКолонки = Новый ОписаниеТипов("СправочникСсылка.Контрагенты");
		ИначеЕсли Элемент.Ключ = "НоменклатураИБ" Тогда
			ТипКолонки = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
		ИначеЕсли Элемент.Ключ = "ХарактеристикаИБ" Тогда
			ТипКолонки = Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры");
		ИначеЕсли Элемент.Ключ = "УпаковкаИБ" Тогда
			ТипКолонки = Новый ОписаниеТипов("СправочникСсылка.ЕдиницыИзмерения");
		КонецЕсли;
		Таблица.Колонки.Добавить(ИмяКолонки, ТипКолонки);
		
	КонецЦикла;

КонецПроцедуры
