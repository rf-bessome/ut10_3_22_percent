
#Область ПрограммныйИнтерфейс

#Область ОчередьЧековККТ 

// Чеки в очереди на фискализацию.
//
// Параметры:
//  КассаККМ - ОпределяемыйТип.КассаБПО - Касса по которой провести фискализацию, если не указано тогда по всем.
//
// Возвращаемое значение:
//  Массив.
Функция ЧекиВОчередиНаФискализацию(КассаККМ = Неопределено) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОчередьЧековККТ.ИдентификаторЗаписи КАК ИдентификаторЗаписи,
	|	ОчередьЧековККТ.ТипДокумента КАК ТипДокумента,
	|	ОчередьЧековККТ.ТипРасчета КАК ТипРасчета,
	|	ОчередьЧековККТ.Организация КАК Организация,
	|	ОчередьЧековККТ.СтатусЧека КАК СтатусЧека,
	|	ОчередьЧековККТ.ТорговыйОбъект КАК ТорговыйОбъект,
	|	ОчередьЧековККТ.ДокументОснование КАК ДокументОснование,
	|	ОчередьЧековККТ.Сумма КАК Сумма,
	|	ОчередьЧековККТ.ДанныеЧека КАК ДанныеЧека,
	|	ОчередьЧековККТ.ТипПакетнойОперации КАК ТипПакетнойОперации,
	|	ОчередьЧековККТ.ЭквайринговыйТерминал КАК ЭквайринговыйТерминал,
	|	ОчередьЧековККТ.ПараметрыПакетнойОперации КАК ПараметрыПакетнойОперации,
	|	ОчередьЧековККТ.КассаККМ КАК КассаККМ
	|ИЗ
	|	РегистрСведений.ОчередьЧековККТ КАК ОчередьЧековККТ
	|ГДЕ
	|	&Условие
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОчередьЧековККТ.Дата УБЫВ");
	
	ТекстУсловия = "ОчередьЧековККТ.СтатусЧека = ЗНАЧЕНИЕ(Перечисление.СтатусЧекаККТВОчереди.Новый)";
	Если ЗначениеЗаполнено(КассаККМ) Тогда
		ТекстУсловия = ТекстУсловия + " И ОчередьЧековККТ.КассаККМ = &КассаККМ";
		Запрос.УстановитьПараметр("КассаККМ", КассаККМ);
	КонецЕсли;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие", ТекстУсловия);
	
	ЧекиНаФискализацию = Новый Массив();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДанныеЧека = Новый Структура;
		ДанныеЧека.Вставить("ИдентификаторЗаписи");
		ДанныеЧека.Вставить("ТипДокумента");
		ДанныеЧека.Вставить("ТипРасчета");
		ДанныеЧека.Вставить("СтатусЧека");
		ДанныеЧека.Вставить("Организация");
		ДанныеЧека.Вставить("ТорговыйОбъект");
		ДанныеЧека.Вставить("Сумма");
		ДанныеЧека.Вставить("ДокументОснование");
		ДанныеЧека.Вставить("ТипПакетнойОперации");
		ДанныеЧека.Вставить("ЭквайринговыйТерминал");
		ДанныеЧека.Вставить("КассаККМ");
		ДанныеЧека.Вставить("ДанныеЧека", Новый Структура());
		ДанныеЧека.Вставить("ПараметрыПакетнойОперации", Новый Структура());
		
		ЗаполнитьЗначенияСвойств(ДанныеЧека, Выборка);
		
		ХранилищеДанныеЧека = ДанныеЧека.ДанныеЧека;
		Если ТипЗнч(ХранилищеДанныеЧека) = Тип("ХранилищеЗначения") Тогда
			ДанныеЧека.ДанныеЧека = ХранилищеДанныеЧека.Получить();
		КонецЕсли;
		
		ХранилищеПараметрыПакетнойОперации = ДанныеЧека.ПараметрыПакетнойОперации;
		Если ТипЗнч(ХранилищеПараметрыПакетнойОперации) = Тип("ХранилищеЗначения") Тогда
			ДанныеЧека.ПараметрыПакетнойОперации = ХранилищеПараметрыПакетнойОперации.Получить();
		КонецЕсли;
		
		ЧекиНаФискализацию.Добавить(ДанныеЧека);
	КонецЦикла;
	
	Возврат ЧекиНаФискализацию;
	
КонецФункции

#КонецОбласти

#Область ФормированиеДанныхККТ

// Возвращает для каких типов товаров будет заполняться отраслевой реквизит.
//
// Возвращаемое значение:
//   Структура: 
//  * ИзделияИзНатуральногоМеха - Булево - Заполнения для изделия из натурального меха. 
//  * ОбъемноСортовойУчет - Булево - Заполнения для товаров объемно сортового учета.
//  * МолочнаяПродукцияСНечитаемымиКМ - Булево - Заполнения для молочной продукция с нечитаемыми КМ.
//
Функция ТипыТоваровДляЗаполненияОтраслевогоРеквизита() Экспорт;
	
	ТипыТоваров = Новый Структура();
	ТипыТоваров.Вставить("ИзделияИзНатуральногоМеха", Ложь);
	ТипыТоваров.Вставить("ОбъемноСортовойУчет", Ложь);
	ТипыТоваров.Вставить("МолочнаяПродукцияСНечитаемымиКМ", Ложь);
	Возврат ТипыТоваров;
	
КонецФункции   

// Возвращает для каких типов товаров будет заполняться отраслевой реквизит.
//
// Возвращаемое значение:
//   Структура - см.ТипыТоваровДляЗаполненияОтраслевогоРеквизита()  
//
Функция ОтраслевойРеквизитЗаполняетсяДляТиповТоваров() Экспорт
	
	ТипыТоваров = ТипыТоваровДляЗаполненияОтраслевогоРеквизита();
	МенеджерОборудованияВызовСервераПереопределяемый.ОтраслевойРеквизитЗаполняетсяДляТиповТоваров(ТипыТоваров);
	Возврат ТипыТоваров;
	
КонецФункции


// Возвращает ведется объемно сортовой учет.
//
// Возвращаемое значение:
//  Булево.
//           
Функция ВедетсяОбъемноСортовойУчет() Экспорт
	
	Результат = Ложь;
	ОбъемноСортовойУчет = Результат; 
	СтандартнаяОбработка = Истина;
	МенеджерОборудованияВызовСервераПереопределяемый.ВедетсяОбъемноСортовойУчет(ОбъемноСортовойУчет, СтандартнаяОбработка);
	Результат = ?(СтандартнаяОбработка, Результат, ОбъемноСортовойУчет);
	Возврат Результат; 
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс
// Возвращает период хранения платежных операций в регистре сведений
//
// Возвращаемое значение:
//   ПериодХранения - ПеречислениеСсылка.ПериодХраненияИсторииПлатежныхОпераций
Функция ПериодХраненияИсторииОперацийОчередиЧеков() Экспорт
	
	ПериодХранения = Константы.СрокХраненияОперацийОчередиЧеков.Получить();
	Возврат ПериодХранения;
	
КонецФункции

// Возвращает период хранения платежных операций в регистре сведений
//
// Возвращаемое значение:
//   ПериодХранения - ПеречислениеСсылка.ПериодХраненияИсторииПлатежныхОпераций
Функция ПериодХраненияИсторииФискальныхОпераций() Экспорт
	
	ПериодХранения = Константы.СрокХраненияФискальныхОпераций.Получить();
	Возврат ПериодХранения;
	
КонецФункции

// Установить значение констант сроков хранения операций в регистрах сведений
//
Процедура УстановитьСрокХраненияОперацийОчередиЧеков() Экспорт 
	
	Если НЕ ЗначениеЗаполнено(Константы.СрокХраненияОперацийОчередиЧеков.Получить()) Тогда
		Константы.СрокХраненияОперацийОчередиЧеков.Установить(Перечисления.ПериодХраненияИсторииПлатежныхОпераций.ВсеВремя);
	КонецЕсли;
	
КонецПроцедуры

// Установить значение констант сроков хранения операций в регистрах сведений
//
Процедура УстановитьСрокХраненияФискальныхОпераций() Экспорт 
	
	Если НЕ ЗначениеЗаполнено(Константы.СрокХраненияФискальныхОпераций.Получить()) Тогда
		Константы.СрокХраненияФискальныхОпераций.Установить(Перечисления.ПериодХраненияИсторииПлатежныхОпераций.ВсеВремя);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

 #Область СлужебныеПроцедурыИФункции

 #Область ФормированиеДанныхККТ

// Данные запроса проверки кода для разрешительной системы
//
Процедура ЗаполнитьДанныеЗапросПроверкиКода(Позиция)
	
	ЗапросПроверкиКода = Позиция.ЗапросПроверкиКода; 
	ОтраслевойРеквизит = Позиция.ОтраслевойРеквизит;
	
	Если Не ПустаяСтрока(ЗапросПроверкиКода.ИдентификаторЗапроса) И Не ПустаяСтрока(ЗапросПроверкиКода.ВременнаяМетка) Тогда
		ОтраслевойРеквизит.ИдентификаторФОИВ = "030"; // 1262 - "030"
		ОтраслевойРеквизит.ДатаДокументаОснования = Дата("20231121"); // 1263 - "21.11.2023"
		ОтраслевойРеквизит.НомерДокументаОснования = "1944"; // 1264 - "1944"
		ЗначениеРеквизита = "UUID=%1&Time=%2";             
		ЗначениеРеквизита = СтрШаблон(ЗначениеРеквизита, ЗапросПроверкиКода.ИдентификаторЗапроса, ЗапросПроверкиКода.ВременнаяМетка); 
		ОтраслевойРеквизит.ЗначениеРеквизита = ЗначениеРеквизита;  // 1265  
	КонецЕсли;
	
КонецПроцедуры 

Процедура ЗаполнитьОтраслевойРеквизитМолочнаяПродукцияСНечитаемымиКМ(Позиция) Экспорт
	
	ТипыТоваров = ОтраслевойРеквизитЗаполняетсяДляТиповТоваров();
	ОтраслевойРеквизит = Позиция.ОтраслевойРеквизит;
	Если ТипыТоваров.МолочнаяПродукцияСНечитаемымиКМ Тогда   
		ДатаПроизводства = ?(Позиция.ДатаПроизводства <> Неопределено, Формат(Позиция.ДатаПроизводства, "ДФ=""ггММдд"""), "");
		ОтраслевойРеквизит.ИдентификаторФОИВ = "030"; // 1262 
		ОтраслевойРеквизит.ДатаДокументаОснования = Дата("20201215"); // 1263
		ОтраслевойРеквизит.НомерДокументаОснования = "2099"; // 1264
		ОтраслевойРеквизит.ЗначениеРеквизита = "pd=" + ДатаПроизводства + "&km=tech"; // 1265 - ГГММДД – дата производства товара
	КонецЕсли; 
	
КонецПроцедуры 

#КонецОбласти

#Область Служебные

// Преобразовать в булево возможные значения строки
Функция ПреобразоватьВБулево(Строка)
	Попытка
		Возврат XMLЗначение(Тип("Булево"), Строка);
	Исключение
		Возврат ВРег(Строка) = "ДА" Или ВРег(Строка) = "TRUE" Или ВРег(Строка) = "YES";
	КонецПопытки;
КонецФункции

#КонецОбласти

#КонецОбласти
