
Процедура ПроверитьСериюРассчитатьСтатус(ТекущаяСтрока, СтруктураДействий)
	
	Перем ПараметрыДействия;
	
	Если СтруктураДействий.Свойство("ПроверитьСериюРассчитатьСтатус", ПараметрыДействия) Тогда
		
		РезультатРасчета = НоменклатураСервер.ПроверитьСериюРассчитатьСтатусПриИзмененииРеквизитаВТЧ(
								ТекущаяСтрока,
								ПараметрыДействия.Склад,
								ПараметрыДействия.ПараметрыУказанияСерий);
								
		ЗаполнитьЗначенияСвойств(ТекущаяСтрока, РезультатРасчета);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Если КэшированныеЗначения = Неопределено Тогда
		КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСерверИСУТ.ПолучитьСтруктуруКэшируемыеЗначения();
	КонецЕсли;
	
	ПересчитатьКоличествоЕдиницВСтрокеТЧ(               ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВСтрокеТЧ( ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ПроверитьХарактеристикуПоВладельцу(                 ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ПроверитьЗаполнитьУпаковкуПоВладельцу(              ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ЗаполнитьСтавкуНДС(                                 ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ПроверитьСериюРассчитатьСтатус(                     ТекущаяСтрока, СтруктураДействий                      );
	ЗаполнитьНоменклатуруЕГАИС(                         ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ЗаполнитьАлкогольнуюПродукцию(                      ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ПересчитатьСуммуВСтрокеТЧ(                          ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ПересчитатьСуммуНДСВСтрокеТЧ(                       ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ПересчитатьСуммуСНДСВСтрокеТЧ(                      ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ПересчитатьЦенуПоСуммеВСтрокеТЧ(                    ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ПересчитатьКоличествоУпаковокВСтрокеТЧ(             ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ПересчитатьКоличествоЗЕРНОВСтрокеТЧ(                ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ЗаполнитьИндексАкцизнойМаркиДляСтрокиТабличнойЧасти(ТекущаяСтрока, СтруктураДействий                      );
	НоменклатураПриИзмененииПереопределяемый(           ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);

	ПоместитьОбработанныеСтрокиВКэшированныеЗначения(   ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

Процедура ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения)
	
	Перем СтруктураПараметровДействия;
	СтруктураДействийЗаполнения = Новый Структура;
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакМаркируемаяАлкогольнаяПродукция", СтруктураПараметровДействия)
		И ЗначениеЗаполнено(СтруктураПараметровДействия)
		И ТекущаяСтрока.Свойство(СтруктураПараметровДействия.Номенклатура) Тогда
		
		СтруктураДействийЗаполнения.Вставить("ЗаполнитьПризнакМаркируемаяАлкогольнаяПродукция", СтруктураПараметровДействия);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакАлкогольнаяПродукция", СтруктураПараметровДействия)
		И ЗначениеЗаполнено(СтруктураПараметровДействия) Тогда
		
		СтруктураДействийЗаполнения.Вставить("ЗаполнитьПризнакАлкогольнаяПродукция", СтруктураПараметровДействия);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакЕдиницаИзмерения", СтруктураПараметровДействия)
		И ЗначениеЗаполнено(СтруктураПараметровДействия) Тогда
		
		СтруктураДействийЗаполнения.Вставить("ЗаполнитьПризнакЕдиницаИзмерения", СтруктураПараметровДействия);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакАртикул", СтруктураПараметровДействия)
		И ЗначениеЗаполнено(СтруктураПараметровДействия) Тогда
		
		СтруктураДействийЗаполнения.Вставить("ЗаполнитьПризнакАртикул", СтруктураПараметровДействия);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакХарактеристикиИспользуются", СтруктураПараметровДействия)
		И ЗначениеЗаполнено(СтруктураПараметровДействия) Тогда
		
		СтруктураДействийЗаполнения.Вставить("ЗаполнитьПризнакХарактеристикиИспользуются", СтруктураПараметровДействия);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакСтатусУказанияСерий", СтруктураПараметровДействия)
		И ЗначениеЗаполнено(СтруктураПараметровДействия) Тогда
		
		СтруктураДействийЗаполнения.Вставить("ЗаполнитьПризнакСтатусУказанияСерий", СтруктураПараметровДействия);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакТипНоменклатуры", СтруктураПараметровДействия)
		И ЗначениеЗаполнено(СтруктураПараметровДействия) Тогда
		
		СтруктураДействийЗаполнения.Вставить("ЗаполнитьПризнакТипНоменклатуры", СтруктураПараметровДействия);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакВедетсяУчетПоГТД", СтруктураПараметровДействия)
		И ЗначениеЗаполнено(СтруктураПараметровДействия) Тогда
		
		СтруктураДействийЗаполнения.Вставить("ЗаполнитьПризнакВедетсяУчетПоГТД", СтруктураПараметровДействия);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакЭтоУслуга", СтруктураПараметровДействия)
		И ЗначениеЗаполнено(СтруктураПараметровДействия) Тогда
		
		СтруктураДействийЗаполнения.Вставить("ЗаполнитьПризнакЭтоУслуга", СтруктураПараметровДействия);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакПодакцизныйТовар", СтруктураПараметровДействия)
		И ЗначениеЗаполнено(СтруктураПараметровДействия) Тогда
		
		СтруктураДействийЗаполнения.Вставить("ЗаполнитьПризнакПодакцизныйТовар", СтруктураПараметровДействия);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакСерииИспользуются", СтруктураПараметровДействия)
		И ЗначениеЗаполнено(СтруктураПараметровДействия) Тогда
		
		СтруктураДействийЗаполнения.Вставить("ЗаполнитьПризнакСерииИспользуются", СтруктураПараметровДействия);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ПересчитатьВесОбъем") Тогда
		СтруктураДействийЗаполнения.Вставить("ПересчитатьВесОбъем", СтруктураДействий.ПересчитатьВесОбъем);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьКодТНВЭД") Тогда
		СтруктураДействийЗаполнения.Вставить("КодТНВЭД", СтруктураДействий.ЗаполнитьКодТНВЭД);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьПредставлениеОКПД2") Тогда
		СтруктураДействийЗаполнения.Вставить("ОКПД2Представление", СтруктураДействий.ЗаполнитьПредставлениеОКПД2);
	КонецЕсли;
	Если СтруктураДействий.Свойство("ЗаполнитьКодОКПД2") Тогда
		СтруктураДействийЗаполнения.Вставить("ОКПД2", СтруктураДействий.ЗаполнитьКодОКПД2);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакТребуетВзвешивания", СтруктураПараметровДействия)
		И ЗначениеЗаполнено(СтруктураПараметровДействия) Тогда
		
		СтруктураДействийЗаполнения.Вставить("ЗаполнитьПризнакТребуетВзвешивания", СтруктураПараметровДействия);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакПроизвольнаяЕдиницаУчета", СтруктураПараметровДействия)
		И ЗначениеЗаполнено(СтруктураПараметровДействия) Тогда
		
		СтруктураДействийЗаполнения.Вставить("ЗаполнитьПризнакПроизвольнаяЕдиницаУчета", СтруктураПараметровДействия);
	КонецЕсли;

	Если СтруктураДействийЗаполнения.Количество() <> 0 Тогда
		ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВСтруктуре(ТекущаяСтрока, СтруктураДействийЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Функция ОписаниеДополнительнойИнформации(СтруктураДействий) Экспорт
	
	СтруктураИсточников = Новый Структура;
	СтрокаРеквизитовЗаполнения = "";
	СтрокаРеквизитовВыгрузки = "";
	
	Для Каждого Действие Из СтруктураДействий Цикл
		Если ЗначениеЗаполнено(Действие.Значение) Тогда
			Для Каждого Поле Из Действие.Значение Цикл
				Если Не СтруктураИсточников.Свойство(Поле.Ключ) Тогда
					СтруктураИсточников.Вставить(Поле.Ключ);
					СтрокаРеквизитовВыгрузки = СтрокаРеквизитовВыгрузки + ", " + Поле.Ключ;
				КонецЕсли;
				Если ЗначениеЗаполнено(Поле.Значение) Тогда
					СтрокаРеквизитовЗаполнения = СтрокаРеквизитовЗаполнения + ", " + Поле.Значение;
				КонецЕсли;
			КонецЦикла;
		Иначе 
			Если Не СтруктураИсточников.Свойство("Номенклатура") Тогда
				СтруктураИсточников.Вставить("Номенклатура");
				СтрокаРеквизитовВыгрузки = СтрокаРеквизитовВыгрузки + ", " + "Номенклатура";
			КонецЕсли;
			СтрокаРеквизитовЗаполнения = СтрокаРеквизитовЗаполнения + ", " + Действие.Ключ;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Новый Структура(
		"СтруктураИсточников, РеквизитыЗаполнения, РеквизитыВыгрузки",
		СтруктураИсточников,
		Сред(СтрокаРеквизитовЗаполнения, 2), // Отрезать первый символ строки, т.к. это запятая
		СтрокаРеквизитовВыгрузки);
	
КонецФункции

// Преобразует структуру действий для использования в ПолучитьТекстЗапросаПоСлужебнымРеквизитамТЧ(),
// чтобы рассчитывать нужные поля одним запросом по всей коллекции.
//
// Параметры:
// 		СтруктураДействий - Структура - Структура с действиями по получению служебных реквизитов.
//
Процедура НормализоватьДействия(СтруктураДействий) Экспорт
	
	Суффикс = Неопределено;
	Если СтруктураДействий.Свойство("ПересчитатьВесОбъем", Суффикс)
		И ТипЗнч(Суффикс) <> Тип("Структура") Тогда
		
		СтруктураДействий.Вставить("ПересчитатьВесОбъем",
			Новый Структура("КоличествоУпаковок"+Суффикс+",Номенклатура,Упаковка", "Вес,Объем"));
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПоместитьОбработанныеСтрокиВКэшированныеЗначения(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения)
	
	Если СтруктураДействий.Свойство("ПоместитьОбработанныеСтрокиВКэшированныеЗначения") Тогда
		
		Если ТипЗнч(ТекущаяСтрока) = Тип("ДанныеФормыЭлементКоллекции") Тогда
			КэшированныеЗначения.ОбработанныеСтроки.Добавить(ТекущаяСтрока.ПолучитьИдентификатор());
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает описание типов по переданному типу
// 
// Параметры:
//	Тип - Тип - тип значения
//
// Возвращаемое значение:
//	ОписаниеТипов - созданное на основании переданного типа описание типов.
//
Функция ОписаниеТиповПоТипу(Тип) Экспорт
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип);
	
	ОписаниеТипов = Новый ОписаниеТипов(МассивТипов);
	
	Возврат ОписаниеТипов;
КонецФункции

Функция ПолучитьТекстЗапросаПоСлужебнымРеквизитамТЧ(СтруктураДействий, СтруктураДопДанных) Экспорт
	
	ШаблонЗапроса = "";
	
	// Формирование шаблона запроса временной таблицы по номенклатуре
	ШаблонЗапроса = "
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(Таблица.НомерСтроки КАК ЧИСЛО) КАК НомерСтроки%ТекстВЫБРАТЬ%
	|ПОМЕСТИТЬ втТаблицаНоменклатуры
	|ИЗ
	|	&КоллекцияДанных КАК Таблица;";
	
	ШаблонВЫБРАТЬ = ",
	|	Таблица.%ИмяПоля% КАК %ИмяПоля%";
	ШаблонВЫБРАТЬНоменклатура = ",
	|	ВЫРАЗИТЬ(Таблица.%ИмяПоля% КАК Справочник.Номенклатура) КАК %ИмяПоля%";
	
	ТекстВЫБРАТЬ = "";
	Для Каждого Поле Из СтруктураДопДанных.СтруктураИсточников Цикл
		Если Поле.Ключ = "Упаковка" Тогда
			ТекстВЫБРАТЬ = ТекстВЫБРАТЬ + ",
			|	ВЫРАЗИТЬ(Таблица.Упаковка КАК Справочник.ЕдиницыИзмерения) КАК Упаковка";
		ИначеЕсли СтрНачинаетсяС(Поле.Ключ, "Номенклатура") Тогда
			ТекстВЫБРАТЬ = ТекстВЫБРАТЬ + СтрЗаменить(ШаблонВЫБРАТЬНоменклатура, "%ИмяПоля%", Поле.Ключ);
		Иначе
			ТекстВЫБРАТЬ = ТекстВЫБРАТЬ + СтрЗаменить(ШаблонВЫБРАТЬ, "%ИмяПоля%", Поле.Ключ);
		КонецЕсли;
	КонецЦикла;
	ШаблонЗапроса = СтрЗаменить(ШаблонЗапроса, "%ТекстВЫБРАТЬ%", ТекстВЫБРАТЬ);
	
	// Шаблон запроса основной выборки
	ШаблонЗапроса = ШаблонЗапроса + "
	|///////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТаблицаНоменклатуры.НомерСтроки КАК НомерСтроки%ТекстВЫБРАТЬ%
	|ИЗ
	|	втТаблицаНоменклатуры КАК втТаблицаНоменклатуры
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	// Формирование полей запроса основной выборки
	ТекстВЫБРАТЬ = "";
	Для Каждого Действие Из СтруктураДействий Цикл
		ШаблонВЫБРАТЬ = ШаблонПоляВыборкиПоКлючуДействия(Действие.Ключ);
		Если ШаблонВЫБРАТЬ <> Неопределено Тогда
			Если ЗначениеЗаполнено(Действие.Значение) Тогда 
				Для Каждого Поле Из Действие.Значение Цикл
					Если ЗначениеЗаполнено(Поле.Значение) Тогда
						ТекстВыбрать = ТекстВЫБРАТЬ + СтрЗаменить(СтрЗаменить(ШаблонВЫБРАТЬ, "%Значение%", Поле.Значение), "%Ключ%", Поле.Ключ);
					КонецЕсли;
				КонецЦикла;
			Иначе 
				ТекстВыбрать = ТекстВЫБРАТЬ + СтрЗаменить(СтрЗаменить(ШаблонВЫБРАТЬ, "%Значение%", Действие.Ключ), "%Ключ%", "Номенклатура");
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтрЗаменить(ШаблонЗапроса, "%ТекстВЫБРАТЬ%", ТекстВЫБРАТЬ);
	
КонецФункции

Функция ШаблонПоляВыборкиПоКлючуДействия(КлючДействия, ТолькоПроверитьПоддерживаемыеКлючи = Ложь)
	
	Если КлючДействия = "ЗаполнитьПризнакТипНоменклатуры" Тогда
		Возврат ",
		|	втТаблицаНоменклатуры.%Ключ%.ВидНоменклатуры.ТипНоменклатуры КАК %Значение%";
	КонецЕсли;
	
	Если КлючДействия = "ЗаполнитьПризнакМаркируемаяАлкогольнаяПродукция" Тогда
		Возврат ",
		|	ВЫБОР КОГДА втТаблицаНоменклатуры.%Ключ%.АлкогольнаяПродукцияВоВскрытойТаре ТОГДА
		|		ЛОЖЬ
		|	ИНАЧЕ
		|		ЕСТЬNULL(втТаблицаНоменклатуры.%Ключ%.ВидАлкогольнойПродукцииЕГАИС.Маркируемый, ЛОЖЬ)
		|	КОНЕЦ КАК %Значение%";
	КонецЕсли;
	
	Если КлючДействия = "ЗаполнитьПризнакАлкогольнаяПродукция" Тогда
		Возврат ",
		|	втТаблицаНоменклатуры.%Ключ%.АлкогольнаяПродукция КАК %Значение%";
	КонецЕсли;
	
	Если КлючДействия = "ЗаполнитьПризнакЕдиницаИзмерения" Тогда
		Возврат ",
		|	втТаблицаНоменклатуры.%Ключ%.ЕдиницаХраненияОстатков КАК %Значение%";
	КонецЕсли;
	
	Если КлючДействия = "ЗаполнитьПризнакТипИзмеряемойВеличины" Тогда
		Возврат ",
		|	втТаблицаНоменклатуры.%Ключ%.ЕдиницаИзмерения.ТипИзмеряемойВеличины КАК %Значение%";
	КонецЕсли;
	
	Если КлючДействия = "ЗаполнитьЕдиницуИзмеренияВеса" Тогда
		Возврат ",
		|	ВЫБОР КОГДА втТаблицаНоменклатуры.Упаковка.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Упаковка)
		|		ТОГДА втТаблицаНоменклатуры.Упаковка.ВесЕдиницаИзмерения
		|	ИНАЧЕ втТаблицаНоменклатуры.Номенклатура.ВесЕдиницаИзмерения
		|	КОНЕЦ КАК ЕдиницаИзмеренияВеса";
	КонецЕсли;
	
	Если КлючДействия = "ЗаполнитьЕдиницуИзмеренияОбъема" Тогда
		Возврат ",
		|	ВЫБОР КОГДА втТаблицаНоменклатуры.Упаковка.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Упаковка)
		|		ТОГДА втТаблицаНоменклатуры.Упаковка.ОбъемЕдиницаИзмерения
		|	ИНАЧЕ втТаблицаНоменклатуры.Номенклатура.ОбъемЕдиницаИзмерения
		|	КОНЕЦ КАК ЕдиницаИзмеренияОбъема";
	КонецЕсли;
	
	Если КлючДействия = "ПересчитатьВесОбъем" Тогда
		ТекстЗапроса = ",
		|	втТаблицаНоменклатуры.%Ключ%
		|	*(ВЫБОР
		|	КОГДА втТаблицаНоменклатуры.Упаковка.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Вес) И втТаблицаНоменклатуры.Номенклатура.ВесИспользовать
		|		ТОГДА 1
		|	КОГДА втТаблицаНоменклатуры.Упаковка.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Объем) И втТаблицаНоменклатуры.Номенклатура.ВесИспользовать
		|		И ЕСТЬNULL(втТаблицаНоменклатуры.Упаковка.Знаменатель,0) <> 0 И ЕСТЬNULL(втТаблицаНоменклатуры.Номенклатура.ОбъемЕдиницаИзмерения.Числитель,0) <> 0 И ЕСТЬNULL(втТаблицаНоменклатуры.Номенклатура.ОбъемЕдиницаИзмерения.Знаменатель,0) <> 0 И ЕСТЬNULL(втТаблицаНоменклатуры.Номенклатура.ОбъемЧислитель,0) <> 0 И ЕСТЬNULL(втТаблицаНоменклатуры.Номенклатура.ОбъемЗнаменатель,0) <> 0 И ЕСТЬNULL(втТаблицаНоменклатуры.Номенклатура.ВесЗнаменатель,0) <> 0
		|		ТОГДА втТаблицаНоменклатуры.Упаковка.Числитель / втТаблицаНоменклатуры.Упаковка.Знаменатель / (втТаблицаНоменклатуры.Номенклатура.ОбъемЕдиницаИзмерения.Числитель / втТаблицаНоменклатуры.Номенклатура.ОбъемЕдиницаИзмерения.Знаменатель) / (втТаблицаНоменклатуры.Номенклатура.ОбъемЧислитель / втТаблицаНоменклатуры.Номенклатура.ОбъемЗнаменатель) * (втТаблицаНоменклатуры.Номенклатура.ВесЧислитель / втТаблицаНоменклатуры.Номенклатура.ВесЗнаменатель)
		|	КОГДА втТаблицаНоменклатуры.Упаковка.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Площадь) И втТаблицаНоменклатуры.Номенклатура.ВесИспользовать
		|		И ЕСТЬNULL(втТаблицаНоменклатуры.Упаковка.Знаменатель,0) <> 0 И ЕСТЬNULL(втТаблицаНоменклатуры.Номенклатура.ПлощадьЕдиницаИзмерения.Числитель,0) <> 0 И ЕСТЬNULL(втТаблицаНоменклатуры.Номенклатура.ПлощадьЕдиницаИзмерения.Знаменатель,0) <> 0 И ЕСТЬNULL(втТаблицаНоменклатуры.Номенклатура.ПлощадьЧислитель,0) <> 0 И ЕСТЬNULL(втТаблицаНоменклатуры.Номенклатура.ПлощадьЗнаменатель,0) <> 0 И ЕСТЬNULL(втТаблицаНоменклатуры.Номенклатура.ВесЗнаменатель,0) <> 0
		|		ТОГДА втТаблицаНоменклатуры.Упаковка.Числитель / втТаблицаНоменклатуры.Упаковка.Знаменатель / (втТаблицаНоменклатуры.Номенклатура.ПлощадьЕдиницаИзмерения.Числитель / втТаблицаНоменклатуры.Номенклатура.ПлощадьЕдиницаИзмерения.Знаменатель) / (втТаблицаНоменклатуры.Номенклатура.ПлощадьЧислитель / втТаблицаНоменклатуры.Номенклатура.ПлощадьЗнаменатель) * (втТаблицаНоменклатуры.Номенклатура.ВесЧислитель / втТаблицаНоменклатуры.Номенклатура.ВесЗнаменатель)
		|	КОГДА втТаблицаНоменклатуры.Упаковка.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Длина) И втТаблицаНоменклатуры.Номенклатура.ВесИспользовать
		|		И ЕСТЬNULL(втТаблицаНоменклатуры.Упаковка.Знаменатель,0) <> 0 И ЕСТЬNULL(втТаблицаНоменклатуры.Номенклатура.ДлинаЕдиницаИзмерения.Числитель,0) <> 0 И ЕСТЬNULL(втТаблицаНоменклатуры.Номенклатура.ДлинаЕдиницаИзмерения.Знаменатель,0) <> 0 И ЕСТЬNULL(втТаблицаНоменклатуры.Номенклатура.ДлинаЧислитель,0) <> 0 И ЕСТЬNULL(втТаблицаНоменклатуры.Номенклатура.ДлинаЗнаменатель,0) <> 0 И ЕСТЬNULL(втТаблицаНоменклатуры.Номенклатура.ВесЗнаменатель,0) <> 0
		|		ТОГДА втТаблицаНоменклатуры.Упаковка.Числитель / втТаблицаНоменклатуры.Упаковка.Знаменатель / (втТаблицаНоменклатуры.Номенклатура.ДлинаЕдиницаИзмерения.Числитель / втТаблицаНоменклатуры.Номенклатура.ДлинаЕдиницаИзмерения.Знаменатель) / (втТаблицаНоменклатуры.Номенклатура.ДлинаЧислитель / втТаблицаНоменклатуры.Номенклатура.ДлинаЗнаменатель) * (втТаблицаНоменклатуры.Номенклатура.ВесЧислитель / втТаблицаНоменклатуры.Номенклатура.ВесЗнаменатель)
		|	КОГДА втТаблицаНоменклатуры.Упаковка.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Упаковка)
		|		ТОГДА втТаблицаНоменклатуры.Упаковка.Вес
		|	КОГДА (втТаблицаНоменклатуры.Упаковка.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.КоличествоШтук)
		|				ИЛИ втТаблицаНоменклатуры.Упаковка.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.ПустаяСсылка)
		|				ИЛИ ЕСТЬNULL(втТаблицаНоменклатуры.Упаковка.Ссылка, ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка))
		|		И втТаблицаНоменклатуры.Номенклатура.ВесИспользовать
		|		И ЕСТЬNULL(втТаблицаНоменклатуры.Номенклатура.ВесЗнаменатель,0) <> 0
		|		ТОГДА втТаблицаНоменклатуры.Номенклатура.ВесЧислитель / втТаблицаНоменклатуры.Номенклатура.ВесЗнаменатель
		|	ИНАЧЕ 0
		|	КОНЕЦ) КАК Вес,
		|
		|	втТаблицаНоменклатуры.%Ключ%
		|	*(ВЫБОР
		|	КОГДА втТаблицаНоменклатуры.Упаковка.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Вес) И втТаблицаНоменклатуры.Номенклатура.ОбъемИспользовать
		|		И ЕСТЬNULL(втТаблицаНоменклатуры.Упаковка.Знаменатель,0) <> 0 И ЕСТЬNULL(втТаблицаНоменклатуры.Номенклатура.ВесЕдиницаИзмерения.Числитель,0) <> 0 И ЕСТЬNULL(втТаблицаНоменклатуры.Номенклатура.ВесЕдиницаИзмерения.Знаменатель,0) <> 0 И ЕСТЬNULL(втТаблицаНоменклатуры.Номенклатура.ВесЧислитель,0) <> 0 И ЕСТЬNULL(втТаблицаНоменклатуры.Номенклатура.ВесЗнаменатель,0) <> 0 И ЕСТЬNULL(втТаблицаНоменклатуры.Номенклатура.ОбъемЗнаменатель,0) <> 0
		|		ТОГДА втТаблицаНоменклатуры.Упаковка.Числитель / втТаблицаНоменклатуры.Упаковка.Знаменатель / (втТаблицаНоменклатуры.Номенклатура.ВесЕдиницаИзмерения.Числитель / втТаблицаНоменклатуры.Номенклатура.ВесЕдиницаИзмерения.Знаменатель) / (втТаблицаНоменклатуры.Номенклатура.ВесЧислитель / втТаблицаНоменклатуры.Номенклатура.ВесЗнаменатель) * (втТаблицаНоменклатуры.Номенклатура.ОбъемЧислитель / втТаблицаНоменклатуры.Номенклатура.ОбъемЗнаменатель)
		|	КОГДА втТаблицаНоменклатуры.Упаковка.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Объем) И втТаблицаНоменклатуры.Номенклатура.ОбъемИспользовать
		|		ТОГДА 1
		|	КОГДА втТаблицаНоменклатуры.Упаковка.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Площадь) И втТаблицаНоменклатуры.Номенклатура.ОбъемИспользовать
		|		И ЕСТЬNULL(втТаблицаНоменклатуры.Упаковка.Знаменатель,0) <> 0 И ЕСТЬNULL(втТаблицаНоменклатуры.Номенклатура.ПлощадьЕдиницаИзмерения.Числитель,0) <> 0 И ЕСТЬNULL(втТаблицаНоменклатуры.Номенклатура.ПлощадьЕдиницаИзмерения.Знаменатель,0) <> 0 И ЕСТЬNULL(втТаблицаНоменклатуры.Номенклатура.ПлощадьЧислитель,0) <> 0 И ЕСТЬNULL(втТаблицаНоменклатуры.Номенклатура.ПлощадьЗнаменатель,0) <> 0 И ЕСТЬNULL(втТаблицаНоменклатуры.Номенклатура.ОбъемЗнаменатель,0) <> 0
		|		ТОГДА втТаблицаНоменклатуры.Упаковка.Числитель / втТаблицаНоменклатуры.Упаковка.Знаменатель / (втТаблицаНоменклатуры.Номенклатура.ПлощадьЕдиницаИзмерения.Числитель / втТаблицаНоменклатуры.Номенклатура.ПлощадьЕдиницаИзмерения.Знаменатель) / (втТаблицаНоменклатуры.Номенклатура.ПлощадьЧислитель / втТаблицаНоменклатуры.Номенклатура.ПлощадьЗнаменатель) * (втТаблицаНоменклатуры.Номенклатура.ОбъемЧислитель / втТаблицаНоменклатуры.Номенклатура.ОбъемЗнаменатель)
		|	КОГДА втТаблицаНоменклатуры.Упаковка.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Длина) И втТаблицаНоменклатуры.Номенклатура.ОбъемИспользовать
		|		И ЕСТЬNULL(втТаблицаНоменклатуры.Упаковка.Знаменатель,0) <> 0 И ЕСТЬNULL(втТаблицаНоменклатуры.Номенклатура.ДлинаЕдиницаИзмерения.Числитель,0) <> 0 И ЕСТЬNULL(втТаблицаНоменклатуры.Номенклатура.ДлинаЕдиницаИзмерения.Знаменатель,0) <> 0 И ЕСТЬNULL(втТаблицаНоменклатуры.Номенклатура.ДлинаЧислитель,0) <> 0 И ЕСТЬNULL(втТаблицаНоменклатуры.Номенклатура.ДлинаЗнаменатель,0) <> 0 И ЕСТЬNULL(втТаблицаНоменклатуры.Номенклатура.ОбъемЗнаменатель,0) <> 0
		|		ТОГДА втТаблицаНоменклатуры.Упаковка.Числитель / втТаблицаНоменклатуры.Упаковка.Знаменатель / (втТаблицаНоменклатуры.Номенклатура.ДлинаЕдиницаИзмерения.Числитель / втТаблицаНоменклатуры.Номенклатура.ДлинаЕдиницаИзмерения.Знаменатель) / (втТаблицаНоменклатуры.Номенклатура.ДлинаЧислитель / втТаблицаНоменклатуры.Номенклатура.ДлинаЗнаменатель) * (втТаблицаНоменклатуры.Номенклатура.ОбъемЧислитель / втТаблицаНоменклатуры.Номенклатура.ОбъемЗнаменатель)
		|	КОГДА втТаблицаНоменклатуры.Упаковка.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Упаковка)
		|		ТОГДА втТаблицаНоменклатуры.Упаковка.Объем
		|	КОГДА (втТаблицаНоменклатуры.Упаковка.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.КоличествоШтук)
		|				ИЛИ втТаблицаНоменклатуры.Упаковка.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.ПустаяСсылка)
		|				ИЛИ ЕСТЬNULL(втТаблицаНоменклатуры.Упаковка.Ссылка, ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка))
		|		И втТаблицаНоменклатуры.Номенклатура.ОбъемИспользовать
		|		И ЕСТЬNULL(втТаблицаНоменклатуры.Номенклатура.ОбъемЗнаменатель,0) <> 0
		|		ТОГДА втТаблицаНоменклатуры.Номенклатура.ОбъемЧислитель / втТаблицаНоменклатуры.Номенклатура.ОбъемЗнаменатель
		|	ИНАЧЕ 0
		|	КОНЕЦ) КАК Объем";
		
		Возврат ТекстЗапроса
	КонецЕсли;
	
	Если КлючДействия = "ЗаполнитьПризнакАртикул" Тогда
		Возврат ",
		|	втТаблицаНоменклатуры.%Ключ%.Артикул КАК %Значение%";
	КонецЕсли;
	
	Если КлючДействия = "ЗаполнитьПризнакВариантОформленияПродажи" Тогда
		Возврат ",
		|	втТаблицаНоменклатуры.%Ключ%.ВариантОформленияПродажи КАК %Значение%";
	КонецЕсли;
	
	Если КлючДействия = "ЗаполнитьПризнакХарактеристикиИспользуются" Тогда
		Возврат ",
		|	втТаблицаНоменклатуры.%Ключ%.ВестиУчетПоХарактеристикам КАК %Значение%";
	КонецЕсли;
	
	Если КлючДействия = "ЗаполнитьПризнакСтатусУказанияСерий" Тогда
		Возврат ",
		|	ВЫБОР КОГДА втТаблицаНоменклатуры.%Ключ%.ВестиУчетПоСериям ТОГДА 1 ИНАЧЕ 0 КОНЕЦ КАК %Значение%";
	КонецЕсли;
	
	Если КлючДействия = "ЗаполнитьПризнакВедетсяУчетПоГТД" Тогда
		Возврат ",
		|	втТаблицаНоменклатуры.%Ключ%.ВестиУчетПоГТД КАК %Значение%";
	КонецЕсли;
	
	Если КлючДействия = "ЗаполнитьПризнакЭтоУслуга" Тогда
		Возврат ",
		|	втТаблицаНоменклатуры.%Ключ%.Услуга КАК %Значение%";
	КонецЕсли;
	
	Если КлючДействия = "ОКПД2" Тогда
		Возврат ",
		|	втТаблицаНоменклатуры.%Ключ%.ОКПД2.Код КАК %Значение%";
	КонецЕсли;
	Если КлючДействия = "ОКПД2Представление" Тогда
		Возврат ",
		|	втТаблицаНоменклатуры.%Ключ%.ОКПД2.Код КАК %Значение%";
	КонецЕсли;
	
	
	Если КлючДействия = "ЗаполнитьПризнакПодакцизныйТовар" Тогда
		Возврат ",
		|	втТаблицаНоменклатуры.%Ключ%.ПодакцизныйТовар КАК %Значение%";
	КонецЕсли;
	
	Если КлючДействия = "ЗаполнитьПризнакСерииИспользуются" Тогда
		Возврат ",
		|	втТаблицаНоменклатуры.%Ключ%.ВестиУчетПоСериям КАК %Значение%";
	КонецЕсли;
	
	Если КлючДействия = "ЗаполнитьПризнакТребуетВзвешивания" Тогда
		Возврат ",
		|	1 В
		|		(ВЫБРАТЬ ПЕРВЫЕ 1
		|			1
		|		ИЗ
		|			РегистрСведений.ОписаниеНоменклатурыИС КАК ОписаниеИС
		|		ГДЕ
		|			втТаблицаНоменклатуры.%Ключ% = ОписаниеИС.Номенклатура
		|
		|	И втТаблицаНоменклатуры.%Ключ%.ВидПродукцииИС В (
		|		ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МолочнаяПродукцияПодконтрольнаяВЕТИС),
		|		ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МолочнаяПродукцияБезВЕТИС),
		|		ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Пиво))
		|	И ОписаниеИС.КоличествоВПотребительскойУпаковке = 0
		| ) КАК %Значение%";
	КонецЕсли;
	
	Если КлючДействия = "ЗаполнитьПризнакПроизвольнаяЕдиницаУчета" Тогда
		Возврат ",
		|	1 В
		|		(ВЫБРАТЬ ПЕРВЫЕ 1
		|			1
		|		ИЗ
		|			РегистрСведений.ОписаниеНоменклатурыИС КАК ОписаниеИС
		|		ГДЕ
		|			втТаблицаНоменклатуры.%Ключ% = ОписаниеИС.Номенклатура
		|			И (ОписаниеИС.КоличествоВПотребительскойУпаковке <> 1
		|			Или ОписаниеИС.ЕмкостьПотребительскойУпаковки > 1)
		|	) КАК %Значение%";
	КонецЕсли;
	
	Если ТипЗнч(КлючДействия) = Тип("Строка")
		И Не ТолькоПроверитьПоддерживаемыеКлючи Тогда
		ТекстЗапроса =
			",
			|	втТаблицаНоменклатуры.%Ключ%.%ИмяПоля% КАК %Значение%";
		Возврат СтрЗаменить(ТекстЗапроса, "%ИмяПоля%", КлючДействия);
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Получает служебные реквизиты по номенклатуре в структуре
//
// Параметры:
// 		СтруктураДанных - Структура, СтрокаТаблицыЗначений - Структура данных, в которой необходимо заполнить поля
// 		СтруктураДействий - Структура - Структура с действиями по получению служебных реквизитов.
//
Процедура ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВСтруктуре(СтруктураДанных, СтруктураДействий) Экспорт
	
	НормализоватьДействия(СтруктураДействий);
	
	СтруктураДопДанных = ОписаниеДополнительнойИнформации(СтруктураДействий);
	
	ТаблицаВыгрузки = Новый ТаблицаЗначений;
	ТаблицаВыгрузки.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"));
	Для Каждого Источник Из СтруктураДопДанных.СтруктураИсточников Цикл
		ТаблицаВыгрузки.Колонки.Добавить(Источник.Ключ, ОписаниеТиповПоТипу(ТипЗнч(СтруктураДанных[Источник.Ключ])));
	КонецЦикла;
	ЗаполнитьЗначенияСвойств(ТаблицаВыгрузки.Добавить(), СтруктураДанных);
	
	Запрос = Новый Запрос(ПолучитьТекстЗапросаПоСлужебнымРеквизитамТЧ(СтруктураДействий, СтруктураДопДанных));
	Запрос.УстановитьПараметр("КоллекцияДанных", ТаблицаВыгрузки);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(СтруктураДанных, Выборка, СтруктураДопДанных.РеквизитыЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьХарактеристикуПоВладельцу(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения)
	
	Перем Характеристика;
	
	ПроверитьХарактеристикуПоВладельцу = СтруктураДействий.Свойство("ПроверитьХарактеристикуПоВладельцу", Характеристика);
	
	Если ПроверитьХарактеристикуПоВладельцу Тогда
		
		Если Не ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура)
		 ИЛИ глЗначениеПеременной("ИспользоватьХарактеристикиНоменклатуры") <> Истина Тогда
			ТекущаяСтрока.Характеристика             = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "ХарактеристикиИспользуются") Тогда
				ТекущаяСтрока.ХарактеристикиИспользуются = Ложь;
			КонецЕсли;
		Иначе
			Запрос = Новый Запрос();
			Запрос.УстановитьПараметр("Номенклатура",   ТекущаяСтрока.Номенклатура);
			Запрос.УстановитьПараметр("Характеристика", Характеристика);
			
			Запрос.Текст =
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ЕСТЬNULL(СпрХарактеристики.Ссылка, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)) КАК Характеристика,
			|	ЕСТЬNULL(СпрНоменклатура.ВестиУчетПоХарактеристикам, ЛОЖЬ) КАК ХарактеристикиИспользуются
			|ИЗ
			|	Справочник.Номенклатура КАК СпрНоменклатура
			|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК СпрХарактеристики
			|		ПО СпрНоменклатура.Ссылка = СпрХарактеристики.Владелец
			|		 И СпрХарактеристики.Ссылка = &Характеристика
			|ГДЕ
			|	СпрНоменклатура.Ссылка = &Номенклатура";
			
			Выборка = Запрос.Выполнить().Выбрать();
			Выборка.Следующий();
			ЗаполнитьЗначенияСвойств(ТекущаяСтрока, Выборка);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьЗаполнитьУпаковкуПоВладельцу(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения)
	
	Перем Упаковка;
	
	ПроверитьЗаполнитьУпаковкуПоВладельцу = СтруктураДействий.Свойство("ПроверитьЗаполнитьУпаковкуПоВладельцу", Упаковка);
	
	Если ПроверитьЗаполнитьУпаковкуПоВладельцу И ТекущаяСтрока.Свойство("Упаковка") Тогда
		
		Если Не ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда
			ТекущаяСтрока.Упаковка = Справочники.ЕдиницыИзмерения.ПустаяСсылка();
		ИначеЕсли Упаковка = Неопределено Тогда
			ТекущаяСтрока.Упаковка = Справочники.ЕдиницыИзмерения.ПустаяСсылка();
		ИначеЕсли ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Упаковка, "Владелец") <> ТекущаяСтрока.Номенклатура Тогда
			ТекущаяСтрока.Упаковка = Справочники.ЕдиницыИзмерения.ПустаяСсылка();
		Иначе
			ТекущаяСтрока.Упаковка = Упаковка;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСтавкуНДС(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения)
	
	ЗаполнитьСтавкуНДС = СтруктураДействий.Свойство("ЗаполнитьСтавкуНДС");
	Если ЗаполнитьСтавкуНДС Тогда 
		Если ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура)Тогда 
			ТекущаяСтрока.СтавкаНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущаяСтрока.Номенклатура, "СтавкаНДС");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ТоварыПередНачаломДобавления(Элемент, Отказ, Копирование, ЭтаФорма, Этотобъект) Экспорт
	
	Если Копирование Тогда
		Если Элемент.ТекущиеДанные <> Неопределено И Элемент.ТекущиеДанные.НеобходимостьВводаАкцизнойМарки Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ТоварыПередНачаломИзменения(Элемент, Отказ, ЭтаФорма, ЭтотОбъект) Экспорт
	
	Если Элемент.ТекущиеДанные <> Неопределено И Элемент.ТекущиеДанные.НеобходимостьВводаАкцизнойМарки
		И (Элемент.ТекущаяКолонка.Имя = "Количество" ИЛИ Элемент.ТекущаяКолонка.Имя = "Номенклатура") Тогда
		Отказ = Истина;
	КонецЕсли;

КонецПроцедуры

Процедура ТоварыПередУдалением(Элемент, Отказ, ЭтаФорма, ЭтотОбъект) Экспорт
	
	СтрокаТабличнойЧасти = Элемент.ТекущиеДанные;
	УправлениеЗапасами.ОчиститьАкцизныеМарки(СтрокаТабличнойЧасти, ЭтотОбъект);

КонецПроцедуры

Процедура ТоварыПриНачалеРедактирования(Элемент, НоваяСтрока, ЭтаФорма, ЭтотОбъект) Экспорт
	
	Если Не НоваяСтрока Тогда
		Элемент.Колонки.Количество.ЭлементУправления.Доступность = НЕ Элемент.ТекущиеДанные.НеобходимостьВводаАкцизнойМарки;
		Элемент.Колонки.Номенклатура.ЭлементУправления.Доступность = НЕ Элемент.ТекущиеДанные.НеобходимостьВводаАкцизнойМарки;
	КонецЕсли;

КонецПроцедуры

Процедура ВводАкцизнойМарки(ЭтаФорма, ЭтотОбъект, ФормаСканированияМарки
		) Экспорт
	
	СтрокаТабличнойЧасти = ЭтаФорма.ЭлементыФормы.Товары.ТекущиеДанные;
	Если СтрокаТабличнойЧасти = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ШтрихкодМарки = ФормаСканированияМарки.ОткрытьМодально();
	
	Если НЕ ЗначениеЗаполнено(ШтрихкодМарки) Тогда
		ЭтотОбъект.Товары.Удалить(СтрокаТабличнойЧасти);
		Возврат;
	КонецЕсли;
	
	ПараметрыСканированияАкцизныхМарок = ШтрихкодированиеИС.ПараметрыСканирования(ЭтаФорма);
	
	СписокШтрихкодов = Новый Массив;
	СписокШтрихкодов.Добавить(ШтрихкодМарки);
	ДанныеШтрихкодов = ШтрихкодированиеИС.ДанныеПоШтрихкодам(СписокШтрихкодов, ПараметрыСканированияАкцизныхМарок, Неопределено);
	
	СтрокаАкцизныеМарки = ЭтотОбъект.АкцизныеМарки.Добавить();
	СтрокаАкцизныеМарки.КлючСтроки       = СтрокаТабличнойЧасти.КлючСтроки;
	СтрокаАкцизныеМарки.КодАкцизнойМарки = ШтрихкодМарки;
	
	Если ДанныеШтрихкодов.ОбработанныеШтрихкоды[ШтрихкодМарки] <> Неопределено Тогда
		СтрокаАкцизныеМарки.АкцизнаяМарка    = ДанныеШтрихкодов.ОбработанныеШтрихкоды[ШтрихкодМарки].ШтрихкодУпаковки;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьНоменклатуруЕГАИС(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения)
	
	Перем Параметры;
	
	Если СтруктураДействий.Свойство("ЗаполнитьНоменклатуруЕГАИС", Параметры) Тогда
		
		Если Параметры.Свойство("ИмяКолонки") Тогда
			ИмяКолонки = Параметры.ИмяКолонки;
		Иначе
			ИмяКолонки = "НоменклатураЕГАИС";
		КонецЕсли;
		
		Если Параметры.Свойство("ЗаполнитьФлагАлкогольнаяПродукция") Тогда
			ЗаполнитьФлагАлкогольнаяПродукция = Параметры.ЗаполнитьФлагАлкогольнаяПродукция;
		Иначе
			ЗаполнитьФлагАлкогольнаяПродукция = Истина;
		КонецЕсли;
		
		Если Параметры.Свойство("Серии") Тогда
			Серии = Параметры.Серии.Выгрузить();
		Иначе
			Серии = Новый ТаблицаЗначений;
			Серии.Колонки.Добавить("Номенклатура"  , Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
			Серии.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
			Серии.Колонки.Добавить("Серия"         , Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
		КонецЕсли;
		
		Если Параметры.Свойство("ЗаполнитьФлагМаркируемаяАлкогольнаяПродукция") Тогда
			ЗаполнитьФлагЗаполнитьФлагМаркируемаяАлкогольнаяПродукция = Параметры.ЗаполнитьФлагМаркируемаяАлкогольнаяПродукция;
		Иначе
			ЗаполнитьФлагЗаполнитьФлагМаркируемаяАлкогольнаяПродукция = Ложь;
		КонецЕсли;
		
		Запрос = Новый Запрос("
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТЧСерии.Серия КАК Серия,
		|	ТЧСерии.Номенклатура КАК Номенклатура,
		|	ТЧСерии.Характеристика КАК Характеристика
		|ПОМЕСТИТЬ
		|	Серии
		|ИЗ
		|	&Серии КАК ТЧСерии
		|ГДЕ
		|	ТЧСерии.Номенклатура = &Номенклатура
		|	И ТЧСерии.Характеристика = &Характеристика;
		|
		|ВЫБРАТЬ
		|	ТЧСерии.Серия КАК Серия,
		|	ТЧСерии.Номенклатура КАК Номенклатура,
		|	ТЧСерии.Характеристика КАК Характеристика
		|ПОМЕСТИТЬ
		|	ВтСерии
		|ИЗ
		|	Серии КАК ТЧСерии
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Серия КАК Серия,
		|	&Номенклатура КАК Номенклатура,
		|	&Характеристика КАК Характеристика
		|;
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция КАК НоменклатураЕГАИС,
		|	СоответствиеНоменклатурыЕГАИС.Номенклатура КАК Номенклатура,
		|	ВЫБОР
		|		КОГДА СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция.ВидПродукции.Маркируемый
		|				ИЛИ СоответствиеНоменклатурыЕГАИС.Номенклатура.ВидАлкогольнойПродукцииЕГАИС.Маркируемый
		|			ТОГДА НЕ СоответствиеНоменклатурыЕГАИС.Номенклатура.АлкогольнаяПродукцияВоВскрытойТаре
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК МаркируемаяАлкогольнаяПродукция,
		|	ВЫБОР
		|		КОГДА СоответствиеНоменклатурыЕГАИС.Номенклатура.АлкогольнаяПродукция
		|				ИЛИ НЕ СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция ЕСТЬ NULL 
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК АлкогольнаяПродукция
		|ИЗ
		|	РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСерии КАК ВтСерии
		|		ПО ВтСерии.Номенклатура = СоответствиеНоменклатурыЕГАИС.Номенклатура
		|			И ВтСерии.Характеристика = СоответствиеНоменклатурыЕГАИС.Характеристика
		|			И ВтСерии.Серия = СоответствиеНоменклатурыЕГАИС.Серия
		|ГДЕ
		|	СоответствиеНоменклатурыЕГАИС.Номенклатура = &Номенклатура
		|	И СоответствиеНоменклатурыЕГАИС.Характеристика = &Характеристика");
		
		Запрос.УстановитьПараметр("Номенклатура",   ТекущаяСтрока.Номенклатура);
		Запрос.УстановитьПараметр("Характеристика", ТекущаяСтрока.Характеристика);
		Запрос.УстановитьПараметр("Серия", ТекущаяСтрока.Серия);
		Запрос.УстановитьПараметр("Серии", Серии);
		
		УстановитьПривилегированныйРежим(Истина);
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		КоличествоСопоставлено = Выборка.Количество();
		ТекущаяСтрока.НоменклатураДляВыбора.Очистить();
		
		Пока Выборка.Следующий() Цикл
			Если КоличествоСопоставлено = 1 Тогда
				ТекущаяСтрока[ИмяКолонки] = Выборка.НоменклатураЕГАИС;
				Если ЗаполнитьФлагЗаполнитьФлагМаркируемаяАлкогольнаяПродукция Тогда
					ТекущаяСтрока.МаркируемаяАлкогольнаяПродукция = Выборка.МаркируемаяАлкогольнаяПродукция;
				КонецЕсли;
				Если ЗаполнитьФлагАлкогольнаяПродукция Тогда
					ТекущаяСтрока.АлкогольнаяПродукция = Выборка.АлкогольнаяПродукция;
				КонецЕсли;
			КонецЕсли;
			ТекущаяСтрока.НоменклатураДляВыбора.Добавить(Выборка.НоменклатураЕГАИС);
		КонецЦикла;
		
		Если КоличествоСопоставлено = 0 Тогда
			ТекущаяСтрока.АлкогольнаяПродукция = Неопределено;
		КонецЕсли;
		Если КоличествоСопоставлено > 1 Тогда
			ТекущаяСтрока.СопоставлениеАлкогольнаяПродукция = СтрШаблон(НСтр("ru = '<Несколько позиций (%1)>'"), КоличествоСопоставлено);
		ИначеЕсли КоличествоСопоставлено = 1 Тогда
			ТекущаяСтрока.СопоставлениеАлкогольнаяПродукция = "";
		Иначе
			ТекущаяСтрока.СопоставлениеАлкогольнаяПродукция = НСтр("ru = '<Не сопоставлено>'");
		КонецЕсли;
		
		Если КоличествоСопоставлено <> 1 Тогда
			
			Если ТекущаяСтрока.НоменклатураДляВыбора.НайтиПоЗначению(ТекущаяСтрока[ИмяКолонки]) = Неопределено Тогда
				ТекущаяСтрока[ИмяКолонки] = Неопределено;
			КонецЕсли;
			Если ЗаполнитьФлагЗаполнитьФлагМаркируемаяАлкогольнаяПродукция Тогда
				ТекущаяСтрока.МаркируемаяАлкогольнаяПродукция = Ложь;
			КонецЕсли;
			Если ЗаполнитьФлагАлкогольнаяПродукция Тогда
				ТекущаяСтрока.АлкогольнаяПродукция = Ложь;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


Процедура ЗаполнитьАлкогольнуюПродукцию(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения)
	
	Если СтруктураДействий.Свойство("ЗаполнитьАлкогольнуюПродукцию") Тогда
		СобытияФормЕГАИС.ЗаполнитьАлкогольнуюПродукциюСтрокиТабличнойЧасти(ТекущаяСтрока, СтруктураДействий.ЗаполнитьАлкогольнуюПродукцию);
	КонецЕсли;
	
КонецПроцедуры

// Пересчитывает количество товара (в единицах хранения) в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - Структура - кэшированные значения.
//
Процедура ПересчитатьКоличествоЕдиницВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт 

	Перем УпаковкаНоменклатура;
	
	Если СтруктураДействий.Свойство("ПересчитатьКоличествоЕдиниц", УпаковкаНоменклатура) Тогда
		ПараметрыПересчета = НормализоватьПараметрыПересчетаЕдиниц(ТекущаяСтрока, УпаковкаНоменклатура);
		ДанныеУпаковки = ПолучитьКоэффициентУпаковки(ПараметрыПересчета.Упаковка, КэшированныеЗначения, ПараметрыПересчета.Номенклатура);
		Количество = ?(ТекущаяСтрока.Свойство("КоличествоУпаковок"),ТекущаяСтрока.КоличествоУпаковок, ТекущаяСтрока.Количество) * ДанныеУпаковки.Коэффициент;
		
		Если ДанныеУпаковки.НужноОкруглятьКоличество
			И ПараметрыПересчета.НужноОкруглять Тогда
			ТекущаяСтрока.Количество = Окр(Количество, 0 ,РежимОкругления.Окр15как20);
		Иначе
			ТекущаяСтрока.Количество = Количество;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция НормализоватьПараметрыПересчетаЕдиниц(ТекущаяСтрока, УпаковкаНоменклатура) Экспорт
	
	Номенклатура   = ТекущаяСтрока.Номенклатура;
	Упаковка       = ТекущаяСтрока.ЕдиницаИзмерения;
	НужноОкруглять = Истина;
	
	Если ТипЗнч(УпаковкаНоменклатура) = Тип("Структура") Тогда
		
		Если УпаковкаНоменклатура.Свойство("Упаковка") Тогда
			Упаковка = УпаковкаНоменклатура.Упаковка;
		КонецЕсли;
		
		Если УпаковкаНоменклатура.Свойство("Номенклатура") Тогда
			Номенклатура = УпаковкаНоменклатура.Номенклатура;
		КонецЕсли;
		
		Если УпаковкаНоменклатура.Свойство("НужноОкруглять") Тогда
			НужноОкруглять = УпаковкаНоменклатура.НужноОкруглять;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Новый Структура("Номенклатура,Упаковка,НужноОкруглять", Номенклатура, Упаковка, НужноОкруглять);
	
КонецФункции

Функция ПолучитьКоэффициентУпаковки(ТекУпаковка, КэшированныеЗначения, ТекНоменклатура = Неопределено) Экспорт
	
	Результат = Новый Структура("Коэффициент,НужноОкруглятьКоличество");
	
	Если ЗначениеЗаполнено(ТекУпаковка) Тогда
		КлючКоэффициента = КлючКэшаУпаковки(ТекНоменклатура, ТекУпаковка); 
		Кэш = КэшированныеЗначения.КоэффициентыУпаковок[КлючКоэффициента];
		Если Кэш = Неопределено Тогда
			#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
				ЗначенияРеквизитов = ДанныеОбУпаковке(ТекНоменклатура, ТекУпаковка, КэшированныеЗначения);
				Результат.Коэффициент    		   =  ЗначенияРеквизитов.Коэффициент;
				Результат.НужноОкруглятьКоличество =  ЗначенияРеквизитов.НужноОкруглятьКоличество;
			#Иначе
				ТекстИсключения = НСтр("ru = 'Попытка получения коэффициента упаковки на клиенте.'");
				ВызватьИсключение ТекстИсключения;
			#КонецЕсли
		Иначе
			Результат = Кэш;
		КонецЕсли;
	Иначе
		Результат.Коэффициент = 1;
		Результат.НужноОкруглятьКоличество = Ложь;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция КлючКэшаУпаковки(Номенклатура, Упаковка) Экспорт
	
	Если ЗначениеЗаполнено(Номенклатура) Тогда
		КлючНоменклатура = Строка(Номенклатура.УникальныйИдентификатор());
	Иначе
		КлючНоменклатура = "ПустоеЗначение";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Упаковка) Тогда
		КлючУпаковка = Строка(Упаковка.УникальныйИдентификатор());
	Иначе
		КлючУпаковка = "ПустоеЗначение";
	КонецЕсли;
	
	Возврат КлючНоменклатура + КлючУпаковка;
	
КонецФункции

Функция ДанныеОбУпаковке(Номенклатура, Упаковка, КэшированныеЗначения) Экспорт 
	
	ДанныеОбУпаковке = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Упаковка, "Коэффициент,Вес,Объем");
	ДанныеОбУпаковке.Вставить("НужноОкруглятьКоличество", Ложь);
	
	КлючКоэффициента = КлючКэшаУпаковки(Номенклатура, Упаковка);
	
	КэшированныеЗначения.КоэффициентыУпаковок.Вставить(КлючКоэффициента,	Новый Структура("Коэффициент, НужноОкруглятьКоличество",
																							ДанныеОбУпаковке.Коэффициент,
																							Ложь));
	КэшированныеЗначения.ОбъемУпаковок.Вставить(КлючКоэффициента, 			ДанныеОбУпаковке.Вес);
	КэшированныеЗначения.ВесУпаковок.Вставить(КлючКоэффициента, 			ДанныеОбУпаковке.Объем);
	
	Возврат ДанныеОбУпаковке;
	
КонецФункции

// Пересчитывает сумму товара в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - Структура - кэшированные значения.
//
Процедура ПересчитатьСуммуВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт

	ИмяКоличества = Неопределено;
	
	Если СтруктураДействий.Свойство("ПересчитатьСумму", ИмяКоличества) Тогда
		Если НЕ ЗначениеЗаполнено(ИмяКоличества) Тогда
			ИмяКоличества = "Коэффициент"
		КонецЕсли;
		Если ТекущаяСтрока <> Неопределено Тогда
			Если Не ТекущаяСтрока.Свойство(ИмяКоличества) Тогда
				Если ТекущаяСтрока.Свойство("КоличествоУпаковок") Тогда
					ИмяКоличества = "КоличествоУпаковок";
				ИначеЕсли ТекущаяСтрока.Свойство("Коэффициент") Тогда
					ИмяКоличества = "Коэффициент";
				КонецЕсли;
			КонецЕсли;
			ТекущаяСтрока.Сумма = ТекущаяСтрока.Цена * ТекущаяСтрока[ИмяКоличества];
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Пересчитывает сумму НДС товара в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - Структура - кэшированные значения.
//
Процедура ПересчитатьСуммуНДСВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт

	Если СтруктураДействий.Свойство("ПересчитатьСуммуНДС") Тогда
		Если ТекущаяСтрока <> Неопределено Тогда
			ТекущаяСтрока.СуммаНДС = УчетНДС.РассчитатьСуммуНДС(ТекущаяСтрока.Сумма, Истина, Истина,
			   УчетНДС.ПолучитьСтавкуНДС(ТекущаяСтрока.СтавкаНДС));
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Пересчитывает сумму НДС товара в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - Структура - кэшированные значения.
//
Процедура ПересчитатьСуммуСНДСВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт

	Если СтруктураДействий.Свойство("ПересчитатьСуммуСНДС") Тогда
		Если ТекущаяСтрока <> Неопределено Тогда
			ТекущаяСтрока.СуммаСНДС = ТекущаяСтрока.Сумма;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Пересчитывает цену от стоимости товара в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - Структура - кэшированные значения.
//
Процедура ПересчитатьЦенуПоСуммеВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	ИмяКоличества = Неопределено;
	
	Если СтруктураДействий.Свойство("ПересчитатьЦенуПоСумме",ИмяКоличества) Тогда
		Если Не ЗначениеЗаполнено(ИмяКоличества) Тогда
			ИмяКоличества = "КоличествоУпаковок";
		КонецЕсли;
		Если ТекущаяСтрока[ИмяКоличества] = 0 Тогда
			ТекущаяСтрока.Цена = ТекущаяСтрока.Сумма;
		Иначе
			ТекущаяСтрока.Цена = Окр(ТекущаяСтрока.Сумма / ТекущаяСтрока[ИмяКоличества], 2);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Пересчитывает количество упаковок товара в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - Структура - кэшированные значения.
//
Процедура ПересчитатьКоличествоУпаковокВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Если СтруктураДействий.Свойство("ПересчитатьКоличествоУпаковок") Тогда
		Если ТекущаяСтрока.Количество = 0 Тогда
			ТекущаяСтрока.КоличествоУпаковок = 0;
		Иначе
			Коэффициент = ПолучитьКоэффициентУпаковки(ТекущаяСтрока.Упаковка, КэшированныеЗначения, ТекущаяСтрока.Номенклатура).Коэффициент;
			Если Коэффициент <> 0 Тогда
				ТекущаяСтрока.КоличествоУпаковок = ТекущаяСтрока.Количество / Коэффициент;
			Иначе
				ТекстИсключения = НСтр("ru = 'При попытке пересчета количества в %ЕдИзмерения% превышена допустимая разрядность.'");
				ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ЕдИзмерения%", ТекущаяСтрока.Упаковка);
				
				ТекущаяСтрока.Количество = 0;
				ТекущаяСтрока.КоличествоУпаковок = 0;
				ТекущаяСтрока.Упаковка = ПредопределенноеЗначение("Справочник.ЕдиницыИзмерения.ПустаяСсылка");
				
				ВызватьИсключение ТекстИсключения;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Пересчитывает количество упаковок товара в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - Структура - кэшированные значения.
//
Процедура ПересчитатьКоличествоЗЕРНОВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Если СтруктураДействий.Свойство("ПересчитатьКоличествоЕдиницПоЗЕРНО") Тогда
		Если ТекущаяСтрока.КоличествоЗЕРНО = 0 Тогда
			Возврат;
		ИначеЕсли Не ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда
			Возврат;
		Иначе
			Массив = Новый Массив;
			Массив.Добавить(ТекущаяСтрока);
			Коэффициент = ИнтеграцияЗЕРНОУТ.КоэффициентыЕдиницИзмеренияЗЕРНО(Массив).Получить(ТекущаяСтрока.Номенклатура).Коэффициент;
			ТекущаяСтрока.Количество = ТекущаяСтрока.КоличествоЗЕРНО / Коэффициент;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Заполняет поле индекса акцизной марки в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//
Процедура ЗаполнитьИндексАкцизнойМаркиДляСтрокиТабличнойЧасти(ТекущаяСтрока, СтруктураДействий) Экспорт
	
	ПараметрыДействия = Неопределено;
	Если СтруктураДействий.Свойство("ЗаполнитьИндексАкцизнойМарки", ПараметрыДействия) Тогда
		
		Если ПараметрыДействия <> Неопределено И ПараметрыДействия.Свойство("ИмяКолонкиКоличество") Тогда
			ИмяКолонкиКоличество = ПараметрыДействия.ИмяКолонкиКоличество;
		Иначе
			ИмяКолонкиКоличество = "Количество";
		КонецЕсли;
		
		АкцизныеМаркиКлиентСервер.ЗаполнитьИндексАкцизнойМарки(ТекущаяСтрока, ИмяКолонкиКоличество);
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура, вызываемая из обработчика реквизита "Номенклатура" табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - текущая строка табличной части.
//	СтруктураДействий - Структура - допустимые действия для табличной части.
//	КэшированныеЗначения - Структура - Кэшированные значения табличной части.
//
Процедура НоменклатураПриИзмененииПереопределяемый(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	
КонецПроцедуры

// Процедура, вызываемая из обработчика реквизита "Характеристика" табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - текущая строка табличной части.
//	СтруктураДействий - Структура - допустимые действия для табличной части.
//	КэшированныеЗначения - Структура - Кэшированные значения табличной части.
//
Процедура ХарактеристикаПриИзмененииПереопределяемый(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	
КонецПроцедуры