#Область ПрограммныйИнтерфейс

Процедура ПриОпределенииТекстаЗапросаОформленныхДокументов(ТекстЗапроса, ОтчетОбъект) Экспорт
	
	Текст = Неопределено;
	Отчет = ОтчетОбъект.Метаданные();
	
	Если Отчет = Метаданные.Отчеты.АнализРасхожденийПриПоступленииАлкогольнойПродукции Тогда
		Текст = ОформленныеДокументыПоПоступлениюАлкогольнойПродукции();
	ИначеЕсли Отчет = Метаданные.Отчеты.АнализРасхожденийПриОтгрузкеАлкогольнойПродукции Тогда
		Текст = ОформленныеДокументыПоОтгрузкеАлкогольнойПродукции();
	
	ИначеЕсли Отчет = Метаданные.Отчеты.АнализРасхожденийПриМаркировкеТоваровИСМП Тогда
		Текст = ОформленныеДокументыПоМаркировкеТоваровИСМП();
	ИначеЕсли Отчет = Метаданные.Отчеты.АнализРасхожденийПриВыводеИзОборотаИСМП Тогда
		Текст = ОформленныеДокументыВыводуИзОборотаИСМП();
		
	ИначеЕсли Отчет = Метаданные.Отчеты.АнализРасхожденийПриДвиженииЗЕРНО Тогда
		Текст = ОформленныеДокументыЗЕРНО();
	КонецЕсли;
	
	Если Не(Текст = Неопределено) Тогда
		ТекстЗапроса = Текст;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ТекстыЗапросов

Функция ОформленныеДокументыВыводуИзОборотаИСМП()
	
	МассивТекстовЗапросов = Новый Массив;
	
	Реализации = "";
	Если ДобавитьОснование(МассивТекстовЗапросов, "РеализацияТоваровУслуг", "Товары") Тогда
		Реализации = СтрСоединить(МассивТекстовЗапросов, " ОБЪЕДИНИТЬ ВСЕ ");
		ДопОтбор = СтрШаблон("И (%1 ИЛИ %2)",
			"ТаблицаТовары.Ссылка.Контрагент.ИНН=""""",
			"ТаблицаТовары.Ссылка.Контрагент.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо)
			|И ТаблицаТовары.Ссылка.ККМ В(ВЫБРАТЬ Касса Из РегистрСведений.КассыНеПередающиеДанныеВИСМП)");
		Реализации = СтрЗаменить(Реализации, ".Проведен", ".Проведен "+ДопОтбор);
	КонецЕсли;
	МассивТекстовЗапросов.Очистить();
	
	Розница = "";
	ДобавитьОснование(МассивТекстовЗапросов, "ЧекККМ", "Товары");
	ДобавитьОснование(МассивТекстовЗапросов, "ОтчетОРозничныхПродажах", "Товары");
	Если МассивТекстовЗапросов.Количество() Тогда
		Розница = СтрСоединить(МассивТекстовЗапросов, " ОБЪЕДИНИТЬ ВСЕ ");
		Розница = СтрЗаменить(Розница, ".Проведен", ".Проведен И ТаблицаТовары.Ссылка.КассаККМ В(ВЫБРАТЬ Касса Из РегистрСведений.КассыНеПередающиеДанныеВИСМП)");
	КонецЕсли;
	МассивТекстовЗапросов.Очистить();
	
	Возвраты = "";
	Если ДобавитьОснование(МассивТекстовЗапросов, "ВозвратТоваровПоставщику", "Товары") Тогда
		Возвраты = СтрСоединить(МассивТекстовЗапросов, " ОБЪЕДИНИТЬ ВСЕ ");
		Возвраты = СтрЗаменить(Реализации, ".Проведен", ".Проведен И ТаблицаТовары.Ссылка.Контрагент.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо)");
	КонецЕсли;
	МассивТекстовЗапросов.Очистить();
	
	ЗнакСборки = "ВЫБОР КОГДА ТаблицаТовары.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Комплектация) ТОГДА 0 ИНАЧЕ 1 КОНЕЦ * ";
	Если ДобавитьОснование(МассивТекстовЗапросов, "КомплектацияНоменклатуры", "", ЗнакСборки) Тогда
		МассивТекстовЗапросов[0] = СтрЗаменить(МассивТекстовЗапросов[0], "Документ.КомплектацияНоменклатуры.", "Документ.КомплектацияНоменклатуры");
	КонецЕсли;
	ДобавитьОснование(МассивТекстовЗапросов, "ТребованиеНакладная", "Материалы");
	ДобавитьОснование(МассивТекстовЗапросов, "СписаниеТоваров", "Товары");
	
	Если ЗначениеЗаполнено(Реализации) Тогда
		МассивТекстовЗапросов.Добавить(Реализации);
	КонецЕсли;
	Если ЗначениеЗаполнено(Возвраты) Тогда
		МассивТекстовЗапросов.Добавить(Возвраты);
	КонецЕсли;
	Если ЗначениеЗаполнено(Розница) Тогда
		МассивТекстовЗапросов.Добавить(Розница);
	КонецЕсли;
	
	Текст = СтрСоединить(МассивТекстовЗапросов, "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|");
	
	ДоработатьЗапросПродукцииИСМП(Текст);
	
	Возврат Текст;
	
КонецФункции

Функция ОформленныеДокументыПоМаркировкеТоваровИСМП()
	
	МассивТекстовЗапросов = Новый Массив;
	
	ЗнакСборки = "ВЫБОР КОГДА ТаблицаТовары.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Комплектация) ТОГДА 1 ИНАЧЕ 0 КОНЕЦ * ";
	Если ДобавитьОснование(МассивТекстовЗапросов, "КомплектацияНоменклатуры", "", ЗнакСборки) Тогда
		МассивТекстовЗапросов[0] = СтрЗаменить(МассивТекстовЗапросов[0], "Документ.КомплектацияНоменклатуры.", "Документ.КомплектацияНоменклатуры");
	КонецЕсли;
	
	Импорт = СтрЗаменить(РасчетСтатусовОформленияИСУТ.Импорт(), "ТаблицаДокумента", "ТаблицаТовары.Ссылка");
	Знак = СтрШаблон("(%1 ИЛИ %2)", Импорт, "ТаблицаТовары.Ссылка.Контрагент.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо)");
	ЗнакПоступление = СтрШаблон("ВЫБОР КОГДА %1 ТОГДА 1 ИНАЧЕ 0 КОНЕЦ * ", Знак);
	ДобавитьОснование(МассивТекстовЗапросов,"ПоступлениеТоваровУслуг", "Товары", ЗнакПоступление);
	
	ДобавитьОснование(МассивТекстовЗапросов, "ОприходованиеТоваров", "Товары");
	
	Текст = СтрСоединить(МассивТекстовЗапросов, "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|");
	
	ДоработатьЗапросПродукцииИСМП(Текст);
	
	Возврат Текст;
	
КонецФункции

Функция ОформленныеДокументыПоОтгрузкеАлкогольнойПродукции()
	
	МассивТекстовЗапросов = Новый Массив;
	ДобавитьОснование(МассивТекстовЗапросов,"ВозвратТоваровПоставщику", "Товары");
	ДобавитьОснование(МассивТекстовЗапросов,"ПеремещениеТоваров", "Товары");
	ДобавитьОснование(МассивТекстовЗапросов,"РеализацияТоваровУслуг", "Товары");
	
	Текст = СтрСоединить(МассивТекстовЗапросов, "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|");
	ДоработатьЗапросПродукцииЕГАИС(Текст);
	
	Возврат Текст;
	
КонецФункции

Функция ОформленныеДокументыПоПоступлениюАлкогольнойПродукции()
	
	МассивТекстовЗапросов = Новый Массив;
	
	Если ДобавитьОснование(МассивТекстовЗапросов,"ВозвратТоваровОтПокупателя", "Товары") Тогда
		МассивТекстовЗапросов[0] = СтрЗаменить(МассивТекстовЗапросов[0],
			".Проведен",
			".Проведен И ТаблицаТовары.Ссылка.Контрагент.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)"); 
	КонецЕсли;
	
	ДобавитьОснование(МассивТекстовЗапросов,"ПоступлениеТоваровУслуг", "Товары");
	ДобавитьОснование(МассивТекстовЗапросов,"ПеремещениеТоваров", "Товары");
	
	
	Текст = СтрСоединить(МассивТекстовЗапросов, "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|");
	ДоработатьЗапросПродукцииЕГАИС(Текст);
	
	Возврат Текст;
	
КонецФункции

Функция ОформленныеДокументыЗЕРНО()
	
	МассивТекстовЗапросов = Новый Массив;
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов,"ВозвратТоваровПоставщику", "Товары",        "-");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов,"РеализацияТоваровУслуг",   "Товары",        "-");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов,"ПоступлениеТоваровУслуг",  "Товары");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов,"ВозвратТоваровОтПокупателя", "Товары");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов,"ОприходованиеТоваров",     "Товары");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов,"ЗаказПоставщику",          "Товары",           , СтрЗаменить(РасчетСтатусовОформленияИСУТ.Импорт(), "И ТаблицаДокумента", "ТаблицаТовары.Ссылка"), "ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов,"ТребованиеНакладная",      "Материалы",     "-");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов,"СписаниеТоваров",          "Товары",        "-");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов,"ПеремещениеТоваров",       "Товары");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов,"ПеремещениеТоваров",       "Товары",        "-");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов,"КомплектацияНоменклатуры", "Комплектующие",    , "ТаблицаТовары.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Разукомплектация)");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов,"КомплектацияНоменклатуры", "Комплектующие", "-", "ТаблицаТовары.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Комплектация)");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов,"КомплектацияНоменклатуры", "",                 , "ТаблицаТовары.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Комплектация)");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов,"КомплектацияНоменклатуры", "",              "-", "ТаблицаТовары.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Разукомплектация)");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов,"ПеремещениеТоваров",       "Товары",        "-");
	Текст = СтрСоединить(МассивТекстовЗапросов, "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|");
	
	ДоработатьЗапросПродукцииЗЕРНО(Текст);
	
	Возврат Текст;
КонецФункции

#КонецОбласти

#Область Шаблоны

Функция ДобавитьОснование(МассивТекстовЗапросов, ИмяДокумента, ИмяТабличнойЧасти, Знак = "", Постфикс = "")
	
	Если Не ПравоДоступа("Просмотр", Метаданные.Документы[ИмяДокумента]) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Шаблон = 
	"ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка              КАК Ссылка,
	|	ТаблицаТовары.Номенклатура%4      КАК Номенклатура,
	|	ТаблицаТовары.ХарактеристикаНоменклатуры%4    КАК Характеристика,
	|	ТаблицаТовары.СерияНоменклатуры%4             КАК Серия,
	|	%3СУММА(ТаблицаТовары.Количество) КАК Количество
	|ИЗ
	|	Документ.%1.%2 КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка.Проведен
	|	И ТаблицаТовары.Ссылка В
	|		(ВЫБРАТЬ
	|			ПрикладныеДокументы.Ссылка
	|		ИЗ
	|			ПрикладныеДокументы КАК ПрикладныеДокументы)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Ссылка,
	|	ТаблицаТовары.Номенклатура%4,
	|	ТаблицаТовары.ХарактеристикаНоменклатуры%4,
	|	ТаблицаТовары.СерияНоменклатуры%4";
	
	Шаблон = СтрШаблон(Шаблон,
		ИмяДокумента,
		ИмяТабличнойЧасти,
		Знак,
		Постфикс);
	
	МассивТекстовЗапросов.Добавить(Шаблон);
	Возврат Истина;
	
КонецФункции

Процедура ДоработатьЗапросПродукцииЕГАИС(Текст)
	
	Позиция = СтрНайти(Текст, "ВЫБРАТЬ");
	Текст = Лев(Текст, Позиция-1) + "ВЫБРАТЬ РАЗРЕШЕННЫЕ" + Сред(Текст, Позиция + СтрДлина("ВЫБРАТЬ"));
	Позиция = СтрНайти(Текст, "
	|ИЗ");
	Текст = Лев(Текст, Позиция -1)+ "
	|ПОМЕСТИТЬ ТоварыНакладнойПредварительно
	|"+ Сред(Текст, Позиция);
	
	Текст = Текст + 
	";
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Серия КАК Серия,
	|	Товары.Количество КАК Количество
	|ПОМЕСТИТЬ ТоварыНакладной
	|ИЗ
	|	ТоварыНакладнойПредварительно КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|		И СправочникНоменклатура.АлкогольнаяПродукция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ТоварыНакладной.Номенклатура КАК Номенклатура,
	|	ТоварыНакладной.Характеристика КАК Характеристика,
	|	ТоварыНакладной.Номенклатура.ОбъемДАЛ КАК ОбъемДАЛ
	|ПОМЕСТИТЬ НоменклатураПереопределяемый
	|ИЗ
	|	ТоварыНакладной КАК ТоварыНакладной
	|;
	|
	|";
	
КонецПроцедуры

Процедура ДоработатьЗапросПродукцииИСМП(Текст)
	
	Позиция = СтрНайти(Текст, "ВЫБРАТЬ");
	Текст = Лев(Текст, Позиция-1) + "ВЫБРАТЬ РАЗРЕШЕННЫЕ" + Сред(Текст, Позиция + СтрДлина("ВЫБРАТЬ"));
	
	Позиция = СтрНайти(Текст, "
	|ИЗ");
	Текст = Лев(Текст, Позиция -1)+ "
	|ПОМЕСТИТЬ ТоварыНакладнойПредварительно
	|"+ Сред(Текст, Позиция)+"
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Текст = Текст + 
	
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Серия КАК Серия,
	|	Товары.Количество КАК Количество
	|ПОМЕСТИТЬ ТоварыНакладной
	|ИЗ
	|	ТоварыНакладнойПредварительно КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|		И Не СправочникНоменклатура.ВидПродукцииИС В(
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПустаяСсылка),
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Алкогольная))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ТоварыНакладной.Номенклатура КАК Номенклатура,
	|	&ОпределениеВидаПродукции КАК ВидПродукции
	|ПОМЕСТИТЬ НоменклатураПереопределяемый
	|ИЗ
	|	ТоварыНакладной КАК ТоварыНакладной
	|;
	|
	|";
	
	ИнтеграцияИСУТ.ОпределитьВидПродукцииТекстаЗапроса(Текст,"ТоварыНакладной.Номенклатура");
	
КонецПроцедуры

Процедура ДоработатьЗапросПродукцииЗЕРНО(Текст)
	
	Позиция = СтрНайти(Текст, "ВЫБРАТЬ");
	Текст = Лев(Текст, Позиция-1) + "ВЫБРАТЬ РАЗРЕШЕННЫЕ" + Сред(Текст, Позиция + СтрДлина("ВЫБРАТЬ"));
	Позиция = СтрНайти(Текст, "
	|ИЗ");
	Текст = Лев(Текст, Позиция -1)+ "
	|ПОМЕСТИТЬ ТоварыНакладнойПредварительно
	|"+ Сред(Текст, Позиция);
	
	Текст = Текст + 
	";
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Серия КАК Серия,
	|	Товары.КоличествоПриход КАК КоличествоПриход,
	|	Товары.КоличествоРасход КАК КоличествоРасход
	|ПОМЕСТИТЬ ТоварыНакладной
	|ИЗ
	|	ТоварыНакладнойПредварительно КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|		И СправочникНоменклатура.ВидПродукцииИС В(
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Зерно),
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПродуктыПереработкиЗерна))
	|;
	|
	|";
КонецПроцедуры

Функция ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, ИмяДокумента, ИмяТабличнойЧасти, Знак = "", УсловиеОтбора = "ИСТИНА", ИмяПоляСерия = "ТаблицаТовары.СерияНоменклатуры")
	
	Если Не ПравоДоступа("Просмотр", Метаданные.Документы[ИмяДокумента]) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Шаблон = 
	"ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка                     КАК Ссылка,
	|	ТаблицаТовары.Номенклатура               КАК Номенклатура,
	|	ТаблицаТовары.ХарактеристикаНоменклатуры КАК Характеристика,
	|	%2          КАК Серия,
	|	ВЫБОР 
	|		КОГДА %3 = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			СУММА(ТаблицаТовары.Количество * ТаблицаТовары.Коэффициент)
	|		ИНАЧЕ
	|			0 
	|	КОНЕЦ КАК КоличествоПриход,
	|	ВЫБОР 
	|		КОГДА %3 = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА
	|			СУММА(ТаблицаТовары.Количество * ТаблицаТовары.Коэффициент)
	|		ИНАЧЕ
	|			0 
	|	КОНЕЦ КАК КоличествоРасход
	|ИЗ
	|	Документ.%1 КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка.Проведен
	|	И ТаблицаТовары.Ссылка В
	|		(ВЫБРАТЬ
	|			ПрикладныеДокументы.Ссылка
	|		ИЗ
	|			ПрикладныеДокументы КАК ПрикладныеДокументы)
	|	И %4
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Ссылка,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.ХарактеристикаНоменклатуры,
	|	%2";
	
	Если ЗначениеЗаполнено(ИмяТабличнойЧасти) Тогда
		Источник = СтрШаблон("%1.%2",ИмяДокумента, ИмяТабличнойЧасти);
	Иначе 
		Источник = ИмяДокумента;
	КонецЕсли;
	Шаблон = СтрШаблон(Шаблон,
		Источник,
		ИмяПоляСерия,
		?(Знак = "-", "ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)", "ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)"),
		УсловиеОтбора);
	
	МассивТекстовЗапросов.Добавить(Шаблон);
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#КонецОбласти
