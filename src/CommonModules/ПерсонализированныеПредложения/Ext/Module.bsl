// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С БАННЕРАМИ

#Область КоличествоРеализаций

Функция НовыйБаннерКоличествоРеализаций()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервис1СЭДО();
	Баннер.Идентификатор       = ИдентификаторБаннераКоличествоРеализаций();
	Баннер.ЦветФонаБаннер      = "ЦветФонаБаннерЭДО";
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиЛоготип1СЭДО();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераКоличествоРеализаций();
	Баннер.ДоступенПоПравам    = ПравоДоступа(
		"Просмотр",
		Метаданные.Документы.РеализацияТоваровУслуг);
	Баннер.ДанныеБаннера.Вставить("КоличествоРеализаций", 0);
	Баннер.ОбстоятельстваЗакрытия.Периодичность = Перечисления.Периодичность.Квартал;
	Баннер.ТекстЗапроса        = ТекстЗапросаКоличествоРеализаций();
	
	Возврат Баннер;
	
КонецФункции

Функция ТекстЗапросаКоличествоРеализаций()
	
	ТекстЗапроса =
	 "ВЫБРАТЬ
	 |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ РеализацияТоваровУслуг.Ссылка) КАК КоличествоРеализаций
	 |ИЗ
	 |	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	 |ГДЕ
	 |	РеализацияТоваровУслуг.Проведен
	 |	И РеализацияТоваровУслуг.Организация = &Организация
	 |	И РеализацияТоваровУслуг.Дата МЕЖДУ &НачалоПериода И &КонецПериода";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ВыполняютсяУсловияБаннераКоличествоРеализаций(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ДанныеБаннера[0].КоличествоРеализаций > КоличествоРеализацийДляРекламыЭДО() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераКоличествоРеализаций(Баннер, ДанныеБаннера, ТекущаяДата)
	
	Баннер.ТекстБаннера = ТекстБаннераКоличествоРеализаций(Баннер.НавигационнаяСсылка, ДанныеБаннера[0]);
	ЗаполнитьЗначенияСвойств(Баннер.ДанныеБаннера, ДанныеБаннера[0]);
	Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия = ТекущаяДата;
	
КонецПроцедуры

Функция ТекстБаннераКоличествоРеализаций(НавигационнаяСсылка, ДанныеБаннера)
	
	ТекстЗаголовка = НовыйЗаголовок(СтрШаблон(НСтр("ru='Вы оформили %1 реализаций в этом году'"),
		СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(ДанныеБаннера.КоличествоРеализаций, НСтр("ru='комплект, комплекта, комплектов'"))));
	
	ТекстПодзаголовка = "";
		
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);

КонецФункции

Функция ИдентификаторБаннераКоличествоРеализаций()

	Возврат "КоличествоРеализаций";

КонецФункции

#КонецОбласти

#Область КоличествоПоступлений

Функция НовыйБаннерКоличествоПоступлений()
	
	Баннер                     = НовыйБаннерДляСписка();
	Баннер.ИмяСервиса          = ИмяСервис1СЭДО();
	Баннер.Идентификатор       = ИдентификаторБаннераКоличествоПоступлений();
	Баннер.ЦветФонаБаннер      = "ЦветФонаБаннерЭДО";
	Баннер.ИмяКартинкаЛоготипа = ИмяКартинкиЛоготип1СЭДО();
	Баннер.НавигационнаяСсылка = ИдентификаторБаннераКоличествоПоступлений();
	Баннер.ДоступенПоПравам    = ПравоДоступа(
		"Просмотр",
		Метаданные.Документы.ПоступлениеТоваровУслуг);
	Баннер.ДанныеБаннера.Вставить("КоличествоПоступлений", 0);
	Баннер.ОбстоятельстваЗакрытия.Периодичность = Перечисления.Периодичность.Квартал;
	Баннер.ТекстЗапроса        = ТекстЗапросаКоличествоПоступлений();
	
	Возврат Баннер;
	
КонецФункции

Функция ТекстЗапросаКоличествоПоступлений()
		
	ТекстЗапроса =
	 "ВЫБРАТЬ
	 |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПоступлениеТоваровУслуг.Ссылка) КАК КоличествоПоступлений
	 |ИЗ
	 |	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	 |ГДЕ
	 |	ПоступлениеТоваровУслуг.Проведен
	 |	И ПоступлениеТоваровУслуг.Организация = &Организация
	 |	И ПоступлениеТоваровУслуг.Дата МЕЖДУ &НачалоПериода И &КонецПериода";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ВыполняютсяУсловияБаннераКоличествоПоступлений(ДанныеБаннера, Баннер)
	
	Если ДанныеБаннера.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ДанныеБаннера[0].КоличествоПоступлений > КоличествоПоступленийДляРекламыЭДО() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннераКоличествоПоступлений(Баннер, ДанныеБаннера, ТекущаяДата)
	
	Баннер.ТекстБаннера = ТекстБаннераКоличествоПоступлений(Баннер.НавигационнаяСсылка, ДанныеБаннера[0]);
	ЗаполнитьЗначенияСвойств(Баннер.ДанныеБаннера, ДанныеБаннера[0]);
	Баннер.ОбстоятельстваЗакрытия.ДатаЗакрытия = ТекущаяДата;
	
КонецПроцедуры

Функция ТекстБаннераКоличествоПоступлений(НавигационнаяСсылка, ДанныеБаннера)
	
	ТекстЗаголовка = НовыйЗаголовок(СтрШаблон(НСтр("ru='Вы ввели %1 поступлений в этом году'"),
		СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(ДанныеБаннера.КоличествоПоступлений, НСтр("ru='комплект, комплекта, комплектов'"))));
	
	ТекстПодзаголовка = "";
		
	Возврат НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);

КонецФункции

Функция ИдентификаторБаннераКоличествоПоступлений()

	Возврат "КоличествоПоступлений";

КонецФункции

#КонецОбласти


Функция ИмяСервис1СЭДО()

	Возврат "Сервис1СЭДО";

КонецФункции

Функция ИмяКартинкиЛоготип1СЭДО()
	
	Возврат "ЛоготипЭДО";
	
КонецФункции

// Возвращает имя размещения для формы списка Реализации товаров и услуг.
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияРеализация() Экспорт

	Возврат "Реализация"

КонецФункции

// Возвращает имя размещения для формы списка Поступление товаров и услуг.
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияПоступление() Экспорт

	Возврат "Поступление"

КонецФункции

// Возвращает имя размещения для общих форм ИнформационнаяПанель и СписокЗадач.
// Возвращаемое значение:
//		Строка - Имя размещения
//
Функция ИмяРазмещенияОбщее() Экспорт

	Возврат "Общее"

КонецФункции

// Ключи хранилища общих настроек

Функция ИмяНастройкиПерсонализированныеПредложенияСервисов() Экспорт
	
	Возврат "ПерсонализированныеПредложенияСервисов";
	
КонецФункции

Функция ИмяКлючНастройкиЗакрытыеПользователемБаннеры() Экспорт

	Возврат "ЗакрытыеПользователемБаннеры";

КонецФункции

Функция КоличествоРеализацийДляРекламыЭДО()
	
	Возврат 50;
	
КонецФункции

Функция КоличествоПоступленийДляРекламыЭДО()
	
	Возврат 50;
	
КонецФункции


#Область Конструкторы

Функция НовыйБаннерДляСписка()
	
	Баннер = Новый Структура;
	Баннер.Вставить("ИмяСервиса",              "");
	Баннер.Вставить("Идентификатор",           "");
	Баннер.Вставить("НавигационнаяСсылка",     "");
	Баннер.Вставить("ТекстБаннера",            Новый ФорматированнаяСтрока(""));
	Баннер.Вставить("ИмяКартинкаЛоготипа",     "");
	Баннер.Вставить("ЦветФонаБаннер",         "ЦветФонаБаннер");
	Баннер.Вставить("ЗависитОтОрганизации",   Истина);
	Баннер.Вставить("Исключительный",         Ложь);
	Баннер.Вставить("ДоступенПоПравам",       Ложь);
	Баннер.Вставить("ДанныеБаннера",          Новый Структура);
	Баннер.Вставить("ТекстЗапроса",           "");
	Баннер.Вставить("ОбстоятельстваЗакрытия", Новый Структура);
	Баннер.ОбстоятельстваЗакрытия.Вставить("ДатаЗакрытия", '00010101');
	Баннер.ОбстоятельстваЗакрытия.Вставить("Периодичность", Перечисления.Периодичность.Квартал);
	
	Возврат Баннер;
	
КонецФункции

Функция НовыйЗаголовок(ТекстЗаголовка, НавигационнаяСсылка = "", Шрифт = Неопределено)
	
	ШрифтЗаголовка = ?(Шрифт = Неопределено, ШрифтыСтиля.ШрифтЗаголовкаБаннера, Шрифт);
	
	Если ЗначениеЗаполнено(НавигационнаяСсылка) Тогда
		Возврат Новый ФорматированнаяСтрока(ТекстЗаголовка, ШрифтЗаголовка, , , НавигационнаяСсылка);
	Иначе
		Возврат Новый ФорматированнаяСтрока(ТекстЗаголовка, ШрифтЗаголовка);
	КонецЕсли;
	
	Возврат Новый ФорматированнаяСтрока(ТекстЗаголовка, ШрифтыСтиля.ШрифтЗаголовкаБаннера);
	
КонецФункции

Функция НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка)
	
	Возврат Новый ФорматированнаяСтрока(
		ТекстЗаголовка,
		Символы.ПС,
		Новый ФорматированнаяСтрока(" ", Новый Шрифт(,5)), // Отступ между заголовком и подзаголовком.
		Символы.ПС,
		ТекстПодзаголовка);
	
КонецФункции
	
Функция НовыйЗакрытыеПользователемБаннеры() Экспорт
	
	ЗакрытыеПользователемБаннеры = Новый Структура;
	ЗакрытыеПользователемБаннеры.Вставить("БаннерыПоОрганизации",  Новый Соответствие);
	
	Возврат ЗакрытыеПользователемБаннеры;
	
КонецФункции

Функция НовыйИнформацияОЗакрытии()
	
	ИнформацияОЗакрытии = Новый Структура;
	ИнформацияОЗакрытии.Вставить("Идентификатор");
	ИнформацияОЗакрытии.Вставить("ОбстоятельстваЗакрытия");
	
	Возврат ИнформацияОЗакрытии;
	
КонецФункции

#КонецОбласти
	
#Область РаботаСоСпискомБаннеров

Функция ПолучитьОбстоятельстваЗакрытияБаннера(Организация, ИдентификаторБаннера)
	
	ЗакрытыеПользователемБаннеры = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		ИмяНастройкиПерсонализированныеПредложенияСервисов(),
		ИмяКлючНастройкиЗакрытыеПользователемБаннеры(),
		НовыйЗакрытыеПользователемБаннеры());
		
	ОбстоятельстваЗакрытия = Неопределено;
		
	БаннерыПоОрганизации = ЗакрытыеПользователемБаннеры.БаннерыПоОрганизации[Организация];
	
	Если БаннерыПоОрганизации <> Неопределено Тогда
		
		БаннерыПоОрганизации.Свойство(ИдентификаторБаннера, ОбстоятельстваЗакрытия);
		
	КонецЕсли;
	
	Возврат ОбстоятельстваЗакрытия;
	
КонецФункции

Функция ПолучитьБаннерПользователя(Организация, Размещение, ТекущаяДата)
	
	ТекущийБаннер = Неопределено;
	
	Если Размещение = ИмяРазмещенияРеализация() Тогда
		
		ТекущийБаннер = НовыйБаннерКоличествоРеализаций();
		
	ИначеЕсли Размещение = ИмяРазмещенияПоступление() Тогда
		
		ТекущийБаннер = НовыйБаннерКоличествоПоступлений();
		
	КонецЕсли;
	
	ОбстоятельстваЗакрытия = ПолучитьОбстоятельстваЗакрытияБаннера(Организация, ТекущийБаннер.Идентификатор);
	
	Если ОбстоятельстваЗакрытия <> Неопределено Тогда
		
		Если НачалоКвартала(ОбстоятельстваЗакрытия.ДатаЗакрытия) = НачалоКвартала(ТекущаяДата) Тогда
			ТекущийБаннер = Неопределено;
		КонецЕсли;
		
	КонецЕсли;
		
	Возврат ТекущийБаннер;
	
КонецФункции

#КонецОбласти

#Область ПолучениеИЗакрытиеБаннеров

// Получает баннер, в случае если есть что показать. Если баннер не найден, в хранилище помещается Неопределено.
// Параметры:
//	СтруктураПараметров - Струкутура - Структура параметров:
//		Организация - СправочникСсылка.Организации - Организация, по которой ищем баннер.
//		Размещение - Строка - Идентификатор формы по которой нужно показать баннер. см. функции ИмяРазмещения...().
//	АдресХранилища - Строка -Адрес хранилища, по которому будет помещен баннер.
//
Функция ПолучитьБаннер(СтруктураПараметров) Экспорт
	
	Организация = СтруктураПараметров.Организация;
	Размещение = СтруктураПараметров.Размещение;
	
	Если Организация = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ЭлектронныеДокументыПереопределяемый.ОрганизацияПодключена(Организация) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТекущаяДата = НачалоДня(ТекущаяДатаСеанса());
	ТекущийБаннер = ПолучитьБаннерПользователя(Организация, Размещение, ТекущаяДата);
	
	Если ТекущийБаннер <> Неопределено Тогда
		// Получим данные для баннеров.
		ДанныеБаннера = ДанныеБаннера(ТекущийБаннер, Организация, ТекущаяДата);
		
		Если ВыполняютсяУсловияБаннера(ТекущийБаннер, ДанныеБаннера) Тогда
			ЗаполнитьДанныеБаннера(ТекущийБаннер, ДанныеБаннера);
		Иначе
			ТекущийБаннер = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТекущийБаннер;
	
КонецФункции

#КонецОбласти	

#Область РаботаСБаннерами

Функция ДанныеБаннера(Баннер, Организация, ТекущаяДата)
	
	Запрос = Новый Запрос;
	Запрос.Текст = Баннер.ТекстЗапроса;
	
	Запрос.УстановитьПараметр("Организация",          Организация);
	Запрос.УстановитьПараметр("НачалоПериода",        НачалоГода(ТекущаяДата));
	Запрос.УстановитьПараметр("КонецПериода",         КонецГода(ТекущаяДата));
	
	ДанныеБаннера = Новый Структура;
	ДанныеБаннера.Вставить("ТекущаяДата", ТекущаяДата);
	
	УстановитьПривилегированныйРежим(Истина);
	Если НЕ ПустаяСтрока(Запрос.Текст) Тогда
		Результат = Запрос.Выполнить();
		ДанныеБаннера.Вставить(Баннер.Идентификатор, Результат.Выгрузить());
	КонецЕсли;
		
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ДанныеБаннера;
	
КонецФункции

Функция ВыполняютсяУсловияБаннера(Баннер, ДанныеБаннеров)
	
	Идентификатор = Баннер.Идентификатор;
	Если НЕ ДанныеБаннеров.Свойство(Идентификатор) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ДанныеБаннера = ДанныеБаннеров[Идентификатор];
	Если Идентификатор = ИдентификаторБаннераКоличествоПоступлений() Тогда
		Возврат ВыполняютсяУсловияБаннераКоличествоПоступлений(ДанныеБаннера, Баннер);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераКоличествоРеализаций() Тогда
		Возврат ВыполняютсяУсловияБаннераКоличествоРеализаций(ДанныеБаннера, Баннер);
				
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьДанныеБаннера(Баннер, ДанныеБаннеров)
	
	Идентификатор = Баннер.Идентификатор;
	ДанныеБаннера = ДанныеБаннеров[Идентификатор];
	ТекущаяДата   = ДанныеБаннеров.ТекущаяДата;
	
	Если Идентификатор = ИдентификаторБаннераКоличествоПоступлений() Тогда
		ЗаполнитьДанныеБаннераКоличествоПоступлений(Баннер, ДанныеБаннера, ТекущаяДата);
		
	ИначеЕсли Идентификатор = ИдентификаторБаннераКоличествоРеализаций() Тогда
		ЗаполнитьДанныеБаннераКоличествоРеализаций(Баннер, ДанныеБаннера, ТекущаяДата);
				
	КонецЕсли;
	
КонецПроцедуры

Процедура СохранитьОбстоятельстваЗакрытияБаннера(Баннер, Организация) Экспорт
		
	ЗакрытыеБаннеры = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		ИмяНастройкиПерсонализированныеПредложенияСервисов(),
		ИмяКлючНастройкиЗакрытыеПользователемБаннеры(),
		НовыйЗакрытыеПользователемБаннеры());
		
	ЗакрытыеБаннерыПоОрганизации = ЗакрытыеБаннеры.БаннерыПоОрганизации[Организация];
	Если ЗакрытыеБаннерыПоОрганизации = Неопределено Тогда
		ЗакрытыеБаннерыПоОрганизации = Новый Структура;
	КонецЕсли;
	ЗакрытыеБаннерыПоОрганизации.Вставить(Баннер.Идентификатор, Баннер.ОбстоятельстваЗакрытия);
	ЗакрытыеБаннеры.БаннерыПоОрганизации.Вставить(Организация, ЗакрытыеБаннерыПоОрганизации);
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		ИмяНастройкиПерсонализированныеПредложенияСервисов(),
		ИмяКлючНастройкиЗакрытыеПользователемБаннеры(),
		ЗакрытыеБаннеры);
		
КонецПроцедуры

#КонецОбласти
