#Область ПрограммныйИнтерфейс

// Получить параметры фискализации чека.
//
// Параметры:
//  ДанныеДокумента - Структура - РозничныеПродажиКлиентСервер.СтруктураДанныхДокументаДляПараметровФискализацииЧека().
//  СуммаПредоплатыКорректировка - Число - Сумма предоплаты.
//  ЭтоВозврат - Булево - Признак возврата.
// 
// Возвращаемое значение:
//  Структура - см. функцию МенеджерОборудованияКлиентСервер.ПараметрыОперацииФискализацииЧека().
//
Функция ПараметрыФискализацииЧека(ДанныеДокумента, СуммаПредоплатыКорректировка = Неопределено, ЭтоВозврат = Ложь, ФормироватьСтрокиОплаты = Истина, ВерсияФФД = "1.05") Экспорт
	
	Шапка         = ДанныеДокумента.Шапка;
	Товары        = ДанныеДокумента.Товары;
	
	СуммыПоБезЗаказов = Новый Структура;
	СуммыПоБезЗаказов.Вставить("СуммаПоЗаказам", СуммаПоЗаказам(Шапка, Товары));
	СуммыПоБезЗаказов.Вставить("СуммаБезЗаказов", СуммаБезЗаказа(Шапка, Товары));
	
	СуммаПредоплаты = СуммаПредоплаты(ДанныеДокумента, СуммыПоБезЗаказов.СуммаБезЗаказов, СуммаПредоплатыКорректировка);
	
	АктуальнаяДата = ТекущаяДатаСеанса();
	
	ПараметрыФискализацииЧека = МенеджерОборудованияКлиентСервер.ПараметрыОперацииФискализацииЧека();
	
	// Параметры фискализации чека - Шапка чека
	ПараметрыФискализацииЧекаДокументОснование(ПараметрыФискализацииЧека, Шапка);
	ПараметрыФискализацииЧекаТипРасчета(ПараметрыФискализацииЧека, ЭтоВозврат);
	ПараметрыФискализацииЧекаОрганизацияСведения(ПараметрыФискализацииЧека, Шапка);
	ПараметрыФискализацииЧекаТорговыйОбъект(ПараметрыФискализацииЧека, Шапка);
	ПараметрыФискализацииЧекаПолучательСведения(ПараметрыФискализацииЧека, Шапка);
	
	ПродажаОблагаетсяЕНВД = ОбщегоНазначения.УчетнаяПолитикаНалоговыйУчет("ОрганизацияЯвляетсяПлательщикомЕНВД", Шапка.Организация, АктуальнаяДата);
	ПродажаНаПатенте = ОбщегоНазначения.УчетнаяПолитикаНалоговыйУчет("ОрганизацияЯвляетсяПлательщикомПСН", Шапка.Организация, АктуальнаяДата);
	
	Если ПродажаОблагаетсяЕНВД Тогда
		ПараметрыФискализацииЧека.СистемаНалогообложения = ?(Шапка.ДокументСсылка.Дата > '20201231235959', Перечисления.ТипыСистемНалогообложенияККТ.Патент, Перечисления.ТипыСистемНалогообложенияККТ.ЕНВД);
	Иначе
		ПараметрыФискализацииЧека.СистемаНалогообложения = РозничныеПродажи.СистемаНалогообложенияФискальнойОперации(Шапка.Организация);
	КонецЕсли;
	
	// Параметры фискализации чека - Позиции чека
	ПараметрыФискализацииЧека.Кассир  			   = Шапка.Кассир;

	ПараметрыФискализацииЧека.МестоРасчетов = ПараметрыФискализацииЧека.ТорговыйОбъект;
	
	Для Каждого СтрокаТЧ Из Товары Цикл
		
		СтрокаПозицииЧека = МенеджерОборудованияКлиентСервер.ПараметрыФискальнойСтрокиЧека();
		СтрокаПозицииЧека.Вставить("РезультатРаспределенияВрем");
			
		ПараметрыФискализацииЧекаПозицииЧекаЗаполнить(СтрокаПозицииЧека, СтрокаТЧ, СуммыПоБезЗаказов, СуммаПредоплаты, ВерсияФФД, Шапка.Валюта);
			
		ПараметрыФискализацииЧека.ПозицииЧека.Добавить(СтрокаПозицииЧека);
						
	КонецЦикла;
	
	Если ФормироватьСтрокиОплаты Тогда
		// Параметры фискализации чека - Таблица оплат
		ПараметрыФискализацииЧекаТаблицаОплат(ПараметрыФискализацииЧека, СуммыПоБезЗаказов, СуммаПредоплаты);
	КонецЕсли;
	
	Возврат ПараметрыФискализацииЧека;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПараметрыФискализацииЧека

Функция СуммаПредоплаты(Знач ДанныеДокумента, Знач СуммаБезЗаказов, Знач СуммаПредоплатыКорректировка)
	
	Шапка  = ДанныеДокумента.Шапка;
	Товары = ДанныеДокумента.Товары;
	
	СуммаПредоплаты = 0;
	
	Если СуммаПредоплатыКорректировка <> Неопределено Тогда
		
		СуммаПредоплаты = СуммаПредоплатыКорректировка;
		
	Иначе
		
		ВалютаРегл = Константы.ВалютаРегламентированногоУчета.Получить();
		
		Для Каждого СтрокаТЧ Из Товары Цикл
			
			Если Шапка.ПоЗаказам И СтрокаТЧ.СверхЗаказа Тогда
				
				Если ВалютаРегл <> Шапка.Валюта Тогда
					ПроцентПредоплаты = (Шапка.СуммаПредоплаты / Шапка.СуммаДокумента);
					СуммаПредоплаты = ПроцентПредоплаты * СтрокаТЧ.СуммаСНДС + СуммаПредоплаты;
				Иначе
					// Сумма предоплаты берется из шапки документа
				КонецЕсли;
				
			ИначеЕсли Шапка.ПоЗаказам Тогда
				
				ПроцентПредоплаты = 0;
				
				СуммаПредоплаты = ПроцентПредоплаты * СтрокаТЧ.СуммаСНДС + СуммаПредоплаты;
				
			Иначе
				
				Если ВалютаРегл <> Шапка.Валюта Тогда
					ПроцентПредоплаты = Мин((Шапка.СуммаПредоплаты / СуммаБезЗаказов), 1);
					СуммаПредоплаты = ПроцентПредоплаты * СтрокаТЧ.СуммаСНДС + СуммаПредоплаты;
				Иначе
					// Сумма предоплаты берется из шапки документа
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если Не Шапка.ПоЗаказам
			И ВалютаРегл = Шапка.Валюта Тогда
			СуммаПредоплаты = СуммаПредоплаты + Шапка.СуммаПредоплаты;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СуммаПредоплаты;

КонецФункции

Функция СуммаБезЗаказа(Знач Шапка, Знач Товары)
	
	СуммаБезЗаказов = 0;
	
	Для Каждого СтрокаТЧ Из Товары Цикл
		Если НЕ Шапка.ПоЗаказам ИЛИ СтрокаТЧ.СверхЗаказа Тогда
			СуммаБезЗаказов = СуммаБезЗаказов + СтрокаТЧ.СуммаСНДС;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СуммаБезЗаказов;

КонецФункции

Функция СуммаПоЗаказам(Знач Шапка, Знач Товары)
	
	СуммаПоЗаказам = 0;
	
	Для Каждого СтрокаТЧ Из Товары Цикл
		Если Шапка.ПоЗаказам И НЕ СтрокаТЧ.СверхЗаказа Тогда
			СуммаПоЗаказам = СуммаПоЗаказам + СтрокаТЧ.СуммаСНДС;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СуммаПоЗаказам;

КонецФункции

#Область ПараметрыФискализацииЧекаШапкаЧека

Процедура ПараметрыФискализацииЧекаДокументОснование(Знач ПараметрыФискализацииЧека, Знач Шапка)
	
	ПараметрыФискализацииЧека.ДокументОснование = Шапка.ДокументСсылка;

КонецПроцедуры

Процедура ПараметрыФискализацииЧекаТипРасчета(Знач ПараметрыФискализацииЧека, Знач ЭтоВозврат)
	
	Если ЭтоВозврат Тогда
		ПараметрыФискализацииЧека.ТипРасчета = Перечисления.ТипыРасчетаДенежнымиСредствами.ВозвратДенежныхСредств;
	Иначе
		ПараметрыФискализацииЧека.ТипРасчета = Перечисления.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств;
	КонецЕсли;

КонецПроцедуры

Процедура ПараметрыФискализацииЧекаОрганизацияСведения(Знач ПараметрыФискализацииЧека, Знач Шапка)
	
	// Параметры необходимые для чека ЕНВД на принтере чеков
	СведенияОЮрФизЛице = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Шапка.Организация, ТекущаяДатаСеанса());
	
	ПараметрыФискализацииЧека.Организация		   = Шапка.Организация;
	ПараметрыФискализацииЧека.ОрганизацияНазвание  = СведенияОЮрФизЛице.ПолноеНаименование;
	ПараметрыФискализацииЧека.ОрганизацияИНН       = СведенияОЮрФизЛице.ИНН;
	ПараметрыФискализацииЧека.ОрганизацияКПП       = СведенияОЮрФизЛице.КПП;
	
	ПараметрыФискализацииЧека.АдресМагазина        = СведенияОЮрФизЛице.ФактическийАдрес;
	ПараметрыФискализацииЧека.НаименованиеМагазина = СведенияОЮрФизЛице.Представление;

КонецПроцедуры

Процедура ПараметрыФискализацииЧекаТорговыйОбъект(Знач ПараметрыФискализацииЧека, Знач Шапка)
	
	СведенияОЮрФизЛице = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Шапка.Организация, ТекущаяДатаСеанса());
	
	Попытка
		ТорговыйОбъектНаименование = Шапка.ТорговыйОбъект;
	Исключение
		ТорговыйОбъектНаименование = СведенияОЮрФизЛице.СокращенноеНаименование;
	КонецПопытки;

	ПараметрыФискализацииЧека.ТорговыйОбъект = ТорговыйОбъектНаименование;
	ПараметрыФискализацииЧека.МестоРасчетов = ТорговыйОбъектНаименование;
	
КонецПроцедуры

Процедура ПараметрыФискализацииЧекаПолучательСведения(Знач ПараметрыФискализацииЧека, Знач Шапка)
	
	СведенияОПокупателе = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Шапка.Контрагент, ТекущаяДатаСеанса());    
	
	Если ПустаяСтрока(СведенияОПокупателе.ИНН) Тогда
		ПараметрыФискализацииЧека.ЕстьПерсональныеДанные = Ложь;
	Иначе
		ПараметрыФискализацииЧека.Получатель    = СведенияОПокупателе.ПолноеНаименование;
		ПараметрыФискализацииЧека.ПолучательИНН = СведенияОПокупателе.ИНН;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ПараметрыФискализацииЧекаТаблицаПозицииЧека

Процедура ПараметрыФискализацииЧекаПозицииЧекаЗаполнить(Знач СтрокаПозицииЧека, Знач СтрокаТЧ, Знач СуммыПоБезЗаказов, Знач СуммаПредоплаты, Знач ВерсияФФД, Знач Валюта)
	
	СтрокаПозицииЧека.НомерСтрокиТовара = СтрокаТЧ.НомерСтроки;
	СтрокаПозицииЧека.НомерСекции = 1;
	
	Если ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(СтрокаПозицииЧека, "ПодакцизныйТовар")
		И ВерсияФФД = "1.2"
		И СтрокаПозицииЧека.ПодакцизныйТовар = Истина Тогда
		СтрокаПозицииЧека.СуммаАкциза = 0;
	КонецЕсли;
	
	ПараметрыФискализацииЧекаПозицииЧекаПризнакСпособаРасчета(СтрокаПозицииЧека, СуммыПоБезЗаказов, СуммаПредоплаты);
	ПараметрыФискализацииЧекаПозицииЧекаПризнакПредметаРасчета(СтрокаПозицииЧека, СтрокаТЧ);
	ПараметрыФискализацииЧекаПозицииЧекаНаименованиеПредметаРасчета(СтрокаПозицииЧека, СтрокаТЧ);
	ПараметрыФискализацииЧекаПозицииЧекаЕдиницаИзмеренияПоВерсииФФД(СтрокаПозицииЧека, СтрокаТЧ, ВерсияФФД);
	
	ПараметрыФискализацииЧекаПозицииЧекаРасчетныеДанные(СтрокаПозицииЧека, СтрокаТЧ);
	ПараметрыФискализацииЧекаПозицииЧекаСкидки(СтрокаПозицииЧека, СтрокаТЧ);
	
	ПараметрыФискализацииЧекаПозицииЧекаДанныеМаркированногоТовара(СтрокаПозицииЧека, СтрокаТЧ);
	ПараметрыФискализацииЧекаПозицииЧекаШтрихкод(СтрокаПозицииЧека, СтрокаТЧ, ВерсияФФД, Валюта);
	ПараметрыФискализацииЧекаПозицииЧекаКодВидаНоменклатурнойКлассификации(СтрокаПозицииЧека, СтрокаТЧ);
	
	ПараметрыФискализацииЧекаПозицииЧекаДанныеКомиссионнойПоставки(СтрокаПозицииЧека, СтрокаТЧ);
	ПараметрыФискализацииЧекаПозицииЧекаДанныеАгентскойПоставки(СтрокаПозицииЧека, СтрокаТЧ);
	ПараметрыФискализацииЧекаПозицииЧекаДанныеПоИмпортнойПоставке(СтрокаПозицииЧека, СтрокаТЧ);
	
	Если ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(СтрокаПозицииЧека, "ПризнакПредметаРасчета")
		И ВерсияФФД = "1.2"
		И СтрокаПозицииЧека.ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.ПодакцизныйТовар Тогда
		СтрокаПозицииЧека.СуммаАкциза = 0;
	КонецЕсли;

КонецПроцедуры

Процедура ПараметрыФискализацииЧекаПозицииЧекаПризнакСпособаРасчета(Знач СтрокаПозицииЧека, Знач СуммыПоБезЗаказов, Знач СуммаПредоплаты)
	
	СтрокаПозицииЧека.ПризнакСпособаРасчета = Перечисления.ПризнакиСпособаРасчета.ПередачаСПолнойОплатой;

КонецПроцедуры

Процедура ПараметрыФискализацииЧекаПозицииЧекаПризнакПредметаРасчета(Знач СтрокаПозицииЧека, Знач СтрокаТЧ)
	
	СтрокаПозицииЧека.ПризнакПредметаРасчета = РозничныеПродажиКлиентСервер.ПризнакПредметаРасчетаФискальнойОперации(
	                                               СтрокаТЧ.ТипНоменклатуры, СтрокаТЧ.ПодакцизныйТовар);

КонецПроцедуры

Процедура ПараметрыФискализацииЧекаПозицииЧекаНаименованиеПредметаРасчета(Знач СтрокаПозицииЧека, Знач СтрокаТЧ)
	
	СтрокаПозицииЧека.Наименование = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
										СтрокаТЧ.НоменклатураНаименование, СтрокаТЧ.ХарактеристикаНаименование);

КонецПроцедуры

Процедура ПараметрыФискализацииЧекаПозицииЧекаЕдиницаИзмеренияПоВерсииФФД(Знач СтрокаПозицииЧека, Знач СтрокаТЧ, Знач ВерсияФФД)
	
	Упаковка = СтрокаТЧ.УпаковкаНаименование;
		
	Если НЕ ЗначениеЗаполнено(Упаковка) Тогда
		Упаковка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТЧ.Номенклатура, "БазоваяЕдиницаИзмерения");
	КонецЕсли;
	
	Если ВерсияФФД = "1.2" Тогда
		СтрокаПозицииЧека.КодЕдиницыИзмерения = РозничныеПродажи.КодЕдиницыИзмеренияПараметраЧека(Упаковка);
	Иначе
		СтрокаПозицииЧека.ЕдиницаИзмерения = СокрЛП(Упаковка);
	КонецЕсли;

КонецПроцедуры

Процедура ПараметрыФискализацииЧекаПозицииЧекаРасчетныеДанные(Знач СтрокаПозицииЧека, Знач СтрокаТЧ)
	
	СтрокаПозицииЧека.Количество = ?(СтрокаТЧ.КоличествоУпаковок = 0, 1, СтрокаТЧ.КоличествоУпаковок);
	СтрокаПозицииЧека.Цена       = СтрокаТЧ.Цена;
	СтрокаПозицииЧека.СтавкаНДС  = РозничныеПродажиКлиентСервер.СтавкаНДСФискальнойОперации(СтрокаТЧ.СтавкаНДС);
	СтрокаПозицииЧека.СуммаНДС   = СтрокаТЧ.СуммаНДС;
	СтрокаПозицииЧека.Сумма      = СтрокаТЧ.СуммаСНДС;

КонецПроцедуры

Процедура ПараметрыФискализацииЧекаПозицииЧекаСкидки(Знач СтрокаПозицииЧека, Знач СтрокаТЧ)
	
	Если СтрокаПозицииЧека.Количество <> 0 Тогда
		СтрокаПозицииЧека.ЦенаСоСкидками = Окр(СтрокаПозицииЧека.Сумма / СтрокаПозицииЧека.Количество, 2);
	КонецЕсли;
	СтрокаПозицииЧека.СуммаСкидок = СтрокаТЧ.СуммаСкидки;

КонецПроцедуры

Процедура ПараметрыФискализацииЧекаПозицииЧекаДанныеМаркированногоТовара(Знач СтрокаПозицииЧека, Знач СтрокаТЧ)
	
	СтрокаПозицииЧека.ДанныеКодаТоварнойНоменклатуры.ТипМаркировки                          = Неопределено;
	СтрокаПозицииЧека.ДанныеКодаТоварнойНоменклатуры.КонтрольныйИдентификационныйЗнак       = Неопределено;
	СтрокаПозицииЧека.ДанныеКодаТоварнойНоменклатуры.ГлобальныйИдентификаторТорговойЕдиницы = Неопределено;
	СтрокаПозицииЧека.ДанныеКодаТоварнойНоменклатуры.СерийныйНомер                          = Неопределено;
	
	Если НЕ (ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(СтрокаТЧ, "ВидПродукцииИС")
				И ЗначениеЗаполнено(СтрокаТЧ.ВидПродукцииИС)) // ВидПродукцииИС
				ИЛИ НЕ (ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(СтрокаТЧ, "Штрихкод")
				И ЗначениеЗаполнено(СтрокаТЧ.Штрихкод)) Тогда // Штрихкод
		Возврат;
	КонецЕсли;
	
	ВидПродукции = СтрокаТЧ.ВидПродукцииИС;
	Если НЕ (ЗначениеЗаполнено(ВидПродукции) И ИнтеграцияИСПовтИсп.ЭтоПродукцияИСМП(ВидПродукции, Истина)) Тогда
		Возврат;
	КонецЕсли;
	
	ТипМаркировкиККТ = ИнтеграцияИСКлиентСервер.ТипМаркировкиККТПоВидуПродукции(ВидПродукции);
	Если НЕ ЗначениеЗаполнено(ТипМаркировкиККТ) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеРазбора = РазборКодаМаркировкиИССлужебный.РазобратьКодМаркировки(СтрокаТЧ.Штрихкод, ВидПродукции);
	Если ДанныеРазбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаПозицииЧека.Штрихкод                                                              = СтрокаТЧ.Штрихкод;
	СтрокаПозицииЧека.ДанныеКодаТоварнойНоменклатуры.ТипМаркировки                          = ТипМаркировкиККТ;
	СтрокаПозицииЧека.ДанныеКодаТоварнойНоменклатуры.ГлобальныйИдентификаторТорговойЕдиницы = ДанныеРазбора.СоставКодаМаркировки.GTIN;
	СтрокаПозицииЧека.ДанныеКодаТоварнойНоменклатуры.СерийныйНомер                          = ДанныеРазбора.СоставКодаМаркировки.СерийныйНомер;
	
КонецПроцедуры

Процедура ПараметрыФискализацииЧекаПозицииЧекаШтрихкод(Знач СтрокаПозицииЧека, Знач СтрокаТЧ, Знач ВерсияФФД, Знач Валюта)
	
	Если ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(СтрокаТЧ, "Штрихкод") Тогда
		СтрокаПозицииЧека.Штрихкод = СтрокаТЧ.Штрихкод;
		
		Упаковка = СтрокаТЧ.Упаковка;
		
		Если НЕ ЗначениеЗаполнено(Упаковка) Тогда
			Если ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(СтрокаТЧ, "Номенклатура") Тогда
				Упаковка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТЧ.Номенклатура, "ЕдиницаХраненияОстатков");
			Иначе
				Упаковка = "шт.";
			КонецЕсли;
		КонецЕсли;
		
		Если ВерсияФФД = "1.2" Тогда        
			СтрокаПозицииЧека.КодЕдиницыИзмерения = РозничныеПродажи.КодЕдиницыИзмеренияПараметраЧека(Упаковка);
		Иначе
			СтрокаПозицииЧека.ЕдиницаИзмерения = СокрЛП(Упаковка);
		КонецЕсли;
				
		Если ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(СтрокаТЧ, "РезультатРаспределения")
			И ЗначениеЗаполнено(СтрокаТЧ.РезультатРаспределения) Тогда
			СтрокаПозицииЧека.РезультатРаспределенияВрем = СтрокаТЧ.РезультатРаспределения;
			
			ПолныйКодМаркировки = СтрокаТЧ.РезультатРаспределения.ПолныйКодМаркировки;
			Если ВерсияФФД = "1.2" И ЗначениеЗаполнено(ПолныйКодМаркировки) Тогда
				СтрокаПозицииЧека.ШтрихкодBase64 = СтрокаТЧ.РезультатРаспределения.ПолныйКодМаркировки;
			КонецЕсли;
			
			Если ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(СтрокаТЧ, "ДополнениеКНаименованиюТовара")
				И ТипЗнч(СтрокаТЧ.ДополнениеКНаименованиюТовара) = Тип("Структура") Тогда
				
				РозничныеПродажи.ЗаполнитьДополнениеКНаименованиюТовара(СтрокаПозицииЧека, СтрокаТЧ, Упаковка, Валюта, ВерсияФФД);
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ПараметрыФискализацииЧекаПозицииЧекаКодВидаНоменклатурнойКлассификации(Знач СтрокаПозицииЧека, Знач СтрокаТЧ)
	
	Если ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(СтрокаТЧ, "КодВидаНоменклатурнойКлассификации") Тогда
		СтрокаПозицииЧека.КодВидаНоменклатурнойКлассификации = СтрокаТЧ.КодВидаНоменклатурнойКлассификации;
	КонецЕсли;

КонецПроцедуры

Процедура ПараметрыФискализацииЧекаПозицииЧекаДанныеКомиссионнойПоставки(Знач СтрокаПозицииЧека, Знач СтрокаТЧ)
	
КонецПроцедуры

Процедура ПараметрыФискализацииЧекаПозицииЧекаДанныеАгентскойПоставки(Знач СтрокаПозицииЧека, Знач СтрокаТЧ)

КонецПроцедуры

Процедура ПараметрыФискализацииЧекаПозицииЧекаДанныеПоИмпортнойПоставке(Знач СтрокаПозицииЧека, Знач СтрокаТЧ)
	
	ДанныеПоИмпортнойПоставке = Новый Структура("НомерТаможеннойДекларации, КодСтраныПроисхождения", Неопределено, Неопределено);
	ЗаполнитьЗначенияСвойств(ДанныеПоИмпортнойПоставке, СтрокаТЧ);
	
	СтрокаПозицииЧека.НомерТаможеннойДекларации = ?(ЗначениеЗаполнено(ДанныеПоИмпортнойПоставке.НомерТаможеннойДекларации), СокрЛП(ДанныеПоИмпортнойПоставке.НомерТаможеннойДекларации.РегистрационныйНомер), ""); 
	СтрокаПозицииЧека.КодСтраныПроисхожденияТовара = ДанныеПоИмпортнойПоставке.КодСтраныПроисхождения;
			
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТЧ, "КодВидаНоменклатурнойКлассификации") Тогда
		СтрокаПозицииЧека.КодВидаНоменклатурнойКлассификации = СтрокаТЧ.КодВидаНоменклатурнойКлассификации;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ПараметрыФискализацииЧекаТаблицаОплатЧека

Процедура ПараметрыФискализацииЧекаТаблицаОплат(Знач ПараметрыФискализацииЧека, Знач СуммыПоБезЗаказов, Знач СуммаПредоплаты)
	
	СуммаПоЗаказам  = СуммыПоБезЗаказов.СуммаПоЗаказам;
	СуммаБезЗаказов = СуммыПоБезЗаказов.СуммаБезЗаказов;
	
	// Сумма постоплатой (в кредит)
	СуммаПостОплаты = СуммаПоЗаказам + СуммаБезЗаказов - СуммаПредоплаты;
	Если СуммаПостОплаты <> 0 Тогда
		СтрокаОплаты = Новый Структура();
		СтрокаОплаты.Вставить("ТипОплаты", Перечисления.ТипыОплатыККТ.Постоплата);
		СтрокаОплаты.Вставить("Сумма",     СуммаПостОплаты);
		ПараметрыФискализацииЧека.ТаблицаОплат.Добавить(СтрокаОплаты);
	КонецЕсли;
	
	// Сумма предоплатой (зачетом аванса)
	Если СуммаПредоплаты <> 0 Тогда
		СтрокаОплаты = Новый Структура();
		СтрокаОплаты.Вставить("ТипОплаты", Перечисления.ТипыОплатыККТ.Предоплата);
		СтрокаОплаты.Вставить("Сумма",     СуммаПредоплаты);
		ПараметрыФискализацииЧека.ТаблицаОплат.Добавить(СтрокаОплаты);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

