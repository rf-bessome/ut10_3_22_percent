#Область ПрограмныйИнтерфейс

Функция ЭтоМаркируемаяПродукция(Номенклатура) Экспорт
	
	ВидПродукцииИС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ВидПродукцииИС");
	Если ВидПродукцииИС = Перечисления.ВидыПродукцииИС.ПодконтрольнаяПродукцияВЕТИС
		Или ВидПродукцииИС = Перечисления.ВидыПродукцииИС.Зерно
		Или ВидПродукцииИС = Перечисления.ВидыПродукцииИС.ПродуктыПереработкиЗерна Тогда 
		Возврат Ложь;
	КонецЕсли;
	Возврат ЗначениеЗаполнено(ВидПродукцииИС);
	
КонецФункции

Функция НастройкиНоменклатурыИнтерфейса(Номенклатура) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Маркируемая", Ложь);
	Результат.Вставить("АвтоОСУ", Ложь);
	ВидПродукцииИС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ВидПродукцииИС");
	Если ВидПродукцииИС = Перечисления.ВидыПродукцииИС.ПодконтрольнаяПродукцияВЕТИС
		Или ВидПродукцииИС = Перечисления.ВидыПродукцииИС.Зерно
		Или ВидПродукцииИС = Перечисления.ВидыПродукцииИС.ПродуктыПереработкиЗерна
		Или Не ЗначениеЗаполнено(ВидПродукцииИС) Тогда 
		Возврат Результат;
	КонецЕсли;
	Результат.Маркируемая = Истина;
	Результат.АвтоОСУ = ИнтеграцияИСМПКлиентСерверПовтИсп.ВидПродукцииПоддерживаетОбъемноСортовойУчет(ВидПродукцииИС);
	Возврат Результат;
	
КонецФункции

// Предназначена для определения факта наличия в данных документа номенклатуры, являющейся табачной продукцией.
// 
// Параметры:
//	* Коллекция             – ДанныеФормыКоллекция – состав номенклатуры документа
//	* ЕстьМаркируемаяПродукция – Булево – исходящий признак наличия табачной продукции
//
Процедура ЕстьМаркируемаяПродукцияВКоллекции(Коллекция, ВидПродукции, ЕстьМаркируемаяПродукция) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаТовары", Коллекция.Выгрузить(, "Номенклатура"));
	Запрос.УстановитьПараметр("ВидПродукции", ВидПродукции);
	
	Запрос.Текст = СтрШаблон("
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ
	|	ВремТаблТаблицаТовары
	|ИЗ
	|	&ТаблицаТовары КАК ТаблицаТовары
	|
	|;
	|
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ИСТИНА КАК ЕстьМаркируемаяПродукция
	|ИЗ
	|	ВремТаблТаблицаТовары КАК ТаблицаТовары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТаблицаТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	%1
	|",УсловиеВидПродукции(ВидПродукции));
	
	Результат = Запрос.Выполнить();
	
	ЕстьМаркируемаяПродукция = НЕ Результат.Пустой();
	
КонецПроцедуры

// Заполняет переданную таблицу товарами переданного документа, являющимися маркируемой продукцией требуемого вида.
// 
// Параметры:
//  Контекст - ДанныеФормыСтруктура, ФормаКлиентскогоПриложения, ДокументСсылка - документ, маркируемую продукцию которого необходимо получить.
//  ВидМаркируемойПродукции - ПеречислениеСсылка.ВидыПродукцииИС - вид маркируемой продукции, которую необходимо получить.
//  ТаблицаМаркируемойПродукции - ТаблицаЗначений - таблица маркируемой продукции документа. (См. ПроверкаИПодборПродукцииИСМП.ТаблицаМаркируемойПродукцииДокумента())
//
Процедура ЗаполнитьМаркируемуюПродукциюДокумента(Контекст, ВидМаркируемойПродукции, ТаблицаМаркируемойПродукции) Экспорт
	
	Если ТаблицаМаркируемойПродукции.Количество() > 0 Тогда
		РезультатЗапроса = ЗапросGTINпоТаблицеМаркируемойПродукции(ВидМаркируемойПродукции, ТаблицаМаркируемойПродукции);
		ТаблицаМаркируемойПродукции.Очистить();
	ИначеЕсли ИнтеграцияИСМПУТКлиентСервер.ЭтоДокументПриобретения(Контекст) Тогда
		РезультатЗапроса = ЗапросМаркируемойПродукцииПриобретениеТоваровУслуг(Контекст, ВидМаркируемойПродукции);
	ИначеЕсли ИнтеграцияИСМПУТКлиентСервер.ЭтоЧекККМ(Контекст)
		Или ИнтеграцияИСМПУТКлиентСервер.ЭтоЧекККМВозврат(Контекст) Тогда
		РезультатЗапроса = ЗапросМаркируемойПродукцииЧекККМ(Контекст, ВидМаркируемойПродукции);
	ИначеЕсли ИнтеграцияИСМПУТКлиентСервер.ЭтоДокументПоНаименованию(Контекст, "РеализацияТоваровУслуг") Тогда
		РезультатЗапроса = ЗапросМаркируемойПродукцииРеализацияТоваровУслуг(Контекст, ВидМаркируемойПродукции);
	ИначеЕсли ИнтеграцияИСМПУТКлиентСервер.ЭтоДокументПоНаименованию(Контекст, "ВозвратТоваровПоставщику") Тогда
		РезультатЗапроса = ЗапросМаркируемойПродукцииВозвратТоваровПоставщику(Контекст, ВидМаркируемойПродукции);
	ИначеЕсли ИнтеграцияИСМПУТКлиентСервер.ЭтоДокументПоНаименованию(Контекст, "КорректировкаРеализации") Тогда
		РезультатЗапроса = ЗапросМаркируемойПродукцииКорректировкаРеализации(Контекст, ВидМаркируемойПродукции);
	ИначеЕсли ИнтеграцияИСМПУТКлиентСервер.ЭтоДокументПоНаименованию(Контекст, "ВозвратТоваровОтПокупателя") Тогда
		РезультатЗапроса = ЗапросМаркируемойПродукцииВозвратТоваровОтПокупателя(Контекст, ВидМаркируемойПродукции);
	Иначе
		Возврат;
	КонецЕсли;
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНоменклатура.Следующий() Цикл
		ВыборкаХарактеристика = ВыборкаНоменклатура.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаХарактеристика.Следующий() Цикл
			ПродукцияПоGTIN = ТаблицаМаркируемойПродукции.СкопироватьКолонки();
			СписокКодовGTIN = Новый Массив;
			
			ВыборкаGTIN = ВыборкаХарактеристика.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаGTIN.Следующий() Цикл
				
				Если Не ЗначениеЗаполнено(ВыборкаНоменклатура.Номенклатура) Тогда
					// Соответственно тут должен быть остаточный GTIN
					Выборка = ВыборкаGTIN.Выбрать();
					Пока Выборка.Следующий() Цикл
						ЗаполнитьЗначенияСвойств(ТаблицаМаркируемойПродукции.Добавить(), Выборка);
					КонецЦикла;
					Продолжить;
				КонецЕсли;
				
				Если ПродукцияПоGTIN.Количество() = 0 Тогда
					Выборка = ВыборкаGTIN.Выбрать();
					Пока Выборка.Следующий() Цикл
						ЗаполнитьЗначенияСвойств(ПродукцияПоGTIN.Добавить(), Выборка,, "GTIN");
					КонецЦикла;
				КонецЕсли;
				
				Если МенеджерОборудованияКлиентСервер.ПроверитьКорректностьGTIN(ВыборкаGTIN.GTIN) Тогда
					GTIN = ШтрихкодированиеИСКлиентСервер.GTINПоШтрихкодуEAN(ВыборкаGTIN.GTIN);
					СписокКодовGTIN.Добавить(GTIN);
				КонецЕсли;
			КонецЦикла;
			
			Для Каждого СтрокаПродукцииПоGTIN Из ПродукцияПоGTIN Цикл
				ЗаполнитьЗначенияСвойств(ТаблицаМаркируемойПродукции.Добавить(), СтрокаПродукцииПоGTIN);
				СтрокаПродукцииПоGTIN.КодыGTIN.ЗагрузитьЗначения(СписокКодовGTIN);
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Функция ЗапросGTINпоТаблицеМаркируемойПродукции(ВидМаркируемойПродукции, ТаблицаМаркируемойПродукции)
	
	ПоляВЫБРАТЬ = Новый Массив;
	ПоляБезGTIN = Новый Массив;
	Для Каждого Колонка Из ТаблицаМаркируемойПродукции.Колонки Цикл
		Если Колонка.Имя <> "КодыGTIN"
				И Колонка.Имя <> "ИдентификаторыПроисхожденияВЕТИС" Тогда
			ПоляВЫБРАТЬ.Добавить(СтрШаблон("МаркируемаяПродукция.%1 КАК %1", Колонка.Имя));
			Если Колонка.Имя <> "GTIN" Тогда
				ПоляБезGTIN.Добавить(СтрШаблон("МаркируемаяПродукция.%1 КАК %1", Колонка.Имя));
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&ПоляВЫБРАТЬ
	|ПОМЕСТИТЬ ВТМаркируемаяПродукция
	|ИЗ
	|	&МаркируемаяПродукция КАК МаркируемаяПродукция
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ШтрихкодыНоменклатуры.Штрихкод, """") КАК GTIN,
	|	&ПоляБезGTIN
	|ИЗ
	|	ВТМаркируемаяПродукция КАК МаркируемаяПродукция
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Штрихкоды КАК ШтрихкодыНоменклатуры
	|		ПО МаркируемаяПродукция.Номенклатура = ШтрихкодыНоменклатуры.Владелец
	|		 И МаркируемаяПродукция.Характеристика = ШтрихкодыНоменклатуры.ХарактеристикаНоменклатуры
	|ГДЕ
	|	МаркируемаяПродукция.Номенклатура <> Значение(Справочник.Номенклатура.ПустаяСсылка)
	|	И МаркируемаяПродукция.GTIN = """"
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ // Номенклатура не заполнена только для строк с остаточными GTIN
	|	&ПоляВЫБРАТЬ
	|ИЗ
	|	ВТМаркируемаяПродукция КАК МаркируемаяПродукция
	|ГДЕ
	|	МаркируемаяПродукция.Номенклатура = Значение(Справочник.Номенклатура.ПустаяСсылка)
	|	И МаркируемаяПродукция.GTIN <> """"
	|ИТОГИ ПО
	|	Номенклатура,
	|	Характеристика,
	|	GTIN";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("МаркируемаяПродукция", ТаблицаМаркируемойПродукции);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПоляВЫБРАТЬ", СтрСоединить(ПоляВЫБРАТЬ, ",
	|"));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПоляБезGTIN", СтрСоединить(ПоляБезGTIN, ",
	|"));
	
	Возврат Запрос.Выполнить();
	
КонецФункции

// Возвращает для передеанного документа таблицу его товаров, являющихся табачной продукцией.
// 
// Параметры:
//	* Контекст - ФормаКлиентскогоПриложения, ДокументСсылка - документ, табачную продукцию которого необходимо получить.
// ВозвращаемоеЗначение:
//	* ТаблицаТабачнойПродукции - ТаблицаЗначений - таблица табачной продукции документа с колонками:
//		** GTIN           - Строка - GTIN продукции
//		** Номенклатура   - ОпределяемыйТип.Номенклатура - номенклатура табачной продукции
//		** Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатуры - характеристика номенклатура табачной продукции
//		** Серия          - ОпределяемыйТип.СерияНоменклатуры - серия номенклатура табачной продукции
//		** Количество     - Число - количество табачной продукции
//
Функция ТаблицаМаркируемойПродукцииДокумента(Контекст, ВидПродукции) Экспорт
	
	ТаблицаМаркируемойПродукции = ТаблицаМаркируемойПродукции();
	
	Если ИнтеграцияИСМПУТКлиентСервер.ЭтоДокументПриобретения(Контекст) Тогда
		РезультатЗапроса = ЗапросМаркируемойПродукцииПриобретениеТоваровУслуг(Контекст, ВидПродукции);
	ИначеЕсли ИнтеграцияИСМПУТКлиентСервер.ЭтоЧекККМ(Контекст)
		Или ИнтеграцияИСМПУТКлиентСервер.ЭтоЧекККМВозврат(Контекст) Тогда
		РезультатЗапроса = ЗапросМаркируемойПродукцииЧекККМ(Контекст, ВидПродукции);
	ИначеЕсли ИнтеграцияИСМПУТКлиентСервер.ЭтоДокументПоНаименованию(Контекст, "РеализацияТоваровУслуг") Тогда
		РезультатЗапроса = ЗапросМаркируемойПродукцииРеализацияТоваровУслуг(Контекст, ВидПродукции);
	ИначеЕсли ИнтеграцияИСМПУТКлиентСервер.ЭтоДокументПоНаименованию(Контекст, "ВозвратТоваровПоставщику") Тогда
		РезультатЗапроса = ЗапросМаркируемойПродукцииВозвратТоваровПоставщику(Контекст, ВидПродукции);
	ИначеЕсли ИнтеграцияИСМПУТКлиентСервер.ЭтоДокументПоНаименованию(Контекст, "КорректировкаРеализации") Тогда
		РезультатЗапроса = ЗапросМаркируемойПродукцииКорректировкаРеализации(Контекст, ВидПродукции);
	Иначе
		Возврат ТаблицаМаркируемойПродукции;
	КонецЕсли;
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНоменклатура.Следующий() Цикл
		ВыборкаХарактеристика = ВыборкаНоменклатура.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаХарактеристика.Следующий() Цикл
			ПродукцияПоGTIN = ТаблицаМаркируемойПродукции();
			ДлинаСтрокиGTIN = 0;
			
			ВыборкаGTIN = ВыборкаХарактеристика.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаGTIN.Следующий() Цикл
				Если ПродукцияПоGTIN.Количество() = 0 Тогда
					Выборка = ВыборкаGTIN.Выбрать();
					Пока Выборка.Следующий() Цикл
						ЗаполнитьЗначенияСвойств(ПродукцияПоGTIN.Добавить(), Выборка,, "GTIN");
					КонецЦикла;
				КонецЕсли;
				
				Если МенеджерОборудованияКлиентСервер.ПроверитьКорректностьGTIN(ВыборкаGTIN.GTIN)
					И СтрДлина(ВыборкаGTIN.GTIN) > ДлинаСтрокиGTIN Тогда
					ПродукцияПоGTIN.ЗаполнитьЗначения(ВыборкаGTIN.GTIN, "GTIN");
					ДлинаСтрокиGTIN = СтрДлина(ВыборкаGTIN.GTIN);
				КонецЕсли;
			КонецЦикла;
			
			Для Каждого СтрокаПродукцииПоGTIN Из ПродукцияПоGTIN Цикл
				ЗаполнитьЗначенияСвойств(ТаблицаМаркируемойПродукции.Добавить(), СтрокаПродукцииПоGTIN);
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ТаблицаМаркируемойПродукции;
		
КонецФункции

// Заполняет в табличной части служебные реквизиты, например: признак использования характеристик номенклатуры.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма.
//  ТабличнаяЧасть - ДанныеФормыКоллекция, ТаблицаЗначений - таблица для заполнения.
//
Процедура ЗаполнитьСлужебныеРеквизитыВКоллекции(Форма, ТабличнаяЧасть) Экспорт
	
	ПараметрыЗаполненияРеквизитов = Новый Структура;
		
	ПараметрыЗаполненияРеквизитов.Вставить(
		"ЗаполнитьПризнакХарактеристикиИспользуются",
		Новый Структура("Номенклатура", "ХарактеристикиИспользуются"));
	
	ПараметрыЗаполненияРеквизитов.Вставить(
		"ЗаполнитьПризнакТипНоменклатуры",
		Новый Структура("Номенклатура", "ТипНоменклатуры"));
	
	НоменклатураСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(
		ТабличнаяЧасть, ПараметрыЗаполненияРеквизитов);
	
КонецПроцедуры

#Область СерииНоменклатуры

// Определяет параметры указания серий для товаров, указанных в форме.
//
// Параметры:
//		Форма						- ФормаКлиентскогоПриложения	- форма с товарами, для которой необходимо определить параметры указания серий.
//		ПараметрыУказанияСерий	- Структура			- заполняемые параметры указания серий, состав полей структуры задается в функции НоменклатураКлиентСервер.ПараметрыУказанияСерий.
//
Процедура ЗаполнитьПараметрыУказанияСерий(Форма, ПараметрыУказанияСерий) Экспорт
	
	ПараметрыУказанияСерий = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта                  = Форма.ИмяФормы;
	ПараметрыУказанияСерий.ИмяИсточникаЗначенийВФормеОбъекта = "ЭтаФорма";
	
	ПараметрыУказанияСерий.ИмяТЧТовары       = "ПодобраннаяТабачнаяПродукция";
	ПараметрыУказанияСерий.ИмяТЧСерии        = "ПодобраннаяТабачнаяПродукция";
	ПараметрыУказанияСерий.ИмяПоляСклад      = "Склад";
	ПараметрыУказанияСерий.ИмяПоляКоличество = "Количество";
	
	ПараметрыУказанияСерий.ИменаПолейДополнительные.Добавить("КоличествоПодобрано");

	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = глЗначениеПеременной("ИспользоватьСерииНоменклатуры");
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = глЗначениеПеременной("ИспользоватьСерииНоменклатуры");
	
	ПараметрыУказанияСерий.Дата = ТекущаяДатаСеанса();
	
КонецПроцедуры

// Формирует текст запроса для расчета статусов указания серий
//	Параметры:
//		ТекстЗапроса				- Строка		- формируемый текст запроса.
//		ПараметрыУказанияСерий	- Структура	- состав полей задается в функции НоменклатураКлиентСервер.ПараметрыУказанияСерий
//
Процедура СформироватьТекстЗапросаЗаполненияСтатусовУказанияСерий(ПараметрыУказанияСерий, ТекстЗапроса) Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Товары.НомерСтроки,
	|	Товары.Номенклатура,
	|	Товары.Серия,
	|	Товары." + ПараметрыУказанияСерий.ИмяПоляКоличество + " КАК Количество,";
	
	Для Каждого ИмяПоля Из ПараметрыУказанияСерий.ИменаПолейДополнительные Цикл
		ТекстЗапроса = ТекстЗапроса + "
		|	Товары." + ИмяПоля + " КАК " + ИмяПоля + ",";
	КонецЦикла;
	
	ТекстЗапроса = ТекстЗапроса + "
	|	Товары.СтатусУказанияСерий КАК СтатусУказанияСерий
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.СтатусУказанияСерий КАК СтарыйСтатусУказанияСерий,
	|	ВЫБОР
	|		КОГДА СправочникНоменклатура.Ссылка ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА НЕ СправочникНоменклатура.ВестиУчетПоСериям
	|			ТОГДА 0
	|		КОГДА (Товары.Количество <> 0 ИЛИ Товары.КоличествоПодобрано <> 0)
	|			И Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|			ТОГДА 4
	|		ИНАЧЕ 3
	|	КОНЕЦ КАК СтатусУказанияСерий
	|ПОМЕСТИТЬ ТаблицаСтатусов
	|ИЗ
	|	Товары КАК Товары
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСтатусов.НомерСтроки КАК НомерСтроки,
	|	ТаблицаСтатусов.СтатусУказанияСерий КАК СтатусУказанияСерий
	|ИЗ
	|	ТаблицаСтатусов КАК ТаблицаСтатусов
	|ГДЕ
	|	ТаблицаСтатусов.СтарыйСтатусУказанияСерий <> ТаблицаСтатусов.СтатусУказанияСерий
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|";
	
КонецПроцедуры

// Предназначена для расчета статусов указания серий во всех строках таблицы товаров
// см. НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий
// 
// Параметры:
// * Форма        - ФормаКлиентскогоПриложения - форма с таблицей товаров
// * ТекстЗапроса - Строка - текст запроса для расчета статусов указания серий 
//
Процедура ЗаполнитьСтатусыУказанияСерий(Форма, ПараметрыУказанияСерий) Экспорт
	
	НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(Форма, ПараметрыУказанияСерий);
	
КонецПроцедуры

// Возвращает через параметр наличие права на добавление элементов справочника СерииНоменклатуры
// 
// Параметры:
// 	ПравоДобавлениеСерий - Булево - исходящий, наличие права на добавление.
//
Процедура ОпределитьПравоДобавлениеСерий(ПравоДобавлениеСерий) Экспорт
	
	ПравоДобавлениеСерий = ПравоДоступа("Добавление", Метаданные.Справочники.СерииНоменклатуры);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

#Область ТаблицаТабачнойПродукцииДокумента

// Формирует пустую таблицу табачной продукции
// 
// Параметры:
// Возвращаемое значение:
// 	ТаблицаЗначений - таблица определяющая состав табачной продукции документа:
// * GTIN           - ОпределяемыйТип.GTIN                       - штрихкод
// * Номенклатура   - ОпределяемыйТип.Номенклатура               - номенклатура
// * Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатуры - характеристика
// * Серия          - ОпределяемыйТип.СерияНоменклатуры          - серия
// * Количество     - Число                                      - количество
Функция ТаблицаМаркируемойПродукции()

	Таблица = Новый ТаблицаЗначений();
	Таблица.Колонки.Добавить("GTIN",           Новый ОписаниеТипов("Строка"));
	Таблица.Колонки.Добавить("Номенклатура",   Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	Таблица.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	Таблица.Колонки.Добавить("Серия",          Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	Таблица.Колонки.Добавить("Количество",     Новый ОписаниеТипов("Число"));
	
	Возврат Таблица;
	
КонецФункции

Функция ЗапросМаркируемойПродукцииПриобретениеТоваровУслуг(Документ, ВидПродукции)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументСсылка", Документ);
	Запрос.УстановитьПараметр("ВидПродукции", ВидПродукции);
	Запрос.Текст = СтрШаблон("
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЕСТЬNULL(ШтрихкодыНоменклатуры.Штрихкод, """")           КАК GTIN,
	|	ПоступлениеТоваровУслугТовары.Номенклатура               КАК Номенклатура,
	|	ПоступлениеТоваровУслугТовары.ХарактеристикаНоменклатуры КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)      КАК Серия,
	|	ПоступлениеТоваровУслугТовары.Количество                 КАК Количество
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ПоступлениеТоваровУслугТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Штрихкоды КАК ШтрихкодыНоменклатуры
	|		ПО ПоступлениеТоваровУслугТовары.Номенклатура = ШтрихкодыНоменклатуры.Владелец
	|		 И ПоступлениеТоваровУслугТовары.ХарактеристикаНоменклатуры = ШтрихкодыНоменклатуры.ХарактеристикаНоменклатуры
	|		 И ШтрихкодыНоменклатуры.СерияНоменклатуры = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		 И ШтрихкодыНоменклатуры.Качество = ЗНАЧЕНИЕ(Справочник.Качество.Новый)
	|ГДЕ
	|	ПоступлениеТоваровУслугТовары.Ссылка = &ДокументСсылка
	|	И %1
	|ИТОГИ ПО
	|	Номенклатура,
	|	Характеристика,
	|	GTIN
	|",УсловиеВидПродукции(ВидПродукции));
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция ЗапросМаркируемойПродукцииЧекККМ(ФормаОбъект, ВидПродукции)
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ФормаОбъект, "ДокументОбъект") Тогда
		Товары = ФормаОбъект.ДокументОбъект.Товары.Выгрузить();
	ИначеЕсли ТипЗнч(ФормаОбъект) = Тип("ФормаКлиентскогоПриложения") Тогда
		Товары = ФормаОбъект.Объект.Товары.Выгрузить();
	Иначе
		Товары = ФормаОбъект.Товары.Выгрузить();
	КонецЕсли;
	
	Возврат ЗапросМаркируемойПродукцииПоТаблице(Товары, ВидПродукции);
	
КонецФункции

Функция ЗапросМаркируемойПродукцииРеализацияТоваровУслуг(Документ, ВидПродукции)
	
	Возврат ЗапросМаркируемойПродукцииПоДокументу(Документ, ВидПродукции);
	
КонецФункции

Функция ЗапросМаркируемойПродукцииВозвратТоваровПоставщику(Документ, ВидПродукции)
	
	Возврат ЗапросМаркируемойПродукцииПоДокументу(Документ, ВидПродукции);
	
КонецФункции

Функция ЗапросМаркируемойПродукцииКорректировкаРеализации(Документ, ВидПродукции)
	
	Возврат ЗапросМаркируемойПродукцииПоДокументу(Документ, ВидПродукции);
	
КонецФункции

Функция ЗапросМаркируемойПродукцииВозвратТоваровОтПокупателя(Документ, ВидПродукции)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументСсылка", Документ);
	Запрос.УстановитьПараметр("ВидПродукции", ВидПродукции);
	Запрос.Текст = СтрШаблон("
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЕСТЬNULL(ШтрихкодыНоменклатуры.Штрихкод, """")           КАК GTIN,
	|	ВозвратТоваровОтПокупателяТовары.Номенклатура               КАК Номенклатура,
	|	ВозвратТоваровОтПокупателяТовары.ХарактеристикаНоменклатуры КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)      КАК Серия,
	|	ВозвратТоваровОтПокупателяТовары.Количество                 КАК Количество
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ВозвратТоваровОтПокупателяТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Штрихкоды КАК ШтрихкодыНоменклатуры
	|		ПО ВозвратТоваровОтПокупателяТовары.Номенклатура = ШтрихкодыНоменклатуры.Владелец
	|		 И ВозвратТоваровОтПокупателяТовары.ХарактеристикаНоменклатуры = ШтрихкодыНоменклатуры.ХарактеристикаНоменклатуры
	|		 И ШтрихкодыНоменклатуры.СерияНоменклатуры = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		 И ШтрихкодыНоменклатуры.Качество = ЗНАЧЕНИЕ(Справочник.Качество.Новый)
	|ГДЕ
	|	ВозвратТоваровОтПокупателяТовары.Ссылка = &ДокументСсылка
	|	И %1
	|ИТОГИ ПО
	|	Номенклатура,
	|	Характеристика,
	|	GTIN
	|",УсловиеВидПродукции(ВидПродукции));
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция ЗапросМаркируемойПродукцииПоДокументу(Документ, ВидПродукции)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументСсылка", Документ);
	Запрос.УстановитьПараметр("ВидПродукции", ВидПродукции);
	Запрос.Текст = СтрШаблон("
	|ВЫБРАТЬ
	|	Товары.Номенклатура               КАК Номенклатура,
	|	Товары.ХарактеристикаНоменклатуры КАК Характеристика,
	|	Товары.СерияНоменклатуры          КАК Серия,
	|	СУММА(Товары.Количество * Товары.Коэффициент)          КАК Количество
	|ПОМЕСТИТЬ
	|	ВТМаркируемаяПродукция
	|ИЗ
	|	Документ.%2.Товары КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Товары.Ссылка = &ДокументСсылка
	|	И %1
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.ХарактеристикаНоменклатуры,
	|	Товары.СерияНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ШтрихкодыНоменклатуры.Штрихкод ЕСТЬ NULL
	|			ТОГДА 3
	|		ИНАЧЕ 1
	|	КОНЕЦ                                          КАК Приоритет,
	|	ЕСТЬNULL(ШтрихкодыНоменклатуры.Штрихкод, """") КАК GTIN,
	|	Продукция.Номенклатура                 КАК Номенклатура,
	|	Продукция.Характеристика               КАК Характеристика,
	|	Продукция.Серия                        КАК Серия,
	|	Продукция.Количество                   КАК Количество
	|ПОМЕСТИТЬ
	|	ВТПродукцияGTIN
	|ИЗ
	|	ВТМаркируемаяПродукция КАК Продукция
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Штрихкоды КАК ШтрихкодыНоменклатуры
	|		ПО Продукция.Номенклатура = ШтрихкодыНоменклатуры.Владелец
	|		 И Продукция.Характеристика = ШтрихкодыНоменклатуры.ХарактеристикаНоменклатуры
	|		 И Продукция.Серия = ШтрихкодыНоменклатуры.СерияНоменклатуры
	|		 И ШтрихкодыНоменклатуры.Качество = ЗНАЧЕНИЕ(Справочник.Качество.Новый)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2                              КАК Приоритет,
	|	ШтрихкодыНоменклатуры.Штрихкод КАК GTIN,
	|	Продукция.Номенклатура         КАК Номенклатура,
	|	Продукция.Характеристика       КАК Характеристика,
	|	Продукция.Серия                КАК Серия,
	|	Продукция.Количество           КАК Количество
	|ИЗ
	|	ВТМаркируемаяПродукция КАК Продукция
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Штрихкоды КАК ШтрихкодыНоменклатуры
	|		ПО Продукция.Номенклатура = ШтрихкодыНоменклатуры.Владелец
	|		 И Продукция.Характеристика = ШтрихкодыНоменклатуры.ХарактеристикаНоменклатуры
	|		 И ШтрихкодыНоменклатуры.СерияНоменклатуры = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		 И ШтрихкодыНоменклатуры.Качество = ЗНАЧЕНИЕ(Справочник.Качество.Новый)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Продукция.Номенклатура       КАК Номенклатура,
	|	Продукция.Характеристика     КАК Характеристика,
	|	Продукция.Серия              КАК Серия,
	|	МИНИМУМ(Продукция.Приоритет) КАК Приоритет
	|ПОМЕСТИТЬ
	|	Приоритеты
	|ИЗ
	|	ВТПродукцияGTIN КАК Продукция
	|СГРУППИРОВАТЬ ПО
	|	Продукция.Номенклатура,
	|	Продукция.Характеристика,
	|	Продукция.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Продукция.GTIN           КАК GTIN,
	|	Продукция.Номенклатура   КАК Номенклатура,
	|	Продукция.Характеристика КАК Характеристика,
	|	Продукция.Серия          КАК Серия,
	|	Продукция.Количество     КАК Количество
	|ИЗ
	|	ВТПродукцияGTIN КАК Продукция
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Приоритеты КАК Приоритеты
	|		ПО Продукция.Номенклатура = Приоритеты.Номенклатура
	|		 И Продукция.Характеристика = Приоритеты.Характеристика
	|		 И Продукция.Серия = Приоритеты.Серия
	|		 И Продукция.Приоритет = Приоритеты.Приоритет
	|ИТОГИ ПО
	|	Номенклатура,
	|	Характеристика,
	|	GTIN
	|", 
	УсловиеВидПродукции(ВидПродукции),
	Документ.Метаданные().Имя);
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция ЗапросМаркируемойПродукцииПоТаблице(ТаблицаТовары, ВидПродукции)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Товары", ТаблицаТовары);
	Запрос.УстановитьПараметр("ВидПродукции", ВидПродукции);
	Запрос.Текст = СтрШаблон(
	"ВЫБРАТЬ
	|	Товары.Номенклатура               КАК Номенклатура,
	|	Товары.ХарактеристикаНоменклатуры КАК Характеристика,
	|	Товары.СерияНоменклатуры          КАК Серия,
	|	Товары.Количество                 КАК Количество
	|ПОМЕСТИТЬ
	|	ВТТовары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура      КАК Номенклатура,
	|	Товары.Характеристика    КАК Характеристика,
	|	Товары.Серия             КАК Серия,
	|	СУММА(Товары.Количество) КАК Количество
	|ПОМЕСТИТЬ ВТПродукция
	|ИЗ
	|	ВТТовары КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	%1
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ШтрихкодыНоменклатуры.Штрихкод ЕСТЬ NULL
	|			ТОГДА 3
	|		ИНАЧЕ 1
	|	КОНЕЦ                                          КАК Приоритет,
	|	ЕСТЬNULL(ШтрихкодыНоменклатуры.Штрихкод, """") КАК GTIN,
	|	Продукция.Номенклатура                 КАК Номенклатура,
	|	Продукция.Характеристика               КАК Характеристика,
	|	Продукция.Серия                        КАК Серия,
	|	Продукция.Количество                   КАК Количество
	|ПОМЕСТИТЬ ВТПродукцияGTIN
	|ИЗ
	|	ВТПродукция КАК Продукция
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Штрихкоды КАК ШтрихкодыНоменклатуры
	|		ПО Продукция.Номенклатура = ШтрихкодыНоменклатуры.Владелец
	|		 И Продукция.Характеристика = ШтрихкодыНоменклатуры.ХарактеристикаНоменклатуры
	|		 И Продукция.Серия = ШтрихкодыНоменклатуры.СерияНоменклатуры
	|		 И ШтрихкодыНоменклатуры.Качество = ЗНАЧЕНИЕ(Справочник.Качество.Новый)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2                                КАК Приоритет,
	|	ШтрихкодыНоменклатуры.Штрихкод   КАК GTIN,
	|	Продукция.Номенклатура   КАК Номенклатура,
	|	Продукция.Характеристика КАК Характеристика,
	|	Продукция.Серия          КАК Серия,
	|	Продукция.Количество     КАК Количество
	|ИЗ
	|	ВТПродукция КАК Продукция
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Штрихкоды КАК ШтрихкодыНоменклатуры
	|		ПО Продукция.Номенклатура = ШтрихкодыНоменклатуры.Владелец
	|		 И Продукция.Характеристика = ШтрихкодыНоменклатуры.ХарактеристикаНоменклатуры
	|		 И ШтрихкодыНоменклатуры.СерияНоменклатуры = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		 И ШтрихкодыНоменклатуры.Качество = ЗНАЧЕНИЕ(Справочник.Качество.Новый)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Продукция.Номенклатура       КАК Номенклатура,
	|	Продукция.Характеристика     КАК Характеристика,
	|	Продукция.Серия              КАК Серия,
	|	МИНИМУМ(Продукция.Приоритет) КАК Приоритет
	|ПОМЕСТИТЬ
	|	ВТПриоритеты
	|ИЗ
	|	ВТПродукцияGTIN КАК Продукция
	|СГРУППИРОВАТЬ ПО
	|	Продукция.Номенклатура,
	|	Продукция.Характеристика,
	|	Продукция.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Продукция.GTIN           КАК GTIN,
	|	Продукция.Номенклатура   КАК Номенклатура,
	|	Продукция.Характеристика КАК Характеристика,
	|	Продукция.Серия          КАК Серия,
	|	Продукция.Количество     КАК Количество,
	|	Продукция.Номенклатура.ВидПродукцииИС КАК ВидПродукцииИС
	|ИЗ
	|	ВТПродукцияGTIN КАК Продукция
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПриоритеты КАК Приоритеты
	|		ПО Продукция.Номенклатура = Приоритеты.Номенклатура
	|		 И Продукция.Характеристика = Приоритеты.Характеристика
	|		 И Продукция.Серия = Приоритеты.Серия
	|		 И Продукция.Приоритет = Приоритеты.Приоритет
	|ИТОГИ ПО
	|	Номенклатура,
	|	Характеристика,
	|	GTIN
	|",
	УсловиеВидПродукции(ВидПродукции));
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция УсловиеВидПродукции(ВидПродукции)
	
	Если ТипЗнч(ВидПродукции) = Тип("ПеречислениеСсылка.ВидыПродукцииИС") Тогда
		Возврат "СправочникНоменклатура.ВидПродукцииИС = &ВидПродукции";
	Иначе 
		Возврат "(СправочникНоменклатура.ВидПродукцииИС <> ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПустаяСсылка) И СправочникНоменклатура.ВидПродукцииИС <> ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Алкогольная))
				|	И СправочникНоменклатура.ВидПродукцииИС Не В (ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Зерно), ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПродуктыПереработкиЗерна))";
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область РаботаСПрикладнымиДокументами

Процедура ЗаполнитьПризнакМаркируемаяПродукцияГосИС(Форма, ТабличнаяЧасть)
		//Соответствие[Номенклатура]->Булево[МаркируемаяПродукция], Булево(АвтоОСУ)
	    МаркируемаяПродукция = Новый Соответствие;
		МаркируемаяПродукция.Вставить(Справочники.Номенклатура.ПустаяСсылка(), Новый Структура("Маркируемая", Ложь));
		
		РезультатЗапроса = ЗапросМаркируемойПродукцииПоТаблице(ТабличнаяЧасть.Выгрузить(), Неопределено);
		ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаНоменклатура.Следующий() Цикл
			Если МаркируемаяПродукция.Получить(ВыборкаНоменклатура.Номенклатура) = Неопределено Тогда
				Реквизиты = Новый Структура("Маркируемая,АвтоОСУ", Истина, ИнтеграцияИСМПКлиентСерверПовтИсп.ВидПродукцииПоддерживаетОбъемноСортовойУчет(ВыборкаНоменклатура.ВидПродукцииИС));
				МаркируемаяПродукция.Вставить(ВыборкаНоменклатура.Номенклатура, Реквизиты);
			КонецЕсли;
		КонецЦикла;
				
		Форма.МаркируемаяПродукцияГосИС = МаркируемаяПродукция;
	
КонецПроцедуры

Процедура ПриСозданииНаСервере(Форма, ПараметрыИнтеграции) Экспорт
	
	Объект = Форма[ПараметрыИнтеграции.ИмяРеквизитаФормыОбъект];
	ДатаОбъекта = ?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДатаСеанса());
	ТабличнаяЧастьТовары = Объект[ПараметрыИнтеграции.ИмяТабличнойЧастиТовары];
	
	ИспользуетсяМаркируемаяПродукция = ПолучитьФункциональнуюОпцию("ВестиУчетМаркируемойПродукцииИСМП");
	
	ЕстьМаркируемаяПродукция = Ложь;
	ЕстьМаркируемаяПродукцияВКоллекции(ТабличнаяЧастьТовары, Неопределено, ЕстьМаркируемаяПродукция);
	ЕстьМаркируемаяПродукция = ЕстьМаркируемаяПродукция ИЛИ ПараметрыИнтеграции.ИспользоватьБезМаркируемойПродукции;
	
	Если НЕ ИспользуетсяМаркируемаяПродукция Тогда 
		Если ПараметрыИнтеграции.ИспользоватьКолонкуСтатусаПроверкиПодбора Тогда 
			Форма.ЭлементыФормы.Товары.Колонки.СтатусПроверкиПодбора.Видимость = Ложь;
		КонецЕсли;
			Если Форма.ЭлементыФормы.Найти("КоманднаяПанельТовары") <> Неопределено Тогда
				Форма.ЭлементыФормы.КоманднаяПанельТовары.Кнопки.Проверить.Кнопки.Удалить(
				Форма.ЭлементыФормы.КоманднаяПанельТовары.Кнопки.Проверить.Кнопки.МаркируемуюПродукцию);
			КонецЕсли;	
	КонецЕсли;	
		
	Если ПараметрыИнтеграции.ИспользоватьКолонкуСтатусаПроверкиПодбора Тогда
		
		ТаблицаМаркируемойПродукции = Новый ТаблицаЗначений;
		ТаблицаМаркируемойПродукции.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
		ТаблицаМаркируемойПродукции.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
		ТаблицаМаркируемойПродукции.Колонки.Добавить("Серия", Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
		ТаблицаМаркируемойПродукции.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число"));
		ТаблицаМаркируемойПродукции.Колонки.Добавить("БезКоличества", Новый ОписаниеТипов("Булево"));
		ТаблицаМаркируемойПродукции.Колонки.Добавить("Статус", Новый ОписаниеТипов("Число"));
		ТаблицаМаркируемойПродукции.Колонки.Добавить("ШтрихкодыУпаковок", Новый ОписаниеТипов("СписокЗначений"));
		
		Форма.ДанныеШтрихкодовУпаковокГосИС = ТаблицаМаркируемойПродукции;
		
		ЗаполнитьПризнакМаркируемаяПродукцияГосИС(Форма, Форма.ДокументОбъект.Товары);
		
		ЗаполнитьКешШтрихкодовУпаковок(Форма, ПараметрыИнтеграции);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПрименитьКешШтрихкодовУпаковок(Форма, ПараметрыИнтеграции, ОбновлениеТаблицыТоваров = Ложь) Экспорт

	Если НЕ ПараметрыИнтеграции.ИспользоватьКолонкуСтатусаПроверкиПодбора Тогда
		Возврат;
	КонецЕсли;
	
	Объект = ПараметрыИнтеграции.ИмяРеквизитаФормыОбъект;
	ТабличнаяЧастьТовары = Форма[Объект][ПараметрыИнтеграции.ИмяТабличнойЧастиТовары];
	
	ЗаполнитьПризнакМаркируемаяПродукцияГосИС(Форма, ТабличнаяЧастьТовары);
	
	СтруктураПоиска = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры,СерияНоменклатуры");

	Если ОбновлениеТаблицыТоваров Тогда
		ТабличнаяЧастьШтрихкоды = Форма.ДокументОбъект[ПараметрыИнтеграции.ИмяТабличнойЧастиШтрихкодыУпаковок];
		УдалитьСвязанныеШтрихкодыУпаковок = Новый Соответствие;
		УдалитьСтрокиШтрихкодов = Новый Массив;
		//Удалились товары
		Для Каждого СтрокаТовары Из Форма.ДанныеШтрихкодовУпаковокГосИС Цикл
			СтруктураПоиска.Номенклатура = СтрокаТовары.Номенклатура;
			СтруктураПоиска.ХарактеристикаНоменклатуры = СтрокаТовары.Характеристика;
			СтруктураПоиска.СерияНоменклатуры = СтрокаТовары.Серия;
			СтрокиТовары = ТабличнаяЧастьТовары.НайтиСтроки(СтруктураПоиска);
			Если СтрокиТовары.Количество() = 0 Тогда
				Для Каждого ЭлементСписка Из СтрокаТовары.ШтрихкодыУпаковок Цикл
					УдалитьСвязанныеШтрихкодыУпаковок.Вставить(ЭлементСписка.Значение, Истина);
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;

		Если (УдалитьСвязанныеШтрихкодыУпаковок.Количество()) Тогда
			//Удалим все упаковки верхнего уровня где они есть из ТЧ
			Для Каждого СтрокаШтрихкоды Из ТабличнаяЧастьШтрихкоды Цикл
				Если УдалитьСвязанныеШтрихкодыУпаковок.Получить(СтрокаШтрихкоды[ПараметрыИнтеграции.ИмяКолонкиШтрихкодУпаковки])=Истина Тогда
					УдалитьСтрокиШтрихкодов.Добавить(СтрокаШтрихкоды);
				КонецЕсли;
			КонецЦикла;
			Для Каждого СтрокаУдалить Из УдалитьСтрокиШтрихкодов Цикл
				ТабличнаяЧастьШтрихкоды.Удалить(СтрокаУдалить);
			КонецЦикла;
			ЗаполнитьКешШтрихкодовУпаковок(Форма, Форма.ПараметрыИнтеграцииФормыПроверкиИСМП());
		КонецЕсли;
		
	КонецЕсли;
	 
	Для Каждого СтрокаТовары Из ТабличнаяЧастьТовары Цикл
		Реквизиты = Форма.МаркируемаяПродукцияГосИС.Получить(СтрокаТовары.Номенклатура);
		Если Реквизиты = Неопределено Тогда
			//немаркируемая
		Иначе 
			ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаТовары);
			СтрокиТовары = ТабличнаяЧастьТовары.НайтиСтроки(СтруктураПоиска);
			ПоискКеша =	Новый Структура("Номенклатура, Характеристика, Серия",
					СтруктураПоиска.Номенклатура,
					СтруктураПоиска.ХарактеристикаНоменклатуры,
					СтруктураПоиска.СерияНоменклатуры);
			СтрокиКеша = Форма.ДанныеШтрихкодовУпаковокГосИС.НайтиСтроки(ПоискКеша);
			КоличествоПоКлючу = 0;
			Для Каждого СтрокаПоКлючу Из СтрокиТовары Цикл
				КоличествоПоКлючу = КоличествоПоКлючу + СтрокаПоКлючу.Количество * СтрокаПоКлючу.Коэффициент;
			КонецЦикла;
			
			Если СтрокиКеша.Количество() = 0 Тогда
				Если Реквизиты.Свойство("АвтоОСУ") И Реквизиты.АвтоОСУ Тогда
					//автоОСУ
				Иначе 
					НоваяСтрокаКеша = Форма.ДанныеШтрихкодовУпаковокГосИС.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрокаКеша,ПоискКеша); 
					НоваяСтрокаКеша.Статус = 2;
				КонецЕсли;
			ИначеЕсли СтрокиКеша[0].БезКоличества Тогда
				СтрокиКеша[0].Статус = 3;
			ИначеЕсли КоличествоПоКлючу = СтрокиКеша[0].Количество Тогда
				СтрокиКеша[0].Статус = 1;
			ИначеЕсли СтрокиКеша[0].Количество = 0 Тогда
				СтрокиКеша[0].Статус = 2;
			Иначе
				Описание = РегистрыСведений.ОписаниеНоменклатурыИС.ПолучитьОписание(СтруктураПоиска.Номенклатура).Получить(СтруктураПоиска.Номенклатура);
				Если Описание.Количество() > 0 И ЗначениеЗаполнено(Описание.Номенклатура) Тогда
					// частичное выбытие
					КоэффициентУпаковки = (СтрокиКеша[0].Количество/?(КоличествоПоКлючу=0,1,КоличествоПоКлючу));
					Если Окр(СтрокиКеша[0].Количество/КоэффициентУпаковки, 3) = КоличествоПоКлючу Тогда
						СтрокиКеша[0].Количество = КоличествоПоКлючу;
						СтрокиКеша[0].Статус = 1;
					КонецЕсли;
				Иначе
					СтрокиКеша[0].Статус = 2;
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	
КонецПроцедуры

Функция ПрименитьКешПоСтроке(Форма, ПараметрыИнтеграцииФормыПроверки, ДанныеСтроки, ДанныеКешаСтроки) Экспорт
	
	Если ДанныеКешаСтроки = Неопределено Тогда
		ДанныеКешаСтроки = Новый Структура;
		ДанныеКешаСтроки.Вставить("Номенклатура", Справочники.Номенклатура.ПустаяСсылка());
		ДанныеКешаСтроки.Вставить("ХарактеристикаНоменклатуры", Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
		ДанныеКешаСтроки.Вставить("СерияНоменклатуры", Справочники.СерииНоменклатуры.ПустаяСсылка());
		ДанныеКешаСтроки.Вставить("МаркируемаяПродукцияГосИС", Ложь);
	КонецЕсли;
	
	КлючСвязиИзменен = 
			(ДанныеСтроки.Номенклатура<>ДанныеКешаСтроки.Номенклатура
			ИЛИ ДанныеСтроки.ХарактеристикаНоменклатуры<>ДанныеКешаСтроки.ХарактеристикаНоменклатуры
			ИЛИ ДанныеСтроки.СерияНоменклатуры<>ДанныеКешаСтроки.СерияНоменклатуры)
		И ДанныеКешаСтроки.МаркируемаяПродукцияГосИС;
	
	Объект = ПараметрыИнтеграцииФормыПроверки.ИмяРеквизитаФормыОбъект;
	ТабличнаяЧастьТовары = Форма[Объект][ПараметрыИнтеграцииФормыПроверки.ИмяТабличнойЧастиТовары];
	
	СтруктураПоиска = Новый Структура("Номенклатура,Характеристика,Серия");
    СтруктураПоискаТЧ = Новый Структура("Номенклатура, ХарактеристикаНоменклатуры, СерияНоменклатуры");
	
	Если КлючСвязиИзменен Тогда
		СтруктураПоиска.Номенклатура = ДанныеКешаСтроки.Номенклатура;
		СтруктураПоиска.Характеристика = ДанныеКешаСтроки.ХарактеристикаНоменклатуры;
		СтруктураПоиска.Серия = ДанныеКешаСтроки.СерияНоменклатуры;
		СтрокиКеша = Форма.ДанныеШтрихкодовУпаковокГосИС.НайтиСтроки(СтруктураПоиска);
		Если СтрокиКеша.Количество() Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если Форма.МаркируемаяПродукцияГосИС.Получить(ДанныеСтроки.Номенклатура).Маркируемая Тогда
		
		СтруктураПоиска.Номенклатура = ДанныеСтроки.Номенклатура;
		СтруктураПоиска.Характеристика = ДанныеСтроки.ХарактеристикаНоменклатуры;
		СтруктураПоиска.Серия = ДанныеСтроки.СерияНоменклатуры;
		ЗаполнитьЗначенияСвойств(СтруктураПоискаТЧ, ДанныеСтроки);
		СтрокиКеша = Форма.ДанныеШтрихкодовУпаковокГосИС.НайтиСтроки(СтруктураПоиска);
		СтрокиТовары = ТабличнаяЧастьТовары.НайтиСтроки(СтруктураПоискаТЧ);
		КоличествоПоКлючу = 0;
		Для Каждого СтрокаПоКлючу Из СтрокиТовары Цикл
			КоличествоПоКлючу = КоличествоПоКлючу + СтрокаПоКлючу.Количество * СтрокаПоКлючу.Коэффициент;
		КонецЦикла;
		Если СтрокиКеша.Количество() = 0 Тогда
		ИначеЕсли СтрокиКеша[0].БезКоличества Тогда
			СтрокиКеша[0].Статус = 3;
		ИначеЕсли КоличествоПоКлючу = СтрокиКеша[0].Количество Тогда
			СтрокиКеша[0].Статус = 1;
		Иначе
			СтрокиКеша[0].Статус = 2;
		КонецЕсли;
	Иначе
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Процедура ЗаполнитьКешШтрихкодовУпаковок(Форма, ПараметрыИнтеграцииФормыПроверки) Экспорт
	
	Если НЕ ПараметрыИнтеграцииФормыПроверки.ИспользоватьКолонкуСтатусаПроверкиПодбора Тогда
		Возврат;
	КонецЕсли;
	
	Форма.ДанныеШтрихкодовУпаковокГосИС.Очистить();
	ДанныеШтрихкодовУпаковокГосИС = Форма.ДанныеШтрихкодовУпаковокГосИС;
	ДанныеШтрихкодовУпаковокГосИС.Индексы.Добавить("Номенклатура,Характеристика,Серия");
	
	Объект = ПараметрыИнтеграцииФормыПроверки.ИмяРеквизитаФормыОбъект;
	ШтрихкодыУпаковок = Форма[Объект][ПараметрыИнтеграцииФормыПроверки.ИмяТабличнойЧастиШтрихкодыУпаковок].Выгрузить(); 
	ШтрихкодыУпаковокИсходнаяТаблица = ШтрихкодыУпаковок.Скопировать();
	
	// Частичное выбытие: строки, где указано количество в табличной части штрихкодов, обрабатываем индивидуально
	Если ПараметрыИнтеграцииФормыПроверки.ДоступноЧастичноеВыбытие Тогда
		СтрокиЧастичногоВыбытия = ШтрихкодыУпаковокИсходнаяТаблица.СкопироватьКолонки();
		Для Каждого СтрокаШтрихкод Из ШтрихкодыУпаковокИсходнаяТаблица Цикл
			Если ЗначениеЗаполнено(СтрокаШтрихкод.ЧастичноеВыбытиеКоличество) Тогда
				ЗаполнитьЗначенияСвойств(СтрокиЧастичногоВыбытия.Добавить(), СтрокаШтрихкод);
			КонецЕсли;
		КонецЦикла;
		ШтрихкодыУпаковокИсходнаяТаблица = ШтрихкодыУпаковокИсходнаяТаблица.Скопировать(Новый Структура("ЧастичноеВыбытиеКоличество", 0));
	КонецЕсли;
	
	ШтрихкодыУпаковок = ШтрихкодыУпаковокИсходнаяТаблица.ВыгрузитьКолонку(ПараметрыИнтеграцииФормыПроверки.ИмяКолонкиШтрихкодУпаковки);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ШтрихкодыУпаковок.Ссылка КАК Штрихкод,
	|	ШтрихкодыУпаковок.Номенклатура,
	|	ШтрихкодыУпаковок.Характеристика,
	|	ШтрихкодыУпаковок.Серия,
	|	ШтрихкодыУпаковок.Количество,
	|	ВЫБОР
	|		КОГДА НЕ (&ПроизвольнаяЕдиницаУчета)
	|			ТОГДА 0
	|			ИНАЧЕ ШтрихкодыУпаковок.КоличествоПотребительскихУпаковок
	|		КОНЕЦ КАК КоличествоПотребительскихУпаковок,
	|	ШтрихкодыУпаковок.ЗначениеШтрихкода,
	|	ЛОЖЬ КАК ПодменнаяНоменклатура
	|ИЗ
	|	Справочник.ШтрихкодыУпаковокТоваров КАК ШтрихкодыУпаковок
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ШтрихкодыУпаковокТоваров.ВложенныеШтрихкоды КАК ЕстьВложенныеШтрихкоды
	|		ПО ЕстьВложенныеШтрихкоды.Ссылка = ШтрихкодыУпаковок.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОписаниеНоменклатурыИС КАК ОписаниеНоменклатурыИС
	|		ПО &ТоварыОписаниеНоменклатурыИС
	|ГДЕ
	|	ШтрихкодыУпаковок.Ссылка В (&ШтрихкодУпаковки)
	|	И ЕстьВложенныеШтрихкоды.Ссылка ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ШтрихкодыУпаковок.Ссылка КАК Родитель,
	|	ШтрихкодыУпаковок.Штрихкод
	|ИЗ
	|	Справочник.ШтрихкодыУпаковокТоваров.ВложенныеШтрихкоды КАК ШтрихкодыУпаковок
	|ГДЕ
	|	ШтрихкодыУпаковок.Ссылка В (&ШтрихкодУпаковки)
	|";
	ИнтеграцияИС.ОбновитьТекстЗапросаСРегистромОписаниеНоменклатурыИС(ТекстЗапроса, "ОписаниеНоменклатурыИС", "ШтрихкодыУпаковок.Номенклатура");
	Запрос = Новый Запрос(ТекстЗапроса);
	
	КешВложенности = Новый Соответствие;
	СтруктураПоиска = Новый Структура("Номенклатура,Характеристика,Серия");
	
	ОбходТаблицы = Истина;
	
	Пока ОбходТаблицы Цикл
		
		Запрос.УстановитьПараметр("ШтрихкодУпаковки", ШтрихкодыУпаковок);
		МассивРезультатов = Запрос.ВыполнитьПакет();
		СоставУпаковки = МассивРезультатов[0].Выбрать();
		ВложенныеЗаписи = МассивРезультатов[1].Выбрать();
		НуженОбходДочерних = ВложенныеЗаписи.Количество();
	
		Пока СоставУпаковки.Следующий() Цикл
			ИсходныйШтрихкод = КешВложенности.Получить(СоставУпаковки.Штрихкод);
			Если ИсходныйШтрихкод = Неопределено Тогда
				ИсходныйШтрихкод = СоставУпаковки.Штрихкод;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(СтруктураПоиска, СоставУпаковки);
			СтрокиКеша = ДанныеШтрихкодовУпаковокГосИС.НайтиСтроки(СтруктураПоиска);
			Если СтрокиКеша.Количество() Тогда
				Если СоставУпаковки.Количество = 0 И СоставУпаковки.КоличествоПотребительскихУпаковок <> 0 Тогда
					СтрокиКеша[0].БезКоличества = Истина;
				КонецЕсли;
				СтрокиКеша[0].Количество = СтрокиКеша[0].Количество + ?(СоставУпаковки.Количество=0,1,СоставУпаковки.Количество);
				СтрокиКеша[0].ШтрихкодыУпаковок.Добавить(ИсходныйШтрихкод);
			Иначе
				НоваяСтрока = ДанныеШтрихкодовУпаковокГосИС.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СоставУпаковки);
				Если (НоваяСтрока.Количество=0) Тогда
					НоваяСтрока.Количество=1;
					Если СоставУпаковки.КоличествоПотребительскихУпаковок <> 0 Тогда
						НоваяСтрока.БезКоличества = Истина;
					КонецЕсли;
				КонецЕсли;
				НоваяСтрока.ШтрихкодыУпаковок = Новый СписокЗначений;
				НоваяСтрока.ШтрихкодыУпаковок.Добавить(ИсходныйШтрихкод);
			КонецЕсли;
		КонецЦикла;
		
		Если НуженОбходДочерних Тогда
			ШтрихкодыУпаковок = Новый Массив;
			Пока ВложенныеЗаписи.Следующий() Цикл
				ИсходныйШтрихкод = КешВложенности.Получить(ВложенныеЗаписи.Родитель);
				Если ИсходныйШтрихкод = Неопределено Тогда
					ИсходныйШтрихкод = ВложенныеЗаписи.Родитель;
				КонецЕсли;
				КешВложенности.Вставить(ВложенныеЗаписи.Штрихкод, ИсходныйШтрихкод);
				ШтрихкодыУпаковок.Добавить(ВложенныеЗаписи.Штрихкод);
			КонецЦикла;
		Иначе
			ОбходТаблицы = Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
	// Обработка строк с частичным выбытием
	Если ПараметрыИнтеграцииФормыПроверки.ДоступноЧастичноеВыбытие И СтрокиЧастичногоВыбытия.Количество() > 0 Тогда
		Запрос.УстановитьПараметр("ШтрихкодыУпаковки", СтрокиЧастичногоВыбытия);
		Запрос.УстановитьПараметр("ПустаяСерия",       ИнтеграцияИС.ПустоеЗначениеОпределяемогоТипа("СерияНоменклатуры"));
		Запрос.Текст = СтрШаблон(
		"ВЫБРАТЬ
		|	Штрихкоды.%1                             КАК ШтрихкодУпаковки,
		|	Штрихкоды.ЧастичноеВыбытиеКоличество     КАК Количество,
		|	Штрихкоды.ЧастичноеВыбытиеВариантУчета   КАК ВариантУчета,
		|	Штрихкоды.ЧастичноеВыбытиеНоменклатура   КАК НоменклатураЧастичногоВыбытия,
		|	Штрихкоды.ЧастичноеВыбытиеХарактеристика КАК ХарактеристикаЧастичногоВыбытия
		|ПОМЕСТИТЬ ЧастичноеВыбытие
		|ИЗ
		|	&ШтрихкодыУпаковки КАК Штрихкоды
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Штрихкоды.Ссылка КАК Штрихкод,
		|	ВЫБОР
		|		КОГДА ЧастичноеВыбытие.ВариантУчета = ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаЧастичногоВыбытияИС.НастроеннаяНоменклатура)
		|			ТОГДА ЧастичноеВыбытие.НоменклатураЧастичногоВыбытия
		|		ИНАЧЕ Штрихкоды.Номенклатура
		|	КОНЕЦ КАК Номенклатура,
		|	ВЫБОР
		|		КОГДА ЧастичноеВыбытие.ВариантУчета = ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаЧастичногоВыбытияИС.НастроеннаяНоменклатура)
		|			ТОГДА ЧастичноеВыбытие.ХарактеристикаЧастичногоВыбытия
		|		ИНАЧЕ Штрихкоды.Характеристика
		|	КОНЕЦ КАК Характеристика,
		|	ВЫБОР
		|		КОГДА ЧастичноеВыбытие.ВариантУчета = ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаЧастичногоВыбытияИС.НастроеннаяНоменклатура)
		|			ТОГДА &ПустаяСерия
		|		ИНАЧЕ Штрихкоды.Серия
		|	КОНЕЦ КАК Серия,
		|	ВЫБОР
		|		КОГДА ЧастичноеВыбытие.ВариантУчета = ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаЧастичногоВыбытияИС.НастроеннаяНоменклатура)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ПодменнаяНоменклатура,
		|	ЧастичноеВыбытие.Количество КАК Количество,
		|	Штрихкоды.ЗначениеШтрихкода КАК ЗначениеШтрихкода
		|ИЗ
		|	ЧастичноеВыбытие КАК ЧастичноеВыбытие
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ШтрихкодыУпаковокТоваров КАК Штрихкоды
		|		ПО Штрихкоды.Ссылка = ЧастичноеВыбытие.ШтрихкодУпаковки
		|", ПараметрыИнтеграцииФормыПроверки.ИмяКолонкиШтрихкодУпаковки);
		СоставУпаковки = Запрос.Выполнить().Выбрать();
		Пока СоставУпаковки.Следующий() Цикл
			
			ЗаполнитьЗначенияСвойств(СтруктураПоиска, СоставУпаковки);
			СтрокиКеша = ДанныеШтрихкодовУпаковокГосИС.НайтиСтроки(СтруктураПоиска);
			
			Если СтрокиКеша.Количество() Тогда
				СтрокиКеша0 = СтрокиКеша[0];
				СтрокиКеша0.Количество = СтрокиКеша0.Количество + СоставУпаковки.Количество;
				СтрокиКеша0.ШтрихкодыУпаковок.Добавить(СоставУпаковки.Штрихкод, Истина);
			Иначе
				НоваяСтрока = ДанныеШтрихкодовУпаковокГосИС.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СоставУпаковки);
				НоваяСтрока.ШтрихкодыУпаковок = Новый Соответствие;
				НоваяСтрока.ШтрихкодыУпаковок.Добавить(СоставУпаковки.Штрихкод, Истина);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Форма.ДанныеШтрихкодовУпаковокГосИС = ДанныеШтрихкодовУпаковокГосИС;
	
КонецПроцедуры

// Для связи механизма штрихкодирования и кеша строк: обновление кеша по добавленному коду маркировки.
//   Прикладные документы (без специфики без заполнения табличной части).
//
// Параметры:
//   Форма             - ФормаКлиентскогоПриложения           - источник вызова.
//   ОбновляемаяСтрока - (См. ДанныеШтрихкодаДляДобавленияВКеш)
//
Процедура ДополнитьКеш(Форма, ОбновляемаяСтрока) Экспорт
	
	ДанныеШтрихкодовУпаковокГосИС = Форма.ДанныеШтрихкодовУпаковокГосИС;
	
	// Дополнительные ключи связи, специфика: поля поиска для незаполненной номенклатуры.
	ПоляПоиска = ИнтеграцияИС.ПоляПоискаМаркируемойПродукции();
	
	ЗаполнитьЗначенияСвойств(ПоляПоиска, ОбновляемаяСтрока);
	Если ОбновляемаяСтрока.ЧастичноеВыбытиеОтдельнойНоменклатуры Тогда
		ПоляПоиска.Серия = ИнтеграцияИС.ПустоеЗначениеОпределяемогоТипа("СерияНоменклатуры");
	КонецЕсли;
	
	ДанныеКеша = ДанныеШтрихкодовУпаковокГосИС.НайтиСтроки(ПоляПоиска);
	Если ДанныеКеша.Количество() Тогда
		СтрокаКеша = ДанныеКеша[0];
		СтрокаКеша.Количество = СтрокаКеша.Количество + ОбновляемаяСтрока.Количество;
		Если СтрокаКеша.ШтрихкодыУпаковок.НайтиПоЗначению(ОбновляемаяСтрока.ШтрихкодУпаковки) = Неопределено Тогда
			СтрокаКеша.ШтрихкодыУпаковок.Добавить(ОбновляемаяСтрока.ШтрихкодУпаковки, Истина);
		КонецЕсли;
	Иначе
		СтрокаКеша = ДанныеШтрихкодовУпаковокГосИС.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаКеша, ОбновляемаяСтрока);
		Если ОбновляемаяСтрока.ЧастичноеВыбытиеОтдельнойНоменклатуры Тогда
			СтрокаКеша.ПодменнаяНоменклатура = Истина;
		КонецЕсли;
		Штрихкоды = Новый СписокЗначений;
		Штрихкоды.Добавить(ОбновляемаяСтрока.ШтрихкодУпаковки, Истина);
		СтрокаКеша.ШтрихкодыУпаковок = Штрихкоды;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
