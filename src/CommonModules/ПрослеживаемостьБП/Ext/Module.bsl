
#Область ПрограммныйИнтерфейс

// Возвращает дату до которой необходимо отправить уведомление о ввозе/вывозе в налоговую инспекцию
// Параметры:
// Дата - тип дата, в данном параметре передается
//        дата поступления прослеживаемых товаров
// Возвращаемое значение:
// СрокУведомления - тип дата, день до которого нужно отправить уведомление о ввозе
Функция СрокПодачиУведомления(Дата) Экспорт
	
	//КоличествоРабочихДнейОтсрочки = 5;
	//День = 24 * 60 * 60; // количество секунд в одном дне

	//ПроизводственныйКалендарь = КалендарныеГрафики.ОсновнойПроизводственныйКалендарь();
	//
	//Если НЕ ЗначениеЗаполнено(ПроизводственныйКалендарь) Тогда
	//	Возврат Дата + КоличествоРабочихДнейОтсрочки * День;
	//КонецЕсли;
	//
	//Отбор = Новый Структура;
	//Отбор.Вставить("Год",                       Год(Дата));
	//Отбор.Вставить("Дата",                      Дата);
	//Отбор.Вставить("ПроизводственныйКалендарь", ПроизводственныйКалендарь);
	//
	//ДанныеДня = РегистрыСведений.ДанныеПроизводственногоКалендаря.Получить(Отбор);
	//
	//ДатаДокументаЭтоВыходной = (ДанныеДня.ВидДня <> Перечисления.ВидыДнейПроизводственногоКалендаря.Рабочий И 
	//							ДанныеДня.ВидДня <> Перечисления.ВидыДнейПроизводственногоКалендаря.Предпраздничный);
	//
	//Если ДатаДокументаЭтоВыходной Тогда
	//	// Отсрочка меньше на один день
	//	СрокУведомления = КалендарныеГрафики.ДатаПоКалендарю(
	//		ПроизводственныйКалендарь, Дата, КоличествоРабочихДнейОтсрочки - 1, Ложь);
	//Иначе
	//	СрокУведомления = КалендарныеГрафики.ДатаПоКалендарю(
	//		ПроизводственныйКалендарь, Дата, КоличествоРабочихДнейОтсрочки, Ложь);
	//КонецЕсли;
	//
	//Если НЕ ЗначениеЗаполнено(СрокУведомления) Тогда
	//	Возврат Дата + КоличествоРабочихДнейОтсрочки * День;
	//Иначе
	//	Возврат СрокУведомления;
	//КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПодобратьРНПТ(ДокументОбъект, Отказ, ИмяТабличнойЧасти = "Товары") Экспорт
	
	ТаблицаТоваров = ДокументОбъект[ИмяТабличнойЧасти].Выгрузить();
	
	Если ТаблицаТоваров.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаРНПТИсходныйДокумент = ТаблицаРНПТИсходныйДокумент(ДокументОбъект, ТаблицаТоваров);
	ТипДокумента = ТипЗнч(ДокументОбъект);

	Если ТипДокумента = Тип("ДокументОбъект.ВозвратТоваровОтПокупателя") Тогда
		ВидДоговора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОбъект.ДоговорКонтрагента, "ВидДоговора");
		Если ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером Тогда
			ТаблицаРНПТОстатки = ТаблицаРНПТОстатки(ДокументОбъект, ТаблицаТоваров, Истина);
		Иначе
			ТаблицаРНПТОстатки = ТаблицаРНПТОстаткиВозвратОпт(ДокументОбъект, ТаблицаТоваров, ДокументОбъект.Сделка);
		КонецЕсли;
	Иначе
		ТаблицаРНПТОстатки = ТаблицаРНПТОстатки(ДокументОбъект, ТаблицаТоваров, Ложь);
	КонецЕсли;
	
	ЭтоВыпускПродукции = Ложь;
	
	ЭтоВозвратКомитенту = Ложь;
	
	Если ТипДокумента = Тип("ДокументОбъект.ВозвратТоваровПоставщику") Тогда
		ВидДоговора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОбъект.ДоговорКонтрагента, "ВидДоговора");
		Если ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом Тогда
			ЭтоВозвратКомитенту = Истина;
		КонецЕсли;
	КонецЕсли;
	
	//КонтролироватьОстаток = НЕ БухгалтерскийУчетПереопределяемый..ОтключитьКонтрольОтрицательныхОстатков();
	Для Каждого СтрокаТабличнойЧасти ИЗ ДокументОбъект[ИмяТабличнойЧасти] Цикл
		
		Если НЕ СтрокаТабличнойЧасти.ПрослеживаемыйТовар Тогда
			Продолжить;
		КонецЕсли;
		
		Если (ТипДокумента = Тип("ДокументОбъект.РеализацияТоваровУслуг")) Или (ТипДокумента = Тип("ДокументОбъект.ОтчетОРозничныхПродажах")) Или (ТипДокумента = Тип("ДокументОбъект.СписаниеТоваров")) 
			Или (ТипДокумента = Тип("ДокументОбъект.СписаниеТоваров")) Тогда
			
			Если Не СтрокаТабличнойЧасти.АвтоподборРНПТ Тогда
				Продолжить;
			КонецЕсли;
			
		КонецЕсли;
		
		ОсталосьПодобрать = СтрокаТабличнойЧасти.Количество;
		
		Если (СтрокаТабличнойЧасти.ИдентификаторСтроки = "0") Или (ПустаяСтрока(СтрокаТабличнойЧасти.ИдентификаторСтроки)) Тогда
			СтрокаТабличнойЧасти.ИдентификаторСтроки = Строка(Новый УникальныйИдентификатор);
		КонецЕсли;
		
		СтрокиСРНПТ = ДокументОбъект.СведенияПрослеживаемости.НайтиСтроки(Новый Структура("ИдентификаторСтроки", СтрокаТабличнойЧасти.ИдентификаторСтроки));
		КоличествоРНПТ = СтрокиСРНПТ.Количество();
		
		Если КоличествоРНПТ = 0 Тогда
			
			// Для корректировочного документа сначала подберем РНПТ из исходного документа.
			ПодобратьРНПТИзИсходногоДокумента(ДокументОбъект, ОсталосьПодобрать, СтрокаТабличнойЧасти, ТаблицаРНПТИсходныйДокумент);
			
			Если ОсталосьПодобрать <> 0 Тогда
				// Добавляем РНПТ из остатков.
				ПодобратьРНПТИзОстатков(ДокументОбъект, ОсталосьПодобрать, СтрокаТабличнойЧасти, ТаблицаРНПТОстатки);
			КонецЕсли;
			
			Если ОсталосьПодобрать <> 0 Тогда
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Для целей учета прослеживаемости не списано %1 товара %2 (%3)'"),
					Формат(ОсталосьПодобрать, "ЧЦ=15; ЧДЦ=3"),
					СтрокаТабличнойЧасти.Номенклатура,
					СтрокаТабличнойЧасти.СтранаПроисхождения);
				Поле = "" + ИмяТабличнойЧасти + "["+ Формат(СтрокаТабличнойЧасти.НомерСтроки-1,"ЧГ=") + "].Количество";
					
				ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ДокументОбъект.Ссылка, Поле, "Объект", Отказ);
			КонецЕсли;

		Иначе
			
			// Проверяем наличие указанных РНПТ на остатках
			Для Каждого СтрокаСРНПТ ИЗ СтрокиСРНПТ Цикл
				ТребуемоеКоличество = СтрокаСРНПТ.Количество;
				ТребуемоеКоличествоПрослеживаемости = СтрокаСРНПТ.КоличествоПрослеживаемости;
				
				// Сверка РНПТ с исходным документом реализации.
				СверитьОстаткиСИсходнымДокументомРеализации(СтрокаТабличнойЧасти, ТаблицаРНПТИсходныйДокумент, ТребуемоеКоличество, ТребуемоеКоличествоПрослеживаемости);
				
				Если ТребуемоеКоличество = 0
					И ТребуемоеКоличествоПрослеживаемости = 0 Тогда
					// Значит в строке корректировочного документа только РНПТ из исходного документа реализации.
					Продолжить;
				КонецЕсли;
				
				// По новым РНПТ контролируем остатки в регистре.
				СверитьСОстаткамиВПрослеживаемости(ДокументОбъект, Отказ, СтрокаСРНПТ, СтрокаТабличнойЧасти, ТаблицаРНПТОстатки, ИмяТабличнойЧасти, ЭтоВыпускПродукции,,ЭтоВозвратКомитенту);
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
	ДокументОбъект.СведенияПрослеживаемости.Свернуть("РНПТ, ИдентификаторСтроки", "Количество, КоличествоПрослеживаемости, Сумма");
	
КонецПроцедуры

Процедура СформироватьДвиженияРеализацияТоваров(ТаблицаТовары, ТаблицаОперации, ТаблицаРеквизиты, Движения) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаРеквизиты) Тогда
		Возврат;
	КонецЕсли;
	
	ОбщиеПараметры = ПодготовитьОбщиеПараметры(ТаблицаРеквизиты);
	Реквизиты = ОбщиеПараметры.Реквизиты[0];
	
	СформироватьДвиженияПрослеживаемыхТоваров(ТаблицаТовары, Реквизиты, Движения, Истина);
		
	СформироватьДвиженияОперацииСПрослеживаемымиТоварами(ТаблицаОперации, Реквизиты, Движения);
	
КонецПроцедуры

Процедура СформироватьДвиженияКорректировкаРеализацииТоваров(ТаблицаТоварыУвеличение, ТаблицаТоварыУменьшение, ТаблицаОперации, ТаблицаРеквизиты, Движения) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаРеквизиты) Тогда
		Возврат;
	КонецЕсли;
	
	ОбщиеПараметры = ПодготовитьОбщиеПараметры(ТаблицаРеквизиты);
	Реквизиты = ОбщиеПараметры.Реквизиты[0];
	
	СформироватьДвиженияПрослеживаемыхТоваров(ТаблицаТоварыУвеличение, Реквизиты, Движения, Истина);
	СформироватьДвиженияПрослеживаемыхТоваров(ТаблицаТоварыУменьшение, Реквизиты, Движения, Ложь);
	
	СформироватьДвиженияОперацииСПрослеживаемымиТоварами(ТаблицаОперации, Реквизиты, Движения);
	
КонецПроцедуры

Процедура СформироватьДвиженияКорректировкаПоступленияТоваров(ТаблицаТоварыУвеличение, ТаблицаТоварыУменьшение, ТаблицаОперации, ТаблицаРеквизиты, Движения) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаРеквизиты) Тогда
		Возврат;
	КонецЕсли;
	
	ОбщиеПараметры = ПодготовитьОбщиеПараметры(ТаблицаРеквизиты);
	Реквизиты = ОбщиеПараметры.Реквизиты[0];
	
	СформироватьДвиженияПрослеживаемыхТоваров(ТаблицаТоварыУвеличение, Реквизиты, Движения, Ложь);
	СформироватьДвиженияПрослеживаемыхТоваров(ТаблицаТоварыУменьшение, Реквизиты, Движения, Истина);
	
	СформироватьДвиженияОперацииСПрослеживаемымиТоварами(ТаблицаОперации, Реквизиты, Движения);
	
КонецПроцедуры

Процедура СформироватьДвиженияПоступлениеТоваров(ТаблицаТовары, ТаблицаОперации, ТаблицаРНПТ, ТаблицаРеквизиты, Движения) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаРеквизиты) Тогда
		Возврат;
	КонецЕсли;
	
	ОбщиеПараметры = ПодготовитьОбщиеПараметры(ТаблицаРеквизиты);
	Реквизиты = ОбщиеПараметры.Реквизиты[0];
	
	СформироватьДвиженияПрослеживаемыхТоваров(ТаблицаТовары, Реквизиты, Движения);
	
	СформироватьДвиженияОперацииСПрослеживаемымиТоварами(ТаблицаОперации, Реквизиты, Движения);
	
	УстановитьСвойстваРНПТ(ТаблицаРНПТ, Реквизиты);
	
КонецПроцедуры

Процедура СформироватьДвиженияОперацииСПрослеживаемымиТоварами(ТаблицаОперации, Реквизиты, Движения) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаОперации) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыОперацииСПрослеживаемымиТоварами(ТаблицаОперации);
	
	Для Каждого СтрокаТаблицы Из Параметры.Операции Цикл
		Запись = Движения.ОперацииСПрослеживаемымиТоварами.Добавить();
		
		Запись.Период = Реквизиты.Дата;
		
		ЗаполнитьЗначенияСвойств(Запись, Реквизиты);
		ЗаполнитьЗначенияСвойств(Запись, СтрокаТаблицы);
		Если СтрокаТаблицы.ОтражениеВОтчетности <> Перечисления.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ОтчетОбОперациях Тогда
			Запись.СуммаБезНДС = Окр(Запись.СуммаБезНДС, 0);
		КонецЕсли;
	КонецЦикла;
	
	Движения.ОперацииСПрослеживаемымиТоварами.Записывать = Истина;

КонецПроцедуры

Функция ПодготовитьОбщиеПараметры(ТаблицаРеквизиты) Экспорт
	
	Параметры = Новый Структура("Реквизиты", Неопределено);
	
	Если Не ЗначениеЗаполнено(ТаблицаРеквизиты) Тогда
		Возврат Параметры;
	КонецЕсли;
	
	СписокОбязательныхКолонок = ""
	+ "Дата,"      // <Дата>
	+ "Ссылка," // <ДокументСсылка>
	+ "Организация";// <СправочникСсылка.Организации>
	
	// Реквизиты "Страна регистрации" и "Контрагент" будут только в случаях, если движения формируются к документам:
	// - Поступление товаров и услуг
	// - Реализация товаров и услуг
	// в остальных случаях эти реквизиты отсутствуют. 
	Если ТаблицаРеквизиты.Колонки.Найти("СтранаРегистрации") <> Неопределено Тогда 
		СписокОбязательныхКолонок = СписокОбязательныхКолонок + ", СтранаРегистрации";// <СправочникСсылка.КлассификаторСтранМира>
	КонецЕсли;
	Если ТаблицаРеквизиты.Колонки.Найти("Контрагент") <> Неопределено Тогда
		СписокОбязательныхКолонок = СписокОбязательныхКолонок + ", Контрагент"; // <СправочникСсылка.Контрагенты>
	КонецЕсли;
	
	Параметры.Вставить("Реквизиты", ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизиты, СписокОбязательныхКолонок));
		
	Возврат Параметры;
	
КонецФункции

Процедура ДополнительныеСведенияДляОтчетаОбОперациях(МенеджерВременныхТаблиц) Экспорт
	
	// Дополнительные сведения о контрагенте
	УчетНДС.РегистрационныеСведенияНаДаты(МенеджерВременныхТаблиц);
	
	// Дополнительные сведения о номенклатуре
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Ссылка,
	|	Номенклатура.КодТНВЭД КАК КодТНВЭД,
	|	Номенклатура.КодОКПД2 КАК КодОКПД2,
	|	Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмерения,
	|	Номенклатура.КодТНВЭД.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияПрослеживаемости
	|ПОМЕСТИТЬ СведенияОНоменклатуре
	|ИЗ
	|	НоменклатураОпераций КАК НоменклатураОпераций
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
	|		ПО НоменклатураОпераций.НоменклатураСсылка = Номенклатура.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура.Ссылка";
	
	Запрос.Выполнить();

КонецПроцедуры

Функция ПодготовитьТаблицуПоПрослеживаемымОперациям(ПрослеживаемыеОперации, ТаблицаСписанныеТовары) Экспорт
	
	Если ПрослеживаемыеОперации = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если (ТаблицаСписанныеТовары.Колонки.Найти("ИдентификаторСтроки") <> Неопределено) И (ТаблицаСписанныеТовары.Колонки.Найти("СуммаСписания") <> Неопределено)
		Тогда
			ТаблицаСумм = ТаблицаСписанныеТовары.Скопировать(, "ИдентификаторСтроки, СуммаСписания");
			ТаблицаСумм.Свернуть("ИдентификаторСтроки", "СуммаСписания");
			
			Для каждого Строка Из ПрослеживаемыеОперации Цикл
				СтрокаССуммой = ТаблицаСумм.Найти(Строка.ИдентификаторСтроки, "ИдентификаторСтроки");
				
				Если СтрокаССуммой <> Неопределено Тогда
					Строка.СуммаБезНДС = СтрокаССуммой.СуммаСписания;
				КонецЕсли;
				
			КонецЦикла;
			
	КонецЕсли;
		
	ПрослеживаемыеОперации.Свернуть("ДатаДокумента, ДокументОперации, КодОперации, Контрагент,
		|Номенклатура, НомерДокумента, ОтражениеВОтчетности, ПериодОперации, РНПТ, ТипДокументаВПрослеживаемости",
		"СуммаБезНДС, Количество, КоличествоПрослеживаемости");
	
	Возврат ПрослеживаемыеОперации;
		
КонецФункции

Функция ПодготовитьТаблицыПоПрослеживаемости(Реквизиты, ПрослеживаемыеТовары, ПрослеживаемыеОперации) Экспорт
	
	ТаблицаПрослеживаемыеТоварыКомиссия = Неопределено;
	ТаблицаПрослеживаемыеОперации       = ПрослеживаемыеОперации;
	СтруктураТаблиц = Новый Структура();
	СтруктураТаблиц.Вставить("ПрослеживаемыеТоварыКомиссия", ТаблицаПрослеживаемыеТоварыКомиссия);
	СтруктураТаблиц.Вставить("ПрослеживаемыеОперации",       ТаблицаПрослеживаемыеОперации);
	
	Если ПрослеживаемыеТовары = Неопределено Тогда
		Возврат СтруктураТаблиц;
	КонецЕсли;
	
	ЭтоКомиссия = Реквизиты[0].ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером;
	Если ЭтоКомиссия Тогда
		ТаблицаПрослеживаемыеТоварыКомиссия = ПрослеживаемыеТовары.Скопировать();
		ТаблицаПрослеживаемыеТоварыКомиссия.ЗаполнитьЗначения(Реквизиты[0].Контрагент, "Комиссионер");
		ТаблицаПрослеживаемыеОперации = Неопределено;
	КонецЕсли;
	
	СтруктураТаблиц.ПрослеживаемыеТоварыКомиссия = ТаблицаПрослеживаемыеТоварыКомиссия;
	СтруктураТаблиц.ПрослеживаемыеОперации       = ТаблицаПрослеживаемыеОперации;
	
	Возврат СтруктураТаблиц;
	
КонецФункции

Процедура УстановитьСвойстваРНПТ(ТаблицаРНПТ, Реквизиты) Экспорт
	
	Если ТаблицаРНПТ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыСвойстваРНПТ(ТаблицаРНПТ);
	
	Для Каждого СтрокаТаблицы Из Параметры.ТаблицаРНПТ Цикл
		
		МенеджерЗаписи = РегистрыСведений.СвойстваРНПТ.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Организация = Реквизиты.Организация;
		МенеджерЗаписи.РНПТ = СтрокаТаблицы.РНПТ;
		МенеджерЗаписи.ДатаПоступления = Реквизиты.Дата;
		МенеджерЗаписи.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

#Область ПроведениеИнвентаризации

Процедура ЗарегистрироватьПрослеживаемыйТовар(ТаблицаРеквизиты) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаРеквизиты) Тогда
		Возврат;
	КонецЕсли;
		
	Параметры = ПодготовитьПараметрыРегистрацииПрослеживаемогоТовара(ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];
	
	Если НЕ ПрослеживаемостьБРУ.ВедетсяУчетПрослеживаемыхТоваров(Реквизиты.Период) Тогда
		Возврат;
	КонецЕсли;
	
	РегистрыСведений.РегистрацияПрослеживаемыхТоваров.ОбновитьСостояние(Реквизиты.Регистратор, Реквизиты.Регистратор);
	
КонецПроцедуры

Функция ЭтоПерваяИнвентаризацияПоПрослеживаемости(ДокументОснование, Реквизиты) Экспорт
	
	Организация = Реквизиты.Организация;
	Период = Реквизиты.Дата;
	Склад = Реквизиты.Склад;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация",    Организация);
	Запрос.УстановитьПараметр("Склад",          Склад);
	Запрос.УстановитьПараметр("ДатаНачала",     ПрослеживаемостьБРУ.ДатаНачалаУчетаПрослеживаемыхТоваров());
	Запрос.УстановитьПараметр("ДатаОкончания",  Период);
	Запрос.УстановитьПараметр("Ссылка",  ДокументОснование);
	
	//По текущему товару нужно найти инвентаризации
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИнвентаризацияТоваровНаСкладеТовары.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ
	|	Документ.ИнвентаризацияТоваровНаСкладе.Товары КАК ИнвентаризацияТоваровНаСкладеТовары
	|ГДЕ
	|	ИнвентаризацияТоваровНаСкладеТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ИнвентаризацияТоваровНаСкладеТовары.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИнвентаризацияТоваровНаСкладе.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ИнвентаризацияТоваровНаСкладе.Товары КАК ИнвентаризацияТоваровНаСкладеТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияТоваровНаСкладе КАК ИнвентаризацияТоваровНаСкладе
	|		ПО ИнвентаризацияТоваровНаСкладеТовары.Ссылка = ИнвентаризацияТоваровНаСкладе.Ссылка
	|ГДЕ
	|	ИнвентаризацияТоваровНаСкладе.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ИнвентаризацияТоваровНаСкладе.Организация = &Организация
	|	И ИнвентаризацияТоваровНаСкладе.Склад = &Склад
	|	И ИнвентаризацияТоваровНаСкладе.Проведен = ИСТИНА
	|	И ИнвентаризацияТоваровНаСкладе.Ссылка <> &Ссылка
	|	И ИнвентаризацияТоваровНаСкладеТовары.Номенклатура В
	|			(ВЫБРАТЬ
	|				ТаблицаТоваров.Номенклатура
	|			ИЗ
	|				ТаблицаТоваров)
	|
	|СГРУППИРОВАТЬ ПО
	|	ИнвентаризацияТоваровНаСкладе.Ссылка";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	// Если есть инвентаризации ранее, то по прослеживаемому товару движения не делаем
	Если Выборка.Следующий() Тогда
		
		Возврат Ложь;
		
	КонецЕсли;

	
	Возврат Истина;
	
КонецФункции

Функция КоличествоРНПТКСтроке(МассивСтрокСведенияПрослеживаемости) Экспорт
	
	КоличествоРНПТКСтроке = 0;
	Для Каждого ЭлементМассива Из МассивСтрокСведенияПрослеживаемости Цикл
		КоличествоРНПТКСтроке = КоличествоРНПТКСтроке + ЭлементМассива.Количество;
	КонецЦикла;
	
	Возврат КоличествоРНПТКСтроке;
	
КонецФункции

#КонецОбласти

// Переопределяемая процедура обработки проведения документов 
// "Уведомление об остатках прослеживаемых товаров" и "Уведомление о ввозе прослеживаемых товаров"
// 
// Параметры:
//  ПараметрыПроведения - Структура таблиц см. Документы.УведомлениеОбОстаткахПрослеживаемыхТоваров.ПодготовитьПараметрыПроведения и 
//                        Документы.УведомлениеОВвозеПрослеживаемыхТоваров.ПодготовитьПараметрыПроведения
//  Движения - ссылка на движения документа
//  Отказ - Булево - признак отказа от записи
//
Процедура ОбработкаПроведенияУведомления(ПараметрыПроведения, Движения, Отказ) Экспорт
	
	// Формируем таблицу для движений по регистру накопления ПрослеживаемыеТовары
	ТаблицаДвиженийРНПТ = ПодготовитьТаблицуРНПТ(ПараметрыПроведения.ТаблицаТоваров,
		ПараметрыПроведения.Реквизиты, Отказ);
		
	СформироватьДвиженияПрослеживаемостьТоваров(
		ТаблицаДвиженийРНПТ,
		Движения,
		Отказ);
		
	// Формируем движения по свойствам РНПТ для корректного списания товаров по ФИФО.
	Если ЗначениеЗаполнено(ТаблицаДвиженийРНПТ)
		И ЗначениеЗаполнено(ПараметрыПроведения.Реквизиты) Тогда
		РазличныеРНПТ = ТаблицаДвиженийРНПТ.Скопировать();
		РазличныеРНПТ.Свернуть("РНПТ");
		ПрослеживаемостьБП.УстановитьСвойстваРНПТ(РазличныеРНПТ, ПараметрыПроведения.Реквизиты[0]);
	КонецЕсли;
	
	
	// Формируем таблицу для движений по счету ГТД
	ТаблицаОстатковГТД = ПодготовитьТаблицуОстатковГТД(ПараметрыПроведения.ТаблицаТоваров,
		ПараметрыПроведения.Реквизиты, Отказ);
		
	СформироватьДвиженияГТД(
		ТаблицаОстатковГТД,
		Движения,
		Отказ);
		
КонецПроцедуры

#Область УдалениеПроведения

// Процедура удаления проведения первичных документов
// 
// Параметры:
// Регистратор - ДокументСсылка - Документ регистратор
// 
Процедура УдалениеПроведенияПервичногоДокумента(Регистратор) Экспорт
	
	Если НЕ ПолучитьФункциональнуюОпцию("ВестиУчетПрослеживаемыхТоваров") Тогда
		Возврат;
	КонецЕсли;
	
	РегистрыСведений.РегистрацияПрослеживаемыхТоваров.ОбновитьСостояниеУдалениеПроведенияПервичногоДокумента(Регистратор);
	
КонецПроцедуры

#КонецОбласти

#Область ПомощникПолученияРНПТ

Функция ПолучитьНачальныеДанные(Период, Организация) Экспорт
	
// УПП	ВестиСкладскойУчетБУ = БухгалтерскийУчет.ВедетсяУчетПоСкладам(ПланыСчетов.Хозрасчетный.Товары);
	
// УПП	ПорядокСубконтоКоличество = Новый Массив();
// УПП	ПорядокСубконтоКоличество.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
// УПП	Если ВестиСкладскойУчетБУ Тогда
// УПП		ПорядокСубконтоКоличество.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);
// УПП	КонецЕсли;
	
// УПП	МассивИсклСчетов = Новый Массив();
// УПП	МассивИсклСчетов.Добавить(ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахНТТ); // 41.12
// УПП	МассивИсклСчетов.Добавить(ПланыСчетов.Хозрасчетный.ТорговаяНаценка);                            // 42
// УПП	МассивИсклСчетов.Добавить(ПланыСчетов.Хозрасчетный.ГТД);                                        // ГТД
// УПП	МассивИсклСчетов.Добавить(ПланыСчетов.Хозрасчетный.МатериальныеЦенностиВЭксплуатации);          // МЦ
// УПП	МассивИсклСчетов = БухгалтерскийУчет.СформироватьМассивСубсчетов(МассивИсклСчетов);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	
	Запрос.УстановитьПараметр("Организация", 	Организация);
	Запрос.УстановитьПараметр("Период", 		Период);
	Запрос.УстановитьПараметр("НачалоПериода", 	ПрослеживаемостьБРУ.ДатаНачалаУчетаПрослеживаемыхТоваров());
	Запрос.УстановитьПараметр("КонецПериода", 	КонецМесяца(Период));
// УПП	Запрос.УстановитьПараметр("ИсклСчета",                 МассивИсклСчетов);
// УПП	Запрос.УстановитьПараметр("ПорядокСубконтоКоличество", ПорядокСубконтоКоличество);
	
// УПП	ТекстАналитикаПоСкладу = ?(ВестиСкладскойУчетБУ, "ХозрасчетныйОстатки.Субконто2","ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)");
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
// УПП	|	ХозрасчетныйОстатки.Субконто1 КАК Номенклатура,
// УПП	|	0 КАК Сумма,
// УПП	|	СУММА(ХозрасчетныйОстатки.КоличествоОстатокДт) КАК Количество,
// УПП	|	ХозрасчетныйОстатки.Субконто2 КАК Склад
// УПП	|ПОМЕСТИТЬ ТаблицаОстатков
// УПП	|ИЗ
// УПП	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&Период, НЕ Счет В (&ИсклСчета), &ПорядокСубконтоКоличество, Организация = &Организация) КАК ХозрасчетныйОстатки
// УПП	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
// УПП	|		ПО ХозрасчетныйОстатки.Субконто1 = Номенклатура.Ссылка
// УПП	|ГДЕ
// УПП	|	Номенклатура.ПрослеживаемыйТовар
// УПП	|	И ХозрасчетныйОстатки.КоличествоОстатокДт > 0
// УПП	|
// УПП	|СГРУППИРОВАТЬ ПО
// УПП	|	ХозрасчетныйОстатки.Субконто1,
// УПП	|	ХозрасчетныйОстатки.Субконто2
// УПП	|;
// УПП	|
// УПП	|////////////////////////////////////////////////////////////////////////////////
// УПП	|ВЫБРАТЬ
	|	РегистрацияПрослеживаемыхТоваров.Номенклатура КАК Номенклатура,
	|	СУММА(РегистрацияПрослеживаемыхТоваров.Количество) КАК Количество,
	|	СУММА(РегистрацияПрослеживаемыхТоваров.Сумма) КАК Сумма,
	|	РегистрацияПрослеживаемыхТоваров.Основание КАК ДокументРегистрации,
	|	РегистрацияПрослеживаемыхТоваров.Основание.Склад КАК Склад,
	|	РегистрацияПрослеживаемыхТоваров.ТНВЭД КАК ТНВЭД
	|ПОМЕСТИТЬ ЗарегистрированныеОстаткиПоОснованию
	|ИЗ
	|	РегистрСведений.РегистрацияПрослеживаемыхТоваров КАК РегистрацияПрослеживаемыхТоваров
	|ГДЕ
	|	РегистрацияПрослеживаемыхТоваров.Организация = &Организация
	|	И РегистрацияПрослеживаемыхТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацииПрослеживаемыхТоваров.ИнвентаризацияТоваров)
	|	И РегистрацияПрослеживаемыхТоваров.ПериодСобытия <= КОНЕЦПЕРИОДА(&Период, МЕСЯЦ)
	|
	|СГРУППИРОВАТЬ ПО
	|	РегистрацияПрослеживаемыхТоваров.Номенклатура,
	|	РегистрацияПрослеживаемыхТоваров.Основание,
	|	РегистрацияПрослеживаемыхТоваров.Основание.Склад,
	|	РегистрацияПрослеживаемыхТоваров.ТНВЭД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрослеживаемыеТоварыОбороты.Номенклатура КАК Номенклатура,
	|	СУММА(ПрослеживаемыеТоварыОбороты.КоличествоПриход) КАК КоличествоПриход,
	|	СУММА(ПрослеживаемыеТоварыОбороты.КоличествоРасход) КАК КоличествоРасход,
	|	ПрослеживаемыеТоварыОбороты.Регистратор.Склад КАК Склад,
	|	ПрослеживаемыеТоварыОбороты.РНПТ КАК РНПТ,
	|	ВЫБОР
	|		КОГДА НЕ ПрослеживаемыеТоварыОбороты.Регистратор ССЫЛКА Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоРНПТОтПоставщика
	|ПОМЕСТИТЬ ПрослеживаемыеТоварыСРНПТ
	|ИЗ
	|	РегистрНакопления.ПрослеживаемыеТовары.Обороты(&НачалоПериода, &Период, Регистратор, Организация = &Организация) КАК ПрослеживаемыеТоварыОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ПрослеживаемыеТоварыОбороты.Номенклатура,
	|	ПрослеживаемыеТоварыОбороты.Регистратор.Склад,
	|	ПрослеживаемыеТоварыОбороты.РНПТ,
	|	ВЫБОР
	|		КОГДА НЕ ПрослеживаемыеТоварыОбороты.Регистратор ССЫЛКА Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрослеживаемыеТоварыСРНПТ.РНПТ КАК РНПТ
	|ПОМЕСТИТЬ СписокРНПТОтПоставщика
	|ИЗ
	|	ПрослеживаемыеТоварыСРНПТ КАК ПрослеживаемыеТоварыСРНПТ
	|ГДЕ
	|	ПрослеживаемыеТоварыСРНПТ.ЭтоРНПТОтПоставщика = ИСТИНА
	|
	|СГРУППИРОВАТЬ ПО
	|	ПрослеживаемыеТоварыСРНПТ.РНПТ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрослеживаемыеТоварыСРНПТ.Номенклатура КАК Номенклатура,
	|	СУММА(ПрослеживаемыеТоварыСРНПТ.КоличествоПриход - ПрослеживаемыеТоварыСРНПТ.КоличествоРасход) КАК Остаток,
	|	ПрослеживаемыеТоварыСРНПТ.Склад КАК Склад
	|ПОМЕСТИТЬ ТаблицаОстатковПрослеживаемогоТовараСРНПТОтПоставщика
	|ИЗ
	|	ПрослеживаемыеТоварыСРНПТ КАК ПрослеживаемыеТоварыСРНПТ
	|ГДЕ
	|	ПрослеживаемыеТоварыСРНПТ.РНПТ В
	|			(ВЫБРАТЬ
	|				СписокРНПТОтПоставщика.РНПТ
	|			ИЗ
	|				СписокРНПТОтПоставщика КАК СписокРНПТОтПоставщика)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПрослеживаемыеТоварыСРНПТ.Номенклатура,
	|	ПрослеживаемыеТоварыСРНПТ.Склад
	|;
	|
// УПП	|////////////////////////////////////////////////////////////////////////////////
// УПП	|ВЫБРАТЬ
// УПП	|	ТаблицаОстатковБезОснования.Номенклатура КАК Номенклатура,
// УПП	|	СУММА(ЕСТЬNULL(ТаблицаОстатковБезОснования.Количество, 0) - ЕСТЬNULL(ТаблицаОстатковПоОснованию.Количество, 0) - ЕСТЬNULL(ТаблицаОстатковПрослеживаемогоТовараСРНПТОтПоставщика.Остаток, 0)) КАК Количество,
// УПП	|	СУММА(ЕСТЬNULL(ТаблицаОстатковБезОснования.Сумма, 0) - ЕСТЬNULL(ТаблицаОстатковПоОснованию.Сумма, 0)) КАК Сумма,
// УПП	|	ТаблицаОстатковБезОснования.Склад КАК Склад
// УПП	|ПОМЕСТИТЬ ТаблицаОстатковБезОснования
// УПП	|ИЗ
// УПП	|	ТаблицаОстатков КАК ТаблицаОстатковБезОснования
// УПП	|		ЛЕВОЕ СОЕДИНЕНИЕ ЗарегистрированныеОстаткиПоОснованию КАК ТаблицаОстатковПоОснованию
// УПП	|		ПО ТаблицаОстатковБезОснования.Номенклатура = ТаблицаОстатковПоОснованию.Номенклатура
// УПП	|			И ТаблицаОстатковБезОснования.Склад = ТаблицаОстатковПоОснованию.Склад
// УПП	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОстатковПрослеживаемогоТовараСРНПТОтПоставщика КАК ТаблицаОстатковПрослеживаемогоТовараСРНПТОтПоставщика
// УПП	|		ПО ТаблицаОстатковБезОснования.Номенклатура = ТаблицаОстатковПрослеживаемогоТовараСРНПТОтПоставщика.Номенклатура
// УПП	|			И ТаблицаОстатковБезОснования.Склад = ТаблицаОстатковПрослеживаемогоТовараСРНПТОтПоставщика.Склад
// УПП	|ГДЕ
// УПП	|	ЕСТЬNULL(ТаблицаОстатковБезОснования.Количество, 0) - ЕСТЬNULL(ТаблицаОстатковПоОснованию.Количество, 0) > 0
// УПП	|	И ТаблицаОстатковПоОснованию.Количество ЕСТЬ NULL
// УПП	|
// УПП	|СГРУППИРОВАТЬ ПО
// УПП	|	ТаблицаОстатковБезОснования.Номенклатура,
// УПП	|	ТаблицаОстатковБезОснования.Склад
// УПП	|;
// УПП	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗарегистрированныеОстатки.Номенклатура КАК Номенклатура,
	|	СУММА(ЗарегистрированныеОстатки.Количество) КАК Количество,
	|	СУММА(ЗарегистрированныеОстатки.Сумма) КАК Сумма,
	|	ЗарегистрированныеОстатки.ДокументРегистрации КАК ДокументРегистрации,
// УПП	|	НоменклатураСправочник.КодОКПД2 КАК КодОКПД2,
	|	"""" КАК КодОКПД2,
	|	НоменклатураСправочник.ЕдиницаХраненияОстатков КАК ЕдиницаИзмерения,
	|	ЗарегистрированныеОстатки.ТНВЭД КАК КодТНВЭД,
	|	КлассификаторТНВЭД.ЕдиницаИзмерения КАК ЕдиницаПрослеживаемости,
	|	ВЫБОР
	|		КОГДА НоменклатураСправочник.ВесПоСертификатуТовара > 0
	|			ТОГДА ЗарегистрированныеОстатки.Количество * НоменклатураСправочник.ВесПоСертификатуТовара
	|		ИНАЧЕ ЗарегистрированныеОстатки.Количество
	|	КОНЕЦ КАК КоличествоПрослеживаеомсти,
	|	ЗарегистрированныеОстатки.Склад КАК Склад
	|ПОМЕСТИТЬ ТаблицаОстатковПоОснованию
	|ИЗ
	|	ЗарегистрированныеОстаткиПоОснованию КАК ЗарегистрированныеОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСправочник
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторТНВЭД КАК КлассификаторТНВЭД
	|			ПО НоменклатураСправочник.КодТНВЭД = КлассификаторТНВЭД.Ссылка
	|		ПО ЗарегистрированныеОстатки.Номенклатура = НоменклатураСправочник.Ссылка
	|ГДЕ
	|	ЗарегистрированныеОстатки.Количество > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗарегистрированныеОстатки.Номенклатура,
	|	ЗарегистрированныеОстатки.ДокументРегистрации,
	|	НоменклатураСправочник.КодОКПД2,
	|	НоменклатураСправочник.ЕдиницаИзмерения,
	|	КлассификаторТНВЭД.ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА НоменклатураСправочник.ВесПоСертификатуТовара > 0
	|			ТОГДА ЗарегистрированныеОстатки.Количество * НоменклатураСправочник.ВесПоСертификатуТовара
	|		ИНАЧЕ ЗарегистрированныеОстатки.Количество
	|	КОНЕЦ,
	|	ЗарегистрированныеОстатки.Склад,
	|	ЗарегистрированныеОстатки.ТНВЭД
	|;
	|
// УПП	|////////////////////////////////////////////////////////////////////////////////
// УПП	|ВЫБРАТЬ
// УПП	|	ТаблицаОстатковБезОснования.Номенклатура КАК Номенклатура,
// УПП	|	ТаблицаОстатковБезОснования.Количество КАК Количество,
// УПП	|	ТаблицаОстатковБезОснования.Сумма КАК Сумма,
// УПП	|	ТаблицаОстатковБезОснования.Склад КАК Склад
// УПП	|ПОМЕСТИТЬ ТаблицаОстатковБезОснованияБезИнвентаризации
// УПП	|ИЗ
// УПП	|	ТаблицаОстатковБезОснования КАК ТаблицаОстатковБезОснования
// УПП	|		ЛЕВОЕ СОЕДИНЕНИЕ ЗарегистрированныеОстаткиПоОснованию КАК ЗарегистрированныеОстаткиПоОснованию
// УПП	|		ПО ТаблицаОстатковБезОснования.Номенклатура = ЗарегистрированныеОстаткиПоОснованию.Номенклатура
// УПП	|			И ТаблицаОстатковБезОснования.Склад = ЗарегистрированныеОстаткиПоОснованию.Склад
// УПП	|ГДЕ
// УПП	|	ЗарегистрированныеОстаткиПоОснованию.Номенклатура ЕСТЬ NULL
// УПП	|;
// УПП	|
// УПП	|////////////////////////////////////////////////////////////////////////////////
// УПП	|ВЫБРАТЬ
// УПП	|	ТаблицаОстатковБезОснованияБезИнвентаризации.Номенклатура КАК Номенклатура,
// УПП	|	ТаблицаОстатковБезОснованияБезИнвентаризации.Количество КАК Количество,
// УПП	|	ТаблицаОстатковБезОснованияБезИнвентаризации.Сумма КАК Сумма,
// УПП	|	"""" КАК Основание,
// УПП	|	ЛОЖЬ КАК ЕстьПервичныйДокумент,
// УПП	|	НЕОПРЕДЕЛЕНО КАК КодОКПД2,
// УПП	|	НЕОПРЕДЕЛЕНО КАК ЕдиницаИзмерения,
// УПП	|	НЕОПРЕДЕЛЕНО КАК КодТНВЭД,
// УПП	|	НЕОПРЕДЕЛЕНО КАК ЕдиницаПрослеживаемости,
// УПП	|	НЕОПРЕДЕЛЕНО КАК КоличествоПрослеживаемости,
// УПП	|	ТаблицаОстатковБезОснованияБезИнвентаризации.Склад КАК Склад,
// УПП	|	ЛОЖЬ КАК ИмпортИзЕАЭС,
// УПП	|	НЕОПРЕДЕЛЕНО КАК ВидСтавкиНДС
// УПП	|ИЗ
// УПП	|	ТаблицаОстатковБезОснованияБезИнвентаризации КАК ТаблицаОстатковБезОснованияБезИнвентаризации
// УПП	|
// УПП	|ОБЪЕДИНИТЬ ВСЕ
// УПП	|
	|ВЫБРАТЬ
	|	ТаблицаОстатковПоРегистрации.Номенклатура,
	|	ТаблицаОстатковПоРегистрации.Количество,
	|	ТаблицаОстатковПоРегистрации.Сумма,
	|	ТаблицаОстатковПоРегистрации.ДокументРегистрации,
	|	ИСТИНА,
	|	ТаблицаОстатковПоРегистрации.КодОКПД2,
	|	ТаблицаОстатковПоРегистрации.ЕдиницаИзмерения,
	|	ТаблицаОстатковПоРегистрации.КодТНВЭД,
	|	ТаблицаОстатковПоРегистрации.ЕдиницаПрослеживаемости,
	|	ТаблицаОстатковПоРегистрации.КоличествоПрослеживаеомсти,
	|	ТаблицаОстатковПоРегистрации.Склад,
	|	ЛОЖЬ,
	|	НЕОПРЕДЕЛЕНО
	|ИЗ
	|	ТаблицаОстатковПоОснованию КАК ТаблицаОстатковПоРегистрации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РегистрацияПрослеживаемыхТоваров.Номенклатура,
	|	РегистрацияПрослеживаемыхТоваров.Количество,
	|	РегистрацияПрослеживаемыхТоваров.Сумма,
	|	РегистрацияПрослеживаемыхТоваров.Основание,
	|	ИСТИНА,
	|	НоменклатураСправочник.КодОКПД2,
	|	НоменклатураСправочник.ЕдиницаИзмерения,
	|	РегистрацияПрослеживаемыхТоваров.ТНВЭД,
	|	РегистрацияПрослеживаемыхТоваров.ТНВЭД.ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА НоменклатураСправочник.ВесПоСертификатуТовара > 0
	|			ТОГДА РегистрацияПрослеживаемыхТоваров.Количество * НоменклатураСправочник.ВесПоСертификатуТовара
	|		ИНАЧЕ РегистрацияПрослеживаемыхТоваров.Количество
	|	КОНЕЦ,
	|	НЕОПРЕДЕЛЕНО,
	|	ИСТИНА,
	|	НоменклатураСправочник.ВидСтавкиНДС
	|ИЗ
	|	РегистрСведений.РегистрацияПрослеживаемыхТоваров КАК РегистрацияПрослеживаемыхТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСправочник
	|		ПО РегистрацияПрослеживаемыхТоваров.Номенклатура = НоменклатураСправочник.Ссылка
	|ГДЕ
	|	РегистрацияПрослеживаемыхТоваров.Количество > 0
	|	И РегистрацияПрослеживаемыхТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацииПрослеживаемыхТоваров.ВвозТоваровИзЕАЭС)
	|	И РегистрацияПрослеживаемыхТоваров.Организация = &Организация";
	
// УПП	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ХозрасчетныйОстатки.Субконто2", ТекстАналитикаПоСкладу);
	Запрос.Текст = ТекстЗапроса;
	
	Результат = Запрос.Выполнить();
	
	Возврат Результат.Выгрузить();
	
	
КонецФункции

#КонецОбласти

Процедура СверитьОстаткиРНПТ(ДокументОбъект, Отказ, ИмяТабличнойЧасти, ДокументОснованиеВТЧ = Ложь) Экспорт
	
	ТаблицаТоваров = ДокументОбъект[ИмяТабличнойЧасти].Выгрузить();
	
	Если ТаблицаТоваров.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ОтчетКомиссионераОПродажах") Тогда
		Если ИмяТабличнойЧасти = "Товары" Тогда
			ТаблицаРНПТОстатки = ТаблицаРНПТОстатки(ДокументОбъект, ТаблицаТоваров, Истина);     // *
		Иначе
			ТаблицаРНПТОстатки =ТаблицаРНПТОстаткиВозвратКомиссия(ДокументОбъект, ТаблицаТоваров);
		КонецЕсли;
	Иначе
		ТаблицаРНПТОстатки = ТаблицаРНПТОстаткиВозвратПоставщику(ДокументОбъект, ТаблицаТоваров, ДокументОснованиеВТЧ);
	КонецЕсли;
	
	КонтролироватьОстаток = Истина;
	
	Если КонтролироватьОстаток Тогда
		
		Для Каждого СтрокаТабличнойЧасти ИЗ ДокументОбъект[ИмяТабличнойЧасти] Цикл
			
			Если НЕ СтрокаТабличнойЧасти.ПрослеживаемыйТовар Тогда
				Продолжить;
			КонецЕсли;
			
			Если ДокументОснованиеВТЧ Тогда
				Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Сделка) Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			СтрокиСРНПТ = ДокументОбъект.СведенияПрослеживаемости.НайтиСтроки(
				Новый Структура("ИдентификаторСтроки", СтрокаТабличнойЧасти.ИдентификаторСтроки));
				
			Для Каждого СтрокаСРНПТ ИЗ СтрокиСРНПТ Цикл
				ТребуемоеКоличество = СтрокаСРНПТ.Количество;
				ТребуемоеКоличествоПрослеживаемости = СтрокаСРНПТ.КоличествоПрослеживаемости;
				СверитьСОстаткамиВПрослеживаемости(ДокументОбъект, Отказ, СтрокаСРНПТ,
					СтрокаТабличнойЧасти, ТаблицаРНПТОстатки, ИмяТабличнойЧасти, ,ДокументОснованиеВТЧ);
			КонецЦикла;
				
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЭтоПрослеживаемыйТовар(Товар) Экспорт
	
	Если ЗначениеЗаполнено(Товар.КодТНВЭД) Тогда
		Возврат Товар.КодТНВЭД.ПрослеживаемыйТовар;
	КонецЕсли;
	
	Возврат Ложь;
КонецФункции

Функция ПодготовитьПараметрыРегистрацииПрослеживаемогоТовара(ТаблицаРеквизиты)
	
	//Параметры = Новый Структура;
	//
	//СписокОбязательныхКолонок = ""
	//+ "Регистратор,"   // <ДокументСсылка.*> - документ-регистратор движений
	//+ "Основание,"   // <ДокументСсылка.*> - документ-регистратор движений
	//+ "Период,"        // <Дата> - период движений - дата документа
	//+ "Организация,"   // <СправочникСсылка.Организации>
	//+ "Склад";    // <СправочникСсылка.Склады>
	//
	//Параметры.Вставить("Реквизиты", 
	//	ПолучитьТаблицуПараметровПроведения(ТаблицаРеквизиты, СписокОбязательныхКолонок));
	//	
	//Возврат Параметры;
	
КонецФункции

Процедура СформироватьДвиженияПрослеживаемыхТоваров(ТаблицаТовары, Реквизиты, Движения, Списание = Ложь)
	
	Если Не ЗначениеЗаполнено(ТаблицаТовары) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыДвиженияТоваров(ТаблицаТовары);
	
	Для Каждого СтрокаТаблицы Из Параметры.Товары Цикл
		Если Списание Тогда
			Запись = Движения.ПрослеживаемыеТовары.ДобавитьРасход();
		Иначе
			Запись = Движения.ПрослеживаемыеТовары.ДобавитьПриход();
		КонецЕсли;
		
		Запись.Период = Реквизиты.Дата;
		
		ЗаполнитьЗначенияСвойств(Запись, Реквизиты);
		ЗаполнитьЗначенияСвойств(Запись, СтрокаТаблицы);
	КонецЦикла;
	
	Движения.ПрослеживаемыеТовары.Записывать = Истина;

КонецПроцедуры

Процедура ПодобратьРНПТИзОстатков(ДокументОбъект, ОсталосьПодобрать, СтрокаТабличнойЧасти, ТаблицаРНПТОстатки)
	
	Отбор = Новый Структура("Номенклатура, СтранаПроисхождения",
		СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.СтранаПроисхождения);
	ОстаткиРНПТ = ТаблицаРНПТОстатки.НайтиСтроки(Отбор);
	
	Для Каждого ПодобранныйРНПТ Из ОстаткиРНПТ Цикл
		Если ОсталосьПодобрать = 0 Тогда
			Прервать;
		КонецЕсли;
		
		// Возможные "нулевые" остатки пропускаем.
		Если ПодобранныйРНПТ.Количество <= 0
			ИЛИ ПодобранныйРНПТ.КоличествоПрослеживаемости <= 0 Тогда
			Продолжить;
		КонецЕсли;
				
		Если ОсталосьПодобрать >= ПодобранныйРНПТ.Количество Тогда
			// Добавляем все количество из подобранного РНПТ
			НоваяСтрока = ДокументОбъект.СведенияПрослеживаемости.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ПодобранныйРНПТ);
			НоваяСтрока.ИдентификаторСтроки = СтрокаТабличнойЧасти.ИдентификаторСтроки;
			ОсталосьПодобрать = ОсталосьПодобрать - ПодобранныйРНПТ.Количество;
			ТаблицаРНПТОстатки.Удалить(ПодобранныйРНПТ);
		Иначе
			НоваяСтрока = ДокументОбъект.СведенияПрослеживаемости.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ПодобранныйРНПТ);
			НоваяСтрока.ИдентификаторСтроки = СтрокаТабличнойЧасти.ИдентификаторСтроки;
			НоваяСтрока.Количество = ОсталосьПодобрать;
			
			МассивКоэффициентов = Новый Массив;
			МассивКоэффициентов.Добавить(ПодобранныйРНПТ.Количество - ОсталосьПодобрать);
			МассивКоэффициентов.Добавить(ОсталосьПодобрать);
			МассивСумм = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(
			ПодобранныйРНПТ.КоличествоПрослеживаемости, МассивКоэффициентов, 11);
			
			НоваяСтрока.КоличествоПрослеживаемости = МассивСумм[1];
			
			ПодобранныйРНПТ.Количество = ПодобранныйРНПТ.Количество - ОсталосьПодобрать;
			ПодобранныйРНПТ.КоличествоПрослеживаемости = ПодобранныйРНПТ.КоличествоПрослеживаемости - МассивСумм[1];
			
			ОсталосьПодобрать = 0;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ТаблицаРНПТОстатки(ДокументОбъект, ТаблицаТоваров, ЭтоКомиссия) 
	
	// Блокируем регистр ПрослеживаемыеТовары для получения остатков
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ПрослеживаемыеТовары");
	ЭлементБлокировки.УстановитьЗначение("Организация", ДокументОбъект.Организация);
	ЭлементБлокировки.УстановитьЗначение("Период", Новый Диапазон(, ДокументОбъект.Дата));
	ЭлементБлокировки.ИсточникДанных = ТаблицаТоваров;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	//ЭлементБлокировки.ИспользоватьИзИсточникаДанных("СтранаПроисхождения", "СтранаПроисхождения");
	// Установим дополнительные ограничения по комиссионным товарам
	//Комиссионер = ?(ЭтоКомиссия, ДокументОбъект.Контрагент, Справочники.Контрагенты.ПустаяСсылка());
	//Комитент    = Справочники.Контрагенты.ПустаяСсылка();
	//Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ВозвратТоваровПоставщику") Тогда
	//	Комитент    = ДокументОбъект.Контрагент;
	//КонецЕсли;
	//ЭлементБлокировки.УстановитьЗначение("Комиссионер", Комиссионер);
	//ЭлементБлокировки.УстановитьЗначение("Комитент",    Комитент);
	
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	
	МоментСписания = Новый МоментВремени(ДокументОбъект.Дата, ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("МоментСписания", МоментСписания);
	Запрос.УстановитьПараметр("Организация",    ДокументОбъект.Организация);
	//Запрос.УстановитьПараметр("Комиссионер",    Комиссионер);
	//Запрос.УстановитьПараметр("Комитент",       Комитент);
	Запрос.УстановитьПараметр("ТаблицаТоваров", ТаблицаТоваров);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.СтранаПроисхождения КАК СтранаПроисхождения
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ
	|	&ТаблицаТоваров КАК ТаблицаТоваров
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиРНПТСИсключеннымиДвижениями.Номенклатура КАК Номенклатура,
	|	ОстаткиРНПТСИсключеннымиДвижениями.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ОстаткиРНПТСИсключеннымиДвижениями.РНПТ КАК РНПТ,
	|	СУММА(ОстаткиРНПТСИсключеннымиДвижениями.Количество) КАК Количество,
	|	СУММА(ОстаткиРНПТСИсключеннымиДвижениями.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости
	|ПОМЕСТИТЬ ОстаткиРНПТ
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПрослеживаемыеТоварыОстатки.Номенклатура КАК Номенклатура,
	|		ПрослеживаемыеТоварыОстатки.СтранаПроисхождения КАК СтранаПроисхождения,
	|		ПрослеживаемыеТоварыОстатки.РНПТ КАК РНПТ,
	|		ПрослеживаемыеТоварыОстатки.КоличествоОстаток КАК Количество,
	|		ПрослеживаемыеТоварыОстатки.КоличествоПрослеживаемостиОстаток КАК КоличествоПрослеживаемости
	|	ИЗ
	|		РегистрНакопления.ПрослеживаемыеТовары.Остатки(
	|				&МоментСписания,
	|				Организация = &Организация
	|					И (Номенклатура) В
	|						(ВЫБРАТЬ
	|							ТаблицаТоваров.Номенклатура КАК Номенклатура
	|						ИЗ
	|							ТаблицаТоваров КАК ТаблицаТоваров)) КАК ПрослеживаемыеТоварыОстатки) КАК ОстаткиРНПТСИсключеннымиДвижениями
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиРНПТСИсключеннымиДвижениями.Номенклатура,
	|	ОстаткиРНПТСИсключеннымиДвижениями.СтранаПроисхождения,
	|	ОстаткиРНПТСИсключеннымиДвижениями.РНПТ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ЕСТЬNULL(ОстаткиРНПТ.Количество, 0) КАК Количество,
	|	ЕСТЬNULL(ОстаткиРНПТ.КоличествоПрослеживаемости, 0) КАК КоличествоПрослеживаемости,
	|	ЕСТЬNULL(ОстаткиРНПТ.РНПТ, ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)) КАК РНПТ,
	|	СвойстваРНПТ.ДатаПоступления КАК ДатаПоступления
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиРНПТ КАК ОстаткиРНПТ
	|		ПО ТаблицаТоваров.Номенклатура = ОстаткиРНПТ.Номенклатура
	|			И ТаблицаТоваров.СтранаПроисхождения = ОстаткиРНПТ.СтранаПроисхождения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СвойстваРНПТ КАК СвойстваРНПТ
	|		ПО (СвойстваРНПТ.Организация = &Организация)
	|			И (ОстаткиРНПТ.РНПТ = СвойстваРНПТ.РНПТ)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаПоступления";
	
	ТаблицаРНПТОстатки = Запрос.Выполнить().Выгрузить();
	
	ТаблицаРНПТОстатки.Индексы.Добавить("Номенклатура");
	ТаблицаРНПТОстатки.Индексы.Добавить("СтранаПроисхождения");
	
	Возврат ТаблицаРНПТОстатки;
	
КонецФункции

Функция ТаблицаРНПТОстаткиВозвратОпт(ДокументОбъект, ТаблицаТоваров, ДокументРеализации)
	// Блокируем регистр ПрослеживаемыеТовары для получения остатков
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ПрослеживаемыеТовары");
	ЭлементБлокировки.УстановитьЗначение("Организация", ДокументОбъект.Организация);
	ЭлементБлокировки.УстановитьЗначение("Период", Новый Диапазон(, ДокументОбъект.Дата));
	ЭлементБлокировки.ИсточникДанных = ТаблицаТоваров;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("СтранаПроисхождения", "СтранаПроисхождения");
	
	// Установим дополнительные ограничения по комиссионным товарам
	Комиссионер = Справочники.Контрагенты.ПустаяСсылка();
	Комитент    = Справочники.Контрагенты.ПустаяСсылка();
	ВидЗапасов = Перечисления.ВидыЗапасовПрослеживаемыхТоваров.ПустаяСсылка();
	ЭлементБлокировки.УстановитьЗначение("Комиссионер", Комиссионер);
	ЭлементБлокировки.УстановитьЗначение("Комитент",    Комитент);
	ЭлементБлокировки.УстановитьЗначение("ВидЗапасов", ВидЗапасов);
	Блокировка.Заблокировать();

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",           ДокументОбъект.Организация);
	Запрос.УстановитьПараметр("ТаблицаТовары",         ТаблицаТоваров);
	Запрос.УстановитьПараметр("ТекущийДокумент",       ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДатаТекущегоДокумента", ДокументОбъект.Дата);
	Запрос.УстановитьПараметр("Комитент",              Комитент);
	Запрос.УстановитьПараметр("Комиссионер",           Комиссионер);
	Запрос.УстановитьПараметр("ВидЗапасов",            ВидЗапасов);
	Запрос.УстановитьПараметр("ОснованиеДляВозврата",  ДокументРеализации);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ТаблицаТовары.Цена КАК Цена
	|ПОМЕСТИТЬ ТаблицаТовары
	|ИЗ
	|	&ТаблицаТовары КАК ТаблицаТовары
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	СтранаПроисхождения,
	|	Цена
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслугТовары.Номенклатура КАК Номенклатура,
	|	РеализацияТоваровУслугТовары.СтранаПроисхождения КАК СтранаПроисхождения,
	|	РеализацияТоваровУслугТовары.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	РеализацияТоваровУслугТовары.Цена КАК Цена,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.Количество КАК Количество,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.РНПТ КАК РНПТ,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|	РеализацияТоваровУслугТовары.Ссылка.Организация КАК Организация
	|ПОМЕСТИТЬ ИсходнаяРеализация
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.СведенияПрослеживаемости КАК РеализацияТоваровУслугСведенияПрослеживаемости
	|			ПО РеализацияТоваровУслугТовары.Ссылка = РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка
	|				И РеализацияТоваровУслугТовары.ИдентификаторСтроки = РеализацияТоваровУслугСведенияПрослеживаемости.ИдентификаторСтроки
	|		ПО ТаблицаТовары.Номенклатура = РеализацияТоваровУслугТовары.Номенклатура
	|			И ТаблицаТовары.СтранаПроисхождения = РеализацияТоваровУслугТовары.СтранаПроисхождения
	|			И ТаблицаТовары.Цена = РеализацияТоваровУслугТовары.Цена
	|ГДЕ
	|	РеализацияТоваровУслугТовары.Ссылка = &ОснованиеДляВозврата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрослеживаемыеТовары.Регистратор КАК Регистратор,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ТаблицаТовары.Цена КАК Цена
	|ПОМЕСТИТЬ ДвиженияПоВозвратам
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПрослеживаемыеТовары КАК ПрослеживаемыеТовары
	|		ПО ТаблицаТовары.Номенклатура = ПрослеживаемыеТовары.Номенклатура
	|			И ТаблицаТовары.СтранаПроисхождения = ПрослеживаемыеТовары.СтранаПроисхождения
	|			И (ПрослеживаемыеТовары.Организация = &Организация)
	|			И (ПрослеживаемыеТовары.Период < &ДатаТекущегоДокумента)
	|			И (ПрослеживаемыеТовары.ОснованиеДляВозврата = &ОснованиеДляВозврата)
	|			И (ПрослеживаемыеТовары.Комитент = &Комитент)
	|			И (ПрослеживаемыеТовары.Комиссионер = &Комиссионер)
	|			И (ПрослеживаемыеТовары.ВидЗапасов = &ВидЗапасов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПрослеживаемыеТовары.Регистратор,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.СтранаПроисхождения,
	|	ТаблицаТовары.Цена
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВозвратТоваровОтПокупателяТовары.Номенклатура КАК Номенклатура,
	|	ВозвратТоваровОтПокупателяСведенияПрослеживаемости.РНПТ КАК РНПТ,
	|	-ВозвратТоваровОтПокупателяСведенияПрослеживаемости.Количество КАК Количество,
	|	-ВозвратТоваровОтПокупателяСведенияПрослеживаемости.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|	ВозвратТоваровОтПокупателяТовары.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ВозвратТоваровОтПокупателяТовары.Ссылка.Организация КАК Организация
	|ПОМЕСТИТЬ РеализацияМинусВозврат
	|ИЗ
	|	ДвиженияПоВозвратам КАК ДвиженияПоВозвратам
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтПокупателя.СведенияПрослеживаемости КАК ВозвратТоваровОтПокупателяСведенияПрослеживаемости
	|			ПО ВозвратТоваровОтПокупателяТовары.Ссылка = ВозвратТоваровОтПокупателяСведенияПрослеживаемости.Ссылка
	|				И ВозвратТоваровОтПокупателяТовары.ИдентификаторСтроки = ВозвратТоваровОтПокупателяСведенияПрослеживаемости.ИдентификаторСтроки
	|		ПО ДвиженияПоВозвратам.Регистратор = ВозвратТоваровОтПокупателяТовары.Ссылка
	|			И ДвиженияПоВозвратам.Номенклатура = ВозвратТоваровОтПокупателяТовары.Номенклатура
	|			И ДвиженияПоВозвратам.СтранаПроисхождения = ВозвратТоваровОтПокупателяТовары.СтранаПроисхождения
	|			И ДвиженияПоВозвратам.Цена = ВозвратТоваровОтПокупателяТовары.Цена
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.РНПТ,
	|	ЕСТЬNULL(ВложенныйЗапрос.Количество, 0) - ЕСТЬNULL(ИсходнаяРеализация.Количество, 0),
	|	ЕСТЬNULL(ВложенныйЗапрос.КоличествоПрослеживаемости, 0) - ЕСТЬNULL(ИсходнаяРеализация.КоличествоПрослеживаемости, 0),
	|	ИсходнаяРеализация.СтранаПроисхождения,
	|	ИсходнаяРеализация.Организация
	|ИЗ
	|	ИсходнаяРеализация КАК ИсходнаяРеализация
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			КорректировкаРеализацииТовары.Номенклатура КАК Номенклатура,
	|			КорректировкаРеализацииСведенияПрослеживаемости.РНПТ КАК РНПТ,
	|			КорректировкаРеализацииСведенияПрослеживаемости.Количество КАК Количество,
	|			КорректировкаРеализацииСведенияПрослеживаемости.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|			КорректировкаРеализацииСведенияПрослеживаемости.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|			КорректировкаРеализацииТовары.СтранаПроисхожденияДоИзменения КАК СтранаПроисхожденияДоИзменения
	|		ИЗ
	|			ДвиженияПоВозвратам КАК ДвиженияПоВозвратам
	|				ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации.Товары КАК КорректировкаРеализацииТовары
	|					ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации.СведенияПрослеживаемости КАК КорректировкаРеализацииСведенияПрослеживаемости
	|					ПО КорректировкаРеализацииТовары.Ссылка = КорректировкаРеализацииСведенияПрослеживаемости.Ссылка
	|						И КорректировкаРеализацииТовары.ИдентификаторСтроки = КорректировкаРеализацииСведенияПрослеживаемости.ИдентификаторСтроки
	|				ПО ДвиженияПоВозвратам.Регистратор = КорректировкаРеализацииТовары.Ссылка
	|					И ДвиженияПоВозвратам.Номенклатура = КорректировкаРеализацииТовары.Номенклатура
	|					И ДвиженияПоВозвратам.СтранаПроисхождения = КорректировкаРеализацииТовары.СтранаПроисхожденияДоИзменения) КАК ВложенныйЗапрос
	|		ПО ИсходнаяРеализация.ИдентификаторСтроки = ВложенныйЗапрос.ИдентификаторСтроки
	|			И ИсходнаяРеализация.РНПТ = ВложенныйЗапрос.РНПТ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИсходнаяРеализация.Номенклатура,
	|	ИсходнаяРеализация.РНПТ,
	|	ИсходнаяРеализация.Количество,
	|	ИсходнаяРеализация.КоличествоПрослеживаемости,
	|	ИсходнаяРеализация.СтранаПроисхождения,
	|	ИсходнаяРеализация.Организация
	|ИЗ
	|	ИсходнаяРеализация КАК ИсходнаяРеализация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияМинусВозврат.Номенклатура КАК Номенклатура,
	|	РеализацияМинусВозврат.РНПТ КАК РНПТ,
	|	СУММА(РеализацияМинусВозврат.Количество) КАК Количество,
	|	СУММА(РеализацияМинусВозврат.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости,
	|	РеализацияМинусВозврат.СтранаПроисхождения КАК СтранаПроисхождения,
	|	РеализацияМинусВозврат.Организация КАК Организация
	|ПОМЕСТИТЬ РеализацияМинусВозвратСводно
	|ИЗ
	|	РеализацияМинусВозврат КАК РеализацияМинусВозврат
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияМинусВозврат.РНПТ,
	|	РеализацияМинусВозврат.Номенклатура,
	|	РеализацияМинусВозврат.СтранаПроисхождения,
	|	РеализацияМинусВозврат.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияМинусВозвратСводно.Номенклатура КАК Номенклатура,
	|	РеализацияМинусВозвратСводно.РНПТ КАК РНПТ,
	|	РеализацияМинусВозвратСводно.Количество КАК Количество,
	|	РеализацияМинусВозвратСводно.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|	РеализацияМинусВозвратСводно.СтранаПроисхождения КАК СтранаПроисхождения,
	|	РеализацияМинусВозвратСводно.Организация КАК Организация
	|ИЗ
	|	РеализацияМинусВозвратСводно КАК РеализацияМинусВозвратСводно
	|ГДЕ
	|	РеализацияМинусВозвратСводно.Количество > 0";
	
	ТаблицаРезультат = Запрос.Выполнить().Выгрузить();
	
	ТаблицаРезультат.Индексы.Добавить("Номенклатура");
	ТаблицаРезультат.Индексы.Добавить("СтранаПроисхождения");
	
	Возврат ТаблицаРезультат;

КонецФункции

Функция ТаблицаРНПТОстаткиВозвратРозница(ДокументОбъект, ТаблицаТоваров)
		
	// Блокируем регистр ПрослеживаемыеТовары для получения остатков
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ПрослеживаемыеТовары");
	ЭлементБлокировки.УстановитьЗначение("Организация", ДокументОбъект.Организация);
	ЭлементБлокировки.УстановитьЗначение("Период", Новый Диапазон(, ДокументОбъект.Дата));
	ЭлементБлокировки.ИсточникДанных = ТаблицаТоваров;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("СтранаПроисхождения", "СтранаПроисхождения");
	
	// Установим дополнительные ограничения по комиссионным товарам
	Комиссионер = Справочники.Контрагенты.ПустаяСсылка();
	Комитент    = Справочники.Контрагенты.ПустаяСсылка();
	ВидЗапасов = Перечисления.ВидыЗапасовПрослеживаемыхТоваров.ПустаяСсылка();
	ЭлементБлокировки.УстановитьЗначение("Комиссионер", Комиссионер);
	ЭлементБлокировки.УстановитьЗначение("Комитент",    Комитент);
	ЭлементБлокировки.УстановитьЗначение("ВидЗапасов", ВидЗапасов);
	Блокировка.Заблокировать();

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",           ДокументОбъект.Организация);
	Запрос.УстановитьПараметр("ТаблицаТоваров",        ТаблицаТоваров);
	Запрос.УстановитьПараметр("ТекущийДокумент",       ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДатаТекущегоДокумента", КонецДня(ДокументОбъект.Дата));
	Запрос.УстановитьПараметр("Комитент",              Комитент);
	Запрос.УстановитьПараметр("Комиссионер",           Комиссионер);
	Запрос.УстановитьПараметр("ВидЗапасов",            ВидЗапасов);
	Запрос.УстановитьПараметр("ДатаРеализации", 
		НачалоДня(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОбъект.Сделка, "Дата")));
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.СтранаПроисхождения КАК СтранаПроисхождения,
	|	&ДатаРеализации КАК ДатаРеализации
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ
	|	&ТаблицаТоваров КАК ТаблицаТоваров
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	СтранаПроисхождения,
	|	ДатаРеализации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияМинусВозврат.Номенклатура КАК Номенклатура,
	|	РеализацияМинусВозврат.СтранаПроисхождения КАК СтранаПроисхождения,
	|	РеализацияМинусВозврат.ДатаРеализации КАК ДатаРеализации,
	|	РеализацияМинусВозврат.РНПТ КАК РНПТ,
	|	СУММА(РеализацияМинусВозврат.Количество) КАК Количество,
	|	СУММА(РеализацияМинусВозврат.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|		ТаблицаТоваров.СтранаПроисхождения КАК СтранаПроисхождения,
	|		ТаблицаТоваров.ДатаРеализации КАК ДатаРеализации,
	|		ЕСТЬNULL(РеализованныеТовары.Количество, 0) КАК Количество,
	|		ЕСТЬNULL(РеализованныеТовары.КоличествоПрослеживаемости, 0) КАК КоличествоПрослеживаемости,
	|		ЕСТЬNULL(РеализованныеТовары.РНПТ, ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)) КАК РНПТ
	|	ИЗ
	|		ТаблицаТоваров КАК ТаблицаТоваров
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПрослеживаемыеТовары КАК РеализованныеТовары
	|			ПО (&Организация = РеализованныеТовары.Организация)
	|				И ТаблицаТоваров.Номенклатура = РеализованныеТовары.Номенклатура
	|				И ТаблицаТоваров.СтранаПроисхождения = РеализованныеТовары.СтранаПроисхождения
	|				И (ТаблицаТоваров.ДатаРеализации = НАЧАЛОПЕРИОДА(РеализованныеТовары.Период, ДЕНЬ))
	|				И (РеализованныеТовары.Регистратор <> &ТекущийДокумент)
	|				И (РеализованныеТовары.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход))
	|				И (РеализованныеТовары.Регистратор ССЫЛКА Документ.ОтчетОРозничныхПродажах)
	|				И (РеализованныеТовары.Комитент = &Комитент)
	|				И (РеализованныеТовары.Комиссионер = &Комиссионер)
	|				И (РеализованныеТовары.ВидЗапасов = &ВидЗапасов)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаТоваров.Номенклатура,
	|		ТаблицаТоваров.СтранаПроисхождения,
	|		ТаблицаТоваров.ДатаРеализации,
	|		ЕСТЬNULL(-ВозвращенныеТовары.Количество, 0),
	|		ЕСТЬNULL(-ВозвращенныеТовары.КоличествоПрослеживаемости, 0),
	|		ЕСТЬNULL(ВозвращенныеТовары.РНПТ, ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка))
	|	ИЗ
	|		ТаблицаТоваров КАК ТаблицаТоваров
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПрослеживаемыеТовары КАК ВозвращенныеТовары
	|			ПО (&Организация = ВозвращенныеТовары.Организация)
	|				И (ВозвращенныеТовары.Период <= &ДатаТекущегоДокумента)
	|				И (ВозвращенныеТовары.Номенклатура = ТаблицаТоваров.Номенклатура)
	|				И (ВозвращенныеТовары.СтранаПроисхождения = ТаблицаТоваров.СтранаПроисхождения)
	|				И (ВозвращенныеТовары.ДатаРеализации = ТаблицаТоваров.ДатаРеализации)
	|				И (ВозвращенныеТовары.Регистратор <> &ТекущийДокумент)
	|				И (ВозвращенныеТовары.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
	|				И (ВозвращенныеТовары.Комитент = &Комитент)
	|				И (ВозвращенныеТовары.Комиссионер = &Комиссионер)
	|				И (ВозвращенныеТовары.ВидЗапасов = &ВидЗапасов)) КАК РеализацияМинусВозврат
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияМинусВозврат.РНПТ,
	|	РеализацияМинусВозврат.СтранаПроисхождения,
	|	РеализацияМинусВозврат.ДатаРеализации,
	|	РеализацияМинусВозврат.Номенклатура
	|
	|УПОРЯДОЧИТЬ ПО
	|	РНПТ";
	
	ТаблицаРезультат = Запрос.Выполнить().Выгрузить();
	
	ТаблицаРезультат.Индексы.Добавить("Номенклатура");
	ТаблицаРезультат.Индексы.Добавить("СтранаПроисхождения");
	ТаблицаРезультат.Индексы.Добавить("ДатаРеализации");
	
	Возврат ТаблицаРезультат;
	
КонецФункции

Функция ТаблицаРНПТИсходныйДокумент(ДокументОбъект, ТаблицаТоваров)
	
	Если ТипЗнч(ДокументОбъект) <> Тип("ДокументОбъект.КорректировкаРеализации") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДокументРеализации = ДокументОбъект.ДокументРеализации;
	Если ТипЗнч(ДокументРеализации) <> Тип("ДокументСсылка.РеализацияТоваровУслуг") 
		И ТипЗнч(ДокументРеализации) <> Тип("ДокументСсылка.КорректировкаРеализации") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ИмяВидаДокумента = ДокументРеализации.Метаданные().Имя;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументРеализации", ДокументРеализации);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РеализацияТоваровУслугСведенияПрослеживаемости.РНПТ КАК РНПТ,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.Количество КАК Количество,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|	РеализацияТоваровУслугТовары.Номенклатура КАК Номенклатура,
	|	РеализацияТоваровУслугТовары.СтранаПроисхождения КАК СтранаПроисхождения,
	|	РеализацияТоваровУслугТовары.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ИЗ
	|	Документ."+ИмяВидаДокумента+".Товары КАК РеализацияТоваровУслугТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ."+ИмяВидаДокумента+".СведенияПрослеживаемости КАК РеализацияТоваровУслугСведенияПрослеживаемости
	|		ПО РеализацияТоваровУслугТовары.Ссылка = РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка
	|			И РеализацияТоваровУслугТовары.ИдентификаторСтроки = РеализацияТоваровУслугСведенияПрослеживаемости.ИдентификаторСтроки
	|ГДЕ
	|	РеализацияТоваровУслугТовары.Ссылка = &ДокументРеализации
	|
	|УПОРЯДОЧИТЬ ПО
	|	РеализацияТоваровУслугСведенияПрослеживаемости.НомерСтроки";
	
	ТаблицаРНПТИсходныйДокумент = Запрос.Выполнить().Выгрузить();
	ТаблицаРНПТИсходныйДокумент.Индексы.Добавить("ИдентификаторСтроки");
	ТаблицаРНПТИсходныйДокумент.Индексы.Добавить("СтранаПроисхождения");
	
	Возврат ТаблицаРНПТИсходныйДокумент;
	
КонецФункции

Процедура СверитьСОстаткамиВПрослеживаемости(ДокументОбъект, Отказ, СтрокаСРНПТ, СтрокаТабличнойЧасти, ТаблицаРНПТОстатки, ИмяТабличнойЧасти,
											ЭтоВыпускПродукции = Ложь, ДокументОснованиеВТЧ = Ложь, ЭтоВозвратКомитенту = Ложь)
	
	ТребуемоеКоличество = СтрокаСРНПТ.Количество;
	ТребуемоеКоличествоПрослеживаемости = СтрокаСРНПТ.КоличествоПрослеживаемости;
	ОтборОстатки = Новый Структура("Номенклатура, СтранаПроисхождения, РНПТ",
	СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.СтранаПроисхождения, СтрокаСРНПТ.РНПТ);
	ОстаткиРНПТ = ТаблицаРНПТОстатки.НайтиСтроки(ОтборОстатки);
	
	Если ОстаткиРНПТ.Количество() = 0 
		ИЛИ ОстаткиРНПТ[0].Количество < ТребуемоеКоличество
		ИЛИ ОстаткиРНПТ[0].КоличествоПрослеживаемости < ТребуемоеКоличествоПрослеживаемости Тогда
		Если ЭтоВыпускПродукции Тогда
			ИмяТабличнойЧасти = "Продукция"; 
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Для целей учета прослеживаемости не списано %1 РНПТ %2 по материалу %3'"),
			Формат(ТребуемоеКоличество, "ЧЦ=15; ЧДЦ=3"),
			СтрокаСРНПТ.РНПТ,
			СтрокаТабличнойЧасти.Номенклатура);
			СтрокаПродукции = ДокументОбъект.Продукция.Найти(СтрокаТабличнойЧасти.КлючСтроки, "КлючСтроки");
			НомерСтроки = ?(СтрокаПродукции = Неопределено, 0,СтрокаПродукции.НомерСтроки-1);
			Поле = "" + ИмяТабличнойЧасти + "["+ Формат(НомерСтроки,"ЧГ=") + "].Материалы";
			
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ДокументОбъект.Ссылка, Поле, , Отказ);
			
		Иначе
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Для целей учета прослеживаемости не списано %1 РНПТ %2'"),
			Формат(ТребуемоеКоличество, "ЧЦ=15; ЧДЦ=3"),
			СтрокаСРНПТ.РНПТ);
			Поле = "" + ИмяТабличнойЧасти + "["+ Формат(СтрокаТабличнойЧасти.НомерСтроки-1,"ЧГ=") + "].РНПТ";
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ДокументОбъект.Ссылка, Поле, "Объект", Отказ);
		КонецЕсли;
		
	Иначе
		Если ОстаткиРНПТ[0].Количество > ТребуемоеКоличество
			И ОстаткиРНПТ[0].КоличествоПрослеживаемости > ТребуемоеКоличествоПрослеживаемости Тогда
			ОстаткиРНПТ[0].Количество = ОстаткиРНПТ[0].Количество - ТребуемоеКоличество;
			ОстаткиРНПТ[0].КоличествоПрослеживаемости = ОстаткиРНПТ[0].КоличествоПрослеживаемости - ТребуемоеКоличествоПрослеживаемости;
		Иначе
			ТаблицаРНПТОстатки.Удалить(ОстаткиРНПТ[0]);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

Процедура СверитьОстаткиСИсходнымДокументомРеализации(СтрокаТабличнойЧасти, ТаблицаРНПТИсходныйДокумент, ТребуемоеКоличество, ТребуемоеКоличествоПрослеживаемости)
	
	Если ТаблицаРНПТИсходныйДокумент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОтборИсходныйДокумент = Новый Структура("ИдентификаторСтроки, СтранаПроисхождения", 
	СтрокаТабличнойЧасти.ИдентификаторСтроки, СтрокаТабличнойЧасти.СтранаПроисхождения);
	ИсходныеРНПТ = ТаблицаРНПТИсходныйДокумент.НайтиСтроки(ОтборИсходныйДокумент);
	Для Каждого ИсходныйРНПТ ИЗ ИсходныеРНПТ Цикл
		Если ТребуемоеКоличество = 0 И ТребуемоеКоличествоПрослеживаемости = 0 Тогда
			Прервать;
		КонецЕсли;
		Если ИсходныйРНПТ.Количество <= ТребуемоеКоличество
			И ИсходныйРНПТ.КоличествоПрослеживаемости <= ТребуемоеКоличествоПрослеживаемости Тогда
			ТребуемоеКоличество = ТребуемоеКоличество - ИсходныйРНПТ.Количество;
			ТребуемоеКоличествоПрослеживаемости = ТребуемоеКоличествоПрослеживаемости - ИсходныйРНПТ.КоличествоПрослеживаемости;
			ТаблицаРНПТИсходныйДокумент.Удалить(ИсходныйРНПТ);
		Иначе
			Если ИсходныйРНПТ.Количество > ТребуемоеКоличество Тогда
				ИсходныйРНПТ.Количество = ИсходныйРНПТ.Количество - ТребуемоеКоличество;
				ТребуемоеКоличество = 0;
			Иначе
				ТребуемоеКоличество = ТребуемоеКоличество - ИсходныйРНПТ.Количество;
			КонецЕсли;
			Если ИсходныйРНПТ.КоличествоПрослеживаемости > ТребуемоеКоличествоПрослеживаемости Тогда
				ИсходныйРНПТ.КоличествоПрослеживаемости = ИсходныйРНПТ.КоличествоПрослеживаемости - ТребуемоеКоличествоПрослеживаемости;
				ТребуемоеКоличествоПрослеживаемости = 0;
			Иначе
				ТребуемоеКоличествоПрослеживаемости = ТребуемоеКоличествоПрослеживаемости - ИсходныйРНПТ.КоличествоПрослеживаемости;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

Процедура ПодобратьРНПТИзИсходногоДокумента(ДокументОбъект, ОсталосьПодобрать, СтрокаТабличнойЧасти, ТаблицаРНПТИсходныйДокумент)
	
	Если ТаблицаРНПТИсходныйДокумент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИсходныеРНПТ = ТаблицаРНПТИсходныйДокумент.НайтиСтроки(Новый Структура("ИдентификаторСтроки, СтранаПроисхождения", 
	СтрокаТабличнойЧасти.ИдентификаторСтроки, СтрокаТабличнойЧасти.СтранаПроисхождения));
	
	Для Каждого ИсходныйРНПТ ИЗ ИсходныеРНПТ Цикл
		Если ОсталосьПодобрать = 0 Тогда
			Прервать;
		КонецЕсли;
		
		Если ОсталосьПодобрать >= ИсходныйРНПТ.Количество Тогда
			НоваяСтрока = ДокументОбъект.СведенияПрослеживаемости.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ИсходныйРНПТ);
			НоваяСтрока.ИдентификаторСтроки = СтрокаТабличнойЧасти.ИдентификаторСтроки;
			ОсталосьПодобрать = ОсталосьПодобрать - ИсходныйРНПТ.Количество;
			ТаблицаРНПТИсходныйДокумент.Удалить(ИсходныйРНПТ);
		Иначе
			НоваяСтрока = ДокументОбъект.СведенияПрослеживаемости.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ИсходныйРНПТ);
			НоваяСтрока.ИдентификаторСтроки = СтрокаТабличнойЧасти.ИдентификаторСтроки;
			НоваяСтрока.Количество = ОсталосьПодобрать;
			
			МассивКоэффициентов = Новый Массив;
			МассивКоэффициентов.Добавить(ИсходныйРНПТ.Количество - ОсталосьПодобрать);
			МассивКоэффициентов.Добавить(ОсталосьПодобрать);
			МассивСумм = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(
			ИсходныйРНПТ.КоличествоПрослеживаемости, МассивКоэффициентов, 11);
			
			НоваяСтрока.КоличествоПрослеживаемости = МассивСумм[1];
			
			ИсходныйРНПТ.Количество = ИсходныйРНПТ.Количество - ОсталосьПодобрать;
			ИсходныйРНПТ.КоличествоПрослеживаемости = ИсходныйРНПТ.КоличествоПрослеживаемости - МассивСумм[1];
			
			ОсталосьПодобрать = 0;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ПодготовитьПараметрыСвойстваРНПТ(ТаблицаРНПТ)
	
	Параметры = Новый Структура;
	
	// Подготовка таблицы реализованных товаров:
	СписокОбязательныхКолонок = ""
	+ "РНПТ"; // <СправочникСсылка.НомераГТД>
	
	Параметры.Вставить("ТаблицаРНПТ", ПолучитьТаблицуПараметровПроведения(
		ТаблицаРНПТ, СписокОбязательныхКолонок));
		
	Возврат Параметры;
	
КонецФункции

Функция ПодготовитьПараметрыДвиженияТоваров(ТаблицаТовары)
	
	Параметры = Новый Структура;
	
	СписокОбязательныхКолонок = ""
	+ "Номенклатура,"                // <СправочникСсылка.Номенклатура>
	+ "СтранаПроисхождения,"         // <СправочникСсылка.СтраныМира>
	+ "Комитент,"                    // <СправочникСсылка.Контрагенты>
	+ "ВидЗапасов,"                  // <ПеречислениеСсылка.ВидыЗапасовПрослеживаемыхТоваров>
	+ "Комиссионер,"                 // <СправочникСсылка.Контрагенты>
	+ "РНПТ,"                        // <СправочникСсылка.НомераГТД>
	+ "Количество,"                  // <Число,15,3> - количество
	+ "КоличествоПрослеживаемости,"  // <Число,26,11> - количество в единицах прослеживаемости
	+ "ДатаРеализации,"              // <Дата>
	+ "ОснованиеДляВозврата";        // <ДокументСсылка.РеализацияТоваровИУслуг>,<ДокументСсылка.ПоступлениеТоваровУслуг> 
	
	Параметры.Вставить("Товары", ПолучитьТаблицуПараметровПроведения(
		ТаблицаТовары, СписокОбязательныхКолонок));
		
	Возврат Параметры;
	
КонецФункции

Функция ПодготовитьПараметрыОперацииСПрослеживаемымиТоварами(ТаблицаОперации)
	
	Параметры = Новый Структура;
	
	// Подготовка таблицы операций с прослеживаемыми товарами:
	СписокОбязательныхКолонок = ""
	+ "ОтражениеВОтчетности,"          // <ПеречислениеСсылка.ПорядокОтраженияВОтчетностиПоПрослеживаемости> - отчетная форма операции
	+ "ПериодОперации,"                // <Дата> - начало квартала в котором отражается операция
	+ "РНПТ,"                          // <СправочникСсылка.НомераГТД>
	+ "ДокументОперации,"              // <ДокументСсылка>
	+ "Контрагент,"                    // <СправочникСсылка.Контрагенты>
	+ "Номенклатура,"                  // <СправочникСсылка.Номенклатура>
	+ "Количество,"                    // <Число,15,3> - количество
	+ "КоличествоПрослеживаемости,"    // <Число,26,11> - количество в единицах прослеживаемости
	+ "СуммаБезНДС,"                   // <Число,15,2>
	+ "ТипДокументаВПрослеживаемости," // <СправочникСсылка.ТипыДокументов>
	+ "НомерДокумента,"                // <Строка, 11>
	+ "ДатаДокумента,"                 // <Дата>
	+ "КодОперации";                   // <СправочникСсылка.КодыОперацийПрослеживаемости>
	
	Параметры.Вставить("Операции", ПолучитьТаблицуПараметровПроведения(
		ТаблицаОперации, СписокОбязательныхКолонок));
	
	Возврат Параметры;
	
КонецФункции

Функция ПодготовитьТаблицуРНПТ(ТаблицаТоваров, ТаблицаРеквизитов, Отказ)
	
	Если Не ЗначениеЗаполнено(ТаблицаТоваров)
	 Или Не ЗначениеЗаполнено(ТаблицаРеквизитов) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыТаблицыРНПТ(ТаблицаТоваров, ТаблицаРеквизитов);
	Реквизиты = Параметры.Реквизиты[0];
	
	ТаблицаРНПТ = ПодготовитьТаблицуРНПТпоТовару(Реквизиты, Параметры.ТаблицаТоваров, Отказ);
	
	Возврат ТаблицаРНПТ;
	
КонецФункции

Процедура СформироватьДвиженияПрослеживаемостьТоваров(ТаблицаДвиженийРНПТ, Движения, Отказ) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ТаблицаДвиженийРНПТ) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из ТаблицаДвиженийРНПТ Цикл
		Запись = Движения.ПрослеживаемыеТовары.ДобавитьПриход();
		ЗаполнитьЗначенияСвойств(Запись, СтрокаТаблицы);
	КонецЦикла;
	
	Движения.ПрослеживаемыеТовары.Записывать = Истина;

КонецПроцедуры

Процедура СформироватьДвиженияГТД(ТаблицаДвиженийГТД, Движения, Отказ) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ТаблицаДвиженийГТД) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаГТД Из ТаблицаДвиженийГТД Цикл
		
// УПП		Проводка = Движения.Хозрасчетный.Добавить();
// УПП		Проводка.Период = СтрокаГТД.Период;
// УПП		Проводка.Организация = СтрокаГТД.Организация;
// УПП		Проводка.Содержание  = "Списание ГТД";
// УПП		Проводка.СчетДт = ПланыСчетов.Хозрасчетный.ГТД;
// УПП		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "Номенклатура", СтрокаГТД.Номенклатура);
// УПП		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "НомераГТД", СтрокаГТД.НомерГТД);
// УПП		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "СтраныПроисхождения", СтрокаГТД.СтранаПроисхождения);
// УПП		Проводка.КоличествоДт = -СтрокаГТД.Количество;
		
	КонецЦикла;
	
// УПП	Движения.Хозрасчетный.Записывать = Истина;

КонецПроцедуры

Функция ПодготовитьТаблицуОстатковГТД(ТаблицаТоваров, ТаблицаРеквизитов, Отказ)
	
	Если Не ЗначениеЗаполнено(ТаблицаТоваров)
	 Или Не ЗначениеЗаполнено(ТаблицаРеквизитов) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыТаблицыГТД(ТаблицаТоваров, ТаблицаРеквизитов);
	Реквизиты = Параметры.Реквизиты[0];
	
	Если Не ЗначениеЗаполнено(Реквизиты.РНПТ)
		ИЛИ ТипЗнч(Реквизиты.Регистратор) <> Тип("ДокументСсылка.УведомлениеОбОстаткахПрослеживаемыхТоваров") Тогда

		Возврат Неопределено;
	КонецЕсли;
	
	ТаблицаГТД = ПодготовитьТаблицуОстатковГТДПоТовару(Реквизиты, Параметры.ТаблицаТоваров, Отказ);
	
	Возврат ТаблицаГТД;
	
КонецФункции

Функция ПодготовитьТаблицуРНПТпоТовару(Реквизиты, ТаблицаТоваров, Отказ)

	ТаблицаТоваровСРНПТ = ТаблицаТоваров.СкопироватьКолонки();
	ТаблицаТоваровСРНПТ.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаТоваровСРНПТ.Колонки.Добавить("РНПТ", Новый ОписаниеТипов("СправочникСсылка.НомераГТД"));
	ТаблицаТоваровСРНПТ.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	
	Если Не ЗначениеЗаполнено(Реквизиты.РНПТ) Тогда
		Возврат ТаблицаТоваровСРНПТ;
	КонецЕсли;
	
	Для каждого ТекущаяСтрока Из ТаблицаТоваров Цикл
		
		НоваяСтрока = ТаблицаТоваровСРНПТ.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Реквизиты);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		
	КонецЦикла;
	
	Возврат ТаблицаТоваровСРНПТ;
	
КонецФункции

Функция ПодготовитьТаблицуОстатковГТДПоТовару(Реквизиты, ТаблицаТоваров, Отказ)

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.СтранаПроисхождения КАК СтранаПроисхождения
	|ПОМЕСТИТЬ ВТ_ТаблицаТоваров
	|ИЗ
	|	&ТаблицаТоваров КАК ТаблицаТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Счет КАК Счет,
	|	ВТ_ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ХозрасчетныйОстатки.Субконто2 КАК НомерГТД,
	|	ВТ_ТаблицаТоваров.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ХозрасчетныйОстатки.Организация КАК Организация,
	|	ХозрасчетныйОстатки.Подразделение КАК Подразделение,
	|	ХозрасчетныйОстатки.Валюта КАК Валюта,
	|	ХозрасчетныйОстатки.КоличествоОстаток КАК Количество
	|ИЗ
	|	ВТ_ТаблицаТоваров КАК ВТ_ТаблицаТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(&МоментСписания, Счет = &СчетГТД, , Организация = &Организация) КАК ХозрасчетныйОстатки
	|		ПО ВТ_ТаблицаТоваров.Номенклатура = ХозрасчетныйОстатки.Субконто1
	|			И ВТ_ТаблицаТоваров.СтранаПроисхождения = ХозрасчетныйОстатки.Субконто3";
	
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	МоментСписания = Новый Граница(Реквизиты.Регистратор.МоментВремени(), ВидГраницы.Исключая);
	Запрос.УстановитьПараметр("МоментСписания", МоментСписания);
	Запрос.УстановитьПараметр("СчетГТД", ПланыСчетов.Хозрасчетный.ГТД);
	Запрос.УстановитьПараметр("ТаблицаТоваров", ТаблицаТоваров); 
	
	ТаблицаОстатковГТД = Запрос.Выполнить().Выгрузить();
	ТаблицаОстатковГТД.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	ТаблицаОстатковГТД.ЗаполнитьЗначения(Реквизиты.Период, "Период");
	
	Возврат ТаблицаОстатковГТД;
	
КонецФункции

Функция ПодготовитьПараметрыТаблицыГТД(ТаблицаТоваров, ТаблицаРеквизитов)

	//Параметры = Новый Структура;
	//
	//// Подготовка таблицы Параметры.ТаблицаТоваров
	//
	//СписокОбязательныхКолонок = ""
	//	+ "Номенклатура,"				// <СправочникСсылка.*> - списываемая номенклатура
	//	+ "СтранаПроисхождения,";		// <СправочникСсылка.СтраныМира> - страна происхождения
	//	
	//Параметры.Вставить("ТаблицаТоваров", ПолучитьТаблицуПараметровПроведения(
	//	ТаблицаТоваров, СписокОбязательныхКолонок));
	//	
	//// Подготовка таблицы Параметры.Реквизиты
	//
	//СписокОбязательныхКолонок = ""
	//	+ "Период,"				// <Дата> - период движений - дата документа
	//	+ "Регистратор,"		// <ДокументСсылка.*> - документ-регистратор движений
	//	+ "РНПТ,"				// <СправочникСсылка.НомераГТД> - номер гтд
	//	+ "Организация,";		// <СправочникСсылка.Организации> - организация
	//	
	//Параметры.Вставить("Реквизиты", ПолучитьТаблицуПараметровПроведения(
	//	ТаблицаРеквизитов, СписокОбязательныхКолонок));
	//	
	//Возврат Параметры;

КонецФункции

Функция ПодготовитьПараметрыТаблицыРНПТ(ТаблицаТоваров, ТаблицаРеквизитов)

	//Параметры = Новый Структура;
	//
	//// Подготовка таблицы Параметры.ТаблицаТоваров
	//	
	//СписокОбязательныхКолонок = ""
	//	+ "НомерСтроки,"                              // <Число> - номер строки в таблице товаров
	//	+ "Номенклатура,"                             // <СправочникСсылка.Номенклатура> - номенклатура
	//	+ "Количество,"		                          // <Число ,15,3> - количество
	//	+ "КоличествоПрослеживаемости,"               // <Число, 15,3> - количество по прослеживаемости
	//	+ "Сумма,"		                              // <Число, 15,3> - сумма
	//	+ "СтранаПроисхождения";                      // <СправочникСсылка.СтраныМира> - страна происхождения товара
	//	
	//Параметры.Вставить("ТаблицаТоваров", ПолучитьТаблицуПараметровПроведения(
	//	ТаблицаТоваров, СписокОбязательныхКолонок));
	//	
	//// Подготовка таблицы Параметры.Реквизиты
	//
	//СписокОбязательныхКолонок = ""
	//	+ "Период,"							// <Дата> - период движений - дата документа
	//	+ "Регистратор,"					// <ДокументСсылка.*> - документ-регистратор движений
	//	+ "Организация,"					// <СправочникСсылка.Организации> - организация
	//	+ "РНПТ";							// <ОпределяемыеТипы.РНПТ> - РНПТ
	//	
	//Параметры.Вставить("Реквизиты", ПолучитьТаблицуПараметровПроведения(
	//	ТаблицаРеквизитов, СписокОбязательныхКолонок));
	//	
	//Возврат Параметры;

КонецФункции

Функция ТаблицаРНПТОстаткиВозвратПоставщику(ДокументОбъект, ТаблицаТоваров, УказыватьСделкуВСтроке)
		
	// Блокируем регистр ПрослеживаемыеТовары для получения остатков
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ПрослеживаемыеТовары");
	ЭлементБлокировки.УстановитьЗначение("Организация", ДокументОбъект.Организация);
	ЭлементБлокировки.УстановитьЗначение("Период", Новый Диапазон(, ДокументОбъект.Дата));
	ЭлементБлокировки.ИсточникДанных = ТаблицаТоваров;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("СтранаПроисхождения", "СтранаПроисхождения");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Комитент", "Контрагент");
	
	// Установим дополнительные ограничения по комиссионным товарам
	Комиссионер = Справочники.Контрагенты.ПустаяСсылка();
	ЭлементБлокировки.УстановитьЗначение("Комиссионер", Комиссионер);

	Блокировка.Заблокировать();

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",            ДокументОбъект.Организация);
	КолонкаСделка = ТаблицаТоваров.Колонки.Найти("Заказ");

	Если ТаблицаТоваров.Колонки.Найти("Сделка") = Неопределено И КолонкаСделка <> Неопределено Тогда
		КолонкаСделка.Имя = "Сделка";
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ТаблицаТоваров",         ТаблицаТоваров);
	Запрос.УстановитьПараметр("ТекущийДокумент",        ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДатаТекущегоДокумента",  КонецДня(ДокументОбъект.Дата));
	Запрос.УстановитьПараметр("Комиссионер",            Комиссионер);
	Запрос.УстановитьПараметр("УказыватьСделкуВСтроке", УказыватьСделкуВСтроке);
	Запрос.УстановитьПараметр("Сделка",                 ДокументОбъект.Сделка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.СтранаПроисхождения КАК СтранаПроисхождения
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ
	|	&ТаблицаТоваров КАК ТаблицаТоваров
	|ГДЕ
	|	ТаблицаТоваров.ПрослеживаемыйТовар = ИСТИНА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	СтранаПроисхождения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступленияМинусВозврат.Номенклатура КАК Номенклатура,
	|	ПоступленияМинусВозврат.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ПоступленияМинусВозврат.РНПТ КАК РНПТ,
	|	СУММА(ПоступленияМинусВозврат.Количество) КАК Количество,
	|	СУММА(ПоступленияМинусВозврат.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости
	|ПОМЕСТИТЬ ПоступленияМинусВозвратСводно
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|		ТаблицаТоваров.СтранаПроисхождения КАК СтранаПроисхождения,
	|		ЕСТЬNULL(ПоступившиеТовары.Количество, 0) КАК Количество,
	|		ЕСТЬNULL(ПоступившиеТовары.КоличествоПрослеживаемости, 0) КАК КоличествоПрослеживаемости,
	|		ЕСТЬNULL(ПоступившиеТовары.РНПТ, ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)) КАК РНПТ
	|	ИЗ
	|		ТаблицаТоваров КАК ТаблицаТоваров
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПрослеживаемыеТовары КАК ПоступившиеТовары
	|			ПО (&Организация = ПоступившиеТовары.Организация)
	|				И ТаблицаТоваров.Номенклатура = ПоступившиеТовары.Номенклатура
	|				И ТаблицаТоваров.СтранаПроисхождения = ПоступившиеТовары.СтранаПроисхождения) КАК ПоступленияМинусВозврат
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступленияМинусВозврат.РНПТ,
	|	ПоступленияМинусВозврат.СтранаПроисхождения,
	|	ПоступленияМинусВозврат.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступленияМинусВозвратСводно.Номенклатура КАК Номенклатура,
	|	ПоступленияМинусВозвратСводно.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ПоступленияМинусВозвратСводно.РНПТ КАК РНПТ,
	|	ПоступленияМинусВозвратСводно.Количество КАК Количество,
	|	ПоступленияМинусВозвратСводно.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости
	|ИЗ
	|	ПоступленияМинусВозвратСводно КАК ПоступленияМинусВозвратСводно
	|ГДЕ
	|	ПоступленияМинусВозвратСводно.Количество > 0";
	
	ТаблицаРезультат = Запрос.Выполнить().Выгрузить();
	
	ТаблицаРезультат.Индексы.Добавить("Номенклатура");
	ТаблицаРезультат.Индексы.Добавить("СтранаПроисхождения");
	
	Возврат ТаблицаРезультат;
	
КонецФункции

Функция ТаблицаРНПТОстаткиВозвратКомиссия(ДокументОбъект, ТаблицаТоваров)
		
	// Блокируем регистр ПрослеживаемыеТовары для получения остатков
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ПрослеживаемыеТовары");
	ЭлементБлокировки.УстановитьЗначение("Организация", ДокументОбъект.Организация);
	ЭлементБлокировки.УстановитьЗначение("Период", Новый Диапазон(, ДокументОбъект.Дата));
	ЭлементБлокировки.ИсточникДанных = ТаблицаТоваров;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("СтранаПроисхождения", "СтранаПроисхождения");

	// Установим дополнительные ограничения по комиссионным товарам
	Комитент = Справочники.Контрагенты.ПустаяСсылка();
	ВидЗапасов = Перечисления.ВидыЗапасовПрослеживаемыхТоваров.ПустаяСсылка();
	ЭлементБлокировки.УстановитьЗначение("Комиссионер", ДокументОбъект.Контрагент);
	ЭлементБлокировки.УстановитьЗначение("Комитент",    Комитент);
	ЭлементБлокировки.УстановитьЗначение("ВидЗапасов",  ВидЗапасов);

	Блокировка.Заблокировать();

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",           ДокументОбъект.Организация);
	Запрос.УстановитьПараметр("Товары",                ДокументОбъект.Товары.Выгрузить());
	Запрос.УстановитьПараметр("ТоварыВозврат",         ТаблицаТоваров);
	Запрос.УстановитьПараметр("ТаблицаПокупатели",     ДокументОбъект.Покупатели.Выгрузить());
	Запрос.УстановитьПараметр("ТаблицаРНПТ",           ДокументОбъект.СведенияПрослеживаемости.Выгрузить());
	Запрос.УстановитьПараметр("Комиссионер",           ДокументОбъект.Контрагент);
	Запрос.УстановитьПараметр("Комитент",              Комитент);
	Запрос.УстановитьПараметр("ВидЗапасов",            ВидЗапасов);
	Запрос.УстановитьПараметр("ДатаТекущегоДокумента", ДокументОбъект.Дата);

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.СтранаПроисхождения КАК СтранаПроисхождения,
	|	Товары.КлючСтроки КАК КлючСтроки,
	|	Товары.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ТаблицаТовары
	|ИЗ
	|	&Товары КАК Товары
	|ГДЕ
	|	Товары.ПрослеживаемыйТовар = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТоварыВозврат.Номенклатура КАК Номенклатура,
	|	ТоварыВозврат.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ТоварыВозврат.ДокументРеализации КАК ДокументРеализации,
	|	ТоварыВозврат.ПоТекущемуДокументу КАК ПоТекущемуДокументу
	|ПОМЕСТИТЬ ТаблицаТоваровВозврат
	|ИЗ
	|	&ТоварыВозврат КАК ТоварыВозврат
	|ГДЕ
	|	ТоварыВозврат.ПрослеживаемыйТовар = ИСТИНА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	СтранаПроисхождения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаРНПТ.РНПТ КАК РНПТ,
	|	ТаблицаРНПТ.Количество КАК Количество,
	|	ТаблицаРНПТ.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|	ТаблицаРНПТ.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ТаблицаСведений
	|ИЗ
	|	&ТаблицаРНПТ КАК ТаблицаРНПТ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПокупатели.Покупатель КАК Покупатель,
	|	ТаблицаПокупатели.ВыставленСФ КАК ВыставленСФ,
	|	ТаблицаПокупатели.ДатаСФ КАК ДатаСФ,
	|	ТаблицаПокупатели.СчетФактура КАК СчетФактура,
	|	ТаблицаПокупатели.КлючСтроки КАК КлючСтроки,
	|	ТаблицаПокупатели.НомерСФ КАК НомерСФ
	|ПОМЕСТИТЬ ТаблицаПокупателей
	|ИЗ
	|	&ТаблицаПокупатели КАК ТаблицаПокупатели
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрослеживаемыеТовары.Регистратор КАК Регистратор,
	|	ТаблицаТоваровВозврат.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваровВозврат.СтранаПроисхождения КАК СтранаПроисхождения
	|ПОМЕСТИТЬ ДокументыВозврата
	|ИЗ
	|	ТаблицаТоваровВозврат КАК ТаблицаТоваровВозврат
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрослеживаемыеТовары КАК ПрослеживаемыеТовары
	|		ПО ТаблицаТоваровВозврат.Номенклатура = ПрослеживаемыеТовары.Номенклатура
	|			И ТаблицаТоваровВозврат.СтранаПроисхождения = ПрослеживаемыеТовары.СтранаПроисхождения
	|			И ТаблицаТоваровВозврат.ДокументРеализации = ПрослеживаемыеТовары.ОснованиеДляВозврата
	|ГДЕ
	|	ПрослеживаемыеТовары.Период = &ДатаТекущегоДокумента
	|	И ПрослеживаемыеТовары.Комиссионер = &Комиссионер
	|	И ПрослеживаемыеТовары.ВидЗапасов = &ВидЗапасов
	|	И ПрослеживаемыеТовары.Комитент = &Комитент
	|	И ПрослеживаемыеТовары.Организация = &Организация
	|	И ТаблицаТоваровВозврат.ПоТекущемуДокументу = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ТаблицаСведений.РНПТ КАК РНПТ,
	|	ТаблицаСведений.Количество КАК Количество,
	|	ТаблицаСведений.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости
	|ПОМЕСТИТЬ РеализацияПоТекущемуДокументу
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаСведений КАК ТаблицаСведений
	|		ПО ТаблицаТовары.ИдентификаторСтроки = ТаблицаСведений.ИдентификаторСтроки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаПокупателей КАК ТаблицаПокупателей
	|		ПО ТаблицаТовары.КлючСтроки = ТаблицаПокупателей.КлючСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтчетКомиссионераОПродажахСведенияПрослеживаемости.РНПТ КАК РНПТ,
	|	ОтчетКомиссионераОПродажахСведенияПрослеживаемости.Количество КАК Количество,
	|	ОтчетКомиссионераОПродажахСведенияПрослеживаемости.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|	ТаблицаТоваровВозврат.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ТаблицаТоваровВозврат.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваровВозврат.ДокументРеализации КАК ДокументРеализации,
	|	ТаблицаТоваровВозврат.ПоТекущемуДокументу КАК ПоТекущемуДокументу
	|ПОМЕСТИТЬ РеализацияМинусВозврат
	|ИЗ
	|	ТаблицаТоваровВозврат КАК ТаблицаТоваровВозврат
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионераОПродажах.Товары КАК ОтчетКомиссионераОПродажахТовары
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионераОПродажах.СведенияПрослеживаемости КАК ОтчетКомиссионераОПродажахСведенияПрослеживаемости
	|			ПО ОтчетКомиссионераОПродажахТовары.Ссылка = ОтчетКомиссионераОПродажахСведенияПрослеживаемости.Ссылка
	|				И ОтчетКомиссионераОПродажахТовары.ИдентификаторСтроки = ОтчетКомиссионераОПродажахСведенияПрослеживаемости.ИдентификаторСтроки
	|		ПО ТаблицаТоваровВозврат.ДокументРеализации = ОтчетКомиссионераОПродажахТовары.Ссылка
	|			И ТаблицаТоваровВозврат.Номенклатура = ОтчетКомиссионераОПродажахТовары.Номенклатура
	|			И ТаблицаТоваровВозврат.СтранаПроисхождения = ОтчетКомиссионераОПродажахТовары.СтранаПроисхождения
	|ГДЕ
	|	ТаблицаТоваровВозврат.ПоТекущемуДокументу = ЛОЖЬ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтчетКомиссионераОПродажахСведенияПрослеживаемости.РНПТ,
	|	-ОтчетКомиссионераОПродажахСведенияПрослеживаемости.Количество,
	|	-ОтчетКомиссионераОПродажахСведенияПрослеживаемости.КоличествоПрослеживаемости,
	|	ДокументыВозврата.СтранаПроисхождения,
	|	ДокументыВозврата.Номенклатура,
	|	ДокументыВозврата.Регистратор,
	|	ЛОЖЬ
	|ИЗ
	|	ДокументыВозврата КАК ДокументыВозврата
	|		ПОЛНОЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионераОПродажах.ТоварыВозвращенные КАК ОтчетКомиссионераОПродажахТоварыВозвращенные
	|			ПОЛНОЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионераОПродажах.СведенияПрослеживаемости КАК ОтчетКомиссионераОПродажахСведенияПрослеживаемости
	|			ПО ОтчетКомиссионераОПродажахТоварыВозвращенные.Ссылка = ОтчетКомиссионераОПродажахСведенияПрослеживаемости.Ссылка
	|				И ОтчетКомиссионераОПродажахТоварыВозвращенные.ИдентификаторСтроки = ОтчетКомиссионераОПродажахСведенияПрослеживаемости.ИдентификаторСтроки
	|		ПО ДокументыВозврата.Регистратор = ОтчетКомиссионераОПродажахТоварыВозвращенные.Ссылка
	|			И ДокументыВозврата.Номенклатура = ОтчетКомиссионераОПродажахТоварыВозвращенные.Номенклатура
	|			И ДокументыВозврата.СтранаПроисхождения = ОтчетКомиссионераОПродажахТоварыВозвращенные.СтранаПроисхождения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеализацияПоТекущемуДокументу.РНПТ,
	|	РеализацияПоТекущемуДокументу.Количество,
	|	РеализацияПоТекущемуДокументу.КоличествоПрослеживаемости,
	|	РеализацияПоТекущемуДокументу.СтранаПроисхождения,
	|	РеализацияПоТекущемуДокументу.Номенклатура,
	|	ЗНАЧЕНИЕ(Документ.ОтчетКомиссионераОПродажах.ПустаяСсылка),
	|	ТаблицаТоваровВозврат.ПоТекущемуДокументу
	|ИЗ
	|	ТаблицаТоваровВозврат КАК ТаблицаТоваровВозврат
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РеализацияПоТекущемуДокументу КАК РеализацияПоТекущемуДокументу
	|		ПО ТаблицаТоваровВозврат.Номенклатура = РеализацияПоТекущемуДокументу.Номенклатура
	|			И ТаблицаТоваровВозврат.СтранаПроисхождения = РеализацияПоТекущемуДокументу.СтранаПроисхождения
	|ГДЕ
	|	ТаблицаТоваровВозврат.ПоТекущемуДокументу = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияМинусВозврат.РНПТ КАК РНПТ,
	|	СУММА(РеализацияМинусВозврат.Количество) КАК Количество,
	|	СУММА(РеализацияМинусВозврат.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости,
	|	РеализацияМинусВозврат.СтранаПроисхождения КАК СтранаПроисхождения,
	|	РеализацияМинусВозврат.Номенклатура КАК Номенклатура,
	|	РеализацияМинусВозврат.ДокументРеализации КАК ДокументРеализации,
	|	РеализацияМинусВозврат.ПоТекущемуДокументу КАК ПоТекущемуДокументу
	|ПОМЕСТИТЬ РеализацияМинусВозвратСводно
	|ИЗ
	|	РеализацияМинусВозврат КАК РеализацияМинусВозврат
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияМинусВозврат.РНПТ,
	|	РеализацияМинусВозврат.СтранаПроисхождения,
	|	РеализацияМинусВозврат.Номенклатура,
	|	РеализацияМинусВозврат.ДокументРеализации,
	|	РеализацияМинусВозврат.ПоТекущемуДокументу
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияМинусВозвратСводно.РНПТ КАК РНПТ,
	|	РеализацияМинусВозвратСводно.Количество КАК Количество,
	|	РеализацияМинусВозвратСводно.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|	РеализацияМинусВозвратСводно.СтранаПроисхождения КАК СтранаПроисхождения,
	|	РеализацияМинусВозвратСводно.Номенклатура КАК Номенклатура,
	|	РеализацияМинусВозвратСводно.ДокументРеализации КАК ДокументРеализации,
	|	РеализацияМинусВозвратСводно.ПоТекущемуДокументу КАК ПоТекущемуДокументу
	|ИЗ
	|	РеализацияМинусВозвратСводно КАК РеализацияМинусВозвратСводно
	|ГДЕ
	|	РеализацияМинусВозвратСводно.Количество > 0";
	
	ТаблицаРезультат = Запрос.Выполнить().Выгрузить();
	
	ТаблицаРезультат.Индексы.Добавить("Номенклатура");
	ТаблицаРезультат.Индексы.Добавить("СтранаПроисхождения");
	
	Возврат ТаблицаРезультат;
	
КонецФункции

Функция ПолучитьТаблицуПараметровПроведения(ИсходнаяТаблица, СписокКолонок) Экспорт

	Если ИсходнаяТаблица = Неопределено Тогда
		
		ТаблицаРезультат = Новый ТаблицаЗначений;
		Колонки = Новый Структура(СписокКолонок);
		Для каждого Колонка Из Колонки Цикл
			ТаблицаРезультат.Колонки.Добавить(Колонка.Ключ);
		КонецЦикла;
		Возврат ТаблицаРезультат;

	Иначе

		Возврат ИсходнаяТаблица.Скопировать(, СписокКолонок);

	КонецЕсли;

КонецФункции

Функция ТекстРазделителяЗапросовПакета() Экспорт

	ТекстРазделителя =
	"
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";

	Возврат ТекстРазделителя;

КонецФункции

Функция СтруктуруВТаблицуЗначений(СтруктураИсточник) Экспорт

	ТаблицаПриемник = Новый ТаблицаЗначений;
	СтрокаТаблицы = ТаблицаПриемник.Добавить();
	
	Для Каждого СтрокаСтруктуры Из СтруктураИсточник Цикл
		ТаблицаПриемник.Колонки.Добавить(СтрокаСтруктуры.Ключ);
		СтрокаТаблицы[СтрокаСтруктуры.Ключ] = СтрокаСтруктуры.Значение;
	КонецЦикла;

	Возврат ТаблицаПриемник;

КонецФункции

Функция ЭтоКонтрагентИзЕАЭС(Контрагент) Экспорт
	
	СтранаРегистрации = 
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "СтранаРегистрации");
	
		Если СтранаРегистрации <> Справочники.КлассификаторСтранМира.ПустаяСсылка() Тогда 
		
		Возврат Справочники.КлассификаторСтранМира.ГосударствоЧленТаможенногоСоюза(СтранаРегистрации);
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти