#Область СлужебныйПрограммныйИнтерфейс

// Процедура вызывается при выборе ТНВЭД и номенклатуры в документе
// Уведомление об остатках прослеживаемых товаров
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма из которой вызвана формы выбора
//  Выбранное значение - выбранное значение в форме выбора
//  ИсточникВыбора - ФормаКлиенскогоПриложения - форма в которой выбрали значение
//
Процедура ОбработкаВыбораУведомления(Форма, ВыбранноеЗначение, ИсточникВыбора) Экспорт
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.Номенклатура") Тогда
		
		ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
		Если ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;

		ТекущиеДанные.Номенклатура = ВыбранноеЗначение;
		
		ДанныеСтрокиТаблицы = Новый Структура(
			"Номенклатура, ЕдиницаИзмерения, Количество, 
			|КодОКПД2, ЕдиницаИзмеренияПрослеживаемости,
			|КоличествоПрослеживаемости, Сумма");
		
		ЗаполнитьЗначенияСвойств(ДанныеСтрокиТаблицы, ТекущиеДанные);
		
		ДанныеОбъекта = Новый Структура("КодТНВЭД");
		
		ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Форма.Объект);
		
		ПрослеживаемостьФормыВызовСервера.ТоварыНоменклатураПриИзмененииНаСервере(
			ДанныеСтрокиТаблицы,
			ДанныеОбъекта);
	
		ЗаполнитьЗначенияСвойств(ТекущиеДанные, ДанныеСтрокиТаблицы);
		
		ТоварыКоличествоПриИзмененииУведомления(Форма, ТекущиеДанные);
		
	ИначеЕсли ИсточникВыбора.ИмяФормы = 
			"Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.Форма.ФормаДополнительныхСведений" Тогда
		
		ЗаполнитьЗначенияСвойств(Объект, ВыбранноеЗначение,,"Продавцы");
		
		Объект.Продавцы.Очистить();
		
		Для каждого ТекущаяСтрока Из ВыбранноеЗначение.Продавцы Цикл
			
			НоваяСтрока = Объект.Продавцы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
			
		КонецЦикла;
		
		Форма.Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры 

// Процедура вызывается при изменении количества в документе 
// Уведомление об остатках прослеживаемых товаров
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма владелец
//  ТекущиеДанные - ДанныеФормыЭлементКоллекции - текущая строка в таблице товары
//
Процедура ТоварыКоличествоПриИзмененииУведомления(Форма, ТекущиеДанные) Экспорт
	
	Объект= Форма.Объект;;
	
	Если Объект.ЕдиницаИзмерения = Объект.ЕдиницаПрослеживаемости Тогда
		
		ВесПоСертификатуТовара = 1;
		
	Иначе
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Номенклатура.КоэффициентПрослеживаемыхТоваров как КоэффициентПрослеживаемыхТоваров
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.Ссылка = &Номенклатура";
		
		Запрос.УстановитьПараметр("Номенклатура", ТекущиеДанные.Номенклатура);
		
		Результат = Запрос.Выполнить().Выбрать();
		
		Если Результат.Следующий() Тогда
			
			ВесПоСертификатуТовара = Результат.КоэффициентПрослеживаемыхТоваров;
			
		Иначе
			
			// Ошибка
			ВесПоСертификатуТовара = 0;
			
		КонецЕсли;
			
	КонецЕсли;
	
	ТекущиеДанные.КоличествоПрослеживаемости = ТекущиеДанные.Количество * ВесПоСертификатуТовара;
	
КонецПроцедуры

// Выполняет команду печати документа Уведомление об остатках прослеживаемых товаров
//
// Параметры:
//  ОписаниеКоманды - структура, содержащая ключ, соответствующие таблице значений
//						создаваемой функций БСП УправлениеПечатью.СоздатьКоллекциюКомандПечати(),
//						и ключ Форма - управляемая форма, из которой вызвана команда печати.
//
Функция ВыполнитьКомандуПечатиУведомленияОбОСтаткахПрослеживаемыхТоваров(ОписаниеКоманды) Экспорт

//	ПараметрыПечати = ОбщегоНазначенияБПКлиент.ПолучитьЗаголовокПечатнойФормы(ОписаниеКоманды.ОбъектыПечати);
	ПараметрыПечати = Новый Структура;
	ПараметрыПечати.Вставить("АдресВХранилище"       , ПоместитьВоВременноеХранилище(ОписаниеКоманды.ОбъектыПечати));
	ПараметрыПечати.Вставить("КоличествоЭкземпляров" , 1);
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
		"Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров",
		"УведомлениеОбОстаткахПрослеживаемыхТоваров",
		ОписаниеКоманды.ОбъектыПечати,
		ОписаниеКоманды.Форма,
		ПараметрыПечати);
	
КонецФункции

// Выполняет команду печати документа "Уведомление о ввозе прослеживаемых товаров"
//
// Параметры:
//  ОписаниеКоманды - структура,содержащая ключ, соответствующие таблице значений
//						создаваемой функций БСП УправлениеПечатью.СоздатьКоллекциюКомандПечати(),
//						и ключ Форма - управляемая форма, из которой вызвана команда печати.
//
Функция ВыполнитьКомандуПечатиУведомленияОВвозеПрослеживаемыхТоваров(ОписаниеКоманды) Экспорт

	ПараметрыПечати = Новый Структура;
	ПараметрыПечати.Вставить("АдресВХранилище"       , ПоместитьВоВременноеХранилище(ОписаниеКоманды.ОбъектыПечати));
	ПараметрыПечати.Вставить("КоличествоЭкземпляров" , 1);

	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
		"Документ.УведомлениеОВвозеПрослеживаемыхТоваров",
		"УведомлениеОВвозеПрослеживаемыхТоваров",
		ОписаниеКоманды.ОбъектыПечати,
		ОписаниеКоманды.Форма,
		ПараметрыПечати);
	
КонецФункции

Процедура СоздатьПоказатьУведомления(Форма, Объект, Элемент) Экспорт
	
	Если ТипЗнч(Объект) = Тип("ДокументОбъект.ПоступлениеТоваровУслуг") Тогда
		
		Если Форма.СписокУведомлений.Количество() = 0 Тогда
			ДополнительныеПараметры = Новый Структура("ДокументОбъект, Форма", Объект, Форма);
			Если Не Объект.Проведен Или Форма.Модифицированность Тогда 
				ТекстВопроса = НСтр("ru = 'Для создания уведомления необходимо предварительно провести документ.'");
				Оповещение = Новый ОписаниеОповещения("ОткрытьПомощникПолученияРНПТ", ПрослеживаемостьФормыКлиент, ДополнительныеПараметры);
				Кнопки = Новый СписокЗначений;
				Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Провести и продолжить'"));
				Кнопки.Добавить(КодВозвратаДиалога.Отмена);
				ПоказатьВопрос(Оповещение, ТекстВопроса, Кнопки);
			Иначе
				ОткрытьПомощникПолученияРНПТ(КодВозвратаДиалога.Да, ДополнительныеПараметры);
			КонецЕсли;
			
		ИначеЕсли Форма.СписокУведомлений.Количество() = 1 Тогда
			ПоказатьЗначение(, Форма.СписокУведомлений[0].Значение)
		Иначе
			Оповещение = Новый ОписаниеОповещения("СоздатьПоказатьУведомленияОповещение", ПрослеживаемостьФормыКлиент, Форма);
			Форма.ПоказатьВыборИзМеню(Оповещение, Форма.СписокУведомлений, Элемент);
		КонецЕсли;
		
	// Это форма РеализацияТоваровУслуг
	Иначе
		
		ПараметрыДокумента = Новый Структура("Ключ", Форма.УведомлениеОПеремещении);
		
		Если ЗначениеЗаполнено(Форма.УведомлениеОПеремещении) Тогда
			
			
			ОткрытьФорму("Документ.УведомлениеОПеремещенииПрослеживаемыхТоваров.ФормаОбъекта",
				ПараметрыДокумента, Форма.ЭтотОбъект);
			
		Иначе
			
			ЗначенияЗаполнения = Новый Структура();
			ЗначенияЗаполнения.Вставить("Основание", Форма.Ссылка);
			
			ПараметрыДокумента.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
			
			ОткрытьФорму("Документ.УведомлениеОПеремещенииПрослеживаемыхТоваров.ФормаОбъекта",
				ПараметрыДокумента, Форма.ЭтотОбъект);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#Область УведомленияОбОстатках

// Проверяет соответствие реквизитов шапки с реквизитами табличной части
//
// Параметры: 
// ФормаВладелец - ФормаКлиентскогоПриложения - форма владелец
// Отказ - булево - признак отказа
// ПараметрыЗаписи - РежимЗаписиДокумента - режимы записи документа
//
Процедура ПроверитьСоответствиеРеквизитовВШапкеИТабличнойЧасти(Форма, Отказ, ПараметрыЗаписи) Экспорт
	
	Если ПараметрыЗаписи.РежимЗаписи <> РежимЗаписиДокумента.Проведение Тогда
		Возврат;
	КонецЕсли;
	
	Объект = Форма.Объект;
	НомерКорректировки = Объект.НомерКорректировки;
	
	ТекущийКодТНВЭД = ?(НомерКорректировки = 0, Объект.КодТНВЭД, Объект.КодТНВЭДПослеИзменения);
	ТекущаяЕдиницаИзмерения = ?(НомерКорректировки = 0, Объект.ЕдиницаИзмерения, Объект.ЕдиницаИзмеренияПослеИзменения);
	
	Для каждого ТекущаяСтрокаТовары Из Объект.Товары Цикл
		
		Если НомерКорректировки > 0 
			И ТекущаяСтрокаТовары.КоличествоПослеИзменения = 0
			И ТекущаяСтрокаТовары.КоличествоПрослеживаемостиПослеИзменения = 0
			И ТекущаяСтрокаТовары.СуммаПослеИзменения = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыНоменклатуры = ПрослеживаемостьФормыВызовСервера.ПараметрыНоменклатуры(ТекущаяСтрокаТовары.Номенклатура);
		
		Если ПараметрыНоменклатуры.КодТНВЭД <> ТекущийКодТНВЭД Тогда
			
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				"Поле", "Корректность", НСтр("ru = 'Номенклатура'"),,,
				НСтр("ru = 'В номенклатуре %1 ТН ВЭД %2 не совпадает со значением ТН ВЭД %3 в шапке документа'"));
				
			ТекстСообщения = СтрШаблон(
				ТекстСообщения, 
				ТекущаяСтрокаТовары.Номенклатура,
				ПараметрыНоменклатуры.КодТНВЭД,
				ТекущийКодТНВЭД);
				
			ПутьКДанным = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
					"Объект.Товары", ТекущаяСтрокаТовары.НомерСтроки, "Номенклатура");
				
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, ПутьКДанным,, Отказ);
			
		ИначеЕсли ПараметрыНоменклатуры.ЕдиницаХраненияОстатков.ЕдиницаПоКлассификатору <> ТекущаяЕдиницаИзмерения Тогда
			
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				"Поле", "Корректность", НСтр("ru = 'Номенклатура'"),,,
				НСтр("ru = 'В номенклатуре %1 единица измерения %2 не совпадает со значением единицы измерения %3 в шапке документа'"));
				
			ТекстСообщения = СтрШаблон(
				ТекстСообщения,
				ТекущаяСтрокаТовары.Номенклатура,
				ПараметрыНоменклатуры.ЕдиницаХраненияОстатков.ЕдиницаПоКлассификатору,
				ТекущаяЕдиницаИзмерения);
			
			ПутьКДанным = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
					"Объект.Товары", ТекущаяСтрокаТовары.НомерСтроки, "Номенклатура");
				
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, ПутьКДанным,, Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область УведомленияОПеремещении

// Выполняет команду печати документа Уведомление о перемещении прослеживаемых товаров
//
// Параметры:
//  ОписаниеКоманды - структура, содержащая ключ, соответствующие таблице значений
//						создаваемой функций БСП УправлениеПечатью.СоздатьКоллекциюКомандПечати(),
//						и ключ Форма - управляемая форма, из которой вызвана команда печати.
//
Функция ВыполнитьКомандуПечатиУведомленияОПеремещенииПрослеживаемыхТоваров(ОписаниеКоманды) Экспорт

	ПараметрыПечати = Новый Структура;
	ПараметрыПечати.Вставить("АдресВХранилище"       , ПоместитьВоВременноеХранилище(ОписаниеКоманды.ОбъектыПечати));
	ПараметрыПечати.Вставить("КоличествоЭкземпляров" , 1);

	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
		"Документ.УведомлениеОПеремещенииПрослеживаемыхТоваров",
		"УведомлениеОПеремещенииПрослеживаемыхТоваров",
		ОписаниеКоманды.ОбъектыПечати,
		ОписаниеКоманды.Форма,
		ПараметрыПечати);
	
КонецФункции

#КонецОбласти

Процедура СоздатьПоказатьУведомленияОповещение(ВыбранныйЭлемент, Форма) Экспорт
	Если ВыбранныйЭлемент <> Неопределено Тогда
		ПоказатьЗначение(, ВыбранныйЭлемент.Значение)
	КонецЕсли	
КонецПроцедуры

Процедура ОткрытьПомощникПолученияРНПТ(Ответ, ДополнительныеПараметры) Экспорт 
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Объект = ДополнительныеПараметры.ДокументОбъект;
	Форма = ДополнительныеПараметры.Форма;
	Если Не Объект.Проведен Или Форма.Модифицированность Тогда
		Попытка
			
			Форма.ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение); 
			
		Исключение
			
			Возврат;
			
		КонецПопытки;
		
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", Объект.Организация);
	ПараметрыФормы.Вставить("ПериодСобытия", Объект.Дата);
	ПараметрыФормы.Вставить("КонтекстныйВызов", Истина);
	
	ОткрытьФорму("Обработка.ПомощникПолученияРНПТ.Форма.Форма", ПараметрыФормы, Форма);
	
КонецПроцедуры

#КонецОбласти