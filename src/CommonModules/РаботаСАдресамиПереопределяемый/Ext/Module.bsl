///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2023, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Функция СтруктураАдресаВСтруктуруJSON(Знач КонтактнаяИнформация) Экспорт
	
	Результат = РаботаСАдресамиКлиентСервер.ОписаниеНовойКонтактнойИнформации(Перечисления.ТипыКонтактнойИнформации.Адрес);
	
	Если Найти(КонтактнаяИнформация.Поле3, "муниципальный") <> 0 Тогда
		ТипАдреса = РаботаСАдресамиКлиентСервер.МуниципальныйАдрес();
	Иначе
		ТипАдреса = РаботаСАдресамиКлиентСервер.АдминистративноТерриториальныйАдрес();
	КонецЕсли;
	
	// ТипАдреса
	Результат.AddressType	= ТипАдреса;
	
	// Страна
	ОсновнаяСтрана = РаботаСАдресамиКлиентСервер.ОсновнаяСтрана();
	Результат.Country		= ВРег(ОсновнаяСтрана.Наименование);
	Результат.CountryCode	= ОсновнаяСтрана.Код;
	
	// Индекс	
	Результат.ZIPCode		= КонтактнаяИнформация.Поле1;
	
	// Регион
	НаименованиеИСокращение = АдресныйКлассификаторСлужебный.НаименованиеИСокращение(КонтактнаяИнформация.Поле2);
	Результат.Area			= НаименованиеИСокращение.Наименование;
	Результат.AreaType		= НаименованиеИСокращение.Сокращение;
	
	// Район	
	НаименованиеИСокращение	= АдресныйКлассификаторСлужебный.НаименованиеИСокращение(КонтактнаяИнформация.Поле3);
	Если ТипАдреса = РаботаСАдресамиКлиентСервер.АдминистративноТерриториальныйАдрес() Тогда
		Результат.District		= НаименованиеИСокращение.Наименование;
		Результат.DistrictType	= НаименованиеИСокращение.Сокращение;
	Иначе
		Результат.munDistrict		= НаименованиеИСокращение.Наименование;
		Результат.munDistrictType	= НаименованиеИСокращение.Сокращение;
	КонецЕсли;
	
	// Город	
	НаименованиеИСокращение	= АдресныйКлассификаторСлужебный.НаименованиеИСокращение(КонтактнаяИнформация.Поле4);
	Если ТипАдреса = РаботаСАдресамиКлиентСервер.АдминистративноТерриториальныйАдрес() Тогда
		Результат.City			= НаименованиеИСокращение.Наименование;
		Результат.CityType		= НаименованиеИСокращение.Сокращение;
	Иначе
		Результат.settlement		= НаименованиеИСокращение.Наименование;
		Результат.settlementType	= НаименованиеИСокращение.Сокращение;
	КонецЕсли;
	
	// НаселенныйПункт	
	НаименованиеИСокращение	= АдресныйКлассификаторСлужебный.НаименованиеИСокращение(КонтактнаяИнформация.Поле5);
	Результат.Locality		= НаименованиеИСокращение.Наименование;
	Результат.LocalityType	= НаименованиеИСокращение.Сокращение;
	
	// Улица	
	НаименованиеИСокращение	= АдресныйКлассификаторСлужебный.НаименованиеИСокращение(КонтактнаяИнформация.Поле6);
	Результат.Street		= НаименованиеИСокращение.Наименование;
	Результат.StreetType	= НаименованиеИСокращение.Сокращение;
	
	// Дом	
	Результат.HouseNumber	= КонтактнаяИнформация.Поле7;
	Результат.HouseType		= СокрЛП(КонтактнаяИнформация.ТипДома);

	// Корпус
	Если ЗначениеЗаполнено(КонтактнаяИнформация.Поле8) Тогда
		Результат.Buildings.Добавить(ЗначениеСтроенияИлиПомещения(СокрЛП(КонтактнаяИнформация.ТипКорпуса), КонтактнаяИнформация.Поле8));
	КонецЕсли;
	
	// Квартира
	Если ЗначениеЗаполнено(КонтактнаяИнформация.Поле9) Тогда
		Результат.Apartments.Добавить(ЗначениеСтроенияИлиПомещения(СокрЛП(КонтактнаяИнформация.ТипКвартиры), КонтактнаяИнформация.Поле9));
	КонецЕсли;
	
	// Представление
	Результат.value			= КонтактнаяИнформация.Представление;
	
	АдресныйКлассификаторСлужебный.УстановитьИдентификаторыАдреса(Результат);
	
	Возврат Результат;
	
КонецФункции

Функция ЗначениеСтроенияИлиПомещения(Тип, Значение)
	Возврат Новый Структура("type, number", Тип, Значение);
КонецФункции

#КонецОбласти
