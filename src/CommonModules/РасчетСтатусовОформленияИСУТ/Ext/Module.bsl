// Механизм расчета статусов оформления документов ИС МП.
//

#Область ИСМП

// Позволяет переопределить имена реквизитов документа-основания для документа ИСМП.
//
// Параметры:
//  МетаданныеДокументаОснования - ОбъектМетаданных - метаданные документа из ОпределяемыйТип.Основание<Имя документа ИСМП>
//  МетаданныеДокументаИСМП - ОбъектМетаданных - метаданные документа из ОпределяемыйТип.ДокументыИСМППоддерживающиеСтатусыОформления
//  Реквизиты  - Структура - имена реквизитов:
//  * Ключ  - служебное имя реквизита в ИСМП
//  * Значение - имя реквизита документа-основания, которое при необходимости надо переопределить
//  (см. РасчетСтатусовОформленияИСМП.СтруктураРеквизитовДляРасчетаСтатусаОформленияДокументов).
Процедура ПриОпределенииИменРеквизитовДляРасчетаСтатусаОформления(
			Знач МетаданныеДокументаОснования, Знач МетаданныеДокументаИСМП, Реквизиты) Экспорт
	
	// Зададим имена реквизитов по умолчанию.
	Реквизиты.Контрагент = "Организация";
	
	// Определим имя реквизита "Ответственный".
	Если МетаданныеДокументаОснования.Реквизиты.Найти("Менеджер") <> Неопределено Тогда
		Реквизиты.Ответственный = "Менеджер";
	ИначеЕсли МетаданныеДокументаОснования.Реквизиты.Найти("Кассир") <> Неопределено Тогда
		Реквизиты.Ответственный = "Кассир";
	ИначеЕсли МетаданныеДокументаОснования.Реквизиты.Найти("Ответственный") <> Неопределено Тогда
		Реквизиты.Ответственный = "Ответственный";
	Иначе 
		Реквизиты.Ответственный = "";
	КонецЕсли;
	
КонецПроцедуры

// Позволяет переопределить текст запроса выборки данных из документов-основания для расчета статуса оформления.
//   Требования к тексту запроса:
//     Если данные из документа выбирать не требуется, то данному параметру надо присвоить значение "" (пустая строка).
//     Результат запроса обязательно должен содержать следующие поля:
//      * Ссылка - ОпределяемыйТип.Основание<Имя документа ИСМП> - ссылка на документ-основание
//      * ЭтоДвижениеПриход - Булево - вид движения ТМЦ (Истина - приход, Ложь - расход)
//      * Номенклатура - ОпределяемыйТип.Номенклатура - номенклатура
//      * Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатуры - характеристика номенклатуры
//      * Серия - ОпределяемыйТип.СерияНоменклатуры - серия номенклатуры
//      * Количество - Число - количество номенклатуры в ее основной единице измерения
//     В результат запроса нужно включать только подконтрольную номенклатуру ИСМП (табак, обувь)
//     Для отбора документов-основания в запросе нужно использовать отбор "В (&МассивДокументов)"
//     Выбранные данные необходимо поместить во временную таблицу (См. РасчетСтатусовОформленияИСМП.ИмяВременнойТаблицыДляВыборкиДанныхДокумента).
//
//Параметры:
//   МетаданныеДокументаОснования - ОбъектМетаданных - метаданные документа из ОпределяемыйТип.Основание<Имя документа ИСМП>
//   МетаданныеДокументаИСМП - ОбъектМетаданных - метаданные документа из ОпределяемыйТип.ДокументыИСМППоддерживающиеСтатусыОформления
//   ТекстЗапроса - Строка - текст запроса выборки данных, который надо переопределить
//   ДополнительныеПараметрыЗапроса - Структура - дополнительные параметры запроса, требуемые для выполнения запроса 
//       конкретного документа; при необходимости можно дополнить данную структуру
//     Ключ     - имя параметры
//     Значение - значение параметра.
//
Процедура ПриОпределенииТекстаЗапросаДляРасчетаСтатусаОформления(
			Знач МетаданныеДокументаОснования, Знач МетаданныеДокументаИСМП, ТекстЗапроса, ДополнительныеПараметрыЗапроса) Экспорт
	
	ТоварыПриход = ШаблонОписанияПриходнойТабличнойЧастиДокумента();
	
	ОтборНоменклатурыИСМП = "СправочникНоменклатура.ВидПродукцииИС В(
	|	Истина,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Обувь),
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Табак),
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ЛегкаяПромышленность),
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МолочнаяПродукцияПодконтрольнаяВЕТИС),
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Шины),
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Фотоаппараты),
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Велосипеды),
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.КреслаКоляски),
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Духи),
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.АльтернативныйТабак),
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.УпакованнаяВода),
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МолочнаяПродукцияБезВЕТИС),
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Антисептики),
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.БАДы),
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.НикотиносодержащаяПродукция),
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Пиво),
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха),
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.СоковаяПродукция),
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.БезалкогольноеПиво),
	|	Истина)";
	
	ОтборНоменклатурыНеТабак = СтрЗаменить(ОтборНоменклатурыИСМП,  "ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Табак),", "");
	ОтборНоменклатурыНеМолокоВЕТИС = СтрЗаменить(ОтборНоменклатурыИСМП, "ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МолочнаяПродукция),", "");
	ОтборНеТабакНеМолокоВЕТИС = СтрЗаменить(ОтборНоменклатурыНеТабак,   "ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МолочнаяПродукция),", "");
	
	ОтборНоменклатурыДляОтгрузки = СтрЗаменить(ОтборНоменклатурыИСМП, "ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ТабачнаяПродукция),", "");
	ОтборНоменклатурыДляОтгрузки = СтрЗаменить(ОтборНоменклатурыДляОтгрузки, "ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.АльтернативныйТабак),", "");
	ОтборНоменклатурыДляОтгрузки = СтрЗаменить(ОтборНоменклатурыДляОтгрузки, "ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.НикотиносодержащаяПродукция),", "");
	ОтборНоменклатурыДляОтгрузки = СтрЗаменить(ОтборНоменклатурыДляОтгрузки, "ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МолочнаяПродукцияПодконтрольнаяВЕТИС),", "");
	ОтборНоменклатурыДляОтгрузки = СтрЗаменить(ОтборНоменклатурыДляОтгрузки,  "ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МолочнаяПродукцияБезВЕТИС),", "");
	ОтборНоменклатурыДляОтгрузки = СтрЗаменить(ОтборНоменклатурыДляОтгрузки,  "ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Пиво),", "");
	ОтборНоменклатурыДляОтгрузки = СтрЗаменить(ОтборНоменклатурыДляОтгрузки,  "ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.УпакованнаяВода),", "");
	ОтборНоменклатурыДляОтгрузки = СтрЗаменить(ОтборНоменклатурыДляОтгрузки,  "ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.СоковаяПродукция),", "");
	ОтборНоменклатурыДляОтгрузки = СтрЗаменить(ОтборНоменклатурыДляОтгрузки,  "ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.БезалкогольноеПиво),", "");
	
	ТабличныеЧастиДокумента = Новый Массив;
	
	Если МетаданныеДокументаОснования = Метаданные.Документы.ЗаказПоставщику Тогда
		
		// Товары - нет серий
		ТоварыПриход.ИмяПоляСерияТЧТовары = "Неопределено";
		ТоварыПриход.ДопОтборы_Общие = ТоварыПриход.ДопОтборы_Общие + Импорт();
		Если МетаданныеДокументаИСМП = Метаданные.Документы.ЗаказНаЭмиссиюКодовМаркировкиСУЗ Тогда
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТоварыПриход, ОтборНоменклатурыИСМП);
		КонецЕсли;
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ОприходованиеТоваров Тогда
		
		Если МетаданныеДокументаИСМП = Метаданные.Документы.МаркировкаТоваровИСМП Тогда
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТоварыПриход, ОтборНоменклатурыНеМолокоВЕТИС);
		КонецЕсли;
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.КомплектацияНоменклатуры Тогда
		
		Если МетаданныеДокументаИСМП = Метаданные.Документы.МаркировкаТоваровИСМП Тогда
			
			// Номенклатура в шапке при сборке
			НоменклатураПриход = ШаблонОписанияПриходнойТабличнойЧастиДокумента("");
			НоменклатураПриход.ПостфиксВременнойТаблицы = "Приход";
			НоменклатураПриход.ДопОтборы_Общие =
				"И ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Комплектация)";
			
			// Номенклатура в ТЧ при разборке
			НоменклатураРасход = ШаблонОписанияПриходнойТабличнойЧастиДокумента("Комплектующие");
			НоменклатураРасход.ПостфиксВременнойТаблицы = "Комплект";
			НоменклатураРасход.ДопОтборы_Общие =
				"И ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Разукомплектация)";
			
			// Все ТЧ документа
			ТабличныеЧастиДокумента.Добавить(НоменклатураПриход);
			ТабличныеЧастиДокумента.Добавить(НоменклатураРасход);
			
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТабличныеЧастиДокумента, ОтборНоменклатурыНеМолокоВЕТИС);
			
		КонецЕсли;
		
		Если МетаданныеДокументаИСМП = Метаданные.Документы.ВыводИзОборотаИСМП Тогда
			
			// Номенклатура в шапке при разборке
			НоменклатураПриход = ШаблонОписанияПриходнойТабличнойЧастиДокумента("");
			НоменклатураПриход.ПостфиксВременнойТаблицы = "Приход";
			НоменклатураПриход.ДопОтборы_Общие =
				"И ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Разукомплектация)";
			
			// Номенклатура в ТЧ при сборке
			НоменклатураРасход = ШаблонОписанияПриходнойТабличнойЧастиДокумента("Комплектующие");
			НоменклатураРасход.ПостфиксВременнойТаблицы = "Комплект";
			НоменклатураРасход.ДопОтборы_Общие =
				"И ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Комплектация)";
			
			// Все ТЧ документа
			ТабличныеЧастиДокумента.Добавить(НоменклатураПриход);
			ТабличныеЧастиДокумента.Добавить(НоменклатураРасход);
			
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТабличныеЧастиДокумента, ОтборНоменклатурыНеТабак);
			
		КонецЕсли;
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ПоступлениеТоваровУслуг Тогда
		
		Если МетаданныеДокументаИСМП = Метаданные.Документы.МаркировкаТоваровИСМП Тогда
			
			ТоварыПриход.ДопОтборы_Общие = ТоварыПриход.ДопОтборы_Общие + Импорт();
			
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТоварыПриход, ОтборНеТабакНеМолокоВЕТИС);
			
		ИначеЕсли МетаданныеДокументаИСМП = Метаданные.Документы.ПриемкаТоваровИСМП Тогда
			
			ТоварыПриход.ДопОтборы_Общие = 
			"И (1 В (ВЫБРАТЬ ПЕРВЫЕ 1 Ссылка 
			|	ИЗ Документ.ПриемкаТоваровИСМП КАК ПриемкаТоваровИСМП ГДЕ ПриемкаТоваровИСМП.ДокументОснование = ТаблицаДокумента.Ссылка))";
			
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТоварыПриход, ОтборНоменклатурыИСМП);
			
		КонецЕсли;
			
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ЧекККМ Тогда
		
		Если МетаданныеДокументаИСМП = Метаданные.Документы.ВыводИзОборотаИСМП Тогда
			
			ТоварыПриход.ДопОтборы_Общие = "И ТаблицаДокумента.КассаККМ В(ВЫБРАТЬ Касса Из РегистрСведений.КассыНеПередающиеДанныеВИСМП)
			|И ТаблицаДокумента.ЧекПробитНаККМ 
			|И ТаблицаДокумента.Архивный = ЛОЖЬ
			|И ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Продажа)";
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТоварыПриход, ОтборНоменклатурыНеТабак);
			
		КонецЕсли;
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ОтчетОРозничныхПродажах Тогда
		
		Если МетаданныеДокументаИСМП = Метаданные.Документы.ВыводИзОборотаИСМП Тогда
			
			ТоварыПриход.ДопОтборы_Общие = "И ТаблицаДокумента.КассаККМ В(ВЫБРАТЬ Касса Из РегистрСведений.КассыНеПередающиеДанныеВИСМП)";
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТоварыПриход, ОтборНоменклатурыНеТабак);
			
		КонецЕсли;
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.РеализацияТоваровУслуг Тогда
		
		Если МетаданныеДокументаИСМП = Метаданные.Документы.ВыводИзОборотаИСМП Тогда
			
			ТоварыПриход.ДопОтборы_Общие = СтрШаблон("И %1 И %2 И %3",
				"ТаблицаДокумента.Проведен",
				"НЕ(1 В (ВЫБРАТЬ 1 Из РегистрСведений.СостоянияЭД КАК СостоянияЭД
				|	ГДЕ СостоянияЭД.СсылкаНаОбъект = ТаблицаДокумента.Ссылка
				|	И СостоянияЭД.ЭлектронныйДокумент.СодержитДанныеОМаркируемыхТоварах))",
				"НЕ(1 В (ВЫБРАТЬ 1 Из Документ.ОтгрузкаТоваровИСМП КАК ДокументОтгрузки
				|	ГДЕ ДокументОтгрузки.ДокументОснование = ТаблицаДокумента.Ссылка
				|	И ДокументОтгрузки.Проведен))");
			
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТоварыПриход, ОтборНоменклатурыНеТабак);
			
		ИначеЕсли МетаданныеДокументаИСМП = Метаданные.Документы.ОтгрузкаТоваровИСМП Тогда
			
			ТоварыПриход.ДопОтборы_Общие = СтрШаблон("И %1 И (%2 ИЛИ %3)",
				"ТаблицаДокумента.Контрагент.НеЯвляетсяРезидентом = ЛОЖЬ",
				"НЕ(1 В (ВЫБРАТЬ 1 Из РегистрСведений.СостоянияЭД КАК СостоянияЭД 
				|	ГДЕ СостоянияЭД.СсылкаНаОбъект = ТаблицаДокумента.Ссылка 
				|	И СостоянияЭД.ЭлектронныйДокумент.СодержитДанныеОМаркируемыхТоварах))",
				"СправочникНоменклатура.ВидПродукцииИС = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха)");
			
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТоварыПриход, ОтборНоменклатурыДляОтгрузки);
			
		КонецЕсли;
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ВозвратТоваровПоставщику Тогда
		
		Если МетаданныеДокументаИСМП = Метаданные.Документы.ОтгрузкаТоваровИСМП Тогда
			
			ТоварыПриход.ДопОтборы_Общие = СтрШаблон("И %1 И (%2 ИЛИ %3)",
				"ТаблицаДокумента.Контрагент.НеЯвляетсяРезидентом = ЛОЖЬ",
				"НЕ(1 В (ВЫБРАТЬ 1 Из РегистрСведений.СостоянияЭД КАК СостоянияЭД 
				|	ГДЕ СостоянияЭД.СсылкаНаОбъект = ТаблицаДокумента.Ссылка 
				|	И СостоянияЭД.ЭлектронныйДокумент.СодержитДанныеОМаркируемыхТоварах))",
				"СправочникНоменклатура.ВидПродукцииИС = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха)");
			
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТоварыПриход, ОтборНоменклатурыДляОтгрузки);
			
		КонецЕсли;
			
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ТребованиеНакладная Тогда
		
		Если МетаданныеДокументаИСМП = Метаданные.Документы.ВыводИзОборотаИСМП Тогда
			
			Материалы = ШаблонОписанияПриходнойТабличнойЧастиДокумента("Материалы");
			
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, Материалы, ОтборНоменклатурыНеТабак);
			
		КонецЕсли;
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.СписаниеТоваров Тогда
		
		Если МетаданныеДокументаИСМП = Метаданные.Документы.ВыводИзОборотаИСМП Тогда
			
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТоварыПриход, ОтборНоменклатурыНеТабак);
			
		КонецЕсли;
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ВозвратТоваровОтПокупателя Тогда
		
		Если МетаданныеДокументаИСМП = Метаданные.Документы.ПеремаркировкаТоваровИСМП Тогда
			
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТоварыПриход, ОтборНоменклатурыНеТабак);
			
		ИначеЕсли МетаданныеДокументаИСМП = Метаданные.Документы.ПриемкаТоваровИСМП Тогда
			
			ТоварыПриход.ДопОтборы_Общие = "И (1 В (ВЫБРАТЬ ПЕРВЫЕ 1 Ссылка 
			|	ИЗ Документ.ПриемкаТоваровИСМП КАК ПриемкаТоваровИСМП ГДЕ ПриемкаТоваровИСМП.ДокументОснование = ТаблицаДокумента.Ссылка))";
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТоварыПриход, ОтборНоменклатурыИСМП);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЕГАИС

// Позволяет переопределить имена реквизитов документа-основания для документа ЕГАИС.
//   (см. РасчетСтатусовОформленияЕГАИСПереопределяемый.ПриОпределенииРеквизитовОснования).
//
Процедура ПриОпределенииРеквизитовОснованияДляЕГАИС(МетаданныеДокументаОснования, МетаданныеДокументаЕГАИС, Реквизиты) Экспорт
	
	// Определим имя реквизита "Ответственный".
	Если МетаданныеДокументаОснования.Реквизиты.Найти("Менеджер") <> Неопределено Тогда
		Реквизиты.Ответственный = "Менеджер";
	ИначеЕсли МетаданныеДокументаОснования.Реквизиты.Найти("Кассир") <> Неопределено Тогда
		Реквизиты.Ответственный = "Кассир";
	ИначеЕсли МетаданныеДокументаОснования.Реквизиты.Найти("Ответственный") <> Неопределено Тогда
		Реквизиты.Ответственный = "Ответственный";
	Иначе 
		Реквизиты.Ответственный = "";
	КонецЕсли;
	
	Если МетаданныеДокументаОснования.Реквизиты.Найти("СкладОрдер") <> Неопределено Тогда
		Реквизиты.ТорговыйОбъект = "СкладОрдер";
	КонецЕсли;
	
	Если МетаданныеДокументаОснования = Метаданные.Документы.ПеремещениеТоваров Тогда
		
		Реквизиты.Ответственный = "Ответственный";
		
		Если МетаданныеДокументаЕГАИС = Метаданные.Документы.ТТНВходящаяЕГАИС Тогда
			Реквизиты.ТорговыйОбъект = "СкладПолучатель";
		ИначеЕсли МетаданныеДокументаЕГАИС = Метаданные.Документы.ТТНИсходящаяЕГАИС Тогда
			Реквизиты.ТорговыйОбъект = "СкладОтправитель";
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

//Позволяет переопределить текст запроса выборки данных из документов-оснований для расчета статуса оформления.
//   См. РасчетСтатусовОформленияЕГАИСПереопределяемый.ПриОпределенииЗапросаТоварыДокументаОснования
//
Процедура ПриОпределенииЗапросаДляЕГАИС(
	МетаданныеДокументаОснования, МетаданныеДокументаЕГАИС, ТекстЗапроса, ДополнительныеПараметрыЗапроса) Экспорт
	
	ТоварыПриход = ШаблонОписанияПриходнойТабличнойЧастиДокумента();
	ТоварыРасход = ШаблонОписанияРасходнойТабличнойЧастиДокумента();
	ТабличныеЧастиДокумента = Новый Массив;
	ОтборНоменклатуры = "СправочникНоменклатура.АлкогольнаяПродукция
	|	И НЕ СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре";
	ОтборНоменклатурыТолькоМаркируемая = ОтборНоменклатуры + "
	|	И СправочникНоменклатура.ВидАлкогольнойПродукцииЕГАИС.Маркируемый";
	ОтборНоменклатурыТолькоНемаркируемая = ОтборНоменклатуры + "
	|	И НЕ СправочникНоменклатура.ВидАлкогольнойПродукцииЕГАИС.Маркируемый";
	ОтборНоменклатурыПиво = "СправочникНоменклатура.ВидПродукцииИС = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Пиво)";
	
	Если МетаданныеДокументаОснования = Метаданные.Документы.ТребованиеНакладная Тогда
		
		Если МетаданныеДокументаЕГАИС = Метаданные.Документы.ЧекЕГАИС Тогда
			
			ТоварыРасход.ИмяТабличнойЧастиТовары = "Материалы";
			ТоварыРасход.ДопОтборы_Общие = ТоварыРасход.ДопОтборы_Общие +"
				|И ТаблицаДокумента.Склад.ВидСклада = ЗНАЧЕНИЕ(Перечисление.ВидыСкладов.Розничный)";
			
			ТабличныеЧастиДокумента.Добавить(ТоварыРасход);
			ТабличныеЧастиДокумента.Добавить(ШаблонОписанияСвязанныеДокументы("АктСписанияЕГАИС"));
			
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТабличныеЧастиДокумента, ОтборНоменклатурыТолькоМаркируемая);
			ДополнительныеПараметрыЗапроса.Вставить("КонечныеСтатусыАктСписанияЕГАИС",Документы.АктСписанияЕГАИС.КонечныеСтатусы());
			
		ИначеЕсли МетаданныеДокументаЕГАИС = Метаданные.Документы.АктСписанияЕГАИС Тогда
			
			ТоварыРасход.ИмяТабличнойЧастиТовары = "Материалы";
			ТабличныеЧастиДокумента.Добавить(ТоварыРасход);
			ТабличныеЧастиДокумента.Добавить(ШаблонОписанияСвязанныеДокументы("ЧекЕГАИС"));
			ДополнительныеПараметрыЗапроса.Вставить("КонечныеСтатусыЧекЕГАИС",Документы.ЧекЕГАИС.КонечныеСтатусы());
			
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТабличныеЧастиДокумента, ОтборНоменклатуры);
			
		КонецЕсли;
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ОтчетОРозничныхПродажах Тогда
		
		Если МетаданныеДокументаЕГАИС = Метаданные.Документы.АктСписанияЕГАИС Тогда
			
			Если Константы.ДатаНачалаРегистрацииРозничныхПродажВЕГАИС.Получить() >= ТекущаяДатаСеанса() Тогда
				ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТоварыРасход, ОтборНоменклатуры);
			Иначе
				ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТоварыРасход, ОтборНоменклатурыТолькоНемаркируемая);
			КонецЕсли;
		
		КонецЕсли;
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ВозвратТоваровОтПокупателя Тогда
		
		Если МетаданныеДокументаЕГАИС = Метаданные.Документы.ЧекЕГАИСВозврат Тогда
			
			ТоварыПриход.ДопОтборы_Общие = ТоварыПриход.ДопОтборы_Общие + "
			|И ТаблицаДокумента.СкладОрдер.ВидСклада = ЗНАЧЕНИЕ(Перечисление.ВидыСкладов.Розничный)";
			
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТоварыПриход, ОтборНоменклатурыТолькоМаркируемая);
			
		КонецЕсли;
	
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ВозвратТоваровПоставщику Тогда
		
		Если МетаданныеДокументаЕГАИС = Метаданные.Документы.ТТНИсходящаяЕГАИС Тогда
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТоварыРасход, ОтборНоменклатуры);
		КонецЕсли;
	
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ОприходованиеТоваров Тогда
		
		Если МетаданныеДокументаЕГАИС = Метаданные.Документы.АктПостановкиНаБалансЕГАИС Тогда
			
			ТабличныеЧастиДокумента.Добавить(ТоварыПриход);
			ТабличныеЧастиДокумента.Добавить(ШаблонОписанияСвязанныеДокументы("ОтчетОПроизводствеЕГАИС"));
			ДополнительныеПараметрыЗапроса.Вставить("КонечныеСтатусыОтчетОПроизводствеЕГАИС",Документы.ОтчетОПроизводствеЕГАИС.КонечныеСтатусы());
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТабличныеЧастиДокумента, ОтборНоменклатуры);
			
		ИначеЕсли МетаданныеДокументаЕГАИС = Метаданные.Документы.АктПостановкиНаБалансЕГАИС Тогда
			
			ТабличныеЧастиДокумента.Добавить(ТоварыПриход);
			ТабличныеЧастиДокумента.Добавить(ШаблонОписанияСвязанныеДокументы("АктПостановкиНаБалансЕГАИС"));
			ДополнительныеПараметрыЗапроса.Вставить("КонечныеСтатусыАктПостановкиНаБалансЕГАИС",Документы.АктПостановкиНаБалансЕГАИС.КонечныеСтатусы());
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТабличныеЧастиДокумента, ОтборНоменклатурыПиво);
			
		КонецЕсли;
	
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ПеремещениеТоваров Тогда
		
		Если МетаданныеДокументаЕГАИС = Метаданные.Документы.ТТНВходящаяЕГАИС Тогда
			// не формируем - будет получен через обмен
		ИначеЕсли МетаданныеДокументаЕГАИС = Метаданные.Документы.ТТНИсходящаяЕГАИС Тогда
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТоварыРасход, ОтборНоменклатуры);
		КонецЕсли;
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ПоступлениеТоваровУслуг Тогда
		
		Если МетаданныеДокументаЕГАИС = Метаданные.Документы.ТТНВходящаяЕГАИС Тогда
			
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТоварыПриход, ОтборНоменклатуры);
			
		КонецЕсли;
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.РеализацияТоваровУслуг Тогда
		
		Если МетаданныеДокументаЕГАИС = Метаданные.Документы.ТТНИсходящаяЕГАИС Тогда
			
			// Все ТЧ документа
			ТабличныеЧастиДокумента.Добавить(ТоварыРасход);
			ТабличныеЧастиДокумента.Добавить(ШаблонОписанияСвязанныеДокументы("ЧекЕГАИС"));
			ТабличныеЧастиДокумента.Добавить(ШаблонОписанияСвязанныеДокументы("АктСписанияЕГАИС"));
			ДополнительныеПараметрыЗапроса.Вставить("КонечныеСтатусыЧекЕГАИС",Документы.ЧекЕГАИС.КонечныеСтатусы());
			ДополнительныеПараметрыЗапроса.Вставить("КонечныеСтатусыАктСписанияЕГАИС",Документы.АктСписанияЕГАИС.КонечныеСтатусы());
			
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТабличныеЧастиДокумента, ОтборНоменклатуры);
			
		ИначеЕсли МетаданныеДокументаЕГАИС = Метаданные.Документы.ЧекЕГАИС Тогда
			
			ТоварыРасход.ДопОтборы_Общие = ТоварыРасход.ДопОтборы_Общие + "
			|И ТаблицаДокумента.Склад.ВидСклада = ЗНАЧЕНИЕ(Перечисление.ВидыСкладов.Розничный)";
			
			// Все ТЧ документа
			ТабличныеЧастиДокумента.Добавить(ТоварыРасход);
			ТабличныеЧастиДокумента.Добавить(ШаблонОписанияСвязанныеДокументы("ТТНИсходящаяЕГАИС"));
			ДополнительныеПараметрыЗапроса.Вставить("КонечныеСтатусыТТНИсходящаяЕГАИС",Документы.ТТНИсходящаяЕГАИС.КонечныеСтатусы());
			
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТабличныеЧастиДокумента, ОтборНоменклатурыТолькоМаркируемая);
			
		ИначеЕсли МетаданныеДокументаЕГАИС = Метаданные.Документы.АктСписанияЕГАИС Тогда
			
			ТоварыРасход.ДопОтборы_Общие = ТоварыРасход.ДопОтборы_Общие + "
			|И ТаблицаДокумента.Склад.ВидСклада = ЗНАЧЕНИЕ(Перечисление.ВидыСкладов.Розничный)";
			
			// Все ТЧ документа
			ТабличныеЧастиДокумента.Добавить(ТоварыРасход);
			ТабличныеЧастиДокумента.Добавить(ШаблонОписанияСвязанныеДокументы("ТТНИсходящаяЕГАИС"));
			ДополнительныеПараметрыЗапроса.Вставить("КонечныеСтатусыТТНИсходящаяЕГАИС",Документы.ТТНИсходящаяЕГАИС.КонечныеСтатусы());
			
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТабличныеЧастиДокумента, ОтборНоменклатурыТолькоНемаркируемая);
			
		КонецЕсли;
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.КомплектацияНоменклатуры Тогда
		
		Если МетаданныеДокументаЕГАИС = Метаданные.Документы.АктСписанияЕГАИС Тогда
			
			// Номенклатура в шапке при разборке (расход)
			НоменклатураРасход = ШаблонОписанияРасходнойТабличнойЧастиДокумента("");
			НоменклатураРасход.ДопОтборы_Общие = ТоварыРасход.ДопОтборы_Общие;
			НоменклатураРасход.ДопСоединения_Общие = ТоварыРасход.ДопСоединения_Общие;
			
			НоменклатураРасход.ДопОтборы_Общие = НоменклатураРасход.ДопОтборы_Общие + "
			|И ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Разукомплектация)";
			
			// Все ТЧ документа
			ТабличныеЧастиДокумента.Добавить(НоменклатураРасход);
			ТабличныеЧастиДокумента.Добавить(ШаблонОписанияСвязанныеДокументы("ЧекЕГАИС"));
			ДополнительныеПараметрыЗапроса.Вставить("КонечныеСтатусыЧекЕГАИС",Документы.ЧекЕГАИС.КонечныеСтатусы());
			
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТабличныеЧастиДокумента, ОтборНоменклатуры);
		
		ИначеЕсли МетаданныеДокументаЕГАИС = Метаданные.Документы.ЧекЕГАИС Тогда
			
			// Номенклатура в шапке при разборке (расход)
			НоменклатураРасход = ШаблонОписанияРасходнойТабличнойЧастиДокумента("");
			НоменклатураРасход.ДопОтборы_Общие = ТоварыРасход.ДопОтборы_Общие;
			НоменклатураРасход.ДопСоединения_Общие = ТоварыРасход.ДопСоединения_Общие;
			
			НоменклатураРасход.ДопОтборы_Общие = НоменклатураРасход.ДопОтборы_Общие + "
			|И ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Разукомплектация)
			|И ТаблицаДокумента.Склад.ВидСклада = ЗНАЧЕНИЕ(Перечисление.ВидыСкладов.Розничный)";
			
			// Все ТЧ документа
			ТабличныеЧастиДокумента.Добавить(НоменклатураРасход);
			ТабличныеЧастиДокумента.Добавить(ШаблонОписанияСвязанныеДокументы("АктСписанияЕГАИС"));
			ДополнительныеПараметрыЗапроса.Вставить("КонечныеСтатусыАктСписанияЕГАИС",Документы.АктСписанияЕГАИС.КонечныеСтатусы());
			
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, НоменклатураРасход, ОтборНоменклатурыТолькоМаркируемая);
			
		ИначеЕсли МетаданныеДокументаЕГАИС = Метаданные.Документы.ОтчетОПроизводствеЕГАИС Тогда
			
			// Номенклатура в шапке при сборке (приход)
			НоменклатураРасход = ШаблонОписанияПриходнойТабличнойЧастиДокумента("");
			НоменклатураРасход.ДопОтборы_Общие = ТоварыРасход.ДопОтборы_Общие;
			НоменклатураРасход.ДопСоединения_Общие = ТоварыРасход.ДопСоединения_Общие;
			
			НоменклатураРасход.ДопОтборы_Общие = НоменклатураРасход.ДопОтборы_Общие + "
			|И ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Комплектация)";
			
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, НоменклатураРасход, ОтборНоменклатурыПиво);
			
		КонецЕсли;
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.СписаниеТоваров Тогда
		
		Если МетаданныеДокументаЕГАИС = Метаданные.Документы.АктСписанияЕГАИС Тогда
			
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТоварыРасход, ОтборНоменклатуры);
			
		КонецЕсли;
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ЗаказПоставщику Тогда
		
		Если МетаданныеДокументаЕГАИС = Метаданные.Документы.УведомлениеОПланируемомИмпортеЕГАИС Тогда
			
			ТоварыПриход.ИмяПоляСерияТЧТовары = "Неопределено";
			ТоварыПриход.ДопОтборы_Общие = ТоварыПриход.ДопОтборы_Общие + Импорт();
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТоварыПриход, ОтборНоменклатурыПиво);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЗЕРНО

Процедура ПриОпределенииРеквизитовОснованияДляЗЕРНО(МетаданныеДокументаОснования, МетаданныеДокументаЗЕРНО, Реквизиты) Экспорт
	
	// Зададим имена реквизитов по умолчанию.
	Если МетаданныеДокументаОснования.Реквизиты.Найти("Организация") <> Неопределено Тогда
		Реквизиты.Контрагент                        = "Организация";
	КонецЕсли;
	
	// Определим имя реквизита "Ответственный".
	Если МетаданныеДокументаОснования.Реквизиты.Найти("Менеджер") <> Неопределено Тогда
		Реквизиты.Ответственный = "Менеджер";
	ИначеЕсли МетаданныеДокументаОснования.Реквизиты.Найти("Кассир") <> Неопределено Тогда
		Реквизиты.Ответственный = "Кассир";
	ИначеЕсли МетаданныеДокументаОснования.Реквизиты.Найти("Ответственный") <> Неопределено Тогда
		Реквизиты.Ответственный = "Ответственный";
	Иначе 
		Реквизиты.Ответственный = "";
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОпределенииЗапросаДляЗЕРНО(МетаданныеДокументаОснования, МетаданныеДокументаЗЕРНО, ТекстЗапроса, ДополнительныеПараметрыЗапроса) Экспорт
	
	ТоварыПриход = ШаблонОписанияПриходнойТабличнойЧастиДокумента();
	ТоварыПриход.ДопОтборы_Общие = СтрШаблон("%1", ДокументПроведен());
	ТоварыРасход = ШаблонОписанияРасходнойТабличнойЧастиДокумента();
	ТоварыРасход.ДопОтборы_Общие = СтрШаблон("%1", ДокументПроведен());
	ТабличныеЧастиДокумента = Новый Массив;
	ОтборНоменклатуры =
		"(СправочникНоменклатура.ВидПродукцииИС В (
		|	ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Зерно),
		|	ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПродуктыПереработкиЗерна)))";
	Зерно = "СправочникНоменклатура.ВидПродукцииИС = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Зерно)";
	ПродуктыПереработки = "СправочникНоменклатура.ВидПродукцииИС = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПродуктыПереработкиЗерна)";
	
	Если МетаданныеДокументаОснования = Метаданные.Документы.ОприходованиеТоваров Тогда
			
		ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТоварыПриход, ОтборНоменклатуры);
			
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.СписаниеТоваров Тогда
		
		Если МетаданныеДокументаЗЕРНО = Метаданные.Документы.СписаниеПартийЗЕРНО Тогда
			
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТоварыРасход, ОтборНоменклатуры);
			
		КонецЕсли;	
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ТребованиеНакладная Тогда
		
		Если МетаданныеДокументаЗЕРНО = Метаданные.Документы.СписаниеПартийЗЕРНО Тогда
			
			Материалы = ШаблонОписанияРасходнойТабличнойЧастиДокумента("Материалы");
			Материалы.ДопОтборы_Общие = СтрШаблон("%1", ДокументПроведен());
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, Материалы, ОтборНоменклатуры);
			
		КонецЕсли;
	
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.РеализацияТоваровУслуг Тогда
		
		ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТоварыРасход, ОтборНоменклатуры);
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ВозвратТоваровПоставщику Тогда
		
		Если МетаданныеДокументаЗЕРНО = Метаданные.Документы.ОформлениеСДИЗЗЕРНО Тогда
			
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТоварыРасход, ОтборНоменклатуры);
			
		КонецЕсли;
	
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ПеремещениеТоваров Тогда
		
		Если МетаданныеДокументаЗЕРНО = Метаданные.Документы.ОформлениеСДИЗЗЕРНО Тогда
			
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТоварыРасход, ОтборНоменклатуры);
			
		КонецЕсли;
	
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ЗаказПоставщику Тогда
		
		Если МетаданныеДокументаЗЕРНО = Метаданные.Документы.ФормированиеПартийЗЕРНО Тогда
			
			// Закупка по импорту
			ТоварыПриход.ИмяПоляСерияТЧТовары = "Неопределено";
			ТоварыПриход.ДопОтборы_Общие = ТоварыПриход.ДопОтборы_Общие + Импорт();
			
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТоварыПриход, ОтборНоменклатуры);	
			
		КонецЕсли;
	
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.КомплектацияНоменклатуры Тогда
		
		Если МетаданныеДокументаЗЕРНО = Метаданные.Документы.ФормированиеПартийПриПроизводствеЗЕРНО Тогда
			
			// Номенклатура в шапке при сборке
			НоменклатураПриход = ШаблонОписанияПриходнойТабличнойЧастиДокумента("");
			НоменклатураПриход.ПостфиксВременнойТаблицы = "Приход";
			НоменклатураПриход.ДопОтборы_Общие =НоменклатураПриход.ДопОтборы_Общие + ДокументПроведен() + "
				|И ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Комплектация)";
		
			// Номенклатура в ТЧ при разборке
			НоменклатураРасход = ШаблонОписанияПриходнойТабличнойЧастиДокумента("Комплектующие");
			НоменклатураРасход.ПостфиксВременнойТаблицы = "Комплект";
			НоменклатураРасход.ДопОтборы_Общие = НоменклатураРасход.ДопОтборы_Общие + ДокументПроведен() + "
				|И ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Разукомплектация)";
		
			// Все ТЧ документа
			ТабличныеЧастиДокумента.Добавить(НоменклатураПриход);
			ТабличныеЧастиДокумента.Добавить(НоменклатураРасход);
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТабличныеЧастиДокумента, ПродуктыПереработки);
			
		ИначеЕсли МетаданныеДокументаЗЕРНО = Метаданные.Документы.СписаниеПартийЗЕРНО Тогда
			
			// Номенклатура в шапке при разборке
			НоменклатураПриход = ШаблонОписанияРасходнойТабличнойЧастиДокумента("");
			НоменклатураПриход.ПостфиксВременнойТаблицы = "Приход";
			НоменклатураПриход.ДопОтборы_Общие =НоменклатураПриход.ДопОтборы_Общие + ДокументПроведен() + "
				|И ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Разукомплектация)"
				+ ВДокументеДругиеВидыПродукцииИНетЗерна("КомплектацияНоменклатуры", "Комплектующие");
		
			// Номенклатура в ТЧ при сборке
			НоменклатураРасход = ШаблонОписанияРасходнойТабличнойЧастиДокумента("Комплектующие");
			НоменклатураРасход.ПостфиксВременнойТаблицы = "Комплект";
			НоменклатураРасход.ДопОтборы_Общие = НоменклатураРасход.ДопОтборы_Общие + ДокументПроведен() + "
				|И ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Комплектация)"
				+ ВДокументеДругиеВидыПродукцииИНетЗерна("КомплектацияНоменклатуры", "");
		
			// Все ТЧ документа
			ТабличныеЧастиДокумента.Добавить(НоменклатураПриход);
			ТабличныеЧастиДокумента.Добавить(НоменклатураРасход);
			ТекстЗапроса = ТекстЗапросаДляРасчетаСтатусаОформления(МетаданныеДокументаОснования, ТабличныеЧастиДокумента, ОтборНоменклатуры);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Шаблоны

Функция ШаблонОписанияПриходнойТабличнойЧастиДокумента(ИмяТабличнойЧастиТовары = "Товары", ПостфиксВременнойТаблицы = "")
	
	Возврат ШаблонОписанияТабличнойЧастиДокумента(Истина, ИмяТабличнойЧастиТовары, ПостфиксВременнойТаблицы);
	
КонецФункции

Функция ШаблонОписанияРасходнойТабличнойЧастиДокумента(ИмяТабличнойЧастиТовары = "Товары", ПостфиксВременнойТаблицы = "")
	
	Возврат ШаблонОписанияТабличнойЧастиДокумента(Ложь, ИмяТабличнойЧастиТовары, ПостфиксВременнойТаблицы);
	
КонецФункции

Функция ШаблонОписанияТабличнойЧастиДокумента(ВидДвиженияПриход = Истина,
			ИмяТабличнойЧастиТовары = "Товары", ПостфиксВременнойТаблицы = "")
	
	ОписаниеТЧ = Новый Структура;
	// Вид движения
	ОписаниеТЧ.Вставить("ВидДвиженияПриход",             ВидДвиженияПриход);
	// ТЧ Товары
	ОписаниеТЧ.Вставить("ИмяТабличнойЧастиТовары",       ИмяТабличнойЧастиТовары);
	// Имена полей ТЧ Товары
	ОписаниеТЧ.Вставить("ИмяПоляНоменклатураТЧТовары",   "ТаблицаТовары.Номенклатура");
	ОписаниеТЧ.Вставить("ИмяПоляХарактеристикаТЧТовары", "ТаблицаТовары.ХарактеристикаНоменклатуры");
	ОписаниеТЧ.Вставить("ИмяПоляСерияТЧТовары",          "ТаблицаТовары.СерияНоменклатуры");
	ОписаниеТЧ.Вставить("ИмяПоляКоличествоТЧТовары",     "ТаблицаТовары.Количество");
	// Модификаторы текста запроса.
	ОписаниеТЧ.Вставить("ДопСоединения_Общие",           "");
	ОписаниеТЧ.Вставить("ДопСоединения_ТаблицаТовары",   "");
	ОписаниеТЧ.Вставить("ДопОтборы_Общие",               "");
	ОписаниеТЧ.Вставить("ДопОтборы_ТаблицаТовары",       "");
	// Номер таблицы по порядку.
	ОписаниеТЧ.Вставить("ПостфиксВременнойТаблицы",      ПостфиксВременнойТаблицы);
	
	Возврат ОписаниеТЧ;
	
КонецФункции

Функция ШаблонОписанияСвязанныеДокументы(ИмяСвязанногоДокумента, ЭтоПриход = Ложь)

	Если ЭтоПриход Тогда
		Шаблон = ШаблонОписанияПриходнойТабличнойЧастиДокумента("", Неопределено);
	Иначе
		Шаблон = ШаблонОписанияРасходнойТабличнойЧастиДокумента("", Неопределено);
	КонецЕсли;
	Шаблон.ДопСоединения_Общие = СтрШаблон("
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.%1.Товары КАК СвязанныеДокументы
		|		ПО СвязанныеДокументы.Ссылка.ДокументОснование = ТаблицаДокумента.Ссылка
		|		И СвязанныеДокументы.Ссылка.Проведен
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС%1
		|		ПО СтатусыДокументовЕГАИС%1.Документ = СвязанныеДокументы.Ссылка",
		ИмяСвязанногоДокумента);
	
	Шаблон.ДопОтборы_Общие = СтрШаблон("И СтатусыДокументовЕГАИС%1.Статус НЕ В(&КонечныеСтатусы%1)", ИмяСвязанногоДокумента);
	
	Шаблон.ИмяПоляНоменклатураТЧТовары   = "СвязанныеДокументы.Номенклатура";
	Шаблон.ИмяПоляХарактеристикаТЧТовары = "СвязанныеДокументы.Характеристика";
	Шаблон.ИмяПоляСерияТЧТовары          = "СвязанныеДокументы.Серия";
	Шаблон.ИмяПоляКоличествоТЧТовары     = "-СвязанныеДокументы.Количество";
	
	Возврат Шаблон;

КонецФункции

Функция ТекстЗапросаДляРасчетаСтатусаОформления(
	МетаданныеДокументаОснования,
	ОписаниеТабличныхЧастей,
	ОтборНоменклатуры)
	
	Если ТипЗнч(ОписаниеТабличныхЧастей) <> Тип("Массив") Тогда
		// Передано описание единственной табличной части
		ТабличныеЧастиОснования = Новый Массив;
		ТабличныеЧастиОснования.Добавить(ОписаниеТабличныхЧастей);
	Иначе
		ТабличныеЧастиОснования = ОписаниеТабличныхЧастей;
	КонецЕсли;
	
	КоличествоТЧ = ТабличныеЧастиОснования.Количество();
	Если КоличествоТЧ = 0 Тогда
		УточнениеОшибки = НСтр("ru='Некорректный вызов ТекстЗапросаДляРасчетаСтатусаОформления'");
		ВызватьИсключение ИнтеграцияИСКлиентСервер.ТекстОшибки("ИСМП",УточнениеОшибки); // Некорректный вызов ТекстЗапросаДляРасчетаСтатусаОформления
	КонецЕсли;
	
	ШаблонВременнойТаблицыПодзапроса = 
	"
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаТовары.Ссылка КАК Ссылка,
	|	%ИмяПоляНоменклатураТЧТовары% КАК Номенклатура,
	|	%ИмяПоляХарактеристикаТЧТовары% КАК Характеристика,
	|ПОМЕСТИТЬ
	|	%ИмяВременнойТаблицы%
	|ИЗ
	|	Документ.%ИмяДокумента%%ИмяТабличнойЧастиТовары% КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка В (&МассивДокументов)
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	Номенклатура,
	|	Характеристика,
	|	Назначение";
	
	ШаблонЗапросаБезСерий =
	"ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка КАК Ссылка,
	|	%ВидДвиженияПриход% КАК ЭтоДвижениеПриход,
	|	%ИмяПоляНоменклатураТЧТовары% КАК Номенклатура,
	|	%ИмяПоляХарактеристикаТЧТовары% КАК Характеристика,
	|	%ИмяПоляСерияТЧТовары% КАК Серия,
	|	%ИмяПоляКоличествоТЧТовары% КАК Количество
	|%СозданиеВременнойТаблицы%
	|ИЗ
	|	Документ.%ИмяДокумента%%ИмяТабличнойЧастиТовары% КАК ТаблицаТовары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.%ИмяДокумента% КАК ТаблицаДокумента
	|		ПО ТаблицаДокумента.Ссылка = ТаблицаТовары.Ссылка
	|%ДопСоединения_Общие%
	|%ДопСоединения_ТаблицаТовары%
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО СправочникНоменклатура.Ссылка = %ИмяПоляНоменклатураТЧТовары%
	|
	|ГДЕ
	|	ТаблицаТовары.Ссылка В (&МассивДокументов)
	|	И %ОтборНоменклатуры%
	|%ДопОтборы_Общие%
	|%ДопОтборы_ТаблицаТовары%
	|";
	
	МассивТекстовСуммированияЗапросов = Новый Массив;
	МассивТекстовОсновногоЗапроса = Новый Массив;
	
	Для НомерТаблицы = 0 По КоличествоТЧ - 1 Цикл
		
		ОписаниеТЧ = ТабличныеЧастиОснования[НомерТаблицы];
		ИмяТабличнойЧастиТовары = ?(ОписаниеТЧ.ИмяТабличнойЧастиТовары = "", "", "." + ОписаниеТЧ.ИмяТабличнойЧастиТовары);
		
		ТекстЗапросаПоТабличнойЧасти = ШаблонЗапросаБезСерий;
		
		ТекстЗапросаПоТабличнойЧасти = СтрЗаменить(ТекстЗапросаПоТабличнойЧасти, "%ИмяДокумента%", МетаданныеДокументаОснования.Имя);
		ТекстЗапросаПоТабличнойЧасти = СтрЗаменить(ТекстЗапросаПоТабличнойЧасти, "%СозданиеВременнойТаблицы%",
			?(НомерТаблицы = 0, "ПОМЕСТИТЬ " + РасчетСтатусовОформленияИС.ИмяВременнойТаблицыДляВыборкиДанныхДокумента(), ""));
		
		ТекстЗапросаПоТабличнойЧасти = СтрЗаменить(ТекстЗапросаПоТабличнойЧасти, "%ВидДвиженияПриход%",
			Формат(ОписаниеТЧ.ВидДвиженияПриход, "БЛ=Ложь; БИ=Истина"));
		
		ТекстЗапросаПоТабличнойЧасти = СтрЗаменить(ТекстЗапросаПоТабличнойЧасти, "%ИмяТабличнойЧастиТовары%",       ИмяТабличнойЧастиТовары);
		ТекстЗапросаПоТабличнойЧасти = СтрЗаменить(ТекстЗапросаПоТабличнойЧасти, "%ИмяПоляНоменклатураТЧТовары%",   ОписаниеТЧ.ИмяПоляНоменклатураТЧТовары);
		ТекстЗапросаПоТабличнойЧасти = СтрЗаменить(ТекстЗапросаПоТабличнойЧасти, "%ИмяПоляХарактеристикаТЧТовары%", ОписаниеТЧ.ИмяПоляХарактеристикаТЧТовары);
		ТекстЗапросаПоТабличнойЧасти = СтрЗаменить(ТекстЗапросаПоТабличнойЧасти, "%ИмяПоляСерияТЧТовары%",          ОписаниеТЧ.ИмяПоляСерияТЧТовары);
		ТекстЗапросаПоТабличнойЧасти = СтрЗаменить(ТекстЗапросаПоТабличнойЧасти, "%ИмяПоляКоличествоТЧТовары%",     ОписаниеТЧ.ИмяПоляКоличествоТЧТовары);
		
		ТекстЗапросаПоТабличнойЧасти = СтрЗаменить(ТекстЗапросаПоТабличнойЧасти, "%ДопСоединения_Общие%",           ОписаниеТЧ.ДопСоединения_Общие);
		ТекстЗапросаПоТабличнойЧасти = СтрЗаменить(ТекстЗапросаПоТабличнойЧасти, "%ДопСоединения_ТаблицаТовары%",   ОписаниеТЧ.ДопСоединения_ТаблицаТовары);
		ТекстЗапросаПоТабличнойЧасти = СтрЗаменить(ТекстЗапросаПоТабличнойЧасти, "%ДопОтборы_Общие%",               ОписаниеТЧ.ДопОтборы_Общие);
		ТекстЗапросаПоТабличнойЧасти = СтрЗаменить(ТекстЗапросаПоТабличнойЧасти, "%ДопОтборы_ТаблицаТовары%",       ОписаниеТЧ.ДопОтборы_ТаблицаТовары);
		
		МассивТекстовОсновногоЗапроса.Добавить(ТекстЗапросаПоТабличнойЧасти);
		
	КонецЦикла;
	
	ШаблонОбъединение = "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|";
	МассивТекстовСуммированияЗапросов.Добавить(СтрСоединить(МассивТекстовОсновногоЗапроса, ШаблонОбъединение));
	
	ШаблонСуммирование = "
	|
	|;
	|
	|";
	
	ОбщийЗапрос = СтрСоединить(МассивТекстовСуммированияЗапросов,ШаблонСуммирование);
	ОбщийЗапрос = СтрЗаменить(ОбщийЗапрос, "%ОтборНоменклатуры%", ОтборНоменклатуры);
	Возврат ОбщийЗапрос;
	
КонецФункции

Функция ВДокументеДругиеВидыПродукцииИНетЗерна(ИмяДокумента, ИсточникНоменклатуры)
	
	Если ЗначениеЗаполнено(ИсточникНоменклатуры) Тогда 
		ИсточникНоменклатуры = "."+ИсточникНоменклатуры;
	КонецЕсли;
	
	Возврат СтрШаблон("
		|И НЕ 1 В (
		|	ВЫБРАТЬ 1 
		|	ИЗ Документ.%1%2 КАК Т
		|	ГДЕ
		|		Т.Ссылка = ТаблицаДокумента.Ссылка
		|		И Т.Номенклатура.ВидПродукцииИС В (
		|	ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Зерно),
		|	ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПродуктыПереработкиЗерна)))",
	    ИмяДокумента,
		ИсточникНоменклатуры);
		
КонецФункции

Функция ДокументПроведен()
	
	Возврат "
	|И ТаблицаДокумента.Проведен";
	
КонецФункции

Функция Импорт() Экспорт 
	
	Возврат "
	|И ТаблицаДокумента.Контрагент.НеЯвляетсяРезидентом = Истина";
	
КонецФункции

#КонецОбласти

#КонецОбласти
