// Возвращает параметры электронного документа для определения состояния.
// 
// Возвращаемое значение:
//  Структура:
//  * ТипРегламента - ПеречислениеСсылка.ТипыЭлементовВерсииЭД - Тип регламента ЭДО.
//  * СпособОбмена - Строка - Способ обмена электронными документами.
//  * ОбменБезПодписи - Булево - Обмен без подписи (прямой обмен).
//  * ТребуетсяПодтверждение - Булево - Необходимость формирования ответного титула или ответной подписи.
//  * ТребуетсяИзвещение - Булево - необходимость формирования извещения о получении.
//  * Остановлен - Булево - признак остановленного документа.
//  * ПричинаОстановки - Строка - причина остановки документа.
//  * Исправлен - Булево - признак того, что создан документ исправление.
//
Функция НовыеПараметрыДокументаДляОпределенияСостояния() Экспорт
	
	ПараметрыДокумента = Новый Структура;
	ПараметрыДокумента.Вставить("ТипРегламента", Перечисления.ВидыЭД.ПустаяСсылка());
	ПараметрыДокумента.Вставить("СпособОбмена", "");
	ПараметрыДокумента.Вставить("ОбменБезПодписи", Ложь);
	ПараметрыДокумента.Вставить("ТребуетсяИзвещение", Ложь);
	ПараметрыДокумента.Вставить("ТребуетсяПодтверждение", Ложь);
	ПараметрыДокумента.Вставить("Остановлен", Ложь);
	ПараметрыДокумента.Вставить("ПричинаОстановки", "");
	ПараметрыДокумента.Вставить("Исправлен", Ложь);
	
	Возврат ПараметрыДокумента;
	
КонецФункции

// Возвращает состояния электронного документа.
// 
// Параметры:
//  ПараметрыДокумента - См. НовыеПараметрыДокументаДляОпределенияСостояния
//  СостоянияЭлементовРегламента - См. НовыеСостоянияЭлементовРегламента
//  ЭтоВходящийЭДО - Булево
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СостоянияДокументовЭДО
//
Функция СостояниеДокумента(ПараметрыДокумента, СостоянияЭлементовРегламента, ЭтоВходящийЭДО = Ложь) Экспорт
	
	Если ПараметрыДокумента.Остановлен Тогда
		Состояние = СостояниеОстановленногоДокумента(ПараметрыДокумента.ПричинаОстановки,
			ПараметрыДокумента.Исправлен);
	ИначеЕсли ЭтоВходящийЭДО Тогда
		Состояние = СостояниеВходящегоДокумента(ПараметрыДокумента, СостоянияЭлементовРегламента);
	Иначе
		Состояние = СостояниеИсходящегоДокумента(ПараметрыДокумента, СостоянияЭлементовРегламента);
	КонецЕсли;
	
	Возврат Состояние;
	
КонецФункции

Функция СостоянияЭлементовРегламента(СсылкаНаЭД)
	
	СостоянияЭлементовРегламента = Новый ТаблицаЗначений;
	
	Запрос = Новый Запрос;
	//Запрос.Текст = "ВЫБРАТЬ
	//	               |	СостоянияЭД.СсылкаНаОбъект,
	//	               |	ЭДПрисоединенныеФайлы.ТипЭлементаВерсииЭД КАК ТипЭлементаРегламента,
	//	               |	СостоянияЭД.ЭлектронныйДокумент.Дата КАК Дата,
	//	               |	ЭДПрисоединенныеФайлы.СтатусЭД КАК Статус,
	//	               |	ЭДПрисоединенныеФайлы.Наименование КАК ПолноеИмяФайла,
	//	               |	ЭДПрисоединенныеФайлы.Ссылка
	//	               |ИЗ
	//	               |	РегистрСведений.СостоянияЭД КАК СостоянияЭД
	//	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЭДПрисоединенныеФайлы КАК ЭДПрисоединенныеФайлы
	//	               |		ПО СостоянияЭД.ЭлектронныйДокумент = ЭДПрисоединенныеФайлы.ВладелецФайла
	//	               |ГДЕ
	//	               |	ЭДПрисоединенныеФайлы.Ссылка = &ОбъектУчета
	//	               |
	//	               |УПОРЯДОЧИТЬ ПО
	//	               |	СостоянияЭД.СсылкаНаОбъект.МоментВремени";
	
	Запрос.Текст = "ВЫБРАТЬ
		               |	СостоянияЭД.СсылкаНаОбъект,
		               |	ЭДПрисоединенныеФайлы.ТипЭлементаВерсииЭД КАК ТипЭлементаРегламента,
		               |	ЭДПрисоединенныеФайлы.ДатаСоздания КАК Дата,
		               |	ЭДПрисоединенныеФайлы.СтатусЭД КАК Состояние,
		               |	ЭДПрисоединенныеФайлы.Наименование КАК ПолноеИмяФайла,
		               |	ЭДПрисоединенныеФайлы.Ссылка
		               |ИЗ
		               |	Справочник.ЭДПрисоединенныеФайлы КАК ЭДПрисоединенныеФайлы
		               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияЭД КАК СостоянияЭД
		               |		ПО СостоянияЭД.ЭлектронныйДокумент = ЭДПрисоединенныеФайлы.ВладелецФайла
		               |ГДЕ
		               |	ЭДПрисоединенныеФайлы.ВладелецФайла = &ОбъектУчета
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	ЭДПрисоединенныеФайлы.ДатаСоздания";
	
	
	Запрос.УстановитьПараметр("ОбъектУчета", СсылкаНаЭД.ВладелецФайла);
	
	РезультатВыборки = Запрос.Выполнить();
	
	Если РезультатВыборки.Пустой() Тогда
		Возврат СостоянияЭлементовРегламента;
	КонецЕсли;
	
	СостоянияЭлементовРегламента = РезультатВыборки.Выгрузить();
	
	Возврат СостоянияЭлементовРегламента;
	
КонецФункции

Функция ВычислитьСостояниеДокумента(СсылкаНаЭД) Экспорт
	
	РеквизитыФайлаЭД = ЭлектронныеДокументыСлужебный.ЗначенияРеквизитовОбъектаИлиСсылки(СсылкаНаЭД,
		"ТипЭлементаВерсииЭД, СтатусЭД, ВладелецФайла, Ссылка, ВидЭД, Наименование");
	Если НЕ ЗначениеЗаполнено(РеквизитыФайлаЭД.ТипЭлементаВерсииЭД) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	РеквизитыВладельцаЭД = ЭлектронныеДокументыСлужебный.ЗначенияРеквизитовОбъектаИлиСсылки(РеквизитыФайлаЭД.ВладелецФайла, 
		"СостояниеЭДО, ВидЭД, НастройкаЭДО");
	
	РеквизитыНастройкаЭДО = ЭлектронныеДокументыСлужебный.ЗначенияРеквизитовОбъектаИлиСсылки(РеквизитыВладельцаЭД.НастройкаЭДО,
		"ИдентификаторОрганизации");
	
	НастройкиОбмена = ЭлектронныеДокументыСлужебный.НастройкиОбменаЭД(СсылкаНаЭД);
	ЭтоВходящий = НастройкиОбмена.Направление = Перечисления.НаправленияЭД.Входящий;
	
	СостоянияЭлементовРегламента = СостоянияЭлементовРегламента(СсылкаНаЭД);
	Если СостоянияЭлементовРегламента.Найти(РеквизитыФайлаЭД.ТипЭлементаВерсииЭД, "ТипЭлементаРегламента") = Неопределено Тогда
		Строка = СостоянияЭлементовРегламента.Добавить();
		Строка.ТипЭлементаРегламента = РеквизитыФайлаЭД.ТипЭлементаВерсииЭД;
		Если ЭтоВходящий Тогда
			Строка.Состояние = Перечисления.СтатусыЭД.Получен;
		Иначе
			Строка.Состояние = Перечисления.СтатусыЭД.Сформирован;
		КонецЕсли;
		Строка.Ссылка = РеквизитыФайлаЭД.Ссылка;
		Строка.ПолноеИмяФайла = РеквизитыФайлаЭД.Наименование;
	КонецЕсли;
	
	ПараметрыДокумента = РегламентыЭДО.НовыеПараметрыДокументаДляОпределенияСостояния();
	ПараметрыДокумента.ТипРегламента = РеквизитыВладельцаЭД.ВидЭД;
	ПараметрыДокумента.Вставить("ИдентификаторОрганизации", РеквизитыНастройкаЭДО.ИдентификаторОрганизации);
	
	Попытка
		Состояние = СостояниеДокумента(ПараметрыДокумента, СостоянияЭлементовРегламента, ЭтоВходящий);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ТекстОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат Состояние;
	
КонецФункции

// Возвращает состояния входящего электронного документа.
// 
// Параметры:
//  ПараметрыДокумента - См. НовыеПараметрыДокументаДляОпределенияСостояния
//  СостоянияЭлементовРегламента - См. НовыеСостоянияЭлементовРегламента
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СостоянияДокументовЭДО
//
Функция СостояниеВходящегоДокумента(ПараметрыДокумента, СостоянияЭлементовРегламента)
	
	Если Не ЗначениеЗаполнено(СостоянияЭлементовРегламента) Тогда
		Возврат НачальноеСостояниеДокумента();
	КонецЕсли;
	
	МенеджерРегламента = МенеджерРегламента(ПараметрыДокумента.ТипРегламента);
	Возврат МенеджерРегламента.СостояниеВходящегоДокумента(ПараметрыДокумента, СостоянияЭлементовРегламента);
	
КонецФункции

// Возвращает состояния исходящего электронного документа.
// 
// Параметры:
//  ПараметрыДокумента - См. НовыеПараметрыДокументаДляОпределенияСостояния
//  СостоянияЭлементовРегламента - См. НовыеСостоянияЭлементовРегламента
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СостоянияДокументовЭДО
//
Функция СостояниеИсходящегоДокумента(ПараметрыДокумента, СостоянияЭлементовРегламента)
	
	Если Не ЗначениеЗаполнено(СостоянияЭлементовРегламента) Тогда
		Возврат НачальноеСостояниеДокумента();
	КонецЕсли;
	
	МенеджерРегламента = МенеджерРегламента(ПараметрыДокумента.ТипРегламента);
	Возврат МенеджерРегламента.СостояниеИсходящегоДокумента(ПараметрыДокумента, СостоянияЭлементовРегламента);
	
КонецФункции

// Общий модуль обработки указанного типа регламента.
// 
// Параметры:
//  ТипРегламента - ПеречислениеСсылка.ВидыЭД - Тип регламента ЭДО.
// 
// Возвращаемое значение:
//  - ОбщийМодуль
//
Функция МенеджерРегламента(ТипРегламента)
	
	Если ОбщегоНазначенияКлиентСервер.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСГИСЭПД") Тогда
		МодульОбменСГИСЭПД = ОбщегоНазначенияКлиентСервер.ОбщийМодуль("ОбменСГИСЭПД");
		РегламентЭПД = МодульОбменСГИСЭПД.РегламентЭПД(ТипРегламента);
	КонецЕсли;
	Если РегламентЭПД <> Неопределено Тогда
		Возврат РегламентЭПД;
	Иначе
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Некорректный тип регламента ЭДО: %1'"), ТипРегламента);
	КонецЕсли;
	
КонецФункции

// Возвращает признак наличия элемента регламента.
// 
// Параметры:
//  КоллекцияЭлементовРегламента - См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
//  ТипЭлементаРегламента        - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
//  ЭлементРегламента            - Неопределено,
//                                 СтрокаТаблицыЗначений: См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
//
// Возвращаемое значение:
//  Булево
//
Функция ЕстьЭлементРегламента(КоллекцияЭлементовРегламента, ТипЭлементаРегламента, ЭлементРегламента = Неопределено) Экспорт
	
	ЭлементРегламента = КоллекцияЭлементовРегламента.Найти(ТипЭлементаРегламента, "ТипЭлементаРегламента");
	Возврат ЭлементРегламента <> Неопределено;
	
КонецФункции

// Начальное состояние исходящего электронного документа.
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.СостоянияДокументовЭДО
//
Функция НачальноеСостояниеДокумента() Экспорт
	Возврат Перечисления.СостоянияВерсийЭД.НеСформирован;
КонецФункции

// Возвращает состояние остановленного документа.
// 
// Параметры:
//  ПричинаОстановки - ПеречислениеСсылка.ПричиныОстановкиЭДО
//  Исправлен - Булево
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СостоянияДокументовЭДО
//
Функция СостояниеОстановленногоДокумента(ПричинаОстановки, Исправлен)
	
	Состояние = Перечисления.СостоянияВерсийЭД.ЗакрытПринудительно;
	
	Возврат Состояние;
	
КонецФункции

// Возвращает коллекцию добавленных элементов схемы регламента.
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//  * Ключ - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
//  * Значение - СтрокаДереваЗначений:
//  ** ТипЭлементаРегламента - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
//  ** Дополнительный - Булево
//  ** Данные - СтрокаТаблицыЗначений
//
Функция НоваяКоллекцияЭлементовСхемыРегламента() Экспорт
	Возврат Новый Соответствие;
КонецФункции