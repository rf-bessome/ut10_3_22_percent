//@strict-types

#Область СлужебныеПроцедурыИФункции

#Область ДляВызоваИзМодуляРегламентыЭДО

// Возвращает состояние входящего электронного документа.
// 
// Параметры:
//  ПараметрыДокумента - См. РегламентыЭДО.НовыеПараметрыДокументаДляОпределенияСостояния
//  СостоянияЭлементовРегламента - См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.СостоянияДокументовЭДО
//
Функция СостояниеВходящегоДокумента(ПараметрыДокумента, СостоянияЭлементовРегламента) Экспорт
	
	Состояние = Перечисления.СостоянияВерсийЭД.ПустаяСсылка();
	
	Если ЗаполнитьСостояниеПоВходящейИнформацииОтправителя(ПараметрыДокумента, СостоянияЭлементовРегламента, Состояние)
		ИЛИ ЗначениеЗаполнено(Состояние) Тогда
		
		Возврат Состояние;
		
	ИначеЕсли ПараметрыДокумента.Исправлен Тогда
		
		Возврат Перечисления.СостоянияВерсийЭД.ОбменЗавершенСИсправлением;
		
	КонецЕсли;
	
	Возврат Перечисления.СостоянияВерсийЭД.ОбменЗавершен;
	
КонецФункции

// Возвращает состояние исходящего электронного документа.
// 
// Параметры:
//  ПараметрыДокумента - См. РегламентыЭДО.НовыеПараметрыДокументаДляОпределенияСостояния
//  СостоянияЭлементовРегламента - См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.СостоянияДокументовЭДО
//
Функция СостояниеИсходящегоДокумента(ПараметрыДокумента, СостоянияЭлементовРегламента) Экспорт
	
	Состояние = Перечисления.СостоянияВерсийЭД.ПустаяСсылка();
	
	Если ЗаполнитьСостояниеПоИсходящейИнформацииОтправителя(ПараметрыДокумента, СостоянияЭлементовРегламента, Состояние)
		ИЛИ ЗначениеЗаполнено(Состояние) Тогда
		
		Возврат Состояние;
		
	ИначеЕсли ПараметрыДокумента.Исправлен Тогда
		
		Возврат Перечисления.СостоянияВерсийЭД.ОбменЗавершенСИсправлением;
		
	КонецЕсли;
	
	Возврат Перечисления.СостоянияВерсийЭД.ОбменЗавершен;
	
КонецФункции

// Возвращает состояние сообщения.
//
// Параметры:
//  ПараметрыСообщения - См. РегламентыЭДО.НовыеПараметрыСообщенияДляОпределенияСостояния
//  ПараметрыДокумента - См. РегламентыЭДО.НовыеПараметрыДокументаДляОпределенияСостоянияСообщения
//  ИспользоватьУтверждение  - Булево
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СостоянияСообщенийЭДО
//
Функция СостояниеСообщения(ПараметрыСообщения, ПараметрыДокумента, ИспользоватьУтверждение) Экспорт
	
	Состояние = Перечисления.СтатусыЭД.ПустаяСсылка();
	
	Статус = ПараметрыСообщения.Статус;
	
	Если Статус = Перечисления.СтатусыЭД.Получен Тогда
		
		Состояние = Перечисления.СтатусыЭД.Получен;
		
	ИначеЕсли Статус = Перечисления.СтатусыЭД.Утвержден Тогда
		
		Состояние = Перечисления.СтатусыЭД.Получен;
		
	ИначеЕсли Статус = Перечисления.СтатусыЭД.Сформирован Тогда
		
		Если ПараметрыДокумента.ОбменБезПодписи Тогда
			Состояние = Перечисления.СтатусыЭД.Сформирован;
		Иначе
			Состояние = Перечисления.СтатусыЭД.Сформирован;
		КонецЕсли;
		
	ИначеЕсли Статус = Перечисления.СтатусыЭД.ЧастичноПодписан Тогда
		
		Состояние = Перечисления.СтатусыЭД.Сформирован;
		
	ИначеЕсли Статус = Перечисления.СтатусыЭД.Подписан Тогда
		
		Состояние = Перечисления.СтатусыЭД.Сформирован;
				
	ИначеЕсли Статус = Перечисления.СтатусыЭД.ПодготовленКОтправке Тогда
		
		Состояние = Перечисления.СтатусыЭД.ПодготовленКОтправке;
		
	ИначеЕсли Статус = Перечисления.СтатусыЭД.Отправлен Тогда
		
		Состояние = Перечисления.СтатусыЭД.Получен; 

	КонецЕсли;
	
	Возврат Состояние;
	
КонецФункции


// Параметры:
//  СхемаРегламента - см. РегламентыЭДО.НоваяСхемаРегламента
//  НастройкиСхемыРегламента - см. РегламентыЭДО.НовыеНастройкиСхемыРегламента
//  ТипЭлементаРегламента - ПеречислениеСсылка.ТипыЭлементовВерсииЭД
// 
// Возвращаемое значение:
//  См. РегламентыЭДО.НоваяКоллекцияЭлементовСхемыРегламента
Функция ДобавитьЭлементыСхемыВложенногоРегламента(СхемаРегламента, НастройкиСхемыРегламента, ТипЭлементаРегламента) Экспорт
	
	Возврат РегламентыЭДО.НоваяКоллекцияЭлементовСхемыРегламента();
	
КонецФункции

// Возвращает тип извещения для элемента входящего документа.
// 
// Параметры:
//  ТипЭлементаРегламента - ПеречислениеСсылка.ТипыЭлементовВерсииЭД
//
// Возвращаемое значение:
//  ПеречислениеСсылка.ТипыЭлементовВерсииЭД
//
Функция ТипИзвещенияДляЭлементаВходящегоДокумента(ТипЭлементаРегламента) Экспорт
	

	Результат = Перечисления.ТипыЭлементовВерсииЭД.ПустаяСсылка();
	
	Возврат Результат;
	
КонецФункции

// Возвращает тип извещения для элемента исходящего документа.
// 
// Параметры:
//  ТипЭлементаРегламента - ПеречислениеСсылка.ТипыЭлементовВерсииЭД
//
// Возвращаемое значение:
//  ПеречислениеСсылка.ТипыЭлементовВерсииЭД
//
Функция ТипИзвещенияДляЭлементаИсходящегоДокумента(ТипЭлементаРегламента) Экспорт
	
	Результат = Перечисления.ТипыЭлементовВерсииЭД.ПустаяСсылка();
	
	Возврат Результат;
	
КонецФункции

// Возвращает признак наличия информации получателя в регламенте.
// 
// Возвращаемое значение:
//  Булево
//
Функция ЕстьИнформацияПолучателя() Экспорт
	Возврат Истина;
КонецФункции

#КонецОбласти

#Область СостояниеДокумента

// Возвращает признак успешности заполнения состояния по входящей информации отправителя.
// 
// Параметры:
//  СостоянияЭлементовРегламента - См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
//  Состояние - ПеречислениеСсылка.СостоянияДокументовЭДО
//
// Возвращаемое значение:
//  Булево
//
Функция ЗаполнитьСостояниеПоВходящейИнформацииОтправителя(ПараметрыДокумента, СостоянияЭлементовРегламента, Состояние)
	
	Результат = Истина;

	Результат = Истина;
	ЭлементРегламента = Неопределено; // Неопределено,СтрокаТаблицыЗначений: См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
	
	ОбменЗавершен = Ложь;
	
	Если ЗаполнитьСостояниеПоОтклонению(ПараметрыДокумента, 
									СостоянияЭлементовРегламента, Состояние) Тогда
										
		Возврат Истина;
		
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭПЛ_Титул6, ЭлементРегламента) Тогда
			
			ОбменЗавершен = Истина;
		
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭПЛ_Титул5, ЭлементРегламента) Тогда
		
		РольУчастника = РольУчастникаЭлементаРегламента(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента);
			
		Если РольУчастника = 8 Тогда
			
				ОбменЗавершен = Истина;
				
		Иначе                    
			МассивЭД = Новый Массив;
			МассивЭД.Добавить(ЭлементРегламента.Ссылка);
			СоответствиеЭД = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСоответствиеВладельцевИЭД(, МассивЭД);
			
			ОснованиеЭД = Неопределено;
			Для Каждого КиЗ Из СоответствиеЭД Цикл
				ВладелецЭД = КиЗ.Ключ;
				ОснованияЭД = ЭлектронныеДокументыСлужебныйВызовСервера.ОснованияЭлектронногоДокумента(ВладелецЭД);
				Если ОснованияЭД.Количество() > 0 Тогда
					ОснованиеЭД = ОснованияЭД[0];	
				КонецЕсли;
				Прервать;
			КонецЦикла;
				
			Если ОснованиеЭД <> Неопределено Тогда
				Если ОснованиеЭД.ТитулОформлениеОбязательностьМедОсмотраПосле = "2" Тогда	
					ОбменЗавершен = Истина;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
			
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭПЛ_Титул4, ЭлементРегламента) Тогда
		
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭПЛ_Титул3, ЭлементРегламента) Тогда
		
		РольУчастника = РольУчастникаЭлементаРегламента(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента);
		
		Если РольУчастника = 4 Тогда
			
				ОбменЗавершен = Истина;
				
		КонецЕсли;
		
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭПЛ_Титул2, ЭлементРегламента) Тогда
		
		РольУчастника = РольУчастникаЭлементаРегламента(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента);
		
		Если РольУчастника = 2 Тогда
			
				ОбменЗавершен = Истина;
				
		КонецЕсли;

	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭПЛ_Титул1, ЭлементРегламента) Тогда
		
	Иначе
		
		Состояние = РегламентыЭДО.НачальноеСостояниеДокумента();
		Возврат Ложь;
		
	КонецЕсли;	
	
	Если ОбменЗавершен = Истина 
		И (ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.ПереданОператору
			Или ЭтоВходящийЭлементРегламента(ЭлементРегламента)) Тогда
		Состояние = Перечисления.СостоянияВерсийЭД.ОбменЗавершен;
	ИначеЕсли ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.ПодготовленКОтправке Тогда
		Состояние = Перечисления.СостоянияВерсийЭД.ОжидаетсяОтправка;
	ИначеЕсли ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.Сформирован Тогда
		Состояние = Перечисления.СостоянияВерсийЭД.ОжидаетсяОтправка;
	Иначе
		Если СостоянияЭлементовРегламента.Колонки.Найти("Ссылка") = Неопределено Тогда
			// Новый документ
			ТекущийДоступныйТитул = Неопределено;
		Иначе	
			ТекущийДоступныйТитул = ТекущийДоступныйТитул(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента);
		КонецЕсли;
		
		Если ЗаполнитьСостояниеДляИзвещения(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента, Состояние) Тогда
			Возврат Истина;		
		ИначеЕсли ЗначениеЗаполнено(ТекущийДоступныйТитул) 
			Тогда
			Состояние = Перечисления.СостоянияВерсийЭД.НаПодписи;
		Иначе	
			Состояние = Перечисления.СостоянияВерсийЭД.ОжидаетсяПодтверждение;	
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции


Функция ЗаполнитьСостояниеДляИзвещения(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента, Состояние)
	
	Возврат Ложь;
	
КонецФункции


Функция РольУчастникаЭлементаРегламента(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента)
	
	Если ТипЗнч(ЭлементРегламента) = Тип("СтрокаТаблицыЗначений") Тогда
		Если ЭлементРегламента.Владелец().Колонки.Найти("Ссылка") <> Неопределено
			И ТипЗнч(ЭлементРегламента.Ссылка) = Тип("СправочникСсылка.ЭДПрисоединенныеФайлы") Тогда
				ИдФайл = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭлементРегламента.Ссылка, "НаименованиеФайла");
				Если ТипЗнч(ИдФайл) = Тип("Строка") Тогда		
					Адресаты = ОбменСГИСЭПДКлиентСервер.АдресатыПоИдФайл(ИдФайл);			
					ИдентификаторОрганизации = Неопределено;
					Если ТипЗнч(ПараметрыДокумента) = Тип("Структура") Тогда
						ПараметрыДокумента.Свойство("ИдентификаторОрганизации", ИдентификаторОрганизации);
					ИначеЕсли ТипЗнч(ПараметрыДокумента) = Тип("ВыборкаИзРезультатаЗапроса") Тогда
						Если ПараметрыДокумента.Владелец().Колонки.Найти("ИдентификаторОрганизации") <> Неопределено Тогда
							ИдентификаторОрганизации = ПараметрыДокумента.ИдентификаторОрганизации;
						КонецЕсли;
					КонецЕсли;		
					РольУчастника = 0;
					Если ИдентификаторОрганизации = Адресаты.Оформитель Тогда
						РольУчастника = РольУчастника + 1;
					КонецЕсли;
					Если ИдентификаторОрганизации = Адресаты.Медосмотр Тогда 
						РольУчастника = РольУчастника + 2;
					КонецЕсли;
					Если ИдентификаторОрганизации = Адресаты.Техконтроль Тогда 
						РольУчастника = РольУчастника + 4;
					КонецЕсли;
					Если ИдентификаторОрганизации = Адресаты.Одометр Тогда
						РольУчастника = РольУчастника + 8;
					КонецЕсли;
				КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат РольУчастника;
	
КонецФункции


Функция ТекущийДоступныйТитул(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента)
	
	РольУчастника = РольУчастникаЭлементаРегламента(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента);
	
	Результат = Неопределено;
	// Одометр
	Если ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(РольУчастника, 8) Тогда	
		Если ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭПЛ_Титул4") Тогда
			Результат = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭПЛ_Титул5");	
		ИначеЕсли ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭПЛ_Титул3") Тогда
			Результат = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭПЛ_Титул4");
		ИначеЕсли (ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭПЛ_Титул4")
				Или ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭПЛ_Титул5"))
			И (ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.НеСформирован
				Или ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.Сформирован) Тогда
				Результат = ЭлементРегламента.ТипЭлементаРегламента;
		КонецЕсли;			
	// Техконтроль
	ИначеЕсли ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(РольУчастника, 4) Тогда
		Если ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭПЛ_Титул2") Тогда
			Результат = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭПЛ_Титул3");
		ИначеЕсли ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭПЛ_Титул3")
			И (ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.НеСформирован
				Или ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.Сформирован) Тогда	
			Результат = ЭлементРегламента.ТипЭлементаРегламента;		
		КонецЕсли;
	// Медосмотр
	ИначеЕсли ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(РольУчастника, 2) Тогда	
		Если ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭПЛ_Титул5") Тогда	
			Результат = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭПЛ_Титул6");
		ИначеЕсли ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭПЛ_Титул1") Тогда	
			Результат = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭПЛ_Титул2");
		ИначеЕсли (ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭПЛ_Титул2")
				Или ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭПЛ_Титул6"))
			И (ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.НеСформирован
				Или ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.Сформирован) Тогда
			Результат = ЭлементРегламента.ТипЭлементаРегламента;
		КонецЕсли;
	// Оформление
	ИначеЕсли ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(РольУчастника, 1) Тогда
		Если ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭПЛ_Титул1")
			И (ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.НеСформирован
				Или ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.Сформирован) Тогда	
			Результат = ЭлементРегламента.ТипЭлементаРегламента;		
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции


// Возвращает признак успешности заполнения состояния по исходящей информации отправителя.
// 
// Параметры:
//  ПараметрыДокумента - См. РегламентыЭДО.НовыеПараметрыДокументаДляОпределенияСостояния
//  СостоянияЭлементовРегламента - См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
//  Состояние - ПеречислениеСсылка.СостоянияДокументовЭДО
//
// Возвращаемое значение:
//  Булево
//
Функция ЗаполнитьСостояниеПоИсходящейИнформацииОтправителя(ПараметрыДокумента, СостоянияЭлементовРегламента, Состояние)
	
	Результат = Истина;
	ЭлементРегламента = Неопределено; // Неопределено,СтрокаТаблицыЗначений: См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
	
	ОбменЗавершен = Ложь;
	
	Если ЗаполнитьСостояниеПоОтклонению(ПараметрыДокумента, 
									СостоянияЭлементовРегламента, Состояние) Тогда
										
		Возврат Истина;
		
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭПЛ_Титул6, ЭлементРегламента) Тогда
			
			ОбменЗавершен = Истина;
		
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭПЛ_Титул5, ЭлементРегламента) Тогда
		
		РольУчастника = РольУчастникаЭлементаРегламента(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента);
			
		Если РольУчастника = 8 Тогда
			
			ОбменЗавершен = Истина;
				
		Иначе
			МассивЭД = Новый Массив;
			МассивЭД.Добавить(ЭлементРегламента.Ссылка);
			СоответствиеЭД = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСоответствиеВладельцевИЭД(, МассивЭД);
			
			ВладелецЭД = СоответствиеЭД.Получить(ЭлементРегламента.Ссылка);
			Если ВладелецЭД <> Неопределено Тогда
				Если ВладелецЭД.ТитулОформлениеОбязательностьМедОсмотраПосле = "2" Тогда	
					ОбменЗавершен = Истина;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
			
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭПЛ_Титул4, ЭлементРегламента) Тогда
		
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭПЛ_Титул3, ЭлементРегламента) Тогда
		
		РольУчастника = РольУчастникаЭлементаРегламента(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента);
		
		Если РольУчастника = 4 Тогда
			
				ОбменЗавершен = Истина;
				
		КонецЕсли;
		
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭПЛ_Титул2, ЭлементРегламента) Тогда
		
		РольУчастника = РольУчастникаЭлементаРегламента(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента);
		
		Если РольУчастника = 2 Тогда
			
				ОбменЗавершен = Истина;
				
		КонецЕсли;
		
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭПЛ_Титул1, ЭлементРегламента) Тогда
		
	Иначе
		
		Состояние = РегламентыЭДО.НачальноеСостояниеДокумента();
		Возврат Ложь;
		
	КонецЕсли;
	
	Если ОбменЗавершен = Истина 
		И (ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.ПереданОператору
			Или ЭтоВходящийЭлементРегламента(ЭлементРегламента)) Тогда
		Состояние = Перечисления.СостоянияВерсийЭД.ОбменЗавершен;
	ИначеЕсли ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.ПодготовленКОтправке Тогда
		Состояние = Перечисления.СостоянияВерсийЭД.ОжидаетсяОтправка;
	ИначеЕсли ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.Сформирован Тогда
		Состояние = Перечисления.СостоянияВерсийЭД.ОжидаетсяОтправка;
	Иначе
		Если СостоянияЭлементовРегламента.Колонки.Найти("Ссылка") = Неопределено Тогда
			// Новый документ
			ТекущийДоступныйТитул = Перечисления.ТипыЭлементовВерсииЭД.ЭПЛ_Титул1;
		Иначе	
			ТекущийДоступныйТитул = ТекущийДоступныйТитул(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента);
		КонецЕсли;
		
		Если ЗаполнитьСостояниеДляИзвещения(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента, Состояние) Тогда
			Возврат Истина;		
		ИначеЕсли ЗначениеЗаполнено(ТекущийДоступныйТитул) 
			Тогда
			Состояние = Перечисления.СостоянияВерсийЭД.НаПодписи;
		Иначе
			Состояние = Перечисления.СостоянияВерсийЭД.ОжидаетсяПодтверждение;	
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ЭтоВходящийЭлементРегламента(ЭлементРегламента)
	
	Если ЭлементРегламента.Владелец().Колонки.Найти("Ссылка") = Неопределено Тогда
		Результат = Ложь;
	Иначе
		СообщениеСсылка = ЭлементРегламента.Ссылка;
		Если ТипЗнч(СообщениеСсылка) = Тип("СправочникСсылка.ЭДПрисоединенныеФайлы") Тогда
			Результат = ТипЗнч(СообщениеСсылка.ВладелецФайла) = Тип("ДокументСсылка.ЭлектронныйДокументВходящий");	
		Иначе
			Результат = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает признак успешности заполнения состояния по исходящему отклонению.
// 
// Параметры:
//  ПараметрыДокумента - См. РегламентыЭДО.НовыеПараметрыДокументаДляОпределенияСостояния
//  СостоянияЭлементовРегламента - См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
//  Состояние - ПеречислениеСсылка.СостоянияДокументовЭДО
//
// Возвращаемое значение:
//  Булево
//
Функция ЗаполнитьСостояниеПоОтклонению(ПараметрыДокумента, СостоянияЭлементовРегламента, Состояние)
	
	Результат = Истина;
	ЭлементРегламента = Неопределено; // Неопределено,СтрокаТаблицыЗначений: См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
	
	Если ПараметрыДокумента.Исправлен = Истина Тогда
		Результат = Ложь;	
	КонецЕсли;
	
	Если Не ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭПЛ_ОткМ, ЭлементРегламента) Тогда
		
		Результат = Ложь;
		
	ИначеЕсли ЭтоВходящийЭлементРегламента(ЭлементРегламента) Тогда
		
		Состояние = Перечисления.СостоянияВерсийЭД.ОбменЗавершенСИсправлением;			
		
	ИначеЕсли ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.Сформирован Тогда 
		
		Состояние = Перечисления.СостоянияВерсийЭД.НаПодписи; //НаПодписиОтклонения;
		
	ИначеЕсли ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.Подписан Тогда // НаПодписиОтклонения
		
		Состояние = Перечисления.СостоянияВерсийЭД.ОжидаетсяОтправка; //ОжидаетсяОтправкаОтклонения;
		
	ИначеЕсли ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.ПодготовленКОтправке Тогда 
		
		Состояние = Перечисления.СостоянияВерсийЭД.ОжидаетсяОтправка; // ОжидаетсяОтправкаОтклонения;
		
	ИначеЕсли Не ПараметрыДокумента.Исправлен Тогда
		
		Состояние = Перечисления.СостоянияВерсийЭД.ОбменЗавершенСИсправлением;	
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти