//@strict-types

#Область СлужебныеПроцедурыИФункции

#Область ДляВызоваИзМодуляРегламентыЭДО

// Возвращает состояние входящего электронного документа.
// 
// Параметры:
//  ПараметрыДокумента - См. РегламентыЭДО.НовыеПараметрыДокументаДляОпределенияСостояния
//  СостоянияЭлементовРегламента - См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.СостоянияДокументовЭДО
//
Функция СостояниеВходящегоДокумента(ПараметрыДокумента, СостоянияЭлементовРегламента) Экспорт
	
	Состояние = Перечисления.СостоянияВерсийЭД.ПустаяСсылка();
	
	Если ЗаполнитьСостояниеПоВходящейИнформацииОтправителя(ПараметрыДокумента, СостоянияЭлементовРегламента, Состояние)
		ИЛИ ЗначениеЗаполнено(Состояние) Тогда
		
		Возврат Состояние;
		
	ИначеЕсли ПараметрыДокумента.Исправлен Тогда
		
		Возврат Перечисления.СостоянияВерсийЭД.ОбменЗавершенСИсправлением;
		
	КонецЕсли;
	
	Возврат Перечисления.СостоянияВерсийЭД.ОбменЗавершен;
	
КонецФункции

// Возвращает состояние исходящего электронного документа.
// 
// Параметры:
//  ПараметрыДокумента - См. РегламентыЭДО.НовыеПараметрыДокументаДляОпределенияСостояния
//  СостоянияЭлементовРегламента - См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.СостоянияДокументовЭДО
//
Функция СостояниеИсходящегоДокумента(ПараметрыДокумента, СостоянияЭлементовРегламента) Экспорт
	
	Состояние = Перечисления.СостоянияВерсийЭД.ПустаяСсылка();
	
	Если ЗаполнитьСостояниеПоИсходящейИнформацииОтправителя(ПараметрыДокумента, СостоянияЭлементовРегламента, Состояние)
		ИЛИ ЗначениеЗаполнено(Состояние) Тогда
		
		Возврат Состояние;
		
	ИначеЕсли ПараметрыДокумента.Исправлен Тогда
		
		Возврат Перечисления.СостоянияВерсийЭД.ОбменЗавершенСИсправлением;
		
	КонецЕсли;
	
	Возврат Перечисления.СостоянияВерсийЭД.ОбменЗавершен;
	
КонецФункции

// Возвращает состояние сообщения.
//
// Параметры:
//  ПараметрыСообщения - См. РегламентыЭДО.НовыеПараметрыСообщенияДляОпределенияСостояния
//  ПараметрыДокумента - См. РегламентыЭДО.НовыеПараметрыДокументаДляОпределенияСостоянияСообщения
//  ИспользоватьУтверждение  - Булево
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СостоянияСообщенийЭДО
//
Функция СостояниеСообщения(ПараметрыСообщения, ПараметрыДокумента, ИспользоватьУтверждение) Экспорт
	
	Состояние = Перечисления.СтатусыЭД.ПустаяСсылка();
	
	Статус = ПараметрыСообщения.Статус;
	
	Если Статус = Перечисления.СтатусыЭД.Получен Тогда
		
		Состояние = Перечисления.СтатусыЭД.Получен;
		
	ИначеЕсли Статус = Перечисления.СтатусыЭД.Утвержден Тогда
		
		Состояние = Перечисления.СтатусыЭД.Получен;
		
	ИначеЕсли Статус = Перечисления.СтатусыЭД.Сформирован Тогда
		
		Если ПараметрыДокумента.ОбменБезПодписи Тогда
			Состояние = Перечисления.СтатусыЭД.Сформирован;
		Иначе
			Состояние = Перечисления.СтатусыЭД.Сформирован;
		КонецЕсли;
		
	ИначеЕсли Статус = Перечисления.СтатусыЭД.ЧастичноПодписан Тогда
		
		Состояние = Перечисления.СтатусыЭД.Сформирован;
		
	ИначеЕсли Статус = Перечисления.СтатусыЭД.Подписан Тогда
		
		Состояние = Перечисления.СтатусыЭД.Сформирован;
				
	ИначеЕсли Статус = Перечисления.СтатусыЭД.ПодготовленКОтправке Тогда
		
		Состояние = Перечисления.СтатусыЭД.ПодготовленКОтправке;
		
	ИначеЕсли Статус = Перечисления.СтатусыЭД.Отправлен Тогда
		
		Состояние = Перечисления.СтатусыЭД.Получен; 

	КонецЕсли;
	
	Возврат Состояние;
	
КонецФункции

// Возвращает тип извещения для элемента входящего документа.
// 
// Параметры:
//  ТипЭлементаРегламента - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
//
// Возвращаемое значение:
//  ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
//
Функция ТипИзвещенияДляЭлементаВходящегоДокумента(ТипЭлементаРегламента) Экспорт
	
	Если ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_Титул1") Тогда
		Результат = Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул1_ИОП;
	ИначеЕсли ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_Титул3") Тогда
		Результат = Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул3_ИОП;
	ИначеЕсли ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_Титул5") Тогда
		Результат = Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул5_ИОП;	
	Иначе
		Результат = Перечисления.ТипыЭлементовВерсииЭД.ПустаяСсылка();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает тип извещения для элемента исходящего документа.
// 
// Параметры:
//  ТипЭлементаРегламента - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
//
// Возвращаемое значение:
//  ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
//
Функция ТипИзвещенияДляЭлементаИсходящегоДокумента(ТипЭлементаРегламента) Экспорт
	
	Если ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_Титул1") Тогда
		Результат = Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул1_ИОП;
	ИначеЕсли ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_Титул3") Тогда
		Результат = Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул3_ИОП;
	ИначеЕсли ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_Титул5") Тогда
		Результат = Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул5_ИОП;	
	Иначе
		Результат = Перечисления.ТипыЭлементовВерсииЭД.ПустаяСсылка();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает признак наличия информации получателя в регламенте.
// 
// Возвращаемое значение:
//  Булево
//
Функция ЕстьИнформацияПолучателя() Экспорт
	Возврат Истина;
КонецФункции

#КонецОбласти


#Область СостояниеДокумента

// Возвращает признак успешности заполнения состояния по входящей информации отправителя.
// 
// Параметры:
//  СостоянияЭлементовРегламента - См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
//  Состояние - ПеречислениеСсылка.СостоянияДокументовЭДО
//
// Возвращаемое значение:
//  Булево
//
Функция ЗаполнитьСостояниеПоВходящейИнформацииОтправителя(ПараметрыДокумента, СостоянияЭлементовРегламента, Состояние)
	
	Результат = Истина;

	Результат = Истина;
	ЭлементРегламента = Неопределено; // Неопределено,СтрокаТаблицыЗначений: См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
	
	ОбменЗавершен = Ложь;
	
	Если ЗаполнитьСостояниеПоОтклонению(ПараметрыДокумента, 
									СостоянияЭлементовРегламента, Состояние) Тогда
										
		Возврат Истина;
		
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул6, ЭлементРегламента) Тогда
			
			ОбменЗавершен = Истина;
		
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул5, ЭлементРегламента) Тогда
			
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_ДопТитул4, ЭлементРегламента) Тогда
			
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_ДопТитул3, ЭлементРегламента) Тогда
			
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_ДопТитул8, ЭлементРегламента) Тогда
			
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_ДопТитул7, ЭлементРегламента) Тогда
			
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_ДопТитул9, ЭлементРегламента) Тогда
			
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул4, ЭлементРегламента) Тогда
			
			ОбменЗавершен = Истина;
			
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул3, ЭлементРегламента) Тогда
		
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул8, ЭлементРегламента) Тогда
		
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул7, ЭлементРегламента) Тогда
		
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул9, ЭлементРегламента) Тогда
		
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул2, ЭлементРегламента) Тогда
		
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул1, ЭлементРегламента) Тогда
		
	Иначе
		
		Состояние = РегламентыЭДО.НачальноеСостояниеДокумента();
		Возврат Ложь;
		
	КонецЕсли;	
	
	Если ОбменЗавершен = Истина 
		И (ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.ПереданОператору
			Или ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.Получен
			Или ЭтоВходящийЭлементРегламента(ЭлементРегламента)) Тогда
		Состояние = Перечисления.СостоянияВерсийЭД.ОбменЗавершен;
	ИначеЕсли ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.ПодготовленКОтправке Тогда
		Состояние = Перечисления.СостоянияВерсийЭД.ОжидаетсяОтправка;
	ИначеЕсли ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.Сформирован Тогда
		Состояние = Перечисления.СостоянияВерсийЭД.ОжидаетсяОтправка;
	ИначеЕсли ЭлементРегламента.ТипЭлементаРегламента = Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул7
		И РольУчастникаЭлементаРегламента(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента) = 9 Тогда
		// При переадресации со сменой ГП для старого ГП документооборот завершается 
		Состояние = Перечисления.СостоянияВерсийЭД.ОбменЗавершен;
	Иначе
		Если СостоянияЭлементовРегламента.Колонки.Найти("Ссылка") = Неопределено Тогда
			// Новый документ
			ТекущийДоступныйТитул = Неопределено;
		Иначе	
			ТекущийДоступныйТитул = ТекущийДоступныйТитул(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента);
		КонецЕсли;
		
		Если ЗаполнитьСостояниеДляИзвещения(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента, Состояние) Тогда
			Возврат Истина;		
		ИначеЕсли ЗначениеЗаполнено(ТекущийДоступныйТитул) 
			Тогда
			Состояние = Перечисления.СостоянияВерсийЭД.НаПодписи;
		Иначе	
			Состояние = Перечисления.СостоянияВерсийЭД.ОжидаетсяПодтверждение;	
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции


Функция ЗаполнитьСостояниеДляИзвещения(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента, Состояние)
	
	Если ПараметрыДокумента.ТребуетсяИзвещение = Истина Тогда 
		Если ЭлементРегламента.ТипЭлементаРегламента = Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул1 Тогда
			ТипЭлементаРегламентаИзвещение = Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул1_ИОП;
		ИначеЕсли ЭлементРегламента.ТипЭлементаРегламента = Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул3 Тогда 
			ТипЭлементаРегламентаИзвещение = Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул3_ИОП;
		ИначеЕсли ЭлементРегламента.ТипЭлементаРегламента = Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул5 Тогда 
			ТипЭлементаРегламентаИзвещение = Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул5_ИОП;
		Иначе
			Возврат Ложь;
		КонецЕсли;
		ЭлементРегламентаИзвещение = Неопределено;
		Если ЭтоВходящийЭлементРегламента(ЭлементРегламента) Тогда		
			Если ЗаполнитьСостояниеПоИсходящемуИзвещениюОПолучении(
					ПараметрыДокумента, СостоянияЭлементовРегламента, Состояние,
					ТипЭлементаРегламентаИзвещение) Тогда
				Возврат Истина;
			КонецЕсли;			
		ИначеЕсли Не ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
				ТипЭлементаРегламентаИзвещение, ЭлементРегламентаИзвещение) Тогда					
			Состояние = Перечисления.СостоянияВерсийЭД.ОжидаетсяИзвещениеОПолучении;
			Возврат Истина;			
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции


// Возвращает признак успешности заполнения состояния по исходящему извещению о получении.
// 
// Параметры:
//  ПараметрыДокумента - См. РегламентыЭДО.НовыеПараметрыДокументаДляОпределенияСостояния
//  СостоянияЭлементовРегламента - См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
//  Состояние - ПеречислениеСсылка.СостоянияДокументовЭДО
//
// Возвращаемое значение:
//  Булево
//
Функция ЗаполнитьСостояниеПоИсходящемуИзвещениюОПолучении(ПараметрыДокумента, СостоянияЭлементовРегламента, Состояние, ТипЭлементаИзвещение)
	
	Если Не ПараметрыДокумента.ТребуетсяИзвещение Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Результат = Истина;
	ЭлементРегламента = Неопределено; // Неопределено,СтрокаТаблицыЗначений: См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
	
	Если Не ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		ТипЭлементаИзвещение, ЭлементРегламента) Тогда
			
		НужноОтправитьИОП = Ложь;
		ЭлементРегламентаТитула = Неопределено;
		Если ТипЭлементаИзвещение = Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул1_ИОП 
			И ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
				Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул1, ЭлементРегламентаТитула) Тогда
					РольУчастника = РольУчастникаЭлементаРегламента(ПараметрыДокумента, ЭлементРегламентаТитула, СостоянияЭлементовРегламента);
					НужноОтправитьИОП = РольУчастника = 1;
		ИначеЕсли ТипЭлементаИзвещение = Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул3_ИОП 
			И ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
				Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул3, ЭлементРегламентаТитула) Тогда
					РольУчастника = РольУчастникаЭлементаРегламента(ПараметрыДокумента, ЭлементРегламентаТитула, СостоянияЭлементовРегламента);
					НужноОтправитьИОП = РольУчастника = 1;
		ИначеЕсли ТипЭлементаИзвещение = Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул5_ИОП 
			И ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
				Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул5, ЭлементРегламентаТитула) Тогда
					РольУчастника = РольУчастникаЭлементаРегламента(ПараметрыДокумента, ЭлементРегламентаТитула, СостоянияЭлементовРегламента);
					НужноОтправитьИОП = РольУчастника = 3;
		КонецЕсли;
		
		Если НужноОтправитьИОП = Истина Тогда
			Состояние = Перечисления.СостоянияВерсийЭД.ТребуетсяПодтверждение;
		Иначе
			Возврат Ложь;
		КонецЕсли;
		
	ИначеЕсли ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.Сформирован Тогда 
		
		Состояние = Перечисления.СостоянияВерсийЭД.ИзвещениеНаПодписи;
		
	ИначеЕсли ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.Сформирован Тогда 
		
		Состояние = Перечисления.СостоянияВерсийЭД.ОжидаетсяОтправкаИзвещения;
		
	ИначеЕсли ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.ПодготовленКОтправке Тогда 
		
		Состояние = Перечисления.СостоянияВерсийЭД.ОжидаетсяОтправкаИзвещения;
		
	Иначе
		
		Результат = Ложь;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции


Функция РольУчастникаЭлементаРегламента(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента)
	
	// Здесь роль участника может отличаться от одноименного реквизита документ,
	// т.к. требуется для определения состояния документа, а не текущего доступного титула.
	// Отличия:
	// 4 - грузоотправитель + грузополучатель (совпадает)
	// 9 - старый ГП в титуле Т7, если имеет место замена ГП
	
	ЭлементРегламентаРазгрузка = Неопределено;
	ЕстьРазгрузка = Ложь;
	Если ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул4, ЭлементРегламентаРазгрузка) Тогда
			ЕстьРазгрузка = Истина;
	КонецЕсли;
	
	Если ТипЗнч(ЭлементРегламента) = Тип("СтрокаТаблицыЗначений") Тогда
		Если ЭлементРегламента.Владелец().Колонки.Найти("Ссылка") <> Неопределено
			И ТипЗнч(ЭлементРегламента.Ссылка) = Тип("СправочникСсылка.ЭДПрисоединенныеФайлы") Тогда
				ИдФайл = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭлементРегламента.Ссылка, "НаименованиеФайла");
				Если ТипЗнч(ИдФайл) = Тип("Строка") Тогда		
					Адресаты = ОбменСГИСЭПДКлиентСервер.АдресатыПоИдФайл(ИдФайл);			
					ИдентификаторОрганизации = Неопределено;
					Если ТипЗнч(ПараметрыДокумента) = Тип("Структура") Тогда
						ПараметрыДокумента.Свойство("ИдентификаторОрганизации", ИдентификаторОрганизации);
					ИначеЕсли ТипЗнч(ПараметрыДокумента) = Тип("ВыборкаИзРезультатаЗапроса") Тогда
						Если ПараметрыДокумента.Владелец().Колонки.Найти("ИдентификаторОрганизации") <> Неопределено Тогда
							ИдентификаторОрганизации = ПараметрыДокумента.ИдентификаторОрганизации;
						КонецЕсли;
					КонецЕсли;					
					Если ИдентификаторОрганизации = Адресаты.Грузоотправитель Тогда
						Если Адресаты.Грузоотправитель = Адресаты.Грузополучатель Тогда
							РольУчастника = 4;
						Иначе
							РольУчастника = 3;
						КонецЕсли;
					ИначеЕсли ИдентификаторОрганизации = Адресаты.Грузоперевозчик Тогда 
						РольУчастника = 1;
					ИначеЕсли ИдентификаторОрганизации = Адресаты.Грузополучатель Тогда
						Если ЭлементРегламента.ТипЭлементаРегламента = Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул7
							И ЕстьРазгрузка = Ложь
							И Адресаты.ЕстьИные = Истина Тогда
							РольУчастника = 9;
						Иначе
							РольУчастника = 2;
						КонецЕсли;	
					Иначе
						Если ЭлементРегламента.ТипЭлементаРегламента = Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул7
							И ЕстьРазгрузка = Ложь
							И Адресаты.ЕстьИные = Истина Тогда
							РольУчастника = 2;
						Иначе
							РольУчастника = 9;	
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат РольУчастника;
	
КонецФункции


Функция ТекущийДоступныйТитул(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента)
	
	РольУчастника = РольУчастникаЭлементаРегламента(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента);
	
	Результат = Неопределено;
	// Перевозчик
	Если РольУчастника = 1 Тогда	
		Если ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_Титул3") Тогда
			Результат = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_Титул4");	
		ИначеЕсли ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_ДопТитул3") Тогда
			Результат = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_ДопТитул4");
		ИначеЕсли ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_ДопТитул9") Тогда
			Результат = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_ДопТитул7");
		ИначеЕсли ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_Титул9") Тогда
			Результат = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_Титул7");
		ИначеЕсли ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_Титул1") Тогда
			Результат = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_Титул2");
		ИначеЕсли (ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_Титул2")
				Или ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_Титул4"))
			И (ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.НеСформирован
				Или ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.Сформирован
				Или ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.Сформирован) Тогда
				Результат = ЭлементРегламента.ТипЭлементаРегламента;
		КонецЕсли;			
	// Грузополучатель
	ИначеЕсли РольУчастника = 2 Тогда
		Если ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_Титул2")
			Или ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_Титул7")
			Или ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_Титул8") Тогда
			Результат = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_Титул3");
		ИначеЕсли ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_Титул3")
			И (ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.НеСформирован
				Или ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.Сформирован
				Или ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.Сформирован) Тогда	
			Результат = ЭлементРегламента.ТипЭлементаРегламента;		
		КонецЕсли;
	// Грузоотправитель
	ИначеЕсли РольУчастника = 3 Тогда	
		Если ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_Титул5") Тогда	
			Результат = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_Титул6");
		ИначеЕсли ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_ДопТитул7")
			Или ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_ДопТитул8") Тогда	
			Результат = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_ДопТитул3");
		ИначеЕсли (ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_Титул1")
				Или ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_Титул6"))
			И (ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.НеСформирован
				Или ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.Сформирован
				Или ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.Сформирован) Тогда
			Результат = ЭлементРегламента.ТипЭлементаРегламента;
		КонецЕсли;
	// Грузоотправитель + грузополучатель
	ИначеЕсли РольУчастника = 4 Тогда
		Если ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_Титул2")
			Или ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_Титул7")
			Или ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_Титул8") Тогда
			Результат = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_Титул3");
		ИначеЕсли ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_Титул3")
			И (ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.НеСформирован
				Или ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.Сформирован
				Или ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.Сформирован) Тогда	
			Результат = ЭлементРегламента.ТипЭлементаРегламента;
		ИначеЕсли ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_Титул5") Тогда	
			Результат = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_Титул6");
		ИначеЕсли ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_ДопТитул7")
			Или ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_ДопТитул8") Тогда	
			Результат = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_ДопТитул3");
		ИначеЕсли (ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_Титул1")
				Или ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_Титул6"))
			И (ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.НеСформирован
				Или ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.Сформирован
				Или ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.Сформирован) Тогда
			Результат = ЭлементРегламента.ТипЭлементаРегламента;		
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции


// Возвращает признак успешности заполнения состояния по исходящей информации отправителя.
// 
// Параметры:
//  ПараметрыДокумента - См. РегламентыЭДО.НовыеПараметрыДокументаДляОпределенияСостояния
//  СостоянияЭлементовРегламента - См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
//  Состояние - ПеречислениеСсылка.СостоянияДокументовЭДО
//
// Возвращаемое значение:
//  Булево
//
Функция ЗаполнитьСостояниеПоИсходящейИнформацииОтправителя(ПараметрыДокумента, СостоянияЭлементовРегламента, Состояние)
	
	Результат = Истина;
	ЭлементРегламента = Неопределено; // Неопределено,СтрокаТаблицыЗначений: См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
	
	ОбменЗавершен = Ложь;
	
	Если ЗаполнитьСостояниеПоОтклонению(ПараметрыДокумента, 
									СостоянияЭлементовРегламента, Состояние) Тогда
										
		Возврат Истина;
		
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул6, ЭлементРегламента) Тогда
			
			ОбменЗавершен = Истина;
		
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул5, ЭлементРегламента) Тогда
			
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_ДопТитул4, ЭлементРегламента) Тогда
			
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_ДопТитул3, ЭлементРегламента) Тогда
			
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_ДопТитул8, ЭлементРегламента) Тогда
			
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_ДопТитул7, ЭлементРегламента) Тогда
			
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_ДопТитул9, ЭлементРегламента) Тогда
			
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул4, ЭлементРегламента) Тогда
			
			ОбменЗавершен = Истина;
			
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул3, ЭлементРегламента) Тогда
		
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул9, ЭлементРегламента) Тогда
		
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул8, ЭлементРегламента) Тогда
		
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул7, ЭлементРегламента) Тогда
		
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул2, ЭлементРегламента) Тогда
		
	ИначеЕсли ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул1, ЭлементРегламента) Тогда
		
	Иначе
		
		Состояние = РегламентыЭДО.НачальноеСостояниеДокумента();
		Возврат Ложь;
		
	КонецЕсли;
	
	Если ОбменЗавершен = Истина 
		И (ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.ПереданОператору  
			Или ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.Получен
			Или ЭтоВходящийЭлементРегламента(ЭлементРегламента)) Тогда
		Состояние = Перечисления.СостоянияВерсийЭД.ОбменЗавершен;
	ИначеЕсли ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.ПодготовленКОтправке Тогда
		Состояние = Перечисления.СостоянияВерсийЭД.ОжидаетсяОтправка;
	ИначеЕсли ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.Сформирован Тогда
		Состояние = Перечисления.СостоянияВерсийЭД.ОжидаетсяОтправка;
	ИначеЕсли ЭлементРегламента.ТипЭлементаРегламента = Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул7
		И РольУчастникаЭлементаРегламента(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента) = 9 Тогда
		// При переадресации со сменой ГП для старого ГП документооборот завершается 
		Состояние = Перечисления.СостоянияВерсийЭД.ОбменЗавершен;
	Иначе
		Если СостоянияЭлементовРегламента.Колонки.Найти("Ссылка") = Неопределено Тогда
			// Новый документ
			ТекущийДоступныйТитул = Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул1;
		Иначе	
			ТекущийДоступныйТитул = ТекущийДоступныйТитул(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента);
		КонецЕсли;
		
		Если ЗаполнитьСостояниеДляИзвещения(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента, Состояние) Тогда
			Возврат Истина;		
		ИначеЕсли ЗначениеЗаполнено(ТекущийДоступныйТитул) 
			Тогда
			Состояние = Перечисления.СостоянияВерсийЭД.НаПодписи;
		Иначе
			Состояние = Перечисления.СостоянияВерсийЭД.ОжидаетсяПодтверждение;	
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ЭтоВходящийЭлементРегламента(ЭлементРегламента)
	
	Если ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.Получен Тогда  
		Результат = Истина;
	ИначеЕсли ЭлементРегламента.Владелец().Колонки.Найти("Ссылка") = Неопределено Тогда
		Результат = Ложь;
	Иначе
		СообщениеСсылка = ЭлементРегламента.Ссылка;
		Если ТипЗнч(СообщениеСсылка) = Тип("СправочникСсылка.ЭДПрисоединенныеФайлы") Тогда  
			Результат = СообщениеСсылка.НаправлениеЭД = Перечисления.НаправленияЭД.Входящий;
		Иначе
			Результат = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает признак успешности заполнения состояния по исходящему отклонению.
// 
// Параметры:
//  ПараметрыДокумента - См. РегламентыЭДО.НовыеПараметрыДокументаДляОпределенияСостояния
//  СостоянияЭлементовРегламента - См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
//  Состояние - ПеречислениеСсылка.СостоянияДокументовЭДО
//
// Возвращаемое значение:
//  Булево
//
Функция ЗаполнитьСостояниеПоОтклонению(ПараметрыДокумента, СостоянияЭлементовРегламента, Состояние)
	
	Результат = Истина;
	ЭлементРегламента = Неопределено; // Неопределено,СтрокаТаблицыЗначений: См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
	
	Если ПараметрыДокумента.Исправлен = Истина Тогда
		Результат = Ложь;	
	КонецЕсли;
	
	Если Не ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
			Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_ОткГО, ЭлементРегламента) 
		И Не ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
			Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_ОткТ3, ЭлементРегламента)
		И Не ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
			Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_ОткП, ЭлементРегламента)
		И Не ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
			Перечисления.ТипыЭлементовВерсииЭД.УОУ, ЭлементРегламента) Тогда
		
		Результат = Ложь;
		
	ИначеЕсли ЭтоВходящийЭлементРегламента(ЭлементРегламента) Тогда
		
		Если Не ПараметрыДокумента.Исправлен Тогда	
			Если ЭлементРегламента.ТипЭлементаРегламента = Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_ОткП Тогда
				Состояние = Перечисления.СостоянияВерсийЭД.ОбменЗавершенСИсправлением;
			Иначе
				ЭлементРегламентаТитула = Неопределено;
				ЭлементРегламентаКорректировки = Неопределено;
				Если ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
					Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул1, ЭлементРегламентаТитула) Тогда
					Если ЭлементРегламента.ТипЭлементаРегламента = Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_ОткТ3 Тогда
						Если ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
							Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул3, ЭлементРегламентаКорректировки)
							И (ЭлементРегламентаКорректировки.Состояние = Перечисления.СтатусыЭД.ПереданОператору
								Или ЭлементРегламентаКорректировки.Состояние = Перечисления.СтатусыЭД.Получен)
							И ЭлементРегламента.Дата <> Неопределено
							И (ЭлементРегламентаКорректировки.Дата = Неопределено
								Или ЭлементРегламентаКорректировки.Дата > ЭлементРегламента.Дата) Тогда  
								Результат = Ложь;
						Иначе                     
							РольУчастника = РольУчастникаЭлементаРегламента(ПараметрыДокумента, ЭлементРегламентаТитула, СостоянияЭлементовРегламента);
							Если РольУчастника = 2 Или РольУчастника = 4 Тогда
								Состояние = Перечисления.СостоянияВерсийЭД.ТребуетсяУточнитьДокумент;
							Иначе
								Состояние = Перечисления.СостоянияВерсийЭД.ОжидаетсяКорректировка;	
							КонецЕсли;
						КонецЕсли;
					ИначеЕсли ЭлементРегламента.ТипЭлементаРегламента = Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_ОткГО Тогда
						Если ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
							Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул5, ЭлементРегламентаКорректировки)
							И (ЭлементРегламентаКорректировки.Состояние = Перечисления.СтатусыЭД.ПереданОператору
								Или ЭлементРегламентаКорректировки.Состояние = Перечисления.СтатусыЭД.Получен)
							И ЭлементРегламента.Дата <> Неопределено
							И (ЭлементРегламентаКорректировки.Дата = Неопределено
								Или ЭлементРегламентаКорректировки.Дата > ЭлементРегламента.Дата) Тогда  
								Результат = Ложь;	
						Иначе
							РольУчастника = РольУчастникаЭлементаРегламента(ПараметрыДокумента, ЭлементРегламентаТитула, СостоянияЭлементовРегламента);
							Если РольУчастника = 1 Тогда
								Состояние = Перечисления.СостоянияВерсийЭД.ТребуетсяУточнитьДокумент;  
							Иначе
								Состояние = Перечисления.СостоянияВерсийЭД.ОжидаетсяКорректировка;	
							КонецЕсли;  
						КонецЕсли;
					Иначе
						Состояние = Перечисления.СостоянияВерсийЭД.ОжидаетсяКорректировка;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		Иначе
			Результат = Ложь;	
		КонецЕсли;
		
	ИначеЕсли ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.Сформирован Тогда 
		
		Состояние = Перечисления.СостоянияВерсийЭД.НаПодписи;
		
	ИначеЕсли ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.Подписан Тогда 
		
		Состояние = Перечисления.СостоянияВерсийЭД.ОжидаетсяОтправка; 
		
	ИначеЕсли ЭлементРегламента.Состояние = Перечисления.СтатусыЭД.ПодготовленКОтправке Тогда 
		
		Состояние = Перечисления.СостоянияВерсийЭД.ОжидаетсяОтправка; 
		
	ИначеЕсли Не ПараметрыДокумента.Исправлен Тогда
		
		Если ЭлементРегламента.ТипЭлементаРегламента = Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_ОткП Тогда
			Состояние = Перечисления.СостоянияВерсийЭД.ОбменЗавершенСИсправлением; 
		Иначе			
			ЭлементРегламентаТитула = Неопределено;
			ЭлементРегламентаКорректировки = Неопределено;
			Если ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
				Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул1, ЭлементРегламентаТитула) Тогда
				Если ЭлементРегламента.ТипЭлементаРегламента = Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_ОткТ3
					И ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
						Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул3, ЭлементРегламентаКорректировки)
					И ЭлементРегламентаКорректировки.Состояние = Перечисления.СтатусыЭД.Получен
					И ЭлементРегламента.Дата <> Неопределено
					И (ЭлементРегламентаКорректировки.Дата = Неопределено
						Или ЭлементРегламентаКорректировки.Дата > ЭлементРегламента.Дата) Тогда  
						Результат = Ложь;	
				ИначеЕсли ЭлементРегламента.ТипЭлементаРегламента = Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_ОткГО
					И ОбменСГИСЭПД.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
						Перечисления.ТипыЭлементовВерсииЭД.ЭТрН_Титул5, ЭлементРегламентаКорректировки)
					И ЭлементРегламентаКорректировки.Состояние = Перечисления.СтатусыЭД.Получен
					И ЭлементРегламента.Дата <> Неопределено
					И (ЭлементРегламентаКорректировки.Дата = Неопределено
						Или ЭлементРегламентаКорректировки.Дата > ЭлементРегламента.Дата) Тогда  
						Результат = Ложь;		
				Иначе
					Состояние = Перечисления.СостоянияВерсийЭД.ОжидаетсяКорректировка;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти