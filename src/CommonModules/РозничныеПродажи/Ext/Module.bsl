#Область ПрограммныйИнтерфейс

#Область ФискальныеОперации

Функция СтруктураДанныеФискальнойОперации() Экспорт
	
	ДанныеФискальнойОперации = Новый Структура;
	ДанныеФискальнойОперации.Вставить("ОтправлятьEmailЧерезОФД",          Константы.ОтправлятьЭлектронныеЧекиПоEmailЧерезОФД.Получить());
	ДанныеФискальнойОперации.Вставить("ОтправлятьSMSЧерезОФД",            Ложь);
	ДанныеФискальнойОперации.Вставить("НеПечататьФискальныйЧек",          Константы.НеПечататьФискальныйЧекПриОтправкеЭлектронного.Получить());
	ДанныеФискальнойОперации.Вставить("ОтправительEmail");
	ДанныеФискальнойОперации.Вставить("ВариантОтправкиЭлектронногоЧека");
	ДанныеФискальнойОперации.Вставить("КонтактныеДанныеЭлектронногоЧека");
	
	ОчиститьДанныеФискальнойОперации(ДанныеФискальнойОперации);
	
	Возврат ДанныеФискальнойОперации;
	
КонецФункции

Процедура ОчиститьДанныеФискальнойОперации(ДанныеФискальнойОперации) Экспорт
	
	ДанныеФискальнойОперации.ВариантОтправкиЭлектронногоЧека = Перечисления.ВариантыОтправкиЭлектронногоЧекаПокупателю.ПустаяСсылка();
	ДанныеФискальнойОперации.КонтактныеДанныеЭлектронногоЧека = "";
	
КонецПроцедуры

Функция СистемаНалогообложенияФискальнойОперации(Организация, Дата = Неопределено) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Т.СистемаНалогообложения КАК СистемаНалогообложения,
	|	Т.ОбъектНалогообложенияУСН КАК ОбъектНалогообложенияУСН
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаНалоговыйУчет.СрезПоследних(&ТекущаяДата, Организация = &Организация) КАК Т
	|");
	
	Если Дата = Неопределено Тогда
		Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	Иначе
		Запрос.УстановитьПараметр("ТекущаяДата", Дата);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	ДанныеСистемыНалогообложения = Запрос.Выполнить().Выбрать();
	ДанныеСистемыНалогообложения.Следующий();
	
	Если ДанныеСистемыНалогообложения.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Общая Тогда
		
		СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.ОСН;
		
	ИначеЕсли ДанныеСистемыНалогообложения.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Упрощенная Тогда
		
		Если ДанныеСистемыНалогообложения.ОбъектНалогообложенияУСН = Перечисления.ОбъектыНалогообложенияПоУСН.Доходы Тогда
			
			СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.УСНДоход;
			
		ИначеЕсли ДанныеСистемыНалогообложения.ОбъектНалогообложенияУСН = Перечисления.ОбъектыНалогообложенияПоУСН.ДоходыМинусРасходы Тогда
			
			СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.УСНДоходРасход;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СистемаНалогообложения;
	
КонецФункции

Функция ДопустимыТоварыБезМарокВДокументе(ДокументСсылка) Экспорт
	
	ДопустимыТоварыБезМарокДляДокумента = Ложь;
	
	Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя")
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ЧекККМ") Тогда
		
		ДопустимыТоварыБезМарокДляДокумента = Истина;
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ЧекККМ") Тогда
		
		Если ДокументСсылка.ВидОперации = Перечисления.ВидыОперацийЧекККМ.Возврат Тогда  
			
			ДопустимыТоварыБезМарокДляДокумента = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ДопустимыТоварыБезМарокДляДокумента;
	
КонецФункции

// Разбивает строки товаров по единичиным кодам маркировки 
//
// Параметры:
//   Товары        - ТаблицаЗначений - Таблица товаров в позиции чека:
//   	*НомерСтроки - Число - Номер строки
//   ДанныеДляИСМП - ТаблицаЗначений - Таблица товаров с маркировкой продукции
//
Процедура ТаблицаТоваровСРаспределениемКодовМаркировкиИСМП(Товары, ДанныеДляИСМП) Экспорт
	
	ТаблицаТоваровСРаспределением = Товары.СкопироватьКолонки();
	Если ТаблицаТоваровСРаспределением.Колонки.Найти("Штрихкод") = Неопределено Тогда
		ТаблицаТоваровСРаспределением.Колонки.Добавить("Штрихкод");
	КонецЕсли;
	Если ТаблицаТоваровСРаспределением.Колонки.Найти("РезультатРаспределения") = Неопределено Тогда
		ТаблицаТоваровСРаспределением.Колонки.Добавить("РезультатРаспределения");
	КонецЕсли;
	Если ТаблицаТоваровСРаспределением.Колонки.Найти("ДополнениеКНаименованиюТовара") = Неопределено Тогда
		ТаблицаТоваровСРаспределением.Колонки.Добавить("ДополнениеКНаименованиюТовара");
	КонецЕсли;
		
	ВидыПродукцииИСМП = ИнтеграцияИСКлиентСервер.ВидыПродукцииИСМП(Истина);
	Для Каждого СтрокаТЧ Из Товары Цикл
		
		МассивСтрокДляДобавленияВТовары = Новый Массив;
		МассивСтрокДляДобавленияВТовары.Добавить(СтрокаТЧ);
		
		// Данные по маркируемой продукции
		ЭтоМаркируемаяПродукция = Ложь;
		Если НЕ ДанныеДляИСМП = Неопределено
			И ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(СтрокаТЧ, "ВидПродукцииИС")
			И ВидыПродукцииИСМП.Найти(СтрокаТЧ.ВидПродукцииИС) <> Неопределено Тогда
			
			ЭтоМаркируемаяПродукция = Истина;
			// заменяем строки для добавления в чек детализированными данными
			МассивСтрокДляДобавленияВТовары = ДанныеДляИСМП.НайтиСтроки(Новый Структура("НомерСтроки", СтрокаТЧ.НомерСтроки));
		КонецЕсли;
		
		Для каждого СтрокаДляДобавленияВТовары из МассивСтрокДляДобавленияВТовары Цикл
			Если НЕ ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(СтрокаДляДобавленияВТовары, "РезультатРаспределения")
				ИЛИ СтрокаДляДобавленияВТовары.РезультатРаспределения = Неопределено Тогда
				Продолжить;
			КонецЕсли;

			СтрокаТоваровСРаспределением = ТаблицаТоваровСРаспределением.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТоваровСРаспределением, СтрокаТЧ);
			
			Если СтрокаДляДобавленияВТовары.КоличествоУпаковок * СтрокаДляДобавленияВТовары.КоэффициентПересчетаУпаковки > СтрокаТЧ.Количество
				И НЕ СтрокаДляДобавленияВТовары.КоличествоУпаковок = 0 Тогда
				
				СтрокаТоваровСРаспределением.КоличествоУпаковок = СтрокаТЧ.КоличествоУпаковок;
				СтрокаТоваровСРаспределением.СуммаСНДС          = СтрокаТЧ.СуммаСНДС;
				СтрокаТоваровСРаспределением.СуммаНДС           = СтрокаТЧ.СуммаНДС;
				СтрокаТоваровСРаспределением.СуммаСкидки        = СтрокаТЧ.СуммаСкидки;
				СтрокаТоваровСРаспределением.Цена               = (СтрокаТЧ.СуммаСНДС - СтрокаТЧ.СуммаСкидки)
																	 / СтрокаТЧ.КоличествоУпаковок;
			Иначе
				
				Если СтрокаТоваровСРаспределением.КоличествоУпаковок = 0 Тогда
					СтрокаТоваровСРаспределением.КоличествоУпаковок = 1;
					СтрокаТоваровСРаспределением.Количество = 1;
				Иначе
					СтрокаТоваровСРаспределением.КоличествоУпаковок = СтрокаДляДобавленияВТовары.КоличествоУпаковок;
					СтрокаТоваровСРаспределением.Количество = СтрокаДляДобавленияВТовары.Количество;
				КонецЕсли;
				
				СтрокаТоваровСРаспределением.СуммаСНДС = СтрокаДляДобавленияВТовары.СуммаСНДС;
				СтрокаТоваровСРаспределением.СуммаНДС = СтрокаДляДобавленияВТовары.СуммаНДС;
				СтрокаТоваровСРаспределением.СуммаСкидки = СтрокаДляДобавленияВТовары.СуммаСкидки;
				СтрокаТоваровСРаспределением.Цена = (СтрокаТоваровСРаспределением.СуммаСНДС - СтрокаДляДобавленияВТовары.СуммаСкидки)
														 / СтрокаТоваровСРаспределением.КоличествоУпаковок;
			КонецЕсли;
			
			КоэффициентПересчетаУпаковки = 1;
			Если ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(СтрокаДляДобавленияВТовары, "КоэффициентПересчетаУпаковки") Тогда
				Если ЗначениеЗаполнено(СтрокаДляДобавленияВТовары.КоэффициентПересчетаУпаковки) Тогда
					КоэффициентПересчетаУпаковки = СтрокаДляДобавленияВТовары.КоэффициентПересчетаУпаковки;
				КонецЕсли;
			КонецЕсли;
			
			Если СтрокаТЧ.КоличествоУпаковок <> СтрокаТЧ.Количество
				И ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(СтрокаТЧ, "Упаковка")
				И ЗначениеЗаполнено(СтрокаТЧ.Упаковка) Тогда                
				
				РеквизитыЕдиницы = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаТЧ.Упаковка, "Коэффициент");
				РеквизитыУпаковки = Новый Структура("Числитель, Знаменатель", РеквизитыЕдиницы.Коэффициент, 1);
				Если ТипЗнч(РеквизитыУпаковки.Знаменатель) = Тип("Число") И РеквизитыУпаковки.Знаменатель > 0
					И ТипЗнч(РеквизитыУпаковки.Числитель) = Тип("Число") И РеквизитыУпаковки.Числитель > 0 Тогда
					
					КоэффициентПересчетаУпаковки = КоэффициентПересчетаУпаковки * РеквизитыУпаковки.Знаменатель / РеквизитыУпаковки.Числитель;
				КонецЕсли;
			КонецЕсли;
			
			Если СтрокаДляДобавленияВТовары.РезультатРаспределения.ЧастичноеВыбытие Тогда
				СтрокаТЧ.КоличествоУпаковок = СтрокаТЧ.КоличествоУпаковок
					- СтрокаДляДобавленияВТовары.РезультатРаспределения.ЧастичноеВыбытиеКоличество;
			Иначе
				Если СтрокаДляДобавленияВТовары.РезультатРаспределения.ЕмкостьПотребительскойУпаковки <> 0
					И СтрокаДляДобавленияВТовары.РезультатРаспределения.ЕмкостьПотребительскойУпаковки = СтрокаДляДобавленияВТовары.РезультатРаспределения.КоличествоУпаковок Тогда
					КоэффициентПересчетаУпаковки = СтрокаДляДобавленияВТовары.РезультатРаспределения.ЕмкостьПотребительскойУпаковки;
				КонецЕсли;
					
				СтрокаТЧ.КоличествоУпаковок = СтрокаТЧ.КоличествоУпаковок
					- СтрокаТоваровСРаспределением.КоличествоУпаковок * КоэффициентПересчетаУпаковки;
			КонецЕсли;
			СтрокаТЧ.Количество         = СтрокаТЧ.Количество - СтрокаТоваровСРаспределением.Количество;
			СтрокаТЧ.СуммаСНДС          = СтрокаТЧ.СуммаСНДС - СтрокаТоваровСРаспределением.СуммаСНДС;
			СтрокаТЧ.СуммаНДС           = СтрокаТЧ.СуммаНДС - СтрокаТоваровСРаспределением.СуммаНДС;
			СтрокаТЧ.СуммаСкидки        = СтрокаТЧ.СуммаСкидки - СтрокаТоваровСРаспределением.СуммаСкидки;
			
			СтрокаДляДобавленияВТовары.Количество         = СтрокаДляДобавленияВТовары.Количество - СтрокаТоваровСРаспределением.Количество;
			СтрокаДляДобавленияВТовары.КоличествоЕдиниц   = СтрокаДляДобавленияВТовары.КоличествоЕдиниц - СтрокаТоваровСРаспределением.Количество;
			СтрокаДляДобавленияВТовары.КоличествоУпаковок = СтрокаДляДобавленияВТовары.КоличествоУпаковок - СтрокаТоваровСРаспределением.КоличествоУпаковок;
			СтрокаДляДобавленияВТовары.СуммаСНДС          = СтрокаДляДобавленияВТовары.СуммаСНДС - СтрокаТоваровСРаспределением.СуммаСНДС;
			СтрокаДляДобавленияВТовары.СуммаНДС           = СтрокаДляДобавленияВТовары.СуммаНДС - СтрокаТоваровСРаспределением.СуммаНДС;
			СтрокаДляДобавленияВТовары.СуммаСкидки        = СтрокаДляДобавленияВТовары.СуммаСкидки - СтрокаТоваровСРаспределением.СуммаСкидки;
			
			Если ЭтоМаркируемаяПродукция
				И ЗначениеЗаполнено(СтрокаДляДобавленияВТовары.Упаковка)
				И СтрокаТоваровСРаспределением.Упаковка <> СтрокаДляДобавленияВТовары.Упаковка Тогда
				СтрокаТоваровСРаспределением.Упаковка = СтрокаДляДобавленияВТовары.Упаковка;
				СтрокаТоваровСРаспределением.УпаковкаНаименование = Строка(СтрокаДляДобавленияВТовары.Упаковка);
			КонецЕсли;
			
			Если ЭтоМаркируемаяПродукция И ЗначениеЗаполнено(СтрокаДляДобавленияВТовары.Штрихкод) Тогда
				СтрокаТоваровСРаспределением.Штрихкод               = СтрокаДляДобавленияВТовары.Штрихкод;
				СтрокаТоваровСРаспределением.РезультатРаспределения = СтрокаДляДобавленияВТовары.РезультатРаспределения;
				СтрокаТоваровСРаспределением.ДополнениеКНаименованиюТовара = СтрокаДляДобавленияВТовары.ДополнениеКНаименованиюТовара;
			ИначеЕсли ЭтоМаркируемаяПродукция И НЕ ЗначениеЗаполнено(СтрокаДляДобавленияВТовары.Штрихкод) Тогда
				СтрокаТоваровСРаспределением.Штрихкод = Неопределено;
			КонецЕсли;
			
			Если СтрокаДляДобавленияВТовары.КоличествоУпаковок > 0 Тогда
				ДанныеДляИСМП_Копия = ДанныеДляИСМП.СкопироватьКолонки();
				НоваяСтрока = ДанныеДляИСМП_Копия.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДляДобавленияВТовары);
				
				ДанныеДляИСМП = ДанныеДляИСМП_Копия;
			КонецЕсли;
		КонецЦикла;
		
		Если СтрокаТЧ.КоличествоУпаковок > 0 Тогда
			СтрокаТоваровСРаспределением = ТаблицаТоваровСРаспределением.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТоваровСРаспределением, СтрокаТЧ);
		КонецЕсли;
		
	КонецЦикла;
	
	Товары = ТаблицаТоваровСРаспределением;
	
КонецПроцедуры

// Заполняет дополнение к наименованию товара по маркированному товару
// 
// Параметры:
//  СтрокаПозицииЧека - СтрокаТаблицыЗначений - Строка позиции чека
//  СтрокаТЧ - СтрокаТаблицыЗначений -Строка ТЧ
//  Упаковка - СправочникСсылка.УпаковкиЕдиницыИзмерения - Упаковка
//  Валюта - СправочникСсылка.Валюты - Валюта
//  ВерсияФФД - Строка - Версия ФФД
//
Процедура ЗаполнитьДополнениеКНаименованиюТовара(СтрокаПозицииЧека, СтрокаТЧ, Упаковка, Валюта, ВерсияФФД) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Упаковка) Тогда
		Упаковка = "шт.";
	КонецЕсли;
	
	Если ВерсияФФД = "1.2" Тогда
		СтрокаПозицииЧека.КодЕдиницыИзмерения = РозничныеПродажи.КодЕдиницыИзмеренияПараметраЧека(Упаковка);
	Иначе
		СтрокаПозицииЧека.ЕдиницаИзмерения = Упаковка;
	КонецЕсли;
	
	СтрокаПозицииЧека.Количество     = 1;
	СтрокаПозицииЧека.Цена           = СтрокаПозицииЧека.Сумма;
	СтрокаПозицииЧека.ЦенаСоСкидками = СтрокаПозицииЧека.Сумма;
	
	
	УпаковкаНаименование = "ед";
	Если ЗначениеЗаполнено(Упаковка) Тогда
		УпаковкаНаименование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Упаковка, "Наименование");
	КонецЕсли;
	УпаковкаНаименование = УпаковкаНаименование + ".";
	
	КоличествоЕдиниц = СтрокаТЧ.ДополнениеКНаименованиюТовара.Количество;
	Если ТипЗнч(СтрокаТЧ.ДополнениеКНаименованиюТовара.КоличествоУпаковок) = Тип("Число")
		И СтрокаТЧ.ДополнениеКНаименованиюТовара.КоличествоУпаковок > 0 Тогда
		КоличествоЕдиниц = СтрокаТЧ.ДополнениеКНаименованиюТовара.КоличествоУпаковок;
	КонецЕсли;
	
	
	ЦенаЕдиницы = СтрокаТЧ.ДополнениеКНаименованиюТовара.Цена;
	Если СтрокаТЧ.РезультатРаспределения.ЧастичноеВыбытие Тогда
		ЧастичноеВыбытиеКоличество = СтрокаТЧ.РезультатРаспределения.ЧастичноеВыбытиеКоличество;
		ЦенаЕдиницы = Окр(СтрокаПозицииЧека.Цена / ЧастичноеВыбытиеКоличество, 2);
	Иначе
		ЦенаЕдиницы = Окр(СтрокаПозицииЧека.Цена / КоличествоЕдиниц, 2);
	КонецЕсли;
	
	СтрокаПозицииЧека.Наименование = СтрокаПозицииЧека.Наименование
			+ " (" + КоличествоЕдиниц + " " + УпаковкаНаименование
			+ ", цена: " + ЦенаЕдиницы + " " + Строка(Валюта) + "/" + УпаковкаНаименование + ")";
	
КонецПроцедуры

// Возвращает версию ФФД ККТ
// 
// Параметры:
//  ИдентификаторУстройства - Строка - Идентификатор устройства
// 
// Возвращаемое значение:
//  Строка -- Версия ФФДККТ
Функция ВерсияФФДККТ(ИдентификаторУстройства) Экспорт
	
	ВерсияФФДФН = "";
	
	Возврат ВерсияФФДФН;
	
КонецФункции

// Возвращает наименование торгового объекта. Если параметр ТорговыйОбъект заполнен, возвращается наименование торгового
// объекта, иначе возвращается сокращенное наименование организации.
// 
// Параметры:
//  Организация - СправочникСсылка.Организации - Организация
//  ТорговыйОбъект - СправочникСсылка.Склады, Неопределено - Торговый объект
// 
// Возвращаемое значение:
//  Произвольный - Торговый объект ККТ
Функция ТорговыйОбъектККТ(Организация, ТорговыйОбъект = Неопределено) Экспорт
	
	ТорговыйОбъектНаименование = "";
	Если ЗначениеЗаполнено(ТорговыйОбъект) Тогда
		ТорговыйОбъектНаименование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТорговыйОбъект, "Наименование");
	Иначе
		СведенияОЮрФизЛице = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Организация, ТекущаяДатаСеанса());
		ТорговыйОбъектНаименование = СведенияОЮрФизЛице.НаименованиеСокращенное;
	КонецЕсли;
	
	Возврат ТорговыйОбъектНаименование;
	
КонецФункции

// Возвращает место расчетов ККТ.
// 
// Параметры:
//  ОборудованиеККТ - СправочникСсылка.ПодключаемоеОборудование - Оборудование ККТ
// 
// Возвращаемое значение:
//  Строка - Место расчетов ККТ
Функция МестоРасчетовККТ(ОборудованиеККТ) Экспорт
	
	ТорговыйОбъектМестоРасчетов = "";
	
	ПараметрыРегистрацииККТ = МенеджерОборудованияВызовСервера.ПолучитьПараметрыРегистрацииУстройства(ОборудованиеККТ);
	Если ТипЗнч(ПараметрыРегистрацииККТ) = Тип("Структура") Тогда
		Если ПараметрыРегистрацииККТ.Свойство("МестоПроведенияРасчетов") Тогда
			ТорговыйОбъектМестоРасчетов = ПараметрыРегистрацииККТ.МестоПроведенияРасчетов;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТорговыйОбъектМестоРасчетов;
	
КонецФункции

// Возвращает адрес расчетов ККТ. Если параметры регистрации кассы содержат адрес проведения расчетов, то 
// возвращается адрес проведения расчетов, иначе возвращается фактический адрес организации. 
// 
// Параметры:
//  ОборудованиеККТ - СправочникСсылка.ПодключаемоеОборудование - Оборудование ККТ
//  Организация - СправочникСсылка.Организации - Организация
// 
// Возвращаемое значение:
//  Строка - Адрес расчетов ККТ
Функция АдресРасчетовККТ(ОборудованиеККТ, Организация) Экспорт
	
	ТорговыйОбъектАдресРасчетов = "";
	
	ПараметрыРегистрацииККТ = МенеджерОборудованияВызовСервера.ПолучитьПараметрыРегистрацииУстройства(ОборудованиеККТ);
	Если ТипЗнч(ПараметрыРегистрацииККТ) = Тип("Структура") Тогда
		Если ПараметрыРегистрацииККТ.Свойство("АдресПроведенияРасчетов") Тогда
			ТорговыйОбъектАдресРасчетов = ПараметрыРегистрацииККТ.АдресПроведенияРасчетов;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТорговыйОбъектАдресРасчетов) Тогда
		СведенияОЮрФизЛице = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Организация, ТекущаяДатаСеанса());
		ТорговыйОбъектАдресРасчетов = СведенияОЮрФизЛице.ЮридическийАдрес;
	КонецЕсли;
	
	Возврат ТорговыйОбъектАдресРасчетов;
	
КонецФункции

// Возвращает код единицы измерения
// 
// Параметры:
//  ЕдиницаИзмерения - СправочникСсылка.УпаковкиЕдиницыИзмерения, Строка, Неопределено - Единица измерения
// 
// Возвращаемое значение:
//  Строка - Код единицы измерения параметра чека
// Возвращает код единицы измерения
// 
// Параметры:
//  ЕдиницаИзмерения - СправочникСсылка.КлассификаторЕдиницИзмерения, Строка, Неопределено - Единица измерения
// 
// Возвращаемое значение:
//  Строка - Код единицы измерения параметра чека
Функция КодЕдиницыИзмеренияПараметраЧека(ЕдиницаИзмерения) Экспорт
	
	КодЕдиницыИзмерения = "";
	Если ТипЗнч(ЕдиницаИзмерения) = Тип("Строка")
		И ЕдиницаИзмерения = "шт." Тогда
		
		КодЕдиницыИзмерения = "796";
		
	ИначеЕсли ТипЗнч(ЕдиницаИзмерения) = Тип("СправочникСсылка.КлассификаторЕдиницИзмерения")
		И НЕ ЕдиницаИзмерения.Пустая() Тогда
		
		ЕдиницаИзмеренияКлассификатора = ЕдиницаИзмерения;
		КодЕдиницыИзмерения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЕдиницаИзмеренияКлассификатора, "Код");
		
	ИначеЕсли ТипЗнч(ЕдиницаИзмерения) = Тип("СправочникСсылка.ЕдиницыИзмерения")
		И НЕ ЕдиницаИзмерения.Пустая() Тогда
		
		ЕдиницаИзмеренияКлассификатора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЕдиницаИзмерения, "ЕдиницаПоКлассификатору");
		КодЕдиницыИзмерения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЕдиницаИзмеренияКлассификатора, "Код");
		
	КонецЕсли;
	
	Возврат СокрЛП(КодЕдиницыИзмерения);
	
КонецФункции

// Возвращает гражданство покупателя по паспорту.
// 
// Параметры:
//  ФизическоеЛицо - СправочникСсылка.ФизическиеЛица - Физическое лицо
//  НаДату - Дата, Неопределено - На дату
// 
// Возвращаемое значение:
//  СправочникСсылка.СтраныМира - гражданство покупателя
Функция ПокупательГражданство(ФизическоеЛицо, НаДату = Неопределено) Экспорт
	
	ПокупательГражданство = Справочники.КлассификаторСтранМира.ПустаяСсылка();
	НаДату = ?(НаДату = Неопределено, ТекущаяДатаСеанса(), НаДату);
	
	Запрос = Новый Запрос;
	Запрос.Текст ="ВЫБРАТЬ ПЕРВЫЕ 1
	              |	ГражданствоФизЛицСрезПоследних.Страна КАК Страна
	              |ИЗ
	              |	РегистрСведений.ГражданствоФизЛиц.СрезПоследних(&ДатаСреза, ФизЛицо = &ФизЛицо) КАК ГражданствоФизЛицСрезПоследних
	              |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
	              |		ПО ГражданствоФизЛицСрезПоследних.ФизЛицо = ФизическиеЛица.Ссылка";
	Запрос.УстановитьПараметр("ДатаСреза", КонецДня(НаДату));
	Запрос.УстановитьПараметр("ФизЛицо", ФизическоеЛицо);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ПокупательГражданство = Выборка.Страна;
	КонецЕсли;
	
	Возврат ПокупательГражданство;
	
КонецФункции

// Возвращает дату рождения физического лица
// 
// Параметры:
//  ФизическоеЛицо - СправочникСсылка.ФизическиеЛица - Физическое лицо
// 
// Возвращаемое значение:
//  Строка - дата рождения покупателя
Функция ПокупательДатаРождения(ФизическоеЛицо) Экспорт
	
	Возврат СокрЛП(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ФизическоеЛицо, "ДатаРождения"));
	
КонецФункции

// Возвращает ИНН физического лица
// 
// Параметры:
//  ФизическоеЛицо - СправочникСсылка.ФизическиеЛица - Физическое лицо
// 
// Возвращаемое значение:
//  Строка - ИНН покупателя
Функция ПокупательИНН(ФизическоеЛицо) Экспорт
	
	Возврат СокрЛП(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ФизическоеЛицо, "ИНН"));
	
КонецФункции

// Возвращает паспортные данные покупателя одной строкой.
// 
// Параметры:
//  ФизическоеЛицо - СправочникСсылка.ФизическиеЛица - Физическое лицо
//  НаДату - Дата, Неопределено - На дату
// 
// Возвращаемое значение:
//  Строка - паспортные данные покупателя
Функция ПокупательПаспортныеДанные(ФизическоеЛицо, НаДату = Неопределено) Экспорт
	
	ПокупательПерсональныеДанные = "";
	НаДату = ?(НаДату = Неопределено, ТекущаяДатаСеанса(), НаДату);
	
	КодыМВД = Новый Массив;
	КодыМВД.Добавить("10");
	КодыМВД.Добавить("21");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВидыДокументовФизическихЛиц.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВидыДокументовФизическихЛиц
	|ИЗ
	|	Справочник.ДокументыУдостоверяющиеЛичность КАК ВидыДокументовФизическихЛиц
	|ГДЕ
	|	ВидыДокументовФизическихЛиц.КодИМНС В(&КодыМВД)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДокументыФизическихЛицСрезПоследних.Период КАК Период,
	|	ДокументыФизическихЛицСрезПоследних.Физлицо КАК Физлицо,
	|	ДокументыФизическихЛицСрезПоследних.ДокументВид КАК ВидДокумента,
	|	ДокументыФизическихЛицСрезПоследних.ДокументСерия КАК Серия,
	|	ДокументыФизическихЛицСрезПоследних.ДокументНомер КАК Номер,
	|	ДокументыФизическихЛицСрезПоследних.ДокументДатаВыдачи КАК ДатаВыдачи,
	|	ДокументыФизическихЛицСрезПоследних.ДокументКемВыдан КАК КемВыдан,
	|	ДокументыФизическихЛицСрезПоследних.ДокументКодПодразделения КАК КодПодразделения
	|ИЗ
	|	РегистрСведений.ПаспортныеДанныеФизЛиц.СрезПоследних(&ДатаСреза, Физлицо = &ФизЛицо) КАК ДокументыФизическихЛицСрезПоследних
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВидыДокументовФизическихЛиц КАК ВидыДокументовФизическихЛиц
	|		ПО ДокументыФизическихЛицСрезПоследних.ДокументВид = ВидыДокументовФизическихЛиц.Ссылка";
	Запрос.УстановитьПараметр("ДатаСреза", КонецДня(НаДату));
	Запрос.УстановитьПараметр("ФизЛицо", ФизическоеЛицо);
	Запрос.УстановитьПараметр("КодыМВД", КодыМВД);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ПокупательПерсональныеДанные = СокрЛП(Выборка.Серия) + " " + СокрЛП(Выборка.Номер) + " " + СокрЛП(Выборка.КемВыдан)
			+ " " + Формат(Выборка.ДатаВыдачи, НСтр("ru='ДФ=dd.MM.yyyy'"));
	КонецЕсли;
	
	Возврат ПокупательПерсональныеДанные;
	
КонецФункции

// Возвращает статус является ли покупатель физическим лицом.
// 
// Параметры:
//  Контрагент - СправочникСсылка.Контрагенты - Контрагент
// 
// Возвращаемое значение:
//  Булево - Истина, если покупатель это физическое лицо
Функция ПокупательФизическоеЛицо(Контрагент) Экспорт
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область ПроцедурыВнесенияИВыемкиДС

#КонецОбласти

#Область Прочее

// Проверить корректность заполнения алкогольной продукции
//
// Параметры:
//  ДанныеДляЕГАИС - Массив - Данные для ЕГАИС
//  Объект - ДокументОбъект - Проверяемый объект
//  Отказ - Булево - Признак отказа
//
Процедура ПроверитьЗаполнениеАкцизныхМарокЧека(Объект, Отказ) Экспорт
	
	РазрешатьПродажуАлкогольнойПродукцииБезСопоставленияЕГАИС = ИнтеграцияЕГАИСВызовСервера.РазрешатьПродажуАлкогольнойПродукцииБезСопоставленияЕГАИС();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Товары.КлючСтроки,
	|	Товары.Номенклатура,
	|	Товары.НоменклатураЕГАИС,
	|	Товары.Количество,
	|	Товары.НомерСтроки
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АкцизныеМарки.КлючСтроки
	|ПОМЕСТИТЬ АкцизныеМарки
	|ИЗ
	|	&АкцизныеМарки КАК АкцизныеМарки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.КлючСтроки,
	|	Товары.Номенклатура,
	|	Товары.НоменклатураЕГАИС,
	|	ВЫБОР КОГДА Товары.НоменклатураЕГАИС = ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка) ТОГДА
	|		Товары.Номенклатура.ОбъемДАЛ * 10
	|	ИНАЧЕ
	|		Товары.НоменклатураЕГАИС.Объем
	|	КОНЕЦ КАК Объем,
	|	Товары.Количество,
	|	Товары.НомерСтроки
	|ПОМЕСТИТЬ МаркируемаяПродукция
	|ИЗ
	|	Товары КАК Товары
	|ГДЕ
	|	ВЫБОР КОГДА Товары.НоменклатураЕГАИС = ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка) ТОГДА
	|		Товары.Номенклатура.ВидАлкогольнойПродукцииЕГАИС.Маркируемый
	|	ИНАЧЕ
	|		Товары.НоменклатураЕГАИС.ВидПродукции.Маркируемый
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АкцизныеМарки.КлючСтроки,
	|	СУММА(1) КАК Количество
	|ПОМЕСТИТЬ АкцизныеМаркиКоличество
	|ИЗ
	|	АкцизныеМарки КАК АкцизныеМарки
	|
	|СГРУППИРОВАТЬ ПО
	|	АкцизныеМарки.КлючСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МаркируемаяПродукция.НомерСтроки                КАК НомерСтроки,
	|	МаркируемаяПродукция.Номенклатура               КАК Номенклатура,
	|	МаркируемаяПродукция.НоменклатураЕГАИС          КАК НоменклатураЕГАИС,
	|	МаркируемаяПродукция.Объем                      КАК Объем,
	|	МаркируемаяПродукция.Количество                 КАК Количество,
	|	ЕСТЬNULL(АкцизныеМаркиКоличество.Количество, 0) КАК КоличествоАкцизныхМарок
	|ИЗ
	|	МаркируемаяПродукция КАК МаркируемаяПродукция
	|		ЛЕВОЕ СОЕДИНЕНИЕ АкцизныеМаркиКоличество КАК АкцизныеМаркиКоличество
	|		ПО МаркируемаяПродукция.КлючСтроки = АкцизныеМаркиКоличество.КлючСтроки
	|ГДЕ
	|	Не МаркируемаяПродукция.Номенклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|");
	
	Запрос.УстановитьПараметр("Товары",        Объект.Товары.Выгрузить(, "Номенклатура, НоменклатураЕГАИС, КлючСтроки, Количество, НомерСтроки"));
	Запрос.УстановитьПараметр("АкцизныеМарки", Объект.АкцизныеМарки.Выгрузить(, "КлючСтроки"));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Не РазрешатьПродажуАлкогольнойПродукцииБезСопоставленияЕГАИС
			И Не ЗначениеЗаполнено(Выборка.НоменклатураЕГАИС) Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтрШаблон(
					НСтр("ru='Позиция номенклатуры %1 не сопоставлена с элементом классификатора алкогольной продукции ЕГАИС в строке %2 списка ""Товары"".'"),
					Выборка.Номенклатура,
					Выборка.НомерСтроки),
				Объект,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", Выборка.НомерСтроки, "Номенклатура"),
				,
				Отказ);
			
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Выборка.Объем) Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтрШаблон(
					НСтр("ru='Не заполнен объем для алкогольной продукции %1 в строке %2 списка ""Товары"".'"),
					Выборка.Номенклатура,
					Выборка.НомерСтроки),
				Объект,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", Выборка.НомерСтроки, "Номенклатура"),
				,
				Отказ);
			
		КонецЕсли;
		
		Если Выборка.Количество <> Выборка.КоличествоАкцизныхМарок Тогда
			
			ТекстОшибки = НСтр("ru = 'В строке %1 для %2 должно быть указано акцизных марок - %3, а указано - %4.'");
			ТекстОшибки = СтрШаблон(ТекстОшибки,
			                        Выборка.НомерСтроки,
			                        Выборка.Номенклатура,
			                        Выборка.Количество,
			                        Выборка.КоличествоАкцизныхМарок);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					Объект,
					ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", Выборка.НомерСтроки, "КоличествоУпаковок"),,
					Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Получает организацию ЕГАИС по складу/организации.
//
// Параметры:
//  Склад		 - СправочникСсылка.Склады - склад документа.
//  Организация	 - СправочникСсылка.Организации - организация документа.
// 
// Возвращаемое значение:
//  СправочникСсылка.КлассификаторОрганизацийЕГАИС - организация ЕГАИС.
//
Функция ПолучитьОрганизациюЕГАИС(Склад, Организация) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	КлассификаторОрганизацийЕГАИС.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.КлассификаторОрганизацийЕГАИС КАК КлассификаторОрганизацийЕГАИС
	|ГДЕ
	|	КлассификаторОрганизацийЕГАИС.ТорговыйОбъект = &Склад
	|	И КлассификаторОрганизацийЕГАИС.Контрагент = &Организация";
	
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка
	КонецЕсли;
	
	Возврат Неопределено
	
КонецФункции

// Сопоставить алкогольную продукцию с номенклатурой.
//
// Параметры:
//  Объект - ДокументОбъект - Объект в котором сопоставляется номенклатура.
//  ИмяКолонки - Строка - Имя колонки с номенклатурой ЕГАИС.
//  ЗаписыватьСправку2 - Булево - признак сопоставления по справке 2.
//  СопоставлятьПоИдентификаторуУпаковки - Булево - признак сопоставления по идентификатору.
//
Процедура СопоставитьАлкогольнуюПродукциюСНоменклатурой(Объект, ИмяКолонки = "НоменклатураЕГАИС", ЗаписыватьСправку2 = Ложь, СопоставлятьПоИдентификаторуУпаковки = Ложь, СопоставлятьПоСерии = Истина) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	КолонкиДляВыгрузки = ИмяКолонки + ", " + "Номенклатура, Характеристика";
	Если СопоставлятьПоСерии Тогда
		КолонкиДляВыгрузки = КолонкиДляВыгрузки + ", Серия";
	КонецЕсли;
	Если СопоставлятьПоИдентификаторуУпаковки Тогда
		КолонкиДляВыгрузки = КолонкиДляВыгрузки + ", ИдентификаторУпаковки";
	КонецЕсли;
	Если ЗаписыватьСправку2 Тогда
		КолонкиДляВыгрузки = КолонкиДляВыгрузки + ", Справка2";
	КонецЕсли;
	Данные = Объект.Товары.Выгрузить(, КолонкиДляВыгрузки);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	&ИмяКолонкиАлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	Т.Номенклатура,
	|	Т.Характеристика,
	|	&ИмяКолонкиСерия КАК Серия,
	|	&ИмяКолонкиИдентификаторУпаковки КАК ИдентификаторУпаковки,
	|	&ИмяКолонкиСправка2 КАК Справка2
	|ПОМЕСТИТЬ Таблица
	|ИЗ
	|	&Таблица КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	Таблица.Номенклатура КАК Номенклатура,
	|	Таблица.Характеристика КАК Характеристика,
	|	Таблица.Серия КАК Серия,
	|	Таблица.ИдентификаторУпаковки КАК ИдентификаторУпаковки,
	|	Таблица.Справка2 КАК Справка2,
	|	Сопоставлено.АлкогольнаяПродукция КАК Сопоставлено,
	|	МАКСИМУМ(ЕСТЬNULL(Сопоставлено.Порядок,ЕСТЬNULL(СопоставленоБЕЗСерии.Порядок,0))) КАК Порядок
	|ИЗ
	|	Таблица КАК Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК Сопоставлено
	|		ПО Сопоставлено.Номенклатура = Таблица.Номенклатура
	|		 И Сопоставлено.Характеристика = Таблица.Характеристика
	|		 И Сопоставлено.ИдентификаторУпаковки = Таблица.ИдентификаторУпаковки
	|		 И Сопоставлено.Справка2 = Таблица.Справка2
	|		 И Сопоставлено.Серия = Таблица.Серия
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СопоставленоБЕЗСерии
	|		ПО СопоставленоБЕЗСерии.Номенклатура = Таблица.Номенклатура
	|		 И СопоставленоБЕЗСерии.Характеристика = Таблица.Характеристика
	|		 И СопоставленоБЕЗСерии.ИдентификаторУпаковки = Таблица.ИдентификаторУпаковки
	|		 И СопоставленоБЕЗСерии.Справка2 = Таблица.Справка2
	|ГДЕ
	|	Таблица.АлкогольнаяПродукция <> ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка)
	|	И Таблица.Номенклатура <> &ПустаяСсылкаНоменклатура
	|	И (Сопоставлено.АлкогольнаяПродукция ЕСТЬ NULL
	|		ИЛИ Сопоставлено.АлкогольнаяПродукция <> Таблица.АлкогольнаяПродукция)
	|СГРУППИРОВАТЬ ПО
	|	Таблица.АлкогольнаяПродукция,
	|	Таблица.Номенклатура,
	|	Таблица.Характеристика,
	|	Таблица.Серия,
	|	Таблица.ИдентификаторУпаковки,
	|	Таблица.Справка2,
	|	Сопоставлено.АлкогольнаяПродукция
	|");
	
	Запрос.УстановитьПараметр("Таблица", Данные);
	Запрос.УстановитьПараметр("ПустаяСсылкаНоменклатура", Справочники.Номенклатура.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустаяСтрока", "");
	Запрос.УстановитьПараметр("ПустаяСправка2", Справочники.Справки2ЕГАИС.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустаяСерия", Справочники.СерииНоменклатуры.ПустаяСсылка());
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИмяКолонкиАлкогольнаяПродукция", ИмяКолонки);
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ИмяКолонкиИдентификаторУпаковки",
		?(СопоставлятьПоИдентификаторуУпаковки, "Т.ИдентификаторУпаковки", "&ПустаяСтрока"));
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ИмяКолонкиСерия",
		?(СопоставлятьПоСерии, "Т.Серия", "&ПустаяСерия"));
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ИмяКолонкиСправка2",
		?(ЗаписыватьСправку2, "Т.Справка2", "&ПустаяСправка2"));
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НаборЗаписей = РегистрыСведений.СоответствиеНоменклатурыЕГАИС.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Номенклатура.Установить(Выборка.Номенклатура, Истина);
		НаборЗаписей.Отбор.Характеристика.Установить(Выборка.Характеристика, Истина);
		НаборЗаписей.Отбор.Серия.Установить(Выборка.Серия, Истина);
		НаборЗаписей.Отбор.АлкогольнаяПродукция.Установить(Выборка.АлкогольнаяПродукция, Истина);
		НаборЗаписей.Отбор.ИдентификаторУпаковки.Установить(Выборка.ИдентификаторУпаковки, Истина);
		НаборЗаписей.Отбор.Справка2.Установить(Выборка.Справка2, Истина);
		
		НоваяЗапись = НаборЗаписей.Добавить();
		
		НоваяЗапись.Номенклатура   = Выборка.Номенклатура;
		НоваяЗапись.Характеристика = Выборка.Характеристика;
		
		ПараметрыУказанияСерий = ИнтеграцияИС.ПараметрыУказанияСерийФормыОбъекта(НоваяЗапись, РегистрыСведений.СоответствиеНоменклатурыЕГАИС);
		ИнтеграцияИСПереопределяемый.ЗаполнитьСтатусыУказанияСерий(НоваяЗапись, ПараметрыУказанияСерий);
		
		НоваяЗапись.Серия                 = Выборка.Серия;
		НоваяЗапись.АлкогольнаяПродукция  = Выборка.АлкогольнаяПродукция;
		НоваяЗапись.ИдентификаторУпаковки = Выборка.ИдентификаторУпаковки;
		НоваяЗапись.Справка2              = Выборка.Справка2;
		НоваяЗапись.Порядок               = Выборка.Порядок + 1;
		
		Попытка
			НаборЗаписей.Записать();
		Исключение
			
			ТекстОшибки = НСтр("ru = 'При записи соответствия номенклатуры произошла ошибка:
			                         |%1'");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтрШаблон(ТекстОшибки, КраткоеПредставлениеОшибки(ИнформацияОбОшибке())));
			
			ИнтеграцияЕГАИСВызовСервера.ЗаписатьОшибкуВЖурналРегистрации(
				СтрШаблон(ТекстОшибки, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())));
			
		КонецПопытки;
		
	КонецЦикла;
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область РегламентныеЗадания

#КонецОбласти

#Область ПроцедурыОткрытияИЗакрытияКассовойСмены

// Выполняет открытие кассовой смены.
//
// Параметры:
//  КассаККМ - СправочникСсылка.КассыККМ - Параметры регламентного задания
//  ОписаниеОшибки - Строка - Возвращаемое описание ошибки.
//
// Возвращаемое значение:
//  Булево - Истина, Если операция выполнена успешно.
//
Функция ОткрытьКассовуюСмену(КассаККМ, ОписаниеОшибки = "") Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Истина;
	
	СтруктураСостояниеКассовойСмены = ПолучитьСостояниеКассовойСмены(КассаККМ);
	
	ДатаОткрытияКассовойСмены = ТекущаяДатаСеанса();
	
	Если СтруктураСостояниеКассовойСмены.СтатусКассовойСмены = Перечисления.СтатусыКассовойСмены.Открыта Тогда
		
		// Если смена открыта, то с момента открытия должно пройти не больше чем 24 часа.
		Если СтруктураСостояниеКассовойСмены.Ошибка24Часа Тогда
			
			Результат = Ложь;
			// Вероятно, что смена была не закрыта.
			ОписаниеОшибки = ОписаниеОшибки24Часа();
			
		КонецЕсли;
		
	Иначе
		
		// Смена закрыта. Откроем новую кассовую смену.
		
		НоваяКассоваяСмена = Документы.КассоваяСмена.СоздатьДокумент();
		НоваяКассоваяСмена.Заполнить(Новый Структура("КассаККМ", КассаККМ));
		
		НоваяКассоваяСмена.Организация            = СтруктураСостояниеКассовойСмены.Организация;
		НоваяКассоваяСмена.КассаККМ               = КассаККМ;
		НоваяКассоваяСмена.Дата                   = ДатаОткрытияКассовойСмены;
		НоваяКассоваяСмена.Статус                 = Перечисления.СтатусыКассовойСмены.Открыта;
		НоваяКассоваяСмена.НачалоКассовойСмены    = ДатаОткрытияКассовойСмены;
		НоваяКассоваяСмена.ОкончаниеКассовойСмены = '00010101';
		
		Если НоваяКассоваяСмена.ПроверитьЗаполнение() Тогда
			НоваяКассоваяСмена.Записать(РежимЗаписиДокумента.Проведение);
		Иначе
			Результат = Ложь;
			ОписаниеОшибки = ПолучитьТекстОшибкиЗаполненияОтчета();
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ПолучениеДанныхПродаж


#КонецОбласти

#Область ФункцииПроверкиСостоянияКассовойСмены

// Функция возвращает структуру, характеризующую состояние последней кассовой смены по кассе ККМ.
//
// Параметры:
//  Объект - СправочникСсылка.КассыККМ, СправочникСсылка.ТорговоеОборудование - Ссылка на кассу ККМ.
//
// Возвращаемое значение:
//  Структура - Структура описания кассовой смены.
//
Функция ПолучитьСостояниеКассовойСмены(Объект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	КассаККМФискальноеУстройство = КассаККМФискальноеУстройство(Объект);
	ФискальноеУстройство = КассаККМФискальноеУстройство.ФискальноеУстройство;
	КассаККМ             = КассаККМФискальноеУстройство.КассаККМ;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КассоваяСмена.Номер                      КАК НомерКассовойСмены,
	|	КассоваяСмена.Ссылка                     КАК КассоваяСмена,
	|	КассоваяСмена.Статус                     КАК СтатусКассовойСмены,
	|	
	|	КассоваяСмена.Организация          КАК Организация,
	|	КассоваяСмена.КассаККМ             КАК КассаККМ,
	|	КассоваяСмена.ФискальноеУстройство КАК ФискальноеУстройство,
	|	
	|	ВЫБОР
	|		КОГДА КассоваяСмена.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыКассовойСмены.Открыта)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СменаОткрыта,
	|	
	|	ВЫБОР
	|		КОГДА КассоваяСмена.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыКассовойСмены.Открыта)
	|			ТОГДА КассоваяСмена.НачалоКассовойСмены
	|		ИНАЧЕ КассоваяСмена.ОкончаниеКассовойСмены
	|	КОНЕЦ КАК ДатаИзмененияСтатуса
	|ИЗ
	|	Документ.КассоваяСмена КАК КассоваяСмена
	|ГДЕ
	|	КассоваяСмена.Проведен
	|	И ((КассоваяСмена.КассаККМ = &КассаККМ И &КассаККМ <> Неопределено)
	|	ИЛИ (КассоваяСмена.ФискальноеУстройство = &ФискальноеУстройство И &ФискальноеУстройство <> Неопределено))
	|
	|УПОРЯДОЧИТЬ ПО
	|	КассоваяСмена.Дата УБЫВ,
	|	КассоваяСмена УБЫВ";
	
	Запрос.УстановитьПараметр("КассаККМ",             КассаККМ);
	Запрос.УстановитьПараметр("ФискальноеУстройство", ФискальноеУстройство.Наименование);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	ОписаниеКассовойСмены = ОписаниеКассовойСмены();
	
	Если Выборка.Следующий() Тогда
		
		ЗаполнитьЗначенияСвойств(ОписаниеКассовойСмены, Выборка);
		
		Если (ТекущаяДатаСеанса() - ОписаниеКассовойСмены.ДатаИзмененияСтатуса >= 86400)
			И Выборка.СменаОткрыта Тогда
			ОписаниеКассовойСмены.Ошибка24Часа = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КассаККМ) Тогда
		ЗаполнитьОписаниеКассовойСменыПоКассеККМ(ОписаниеКассовойСмены, КассаККМ);
	КонецЕсли;
	
	Возврат ОписаниеКассовойСмены;
	
КонецФункции

#КонецОбласти

#Область РаботаСКонтрактнойИнформациейФЗ54

Процедура УстановитьАдресЭПДляОтправкиЧека(Объект, СтарыйКонтрагент, НовыйКонтрагент, НужноУстанавливать) Экспорт
	
	ТипКонтрагенты = Тип("СправочникСсылка.Контрагенты");
	
	Если НужноУстанавливать И 
		ТипЗнч(СтарыйКонтрагент) = ТипКонтрагенты И ТипЗнч(НовыйКонтрагент) = ТипКонтрагенты Тогда
		
		Если СокрЛП(Объект.Email) = МенеджерОборудованияВызовСервера.АдресЭПКонтрагентаПоУмолчанию(СтарыйКонтрагент) 
			ИЛИ ПустаяСтрока(Объект.Email) Тогда
			Объект.Email = МенеджерОборудованияВызовСервера.АдресЭПКонтрагентаПоУмолчанию(НовыйКонтрагент);
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры

// Заполнить параметры отправки электронного чека по контрагенту.
//
// Параметры:
//  Форма - Форма - Форма (ФормаОплатыНаличными или ФормаСмешаннойОплаты).
//
Процедура ЗаполнитьПараметрыОтправкиЭлектронногоЧекаПоКонтрагенту(Форма) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(Форма.ДокументСсылка.Контрагент) Тогда
		
		ВариантОтправкиЭлектронногоЧекаКонтрагента = Перечисления.ВариантыОтправкиЭлектронногоЧекаПокупателю.ПустаяСсылка();
		ВариантОтправкиЭлектронногоЧека = Перечисления.ВариантыОтправкиЭлектронногоЧекаПокупателю.НеОтправлять;
		
	Иначе
		
		УстановитьАдресЭПДляОтправкиЧека(Форма, Справочники.Контрагенты.ПустаяСсылка(), Форма.ДокументСсылка.Контрагент, Истина);
		ВариантОтправкиЭлектронногоЧека = ?(ПустаяСтрока(Форма.Email), Перечисления.ВариантыОтправкиЭлектронногоЧекаПокупателю.НеОтправлять, Перечисления.ВариантыОтправкиЭлектронногоЧекаПокупателю.ОтправитьEmail);
		
	КонецЕсли;
	
	РозничныеПродажиКлиентСервер.УстановитьВариантОтправкиЭлектронногоЧека(Форма, ВариантОтправкиЭлектронногоЧека);
	
КонецПроцедуры

// Обработать данные электронного чека.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма (ФормаОплатыНаличными или ФормаСмешаннойОплаты).
// 
// Возвращаемое значение:
//  Структура - Данные электронного чека, см. функцию СтруктураДанныеЭлектронногоЧека().
//
Функция ОбработатьДанныеЭлектронногоЧека(Форма) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
	
		ДанныеЭлектронногоЧека = РозничныеПродажиКлиентСервер.ДанныеЭлектронногоЧека(Форма);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Розничные продажи'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		Возврат Неопределено;
		
	КонецПопытки;
	
	Возврат ДанныеЭлектронногоЧека;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПроцедурыВнесенияИВыемкиДс

#КонецОбласти

#Область ПроцедурыОткрытияИЗакрытияКассовойСмены

// Функция возвращает текст описания ошибки заполнения отчета о розничных продажах.
//
Функция ПолучитьТекстОшибкиЗаполненияОтчета()
	
	Возврат НСтр("ru = 'Проверьте настройки кассы ККМ.'");
	
КонецФункции

#КонецОбласти

#Область ФункцииПроверкиСостоянияКассовойСмены

Функция КассаККМФискальноеУстройство(Объект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ФискальноеУстройство = Неопределено;
	КассаККМ             = Неопределено;
	
	Если ТипЗнч(Объект) = Тип("СправочникСсылка.КассыККМ") Тогда
	
		КассаККМ = Объект;
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	КассыККМ.Модель КАК ФискальноеУстройство
		|ИЗ
		|	РегистрСведений.ТорговоеОборудование КАК КассыККМ
		|ГДЕ
		|	КассыККМ.КассаККМ = &КассаККМ
		|");
		
		Запрос.УстановитьПараметр("КассаККМ",     КассаККМ);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		
		ФискальноеУстройство = Выборка.ФискальноеУстройство;
		
	Иначе
		
		ФискальноеУстройство = Объект;
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ 
		|	КассыККМ.КассаККМ КАК КассаККМ
		|ИЗ
		|	РегистрСведений.ТорговоеОборудование КАК КассыККМ
		|ГДЕ
		|	КассыККМ.Модель = &ФискальноеУстройство
		|");
		
		Запрос.УстановитьПараметр("ФискальноеУстройство", ФискальноеУстройство);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		
		КассаККМ = Выборка.КассаККМ;
		
	КонецЕсли;
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ФискальноеУстройство", ФискальноеУстройство);
	ВозвращаемоеЗначение.Вставить("КассаККМ",             КассаККМ);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ИдентификаторТОПоКассеККМ(КассаККМ) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КассыККМ.Идентификатор КАК Идентификатор
	|ИЗ
	|	РегистрСведений.ТорговоеОборудование КАК КассыККМ
	|ГДЕ
	|	КассыККМ.КассаККМ = &КассаККМ");
		
	Запрос.УстановитьПараметр("КассаККМ",     КассаККМ);
		
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
		
	Возврат Выборка.Идентификатор;
	
КонецФункции

Функция СменаНаКассе(КассаККМ, МодельТО) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КассоваяСмена.Ссылка КАК КассоваяСмена
	|ИЗ
	|	Документ.КассоваяСмена КАК КассоваяСмена
	|ГДЕ
	|	КассоваяСмена.Проведен
	|	И (КассоваяСмена.КассаККМ = &КассаККМ
	|				И &КассаККМ <> НЕОПРЕДЕЛЕНО
	|			ИЛИ КассоваяСмена.ФискальноеУстройство = &ФискальноеУстройство
	|				И &ФискальноеУстройство <> НЕОПРЕДЕЛЕНО)
	|	И КассоваяСмена.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыКассовойСмены.Открыта)";
	Запрос.УстановитьПараметр("ФискальноеУстройство", МодельТО.Наименование);
	Запрос.УстановитьПараметр("КассаККМ", КассаККМ);
	ВыбСмена = Запрос.Выполнить().Выбрать();
	
	Если ВыбСмена.Следующий() Тогда
		Возврат ВыбСмена.КассоваяСмена;
	Иначе
		Возврат Документы.КассоваяСмена.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьОписаниеКассовойСменыПоКассеККМ(ОписаниеКассовойСмены, КассаККМ)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КассыККМ.Владелец                               КАК Организация
	|ИЗ
	|	Справочник.КассыККМ КАК КассыККМ
	|ГДЕ
	|	КассыККМ.Ссылка = &КассаККМ");
		
	Запрос.УстановитьПараметр("КассаККМ", КассаККМ);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	ОписаниеКассовойСмены.КассаККМ            = КассаККМ;
	ОписаниеКассовойСмены.Организация         = Выборка.Организация;
	
	ТекДатСеанса = ТекущаяДатаСеанса();
	ОписаниеКассовойСмены.СведенияООрганизации = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Выборка.Организация, ТекДатСеанса);

	ОписаниеКассовойСмены.СистемаНалогообложения = МенеджерОборудованияКлиентСервер.СистемаНалогообложения
		(Выборка.Организация, ТекДатСеанса);

КонецПроцедуры

// Возвращает пустую структуру состояния кассовой смены.
//
Функция ОписаниеКассовойСмены()
	
	СостояниеКассовойСмены = Новый Структура;
	СостояниеКассовойСмены.Вставить("ДатаИзмененияСтатуса");
	СостояниеКассовойСмены.Вставить("СтатусКассовойСмены");
	СостояниеКассовойСмены.Вставить("СтатусРегламентныхОпераций");
	СостояниеКассовойСмены.Вставить("КассоваяСмена");
	СостояниеКассовойСмены.Вставить("НаличностьВКассе");
	СостояниеКассовойСмены.Вставить("НомерКассовойСмены");
	СостояниеКассовойСмены.Вставить("СменаОткрыта", Ложь);
	СостояниеКассовойСмены.Вставить("Ошибка24Часа", Ложь);
	СостояниеКассовойСмены.Вставить("ОписаниеОшибки", "");
	
	// Описание реквизитов кассовой смены
	СостояниеКассовойСмены.Вставить("Организация");
	СостояниеКассовойСмены.Вставить("КассаККМ");
	СостояниеКассовойСмены.Вставить("ФискальноеУстройство");
	
	СостояниеКассовойСмены.Вставить("Валюта");
	СостояниеКассовойСмены.Вставить("ВалютаПредставление");
	СостояниеКассовойСмены.Вставить("ВидЦены");
	СостояниеКассовойСмены.Вставить("Кассир");
	СостояниеКассовойСмены.Вставить("Склад");
	СостояниеКассовойСмены.Вставить("АдресСклада", "");
	СостояниеКассовойСмены.Вставить("ЦенаВключаетНДС");
	СостояниеКассовойСмены.Вставить("НалогообложениеНДС");
	
	// Прочее
	СостояниеКассовойСмены.Вставить("СведенияООрганизации");
	СостояниеКассовойСмены.Вставить("СерийныйНомерККМ",       "");
	СостояниеКассовойСмены.Вставить("СистемаНалогообложения", Перечисления.ТипыСистемНалогообложенияККТ.ПустаяСсылка());
	
	Возврат СостояниеКассовойСмены;
	
КонецФункции

// Описание ошибки24 часа
// 
// Возвращаемое значение:
//  Строка - Описание ошибки
//
Функция ОписаниеОшибки24Часа() Экспорт
	
	Возврат НСтр("ru = 'С момента открытия кассовой смены истекло более 24 часов. Необходимо выполнить закрытие кассовой смены.'");
	
КонецФункции

#КонецОбласти

#Область Прочее

// Заполнить реквизит формы "РеквизитыКассира".
//
// Параметры:
//  Кассир - Справочник.Пользователи - Кассир.
//
Функция РеквизитыКассира(Кассир) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	"""" КАК ИНН,
	|	ЕСТЬNULL(Пользователи.ФизЛицо.Наименование, """") КАК Наименование
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|ГДЕ
	|	Пользователи.Ссылка = &Кассир");
	Запрос.УстановитьПараметр("Кассир", Кассир);
	
	РеквизитыКассира = Новый Структура;
	РеквизитыКассира.Вставить("ИНН", "");
	РеквизитыКассира.Вставить("Наименование", НСтр("ru = 'Администратор'"));
	
	ВыборкаРеквизитыКассира = Запрос.Выполнить().Выбрать();
	Если ВыборкаРеквизитыКассира.Следующий() Тогда
		
		Если ЗначениеЗаполнено(ВыборкаРеквизитыКассира.Наименование) Тогда
			РеквизитыКассира.Наименование = ВыборкаРеквизитыКассира.Наименование;
		КонецЕсли;
		
		РеквизитыКассира.ИНН = ВыборкаРеквизитыКассира.ИНН;
		
	КонецЕсли;
	
	Возврат РеквизитыКассира;
	
КонецФункции

Функция ПараметрыРегистрацииУстройства(Знач ИдентификаторУстройства)
	
	Возврат Неопределено;

КонецФункции

#Область ОбновлениеИнформационнойБазы

#КонецОбласти

#КонецОбласти

#КонецОбласти
