#Область ПрограммныйИнтерфейс

// Открывает кассовую смену
//
// Параметры:
//  Параметры - Структура - (Структура - Параметры кассы ККМ (ИдентификаторУстройства,
//                                 ИспользоватьБезПодключенияОборудования, КассаККМ),
//	 							Форма)
//
Процедура ОткрытьКассовуюСмену(Параметры) Экспорт
	
	КассовыеСменыКлиент.ОткрытьКассовуюСмену(РозничныеПродажи.ИдентификаторТОПоКассеККМ(Параметры.Форма.КассаККМ));
	
КонецПроцедуры

// Закрывает кассовую смену
//
// Параметры:
//  Параметры - Структура - (Структура - Параметры кассы ККМ (ИдентификаторУстройства,
//                                 ИспользоватьБезПодключенияОборудования, КассаККМ),
//	 							Форма)
//
Процедура ЗакрытьКассовуюСмену(Параметры) Экспорт
	
	КассовыеСменыКлиент.ЗакрытьКассовуюСмену(РозничныеПродажи.ИдентификаторТОПоКассеККМ(Параметры.Форма.КассаККМ), РозничныеПродажи.СменаНаКассе(Параметры.Форма.КассаККМ, Параметры.Форма.ККТ));
		
КонецПроцедуры

Процедура ПроверитьКодМаркировкиСредствамиККТ(ПозицииЧека, ФормаВладелец, ЗаголовокКнопкиИгнорировать = Неопределено, ОповещениеОЗавершении, ФормаПросмотра = Неопределено, ПараметрыККТ = Неопределено) Экспорт
	
	ПараметрыСканирования = ШтрихкодированиеИСКлиент.ПараметрыСканирования(ФормаВладелец);
	
	Если ФормаПросмотра <> Неопределено
		И МенеджерОборудованияВызовСервера.ФискальноеУстройствоПоддерживаетПроверкуКодовМаркировки(ФормаПросмотра.ОборудованиеККТ, ПараметрыККТ) Тогда
		ПараметрыСканирования.ТребуетсяПроверкаСредствамиККТ = Истина;
		ПараметрыСканирования.ККТФФД12ИСМП                   = ФормаПросмотра.ОборудованиеККТ;
	КонецЕсли;
	
	ДанныеДляПроверки = Новый Массив;
	
	Для Каждого ПозицияЧека Из ПозицииЧека Цикл
		
		Если НЕ ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(ПозицияЧека, "РезультатРаспределенияВрем")
			ИЛИ ПозицияЧека.РезультатРаспределенияВрем = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЭлементДанныхДляПроверки = ШтрихкодированиеИСМПКлиент.НовыйЭлементПроверкиСредствамиККТПоДаннымРаспределения(
			ПозицияЧека.РезультатРаспределенияВрем);
		
		ДанныеДляПроверки.Добавить(ЭлементДанныхДляПроверки);
		
	КонецЦикла;
	
	ПараметрыПроверкиКМСредствамиККТ = ШтрихкодированиеИСМПКлиент.ПараметрыНачалаПроверкиКодовМаркировкиСредствамиККТ();
	ПараметрыПроверкиКМСредствамиККТ.ОповещениеОЗавершении       = ОповещениеОЗавершении;
	ПараметрыПроверкиКМСредствамиККТ.ДанныеДляПроверки           = ДанныеДляПроверки;
	ПараметрыПроверкиКМСредствамиККТ.ПараметрыСканирования       = ПараметрыСканирования;
	ПараметрыПроверкиКМСредствамиККТ.ФормаОсновногоОбъекта       = ФормаВладелец;
	ПараметрыПроверкиКМСредствамиККТ.ФормаВспомогательная        = ФормаПросмотра;
	ПараметрыПроверкиКМСредствамиККТ.ЗаголовокКнопкиИгнорировать = ЗаголовокКнопкиИгнорировать;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ФормаВладелец, "Объект") Тогда
		ДокументСсылка = ФормаВладелец.Объект.Ссылка;
	ИначеЕсли ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ФормаВладелец, "Ссылка") Тогда
		ДокументСсылка = ФормаВладелец.Ссылка;
	КонецЕсли;
	
	Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер")
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.РасходныйКассовыйОрдер")
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОплатаОтПокупателяПлатежнойКартой") Тогда
		
		ПараметрыПроверкиКМСредствамиККТ.ЭтоДокументОплаты = Истина;
	КонецЕсли;
	
	ШтрихкодированиеИСМПКлиент.НачатьПроверкуКодовМаркировкиСредствамиККТ(ПараметрыПроверкиКМСредствамиККТ);
	
КонецПроцедуры

Функция ТребуетсяПроверкаКодовМаркировкиСредствамиККТ(ПараметрыОперацииФискализацииЧека) Экспорт
	
	ТребуетсяПроверкаКодовМаркировкиСредствамиККТ = Ложь;
	
	ПозицииЧека = ПараметрыОперацииФискализацииЧека.ПозицииЧека;
	
	Для Каждого ПозицияЧека Из ПозицииЧека Цикл
		Если ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(ПозицияЧека, "РезультатРаспределенияВрем")
			И ПозицияЧека.РезультатРаспределенияВрем <> Неопределено Тогда
			
			ТребуетсяПроверкаКодовМаркировкиСредствамиККТ = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТребуетсяПроверкаКодовМаркировкиСредствамиККТ;
	
КонецФункции

#Область УправлениеСостояниемСмены

// Контролирует параметры текущей кассовой смены, при необходимости открывает и закрывает кассовую смену.
//
// Параметры:
//  Форма - Форма - Форма документа в которой выполняется контроль кассовой смены
//  ОписаниеОповещенияЗавершение - ОписаниеОповещения - Описание оповещения при завершении операции.
//
Процедура ОбработатьСостояниеСмены(Форма, ОписаниеОповещенияЗавершение) Экспорт
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ККТ") Тогда
		ПараметрыКассыККМ = Форма.КассаККМ;
		Форма.СтруктураСостояниеКассовойСмены = РозничныеПродажиВызовСервера.СостояниеКассовойСмены(ПараметрыКассыККМ);
	Иначе
		ПараметрыКассыККМ = Форма.ПараметрыКассыККМ;
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ОбработкаОбъект")
			И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма.ОбработкаОбъект, "КассаККМ") Тогда
			КассаККМ = Форма.Объект.КассаККМ;
		Иначе
			КассаККМ = Форма.КассаККМ;
		КонецЕсли;
		Форма.СтруктураСостояниеКассовойСмены = РозничныеПродажиВызовСервера.СостояниеКассовойСмены(КассаККМ);
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ОбработкаОбъект")
		И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма.ОбработкаОбъект, "Кассир") Тогда
		ЗаполнитьЗначенияСвойств(Форма.ОбработкаОбъект, Форма.СтруктураСостояниеКассовойСмены,,"Кассир");
	ИначеЕсли ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ОбработкаОбъект") Тогда
		ЗаполнитьЗначенияСвойств(Форма.ОбработкаОбъект, Форма.СтруктураСостояниеКассовойСмены);
	КонецЕсли;
	
	Если Не Форма.СтруктураСостояниеКассовойСмены.СменаОткрыта Тогда
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить("ОткрытьКассовуюСмену", НСтр("ru = 'Открыть смену'"));
		Кнопки.Добавить("Отмена",               НСтр("ru = 'Отмена'"));
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ОбработкаОповещения", ОписаниеОповещенияЗавершение);
		ДополнительныеПараметры.Вставить("ПараметрыКассыККМ", ПараметрыКассыККМ);
		ДополнительныеПараметры.Вставить("Форма", Форма);
		
		ПоказатьВопросИС(
			ОписаниеОповещенияИС("УправлениеСостояниемСменыОбработкаКоманды", РозничныеПродажиКлиент, ДополнительныеПараметры),
			НСтр("ru = 'Перед выполнением операции требуется открыть новую кассовую смену.'"), Кнопки);
		Возврат;
		
	ИначеЕсли Не Форма.СтруктураСостояниеКассовойСмены.СменаОткрыта Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Кассовая смена не открыта.'"));
		
		ВыполнитьОбработкуОповещенияИС(ОписаниеОповещенияЗавершение, Ложь);
		Возврат;
		
	КонецЕсли;
	
	Если Форма.СтруктураСостояниеКассовойСмены.Ошибка24Часа Тогда
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить("ЗакрытьИОткрытьКассовуюСмену", НСтр("ru = 'Открыть смену'"));
		Кнопки.Добавить("Отмена",                       НСтр("ru = 'Отмена'"));
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ОбработкаОповещения", ОписаниеОповещенияЗавершение);
		ДополнительныеПараметры.Вставить("ПараметрыКассыККМ", ПараметрыКассыККМ);
		ДополнительныеПараметры.Вставить("Форма", Форма);
		
		ПоказатьВопросИС(
			ОписаниеОповещенияИС("УправлениеСостояниемСменыОбработкаКоманды", РозничныеПродажиКлиент, ДополнительныеПараметры),
			НСтр("ru = 'С момента открытия смены прошло более 24 часов. Перед выполнением операции требуется открыть новую кассовую смену.'"), Кнопки);
		Возврат;
		
	ИначеЕсли Не Форма.СтруктураСостояниеКассовойСмены.СменаОткрыта Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'С момента открытия смены прошло более 24 часов. Перед выполнением операции требуется открыть новую кассовую смену.'"));
		
		ВыполнитьОбработкуОповещенияИС(ОписаниеОповещенияЗавершение, Ложь);
		Возврат;
		
	КонецЕсли;
	
	ВыполнитьОбработкуОповещенияИС(ОписаниеОповещенияЗавершение, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОплатаПлатежнымиКартами

#КонецОбласти

#Область ОтменаОплатыПлатежнымиКартами

#КонецОбласти

Процедура ЗакрытьСессиюПроверкиКМНаККТ(УникальныйИдентификатор, ИдентификаторУстройства) Экспорт
	
	Если МенеджерОборудованияКлиент.СессияПроверкиКодовМаркировки(ИдентификаторУстройства) <> Неопределено Тогда
		ОповещениеПриЗавершении = Новый ОписаниеОповещения("ЗакрытьСессиюПроверкиКМНаККТЗавершение", РозничныеПродажиКлиент);
		МенеджерОборудованияКлиент.НачатьЗакрытииСессииРегистрацииКМ(ОповещениеПриЗавершении, УникальныйИдентификатор, , ИдентификаторУстройства);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область УправлениеСостояниемСмены

Процедура УправлениеСостояниемСменыОбработкаКоманды(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = "Отмена" Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат = "ОткрытьКассовуюСмену" Тогда
		ОткрытьКассовуюСмену(ДополнительныеПараметры);
	КонецЕсли;
	
	Если Результат = "ЗакрытьИОткрытьКассовуюСмену" Тогда
		ЗакрытьКассовуюСмену(ДополнительныеПараметры);
		ОткрытьКассовуюСмену(ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОплатаПлатежнымиКартами

#КонецОбласти

#Область ОтменаОплатыПлатежнымиКартами

#КонецОбласти

#Область ДисплейПокупателя

#КонецОбласти

#Область КассовыеСмены

#КонецОбласти

#Область ВнесениеИВыемкаДенег

#КонецОбласти

Процедура ЗакрытьСессиюПроверкиКМНаККТЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	// Обработать, при необходимости, результат закрытия сессии проверки КМ на ККТ.
	Возврат;
	
КонецПроцедуры

#КонецОбласти

