#Область ПрограммныйИнтерфейс

#Область ФискальныеОперации

Процедура ЗаполнитьДанныеФискальнойОперации(ПараметрыОперацииФискализацииЧека, ДанныеФискальнойОперации) Экспорт
	
	ПараметрыОперацииФискализацииЧека.Электронно = ДанныеФискальнойОперации.НеПечататьФискальныйЧек;
	
	Если ДанныеФискальнойОперации.ВариантОтправкиЭлектронногоЧека = ПредопределенноеЗначение("Перечисление.ВариантыОтправкиЭлектронногоЧекаПокупателю.ОтправитьSMS")
		И ЗначениеЗаполнено(ДанныеФискальнойОперации.КонтактныеДанныеЭлектронногоЧека) Тогда
		ПараметрыОперацииФискализацииЧека.Отправляет1СSMS = Не ДанныеФискальнойОперации.ОтправлятьSMSЧерезОФД;
		ПараметрыОперацииФискализацииЧека.ПокупательНомер = ДанныеФискальнойОперации.КонтактныеДанныеЭлектронногоЧека;
	КонецЕсли;
	
	Если ДанныеФискальнойОперации.ВариантОтправкиЭлектронногоЧека = ПредопределенноеЗначение("Перечисление.ВариантыОтправкиЭлектронногоЧекаПокупателю.ОтправитьEmail")
		И ЗначениеЗаполнено(ДанныеФискальнойОперации.КонтактныеДанныеЭлектронногоЧека) Тогда
		ПараметрыОперацииФискализацииЧека.Отправляет1СEmail = Не ДанныеФискальнойОперации.ОтправлятьEmailЧерезОФД;
		ПараметрыОперацииФискализацииЧека.ПокупательEmail = ДанныеФискальнойОперации.КонтактныеДанныеЭлектронногоЧека;
	КонецЕсли;
	
	ПараметрыОперацииФискализацииЧека.ОтправительEmail = ДанныеФискальнойОперации.ОтправительEmail;
	
КонецПроцедуры

Функция СтавкаНДСФискальнойОперации(СтавкаНДС, ПрименяютсяРасчетныеСтавки = Ложь) Экспорт
	
	Если СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.БезНДС")
	 ИЛИ СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.ПустаяСсылка") Тогда
		Возврат Неопределено;
	ИначеЕсли СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20_120") И ПрименяютсяРасчетныеСтавки  Тогда 
		Возврат 120;
	ИначеЕсли СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС18_118") И ПрименяютсяРасчетныеСтавки  Тогда 
		Возврат 118;
	ИначеЕсли СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС10_110") И ПрименяютсяРасчетныеСтавки Тогда 
		Возврат 110;
	Иначе
		Возврат ЭлектронныеДокументыПереопределяемый.ПолучитьСтавкуНДСЧислом(СтавкаНДС);
	КонецЕсли;
	
КонецФункции

Функция ПризнакПредметаРасчетаФискальнойОперации(ТипНоменклатуры, ПодакцизныйТовар, ВидПродукцииИС = Неопределено) Экспорт
	
	ВозвращаемоеЗначение = ПредопределенноеЗначение("Перечисление.ПризнакиПредметаРасчета.Товар");
	
	Если ПодакцизныйТовар Тогда
		ВозвращаемоеЗначение = ПредопределенноеЗначение("Перечисление.ПризнакиПредметаРасчета.ПодакцизныйТовар");
	КонецЕсли;
	
	Если ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Услуга") Тогда
		ВозвращаемоеЗначение = ПредопределенноеЗначение("Перечисление.ПризнакиПредметаРасчета.Услуга");
	КонецЕсли;
	
	Если ВидПродукцииИС <> Неопределено Тогда
		Если ЗначениеЗаполнено(ВидПродукцииИС) Тогда
			Если ПодакцизныйТовар Тогда
				Если ВидПродукцииИС = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Алкогольная") Тогда
					Возврат ПредопределенноеЗначение("Перечисление.ПризнакиПредметаРасчета.ПодакцизныйТовар");
				ИначеЕсли ВидПродукцииИС = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Пиво") Тогда
					Возврат ПредопределенноеЗначение("Перечисление.ПризнакиПредметаРасчета.ПодакцизныйТоварПодлМаркировкеСИИмеющийКМ");
				ИначеЕсли ВидПродукцииИС = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Табак")
							Или ВидПродукцииИС = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.АльтернативныйТабак") Тогда
					Возврат ПредопределенноеЗначение("Перечисление.ПризнакиПредметаРасчета.ПодакцизныйТоварПодлМаркировкеСИИмеющийКМ");
				КонецЕсли;
			Иначе
				Если ВидПродукцииИС = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Алкогольная")
					ИЛИ ВидПродукцииИС = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха")
					ИЛИ ВидПродукцииИС = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПодконтрольнаяПродукцияВЕТИС")
					ИЛИ ВидПродукцииИС = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПодконтрольнаяПродукцияСАТУРН")
					ИЛИ ВидПродукцииИС = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ЗерноВЕТИС")
					ИЛИ ВидПродукцииИС = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПродуктыПереработкиЗернаВЕТИС") Тогда
					
					Возврат ПредопределенноеЗначение("Перечисление.ПризнакиПредметаРасчета.Товар");
				Иначе
					Возврат ПредопределенноеЗначение("Перечисление.ПризнакиПредметаРасчета.МаркированныйТоварПодлМаркировкеСИИмеющийКМ");
				КонецЕсли;
			КонецЕсли
		КонецЕсли;
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция РаспределитьШтрихкодыПоТаблицеТоваров(ДокументСсылка, МассивУпаковок, ПараметрыУказанияСерий, Товары) Экспорт
	
	ДанныеРаспределения
		= РозничныеПродажиВызовСервера.ДанныеРаспределенияШтрихкодовУпаковокПоТоварам(
			ДокументСсылка, МассивУпаковок, ПараметрыУказанияСерий, Товары);
	
	Если ДанныеРаспределения.ЕстьОшибки Тогда
		ВызватьИсключение СтрСоединить(ДанныеРаспределения.Ошибки, Символы.ПС);
	КонецЕсли;
	
	ТоварыРазобранные = Товары.СкопироватьКолонки();
	Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТоварыРазобранные, "Штрихкод") Тогда
		ТоварыРазобранные.Колонки.Добавить("Штрихкод");
	КонецЕсли;
	Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТоварыРазобранные, "КоэффициентПересчетаУпаковки") Тогда
		ТоварыРазобранные.Колонки.Добавить("КоэффициентПересчетаУпаковки");
	КонецЕсли;
	Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТоварыРазобранные, "РезультатРаспределения") Тогда
		ТоварыРазобранные.Колонки.Добавить("РезультатРаспределения");
	КонецЕсли;
	Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТоварыРазобранные, "ДополнениеКНаименованиюТовара") Тогда
		ТоварыРазобранные.Колонки.Добавить("ДополнениеКНаименованиюТовара");
	КонецЕсли;
	
	ОписанияНоменклатурыСЧастичнымВыбытием = Новый Соответствие;
	
	Для Каждого СтрокаТовары Из Товары Цикл
		
		РаспределениеПоСтроке = ДанныеРаспределения.РаспределенныеСтроки.Получить(СтрокаТовары);
		
		Если РаспределениеПоСтроке = Неопределено Тогда
			СтрокаТоварыРазобранные = ТоварыРазобранные.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТоварыРазобранные, СтрокаТовары);
			Продолжить;
		КонецЕсли;
		
		Для Каждого СтрокаРаспределения Из РаспределениеПоСтроке Цикл
			
			СтрокаТоварыРазобранные = ТоварыРазобранные.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТоварыРазобранные, СтрокаТовары);
			
			Коэффициент                                = 1;
			СтрокаТоварыРазобранные.КоличествоУпаковок = 1;
			СтрокаТоварыРазобранные.КоличествоЕдиниц   = 1;
			СтрокаТоварыРазобранные.Количество   = 1;
			ЦенаИзТоваров                              = Истина;
			
			Если ЗначениеЗаполнено(СтрокаРаспределения.Штрихкод) Тогда
				
				Если СтрокаРаспределения.ЧастичноеВыбытие
					ИЛИ СтрокаРаспределения.Количество <> 1 Тогда
					
					СтруктураНаименованияТовара = Новый Структура;
					СтруктураНаименованияТовара.Вставить("Упаковка",           СтрокаТовары.Упаковка);
					СтруктураНаименованияТовара.Вставить("Количество",         СтрокаРаспределения.Количество);
					СтруктураНаименованияТовара.Вставить("КоличествоУпаковок", СтрокаРаспределения.ЧастичноеВыбытиеКоличество);
					СтруктураНаименованияТовара.Вставить("Цена",               СтрокаТовары.Цена);
					
					СтрокаТоварыРазобранные.Количество                    = СтрокаРаспределения.Количество;
					СтрокаТоварыРазобранные.КоличествоЕдиниц              = СтрокаРаспределения.КоличествоУпаковок;
					СтрокаТоварыРазобранные.ДополнениеКНаименованиюТовара = СтруктураНаименованияТовара;
					
					ЦенаИзТоваров = Ложь;
					
					Если СтрокаРаспределения.Количество <> 1 Тогда
						Коэффициент = СтрокаРаспределения.Количество;
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
			СтрокаТоварыРазобранные.КоэффициентПересчетаУпаковки = Коэффициент;
			
			КоэффициентПересчетаУпаковки = 1;
			БазоваяЕдиница = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТовары.Номенклатура, "ЕдиницаХраненияОстатков");
			ДанныеЕдиницыИзмеренияЧастичногоВыбытия = Неопределено;
			
			ОписаниеНоменклатуры = ОписанияНоменклатурыСЧастичнымВыбытием.Получить(СтрокаТовары.Номенклатура);
			Если ОписаниеНоменклатуры = Неопределено Тогда
				СоответствиеОписаниеНоменклатуры = РегистрыСведений.ОписаниеНоменклатурыИС.ПолучитьОписание(СтрокаТовары.Номенклатура);
				ОписаниеНоменклатуры = СоответствиеОписаниеНоменклатуры[СтрокаТовары.Номенклатура];
				
				ОписанияНоменклатурыСЧастичнымВыбытием.Вставить(СтрокаТовары.Номенклатура, ОписаниеНоменклатуры);
			КонецЕсли;
			
			ЭтоМернаяЕдиница = Ложь;
			Если СтрокаТовары.КоличествоУпаковок <> СтрокаТовары.Количество Тогда
				
				Если НЕ СтрокаРаспределения.ЧастичноеВыбытие Тогда
					Если Не ЗначениеЗаполнено(СтрокаТоварыРазобранные.Упаковка) Тогда
						СтрокаТоварыРазобранные.Упаковка = БазоваяЕдиница;
					КонецЕсли;
				Иначе
					РеквизитыУпаковки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаТовары.Упаковка, "Коэффициент");
					КоэффициентПересчетаУпаковки = РеквизитыУпаковки.Коэффициент;
				КонецЕсли;
				
			ИначеЕсли СтрокаРаспределения.ЧастичноеВыбытие
				И НЕ ЗначениеЗаполнено(СтрокаТовары.Упаковка)
				И СтрокаТовары.КоличествоУпаковок <> СтрокаРаспределения.ЧастичноеВыбытиеКоличество Тогда
				
				ЭтоМернаяЕдиница = ОписаниеНоменклатуры.ВариантИспользованияЕдиницыХранения = Перечисления.ВариантыИспользованияЕдиницыХраненияИС.МернаяПродукцияТребуетУказанияЗначения;
				СтрокаТоварыРазобранные.Упаковка = ОписаниеНоменклатуры.УпаковкаЧастичногоВыбытия;
				КоэффициентПересчетаУпаковки = ОписаниеНоменклатуры.ЕмкостьПотребительскойУпаковки
				                                   / ОписаниеНоменклатуры.КоличествоВПотребительскойУпаковке;
			Иначе
				Если ОписаниеНоменклатуры.ВариантЧастичногоВыбытия = Перечисления.ВариантыУчетаЧастичногоВыбытияИС.ТекущаяНоменклатура Тогда
					Если СтрокаРаспределения.КоличествоУпаковок = СтрокаРаспределения.ЕмкостьПотребительскойУпаковки Тогда
						КоэффициентПересчетаУпаковки = СтрокаРаспределения.ЕмкостьПотребительскойУпаковки;
						СтрокаТоварыРазобранные.Упаковка = БазоваяЕдиница;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			КоличествоУпаковокКВыбытию = 0;
			Если СтрокаРаспределения.ЧастичноеВыбытие
				И СтрокаРаспределения.ВидПродукции = Перечисления.ВидыПродукцииИС.Пиво
				И ЭтоМернаяЕдиница Тогда
				КоличествоУпаковокКВыбытию = СтрокаРаспределения.Количество;
			ИначеЕсли СтрокаРаспределения.ЧастичноеВыбытие Тогда
				КоличествоУпаковокКВыбытию = СтрокаРаспределения.ЧастичноеВыбытиеКоличество;
			ИначеЕсли СтрокаРаспределения.Количество <> 1
				И ЭтоМернаяЕдиница Тогда

					КоличествоУпаковокКВыбытию = СтрокаРаспределения.Количество;
			Иначе
				КоличествоУпаковокКВыбытию = СтрокаРаспределения.Количество * КоэффициентПересчетаУпаковки;
			КонецЕсли;
			
			Если СтрокаТовары.Количество = Коэффициент Тогда
				СуммаСкидки = СтрокаТовары.СуммаСкидки;
				СуммаНДС    = СтрокаТовары.СуммаНДС;
				СуммаСНДС   = СтрокаТовары.СуммаСНДС;
			ИначеЕсли ЗначениеЗаполнено(СтрокаТовары.Упаковка)
				И СтрокаРаспределения.КоличествоУпаковок <> 0 Тогда
				СуммаСкидки = Окр(СтрокаТовары.СуммаСкидки / СтрокаТовары.КоличествоУпаковок * КоличествоУпаковокКВыбытию, 2);
				СуммаНДС    = Окр(СтрокаТовары.СуммаНДС    / СтрокаТовары.КоличествоУпаковок * КоличествоУпаковокКВыбытию, 2);
				СуммаСНДС   = Окр(СтрокаТовары.СуммаСНДС   / СтрокаТовары.КоличествоУпаковок * КоличествоУпаковокКВыбытию, 2);
			Иначе
				СуммаСкидки = Окр(СтрокаТовары.СуммаСкидки / СтрокаТовары.КоличествоУпаковок * СтрокаРаспределения.Количество, 2);
				СуммаНДС    = Окр(СтрокаТовары.СуммаНДС    / СтрокаТовары.КоличествоУпаковок * СтрокаРаспределения.Количество, 2);
				СуммаСНДС   = Окр(СтрокаТовары.СуммаСНДС   / СтрокаТовары.КоличествоУпаковок * СтрокаРаспределения.Количество, 2);
			КонецЕсли;
			
			Если ЦенаИзТоваров Тогда
				Цена = СтрокаТовары.Цена * КоэффициентПересчетаУпаковки;
			Иначе
				Цена = СуммаСНДС - СуммаСкидки;
			КонецЕсли;
			
			СтрокаТоварыРазобранные.СуммаСкидки            = СуммаСкидки;
			СтрокаТоварыРазобранные.СуммаНДС               = СуммаНДС;
			СтрокаТоварыРазобранные.СуммаСНДС              = СуммаСНДС;
			СтрокаТоварыРазобранные.Цена                   = Цена;
			СтрокаТоварыРазобранные.Штрихкод               = СтрокаРаспределения.Штрихкод;
			СтрокаТоварыРазобранные.РезультатРаспределения = СтрокаРаспределения;
			
			СтрокаТовары.КоличествоУпаковок = СтрокаТовары.КоличествоУпаковок - КоличествоУпаковокКВыбытию;
			СтрокаТовары.КоличествоЕдиниц   = СтрокаТовары.КоличествоЕдиниц   - СтрокаРаспределения.Количество;
			СтрокаТовары.СуммаСкидки        = СтрокаТовары.СуммаСкидки        - СтрокаТоварыРазобранные.СуммаСкидки;
			СтрокаТовары.СуммаНДС           = СтрокаТовары.СуммаНДС           - СтрокаТоварыРазобранные.СуммаНДС;
			СтрокаТовары.СуммаСНДС          = СтрокаТовары.СуммаСНДС          - СтрокаТоварыРазобранные.СуммаСНДС;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТоварыРазобранные;

КонецФункции

#КонецОбласти

// Структура данных для повтора операции записи.
// 
// Возвращаемое значение:
//  Структура - Данные для повтора операции записи.
//
Функция СтруктураПовтораЗаписи() Экспорт
	
	СтруктураПовтораЗаписи = Новый Структура;
	СтруктураПовтораЗаписи.Вставить("РеквизитыФискальнойОперацииКассовогоУзла");
	СтруктураПовтораЗаписи.Вставить("ОписаниеОповещения");
	СтруктураПовтораЗаписи.Вставить("ТекстСообщения");
	СтруктураПовтораЗаписи.Вставить("ВозвращатьРезультатФункции");
	СтруктураПовтораЗаписи.Вставить("РезультатПриУспешномПроведении");
	СтруктураПовтораЗаписи.Вставить("РезультатПриОтмене");
	СтруктураПовтораЗаписи.Вставить("ИмяПроцедуры");
	СтруктураПовтораЗаписи.Вставить("ПараметрыПроцедуры");
	СтруктураПовтораЗаписи.Вставить("РезультатОперации");
	
	Возврат СтруктураПовтораЗаписи;
	
КонецФункции

// Структура строки чека для ЕГАИС
// 
// Возвращаемое значение:
//  Структура - Структура строки чека для ЕГАИС.
//
Функция СтруктураСтрокиЧекаЕГАИС() Экспорт
	
	СтрокаТовара = Новый Структура;
	СтрокаТовара.Вставить("НомерСтроки");
	СтрокаТовара.Вставить("Наименование");
	СтрокаТовара.Вставить("ТипНоменклатуры");
	СтрокаТовара.Вставить("ПодакцизныйТовар");
	СтрокаТовара.Вставить("Номенклатура");
	СтрокаТовара.Вставить("Характеристика");
	СтрокаТовара.Вставить("Упаковка");
	СтрокаТовара.Вставить("Количество");
	СтрокаТовара.Вставить("КоличествоУпаковок");
	СтрокаТовара.Вставить("Цена");
	СтрокаТовара.Вставить("ПроцентАвтоматическойСкидки");
	СтрокаТовара.Вставить("СуммаАвтоматическойСкидки");
	СтрокаТовара.Вставить("ПроцентРучнойСкидки");
	СтрокаТовара.Вставить("СуммаРучнойСкидки");
	СтрокаТовара.Вставить("Сумма");
	СтрокаТовара.Вставить("СтавкаНДС");
	СтрокаТовара.Вставить("СуммаНДС");
	СтрокаТовара.Вставить("Штрихкод");
	
	СтрокаТовара.Вставить("НоменклатураЕГАИС");
	СтрокаТовара.Вставить("АлкогольнаяПродукция");
	СтрокаТовара.Вставить("АлкогольнаяПродукцияВоВскрытойТаре");
	СтрокаТовара.Вставить("Сопоставлено");
	СтрокаТовара.Вставить("КодАкцизнойМарки");
	СтрокаТовара.Вставить("МаркируемаяПродукция");
//	СтрокаТовара.Вставить("МаркируемаяАлкогольнаяПродукция");
	СтрокаТовара.Вставить("ВидПродукции");
	СтрокаТовара.Вставить("КодВидаАлкогольнойПродукции");
	СтрокаТовара.Вставить("Объем");
	СтрокаТовара.Вставить("Крепость");
	СтрокаТовара.Вставить("ИНН");
	СтрокаТовара.Вставить("КПП");
	СтрокаТовара.Вставить("ПроизводительИмпортерАлкогольнойПродукции");
	
	Возврат СтрокаТовара;
	
КонецФункции

// Структура данных документа для передачи параметра ДанныеДокумента в метод ПодключаемоеОборудованиеУТВызовСервера.ПараметрыФискализацииЧека()
// 
// Возвращаемое значение:
//  Структура - Структура параметра ДанныеДокумента.
//
Функция СтруктураДанныхДокументаДляПараметровФискализацииЧека() Экспорт
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("Заказы");
	СтруктураДанных.Вставить("Шапка");
	СтруктураДанных.Вставить("Товары");
	СтруктураДанных.Вставить("ДанныеДляМОТП");
	СтруктураДанных.Вставить("ДанныеДляЕГАИС");
	
	Возврат СтруктураДанных;
КонецФункции

#Область РаботаСКонтрактнойИнформациейФЗ54

// Установить активность элементов формы: ПолеПолученоНаличными, Телефон, Email.
// Активные элементы связаны с цифровой клавиатурой формы.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма (ФормаОплатыНаличными или ФормаСмешаннойОплаты).
//
Процедура УстановитьАктивностьЭлементов(Форма) Экспорт
	
	Если Форма.ТекущийЭлемент = Форма.Элементы.ПолеПолученоНаличными
		И Форма.АктивноеПоле <> Форма.Элементы.ПолеПолученоНаличными.Имя Тогда
		
		РозничныеПродажиКлиентСервер.УстановитьАктивныйЭлемент(Форма, Форма.Элементы.ПолеПолученоНаличными);
		
	КонецЕсли;
	
	Если Форма.ТекущийЭлемент = Форма.Элементы.Телефон
		И Форма.АктивноеПоле <> Форма.Элементы.Телефон.Имя Тогда
		
		РозничныеПродажиКлиентСервер.УстановитьАктивныйЭлемент(Форма, Форма.Элементы.Телефон);
		
	КонецЕсли;
	
	Если Форма.ТекущийЭлемент = Форма.Элементы.Email
		И Форма.АктивноеПоле <> Форма.Элементы.Email.Имя Тогда
		
		РозничныеПродажиКлиентСервер.УстановитьАктивныйЭлемент(Форма, Форма.Элементы.Email);
		
	КонецЕсли;
	
КонецПроцедуры

// Включить использование клавиатуры для элемента формы.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма (ФормаОплатыНаличными или ФормаСмешаннойОплаты).
//  Элемент - ПолеВвода - Элемент формы, для которого необходимо включить клавиатуру.
//
Процедура УстановитьАктивныйЭлемент(Форма, Элемент) Экспорт
	
	Форма.АктивноеПоле = Элемент.Имя;
	
	МассивЭлементов = Новый Массив;
	МассивЭлементов.Добавить(Форма.Элементы.ПолеПолученоНаличными);
	МассивЭлементов.Добавить(Форма.Элементы.Телефон);
	МассивЭлементов.Добавить(Форма.Элементы.Email);
	
	Для Каждого ЭлементФормы Из МассивЭлементов Цикл
		ЭлементФормы.ЦветФона = Новый Цвет;
	КонецЦикла;
	
	Форма.Элементы[Форма.АктивноеПоле].ЦветФона = Форма.ЦветФонаВыделенияПоля;
	
	Если Форма.АктивноеПоле = Форма.Элементы.Email.Имя Тогда
		Форма.Элементы.ГруппаСтраницаПраваяКлавиатура.Видимость = Ложь;
		Форма.Элементы.ГруппаСтраницаПрочее.Видимость = Ложь;
		Форма.Элементы.ГруппаСтраницы.ТекущаяСтраница = Форма.Элементы.ГруппаРежимКлавиатуры;
		Форма.ТекущийЭлемент = Форма.Элементы.Email;
	Иначе
		Форма.Элементы.ГруппаСтраницаПраваяКлавиатура.Видимость = Истина;
		Форма.Элементы.ГруппаСтраницаПрочее.Видимость = Истина;
		Форма.Элементы.ГруппаСтраницы.ТекущаяСтраница = Форма.Элементы.ГруппаСтраницаПраваяКлавиатура;
	КонецЕсли;
	
КонецПроцедуры

// Процедура - Установить вариант отправки электронного чека.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма (ФормаОплатыНаличными или ФормаСмешаннойОплаты).
//  ВариантОтправкиЭлектронногоЧека - ПеречислениеСсылка.ВариантыОтправкиЭлектронногоЧекаПокупателю - Вариант отправки
//                                                                                                    электронного чека.
//
Процедура УстановитьВариантОтправкиЭлектронногоЧека(Форма, ВариантОтправкиЭлектронногоЧека) Экспорт
	
	Если Форма.ВариантОтправкиЭлектронногоЧека = ВариантОтправкиЭлектронногоЧека Тогда
		Возврат;
	КонецЕсли;
	
	Форма.ВариантОтправкиЭлектронногоЧека = ВариантОтправкиЭлектронногоЧека;
	
КонецПроцедуры

// Отформатировать номер телефона. Из номера вида 9999999999 формирует +7(999)999-99-99.
//
// Параметры:
//  Телефон - Строка - Номер телефона.
// 
// Возвращаемое значение:
//  Строка - Отформатированный номер телефона.
//
Функция ОтформатироватьНомерТелефона(Телефон) Экспорт
	
	Если ЗначениеЗаполнено(Телефон) Тогда
		
		Если СтрДлина(Телефон) > 3 Тогда;
			Буфер = Сред(Телефон, 4, СтрДлина(Телефон) - 3);
		Иначе
			Буфер = "";
		КонецЕсли;
		
		Возврат "+7(" + Лев(Телефон, 3) + ")" + Буфер;
		
	Иначе
		
		Возврат "+7";
		
	КонецЕсли;
	
КонецФункции

// Получить 10 знаков номера телефона по переданному представлению.
//
// Параметры:
//  Телефон - Строка - Представление номера телефона.
// 
// Возвращаемое значение:
//  Строка - 10 знаков номера телефона.
//
Функция НомерТелефонаВФормате10Знаков(Телефон) Экспорт
	
	Телефон10Знаков = "";
	Числа = "0123456789";
	
	ДлинаПредставленияТелефона = СтрДлина(Телефон);
	Для Индекс = 1 По ДлинаПредставленияТелефона Цикл
		
		Символ = Сред(Телефон, Индекс, 1);
		Если СтрНайти(Числа, Символ) > 0 Тогда
			Телефон10Знаков = Телефон10Знаков + Символ;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Прав(Телефон10Знаков, 10);
	
КонецФункции

// Возвращает пустую структура результата оплаты чека ККМ.
// 
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * ПолученоНаличными - Число - Сумма оплаты наличными.
//   * ДанныеЭлектронногоЧека - Структура - Данные электронного чека, см. функцию СтруктураДанныеЭлектронногоЧека().
//
Функция СтруктураРезультатаОплаты() Экспорт
	
	РезультатОплаты = Новый Структура;
	РезультатОплаты.Вставить("ПолученоНаличными",      0);
	РезультатОплаты.Вставить("ДанныеЭлектронногоЧека", Неопределено);
	
	Возврат РезультатОплаты;
	
КонецФункции

// Возвращает пустую структура данных электронного чека ККМ.
// 
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * Партнер - СправочникСсылка.Партнеры - Партнер.
//   * ВариантОтправкиЭлектронногоЧека - ПеречислениеСсылка.ВариантыОтправкиЭлектронногоЧекаПокупателю - Вариант
//       отправки электронного чека.
//   * КонтактныеДанныеЭлектронногоЧека - Строка - Телефон или адрес электронной почты.
//
Функция СтруктураДанныеЭлектронногоЧека() Экспорт
	
	ДанныеЭлектронногоЧека = Новый Структура;
	ДанныеЭлектронногоЧека.Вставить("Партнер",                          Справочники.Контрагенты.ПустаяСсылка());
	ДанныеЭлектронногоЧека.Вставить("ВариантОтправкиЭлектронногоЧека",  ПредопределенноеЗначение("Перечисление.ВариантыОтправкиЭлектронногоЧекаПокупателю.НеОтправлять"));
	ДанныеЭлектронногоЧека.Вставить("КонтактныеДанныеЭлектронногоЧека", "");
	
	Возврат ДанныеЭлектронногоЧека;
	
КонецФункции

// Получить данные электронного чека.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма (ФормаОплатыНаличными или ФормаСмешаннойОплаты).
// 
// Возвращаемое значение:
//  Структура - Данные электронного чека, см. функцию СтруктураДанныеЭлектронногоЧека().
//
Функция ДанныеЭлектронногоЧека(Форма) Экспорт
	
	ДанныеЭлектронногоЧека = СтруктураДанныеЭлектронногоЧека();
	
	КонтактныеДанныеЭлектронногоЧека = "";
	Если Форма.ВариантОтправкиЭлектронногоЧека = ПредопределенноеЗначение("Перечисление.ВариантыОтправкиЭлектронногоЧекаПокупателю.ОтправитьEmail") Тогда
		
		КонтактныеДанныеЭлектронногоЧека = Форма.Email;
		
	КонецЕсли;
	
	ДанныеЭлектронногоЧека.ВариантОтправкиЭлектронногоЧека  = Форма.ВариантОтправкиЭлектронногоЧека;
	ДанныеЭлектронногоЧека.КонтактныеДанныеЭлектронногоЧека = КонтактныеДанныеЭлектронногоЧека;
	
	Возврат ДанныеЭлектронногоЧека;
	
КонецФункции

// Проверить необходимость обработки данных электронного чека на сервере.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма (ФормаОплатыНаличными или ФормаСмешаннойОплаты).
// 
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * ТребуетсяОбновитьКонтактнуюИнформацию - Булево - Требуется обновить контактную информацию.
//   * ТребуетсяСоздатьПартнера - Булево - Требуется создать партнера.
//
Функция ПроверитьНеобходимостьОбработкиДанныхЭлектронногоЧека(Форма) Экспорт
	
	Возврат Истина;
	
КонецФункции

// Функция выполняет приведение строки к числу.
//
// Параметры:
//  ЧислоСтрокой           - Строка - Строка приводимая к числу.
//  ВозвращатьНеопределено - Булево - Если Истина и строка содержит некорректное значение, то возвращать Неопределено.
//
// Возвращаемое значение:
//  Число - Приведенное значение.
//
Функция ПривестиСтрокуКЧислу(ЧислоСтрокой) Экспорт
	
	ОписаниеТипаЧисла = Новый ОписаниеТипов("Число");
	ЗначениеЧисла = ОписаниеТипаЧисла.ПривестиЗначение(ЧислоСтрокой);
	
	Возврат ЗначениеЧисла;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти
