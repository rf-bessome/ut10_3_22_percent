#Область СлужебныйПрограммныйИнтерфейс

Процедура ПослеЗаписи(Форма, ПараметрыЗаписи) Экспорт
	
	//++ НЕ ГОСИС
	
	//-- НЕ ГОСИС
	
	Возврат;
	
КонецПроцедуры

#Область СобытияЭлементовФорм

// Переопределяемая процедура, вызываемая из одноименного обработчика события элемента.
//
// Параметры:
//   Форма                   - ФормаКлиентскогоПриложения - форма, из которой происходит вызов процедуры.
//   Элемент                 - Произвольный     - элемент-источник события "При изменении"
//   ДополнительныеПараметры - Структура        - значения дополнительных параметров влияющих на обработку.
//
Процедура ПриИзмененииЭлемента(Форма, Элемент, ДополнительныеПараметры) Экспорт
	
	//++ НЕ ГОСИС
	
	//-- НЕ ГОСИС
	
	Возврат;
	
КонецПроцедуры

// Переопределяемая процедура, вызываемая из одноименного обработчика события элемента.
//
Процедура ПриВыбореЭлемента(Форма, Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка, ДополнительныеПараметры = Неопределено) Экспорт
	
	//++ НЕ ГОСИС
	
	//-- НЕ ГОСИС
	
	Возврат;
	
КонецПроцедуры

// Переопределяемая процедура, вызываемая из одноименного обработчика события элемента.
//
Процедура ПриАктивизацииЯчейки(Форма, Элемент, ДополнительныеПараметры) Экспорт
	
	//++ НЕ ГОСИС
	
	//-- НЕ ГОСИС
	
	Возврат;
	
КонецПроцедуры

// Переопределяемая процедура, вызываемая из одноименного обработчика события элемента.
//
Процедура ПриНачалеРедактирования(Форма, Элемент, НоваяСтрока, Копирование, ДополнительныеПараметры) Экспорт
	
	//++ НЕ ГОСИС
	
	//-- НЕ ГОСИС
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область Номенклатура

// Выполняется при обработке выбора. Требуется выделить и обработать событие выбора номенклатуры.
//
// Параметры:
//  ОповещениеПриЗавершении - ОписаниеОповещения - Метод формы, который обрабатывает событие выбора.
//  ВыбранноеЗначение       - ОпределяемыйТип.Номенклатура - Результат выбора.
//  ИсточникВыбора          - ФормаКлиентскогоПриложения - Форма, в которой произведен выбор.
Процедура ОбработкаВыбораНоменклатуры(ОповещениеПриЗавершении, ВыбранноеЗначение, ИсточникВыбора) Экспорт
	
	Если ТипЗнч(ОповещениеПриЗавершении.Модуль) = Тип("ФормаКлиентскогоПриложения") Тогда
		ОповещениеПриЗавершении.Модуль.Модифицированность = Истина;
	КонецЕсли;

	Если ТипЗнч(ИсточникВыбора) = Тип("ФормаКлиентскогоПриложения") Тогда
		Если СтрНачинаетсяС(ИсточникВыбора.ИмяФормы, "Справочник.Номенклатура") Тогда
			ВыполнитьОбработкуОповещения(ОповещениеПриЗавершении, ВыбранноеЗначение);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Выполняет действия при изменении номенклатуры в строке таблицы формы.
//
// Параметры:
//  Форма                  - ФормаКлиентскогоПриложения - форма, в которой произошло событие,
//  ТекущаяСтрока          - ДанныеФормыЭлементКоллекции - текущие данные редактируемой строки таблицы товаров,
//  КэшированныеЗначения   - Структура - сохраненные значения параметров, используемых при обработке,
//  ПараметрыУказанияСерий - ФиксированнаяСтруктура - параметры указаний серий формы
Процедура ПриИзмененииНоменклатуры(Форма, ТекущаяСтрока, КэшированныеЗначения, ПараметрыУказанияСерий = Неопределено) Экспорт
	
	СтруктураДействий = Новый Структура;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "Характеристика") Тогда
		СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу",
			ТекущаяСтрока.Характеристика);
			
		СтруктураДействий.Вставить("ЗаполнитьПризнакХарактеристикиИспользуются",
			Новый Структура("Номенклатура", "ХарактеристикиИспользуются"));
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "Серия")
			И ПараметрыУказанияСерий <> Неопределено Тогда
		СтруктураДействий.Вставить("ЗаполнитьПризнакТипНоменклатуры",
			Новый Структура("Номенклатура", "ТипНоменклатуры"));
			
		ИсточникЗначенийВФорме = Форма;
		ПараметрыУказанияСерийКопия = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(ПараметрыУказанияСерий, Ложь);
			
		ИмяТЧ = "Товары";
		Если ПараметрыУказанияСерий <> Неопределено Тогда
			
			Если ПараметрыУказанияСерийКопия.Свойство("Сырье") Тогда
				ПараметрыУказанияСерийКопия = ПараметрыУказанияСерийКопия.Сырье;
			КонецЕсли;
			
			Если ПараметрыУказанияСерийКопия.ИмяИсточникаЗначенийВФормеОбъекта = "ТекущиеДанные" Тогда
				ИсточникЗначенийВФорме = ТекущаяСтрока;
			ИначеЕсли ЗначениеЗаполнено(ПараметрыУказанияСерийКопия.ИмяИсточникаЗначенийВФормеОбъекта) Тогда
				ИсточникЗначенийВФорме = Форма[ПараметрыУказанияСерийКопия.ИмяИсточникаЗначенийВФормеОбъекта];
			КонецЕсли;
		КонецЕсли;
	
		Склад = Неопределено;
		СтруктураДействий.Вставить("ПроверитьСериюРассчитатьСтатус",
			Новый Структура("ПараметрыУказанияСерий, Склад", ПараметрыУказанияСерийКопия, Склад));
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "ОКПД2") Тогда
		СтруктураДействий.Вставить("ЗаполнитьКодОКПД2", Новый Структура("Номенклатура", "ОКПД2"));
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "ОКПД2Представление") Тогда
		СтруктураДействий.Вставить("ЗаполнитьПредставлениеОКПД2", Новый Структура("Номенклатура", "ОКПД2Представление"));
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "ЕдиницаИзмерения") Тогда
		СтруктураДействий.Вставить("ЗаполнитьПризнакЕдиницаИзмерения", Новый Структура("Номенклатура", "ЕдиницаИзмерения"));
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "КодТНВЭД") Тогда
		СтруктураДействий.Вставить("ЗаполнитьКодТНВЭД");
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "КоличествоЗЕРНО")
			И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "Количество") Тогда
		СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиницПоЗЕРНО");
	КонецЕсли;
	
	ОбработкаТабличнойЧастиСерверИСУТ.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

// Открывает форму подбора номенклатуры.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой вызывается команда открытия обработки подбора,
//  ОповещениеПриЗавершении - ОписаниеОповещения - процедура, вызываемая после закрытия формы подбора.
Процедура ОткрытьФормуПодбораНоменклатуры(Форма, ОповещениеПриЗавершении = Неопределено) Экспорт
	
	ПараметрЗаголовок = НСтр("ru = 'Подбор товаров в %Документ%'");
	Если ЗначениеЗаполнено(Форма.Объект.Ссылка) Тогда
		ПараметрЗаголовок = СтрЗаменить(ПараметрЗаголовок, "%Документ%", Форма.Объект.Ссылка);
	Иначе
		ТипОбъектаФормыПодбора = ТипЗнч(Форма.Объект.Ссылка);
		Если ТипОбъектаФормыПодбора = Тип("ДокументСсылка.ВнесениеСведенийОСобранномУрожаеЗЕРНО") Тогда
			ПараметрЗаголовок = СтрЗаменить(ПараметрЗаголовок, "%Документ%", НСтр("ru='Внесение сведений о собранном урожае ФГИС ""Зерно""'"));
		ИначеЕсли ТипОбъектаФормыПодбора = Тип("ДокументСсылка.ОформлениеСДИЗЗЕРНО") Тогда
			ПараметрЗаголовок = СтрЗаменить(ПараметрЗаголовок, "%Документ%", НСтр("ru='оформление СДИЗ ФГИС ""Зерно""'"));
		ИначеЕсли ТипОбъектаФормыПодбора = Тип("ДокументСсылка.СписаниеПартийЗЕРНО") Тогда
			ПараметрЗаголовок = СтрЗаменить(ПараметрЗаголовок, "%Документ%", НСтр("ru='списание партий ФГИС ""Зерно""'"));
		ИначеЕсли ТипОбъектаФормыПодбора = Тип("ДокументСсылка.ФормированиеПартийЗЕРНО") Тогда
			ПараметрЗаголовок = СтрЗаменить(ПараметрЗаголовок, "%Документ%", НСтр("ru='формирование партий ФГИС ""Зерно""'"));
		ИначеЕсли ТипОбъектаФормыПодбора = Тип("ДокументСсылка.ФормированиеПартийПриПроизводствеЗЕРНО") Тогда
			ПараметрЗаголовок = СтрЗаменить(ПараметрЗаголовок, "%Документ%", НСтр("ru='формирование партий при производстве ФГИС ""Зерно""'"));
		КонецЕсли;
	КонецЕсли;
	
	СписокЗапросов = Новый СписокЗначений();
	СписокЗапросов.Добавить( , "По справочнику");
	
	СтруктураПараметровПодбора = Новый Структура();
	СтруктураПараметровПодбора.Вставить("Команда"                , "ПодборВТабличнуюЧастьТовары");
	СтруктураПараметровПодбора.Вставить("СписокВидовПодбора"     , СписокЗапросов);
	СтруктураПараметровПодбора.Вставить("ЕстьЦена"               , Ложь);
	СтруктураПараметровПодбора.Вставить("РассчитыватьЦены"       , Ложь);
	СтруктураПараметровПодбора.Вставить("ЕстьСерия"              , Ложь);
	СтруктураПараметровПодбора.Вставить("ВалютаДокумента"        , Неопределено);
	СтруктураПараметровПодбора.Вставить("РазворачиватьДоКачества", Ложь);
	СтруктураПараметровПодбора.Вставить("Заголовок"              , ПараметрЗаголовок);
	СтруктураПараметровПодбора.Вставить("ДатаРасчетов"           , Неопределено);
	СтруктураПараметровПодбора.Вставить("ЗапрашиватьКоличество"  , Ложь);
	
	РаботаСДиалогами.ОткрытьПодборНоменклатуры(Форма, СтруктураПараметровПодбора, Форма.Объект.Ссылка.Метаданные());

КонецПроцедуры

// Обрабатывает результат выбора в форму документа ЗЕРНО (например из формы подбора номенклатуры,
//   при использовании множественного выбора вместо закрытия формы подбора с общим результатом).
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой вызывается команда открытия обработки сопоставления,
//  ВыбранноеЗначение - Произвольный - результат выбора.
//  ИсточникВыбора    - ФормаКлиентскогоПриложения - форма, в которой произведен выбор.
Процедура ОбработкаВыбора(Форма, ВыбранноеЗначение, ИсточникВыбора) Экспорт
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ИсточникВыбора, "ОбработкаОбъект")
		И ТипЗнч(ИсточникВыбора.ОбработкаОбъект) = Тип("ОбработкаОбъект.ПодборНоменклатуры") 
		И ЗначениеЗаполнено(ВыбранноеЗначение.Номенклатура)Тогда 
		
		Если Форма.Объект.ВидПродукции <> ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыбранноеЗначение.Номенклатура,"ВидПродукцииИС") Тогда 
			Возврат;
		КонецЕсли;
			
		Если Форма.ИмяФормы = "Документ.ПогашениеСДИЗЗЕРНО.Форма.ФормаДокумента" Тогда
			КлючПоиска = Новый Структура("Номенклатура,Характеристика, Серия");
		Иначе 
			КлючПоиска = Новый Структура("Номенклатура,Характеристика");
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(КлючПоиска,ВыбранноеЗначение);
		СтрокиТовары = Форма.Объект.Товары.НайтиСтроки(КлючПоиска);
		Если СтрокиТовары.Количество() Тогда 
			Редактируем = СтрокиТовары[0];
		Иначе 
			Редактируем = Форма.Объект.Товары.Добавить();
			ЗаполнитьЗначенияСвойств(Редактируем, КлючПоиска);
			ПриИзмененииНоменклатуры(Форма, Редактируем, Новый Структура, Форма.ПараметрыУказанияСерий);
		КонецЕсли;
		ПриИзмененииКоличества(Форма, Редактируем, Неопределено, Форма.ПараметрыУказанияСерий);
	КонецЕсли;
	
	//Указание адреса
	Если Форма.ИмяФормы = "Документ.ВнесениеСведенийОСобранномУрожаеЗЕРНО.Форма.ФормаДокумента"
		И Форма.ТекущийЭлемент.Имя = "МестоХранения" Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Элемент",            Форма.ТекущийЭлемент);
		ДополнительныеПараметры.Вставить("ДополнительныеПоля", Неопределено);
		ДополнительныеПараметры.Вставить("ПутьКДаннымАдреса",  "");
		Форма.ПолеАдресаОкончаниеВыбора(ВыбранноеЗначение, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ХарактеристикиНоменклатуры

// Выполняется при начале выбора характеристики. Требуется определить и открыть форму выбора.
//
// Параметры:
//  Владелец     - ФормаКлиентскогоПриложения            - форма, в которой вызывается команда выбора характеристики.
//  ДанныеСтроки - ДанныеФормыЭлементКоллекции - текущие данные строки таблицы товаров откуда производится выбор.
//  СтандартнаяОбработка - Булево - Выключается в переопределении
//  ИмяКолонкиНоменклатура - Строка - имя колонки с данными номенклатуры.
//  Описание - ОписаниеОповещения - Вызывается при выборе значения в форме выбора.
//
Процедура ПриНачалеВыбораХарактеристики(
	Владелец, ДанныеСтроки, СтандартнаяОбработка, ИмяКолонкиНоменклатура="Номенклатура", Описание=Неопределено) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ФормаВыбора = Справочники.ХарактеристикиНоменклатуры.ПолучитьФормуВыбора("ФормаВыбораСРасширеннымОтбором", Владелец);
	ФормаВыбора.ПараметрВыборПоВладельцу = ДанныеСтроки.Номенклатура;
	
	ФормаВыбора.Открыть();
	
КонецПроцедуры

// Выполняет действия при изменении характеристики номенклатуры в строке таблицы формы.
//
// Параметры:
//  Форма                - ФормаКлиентскогоПриложения - форма, в которой произошло событие,
//  ТекущаяСтрока        - ДанныеФормыЭлементКоллекции - текущие данные редактируемой строки таблицы товаров,
//  КэшированныеЗначения - Структура - сохраненные значения параметров, используемых при обработке,
Процедура ПриИзмененииХарактеристики(Форма, ТекущаяСтрока, КэшированныеЗначения) Экспорт
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область Серии

// Выполняет действия при изменении серии номенклатуры в строке таблицы формы.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой произошло событие,
//  ТекущаяСтрока - ДанныеФормыЭлементКоллекции - строка таблицы товаров,
//  КэшированныеЗначения - Структура - сохраненные значения параметров, используемых при обработке,
//  ПараметрыУказанияСерий - ФиксированнаяСтруктура - параметры указаний серий формы
Процедура ПриИзмененииСерии(Форма, ТекущаяСтрока, КэшированныеЗначения, ПараметрыУказанияСерий) Экспорт
	
	ВыбранноеЗначение = НоменклатураКлиентСервер.ВыбраннаяСерия();
	
	ВыбранноеЗначение.Значение                   = ТекущаяСтрока.Серия;
	Попытка
		ВыбранноеЗначение.ИдентификаторТекущейСтроки = ТекущаяСтрока.ПолучитьИдентификатор();
		НоменклатураКлиент.ОбработатьУказаниеСерии(Форма, ПараметрыУказанияСерий, ВыбранноеЗначение);
	Исключение
	КонецПопытки;
	
КонецПроцедуры

// Выполняется при обработке выбора. Требуется выделить и обработать событие выбора серии.
// 
// Параметры:
//  Форма                  - ФормаКлиентскогоПриложения - Форма для которой требуется обработать событие выбора.
//  ВыбранноеЗначение      - ОпределяемыйТип.СерияНоменклатуры - результат выбора.
//  ИсточникВыбора         - ФормаКлиентскогоПриложения - Форма, в которой произведен выбор.
//  ПараметрыУказанияСерий - ФиксированнаяСтруктура - параметры указаний серий формы.
//  ТекущиеДанные          - Произвольный - Контекст выбора серии
Процедура ОбработкаВыбораСерии(Форма, ВыбранноеЗначение, ИсточникВыбора, ПараметрыУказанияСерий, ТекущиеДанные = Неопределено) Экспорт
	
	Если НоменклатураКлиент.ЭтоУказаниеСерий(ИсточникВыбора) Тогда
		Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
			ВыбраннаяСерия = ВыбранноеЗначение;
		Иначе
			ВыбраннаяСерия = НоменклатураКлиентСервер.ВыбраннаяСерия();
			ВыбраннаяСерия.Значение                   = ВыбранноеЗначение;
			Если ПараметрыУказанияСерий.ИмяТЧТовары <> "" Тогда
				ВыбраннаяСерия.ИдентификаторТекущейСтроки = Форма.Элементы[ПараметрыУказанияСерий.ИмяТЧТовары].ТекущиеДанные.ПолучитьИдентификатор();
			КонецЕсли;
		КонецЕсли;
		
		НоменклатураКлиент.ОбработатьУказаниеСерии(Форма, ПараметрыУказанияСерий, ВыбраннаяСерия);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Количество

// Выполняет действия при изменении подобранного количества (поле Количество) в строке таблицы формы.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой произошло событие,
//  ТекущаяСтрока - ДанныеФормыЭлементКоллекции - строка таблицы товаров,
//  ЕдиницаИзмеренияКилограмм - ОпределяемыйТип.Упаковка - единица измерения по классификатору "Килограмм",
//  КэшированныеЗначения - Структура - сохраненные значения параметров, используемых при обработке.
Процедура ПриИзмененииКоличества(Форма, ТекущаяСтрока, ЕдиницаИзмеренияКилограмм, КэшированныеЗначения) Экспорт
	
	Если Не ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда
		Возврат;
	КонецЕсли;
	
	Строки = Новый Массив;
	Строки.Добавить(ТекущаяСтрока);
	
	Коэффициент = ИнтеграцияЗЕРНОУТ.КоэффициентыЕдиницИзмеренияЗЕРНО(Строки)[ТекущаяСтрока.Номенклатура].Коэффициент;
	Если Не ЗначениеЗаполнено(Коэффициент) Тогда
		Коэффициент = 1;
	КонецЕсли;
	
	ТекущаяСтрока.КоличествоЗЕРНО = ТекущаяСтрока.Количество / Коэффициент;
	
КонецПроцедуры

// Выполняет действия при изменении количества в килограммах (поле КоличествоЗЕРНО) в строке таблицы формы.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой произошло событие,
//  ТекущаяСтрока - ДанныеФормыЭлементКоллекции - строка таблицы товаров,
//  ЕдиницаИзмеренияКилограмм - ОпределяемыйТип.Упаковка - единица измерения по классификатору "Килограмм",
//  КэшированныеЗначения - Структура - сохраненные значения параметров, используемых при обработке.
Процедура ПриИзмененииКоличестваВКилограммах(Форма, ТекущаяСтрока, ЕдиницаИзмеренияКилограмм, КэшированныеЗначения) Экспорт
	
	Если Не ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда
		Коэффициент = 0;
	Иначе                                                               
		МассивСтроЗЕРНО = Новый Массив;
		МассивСтроЗЕРНО.Добавить(ТекущаяСтрока);
		Коэффициент = ИнтеграцияЗЕРНОУТ.КоэффициентыЕдиницИзмеренияЗЕРНО(МассивСтроЗЕРНО)[ТекущаяСтрока.Номенклатура].Коэффициент;
	КонецЕсли;
	
	ТекущаяСтрока.Количество = ТекущаяСтрока.КоличествоЗЕРНО * Коэффициент;
	
КонецПроцедуры

#КонецОбласти

// Выполняется при начале выбора договора ИС. Требуется определить и открыть форму выбора.
//
// Параметры:
//  Владелец     - ФормаКлиентскогоПриложения  - форма, в которой вызывается команда выбора договора.
//  ДанныеДоговора - Структура - данные договора хранения ЗЕРНО, для которого выбирается прикладной договор:
//  	Договор - ОпределеяемыйТип.ДоговорКонтрагентаИС - текущий договор.
//  	Контрагент - ОпределяемыйТип.ОрганизацияКонтрагентГосИС - организацияя/контрагент с которым заключен договор.
//  	Организация - ОпределеяемыйТип.Организация - организация владелец договора.
//  СтандартнаяОбработка - Булево - Выключается в переопределении
//  ОповещениеПриЗавершении - ОписаниеОповещения - Вызывается при выборе значения в форме выбора.
//
Процедура ПриНачалеВыбораДоговораОрганизацииКонтрагента(Владелец, ДанныеДоговора, СтандартнаяОбработка, ОповещениеПриЗавершении) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("ТекущаяСтрока", ДанныеДоговора.Договор);
	ПараметрыФормы.Вставить("Владелец",      ДанныеДоговора.Контрагент);
	ПараметрыФормы.Вставить("Организация",   ДанныеДоговора.Организация);
	ПараметрыФормы.Вставить("РежимВыбора",   Истина);
	
	ОткрытьФорму("Справочник.ДоговорыКонтрагентов.Форма.ФормаВыбора", ПараметрыФормы, Владелец, Владелец,,, ОповещениеПриЗавершении);
	
КонецПроцедуры

#КонецОбласти
