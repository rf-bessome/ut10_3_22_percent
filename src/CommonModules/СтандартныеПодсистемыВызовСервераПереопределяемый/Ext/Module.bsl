// Возвращает структуру параметров, необходимых для работы клиентского кода
// конфигурации. 
//
// Не предназначена для прямого вызова из клиентского кода. 
// Вместо нее следует использовать одноименную функцию из 
// модуля СтандартныеПодсистемыКлиентПовтИсп.
//
// Реализация:
//   Для установки параметров работы клиента можно использовать шаблон:
//
//     Параметры.Вставить(<ИмяПараметра>, <код получения значения параметра>);
//
// Возвращаемое значение:
//   Структура - структура параметров работы клиента.
//
Функция ПараметрыРаботыКлиента() Экспорт
	
	Параметры = Новый Структура();
	
	ПользователюРазрешенЗапускКонфигурации = УправлениеПользователямиСервер.ПользователюРазрешенЗапускКонфигурации();
	Параметры.Вставить("ПользователюРазрешенЗапускКонфигурации", ПользователюРазрешенЗапускКонфигурации);

	
	Если НЕ ПользователюРазрешенЗапускКонфигурации Тогда
		// Дальше нет необходимости выполнения, т.к. могут быть ошибки нарушения прав
		Возврат Параметры;
	КонецЕсли; 
	
	Параметры.Вставить("ТекущийПользователь", Строка(ПараметрыСеанса.ТекущийПользователь));
	
	Параметры.Вставить("ТекущийПользователь", Строка(ПараметрыСеанса.ТекущийПользователь));
	
	// СтандартныеПодсистемы
	ДобавитьПараметрыРаботыКлиентскойЛогикиСтандартныхПодсистем(Параметры);                
	ОбщегоНазначенияПереопределяемый.ПриДобавленииПараметровРаботыКлиента(Параметры);
	ИнтеграцияИС.ПриДобавленииПараметровРаботыКлиента(Параметры);

	// Конец СтандартныеПодсистемы
	
	Возврат Новый ФиксированнаяСтруктура(Параметры);
	
КонецФункции
//	ОбщегоНазначенияПереопределяемый.ПриДобавленииПараметровРаботыКлиента(СвойстваКлиента);

// Заполняет структуру параметров, необходимых для работы клиентского кода
// конфигурации. 
//
// Параметры:
//   Параметры   - Структура - структура параметров.
//
Процедура ДобавитьПараметрыРаботыКлиентскойЛогикиСтандартныхПодсистем(Параметры) 
	
	// БазоваяФункциональность
	СтандартныеПодсистемыСервер.ДобавитьПараметрыРаботыКлиента(Параметры);
	// Конец БазоваяФункциональность
	
	// ЗавершениеРаботыПользователей
	Параметры.Вставить("ПараметрыБлокировкиСеансов", Новый ФиксированнаяСтруктура(СоединенияИБ.ПараметрыБлокировкиСеансов()));
	// Конец ЗавершениеРаботыПользователей
	
	// ОбновлениеИнформационнойБазы
	Параметры.Вставить("ПервыйЗапуск", ОбновлениеИнформационнойБазы.ПервыйЗапускИнформационнойБазы());
	// Конец ОбновлениеИнформационнойБазы
	
	// ОбновлениеКонфигурации
	Параметры.Вставить("НастройкиОбновления", 
		Новый ФиксированнаяСтруктура(ОбновлениеКонфигурации.ПолучитьНастройкиОбновления()));
	// Конец ОбновлениеКонфигурации
	
	// ПроверкаЛегальностиПолученияОбновлений
	Параметры.Вставить("ЭтоГлавныйУзел", ПланыОбмена.ГлавныйУзел() = Неопределено);
	// Конец ПроверкаЛегальностиПолученияОбновлений
	
	// ПолучениеФайловИзИнтернета
	Параметры.Вставить("НастройкиПроксиСервера", ПолучениеФайловИзИнтернета.ПолучитьНастройкуПроксиСервера());
	// Конец ПолучениеФайловИзИнтернета
	
	// ФайловыеФункции
	Параметры.Вставить("ПерсональныеНастройкиРаботыСФайлами",
		Новый ФиксированнаяСтруктура(ФайловыеФункцииПереопределяемый.ПолучитьПерсональныеНастройкиРаботыСФайлами()));
	// Конец ФайловыеФункции
	
	// ЭлектроннаяЦифроваяПодпись
	Параметры.Вставить("ПерсональныеНастройкиРаботыСЭЦП",
		Новый ФиксированнаяСтруктура(ЭлектроннаяЦифроваяПодпись.ПолучитьПерсональныеНастройкиРаботыСЭЦПСервер()));
	// Конец ЭлектроннаяЦифроваяПодпись
	
	// Замена: ПользователиСлужебный.ПриДобавленииПараметровРаботыКлиента(Параметры)
	Параметры.Вставить("ЭтоПолноправныйПользователь", Пользователи.ЭтоПолноправныйПользователь());
	Параметры.Вставить("ЭтоАдминистраторСистемы", Пользователи.ЭтоПолноправныйПользователь(, Истина));
	
	СвойстваКлиента = Новый Структура;
	СвойстваКлиента.Вставить("ТекущаяДатаНаКлиенте", ТекущаяДатаСеанса());
	СвойстваКлиента.Вставить("ТекущаяУниверсальнаяДатаВМиллисекундахНаКлиенте", ТекущаяУниверсальнаяДатаВМиллисекундах());
	ДобавитьПоправкиКВремени(Параметры, СвойстваКлиента);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ЭлектроннаяПодпись") Тогда
		МодульЭлектроннаяПодписьСлужебный = ОбщегоНазначения.ОбщийМодуль("ЭлектроннаяПодписьСлужебный");
		МодульЭлектроннаяПодписьСлужебный.ПриДобавленииПараметровРаботыКлиента(Параметры);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьПоправкиКВремени(Параметры, СвойстваКлиента)
	
	ДатаСеанса = ТекущаяДатаСеанса();
	ДатаСеансаУниверсальная = УниверсальноеВремя(ДатаСеанса, ЧасовойПоясСеанса());
	
	Параметры.Вставить("ПоправкаКВремениСеанса", ДатаСеанса - СвойстваКлиента.ТекущаяДатаНаКлиенте);
	Параметры.Вставить("ПоправкаКУниверсальномуВремени", ДатаСеансаУниверсальная - ДатаСеанса);
	Параметры.Вставить("СмещениеСтандартногоВремени", СмещениеСтандартногоВремени(ЧасовойПоясСеанса()));
	Параметры.Вставить("СмещениеДатыКлиента", ТекущаяУниверсальнаяДатаВМиллисекундах()
		- СвойстваКлиента.ТекущаяУниверсальнаяДатаВМиллисекундахНаКлиенте);
	
КонецПроцедуры
