

// Параметры фискализации чеков для документа КорректировкаРеализации


// Типизируем колонки таблицы МаркированныеТовары для передачи в запрос
// и добавляем колонку НомерСтроки
Процедура ТипизироватьКолонкиМаркированныхТоваров(ТаблицаМаркированныхТоваров)
	
	ТаблицаМаркированныхТоваров.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"));
	ТаблицаМаркированныхТоваров.Колонки.Добавить("НоменклатураСсылка", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаМаркированныхТоваров.Колонки.Добавить("ХарактеристикаСсылка", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ТаблицаМаркированныхТоваров.Колонки.Добавить("СерияСсылка", Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	ТаблицаМаркированныхТоваров.Колонки.Добавить("ШтрихкодСтрока", Новый ОписаниеТипов("Строка"));
	ТаблицаМаркированныхТоваров.Колонки.Добавить("ВидУпаковкиСсылка", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыУпаковокИС"));
	
	Для Каждого СтрокаШтрихкодыУпаковок Из ТаблицаМаркированныхТоваров Цикл
		СтрокаШтрихкодыУпаковок.НомерСтроки = ТаблицаМаркированныхТоваров.Индекс(СтрокаШтрихкодыУпаковок);
		СтрокаШтрихкодыУпаковок.НоменклатураСсылка = СтрокаШтрихкодыУпаковок.Номенклатура;
		СтрокаШтрихкодыУпаковок.ХарактеристикаСсылка = СтрокаШтрихкодыУпаковок.Характеристика;
		СтрокаШтрихкодыУпаковок.СерияСсылка = СтрокаШтрихкодыУпаковок.Серия;
		СтрокаШтрихкодыУпаковок.ШтрихкодСтрока = СтрокаШтрихкодыУпаковок.ШтрихкодУпаковки.ЗначениеШтрихкода;
		СтрокаШтрихкодыУпаковок.ВидУпаковкиСсылка = СтрокаШтрихкодыУпаковок.ВидУпаковки;
	КонецЦикла;
	
	ТаблицаМаркированныхТоваров.Колонки.Удалить("Номенклатура");
	ТаблицаМаркированныхТоваров.Колонки.Удалить("Характеристика");
	ТаблицаМаркированныхТоваров.Колонки.Удалить("ШтрихкодУпаковки");
	ТаблицаМаркированныхТоваров.Колонки.Удалить("ВидУпаковки");
	
КонецПроцедуры
	
Функция ДанныеДляМОТП(ДокументСсылка, МенеджерВременныхТаблиц, ТекстЗапросаШтрихкодовУпаковок)
	
	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаШтрихкодовУпаковок;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	РезультатЗапроса = Запрос.Выполнить();
	МассивУпаковок = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("ШтрихкодУпаковки");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МаркированныеТовары.НомерСтроки КАК НомерСтроки,
	|	МаркированныеТовары.НоменклатураСсылка КАК Номенклатура,
	|	МаркированныеТовары.ХарактеристикаСсылка КАК Характеристика,
	|	МаркированныеТовары.СерияСсылка КАК Серия,
	|	МаркированныеТовары.ШтрихкодСтрока КАК Штрихкод,
	|	МаркированныеТовары.ВидУпаковкиСсылка
	|ПОМЕСТИТЬ ТаблицаМаркированныеТовары
	|ИЗ
	|	&МаркированныеТовары КАК МаркированныеТовары
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Упаковка КАК Упаковка,
	|	Товары.СуммаСкидки КАК СуммаСкидки,
	|	Товары.СуммаСНДС КАК СуммаСНДС,
	|	Товары.СуммаНДС КАК СуммаНДС,
	|	Товары.Количество,
	|	Товары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ЕСТЬNULL(МаркированныеТовары.Штрихкод, """") КАК Штрихкод,
	|	1 КАК МаркТоварыКоличество,
	|	ВЫБОР
	|		КОГДА НЕ МаркированныеТовары.Штрихкод ЕСТЬ NULL
	|				И Товары.Количество <> 1
	|			ТОГДА ВЫРАЗИТЬ(1 / 1 КАК ЧИСЛО(12, 3))
	|		ИНАЧЕ Товары.КоличествоУпаковок
	|	КОНЕЦ КАК МаркТоварыКоличествоУпаковок,
	|	ВЫБОР
	|		КОГДА НЕ МаркированныеТовары.Штрихкод ЕСТЬ NULL
	|				И Товары.Количество <> 1
	|			ТОГДА ВЫРАЗИТЬ(Товары.НомерСтроки * &КоличествоМаркированныхТоваров + МаркированныеТовары.НомерСтроки КАК ЧИСЛО(10, 0))
	|		ИНАЧЕ ВЫРАЗИТЬ(Товары.НомерСтроки * ВЫБОР
	|					КОГДА &КоличествоМаркированныхТоваров = 0
	|						ТОГДА 1
	|					ИНАЧЕ &КоличествоМаркированныхТоваров
	|				КОНЕЦ КАК ЧИСЛО(10, 0))
	|	КОНЕЦ КАК ИндексМаркированногоТовара,
	|	ВЫБОР
	|		КОГДА НЕ МаркированныеТовары.Штрихкод ЕСТЬ NULL
	|				И Товары.КоличествоУпаковок <> 1
	|			ТОГДА ВЫРАЗИТЬ(Товары.СуммаСкидки / Товары.Количество КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ Товары.СуммаСкидки
	|	КОНЕЦ КАК МаркТоварыСуммаСкидки,
	|	ВЫБОР
	|		КОГДА НЕ МаркированныеТовары.Штрихкод ЕСТЬ NULL
	|				И Товары.КоличествоУпаковок <> 1
	|			ТОГДА ВЫРАЗИТЬ(Товары.СуммаСНДС / Товары.Количество КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ Товары.СуммаСНДС
	|	КОНЕЦ КАК МаркТоварыСуммаСНДС,
	|	ВЫБОР
	|		КОГДА НЕ МаркированныеТовары.Штрихкод ЕСТЬ NULL
	|				И Товары.КоличествоУпаковок <> 1
	|			ТОГДА ВЫРАЗИТЬ(Товары.СуммаНДС / Товары.Количество КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ Товары.СуммаНДС
	|	КОНЕЦ КАК МаркТоварыСуммаНДС,
	|	Товары.ВидПродукцииИС КАК ВидПродукцииИС
	|ПОМЕСТИТЬ ТоварыИСерииПоМаркированнымТоварам
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаМаркированныеТовары КАК МаркированныеТовары
	|		ПО (МаркированныеТовары.Номенклатура = Товары.Номенклатура)
	|			И (МаркированныеТовары.Характеристика = Товары.Характеристика)
	|			И (МаркированныеТовары.Серия = Товары.Серия)
	|			И (МаркированныеТовары.ВидУпаковкиСсылка = ЗНАЧЕНИЕ(Перечисление.ВидыУпаковокИС.Потребительская))
	|ГДЕ
	|	Товары.ВидПродукцииИС В(&ВидПродукцииИСМП)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаМаркированныеТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ТоварыИСерииПоМаркированнымТоварам.ИндексМаркированногоТовара) КАК ИндексМаркированногоТовара,
	|	СУММА(ТоварыИСерииПоМаркированнымТоварам.МаркТоварыСуммаСкидки) КАК СуммаСкидки,
	|	СУММА(ТоварыИСерииПоМаркированнымТоварам.МаркТоварыСуммаСНДС) КАК СуммаСНДС,
	|	СУММА(ТоварыИСерииПоМаркированнымТоварам.МаркТоварыСуммаНДС) КАК СуммаНДС
	|ПОМЕСТИТЬ СуммовыеРазницыМаркированныхТоваров
	|ИЗ
	|	ТоварыИСерииПоМаркированнымТоварам КАК ТоварыИСерииПоМаркированнымТоварам
	|ГДЕ
	|	&КоличествоМаркированныхТоваров > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыИСерииПоМаркированнымТоварам.НомерСтроки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ИндексМаркированногоТовара
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Штрихкод КАК Штрихкод,
	|	Товары.МаркТоварыКоличество КАК Количество,
	|	Товары.МаркТоварыКоличествоУпаковок КАК КоличествоУпаковок,
	|	ВЫБОР
	|		КОГДА СуммовыеРазницыМаркированныхТоваров.СуммаСкидки ЕСТЬ NULL
	|			ТОГДА Товары.МаркТоварыСуммаСкидки
	|		ИНАЧЕ ВЫРАЗИТЬ(Товары.МаркТоварыСуммаСкидки + Товары.СуммаСкидки - СуммовыеРазницыМаркированныхТоваров.СуммаСкидки КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК СуммаСкидки,
	|	ВЫБОР
	|		КОГДА СуммовыеРазницыМаркированныхТоваров.СуммаСНДС ЕСТЬ NULL
	|			ТОГДА Товары.МаркТоварыСуммаСНДС
	|		ИНАЧЕ ВЫРАЗИТЬ(Товары.МаркТоварыСуммаСНДС + Товары.СуммаСНДС - СуммовыеРазницыМаркированныхТоваров.СуммаСНДС КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК СуммаСНДС,
	|	ВЫБОР
	|		КОГДА СуммовыеРазницыМаркированныхТоваров.СуммаНДС ЕСТЬ NULL
	|			ТОГДА Товары.МаркТоварыСуммаНДС
	|		ИНАЧЕ ВЫРАЗИТЬ(Товары.МаркТоварыСуммаНДС + Товары.СуммаНДС - СуммовыеРазницыМаркированныхТоваров.СуммаНДС КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК СуммаНДС,
	|	Товары.ВидПродукцииИС КАК ВидПродукцииИС
	|ИЗ
	|	ТоварыИСерииПоМаркированнымТоварам КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ СуммовыеРазницыМаркированныхТоваров КАК СуммовыеРазницыМаркированныхТоваров
	|		ПО (СуммовыеРазницыМаркированныхТоваров.ИндексМаркированногоТовара = Товары.ИндексМаркированногоТовара)";
	
	ПараметрыСканирования = ШтрихкодированиеИС.ПараметрыСканирования(ДокументСсылка);
	ШтрихкодыУпаковок = ШтрихкодированиеИС.ВложенныеШтрихкодыУпаковок(МассивУпаковок, ПараметрыСканирования);
	// нужно типизировать колонки и добавить НомерСтроки
	ТипизироватьКолонкиМаркированныхТоваров(ШтрихкодыУпаковок.МаркированныеТовары);
	
	Запрос.УстановитьПараметр("МаркированныеТовары", ШтрихкодыУпаковок.МаркированныеТовары);
	Запрос.УстановитьПараметр("КоличествоМаркированныхТоваров", ШтрихкодыУпаковок.МаркированныеТовары.Количество());
	Запрос.УстановитьПараметр("ВидПродукцииИСМП", ИнтеграцияИСКлиентСервер.ВидыПродукцииИСМП(Истина));

	
	Результат = Запрос.Выполнить().Выгрузить();
	Результат.Индексы.Добавить("НомерСтроки");
	
	Возврат Результат;
	
КонецФункции

// Получить данные для передачи МОТП сведений о розничной продаже
Функция ДанныеДляИСМП(ДокументСсылка, МенеджерВременныхТаблиц, ТекстЗапросаШтрихкодовУпаковок)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаШтрихкодовУпаковок;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	МассивУпаковок = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ШтрихкодУпаковки");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Упаковка КАК Упаковка,
	|	Товары.ВидПродукцииИС КАК ВидПродукцииИС,
	|	Товары.Упаковка КАК ЕдиницаИзмерения,
	|	Товары.Количество КАК Количество,
	|	Товары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	Товары.Цена КАК Цена,
	|	Товары.СуммаСкидки КАК СуммаСкидки,
	|	Товары.СтавкаНДС КАК СтавкаНДС,
	|	Товары.СуммаНДС КАК СуммаНДС,
	|	Товары.СуммаСНДС КАК СуммаСНДС,
	|	Товары.Серия КАК Серия,
	|	Товары.Количество КАК КоличествоЕдиниц,
	|	Товары.КоличествоУпаковок КАК СерииКоличествоУпаковок
	|ИЗ
	|	Товары КАК Товары
	|";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Товары = Запрос.Выполнить().Выгрузить();
	
	Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
		РеализацияСтруктурой = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ДокументСсылка,
			"СкладОрдер,Дата");
	Иначе
		РеализацияСтруктурой = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ДокументСсылка,
			"Склад,ВидОперации,Дата");
	КонецЕсли;
	ПараметрыУказанияСерий = Документы.РеализацияТоваровУслуг.ПараметрыУказанияСерий(РеализацияСтруктурой);
	
	ТоварыРазобранные =
		РозничныеПродажиКлиентСервер.РаспределитьШтрихкодыПоТаблицеТоваров(
			ДокументСсылка,
			МассивУпаковок,
			ПараметрыУказанияСерий,
			Товары);

	Возврат ТоварыРазобранные;
	
КонецФункции

// Определяет курс оплаты для печати чека
//
Функция ОпределитьКурсыОплатыДляПечатиЧека(ДокументОбъект, ВВалютеДокумента = Ложь) Экспорт
	
	мВалютаРегламентированногоУчета = глЗначениеПеременной("ВалютаРегламентированногоУчета");
	
	СтруктураКурса = Новый Структура("Курс, Кратность");
	
	Если ВВалютеДокумента = Истина Тогда
		
		СтруктураКурса.Курс      = 1;
		СтруктураКурса.Кратность = 1;
		
		Возврат СтруктураКурса;

	ИначеЕсли ДокументОбъект.ВалютаДокумента <> мВалютаРегламентированногоУчета
		И (ДокументОбъект.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах ИЛИ ДокументОбъект.Дата >= '20090101000000')
		И ДокументОбъект.ДоговорКонтрагента.ВестиПоДокументамРасчетовСКонтрагентом Тогда
		
		ИмяТаблицы = ДокументОбъект.Метаданные().Имя;
		
		ЗапросКурсАванса = Новый Запрос;
		ЗапросКурсАванса.УстановитьПараметр("ДокументСсылка", ДокументОбъект.Ссылка);
		ЗапросКурсАванса.Текст =
		"ВЫБРАТЬ
		|	Док.СуммаВзаиморасчетов,
		|	Док.СуммаРегл
		|ИЗ 
		|	Документ."+ИмяТаблицы+".ДокументыРасчетовСКонтрагентом КАК Док
		|ГДЕ Док.Ссылка = &ДокументСсылка
		|ИТОГИ СУММА(СуммаВзаиморасчетов), СУММА(СуммаРегл) ПО ОБЩИЕ";
		
		Выборка = ЗапросКурсАванса.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Если Выборка.Следующий() Тогда
			КурсОплаты                 = ?(Выборка.СуммаВзаиморасчетов = 0, 0, Выборка.СуммаРегл/Выборка.СуммаВзаиморасчетов);
			ВыборкаСуммаВзаиморасчетов = Выборка.СуммаВзаиморасчетов;
			ВыборкаСуммаРегл           = Выборка.СуммаРегл;
		Иначе
			КурсОплаты                 = 0;
			ВыборкаСуммаВзаиморасчетов = 0;
			ВыборкаСуммаРегл           = 0;
		КонецЕсли;
		
		КурсДляПечати = КурсОплаты;
		
		Если КурсДляПечати = 0 Тогда
			СтруктураКурса.Курс      = ДокументОбъект.КурсВзаиморасчетов;
			СтруктураКурса.Кратность = ДокументОбъект.КратностьВзаиморасчетов;
		Иначе
			СтруктураКурса.Курс      = КурсДляПечати;
			СтруктураКурса.Кратность = 1;
		КонецЕсли;
		
	ИначеЕсли ДокументОбъект.ВалютаДокумента = ДокументОбъект.ДоговорКонтрагента.ВалютаВзаиморасчетов Тогда
		
		// Документ оформлен в валюте взаиморасчетов
		СтруктураКурса.Курс      = ДокументОбъект.КурсВзаиморасчетов;
		СтруктураКурса.Кратность = ДокументОбъект.КратностьВзаиморасчетов;
		
	Иначе
		
		// Документ оформлен в валюте регламентированного учета		
		СтруктураКурса.Курс      = 1;
		СтруктураКурса.Кратность = 1;
		
	КонецЕсли;
	
	Возврат СтруктураКурса;
	
КонецФункции

#Область ПрограммныйИнтерфейс

Функция ДанныеФискальнойОперацииСУчетомКорректировкиРеализации(ДокументОснование) Экспорт
	
	ДанныеФискальнойОперации = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ 
	|	КорректировкаРеализации.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ФискальныеОперации КАК ФискальныеОперации
	|		ПО КорректировкаРеализации.Ссылка = ФискальныеОперации.ДокументОснование
	|ГДЕ
	|	КорректировкаРеализации.ДокументОснование = &ДокументОснование
	|
	|УПОРЯДОЧИТЬ ПО
	|	КорректировкаРеализации.Дата УБЫВ";
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ДанныеФискальнойОперации = МенеджерОборудованияВызовСервера.ДанныеФискальнойОперации(Выборка.Ссылка);
	КонецЕсли;
	
	Возврат ДанныеФискальнойОперации;
	
КонецФункции

#Область КорректировкаРеализации

// Возвращает предварительные параметры фискализации чека для документа.
//
// Параметры:
//   ДокументСсылка - ДокументСсылка.РеализацияТоваровУслуг - Документ.
//   СуммаПредоплатыКорректировка - Число - Откорректированная сумма предоплаты.
//   ВерсияФФД - Строка - Версия ФФД.
//
// ВозвращаемоеЗначение:
//  Неопределено, Структура - см. функцию МенеджерОборудованияКлиентСервер.ПараметрыОперацииФискализацииЧека().
//
Функция ПараметрыОперацииФискализацииЧекаКорректировкаРеализации(ДокументСсылка, СуммаПредоплатыКорректировка = Неопределено, ВерсияФФД = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВалютаРегл = Константы.ВалютаРегламентированногоУчета.Получить();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК ДокументСсылка,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА Контрагенты.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
	|			ТОГДА ДанныеДокумента.Контрагент
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|	КОНЕЦ КАК Контрагент,
	|	ДанныеДокумента.Склад КАК ТорговыйОбъект,
	|	ЕСТЬNULL(ДанныеДокумента.Ответственный.ФизЛицо.Наименование, """") КАК Кассир,
	|	"""" КАК КассирИНН,
	|	ЛОЖЬ КАК ПоЗаказам,
	|	ДанныеДокумента.СуммаВключаетНДС КАК ЦенаВключаетНДС,
	|	ДанныеДокумента.УчитыватьНДС КАК НалогообложениеНДС,
	|	ДанныеДокумента.ВалютаДокумента КАК Валюта,
	|	0 КАК СуммаПредоплаты,
	|	ДанныеДокумента.СуммаДокумента КАК СуммаДокумента
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО ДанныеДокумента.Контрагент = Контрагенты.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.Ссылка,
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.СуммаВключаетНДС,
	|	ДанныеДокумента.УчитыватьНДС,
	|	ДанныеДокумента.ВалютаДокумента,
	|	ДанныеДокумента.СуммаДокумента,
	|	ЕСТЬNULL(ДанныеДокумента.Ответственный.ФизЛицо.Наименование, """"),
	|	"""",
	|	ВЫБОР
	|		КОГДА Контрагенты.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
	|			ТОГДА ДанныеДокумента.Контрагент
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|	КОНЕЦ");
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Шапка = Запрос.Выполнить().Выбрать();
	Шапка.Следующий();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТабличнаяЧасть.НомерСтроки КАК НомерСтроки,
	|	ТабличнаяЧасть.КлючСтроки = 0 КАК СверхЗаказа,
	|	ТабличнаяЧасть.ЗаказПокупателя КАК Заказ,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.ХарактеристикаНоменклатуры КАК Характеристика,
	|	ТабличнаяЧасть.СерияНоменклатуры КАК Серия,
	|	ТабличнаяЧасть.ЕдиницаИзмерения КАК Упаковка,
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ВидНоменклатуры.ТипНоменклатуры, ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПустаяСсылка)) КАК ТипНоменклатуры,
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ВидПродукцииИС, ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПустаяССылка)) = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Табак)
	|		ИЛИ ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.АлкогольнаяПродукция, ЛОЖЬ)
	|		ИЛИ ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ПодакцизныйТовар, ЛОЖЬ) КАК ПодакцизныйТовар,
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.НаименованиеПолное, """") КАК НоменклатураНаименование,
	|	ЕСТЬNULL(ТабличнаяЧасть.ХарактеристикаНоменклатуры.Наименование, """") КАК ХарактеристикаНаименование,
	|	ТабличнаяЧасть.ЕдиницаИзмерения КАК УпаковкаНаименование,
	|	ТабличнаяЧасть.Количество КАК Количество,
	|	ТабличнаяЧасть.Количество КАК КоличествоУпаковок,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА &ЦенаВключаетНДС
	|				ТОГДА ТабличнаяЧасть.Цена
	|			КОГДА ТабличнаяЧасть.Количество = 0
	|				ТОГДА ТабличнаяЧасть.Сумма + ТабличнаяЧасть.СуммаНДС
	|			ИНАЧЕ (ТабличнаяЧасть.Сумма + ТабличнаяЧасть.СуммаНДС) / ТабличнаяЧасть.Количество
	|		КОНЕЦ КАК ЧИСЛО(31, 2)) КАК Цена,
	|	0 КАК СуммаСкидки,
	|	ТабличнаяЧасть.СтавкаНДС КАК СтавкаНДС,
	|	ВЫБОР
	|		КОГДА &ЦенаВключаетНДС
	|			ТОГДА ТабличнаяЧасть.Сумма
	|		ИНАЧЕ ТабличнаяЧасть.Сумма + ТабличнаяЧасть.СуммаНДС
	|	КОНЕЦ КАК СуммаСНДС,
	|	ТабличнаяЧасть.СуммаНДС КАК СуммаНДС,
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ВидПродукцииИС, НЕОПРЕДЕЛЕНО) КАК ВидПродукцииИС,
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ВидНоменклатурнойКлассификации, """") КАК КодВидаНоменклатурнойКлассификации
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.НомерСтроки,
	|	ЛОЖЬ,
	|	ТабличнаяЧасть.ЗаказПокупателя,
	|	ТабличнаяЧасть.Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка),
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ВидНоменклатуры.ТипНоменклатуры, ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПустаяСсылка)),
	|	ЛОЖЬ,
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.НаименованиеПолное, ТабличнаяЧасть.Содержание),
	|	"""",
	|	"""",
	|	ТабличнаяЧасть.Количество,
	|	ТабличнаяЧасть.Количество,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА &ЦенаВключаетНДС
	|				ТОГДА ТабличнаяЧасть.Цена
	|			КОГДА ТабличнаяЧасть.Количество = 0
	|				ТОГДА ТабличнаяЧасть.Сумма + ТабличнаяЧасть.СуммаНДС
	|			ИНАЧЕ (ТабличнаяЧасть.Сумма + ТабличнаяЧасть.СуммаНДС) / ТабличнаяЧасть.Количество
	|		КОНЕЦ КАК ЧИСЛО(31, 2)),
	|	0,
	|	ТабличнаяЧасть.СтавкаНДС,
	|	ВЫБОР
	|		КОГДА &ЦенаВключаетНДС
	|			ТОГДА ТабличнаяЧасть.Сумма
	|		ИНАЧЕ ТабличнаяЧасть.Сумма + ТабличнаяЧасть.СуммаНДС
	|	КОНЕЦ,
	|	ТабличнаяЧасть.СуммаНДС,
	|	НЕОПРЕДЕЛЕНО,
	|	""""
	|ИЗ
	|	Документ.КорректировкаРеализации.Услуги КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки,
	|	Товары.СверхЗаказа,
	|	Товары.Заказ,
	|	Товары.ТипНоменклатуры,
	|	Товары.ПодакцизныйТовар,
	|	Товары.НоменклатураНаименование,
	|	Товары.ХарактеристикаНаименование,
	|	Товары.УпаковкаНаименование,
	|	Товары.КоличествоУпаковок,
	|	Товары.Цена,
	|	Товары.СуммаСкидки,
	|	Товары.СтавкаНДС,
	|	Товары.СуммаСНДС,
	|	Товары.СуммаНДС,
	|	Товары.ВидПродукцииИС,
	|	Товары.КодВидаНоменклатурнойКлассификации,
	|	Товары.Номенклатура,
	|	Товары.Упаковка
	|ИЗ
	|	Товары КАК Товары");
	
	Запрос.УстановитьПараметр("Ссылка",          ДокументСсылка);
	Запрос.УстановитьПараметр("ЦенаВключаетНДС",      Шапка.ЦенаВключаетНДС);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ТекстЗапросаШтрихКодыУпаковок = "ВЫБРАТЬ
	               |	ШтрихкодыУпаковок.ШтрихкодУпаковки КАК ШтрихкодУпаковки
	               |ИЗ
	               |	Документ.КорректировкаРеализации.ШтрихкодыУпаковок КАК ШтрихкодыУпаковок
	               |ГДЕ
	               |	ШтрихкодыУпаковок.Ссылка = &Ссылка
	               |";
	
	Товары = РезультатЗапроса[РезультатЗапроса.Количество() - 1].Выгрузить();
	РозничныеПродажи.ТаблицаТоваровСРаспределениемКодовМаркировкиИСМП(Товары, ДанныеДляИСМП(ДокументСсылка, МенеджерВременныхТаблиц, ТекстЗапросаШтрихКодыУпаковок));
	
	ДанныеДокументаДляЧека = РозничныеПродажиКлиентСервер.СтруктураДанныхДокументаДляПараметровФискализацииЧека();
	ДанныеДокументаДляЧека.Шапка = Шапка;
	ДанныеДокументаДляЧека.Товары = Товары;
	
	ПараметрыФискализацииЧека = ПодключаемоеОборудованиеУТВызовСервера.ПараметрыФискализацииЧека(ДанныеДокументаДляЧека,,,,ВерсияФФД);
		
	Возврат ПараметрыФискализацииЧека;
КонецФункции

Функция ПараметрыОперацииФискализацииЧекаКоррекцииКорректировкаРеализации(ДокументСсылка, ДанныеЧекаКоррекции, СуммаПредоплатыКорректировка = Неопределено, ВерсияФФД = Неопределено) Экспорт
	
	ПараметрыФискализацииЧека = ПараметрыОперацииФискализацииЧекаКорректировкаРеализации(ДокументСсылка, СуммаПредоплатыКорректировка, ВерсияФФД);
	
	Если ПараметрыФискализацииЧека <> Неопределено Тогда
		ПараметрыФискализацииЧека.Вставить("ДанныеКоррекции"		, ДанныеЧекаКоррекции.ДанныеЧекаКоррекции);
		ПараметрыФискализацииЧека.Вставить("НеприменениеККТ"		, ДанныеЧекаКоррекции.НеприменениеККТ);
		ПараметрыФискализацииЧека.Вставить("КорректируемыйДокумент"	, ДанныеЧекаКоррекции.КорректируемыйДокумент);
		
		ПараметрыФискализацииЧека.ДокументОснование = ДанныеЧекаКоррекции.ДокументОснование;
	КонецЕсли;
	
	Возврат ПараметрыФискализацииЧека;
	
КонецФункции

#КонецОбласти

#Область РеализацияТоваровУслуг

// Возвращает предварительные параметры фискализации чека для документа.
//
// Параметры:
//   ДокументСсылка - ДокументСсылка.РеализацияТоваровУслуг - Документ.
//   СуммаПредоплатыКорректировка - Число - Откорректированная сумма предоплаты.
//   ВерсияФФД - Строка - Версия ФФД.
//
// ВозвращаемоеЗначение:
//  Неопределено, Структура - см. функцию МенеджерОборудованияКлиентСервер.ПараметрыОперацииФискализацииЧека().
//
Функция ПараметрыОперацииФискализацииЧекаРеализацияТоваровУслуг(ДокументСсылка, СуммаПредоплатыКорректировка = Неопределено, ВерсияФФД = Неопределено) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	ВалютаРегл = Константы.ВалютаРегламентированногоУчета.Получить();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК ДокументСсылка,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Контрагент.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
	|			ТОГДА ДанныеДокумента.Контрагент
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|	КОНЕЦ КАК Контрагент,
	|	ДанныеДокумента.Склад.Наименование КАК ТорговыйОбъект,
	|	ЕСТЬNULL(ДанныеДокумента.Ответственный.ФизЛицо.Наименование, """") КАК Кассир,
	|	"""" КАК КассирИНН,
	|	ЛОЖЬ КАК ПоЗаказам,
	|	ДанныеДокумента.СуммаВключаетНДС КАК ЦенаВключаетНДС,
	|	ДанныеДокумента.УчитыватьНДС КАК НалогообложениеНДС,
	|	ДанныеДокумента.ВалютаДокумента КАК Валюта,
	|	0 КАК СуммаПредоплаты,
	|	ДанныеДокумента.СуммаДокумента КАК СуммаДокумента
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.Ссылка,
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.СуммаВключаетНДС,
	|	ДанныеДокумента.УчитыватьНДС,
	|	ДанныеДокумента.ВалютаДокумента,
	|	ДанныеДокумента.СуммаДокумента,
	|	ЕСТЬNULL(ДанныеДокумента.Ответственный.ФизЛицо.Наименование, """"),
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Контрагент.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
	|			ТОГДА ДанныеДокумента.Контрагент
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|	КОНЕЦ,
	|	ДанныеДокумента.Склад");
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Шапка = Запрос.Выполнить().Выбрать();
	Шапка.Следующий();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТабличнаяЧасть.НомерСтроки КАК НомерСтроки,
	|	ТабличнаяЧасть.КлючСтроки = 0 КАК СверхЗаказа,
	|	ТабличнаяЧасть.ЗаказПокупателя КАК Заказ,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.ХарактеристикаНоменклатуры КАК Характеристика,
	|	ТабличнаяЧасть.СерияНоменклатуры КАК Серия,
	|	ТабличнаяЧасть.ЕдиницаИзмерения КАК Упаковка,
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ВидНоменклатуры.ТипНоменклатуры, ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПустаяСсылка)) КАК ТипНоменклатуры,
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ВидПродукцииИС, ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПустаяССылка)) = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Табак)
	|		ИЛИ ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.АлкогольнаяПродукция, ЛОЖЬ)
	|		ИЛИ ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ПодакцизныйТовар, ЛОЖЬ) КАК ПодакцизныйТовар,
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.НаименованиеПолное, """") КАК НоменклатураНаименование,
	|	ЕСТЬNULL(ТабличнаяЧасть.ХарактеристикаНоменклатуры.Наименование, """") КАК ХарактеристикаНаименование,
	|	ТабличнаяЧасть.ЕдиницаИзмерения КАК УпаковкаНаименование,
	|	ТабличнаяЧасть.Количество КАК Количество,
	|	ТабличнаяЧасть.Количество КАК КоличествоУпаковок,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА &ЦенаВключаетНДС
	|				ТОГДА ТабличнаяЧасть.Цена
	|			КОГДА ТабличнаяЧасть.Количество = 0
	|				ТОГДА ТабличнаяЧасть.Сумма + ТабличнаяЧасть.СуммаНДС
	|			ИНАЧЕ (ТабличнаяЧасть.Сумма + ТабличнаяЧасть.СуммаНДС) / ТабличнаяЧасть.Количество
	|		КОНЕЦ КАК ЧИСЛО(31, 2)) КАК Цена,
	|	0 КАК СуммаСкидки,
	|	ТабличнаяЧасть.СтавкаНДС КАК СтавкаНДС,
	|	ВЫБОР
	|		КОГДА &ЦенаВключаетНДС
	|			ТОГДА ТабличнаяЧасть.Сумма
	|		ИНАЧЕ ТабличнаяЧасть.Сумма + ТабличнаяЧасть.СуммаНДС
	|	КОНЕЦ КАК СуммаСНДС,
	|	ТабличнаяЧасть.СуммаНДС КАК СуммаНДС,
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.АлкогольнаяПродукция, ЛОЖЬ) КАК АлкогольнаяПродукция,
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ВидПродукцииИС, НЕОПРЕДЕЛЕНО) КАК ВидПродукцииИС,
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ВидНоменклатурнойКлассификации, """") КАК КодВидаНоменклатурнойКлассификации,
	|	ТабличнаяЧасть.СерияНоменклатуры.НомерГТД КАК НомерТаможеннойДекларации,
	|	ТабличнаяЧасть.СерияНоменклатуры.СтранаПроисхождения КАК КодСтраныПроисхожденияТовара
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.НомерСтроки,
	|	ЛОЖЬ,
	|	ТабличнаяЧасть.ЗаказПокупателя,
	|	ТабличнаяЧасть.Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка),
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ВидНоменклатуры.ТипНоменклатуры, ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПустаяСсылка)),
	|	ЛОЖЬ,
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.НаименованиеПолное, ТабличнаяЧасть.Содержание),
	|	"""",
	|	"""",
	|	ТабличнаяЧасть.Количество,
	|	ТабличнаяЧасть.Количество,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА &ЦенаВключаетНДС
	|				ТОГДА ТабличнаяЧасть.Цена
	|			КОГДА ТабличнаяЧасть.Количество = 0
	|				ТОГДА ТабличнаяЧасть.Сумма + ТабличнаяЧасть.СуммаНДС
	|			ИНАЧЕ (ТабличнаяЧасть.Сумма + ТабличнаяЧасть.СуммаНДС) / ТабличнаяЧасть.Количество
	|		КОНЕЦ КАК ЧИСЛО(31, 2)),
	|	0,
	|	ТабличнаяЧасть.СтавкаНДС,
	|	ВЫБОР
	|		КОГДА &ЦенаВключаетНДС
	|			ТОГДА ТабличнаяЧасть.Сумма
	|		ИНАЧЕ ТабличнаяЧасть.Сумма + ТабличнаяЧасть.СуммаНДС
	|	КОНЕЦ,
	|	ТабличнаяЧасть.СуммаНДС,
	|	ЛОЖЬ,
	|	НЕОПРЕДЕЛЕНО,
	|	"""",
	|	"""",
	|	""""
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Услуги КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки,
	|	Товары.СверхЗаказа,
	|	Товары.Заказ,
	|	Товары.ТипНоменклатуры,
	|	Товары.ПодакцизныйТовар,
	|	Товары.НоменклатураНаименование,
	|	Товары.ХарактеристикаНаименование,
	|	Товары.УпаковкаНаименование,
	|	Товары.КоличествоУпаковок,
	|	Товары.Цена,
	|	Товары.СуммаСкидки,
	|	Товары.СтавкаНДС,
	|	Товары.СуммаСНДС,
	|	Товары.СуммаНДС,
	|	Товары.АлкогольнаяПродукция,
	|	Товары.ВидПродукцииИС,
	|	Товары.КодВидаНоменклатурнойКлассификации,
	|	Товары.НомерТаможеннойДекларации,
	|	Товары.КодСтраныПроисхожденияТовара,
	|	Товары.Номенклатура,
	|	Товары.Количество,
	|	Товары.Упаковка
	|ИЗ
	|	Товары КАК Товары");
	
	Запрос.УстановитьПараметр("Ссылка",          ДокументСсылка);
	Запрос.УстановитьПараметр("ЦенаВключаетНДС",      Шапка.ЦенаВключаетНДС);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ТекстЗапросаШтрихКодыУпаковок = "ВЫБРАТЬ
	               |	ШтрихкодыУпаковок.ШтрихкодУпаковки КАК ШтрихкодУпаковки
	               |ИЗ
	               |	Документ.РеализацияТоваровУслуг.ШтрихкодыУпаковок КАК ШтрихкодыУпаковок
	               |ГДЕ
	               |	ШтрихкодыУпаковок.Ссылка = &Ссылка
	               |";

	Товары = РезультатЗапроса[РезультатЗапроса.Количество() - 1].Выгрузить();
	РозничныеПродажи.ТаблицаТоваровСРаспределениемКодовМаркировкиИСМП(Товары, ДанныеДляИСМП(ДокументСсылка, МенеджерВременныхТаблиц, ТекстЗапросаШтрихКодыУпаковок));
	
	ДанныеДокументаДляЧека = РозничныеПродажиКлиентСервер.СтруктураДанныхДокументаДляПараметровФискализацииЧека();
	ДанныеДокументаДляЧека.Шапка = Шапка;
	ДанныеДокументаДляЧека.Товары = Товары;
	
	ПараметрыФискализацииЧека = ПодключаемоеОборудованиеУТВызовСервера.ПараметрыФискализацииЧека(ДанныеДокументаДляЧека, , , Ложь, ВерсияФФД);
		
	Возврат ПараметрыФискализацииЧека;
	
КонецФункции

#КонецОбласти

#Область ВозвратТоваровОтПокупателя

// Возвращает предварительные параметры фискализации чека для документа.
//
// Параметры:
//   ДокументСсылка - ДокументСсылка.ВозвратТоваровОтПокупателя - Документ.
//   СуммаПредоплатыКорректировка - Число - Откорректированная сумма предоплаты.
//   ВерсияФФД - Строка - Версия ФФД.
//
// ВозвращаемоеЗначение:
//  Неопределено, Структура - см. функцию МенеджерОборудованияКлиентСервер.ПараметрыОперацииФискализацииЧека().
//
Функция ПараметрыОперацииФискализацииЧекаВозвратТоваровОтПокупателя(ДокументСсылка, СуммаПредоплатыКорректировка = Неопределено, ВерсияФФД = Неопределено) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	ВалютаРегл = Константы.ВалютаРегламентированногоУчета.Получить();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК ДокументСсылка,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Контрагент.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
	|			ТОГДА ДанныеДокумента.Контрагент
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|	КОНЕЦ КАК Контрагент,
	|	ДанныеДокумента.СкладОрдер КАК ТорговыйОбъект,
	|	ЕСТЬNULL(ДанныеДокумента.Ответственный.ФизЛицо.Наименование, """") КАК Кассир,
	|	"""" КАК КассирИНН,
	|	ЛОЖЬ КАК ПоЗаказам,
	|	ДанныеДокумента.СуммаВключаетНДС КАК ЦенаВключаетНДС,
	|	ДанныеДокумента.УчитыватьНДС КАК НалогообложениеНДС,
	|	ДанныеДокумента.ВалютаДокумента КАК Валюта,
	|	0 КАК СуммаПредоплаты,
	|	ДанныеДокумента.СуммаДокумента КАК СуммаДокумента
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.Ссылка,
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.СуммаВключаетНДС,
	|	ДанныеДокумента.УчитыватьНДС,
	|	ДанныеДокумента.ВалютаДокумента,
	|	ДанныеДокумента.СуммаДокумента,
	|	ЕСТЬNULL(ДанныеДокумента.Ответственный.ФизЛицо.Наименование, """"),
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Контрагент.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
	|			ТОГДА ДанныеДокумента.Контрагент
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|	КОНЕЦ,
	|	ДанныеДокумента.СкладОрдер");
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Шапка = Запрос.Выполнить().Выбрать();
	Шапка.Следующий();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТабличнаяЧасть.НомерСтроки КАК НомерСтроки,
	|	ТабличнаяЧасть.КлючСтроки = 0 КАК СверхЗаказа,
	|	ТабличнаяЧасть.ЗаказПокупателя КАК Заказ,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.ХарактеристикаНоменклатуры КАК Характеристика,
	|	ТабличнаяЧасть.СерияНоменклатуры КАК Серия,
	|	ТабличнаяЧасть.ЕдиницаИзмерения КАК Упаковка,
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ВидНоменклатуры.ТипНоменклатуры, ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПустаяСсылка)) КАК ТипНоменклатуры,
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ВидПродукцииИС, ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПустаяССылка)) = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Табак)
	|		ИЛИ ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.АлкогольнаяПродукция, ЛОЖЬ)
	|		ИЛИ ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ПодакцизныйТовар, ЛОЖЬ) КАК ПодакцизныйТовар,
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.НаименованиеПолное, """") КАК НоменклатураНаименование,
	|	ЕСТЬNULL(ТабличнаяЧасть.ХарактеристикаНоменклатуры.Наименование, """") КАК ХарактеристикаНаименование,
	|	ТабличнаяЧасть.ЕдиницаИзмерения КАК УпаковкаНаименование,
	|	ТабличнаяЧасть.Количество КАК Количество,
	|	ТабличнаяЧасть.Количество КАК КоличествоУпаковок,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА &ЦенаВключаетНДС
	|				ТОГДА ТабличнаяЧасть.Цена
	|			КОГДА ТабличнаяЧасть.Количество = 0
	|				ТОГДА ТабличнаяЧасть.Сумма + ТабличнаяЧасть.СуммаНДС
	|			ИНАЧЕ (ТабличнаяЧасть.Сумма + ТабличнаяЧасть.СуммаНДС) / ТабличнаяЧасть.Количество
	|		КОНЕЦ КАК ЧИСЛО(31, 2)) КАК Цена,
	|	0 КАК СуммаСкидки,
	|	ТабличнаяЧасть.СтавкаНДС КАК СтавкаНДС,
	|	ВЫБОР
	|		КОГДА &ЦенаВключаетНДС
	|			ТОГДА ТабличнаяЧасть.Сумма
	|		ИНАЧЕ ТабличнаяЧасть.Сумма + ТабличнаяЧасть.СуммаНДС
	|	КОНЕЦ КАК СуммаСНДС,
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.АлкогольнаяПродукция, ЛОЖЬ) КАК АлкогольнаяПродукция,
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ВидПродукцииИС, НЕОПРЕДЕЛЕНО) КАК ВидПродукцииИС,
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ВидНоменклатурнойКлассификации, """") КАК КодВидаНоменклатурнойКлассификации,
	|	ТабличнаяЧасть.СерияНоменклатуры.НомерГТД КАК НомерТаможеннойДекларации,
	|	ТабличнаяЧасть.СерияНоменклатуры.СтранаПроисхождения КАК КодСтраныПроисхожденияТовара,
	|	ТабличнаяЧасть.СуммаНДС
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя.Товары КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки,
	|	Товары.СверхЗаказа,
	|	Товары.Заказ,
	|	Товары.ТипНоменклатуры,
	|	Товары.ПодакцизныйТовар,
	|	Товары.НоменклатураНаименование,
	|	Товары.ХарактеристикаНаименование,
	|	Товары.УпаковкаНаименование,
	|	Товары.КоличествоУпаковок КАК Количество,
	|	Товары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	Товары.Цена,
	|	Товары.СуммаСкидки,
	|	Товары.СтавкаНДС,
	|	Товары.СуммаСНДС,
	|	Товары.АлкогольнаяПродукция,
	|	Товары.ВидПродукцииИС,
	|	Товары.КодВидаНоменклатурнойКлассификации,
	|	Товары.НомерТаможеннойДекларации,
	|	Товары.КодСтраныПроисхожденияТовара,
	|	Товары.СуммаНДС,
	|	Товары.Номенклатура,
	|	Товары.Упаковка
	|ИЗ
	|	Товары КАК Товары");
	
	Запрос.УстановитьПараметр("Ссылка",          ДокументСсылка);
	Запрос.УстановитьПараметр("ЦенаВключаетНДС",      Шапка.ЦенаВключаетНДС);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ТекстЗапросаШтрихКодыУпаковок = "ВЫБРАТЬ
	               |	ШтрихкодыУпаковок.ШтрихкодУпаковки КАК ШтрихкодУпаковки
	               |ИЗ
	               |	Документ.ВозвратТоваровОтПокупателя.ШтрихкодыУпаковок КАК ШтрихкодыУпаковок
	               |ГДЕ
	               |	ШтрихкодыУпаковок.Ссылка = &Ссылка
	               |";
	
	Товары = РезультатЗапроса[РезультатЗапроса.Количество() - 1].Выгрузить();
	РозничныеПродажи.ТаблицаТоваровСРаспределениемКодовМаркировкиИСМП(Товары, ДанныеДляИСМП(ДокументСсылка, МенеджерВременныхТаблиц, ТекстЗапросаШтрихКодыУпаковок));
	
	ДанныеДокументаДляЧека = РозничныеПродажиКлиентСервер.СтруктураДанныхДокументаДляПараметровФискализацииЧека();
	ДанныеДокументаДляЧека.Шапка = Шапка;
	ДанныеДокументаДляЧека.Товары = Товары;
	
	ПараметрыФискализацииЧека = ПодключаемоеОборудованиеУТВызовСервера.ПараметрыФискализацииЧека(ДанныеДокументаДляЧека, , Истина, Ложь);
		
	Возврат ПараметрыФискализацииЧека;
	
КонецФункции

#КонецОбласти

#Область ЧекККМ

// Возвращает предварительные параметры фискализации чека для документа.
//
// Параметры:
//   ДокументСсылка - ДокументСсылка.ЧекККМ - Документ.
//   СуммаПредоплатыКорректировка - Число - Откорректированная сумма предоплаты.
//   ВерсияФФД - Строка - Версия ФФД.
//
// ВозвращаемоеЗначение:
//  Неопределено, Структура - см. функцию МенеджерОборудованияКлиентСервер.ПараметрыОперацииФискализацииЧека().
//
Функция ПараметрыОперацииФискализацииЧекаЧекККМ(ДокументСсылка, СуммаПредоплатыКорректировка = Неопределено, ВерсияФФД = Неопределено, ПорядокНалогообложения = Неопределено) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	ВалютаРегл = Константы.ВалютаРегламентированногоУчета.Получить();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК ДокументСсылка,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Контрагент,
	|	ДанныеДокумента.Склад КАК ТорговыйОбъект,
	|	ЕСТЬNULL(ДанныеДокумента.Ответственный.ФизЛицо.Наименование, """") КАК Кассир,
	|	"""" КАК КассирИНН,
	|	ЛОЖЬ КАК ПоЗаказам,
	|	&ВалютаРеглУчета КАК Валюта,
	|	0 КАК СуммаПредоплаты,
	|	ДанныеДокумента.СуммаДокумента КАК СуммаДокумента
	|ИЗ
	|	Документ.ЧекККМ КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.Ссылка,
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.СуммаДокумента,
	|	ЕСТЬNULL(ДанныеДокумента.Ответственный.ФизЛицо.Наименование, """"),
	|	ДанныеДокумента.Склад");
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("ВалютаРеглУчета", глЗначениеПеременной("ВалютаРегламентированногоУчета"));
	
	Шапка = Запрос.Выполнить().Выбрать();
	Шапка.Следующий();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТабличнаяЧасть.НомерСтроки КАК НомерСтроки,
	|	ТабличнаяЧасть.КлючСтроки = 0 КАК СверхЗаказа,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.ХарактеристикаНоменклатуры КАК Характеристика,
	|	ТабличнаяЧасть.СерияНоменклатуры КАК Серия,
	|	ТабличнаяЧасть.ЕдиницаИзмерения КАК Упаковка,
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ВидНоменклатуры.ТипНоменклатуры, ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПустаяСсылка)) КАК ТипНоменклатуры,
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ВидПродукцииИС, ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПустаяССылка)) = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Табак)
	|		ИЛИ ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.АлкогольнаяПродукция, ЛОЖЬ)
	|		ИЛИ ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ПодакцизныйТовар, ЛОЖЬ) КАК ПодакцизныйТовар,
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.НаименованиеПолное, """") КАК НоменклатураНаименование,
	|	ЕСТЬNULL(ТабличнаяЧасть.ХарактеристикаНоменклатуры.Наименование, """") КАК ХарактеристикаНаименование,
	|	ТабличнаяЧасть.ЕдиницаИзмерения КАК УпаковкаНаименование,
	|	ТабличнаяЧасть.Количество КАК Количество,
	|	ТабличнаяЧасть.Количество КАК КоличествоУпаковок,
	|	ВЫРАЗИТЬ(ТабличнаяЧасть.Цена КАК ЧИСЛО(31, 2)) КАК Цена,
	|	ВЫБОР
	|		КОГДА &ПорядокНалогообложения
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|		ИНАЧЕ ТабличнаяЧасть.Номенклатура.СтавкаНДС
	|	КОНЕЦ КАК СтавкаНДС,
	|	0 КАК СуммаСкидки,
	|	ТабличнаяЧасть.Сумма КАК СуммаСНДС,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА &ПорядокНалогообложения
	|				ТОГДА 0
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ТабличнаяЧасть.Номенклатура.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20)
	|						ТОГДА ТабличнаяЧасть.Сумма / 12 * 2
	|					ИНАЧЕ ВЫБОР
	|							КОГДА ТабличнаяЧасть.Номенклатура.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10)
	|								ТОГДА ТабличнаяЧасть.Сумма / 11
	|							ИНАЧЕ 0
	|						КОНЕЦ
	|				КОНЕЦ
	|		КОНЕЦ КАК ЧИСЛО(31, 2)) КАК СуммаНДС,
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.АлкогольнаяПродукция, ЛОЖЬ) КАК АлкогольнаяПродукция,
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ВидПродукцииИС, НЕОПРЕДЕЛЕНО) КАК ВидПродукцииИС,
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ВидНоменклатурнойКлассификации, """") КАК КодВидаНоменклатурнойКлассификации
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	Документ.ЧекККМ.Товары КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки,
	|	Товары.СверхЗаказа,
	|	Товары.ТипНоменклатуры,
	|	Товары.ПодакцизныйТовар,
	|	Товары.НоменклатураНаименование,
	|	Товары.ХарактеристикаНаименование,
	|	Товары.УпаковкаНаименование,
	|	Товары.КоличествоУпаковок,
	|	Товары.Цена,
	|	Товары.СуммаСкидки,
	|	Товары.СтавкаНДС,
	|	Товары.СуммаСНДС,
	|	Товары.СуммаНДС,
	|	Товары.АлкогольнаяПродукция,
	|	Товары.ВидПродукцииИС,
	|	Товары.КодВидаНоменклатурнойКлассификации,
	|	Товары.Номенклатура
	|ИЗ
	|	Товары КАК Товары");
	
	Запрос.УстановитьПараметр("Ссылка",                 ДокументСсылка);
	Запрос.УстановитьПараметр("ПорядокНалогообложения", (ПорядокНалогообложения = "ПСН") ИЛИ (ПорядокНалогообложения = "ЕНВД") ИЛИ (ПорядокНалогообложения = "УПРОЩЕННАЯ"));
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Если ДокументСсылка.АкцизныеМарки.Количество() > 0 Тогда
		ТекстЗапросаШтрихКодыУпаковок = "ВЫБРАТЬ
		               |	ШтрихкодыУпаковок.АкцизнаяМарка КАК ШтрихкодУпаковки
		               |ИЗ
		               |	Документ.ЧекККМ.АкцизныеМарки КАК ШтрихкодыУпаковок
		               |ГДЕ
		               |	ШтрихкодыУпаковок.Ссылка = &Ссылка
		               |";
	Иначе
		ТекстЗапросаШтрихКодыУпаковок = "ВЫБРАТЬ
		                                |	ШтрихкодыУпаковок.ШтрихкодУпаковки КАК ШтрихкодУпаковки
		                                |ИЗ
		                                |	Документ.ЧекККМ.ШтрихкодыУпаковок КАК ШтрихкодыУпаковок
		                                |ГДЕ
		                                |	ШтрихкодыУпаковок.Ссылка = &Ссылка";
	КонецЕсли;
	
	Товары = РезультатЗапроса[РезультатЗапроса.Количество() - 1].Выгрузить();
	РозничныеПродажи.ТаблицаТоваровСРаспределениемКодовМаркировкиИСМП(Товары, ДанныеДляИСМП(ДокументСсылка, МенеджерВременныхТаблиц, ТекстЗапросаШтрихКодыУпаковок));
	
	ДанныеДокументаДляЧека = РозничныеПродажиКлиентСервер.СтруктураДанныхДокументаДляПараметровФискализацииЧека();
	ДанныеДокументаДляЧека.Шапка = Шапка;
	ДанныеДокументаДляЧека.Товары = Товары;
	
	ПараметрыФискализацииЧека = ПодключаемоеОборудованиеУТВызовСервера.ПараметрыФискализацииЧека(ДанныеДокументаДляЧека, , ДокументСсылка.ВидОперации <> Перечисления.ВидыОперацийЧекККМ.Продажа, Ложь, ВерсияФФД);
		
	Возврат ПараметрыФискализацииЧека;
	
КонецФункции

#КонецОбласти

#КонецОбласти


