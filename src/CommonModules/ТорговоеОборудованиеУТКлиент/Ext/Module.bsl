// Программный интерфейс

Процедура ПробитьЧек(Форма, ПараметрыОперации, РежимЗаписи, ОповещениеПриЗавершении = Неопределено) Экспорт
	
	ОчиститьСообщения();
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма",                   Форма);
	ДополнительныеПараметры.Вставить("ПараметрыОперации",       ПараметрыОперации);
	ДополнительныеПараметры.Вставить("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
	ДополнительныеПараметры.Вставить("РежимЗаписиДокумента",    РежимЗаписи);
	ДополнительныеПараметры.Вставить("ИмяПроцедуры",            "ПробитиеЧекаПослеЗаписиДокумента");
	
	ОбработатьРежимЗаписи(Истина, ДополнительныеПараметры);
	
КонецПроцедуры

// Пробить чек выемки денежных средств.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма.
//  ПараметрыОперации - Структура - Структура со свойствами:
//   * ДокументСсылка;
//   * Организация;
//   * ТорговыйОбъект;
//   * ПодключенноеОборудование;
//   * Сумма;
//   * ВыемкаДенежныхСредствИзКассыККМ
//   * ВнесениеДенежныхСредствВКассуККМ
//  РежимЗаписи - РежимЗаписиДокумента - Режим записи.
//  ОповещениеПриЗавершении - ОписаниеОповещения - Описание оповещения.
//
Процедура ПробитьЧекВыемкаДенежныхСредств(Форма, ПараметрыОперации, РежимЗаписи, ОповещениеПриЗавершении) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма",                   Форма);
	ДополнительныеПараметры.Вставить("ПараметрыОперации",       ПараметрыОперации);
	ДополнительныеПараметры.Вставить("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
	ДополнительныеПараметры.Вставить("РежимЗаписиДокумента",    РежимЗаписи);
	ДополнительныеПараметры.Вставить("ИмяПроцедуры",            "ПробитиеЧекаВыемкаДенежныхСредствПослеЗаписиДокумента");
	
	РозничныеПродажиКлиент.ОбработатьСостояниеСмены(
		Форма,
		ОписаниеОповещенияИС("ОбработатьРежимЗаписи", //Обработка.ТОСервер, 
			ДополнительныеПараметры));
	
КонецПроцедуры

// Открыть форму записи фискальной операции. Объединение с БПО
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма.
//  ДокументОснование - ДокументСсылка - Ссылка на документ-основание
//

Процедура ОткрытьЗаписьФискальнойОперации(Форма, ДокументОснование) Экспорт
	
	ДанныеФискальнойОперации = МенеджерОборудованияВызовСервера.ДанныеФискальнойОперации(ДокументОснование);
	Если ДанныеФискальнойОперации = Неопределено Тогда
		
		Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
			
			ДанныеФискальнойОперации = ТорговоеОборудованиеУТВызовСервера.ДанныеФискальнойОперацииСУчетомКорректировкиРеализации(ДокументОснование);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ДанныеФискальнойОперации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗаписи = Новый Структура();
	ПараметрыЗаписи.Вставить("ИдентификаторЗаписи", ДанныеФискальнойОперации.ИдентификаторЗаписи);
	ПараметрыЗаписи.Вставить("ДокументОснование",   ДанныеФискальнойОперации.ДокументОснование);
	
	ЗначениеКлюча = Новый Массив;
	ЗначениеКлюча.Добавить(ПараметрыЗаписи);
	
	КлючЗаписи = Новый("РегистрСведенийКлючЗаписи.ФискальныеОперации", ЗначениеКлюча);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", КлючЗаписи);
	
	ОткрытьФорму("РегистрСведений.ФискальныеОперации.Форма.ФормаЗаписи", ПараметрыФормы, Форма);
	
КонецПроцедуры

// Программный интерфейс (конец)

// Служебные процедуры и функции

Процедура ПробитиеЧекаПослеЗаписиДокумента(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено
		Или Не Результат Тогда
		
		Если ДополнительныеПараметры.РежимЗаписиДокумента = РежимЗаписиДокумента.Запись Тогда
			ПоказатьПредупреждениеИС(
				Неопределено,
				НСтр("ru = 'Не удалось записать документ'"));
		Иначе
			ПоказатьПредупреждениеИС(
				Неопределено,
				НСтр("ru = 'Не удалось провести документ'"));
		КонецЕсли;
		
		Если ДополнительныеПараметры.ОповещениеПриЗавершении <> Неопределено Тогда
			ВыполнитьОбработкуОповещенияИС(ДополнительныеПараметры.ОповещениеПриЗавершении, Ложь);
		КонецЕсли;
		
	Иначе
		
		Если ДополнительныеПараметры.ПараметрыОперации.ДокументСсылка <> Неопределено
			И Не ЗначениеЗаполнено(ДополнительныеПараметры.ПараметрыОперации.ДокументСсылка) Тогда
			ДополнительныеПараметры.ПараметрыОперации.ДокументСсылка = ДополнительныеПараметры.Форма.Ссылка;
		КонецЕсли;
		
		ОткрытьФормуИС(
			"Обработка.ПредпросмотрЧека.Форма",
			ДополнительныеПараметры.ПараметрыОперации,
			ДополнительныеПараметры.Форма,,,,
			ДополнительныеПараметры.ОповещениеПриЗавершении);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПробитиеЧекаВыемкаДенежныхСредствПослеЗаписиДокумента(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено
		Или Не Результат Тогда
		
		Если ДополнительныеПараметры.РежимЗаписиДокумента = РежимЗаписиДокумента.Запись Тогда
			ПоказатьПредупреждениеИС(
				Неопределено,
				НСтр("ru = 'Не удалось записать документ'"));
		Иначе
			ПоказатьПредупреждениеИС(
				Неопределено,
				НСтр("ru = 'Не удалось провести документ'"));
		КонецЕсли;
		
		Если ДополнительныеПараметры.ОповещениеПриЗавершении <> Неопределено Тогда
			ВыполнитьОбработкуОповещенияИС(ДополнительныеПараметры.ОповещениеПриЗавершении, Ложь);
		КонецЕсли;
		
	Иначе
		
		Если ДополнительныеПараметры.ПараметрыОперации.ДокументСсылка <> Неопределено
			И Не ЗначениеЗаполнено(ДополнительныеПараметры.ПараметрыОперации.ДокументСсылка) Тогда
			ДополнительныеПараметры.ПараметрыОперации.ДокументСсылка = ДополнительныеПараметры.Форма.Ссылка;
		КонецЕсли;
		
		ПараметрыОперации = Новый Структура;
		ПараметрыОперации.Вставить("ТипИнкассации", 0);
		ПараметрыОперации.Вставить("Сумма",         ДополнительныеПараметры.ПараметрыОперации.Сумма);
		ПараметрыОперации.Вставить("ДокументОснование", ДополнительныеПараметры.ПараметрыОперации.ДокументСсылка);
		
		МенеджерОборудованияКлиент.НачатьИнкассациюНаФискальномУстройстве(
			ОписаниеОповещенияИС("ЗаписатьВЖурналВыемкуДенежныхСредствВКассуККМ", ТорговоеОборудованиеУТКлиент, ДополнительныеПараметры),
			ДополнительныеПараметры.Форма.УникальныйИдентификатор,
			ПараметрыОперации);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьРежимЗаписи(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено
		Или Не Результат Тогда
		ВыполнитьОбработкуОповещенияИС(ДополнительныеПараметры.ОповещениеПриЗавершении, Ложь);
	КонецЕсли;
	
	Если ДополнительныеПараметры.РежимЗаписиДокумента = РежимЗаписиДокумента.Запись
		И (Не ЗначениеЗаполнено(ДополнительныеПараметры.Форма.Ссылка) Или ДополнительныеПараметры.Форма.Модифицированность) Тогда
		
		ПоказатьВопросИС(
			ОписаниеОповещенияИС("ПослеОтветаНаВопросОЗаписиДокумента", ТорговоеОборудованиеУТКлиент, ДополнительныеПараметры),
			НСтр("ru = 'Операция возможна только после записи документа, записать документ?'"),
			РежимДиалогаВопрос.ДаНет);
		
	ИначеЕсли ДополнительныеПараметры.РежимЗаписиДокумента = РежимЗаписиДокумента.Проведение
		И (Не ДополнительныеПараметры.Форма.Проведен Или ДополнительныеПараметры.Форма.Модифицированность) Тогда
		
		ПоказатьВопросИС(
			ОписаниеОповещенияИС("ПослеОтветаНаВопросОЗаписиДокумента", ТорговоеОборудованиеУТКлиент, ДополнительныеПараметры),
			НСтр("ru = 'Операция возможна только после проведения документа, провести документ?'"),
			РежимДиалогаВопрос.ДаНет);
		
	Иначе
		ОписаниеОповещения = ОписаниеОповещенияИС(ДополнительныеПараметры.ИмяПроцедуры, ТорговоеОборудованиеУТКлиент, ДополнительныеПараметры);
		ВыполнитьОбработкуОповещенияИС(ОписаниеОповещения, Истина);
	КонецЕсли;
	
КонецПроцедуры

// Служебные процедуры и функции (конец)

