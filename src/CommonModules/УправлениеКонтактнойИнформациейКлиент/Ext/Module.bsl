/////////////////////////////////////////////////////////////////////
//
// Адаптационный модуль. Поддержка вызовов новой фуункциональности
//
/////////////////////////////////////////////////////////////////////

Функция ОбъектПоЭлементуФормы(Элемент)
	
	Попытка
		Родитель = Элемент.Родитель;
		Пока ТипЗнч(Родитель) <> Тип("ФормаКлиентскогоПриложения") Цикл
			Родитель = Родитель.Родитель;
		КонецЦикла;
		
		Возврат Родитель.Объект;
	Исключение
		Возврат Неопределено;
	КонецПопытки;

КонецФункции // ОбъектПоЭлементуФормы()

// Открывает подходящую форму контактной информации для редактирования или просмотра.
//
//  Параметры:
//      Параметры    - Произвольный - результат функции ПараметрыФормыКонтактнойИнформации.
//      Владелец     - Произвольный - параметр для открываемой формы.
//      Оповещение   - ОписаниеОповещения - для обработки закрытия формы.
//
//  Возвращаемое значение:
//   ФормаКлиентскогоПриложения - необходимая форма.
//
Функция ОткрытьФормуКонтактнойИнформации(Параметры, Владелец = Неопределено, Оповещение = Неопределено) Экспорт
	
	Если Найти(Параметры.Заголовок, НСтр("ru='Адрес'"))>0 Тогда
		ФормаАдреса = Обработки.РедактированиеКонтактнойИнформации.ПолучитьФорму("ФормаЗаписиАдреса");
		ФормаАдреса.НачальноеЗначениеВыбора = РегламентированнаяОтчетность.РазложитьАдрес(Параметры.Представление);
		
		Если ФормаАдреса.НачальноеЗначениеВыбора <> Неопределено Тогда
			ФормаАдреса.НачальноеЗначениеВыбора.Регион = ФормаАдреса.НачальноеЗначениеВыбора.Регион;
		КонецЕсли;
		
		ФормаАдреса.Тип = Перечисления.ТипыКонтактнойИнформации.Адрес;
		ФормаАдреса.мВозвратСтруктуры = Истина;
		
		Результат = ФормаАдреса.ОткрытьМодально();
		
		Если Результат <> Неопределено Тогда
			
			Если ТипЗнч(Результат) = Тип("Булево") Тогда 
				
				Если Не Результат Тогда
					Возврат Неопределено;
				КонецЕсли;
				
			КонецЕсли;
			
			ВидКИ= Новый Структура;
			ВидКИ.Вставить("Тип", ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Адрес"));
			ВидКИ.Вставить("АдресТолькоРоссийский",        Истина);
			ВидКИ.Вставить("ВключатьСтрануВПредставление", Ложь);
			ВидКИ.Вставить("СкрыватьНеактуальныеАдреса",   Ложь);
	
			ВыбранноеЗначение = Новый Структура("КодСтраны,Поле1,КодРегиона,Поле3,Поле4,Поле5,Поле6,Поле7,Поле8,Поле9,ТипДома,ТипКорпуса,ТипКвартиры,Представление");
			ЗаполнитьЗначенияСвойств(ВыбранноеЗначение, ФормаАдреса);
						
			Представление = ВыбранноеЗначение.Представление;
			
			Если Оповещение<> Неопределено Тогда
				ВыполнитьОбработкуОповещения(Оповещение, ВыбранноеЗначение);
				Возврат Неопределено;
			ИначеЕсли ТипЗнч(Владелец) = Тип("ПолеФормы") Тогда 
				//разберем куда вернуть результат (дублирование обработки выбора)
				Пока ТипЗнч(Владелец) <> Тип("ФормаКлиентскогоПриложения") Цикл
					Владелец = Владелец.Родитель;
				КонецЦикла;
				ИмяОбъекта = "Объект";
				ИмяТабличнойЧасти = "";
				АдресСтрокой = "ПроизводственныйОбъектАдресСтрокой";
				АдресПоля = "ПроизводственныйОбъектАдрес";
				Если Владелец.ИмяФормы = "Документ.ЗаказНаЭмиссиюКодовМаркировкиСУЗ.Форма.ФормаДокумента"
						Или Владелец.ИмяФормы = "Документ.СписаниеКодовМаркировкиИСМП.Форма.ФормаДокумента" Тогда
				ИначеЕсли Владелец.ИмяФормы = "Документ.ВыводИзОборотаИСМП.Форма.ФормаДокумента" Тогда
					АдресСтрокой = "АдресПлощадкиСтрокой";
					АдресПоля = "АдресПлощадки";
				ИначеЕсли Владелец.ИмяФормы = "Документ.ОформлениеСДИЗЗЕРНО.Форма.ФормаДокумента" Тогда
					АдресСтрокой = "ПунктНазначенияСтрокой";
					АдресПоля = "ПунктНазначения";
				ИначеЕсли Владелец.ИмяФормы = "РегистрСведений.НастройкиОбменаСУЗ.Форма.ФормаЗаписи" Тогда
					ИмяОбъекта = "Запись";
				ИначеЕсли Владелец.ИмяФормы = "Справочник.КлассификаторОрганизацийЕГАИС.Форма.ФормаЭлемента" Тогда
					АдресСтрокой = "ПредставлениеАдреса";
					АдресПоля = "Адрес";
				ИначеЕсли Владелец.ИмяФормы = "Справочник.ДоговорыХраненияПартийЗЕРНО.Форма.ФормаЭлемента" Тогда
					АдресСтрокой = "МестоХраненияСтрокой";
					АдресПоля = "МестоХранения";
				ИначеЕсли Владелец.ИмяФормы = "Документ.ВнесениеСведенийОСобранномУрожаеЗЕРНО.Форма.ФормаДокумента" Тогда
					АдресСтрокой = "МестоХраненияСтрокой";
					АдресПоля = "МестоХранения";
				ИначеЕсли Владелец.ИмяФормы = "Справочник.ЗемельныеУчасткиИС.Форма.ФормаЭлемента" Тогда
					АдресСтрокой = "АдресСтрокой";
					АдресПоля = "Адрес";
				ИначеЕсли Владелец.ИмяФормы = "РегистрСведений.КегиНаОборудованииРозливаИСМП.Форма.ФормаПодключенияКегаКОборудованию" Тогда
					ИмяОбъекта = "";
					АдресСтрокой = "АдресПодключенияСтрокой";
					АдресПоля = "АдресПодключения";
				ИначеЕсли Владелец.ИмяФормы = "ОбщаяФорма.ПунктыМаршрутаЗЕРНО" Тогда
					АдресСтрокой = "АдресСтрокой";
					АдресПоля = "Адрес";
					ИмяОбъекта = "ТекущийЭлемент";
				ИначеЕсли Владелец.ИмяФормы = "Документ.ФормированиеПартийЗЕРНО.Форма.ФормаДокумента" Тогда
					Если Владелец.ТекущийЭлемент.Имя = "ТоварыБезРазбиенияМестоположение" 
						Или Владелец.ТекущийЭлемент.Имя = "ТоварыПанельМестоположение" Тогда
						ИмяОбъекта = "Элементы";
						ИмяТабличнойЧасти = "Товары";
						АдресСтрокой = "МестоположениеСтрокой";
						АдресПоля = "Местоположение";
					ИначеЕсли Владелец.ТекущийЭлемент.Имя = "ТоварыБезРазбиенияПроисхождение" Тогда
						ИмяОбъекта = "Элементы";
						ИмяТабличнойЧасти = "Товары";
						АдресСтрокой = "ПроисхождениеСтрокой";
						АдресПоля = "Происхождение";
					Иначе
						ИмяОбъекта = "ТекущийЭлемент";
						АдресСтрокой = "ПроисхождениеСтрокой";
						АдресПоля = "Происхождение";
					КонецЕсли;
				КонецЕсли;
				Объект = ?(ИмяОбъекта = "", Владелец, Владелец[ИмяОбъекта]);
				Если ТипЗнч(Владелец["ТекущийЭлемент"]) = Тип("ТаблицаФормы") Тогда
					Объект = Объект.ТекущиеДанные;
				КонецЕсли;
				Если ИмяТабличнойЧасти <> "" Тогда
					Объект = Объект[ИмяТабличнойЧасти].ТекущиеДанные;
				КонецЕсли;
				Объект[АдресСтрокой] = Представление;
				Объект[АдресПоля]    = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияПоПредставлению(Представление, ВидКИ);
				Владелец.Модифицированность = Истина;
				Возврат Неопределено;
			Иначе 
				Возврат Представление;
			КонецЕсли;
		Иначе
			
			Возврат Неопределено;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции

// Конструктор для структуры параметров открытия формы контактной информации.
// Состав полей может быть расширен в общем модуле РаботаСАдресамиКлиент свойствами с национальной спецификой.
//
// Параметры:
//  ВидКонтактнойИнформации  - СправочникСсылка.ВидыКонтактнойИнформации - вид редактируемой контактной информации.
//                           - Структура - см. УправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации
//  Значение                 - Строка - сериализованное значение полей контактной информации в формате JSON или XML.
//  Представление            - Строка - представление контактной информации.
//  Комментарий              - Строка - комментарий контактной информации.
//  ТипКонтактнойИнформации  - ПеречислениеСсылка.ТипыКонтактнойИнформации - тип контактной информации.
//                             Если указан, то в возвращаемую структуру добавляются поля соответствующие типу.
// 
// Возвращаемое значение:
//  Структура:
//   * ВидКонтактнойИнформации - см. УправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации
//   * ТолькоПросмотр          - Булево - если Истина, то форма будет открыта в режиме только просмотра.
//   * Значение                - Строка - значение полей контактной информации в формате JSON или ХML.
//   * Представление           - Строка - представление контактной информации.
//   * ТипКонтактнойИнформации - ПеречислениеСсылка.ТипыКонтактнойИнформации - тип контактной информации, если был указан
//                                                                            в параметрах.
//   * Страна                  - Строка - страна мира, только если указан тип контактной информации Адрес.
//   * Регион                  - Строка - значение поля региона, только если указан тип контактной информации Адрес.
//                                       Актуально для стран ЕАЭС.
//   * Индекс                  - Строка - почтовый индекс, только если указан тип контактной информации Адрес.
//   * ТипПомещения            - Строка - тип помещения в форме ввода нового адреса, , только если указан тип контактной
//                                       информации Адрес.
//   * КодСтраны               - Строка - телефонный код страны мира, только если указан тип контактной информации Телефон.
//   * КодГорода               - Строка - телефонный код города, только если указан тип контактной информации Телефон.
//   * НомерТелефона           - Строка - телефонный номер, только если указан тип контактной информации Телефон.
//   * Добавочный              - Строка - добавочный телефонный номер, только если указан тип контактной информации Телефон.
//   * Заголовок               - Строка - заголовок формы. По умолчанию, представление вида контактной информации.
//   * ТипАдреса               - Строка - варианты: Пустая строка (по умолчанию), "ВСвободнойФорме", "ЕАЭС";
//                                       Для РФ: "Муниципальный" или "АдминистративноТерриториальный".
//                                       Если не указан (пустая строка), то для существующего адреса будет установлен адрес
//                                       выбранный пользователем в форме ввода адреса, для нового адреса - Муниципальный".
//
Функция ПараметрыФормыКонтактнойИнформации(ВидКонтактнойИнформации, Значение,
	Представление = Неопределено, Комментарий = Неопределено, ТипКонтактнойИнформации = Неопределено) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВидКонтактнойИнформации", ВидКонтактнойИнформации);
	ПараметрыФормы.Вставить("ТолькоПросмотр", Ложь);
	ПараметрыФормы.Вставить("Значение", Значение);
	ПараметрыФормы.Вставить("Представление", Представление);
	ПараметрыФормы.Вставить("Комментарий", Комментарий);
	ПараметрыФормы.Вставить("ТипАдреса", "");
	Если ТипКонтактнойИнформации <> Неопределено Тогда
		ПараметрыФормы.Вставить("ТипКонтактнойИнформации", ТипКонтактнойИнформации);
		Если ТипКонтактнойИнформации = ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Адрес") Тогда
			ПараметрыФормы.Вставить("Страна");
			ПараметрыФормы.Вставить("Регион");
			ПараметрыФормы.Вставить("Индекс");
			ПараметрыФормы.Вставить("ТипПомещения", "Квартира");
		ИначеЕсли ТипКонтактнойИнформации = ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Телефон") Тогда
			ПараметрыФормы.Вставить("КодСтраны");
			ПараметрыФормы.Вставить("КодГорода");
			ПараметрыФормы.Вставить("НомерТелефона");
			ПараметрыФормы.Вставить("Добавочный");
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(ВидКонтактнойИнформации) = Тип("Структура") И ВидКонтактнойИнформации.Свойство("Наименование") Тогда
		ПараметрыФормы.Вставить("Заголовок", ВидКонтактнойИнформации.Наименование);
	Иначе
		ПараметрыФормы.Вставить("Заголовок", Строка(ВидКонтактнойИнформации));
	КонецЕсли;
	
	Возврат ПараметрыФормы;
	
КонецФункции

