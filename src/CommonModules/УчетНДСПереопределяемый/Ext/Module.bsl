
#Область СлужебныйПрограммныйИнтерфейс

Процедура ДополнитьИнформациейОПосредникахИПлатежах(Знач ДополнительнаяИнформация, Знач СтруктураПараметров, Запрос) Экспорт

	Запрос.Текст = ТекстЗапросаРеквизитыКонтрагентовДокументовОплаты();
	Запрос.УстановитьПараметр("Постановление1137_2019", СтруктураПараметров.Постановление1137_2019);
	СтрокаСФ = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Запрос.МенеджерВременныхТаблиц = Неопределено;

	Пока СтрокаСФ.Следующий() Цикл
		
		Если НЕ ЗначениеЗаполнено(СтрокаСФ.СчетФактураДокумент) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаГрафа9 = "";
		СтрокаГрафа10 = "";
		МассивСвПрод = Новый Массив();
		МассивПлатежныеДокументы = Новый Массив();
		
		СтрокаДетальнойИнформации = СтрокаСФ.Выбрать(ОбходРезультатаЗапроса.Прямой);
		
		Пока СтрокаДетальнойИнформации.Следующий() Цикл 
			
			Если ЗначениеЗаполнено(СтрокаДетальнойИнформации.ДатаДокументаОплаты)
				И ЗначениеЗаполнено(СтрокаДетальнойИнформации.НомерДокументаОплаты) Тогда
				
				РеквизитыПлатежногоДокумента = Новый Структура("НомерДокументаОплаты,ДатаДокументаОплаты");
				ЗаполнитьЗначенияСвойств(РеквизитыПлатежногоДокумента, СтрокаДетальнойИнформации);
				МассивПлатежныеДокументы.Добавить(РеквизитыПлатежногоДокумента);
			КонецЕсли; 
			
			Если ЗначениеЗаполнено(СтрокаДетальнойИнформации.Продавец) Тогда
				СтрокаГрафа9 = ?(ПустаяСтрока(СтрокаГрафа9), "", СтрокаГрафа9 + ";" + Символы.ПС) 
				+ СокрЛП(СтрокаДетальнойИнформации.Продавец);
				
				СтрокаГрафа10 = ?(ПустаяСтрока(СтрокаГрафа10), "", СтрокаГрафа10 + ";" + Символы.ПС) 
				+ СокрЛП(СтрокаДетальнойИнформации.ИНН) 
				+ ?(ПустаяСтрока(СтрокаДетальнойИнформации.КПП), "", "/" + СокрЛП(СтрокаДетальнойИнформации.КПП));
				
				СтруктураРеквизитовКонтрагента = Новый Структура();
				
				Если СтрокаДетальнойИнформации.ЮридическоеФизическоеЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
					СтруктураРеквизитовКонтрагента.Вставить("ИННФЛ", СтрокаДетальнойИнформации.ИНН);
				Иначе
					СтруктураРеквизитовКонтрагента.Вставить("ИННЮЛ", СтрокаДетальнойИнформации.ИНН);
					СтруктураРеквизитовКонтрагента.Вставить("КПП",   СтрокаДетальнойИнформации.КПП);
				КонецЕсли;
				
				СтруктураРеквизитовКонтрагента.Вставить("КонтрагентНаименование", СтрокаДетальнойИнформации.Продавец);
				СтруктураРеквизитовКонтрагента.Вставить("Контрагент",             СтрокаДетальнойИнформации.ПродавецСсылка);
				МассивСвПрод.Добавить(СтруктураРеквизитовКонтрагента);
				
			КонецЕсли;
			
		КонецЦикла;
		
		СтрокаДопИнформации = ДополнительнаяИнформация.Найти(СтрокаСФ.СчетФактураДокумент, "Ссылка");
		Если СтрокаДопИнформации = Неопределено Тогда
			СтрокаДопИнформации        = ДополнительнаяИнформация.Добавить();
			СтрокаДопИнформации.Ссылка = СтрокаСФ.СчетФактураДокумент;
		КонецЕсли;
		
		СтрокаДопИнформации.Графа9  = СтрокаГрафа9;
		СтрокаДопИнформации.Графа10 = СтрокаГрафа10;
		СтрокаДопИнформации.МассивСвПрод = МассивСвПрод;
		СтрокаДопИнформации.МассивПлатежныеДокументы = МассивПлатежныеДокументы;
		
	КонецЦикла;

КонецПроцедуры

Процедура ДополнитьИнформациейОРНПТ(Знач ДополнительнаяИнформация, Знач СтруктураПараметров, Запрос, ПоискРНПТПоОснованию = Ложь) Экспорт

	Запрос.Текст = ТекстЗапросаДанныеОРНПТДляОтчетности(ПоискРНПТПоОснованию);
	Запрос.УстановитьПараметр("ОтражениеВОтчетности", СтруктураПараметров.ПорядокОтраженияВОтчетностиПоПрослеживаемости);
	Запрос.УстановитьПараметр("Организация",          СтруктураПараметров.СписокОрганизаций);
	Запрос.МенеджерВременныхТаблиц = СтруктураПараметров.МенеджерВременныхТаблиц;
	ВыборкаПоДокументам = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Пока ВыборкаПоДокументам.Следующий() Цикл
		
		Если НЕ ЗначениеЗаполнено(ВыборкаПоДокументам.СчетФактура) Тогда
			Продолжить;
		КонецЕсли;
		
		МассивСвРегНом = Новый Массив();
		ВыборкаПоРНПТ = ВыборкаПоДокументам.Выбрать(ОбходРезультатаЗапроса.Прямой);
		СтруктураШапкиДокумента = ОбщегоНазначения.СформироватьСтруктуруШапкиДокумента(ВыборкаПоДокументам.СчетФактура.ПолучитьОбъект());
		
		Пока ВыборкаПоРНПТ.Следующий() Цикл 
			
			Если ЗначениеЗаполнено(ВыборкаПоРНПТ.РегНомПросл) Тогда
				
				СтруктураСвРегНом = Новый Структура("РегНомПросл,КолТовПросл,СтоимТовПросл,ОКЕИ");
				ЗаполнитьЗначенияСвойств(СтруктураСвРегНом, ВыборкаПоРНПТ);
				
				Если НЕ СтруктураШапкиДокумента.ВалютаДокумента = СтруктураШапкиДокумента.ВалютаРегламентированногоУчета Тогда
					Данные = МодульВалютногоУчета.ПолучитьКурсВалюты(СтруктураШапкиДокумента.ВалютаРегламентированногоУчета, СтруктураШапкиДокумента.Дата);
					
					СтруктураСвРегНом.СтоимТовПросл = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СтруктураСвРегНом.СтоимТовПросл, СтруктураШапкиДокумента.ВалютаДокумента,
					СтруктураШапкиДокумента.ВалютаРегламентированногоУчета,
					СтруктураШапкиДокумента.КурсДокумента,
					Данные.Курс,
					СтруктураШапкиДокумента.КратностьДокумента,
					Данные.Кратность);
				КонецЕсли;
				
				МассивСвРегНом.Добавить(СтруктураСвРегНом);
				
			КонецЕсли;
			
		КонецЦикла;
		
		СтрокаДопИнформации = ДополнительнаяИнформация.Найти(ВыборкаПоДокументам.СчетФактура, "Ссылка");
		Если СтрокаДопИнформации = Неопределено Тогда
			СтрокаДопИнформации        = ДополнительнаяИнформация.Добавить();
			СтрокаДопИнформации.Ссылка = ВыборкаПоДокументам.СчетФактура;
		КонецЕсли;
		СтрокаДопИнформации.СвРегНом = МассивСвРегНом;
		
	КонецЦикла;

КонецПроцедуры

Процедура ДополнитьИнформациейОТНВЭД(Знач ДополнительнаяИнформация, Знач Запрос) Экспорт

	Запрос.Текст = ТекстЗапросаИнформацияПоТНВЭД();
	
	СтрокаСФ = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Пока СтрокаСФ.Следующий() Цикл
		
		Если НЕ ЗначениеЗаполнено(СтрокаСФ.СчетФактура) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаДопИнформации = ДополнительнаяИнформация.Найти(СтрокаСФ.СчетФактура, "Ссылка");
		Если СтрокаДопИнформации = Неопределено Тогда
			СтрокаДопИнформации        = ДополнительнаяИнформация.Добавить();
			СтрокаДопИнформации.Ссылка = СтрокаСФ.СчетФактура;
		КонецЕсли;
		
		СтрокаДетальныеЗаписи = СтрокаСФ.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока СтрокаДетальныеЗаписи.Следующий() Цикл 
			Если ЗначениеЗаполнено(СтрокаДетальныеЗаписи.КодТНВЭД)
				И Найти(СтрокаДопИнформации.ТНВЭД, СокрЛП(СтрокаДетальныеЗаписи.КодТНВЭД)) = 0 Тогда // Исключаем дубли ТНВЭД
				СтрокаДопИнформации.ТНВЭД = СтрокаДопИнформации.ТНВЭД 
				+ ?(ПустаяСтрока(СтрокаДопИнформации.ТНВЭД ), "", ";") + СокрЛП(СтрокаДетальныеЗаписи.КодТНВЭД);
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;

КонецПроцедуры

Процедура ДополнитьПокупкиИнформациейОНомерахДекларацийНаТовары(Знач ДополнительнаяИнформация, Знач СтруктураПараметров, Запрос) Экспорт

	Запрос.Текст = ТекстЗапросаИнформацияПоТаможеннымДекларациямПокупки();
	СтрокаСФ = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Пока СтрокаСФ.Следующий() Цикл
		
		Если НЕ ЗначениеЗаполнено(СтрокаСФ.СчетФактура) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаИнформации = "";
		
		СтрокаДопИнформации = ДополнительнаяИнформация.Найти(СтрокаСФ.СчетФактура, "Ссылка");
		Если СтрокаДопИнформации = Неопределено Тогда
			СтрокаДопИнформации        = ДополнительнаяИнформация.Добавить();
			СтрокаДопИнформации.Ссылка = СтрокаСФ.СчетФактура;
		КонецЕсли;
		
		СтрокаСтраныПроисхождения = СтрокаСФ.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока СтрокаСтраныПроисхождения.Следующий() Цикл
			СтрокаИнформации = СтрокаИнформации 
			+ ?(ПустаяСтрока(СтрокаИнформации), "", Символы.ПС) + СокрЛП(СтрокаСтраныПроисхождения.СтранаПроисхождения);
			ГТДРез = "";
			СтрокаНомерГТД = СтрокаСтраныПроисхождения.Выбрать(ОбходРезультатаЗапроса.Прямой);
			Пока СтрокаНомерГТД.Следующий() Цикл 
				ГТДРез = ГТДРез + ?(ГТДРез = "","",", ") + СокрЛП(СтрокаНомерГТД.НомерГТД);
				
				Если Найти(СтрокаДопИнформации.ГТД, СокрЛП(СтрокаНомерГТД.НомерГТД)) = 0 Тогда // Исключаем дубли номеров ГТД
					СтрокаДопИнформации.ГТД = СтрокаДопИнформации.ГТД 
					+ ?(ПустаяСтрока(СтрокаДопИнформации.ГТД), "", ";") + СокрЛП(СтрокаНомерГТД.НомерГТД);
				КонецЕсли;
				
				Если Найти(СтрокаДопИнформации.РегистрационныйНомерТД, СокрЛП(СтрокаНомерГТД.РегистрационныйНомерТД)) = 0 Тогда
					СтрокаДопИнформации.РегистрационныйНомерТД = СтрокаДопИнформации.РегистрационныйНомерТД
					+ ?(ПустаяСтрока(СтрокаДопИнформации.РегистрационныйНомерТД), "", ";")
					+ СокрЛП(СтрокаНомерГТД.РегистрационныйНомерТД);
				КонецЕсли;
				
			КонецЦикла;
			СтрокаИнформации = СтрокаИнформации 
			+ ?(ПустаяСтрока(СтрокаИнформации), "", ?(ПустаяСтрока(ГТДРез), "", ";")) + ГТДРез;
		КонецЦикла;
		СтрокаДопИнформации.ГТДиСтрана = СтрокаИнформации;
	КонецЦикла;
	
КонецПроцедуры

Процедура ДополнитьПродажиИнформациейОНомерахДекларацийНаТовары(Знач ДополнительнаяИнформация, Знач СтруктураПараметров, Запрос) Экспорт

	Запрос.Текст = ТекстЗапросаИнформацияПоТаможеннымДекларациямПродажи();
	ВыборкаПоДокументам = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Пока ВыборкаПоДокументам.Следующий() Цикл
		
		Если НЕ ЗначениеЗаполнено(ВыборкаПоДокументам.СчетФактура) Тогда
			Продолжить;
		КонецЕсли;
		
		МассивСвРегНом = Новый Массив();
		ВыборкаПоРегНомерамТД = ВыборкаПоДокументам.Выбрать(ОбходРезультатаЗапроса.Прямой);
		
		Пока ВыборкаПоРегНомерамТД.Следующий() Цикл 
			
			Если ЗначениеЗаполнено(ВыборкаПоРегНомерамТД.РегистрационныйНомер) Тогда
				
				СтруктураСвРегНом = Новый Структура("РегНомПросл,КолТовПросл,СтоимТовПросл,ОКЕИ");
				СтруктураСвРегНом.РегНомПросл = ВыборкаПоРегНомерамТД.РегистрационныйНомер;
				МассивСвРегНом.Добавить(СтруктураСвРегНом);
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если Не ЗначениеЗаполнено(МассивСвРегНом) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаДопИнформации = ДополнительнаяИнформация.Найти(ВыборкаПоДокументам.СчетФактура, "Ссылка");
		Если СтрокаДопИнформации = Неопределено Тогда
			СтрокаДопИнформации        = ДополнительнаяИнформация.Добавить();
			СтрокаДопИнформации.Ссылка = ВыборкаПоДокументам.СчетФактура;
		КонецЕсли;
		СтрокаДопИнформации.СвРегНомТД = МассивСвРегНом;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПреобразоватьПоказателиРНПТКСторнирующейЗаписи(Знач МассивСвРегНом) Экспорт

	Перем ЭлементМассива;

	Для Каждого ЭлементМассива Из МассивСвРегНом Цикл
		Если ЗначениеЗаполнено(ЭлементМассива.КолТовПросл) 
			Или ЗначениеЗаполнено(ЭлементМассива.СтоимТовПросл) Тогда // Для обычных товаров количество и сумма = Неопределено
			ЭлементМассива.КолТовПросл   = - ЭлементМассива.КолТовПросл;
			ЭлементМассива.СтоимТовПросл = - ЭлементМассива.СтоимТовПросл;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаДанныеОРНПТДляОтчетности(ПоискРНПТПоОснованию)

	Если ПоискРНПТПоОснованию Тогда
	
		Возврат
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СписокСчетовФактур.СчетФактура КАК СчетФактура,
		|	ОперацииСПрослеживаемымиТоварами.РНПТ.Код КАК РегНомПросл,
		|	ОперацииСПрослеживаемымиТоварами.КоличествоПрослеживаемости КАК КолТовПросл,
		|	ОперацииСПрослеживаемымиТоварами.СуммаБезНДС КАК СтоимТовПросл,
		|	ЕСТЬNULL(КлассификаторТНВЭД.ЕдиницаИзмерения.Код, """") КАК ОКЕИ,
		|	1 КАК КоличествоЭлементов
		|ИЗ
		|	СписокСчетовФактур КАК СписокСчетовФактур
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОперацииСПрослеживаемымиТоварами КАК ОперацииСПрослеживаемымиТоварами
		|		ПО СписокСчетовФактур.ДокументОснование = ОперацииСПрослеживаемымиТоварами.ДокументОперации
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторТНВЭД КАК КлассификаторТНВЭД
		|		ПО (ОперацииСПрослеживаемымиТоварами.Номенклатура.КодТНВЭД = КлассификаторТНВЭД.Ссылка)
		|ГДЕ
		|	ОперацииСПрослеживаемымиТоварами.Организация В(&Организация)
		|	И ОперацииСПрослеживаемымиТоварами.ОтражениеВОтчетности = &ОтражениеВОтчетности
		|ИТОГИ
		|	СУММА(КоличествоЭлементов)
		|ПО
		|	СчетФактура";
		
	Иначе
	
		Возврат
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СписокСчетовФактур.СчетФактура КАК СчетФактура,
		|	ОперацииСПрослеживаемымиТоварами.РНПТ.Код КАК РегНомПросл,
		|	ОперацииСПрослеживаемымиТоварами.КоличествоПрослеживаемости КАК КолТовПросл,
		|	ОперацииСПрослеживаемымиТоварами.СуммаБезНДС КАК СтоимТовПросл,
		|	ЕСТЬNULL(КлассификаторТНВЭД.ЕдиницаИзмерения.Код, """") КАК ОКЕИ,
		|	1 КАК КоличествоЭлементов
		|ИЗ
		|	СписокСчетовФактур КАК СписокСчетовФактур
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОперацииСПрослеживаемымиТоварами КАК ОперацииСПрослеживаемымиТоварами
		|		ПО СписокСчетовФактур.СчетФактура = ОперацииСПрослеживаемымиТоварами.ДокументОперации
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторТНВЭД КАК КлассификаторТНВЭД
		|		ПО (ОперацииСПрослеживаемымиТоварами.Номенклатура.КодТНВЭД = КлассификаторТНВЭД.Ссылка)
		|ГДЕ
		|	ОперацииСПрослеживаемымиТоварами.Организация В(&Организация)
		|	И ОперацииСПрослеживаемымиТоварами.ОтражениеВОтчетности = &ОтражениеВОтчетности
		|ИТОГИ
		|	СУММА(КоличествоЭлементов)
		|ПО
		|	СчетФактура";
		
	КонецЕсли;

КонецФункции

Функция ТекстЗапросаИнформацияПоТаможеннымДекларациямПокупки()
	
	Возврат
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаТоваров.Ссылка КАК СчетФактура,
	|	ТаблицаТоваров.СерияНоменклатуры.СтранаПроисхождения.Код КАК СтранаПроисхождения,
	|	ТаблицаТоваров.СерияНоменклатуры.НомерГТД.Код КАК НомерГТД,
	|	ТаблицаТоваров.СерияНоменклатуры.НомерГТД.РегистрационныйНомер КАК РегистрационныйНомерТД,
	|	СУММА(1) КАК КоличествоЭлементов
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Товары КАК ТаблицаТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокСчетовФактур КАК СписокСчетовФактур
	|		ПО ТаблицаТоваров.Ссылка = СписокСчетовФактур.СчетФактура
	|ГДЕ
	|	НЕ ТаблицаТоваров.ПрослеживаемыйТовар
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Ссылка,
	|	ТаблицаТоваров.СерияНоменклатуры.СтранаПроисхождения,
	|	ТаблицаТоваров.СерияНоменклатуры.НомерГТД,
	|	ТаблицаТоваров.СерияНоменклатуры.НомерГТД.Код,
	|	ТаблицаТоваров.СерияНоменклатуры.СтранаПроисхождения.Код,
	|	ТаблицаТоваров.СерияНоменклатуры.НомерГТД.РегистрационныйНомер
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка,
	|	ТаблицаТоваров.СтранаПроисхождения.Код,
	|	ТаблицаТоваров.НомерГТД.Код,
	|	ТаблицаТоваров.НомерГТД.РегистрационныйНомер,
	|	СУММА(1)
	|ИЗ
	|	Документ.ОтражениеПоступленияТоваровИУслугНДС.ТоварыИУслуги КАК ТаблицаТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокСчетовФактур КАК СписокСчетовФактур
	|		ПО ТаблицаТоваров.Ссылка = СписокСчетовФактур.СчетФактура
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Ссылка,
	|	ТаблицаТоваров.СтранаПроисхождения,
	|	ТаблицаТоваров.НомерГТД,
	|	ТаблицаТоваров.НомерГТД.Код,
	|	ТаблицаТоваров.СтранаПроисхождения.Код,
	|	ТаблицаТоваров.НомерГТД.РегистрационныйНомер
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка,
	|	ТаблицаТоваров.СерияНоменклатуры.СтранаПроисхождения.Код,
	|	ТаблицаТоваров.СерияНоменклатуры.НомерГТД.Код,
	|	ТаблицаТоваров.СерияНоменклатуры.НомерГТД.РегистрационныйНомер,
	|	СУММА(1)
	|ИЗ
	|	Документ.КорректировкаПоступления.Товары КАК ТаблицаТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокСчетовФактур КАК СписокСчетовФактур
	|		ПО ТаблицаТоваров.Ссылка = СписокСчетовФактур.СчетФактура
	|ГДЕ
	|	ТаблицаТоваров.Ссылка.ВидОперации <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение)
	|	И НЕ ТаблицаТоваров.Ссылка.ИсправляемыйДокументПоступления ССЫЛКА Документ.КорректировкаПоступления
	|	И НЕ ТаблицаТоваров.ПрослеживаемыйТовар
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Ссылка,
	|	ТаблицаТоваров.СерияНоменклатуры.СтранаПроисхождения,
	|	ТаблицаТоваров.СерияНоменклатуры.НомерГТД,
	|	ТаблицаТоваров.СерияНоменклатуры.НомерГТД.Код,
	|	ТаблицаТоваров.СерияНоменклатуры.СтранаПроисхождения.Код,
	|	ТаблицаТоваров.СерияНоменклатуры.НомерГТД.РегистрационныйНомер
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка,
	|	ТаблицаТоваров.СерияНоменклатуры.СтранаПроисхождения.Код,
	|	ТаблицаТоваров.СерияНоменклатуры.НомерГТД.Код,
	|	ТаблицаТоваров.СерияНоменклатуры.НомерГТД.РегистрационныйНомер,
	|	СУММА(1)
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя.Товары КАК ТаблицаТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокСчетовФактур КАК СписокСчетовФактур
	|		ПО ТаблицаТоваров.Ссылка = СписокСчетовФактур.СчетФактура
	|ГДЕ
	|	НЕ ТаблицаТоваров.ПрослеживаемыйТовар
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Ссылка,
	|	ТаблицаТоваров.СерияНоменклатуры.СтранаПроисхождения,
	|	ТаблицаТоваров.СерияНоменклатуры.НомерГТД,
	|	ТаблицаТоваров.СерияНоменклатуры.НомерГТД.Код,
	|	ТаблицаТоваров.СерияНоменклатуры.СтранаПроисхождения.Код,
	|	ТаблицаТоваров.СерияНоменклатуры.НомерГТД.РегистрационныйНомер
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТоваров.СчетФактура,
	|	ТаблицаТоваров.СерияНоменклатуры.СтранаПроисхождения.Код,
	|	ТаблицаТоваров.СерияНоменклатуры.НомерГТД.Код,
	|	ТаблицаТоваров.СерияНоменклатуры.НомерГТД.РегистрационныйНомер,
	|	СУММА(1)
	|ИЗ
	|	Документ.АвансовыйОтчет.Товары КАК ТаблицаТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокСчетовФактур КАК СписокСчетовФактур
	|		ПО (ВЫБОР
	|				КОГДА СписокСчетовФактур.СчетФактура ССЫЛКА Документ.СчетФактураПолученный
	|					ТОГДА ВЫРАЗИТЬ(СписокСчетовФактур.СчетФактура КАК Документ.СчетФактураПолученный).ДокументОснование = ТаблицаТоваров.Ссылка
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.СчетФактура,
	|	ТаблицаТоваров.СерияНоменклатуры.СтранаПроисхождения,
	|	ТаблицаТоваров.СерияНоменклатуры.НомерГТД,
	|	ТаблицаТоваров.СерияНоменклатуры.НомерГТД.Код,
	|	ТаблицаТоваров.СерияНоменклатуры.СтранаПроисхождения.Код,
	|	ТаблицаТоваров.СерияНоменклатуры.НомерГТД.РегистрационныйНомер
	|ИТОГИ
	|	СУММА(КоличествоЭлементов)
	|ПО
	|	СчетФактура,
	|	СтранаПроисхождения";

КонецФункции

Функция ТекстЗапросаИнформацияПоТаможеннымДекларациямПродажи()
	
	Возврат
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаТоваров.Ссылка КАК СчетФактура,
	|	ТаблицаТоваров.СерияНоменклатуры.НомерГТД.РегистрационныйНомер КАК РегистрационныйНомер,
	|	1 КАК КоличествоЭлементов
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК ТаблицаТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокСчетовФактур КАК СписокСчетовФактур
	|		ПО ТаблицаТоваров.Ссылка = СписокСчетовФактур.СчетФактура
	|ГДЕ
	|	НЕ ТаблицаТоваров.ПрослеживаемыйТовар
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка,
	|	ТаблицаТоваров.СерияНоменклатуры.НомерГТД.РегистрационныйНомер,
	|	1
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах.Товары КАК ТаблицаТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокСчетовФактур КАК СписокСчетовФактур
	|		ПО ТаблицаТоваров.Ссылка = СписокСчетовФактур.СчетФактура
	|ГДЕ
	|	НЕ ТаблицаТоваров.ПрослеживаемыйТовар
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СписокСчетовФактур.СчетФактура,
	|	ТаблицаТоваров.СерияНоменклатуры.НомерГТД.РегистрационныйНомер,
	|	1
	|ИЗ
	|	Документ.ОтчетКомиссионераОПродажах.Товары КАК ТаблицаТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионераОПродажах.Покупатели КАК ТаблицаПокупатели
	|		ПО ТаблицаТоваров.КлючСтроки = ТаблицаПокупатели.КлючСтроки
	|			И ТаблицаТоваров.Ссылка = ТаблицаПокупатели.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокСчетовФактур КАК СписокСчетовФактур
	|		ПО (ТаблицаПокупатели.СчетФактура = СписокСчетовФактур.СчетФактура)
	|ГДЕ
	|	НЕ ТаблицаТоваров.ПрослеживаемыйТовар
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка,
	|	ТаблицаТоваров.СерияНоменклатуры.НомерГТД.РегистрационныйНомер,
	|	1
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК ТаблицаТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокСчетовФактур КАК СписокСчетовФактур
	|		ПО ТаблицаТоваров.Ссылка = СписокСчетовФактур.СчетФактура
	|ГДЕ
	|	ТаблицаТоваров.Ссылка.ВидОперации <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение)
	|	И НЕ ТаблицаТоваров.Ссылка.ИсправляемыйДокументРеализации ССЫЛКА Документ.КорректировкаРеализации
	|	И НЕ ТаблицаТоваров.ПрослеживаемыйТовар
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка,
	|	ТаблицаТоваров.СерияНоменклатуры.НомерГТД.РегистрационныйНомер,
	|	1
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК ТаблицаТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокСчетовФактур КАК СписокСчетовФактур
	|		ПО ТаблицаТоваров.Ссылка = СписокСчетовФактур.СчетФактура
	|ГДЕ
	|	ТаблицаТоваров.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение)
	|	И ТаблицаТоваров.Сумма > ТаблицаТоваров.СуммаДоИзменения
	|	И НЕ ТаблицаТоваров.ПрослеживаемыйТовар
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка,
	|	ТаблицаТоваров.СерияНоменклатуры.НомерГТД.РегистрационныйНомер,
	|	1
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК ТаблицаТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокСчетовФактур КАК СписокСчетовФактур
	|		ПО ТаблицаТоваров.Ссылка = СписокСчетовФактур.СчетФактура
	|ГДЕ
	|	ТаблицаТоваров.Ссылка.ВидОперации <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение)
	|	И ТаблицаТоваров.Ссылка.ИсправляемыйДокументРеализации ССЫЛКА Документ.КорректировкаРеализации
	|	И ТаблицаТоваров.Сумма > ТаблицаТоваров.СуммаДоИзменения
	|	И НЕ ТаблицаТоваров.ПрослеживаемыйТовар
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка,
	|	ТаблицаТоваров.СерияНоменклатуры.НомерГТД.РегистрационныйНомер,
	|	1
	|ИЗ
	|	Документ.ВозвратТоваровПоставщику.Товары КАК ТаблицаТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокСчетовФактур КАК СписокСчетовФактур
	|		ПО ТаблицаТоваров.Ссылка = СписокСчетовФактур.СчетФактура
	|ГДЕ
	|	НЕ ТаблицаТоваров.ПрослеживаемыйТовар
	|ИТОГИ
	|	СУММА(КоличествоЭлементов)
	|ПО
	|	СчетФактура";

КонецФункции

Функция ТекстЗапросаИнформацияПоТНВЭД()
	
	Возврат
	"ВЫБРАТЬ
	|	СписокСчетовФактур.СчетФактура КАК СчетФактура
	|ПОМЕСТИТЬ СчетаФактурыВЕАЭС
	|ИЗ
	|	СписокСчетовФактур КАК СписокСчетовФактур
	|ГДЕ
	|	СписокСчетовФактур.Экспорт
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаТоваров.Ссылка КАК СчетФактура,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
	|			ТОГДА ТаблицаТоваров.КодТНВЭД.Код
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК КодТНВЭД,
	|	СУММА(1) КАК КоличествоЭлементов
	|ИЗ
	|	Документ.ВозвратТоваровПоставщику.Товары КАК ТаблицаТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СчетаФактурыВЕАЭС КАК СчетаФактуры
	|		ПО ТаблицаТоваров.Ссылка = СчетаФактуры.СчетФактура
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Ссылка,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
	|			ТОГДА ТаблицаТоваров.КодТНВЭД.Код
	|		ИНАЧЕ """"
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
	|			ТОГДА ТаблицаТоваров.КодТНВЭД.Код
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	СУММА(1)
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК ТаблицаТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СчетаФактурыВЕАЭС КАК СчетаФактуры
	|		ПО ТаблицаТоваров.Ссылка = СчетаФактуры.СчетФактура
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Ссылка,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
	|			ТОГДА ТаблицаТоваров.КодТНВЭД.Код
	|		ИНАЧЕ """"
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтчетКомиссионераОПродажахПокупатели.СчетФактура,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
	|			ТОГДА ТаблицаТоваров.КодТНВЭД.Код
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	СУММА(1)
	|ИЗ
	|	СчетаФактурыВЕАЭС КАК СчетаФактуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионераОПродажах.Покупатели КАК ОтчетКомиссионераОПродажахПокупатели
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионераОПродажах.Товары КАК ТаблицаТоваров
	|			ПО ОтчетКомиссионераОПродажахПокупатели.КлючСтроки = ТаблицаТоваров.КлючСтроки
	|				И ОтчетКомиссионераОПродажахПокупатели.Ссылка = ТаблицаТоваров.Ссылка
	|		ПО (ОтчетКомиссионераОПродажахПокупатели.СчетФактура = СчетаФактуры.СчетФактура)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
	|			ТОГДА ТаблицаТоваров.КодТНВЭД.Код
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	ОтчетКомиссионераОПродажахПокупатели.СчетФактура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
	|			ТОГДА ТаблицаТоваров.КодТНВЭД.Код
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	СУММА(1)
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК ТаблицаТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СчетаФактурыВЕАЭС КАК СчетаФактуры
	|		ПО ТаблицаТоваров.Ссылка = СчетаФактуры.СчетФактура
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Ссылка,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
	|			ТОГДА ТаблицаТоваров.КодТНВЭД.Код
	|		ИНАЧЕ """"
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СчетаФактурыВЕАЭС";

КонецФункции

Функция ТекстЗапросаРеквизитыКонтрагентовДокументовОплаты()
	
	Возврат
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СчетФактураПолученный.Ссылка КАК СчетФактураДокумент,
	|	ВЫБОР
	|		КОГДА ПОДСТРОКА(СчетФактураПолученный.Контрагент.НаименованиеПолное, 1, 250) = """"
	|			ТОГДА СчетФактураПолученный.Контрагент.Наименование
	|		ИНАЧЕ ПОДСТРОКА(СчетФактураПолученный.Контрагент.НаименованиеПолное, 1, 250)
	|	КОНЕЦ КАК Продавец,
	|	СчетФактураПолученный.Контрагент.ИНН КАК ИНН,
	|	СчетФактураПолученный.Контрагент.КПП КАК КПП,
	|	НЕОПРЕДЕЛЕНО КАК ДатаДокументаОплаты,
	|	НЕОПРЕДЕЛЕНО КАК НомерДокументаОплаты,
	|	СУММА(1) КАК КоличествоЭлементов,
	|	СчетФактураПолученный.Контрагент.ЮрФизЛицо КАК ЮридическоеФизическоеЛицо,
	|	СчетФактураПолученный.Контрагент КАК ПродавецСсылка
	|ИЗ
	|	Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученныйДокументыОснования
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокСчетовФактур КАК СписокСчетовФактур
	|		ПО СчетФактураПолученныйДокументыОснования.ДокументОснование = СписокСчетовФактур.СчетФактура
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный КАК СчетФактураПолученный
	|		ПО СчетФактураПолученныйДокументыОснования.Ссылка = СчетФактураПолученный.Ссылка
	|ГДЕ
	|	СчетФактураПолученный.СводныйКомиссионный
	|
	|СГРУППИРОВАТЬ ПО
	|	СчетФактураПолученный.Ссылка,
	|	СчетФактураПолученный.Контрагент.ИНН,
	|	СчетФактураПолученный.Контрагент.КПП,
	|	ВЫБОР
	|		КОГДА ПОДСТРОКА(СчетФактураПолученный.Контрагент.НаименованиеПолное, 1, 250) = """"
	|			ТОГДА СчетФактураПолученный.Контрагент.Наименование
	|		ИНАЧЕ ПОДСТРОКА(СчетФактураПолученный.Контрагент.НаименованиеПолное, 1, 250)
	|	КОНЕЦ,
	|	СчетФактураПолученный.Контрагент.ЮрФизЛицо,
	|	СчетФактураПолученный.Контрагент
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВозвратТоваровОтПокупателя.Ссылка,
	|	НЕОПРЕДЕЛЕНО,
	|	NULL,
	|	NULL,
	|	ВозвратТоваровОтПокупателя.ДатаРасходногоКассовогоОрдера,
	|	ВозвратТоваровОтПокупателя.НомерРасходногоКассовогоОрдера,
	|	1,
	|	NULL,
	|	NULL
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокСчетовФактур КАК СписокСчетовФактур
	|		ПО ВозвратТоваровОтПокупателя.Ссылка = СписокСчетовФактур.СчетФактура
	|ГДЕ
	|	ВозвратТоваровОтПокупателя.НомерРасходногоКассовогоОрдера <> """"
	|	И ВозвратТоваровОтПокупателя.ДатаРасходногоКассовогоОрдера <> ДАТАВРЕМЯ(1, 1, 1)
	|	И ВозвратТоваровОтПокупателя.НомерДокументаОтгрузки <> """"
	|	И ВозвратТоваровОтПокупателя.ДатаДокументаОтгрузки <> ДАТАВРЕМЯ(1, 1, 1)
	|	И НЕ &Постановление1137_2019
	|ИТОГИ
	|	СУММА(КоличествоЭлементов)
	|ПО
	|	СчетФактураДокумент";

КонецФункции

#КонецОбласти


