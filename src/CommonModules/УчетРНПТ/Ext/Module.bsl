// Функция формирует значение нового ключа связи.
//
// Параметры функции
// ПараметрыСвязиСтрокТЧ – соответствие, в котором сохраняется текущее «свободное» значение ключа
// ДокументОбъект – документ, 
// ИмяТЧ – имя «основной» табличной части объекта,
// ПроверятьМаксЗначение – флаг необходимости проверки начальной инициализации ключа
//
Функция ПолучитьНовыйКлючСвязи(ПараметрыСвязиСтрокТЧ, ДокументОбъект, ИмяТЧ, ПроверятьМаксЗначение = Ложь) Экспорт
	
	Если ПроверятьМаксЗначение Тогда
		ПроверитьМаксЗначениеКлюча(ПараметрыСвязиСтрокТЧ, ДокументОбъект, ИмяТЧ);
	КонецЕсли;
	
	МаксКлюч = ПараметрыСвязиСтрокТЧ[ИмяТЧ].СвободныйКлюч; // берется текущее «свободное» значение ключа

	ПараметрыСвязиСтрокТЧ[ИмяТЧ].СвободныйКлюч = МаксКлюч + 1; // после чего «свободное» значение увеличивается на единицу

	ПараметрыСвязиСтрокТЧ[ИмяТЧ].ФлагМодификации = Истина; // устанавливаем признак  модифицированности
	
	Возврат МаксКлюч;
	
КонецФункции // ПолучитьНовыйКлючСвязи()

// Процедура проверяет максимальное значение ключа связи. 
//
Процедура ПроверитьМаксЗначениеКлюча(ПараметрыСвязиСтрокТЧ, ДокументОбъект, ИмяТЧ) Экспорт
	
	Если ПараметрыСвязиСтрокТЧ[ИмяТЧ].СвободныйКлюч <> Неопределено Тогда
		Возврат; // «не занятый» ключ уже был определен ранее.
	КонецЕсли;
	
	// При первом обращении «свободный» ключ необходимо рассчитать.
	Если ДокументОбъект[ИмяТЧ].Количество() = 0 Тогда
		ПараметрыСвязиСтрокТЧ[ИмяТЧ].СвободныйКлюч = 1; // отсчет начинается с нуля
	Иначе
		
		// Если в табл. части уже присутствуют строки, то новое «свободное» значение ключа
		// рассчитывается от максимального существующего значения.
		СписокКлючей = Новый СписокЗначений;				
		Для каждого ЭлКоллекции Из ДокументОбъект[ИмяТЧ] Цикл
			СписокКлючей.Добавить(ЭлКоллекции.ИдентификаторСтроки);
		КонецЦикла;
		СписокКлючей.СортироватьПоЗначению(НаправлениеСортировки.Убыв);
		ПараметрыСвязиСтрокТЧ[ИмяТЧ].СвободныйКлюч = СписокКлючей[0].Значение + 1;
		
	КонецЕсли;
	
КонецПроцедуры // ПроверитьМаксЗначениеКлюча()

// Формирует запрос по табличной части документа.
//
// Параметры: 
//  ДокументОбъект        	- объект проводимого документа, 
//  ИмяТабличнойЧастиТовары - строка, имя табличной части, в которой хранится номенклатура
//  ИмяРеквизитаНоменклатурыВТабличнойЧастиТовары - имя реквизита "Номенклатура" 
//
// Возвращаемое значение:
//  Результат запроса.
//
Функция СформироватьЗапросПоТабличнойЧасти(ДокументОбъект, ИмяТабличнойЧастиТовары, ИмяРеквизитаНоменклатурыВТабличнойЧастиТовары) Экспорт


	ТекстЗапроса = "";
	
	ДокументМетаданные = ДокументОбъект.Метаданные();

	Запрос = Новый Запрос;

	Запрос.Текст = "ВЫБРАТЬ
		 |	ДокТовары.Номенклатура									КАК Номенклатура,
		 |	ДокСведенияПрослеживаемости.РНПТ						КАК РНПТ,
		 |	ДокСведенияПрослеживаемости.Количество					КАК Количество,
		 |	ДокСведенияПрослеживаемости.КоличествоПрослеживаемости 	КАК КоличествоПрослеживаемости
		 |ИЗ
		 |	Документ." + ДокументМетаданные.Имя + ".СведенияПрослеживаемости КАК ДокСведенияПрослеживаемости
		 |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ."  + ДокументМетаданные.Имя + "." + ИмяТабличнойЧастиТовары + " КАК ДокТовары
		 |		ПО ДокСведенияПрослеживаемости.ИдентификаторСтроки = ДокТовары.ИдентификаторСтроки
		 |     ГДЕ ДокСведенияПрослеживаемости.Ссылка = &ДокументСсылка";

	// Установим параметры запроса.
	Запрос.УстановитьПараметр("ДокументСсылка" , ДокументОбъект.Ссылка);
	
	Если ДокументОбъект["СведенияПрослеживаемости"].Количество() = 0 Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ГДЕ ДокСведенияПрослеживаемости.Ссылка = &ДокументСсылка", "ГДЕ ЛОЖЬ");
	КонецЕсли;

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоТабличнойЧасти()

// Проверяет, что в табличной части нет незаполненных стран по прослеживаемым товарам.
//
// Параметры:
//  ДокументОбъект    - объект проводимого документа, 
//  ИмяТабличнойЧасти - табличная часть документа,
//  Отказ             - флаг отказа в проведении.
//
Процедура ПроверитьЗаполнениеСтраныДляПрослеживаемыхТоваров(ДокументОбъект, ИмяТабличнойЧасти, Отказ) Экспорт
    Перем ПредставлениеТабличнойЧасти;
	
	// Цикл по строкам табличной части документа.
		Для каждого СтрокаТаблицы Из ДокументОбъект[ИмяТабличнойЧасти] Цикл
			Если ЗначениеЗаполнено(СтрокаТаблицы.Номенклатура.КодТНВЭД) Тогда   
				
				Если СтрокаТаблицы.Номенклатура.КодТНВЭД.ПрослеживаемыйТовар И Не ЗначениеЗаполнено(СтрокаТаблицы.СтранаПроисхождения) Тогда
				
					Если ПредставлениеТабличнойЧасти = неопределено Тогда
						ПредставлениеТабличнойЧасти = ДокументОбъект.Метаданные().ТабличныеЧасти[ИмяТабличнойЧасти].Представление();
					КонецЕсли;	
					
					ТекстОшибки = НСтр("ru = 'В строке номер ""%НомерСтроки%"" табличной части ""%ПредставлениеТабличнойЧасти%"": содержится прослеживаемый товар. Должна быть указана страна происхождения!'");
					 
					ТекстОшибки = СтрЗаменить(ТекстОшибки, "%НомерСтроки%", СтрокаТаблицы.НомерСтроки);
					ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ПредставлениеТабличнойЧасти%", ПредставлениеТабличнойЧасти);
					 
					ТекстПоля = ОбщегоНазначенияКлиентСервер.ПолучитьТекстДляВыдачиСообщенийПоСтрокеТЧ(ИмяТабличнойЧасти, СтрокаТаблицы.НомерСтроки, "Номенклатура");
				
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ДокументОбъект, ТекстПоля,, Отказ);
				КонецЕсли;
				
			КонецЕсли;
		КонецЦикла;

КонецПроцедуры // ПроверитьЗаполнениеСтраныДляПрослеживаемыхТоваров()

Процедура ЗаполнитьРНПТПоОснованию(ДокументОснование, СведенияПрослеживаемости) Экспорт
	ТипДокументаОснования = ТипЗнч(ДокументОснование);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование"  , ДокументОснование);

	МетаданныеДокумента  = ДокументОснование.Метаданные();
	ДокументОснованиеИмя = МетаданныеДокумента.Имя;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Док.РНПТ,
	|	Док.Количество,
	|	Док.КоличествоПрослеживаемости,
	|	Док.ИдентификаторСтроки
	|";
	
	Запрос.Текст = Запрос.Текст + "
	|ИЗ
	|	Документ." + ДокументОснованиеИмя + ".СведенияПрослеживаемости   КАК Док
	|";

	Запрос.Текст = Запрос.Текст + "
	|ГДЕ
	|	Док.Ссылка = &ДокументОснование
	|
	|УПОРЯДОЧИТЬ ПО
	|	Док.НомерСтроки";

	РезультатЗапроса = Запрос.Выполнить();

	ТекПользователь = глЗначениеПеременной("глТекущийПользователь");

	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл

		СтрокаТабличнойЧасти = СведенияПрослеживаемости.Добавить();
		СтрокаТабличнойЧасти.РНПТ         = Выборка.РНПТ;
		СтрокаТабличнойЧасти.Количество           = Выборка.Количество;
		СтрокаТабличнойЧасти.КоличествоПрослеживаемости     = Выборка.КоличествоПрослеживаемости;
		СтрокаТабличнойЧасти.ИдентификаторСтроки = Выборка.ИдентификаторСтроки;
	КонецЦикла;

КонецПроцедуры // ЗаполнитьРНПТПоОснованию()

// Формирует запрос по табличной части документа.
//
// Параметры: 
//  ДокументОбъект        	- объект проводимого документа, 
//  ИмяТабличнойЧастиТовары - строка, имя табличной части, в которой хранится номенклатура
//  ИмяРеквизитаНоменклатурыВТабличнойЧастиТовары - имя реквизита "Номенклатура" 
//
// Возвращаемое значение:
//  Результат запроса.
//
Функция СформироватьЗапросПоТабличнойЧастиДляПроверкиКоличества(ДокументОбъект, ИмяТабличнойЧастиТовары, ИмяРеквизитаНоменклатурыВТабличнойЧастиТовары) Экспорт


	ТекстЗапроса = "";
	
	ДокументМетаданные = ДокументОбъект.Метаданные();

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДокСведенияПрослеживаемости.ИдентификаторСтроки КАК ИдентификаторСтроки,
	               |	СУММА(ДокСведенияПрослеживаемости.Количество) КАК Количество1
	               |ПОМЕСТИТЬ КоличествоРНПТ
	               |ИЗ
	               |	Документ.ПоступлениеТоваровУслуг.СведенияПрослеживаемости КАК ДокСведенияПрослеживаемости
	               |ГДЕ
	               |	ДокСведенияПрослеживаемости.Ссылка = &ДокументСсылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ДокСведенияПрослеживаемости.ИдентификаторСтроки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПоступлениеТоваровУслугТовары.Количество - ЕСТЬNULL(КоличествоРНПТ2.Количество1, 0) КАК КоличествоРазница,
	               |	ПоступлениеТоваровУслугТовары.ИдентификаторСтроки КАК ИдентификаторСтроки
	               |ПОМЕСТИТЬ КоличествоРНПТСравнение
	               |ИЗ
	               |	Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
	               |		ЛЕВОЕ СОЕДИНЕНИЕ КоличествоРНПТ КАК КоличествоРНПТ2
	               |		ПО ПоступлениеТоваровУслугТовары.ИдентификаторСтроки = КоличествоРНПТ2.ИдентификаторСтроки
	               |ГДЕ
	               |	ПоступлениеТоваровУслугТовары.Ссылка = &ДокументСсылка
	               |	И ПоступлениеТоваровУслугТовары.ПрослеживаемыйТовар
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	НеВсеРНПТ.ИдентификаторСтроки КАК ИдентификаторСтроки
	               |ИЗ
	               |	КоличествоРНПТСравнение КАК НеВсеРНПТ
	               |ГДЕ
	               |	НеВсеРНПТ.КоличествоРазница <> 0";

	// Установим параметры запроса.
	Запрос.УстановитьПараметр("ДокументСсылка" , ДокументОбъект.Ссылка);
	выбЗапроса = Запрос.ВыполнитьПакет();
	
	Возврат выбЗапроса[2];

КонецФункции // СформироватьЗапросПоТабличнойЧастиДляПроверкиКоличества()


Процедура ОчитститьАвтоподборПередПерезаполнением(ДокументОбъект) Экспорт
	
	Для Каждого СтрокаТоваров Из ДокументОбъект.Товары Цикл
				
		Если (СтрокаТоваров.ПрослеживаемыйТовар) И (СтрокаТоваров.АвтоподборРНПТ) Тогда
			Отбор = Новый Структура;
			Отбор.Вставить("ИдентификаторСтроки", СтрокаТоваров.ИдентификаторСтроки);
			СтрокиПрослеживаемостиТовара = ДокументОбъект.СведенияПрослеживаемости.НайтиСтроки(Отбор);	
					
			Если СтрокиПрослеживаемостиТовара.Количество() > 0 Тогда
						
				Для Каждого СтрокаПрослеживаемостиТовара Из СтрокиПрослеживаемостиТовара Цикл
					ДокументОбъект.СведенияПрослеживаемости.Удалить(СтрокаПрослеживаемостиТовара);
				КонецЦикла;
						
			КонецЕсли;
					
		КонецЕсли;
				
	КонецЦикла;
			
КонецПроцедуры