
#Область ПрограммныйИнтерфейс    

// Процедура приводит к формату согласованному с ФНС.
//
//  Параметры:
//    ОсновныеПараметры - см. ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыОперацииФискализацииЧека
//    Отказ - Булево
//    ОписаниеОшибки - Строка
//    ИсправленыОсновныеПараметры - Булево
//
Процедура ПривестиДанныеКТребуемомуФормату(ОсновныеПараметры, Отказ, ОписаниеОшибки, ИсправленыОсновныеПараметры) Экспорт
	
	ИндивидуальныйРежимПодготовкиДанныхКПередачеВОФД = Ложь;
	Если ОсновныеПараметры.Свойство("ИндивидуальныйРежимПодготовкиДанныхКПередачеВОФД", ИндивидуальныйРежимПодготовкиДанныхКПередачеВОФД) 
		И ИндивидуальныйРежимПодготовкиДанныхКПередачеВОФД = Истина Тогда
		Возврат;
	КонецЕсли;
	
	СпособФорматноЛогическогоКонтроля = Неопределено;
	
	Если ОсновныеПараметры.Свойство("СпособФорматноЛогическогоКонтроля", СпособФорматноЛогическогоКонтроля) Тогда
		Если НЕ (СпособФорматноЛогическогоКонтроля = ПредопределенноеЗначение("Перечисление.СпособыФорматноЛогическогоКонтроля.РазделятьСтроки")
			ИЛИ СпособФорматноЛогическогоКонтроля = ПредопределенноеЗначение("Перечисление.СпособыФорматноЛогическогоКонтроля.ЗачитыватьСуммы")) Тогда
			Возврат;
		КонецЕсли;
	Иначе
		Возврат;
	КонецЕсли;
	
	СуммаЧастныхОплат = 0;
	НужноПриводитьСуммы = Ложь;
	СуммаПередачиБезОплаты = 0;
	ОбщаяСуммаЧека = 0;
	СуммаПолнойОплаты = 0;
	Для Каждого ПозицияЧека Из ОсновныеПараметры.ПозицииЧека Цикл
		Если ПозицияЧека.Свойство("ФискальнаяСтрока") Тогда
			БезПередачиТовара = Ложь;
			НужнаПроверкаСтавокНДС = Ложь;
			Если ПозицияЧека.ПризнакСпособаРасчета = ПредопределенноеЗначение("Перечисление.ПризнакиСпособаРасчета.ПредоплатаЧастичная") Тогда
				СуммаЧастныхОплат = СуммаЧастныхОплат + ПозицияЧека.Сумма;
				БезПередачиТовара          = Истина;
				НужнаПроверкаСтавокНДС     = Истина;
				НужноПриводитьСуммы        = Истина;
			ИначеЕсли ПозицияЧека.ПризнакСпособаРасчета = ПредопределенноеЗначение("Перечисление.ПризнакиСпособаРасчета.ОплатаКредита") Тогда
				СуммаЧастныхОплат = СуммаЧастныхОплат + ПозицияЧека.Сумма;
				БезПередачиТовара          = Истина;
				НужноПриводитьСуммы        = Истина;
				Если ПозицияЧека.Свойство("СтавкаНДС") Тогда
					ПозицияЧека.СтавкаНДС = Неопределено;
				КонецЕсли;
				Если ПозицияЧека.Свойство("СуммаНДС") Тогда
					ПозицияЧека.СуммаНДС = 0;
				КонецЕсли;
			ИначеЕсли ПозицияЧека.ПризнакСпособаРасчета = ПредопределенноеЗначение("Перечисление.ПризнакиСпособаРасчета.Аванс")
				ИЛИ ПозицияЧека.ПризнакСпособаРасчета = ПредопределенноеЗначение("Перечисление.ПризнакиСпособаРасчета.ПредоплатаПолная") Тогда
				БезПередачиТовара = Истина;
				НужнаПроверкаСтавокНДС = Истина;
				СуммаПолнойОплаты = СуммаПолнойОплаты + ПозицияЧека.Сумма;
			ИначеЕсли ПозицияЧека.ПризнакСпособаРасчета = ПредопределенноеЗначение("Перечисление.ПризнакиСпособаРасчета.ПередачаБезОплаты") Тогда
				СуммаПередачиБезОплаты = СуммаПередачиБезОплаты + ПозицияЧека.Сумма;
			ИначеЕсли ПозицияЧека.ПризнакСпособаРасчета = ПредопределенноеЗначение("Перечисление.ПризнакиСпособаРасчета.ПередачаСПолнойОплатой") Тогда
				СуммаПолнойОплаты = СуммаПолнойОплаты + ПозицияЧека.Сумма;
			КонецЕсли;
			
			Если БезПередачиТовара Тогда
				
				Если ПозицияЧека.Свойство("ЕдиницаИзмерения") Тогда
					ПозицияЧека.ЕдиницаИзмерения = "Платеж";
				КонецЕсли;
				
				Если ПозицияЧека.Свойство("Количество") Тогда
					ПозицияЧека.Количество = 1;
				КонецЕсли;
				
				Если ПозицияЧека.Свойство("Цена") Тогда
					ПозицияЧека.Цена = ПозицияЧека.Сумма;
				КонецЕсли;
				
				Если ПозицияЧека.Свойство("ЦенаСоСкидками") Тогда
					ПозицияЧека.ЦенаСоСкидками = ПозицияЧека.Сумма;
				КонецЕсли;
				
				Если ПозицияЧека.Свойство("СуммаСкидок") Тогда
					ПозицияЧека.СуммаСкидок = 0;
				КонецЕсли;
				
				ПозицияЧека.Вставить("ПризнакПредметаРасчета", ПредопределенноеЗначение("Перечисление.ПризнакиПредметаРасчета.ПлатежВыплата"));
				ИсправленыОсновныеПараметры = Истина;
			КонецЕсли;
			
			Если НужнаПроверкаСтавокНДС Тогда
				
				Если ПозицияЧека.Свойство("СтавкаНДС") Тогда
					
					Если ПозицияЧека.СтавкаНДС = 18 ИЛИ ПозицияЧека.СтавкаНДС = 10 ИЛИ ПозицияЧека.СтавкаНДС = 20 Тогда
						ПозицияЧека.СтавкаНДС = 100 + ПозицияЧека.СтавкаНДС;
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
			ОбщаяСуммаЧека = ОбщаяСуммаЧека + ПозицияЧека.Сумма;
		КонецЕсли;
		
	КонецЦикла;
	
	Если НужноПриводитьСуммы Тогда
		ПривестиСуммыПозицийЧекаКСуммеТаблицыОплат(ОсновныеПараметры, ОбщаяСуммаЧека, СуммаПолнойОплаты, СуммаПередачиБезОплаты, Отказ, ОписаниеОшибки);
	КонецЕсли;
	
	Если ОсновныеПараметры.Свойство("ВыводитьПризнакСпособаРасчета") ИЛИ ОсновныеПараметры.Свойство("ВыводитьПризнакПредметаРасчета") Тогда
		
		ПозицииЧека = Новый Массив;
		
		Для Каждого ПозицияЧека Из ОсновныеПараметры.ПозицииЧека Цикл
			
			Если ПозицияЧека.Свойство("ФискальнаяСтрока") Тогда
				
				Если ОсновныеПараметры.Свойство("ВыводитьПризнакСпособаРасчета") Тогда
					ПризнакСпособаРасчета = Неопределено;
					
					Если ПозицияЧека.Свойство("ПризнакСпособаРасчета", ПризнакСпособаРасчета)
						И ЗначениеЗаполнено(ПризнакСпособаРасчета) Тогда
						ПризнакСпособаРасчета = Строка(ПризнакСпособаРасчета);
						СтруктураТекстовойСтроки = МенеджерОборудованияКлиентСервер.ПараметрыТекстовойСтрокиЧека(ПризнакСпособаРасчета);
						ПозицииЧека.Добавить(СтруктураТекстовойСтроки);
					КонецЕсли;
					
				КонецЕсли;
				
				Если ОсновныеПараметры.Свойство("ВыводитьПризнакПредметаРасчета") Тогда
					ПризнакПредметаРасчета = Неопределено;
					
					Если ПозицияЧека.Свойство("ПризнакПредметаРасчета", ПризнакПредметаРасчета)
						И ЗначениеЗаполнено(ПризнакПредметаРасчета) Тогда
						ПризнакПредметаРасчета = Строка(ПризнакПредметаРасчета);
						СтруктураТекстовойСтроки = МенеджерОборудованияКлиентСервер.ПараметрыТекстовойСтрокиЧека(ПризнакПредметаРасчета);
						ПозицииЧека.Добавить(СтруктураТекстовойСтроки);
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
			ПозицииЧека.Добавить(ПозицияЧека);
		КонецЦикла;
		
		ОсновныеПараметры.Вставить("ПозицииЧека", ПозицииЧека);
	КонецЕсли;
	
КонецПроцедуры

// Процедура приводит суммы позиций чека к сумме полной оплаты
// 
// Параметры:
//    ОсновныеПараметры - Структура - см. МенеджерОборудованияКлиентСервер.ПараметрыОперацииФискализацииЧека
//    ОбщаяСуммаЧека - Число - Общая сумма позиций чека
//    СуммаПолнойОплаты - Число - Сумма оплаченная
//    СуммаПередачиБезОплаты - Число - Сумма без оплаты
//    Отказ - Булево - Флаг отказа
//    ОписаниеОшибки - Строка - Описание ошибки
//
Процедура ПривестиСуммыПозицийЧекаКСуммеТаблицыОплат(ОсновныеПараметры, ОбщаяСуммаЧека, СуммаПолнойОплаты, СуммаПередачиБезОплаты, Отказ, ОписаниеОшибки) Экспорт
	
	СуммаФактическихОплат = 0;
	Для Каждого СтрокаОплат Из ОсновныеПараметры.ТаблицаОплат Цикл
		Если СтрокаОплат.ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплатыККТ.Наличные")
			ИЛИ СтрокаОплат.ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплатыККТ.Электронно") 
			ИЛИ СтрокаОплат.ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплатыККТ.ВстречноеПредоставление") Тогда
			СуммаФактическихОплат = СуммаФактическихОплат + СтрокаОплат.Сумма;
		КонецЕсли;
	КонецЦикла;
	
	СуммаФактическихОплат = СуммаФактическихОплат - СуммаПолнойОплаты;
	Если СуммаФактическихОплат < 0 Тогда
		Отказ = Истина;
		ОписаниеОшибки = НСтр("ru = 'Сумма по строкам с указанием признака полной оплаты меньше чем сумма самих оплат'") ;
		Возврат;
	ИначеЕсли СуммаФактическихОплат = 0 Тогда
		Отказ = Истина;
		ОписаниеОшибки = НСтр("ru = 'Необходимо указать оплату'") ;
		Возврат;
	КонецЕсли;
	
	СуммаНеобходимойПолнойОплаты = ОбщаяСуммаЧека - СуммаПередачиБезОплаты - СуммаПолнойОплаты; 
	// Сумма всегда больше нулю, т.к. в чеке есть строки в частичной оплатой
	Если СуммаФактическихОплат < СуммаНеобходимойПолнойОплаты Тогда
		Коэффициент = СуммаФактическихОплат / СуммаНеобходимойПолнойОплаты;
		РассчитаннаяСуммаЧека = 0;
		МаксимальнаяПозиция = Неопределено;
		МаксимальнаяСумма = 0;
		СуммаЧастичныхОплат = 0;
		Для Каждого ПозицияЧека Из ОсновныеПараметры.ПозицииЧека Цикл
			Если ПозицияЧека.Свойство("ФискальнаяСтрока") Тогда
				Если ПозицияЧека.ПризнакСпособаРасчета = ПредопределенноеЗначение("Перечисление.ПризнакиСпособаРасчета.ПредоплатаЧастичная")
					ИЛИ ПозицияЧека.ПризнакСпособаРасчета = ПредопределенноеЗначение("Перечисление.ПризнакиСпособаРасчета.ОплатаКредита") Тогда
					Если ПозицияЧека.Сумма > МаксимальнаяСумма Тогда
						МаксимальнаяСумма = ПозицияЧека.Сумма;
						МаксимальнаяПозиция = ПозицияЧека;
					КонецЕсли;
					СуммаЧастичныхОплат = СуммаЧастичныхОплат + ПозицияЧека.Сумма;
					ПозицияЧека.Сумма = Окр(ПозицияЧека.Сумма * Коэффициент,2,1);
					РассчитаннаяСуммаЧека = РассчитаннаяСуммаЧека + ПозицияЧека.Сумма;
					Если ПозицияЧека.Свойство("Цена") Тогда
						ПозицияЧека.Цена = ПозицияЧека.Сумма;
					КонецЕсли;
					
					Если ПозицияЧека.Свойство("ЦенаСоСкидками") Тогда
						ПозицияЧека.ЦенаСоСкидками = ПозицияЧека.Сумма;
					КонецЕсли;
					
					Если ПозицияЧека.Свойство("СуммаНДС") И ПозицияЧека.СуммаНДС <> Неопределено Тогда
						ПозицияЧека.СуммаНДС = Окр(ПозицияЧека.СуммаНДС * Коэффициент,2,1);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	Иначе
		Возврат;
	КонецЕсли;
	
	Отклонение = СуммаФактическихОплат - РассчитаннаяСуммаЧека;
	
	Если Макс(Отклонение, -Отклонение) >= 0.01 Тогда
		СуммаМаксимальноПозиции = МаксимальнаяПозиция.Сумма;
		МаксимальнаяПозиция.Сумма = МаксимальнаяПозиция.Сумма + Отклонение;
		Если МаксимальнаяПозиция.Свойство("Цена") Тогда
			МаксимальнаяПозиция.Цена = МаксимальнаяПозиция.Сумма;
		КонецЕсли;
		
		Если МаксимальнаяПозиция.Свойство("ЦенаСоСкидками") Тогда
			МаксимальнаяПозиция.ЦенаСоСкидками = МаксимальнаяПозиция.Сумма;
		КонецЕсли;
					
		Если МаксимальнаяПозиция.Свойство("СуммаНДС") И МаксимальнаяПозиция.СуммаНДС <> Неопределено И СуммаМаксимальноПозиции <> Неопределено Тогда
			// Максимальная сумма не может быть равна или меньше нуля
			// Определяем отклонение НДС
			МаксимальнаяПозиция.СуммаНДС = Окр(МаксимальнаяПозиция.Сумма * МаксимальнаяПозиция.СуммаНДС / СуммаМаксимальноПозиции, 2,1);
		КонецЕсли;
	КонецЕсли; 
	
	// Общие суммы НДС по ставкам пересчитывается далее по алгоритму.
	
	// ИсправитьТаблицуОплат
	
	СуммаИсправленияОплат = СуммаНеобходимойПолнойОплаты - СуммаФактическихОплат;
	
	Если СуммаИсправленияОплат > 0 Тогда
		Для Каждого СтрокаОплат Из ОсновныеПараметры.ТаблицаОплат Цикл
			Если СтрокаОплат.ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплатыККТ.Постоплата") 
				ИЛИ СтрокаОплат.ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплатыККТ.Предоплата") Тогда
				
				Если СуммаИсправленияОплат >= СтрокаОплат.Сумма Тогда
					СуммаИсправленияОплат = СуммаИсправленияОплат - СтрокаОплат.Сумма;
					СтрокаОплат.Сумма = 0;
				Иначе
					СуммаИсправленияОплат = 0;
					СтрокаОплат.Сумма = СтрокаОплат.Сумма - СуммаИсправленияОплат;
				КонецЕсли;
				Если СуммаИсправленияОплат <= 0 Тогда
					Прервать;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	КоличествоЭлементов = ОсновныеПараметры.ТаблицаОплат.Количество();
	Для Индекс = 1 По КоличествоЭлементов Цикл
		Если ОсновныеПараметры.ТаблицаОплат[КоличествоЭлементов - Индекс].Сумма = 0 Тогда
			ОсновныеПараметры.ТаблицаОплат.Удалить(КоличествоЭлементов - Индекс);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
