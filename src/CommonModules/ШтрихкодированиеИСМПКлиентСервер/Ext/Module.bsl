#Область ПрограммныйИнтерфейс

// Возвращает сокращенное представление штрихкода маркируемой продукции.
// 
// Параметры:
// 	Штрихкод - Строка - Штрихкод строкой.
// 	ВидуУпаковки - ПеречислениеСсылка.ВидыУпаковокИС - Значение упаковки
// Возвращаемое значение:
// 	Строка - Краткое представление штрихкода маркируемой продукции.
Функция ПредставлениеШтрихкодаПоВидуУпаковки(Штрихкод, ВидуУпаковки) Экспорт
	
	Если ВидуУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИС.Потребительская") Тогда
		ПредставлениеШтрихкода = Штрихкод;
	Иначе
		ПредставлениеШтрихкода = ШтрихкодированиеИСКлиентСервер.ПредставлениеШтрихкода(Штрихкод);
	КонецЕсли;
	
	Возврат ПредставлениеШтрихкода;
	
КонецФункции

Функция SSCCАгрегата(Штрихкод) Экспорт
	
	ПараметрыШтрихкода = ШтрихкодыУпаковокКлиентСервер.ПараметрыШтрихкода(Штрихкод);
	Если ПараметрыШтрихкода.Результат = Неопределено Тогда
		ВызватьИсключение НСтр("ru = 'Внутренняя ошибка'");
	КонецЕсли;
	Если ПараметрыШтрихкода.ТипШтрихкода = ПредопределенноеЗначение("Перечисление.ТипыШтрихкодов.SSCC") Тогда
		КодАгрегата = ШтрихкодыУпаковокКлиентСервер.ШтрихкодSSCC(ПараметрыШтрихкода.Результат, Ложь, Ложь);
	Иначе
		ВызватьИсключение НСтр("ru = 'Внутренняя ошибка'");
	КонецЕсли;
	
	Возврат КодАгрегата;
	
КонецФункции

// Возвращает способ ввода в оборот СУЗ по виду операции.
// 
// Параметры:
//  ВидОперации  - ПеречислениеСсылка.ВидыОперацийИСМП - Вид операции документа.
//  ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС  - Вид продукции.
// Возвращаемое значение:
//  ПеречислениеСсылка.СпособыВводаВОборотСУЗ, Массив из ПеречислениеСсылка.СпособыВводаВОборотСУЗ - Способ ввода в оборот СУЗ.
Функция СпособВводаВОборотСУЗПоВидуОперации(ВидОперации, ВидПродукции = Неопределено) Экспорт
	
	Если ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВводВОборотПроизводствоВнеЕАЭС")
		Или ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВводВОборотИмпортСФТС")
		Или ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВводВОборотИмпортСФТСМех") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.СпособыВводаВОборотСУЗ.Импорт");
	ИначеЕсли ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВводВОборотТрансграничнаяТорговля") Тогда
		ВозвращаемоеЗначение = Новый Массив();
		ВозвращаемоеЗначение.Добавить(ПредопределенноеЗначение("Перечисление.СпособыВводаВОборотСУЗ.ТрансграничнаяТорговля"));
		ВозвращаемоеЗначение.Добавить(ПредопределенноеЗначение("Перечисление.СпособыВводаВОборотСУЗ.Импорт"));
		Возврат ВозвращаемоеЗначение;
	ИначеЕсли ВидОперации = ПредопределенноеЗначение(
			"Перечисление.ВидыОперацийИСМП.ВводВОборотПолучениеПродукцииОтФизическихЛиц") Тогда
		ВозвращаемоеЗначение = Новый Массив();
		ВозвращаемоеЗначение.Добавить(ПредопределенноеЗначение("Перечисление.СпособыВводаВОборотСУЗ.Производство"));
		ВозвращаемоеЗначение.Добавить(ПредопределенноеЗначение("Перечисление.СпособыВводаВОборотСУЗ.Комиссия"));
		Возврат ВозвращаемоеЗначение;
	ИначеЕсли ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВводВОборотПроизводствоРФ")
		Или ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВводВОборотПроизводствоРФПоДоговору")
		Или ВидОперации = ПредопределенноеЗначение(
			"Перечисление.ВидыОперацийИСМП.ВводВОборотПроизводствоРФПоДоговоруНаСторонеЗаказчика") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.СпособыВводаВОборотСУЗ.Производство");
	ИначеЕсли ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВводВОборотМаркировкаОстатков") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.СпособыВводаВОборотСУЗ.МаркировкаОстатков");
	ИначеЕсли ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ОтгрузкаЕАЭССПризнаниемКИ") Тогда
		ВозвращаемоеЗначение = Новый Массив();
		ВозвращаемоеЗначение.Добавить(ПредопределенноеЗначение("Перечисление.СпособыВводаВОборотСУЗ.Производство"));
		ВозвращаемоеЗначение.Добавить(ПредопределенноеЗначение("Перечисление.СпособыВводаВОборотСУЗ.ТрансграничнаяТорговля"));
		ВозвращаемоеЗначение.Добавить(ПредопределенноеЗначение("Перечисление.СпособыВводаВОборотСУЗ.Импорт"));
		ВозвращаемоеЗначение.Добавить(ПредопределенноеЗначение("Перечисление.СпособыВводаВОборотСУЗ.Перемаркировка"));
		ВозвращаемоеЗначение.Добавить(ПредопределенноеЗначение("Перечисление.СпособыВводаВОборотСУЗ.Комиссия"));
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	Возврат ПредопределенноеЗначение("Перечисление.СпособыВводаВОборотСУЗ.ПустаяСсылка");
	
КонецФункции

#Область ШаблоныПараметровСканирования

// Настройки параметров сканирования документа производства, заполняемые при внедрении библиотеки.
// 
// Возвращаемое значение:
//  Структура - Настройки параметров сканирования документа производства:
//   * Организация - Неопределено, ОпределяемыйТип.Организация - организация документа производства,
//   * Склад - Неопределено, ОпределяемыйТип.Склад - склад документа производства.
Функция НастройкиПараметровСканированияДокументаПроизводства() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Организация");
	Результат.Вставить("Склад");
	Возврат Результат;
	
КонецФункции

// Заполняет библиотечные настройки параметров сканирования для документов производства товара.
//   Применимо для настроек ИСМП: весовая молочная продукция.
//
// Параметры:
//   ПараметрыСканирования - см. ШтрихкодированиеИСКлиентСервер.БазовыеПараметрыСканирования
//   ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции ИС.
//   Настройки - см. НастройкиПараметровСканированияДокументаПроизводства.
// Возвращаемое значение:
//   Булево - параметры сканирования дополнены спецификой ИС МП.
Функция ЗаполнитьПараметрыСканированияДокументаПроизводства(ПараметрыСканирования, ВидПродукции, Настройки) Экспорт
	
	Если Не ИнтеграцияИСМПКлиентСерверПовтИсп.ВестиУчетМаркируемойПродукции(
			ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МолочнаяПродукцияПодконтрольнаяВЕТИС")) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	КонтрольСтатусов = ИнтеграцияИСМПКлиентСерверПовтИсп.КонтролироватьСтатусыКодовМаркировки();
	
	Если ВключитьПоддержкуПродукцииИСМП(ПараметрыСканирования, ВидПродукции) Тогда
		
		ДоступнаПечатьЭтикеток = (ВидПродукции <> ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха"));
		
		ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Эмитирован"));
		ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Нанесен"));
		
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(
			ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Неопределен"));
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(
			ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Нанесен"));
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(
			ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Эмитирован"));
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(Неопределено);
		
		ИнтеграцияИСМПКлиентСервер.УстановитьПараметрСканированияЗапрашиватьДанныеНеизвестныхУпаковокИСМП(ПараметрыСканирования, КонтрольСтатусов);
		
		ПараметрыСканирования.ОтборПоВидуПродукции                     = Истина;
		
		ПараметрыСканирования.КонтрольПустыхУпаковок             = Ложь;
		ПараметрыСканирования.ПоддерживаютсяОперацииАгрегации    = Истина;
		ПараметрыСканирования.ДоступнаПечатьЭтикеток             = ДоступнаПечатьЭтикеток;
		ПараметрыСканирования.ВозможнаЗагрузкаТСД                = Истина;
		
		ПараметрыСканирования.Организация = Настройки.Организация;
		ПараметрыСканирования.Склад       = Настройки.Склад;
		// Для обогащения информации об ошибке при выводе пользователю
		ПараметрыСканирования.ВидОперацииИСМП = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВводВОборот");
		
		НастроитьПараметрыСканированияПоРаздельномуКонтролю(ПараметрыСканирования, ВидПродукции);
		
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Заполняет библиотечные настройки параметров сканирования для документов розничной продажи товара (например чеков).
//   Применимо для настроек МОТП, ИСМП.
//
// Параметры:
//   ПараметрыСканирования - см. ШтрихкодированиеИСКлиентСервер.БазовыеПараметрыСканирования
//   ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции ИС.
// Возвращаемое значение:
//   Булево - параметры сканирования дополнены спецификой ИС МП.
Функция ЗаполнитьПараметрыСканированияДокументаРозничнойПродажи(ПараметрыСканирования, ВидПродукции) Экспорт
	
	Если Не ИнтеграцияИСМПКлиентСерверПовтИсп.ВестиУчетМаркируемойПродукции() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВидОперацииИСМП = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВыводИзОборотаРозничнаяПродажа");
	
	ВключенаПоддержкаМОТП = ШтрихкодированиеМОТПКлиентСервер.ВключитьПоддержкуТабачнойПродукции(ПараметрыСканирования, ВидПродукции,, ВидОперацииИСМП);
	ВключенаПоддержкаИСМП = ВключитьПоддержкуПродукцииИСМП(ПараметрыСканирования, ВидПродукции);
	
	Если ВключенаПоддержкаМОТП Тогда
		
		ПараметрыСканирования.ДопустимыеСтатусыМОТП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.ВведенВОборот"));
		ПараметрыСканирования.ДопустимыеСтатусыМОТП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.ВведенВОборотВозвращен"));
		
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокМОТП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.ВведенВОборот"));
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокМОТП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.ВведенВОборотВозвращен"));
		
		ПараметрыСканирования.ДопустимыПроверкиСеройЗоныМОТП = ИнтеграцияИСМПКлиентСервер.ЗначениеПараметраСканированияЗапрашиватьДанныеСервисаИСМП(ПараметрыСканирования);
		
	КонецЕсли;
	
	Если ВключенаПоддержкаИСМП Тогда
		
		ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборот"));
		ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборотПриВозврате"));
		ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ПодключенКОборудованиюРозлива"));
		
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборот"));
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборотПриВозврате"));
		
		Если Не ЗначениеЗаполнено(ВидПродукции) И ИнтеграцияИСКлиентСервер.ПрисутствуетМолочнаяПродукция(ПараметрыСканирования.ДопустимыеВидыПродукции)
				Или ИнтеграцияИСКлиентСервер.ЭтоМолочнаяПродукцияИСМП(ВидПродукции) Тогда
			ПараметрыСканирования.КонтролироватьОкончаниеСрокаГодности = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ВключенаПоддержкаИСМП Или ВключенаПоддержкаМОТП Тогда
		
		ПараметрыСканирования.ТребуетсяЧастичноеВыбытие       = Истина;
		ПараметрыСканирования.СохранятьКодыМаркировкиВПулИСМП = Истина;
		ПараметрыСканирования.ДетализацияСтруктурыХранения    = ПредопределенноеЗначение("Перечисление.ДетализацияСтруктурыХраненияИС.ГрупповыеУпаковки");
		ПараметрыСканирования.ОтборПоВидуПродукции            = Истина;
		ПараметрыСканирования.ВидОперацииИСМП                 = ВидОперацииИСМП;
		НастроитьПараметрыСканированияПоРаздельномуКонтролю(ПараметрыСканирования, ВидПродукции);
		Возврат Истина;
		
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Заполняет библиотечные настройки параметров сканирования для документов розничного возврата товара
//   (например чеков на возврат). Применимо для настроек МОТП, ИСМП.
//
// Параметры:
//   ПараметрыСканирования - см. ШтрихкодированиеИСКлиентСервер.БазовыеПараметрыСканирования
//   ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции ИС.
// Возвращаемое значение:
//   Булево - параметры сканирования дополнены спецификой ИС МП.
Функция ЗаполнитьПараметрыСканированияДокументаРозничногоВозврата(ПараметрыСканирования, ВидПродукции) Экспорт
	
	Если Не ИнтеграцияИСМПКлиентСерверПовтИсп.ВестиУчетМаркируемойПродукции() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВидОперацииИСМП = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВозвратВОборотПриРозничнойРеализации");
	
	ВключенаПоддержкаМОТП = ШтрихкодированиеМОТПКлиентСервер.ВключитьПоддержкуТабачнойПродукции(ПараметрыСканирования, ВидПродукции,, ВидОперацииИСМП);
	ВключенаПоддержкаИСМП = ВключитьПоддержкуПродукцииИСМП(ПараметрыСканирования, ВидПродукции);
	
	Если ВключенаПоддержкаМОТП Тогда
		
		ПараметрыСканирования.ДопустимыеСтатусыМОТП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.Продан"));
		ПараметрыСканирования.ДопустимыеСтатусыМОТП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.ВведенВОборот"));
		ПараметрыСканирования.ДопустимыеСтатусыМОТП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.ВведенВОборотВозвращен"));
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокМОТП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.Продан"));
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокМОТП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.ВведенВОборот"));
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокМОТП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.ВведенВОборотВозвращен"));
		
		ПараметрыСканирования.ДопустимыПроверкиСеройЗоныМОТП = ИнтеграцияИСМПКлиентСервер.ЗначениеПараметраСканированияЗапрашиватьДанныеСервисаИСМП(ПараметрыСканирования);
		
	КонецЕсли;
	
	Если ВключенаПоддержкаИСМП Тогда
		
		ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборота"));
		ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборотаРозничнаяПродажа"));
		ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборот"));
		ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборотПриВозврате"));
		ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ПодключенКОборудованиюРозлива"));
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборота"));
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборотаРозничнаяПродажа"));
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборот"));
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборотПриВозврате"));
		
	КонецЕсли;
	
	Если ВключенаПоддержкаИСМП Или ВключенаПоддержкаМОТП Тогда
		
		ПараметрыСканирования.ТребуетсяЧастичноеВыбытие       = Истина;
		ПараметрыСканирования.СохранятьКодыМаркировкиВПулИСМП = Истина;
		ПараметрыСканирования.ДетализацияСтруктурыХранения    = ПредопределенноеЗначение("Перечисление.ДетализацияСтруктурыХраненияИС.ГрупповыеУпаковки");
		ПараметрыСканирования.ОтборПоВидуПродукции            = Истина;
		ПараметрыСканирования.ВидОперацииИСМП                 = ВидОперацииИСМП;
		
		НастроитьПараметрыСканированияПоРаздельномуКонтролю(ПараметрыСканирования, ВидПродукции);
		Возврат Истина;
		
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Заполняет библиотечные настройки параметров сканирования для документов оптовой продажи товара (например реализации).
//   Применимо для настроек МОТП, ИСМП.
//
// Параметры:
//   ПараметрыСканирования - см. ШтрихкодированиеИСКлиентСервер.БазовыеПараметрыСканирования
//   ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции ИС.
// Возвращаемое значение:
//   Булево - параметры сканирования дополнены спецификой ИС МП.
Функция ЗаполнитьПараметрыСканированияДокументаОптовойОтгрузки(ПараметрыСканирования, ВидПродукции) Экспорт
	
	Если Не ИнтеграцияИСМПКлиентСерверПовтИсп.ВестиУчетМаркируемойПродукции() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВидОперацииИСМП = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ОтгрузкаПродажа");
	
	ВключенаПоддержкаМОТП = ШтрихкодированиеМОТПКлиентСервер.ВключитьПоддержкуТабачнойПродукции(ПараметрыСканирования, ВидПродукции,, ВидОперацииИСМП);
	ВключенаПоддержкаИСМП = ВключитьПоддержкуПродукцииИСМП(ПараметрыСканирования, ВидПродукции);
	
	Если ВключенаПоддержкаМОТП Тогда
		
		ПараметрыСканирования.ДопустимыеСтатусыМОТП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.ВведенВОборот"));
		ПараметрыСканирования.ДопустимыеСтатусыМОТП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.ВведенВОборотВозвращен"));
		ПараметрыСканирования.ДопустимыеСтатусыМОТП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.Нанесен"));
		ПараметрыСканирования.ДопустимыеСтатусыМОТП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.НанесенОплачен"));
		
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокМОТП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.ВведенВОборот"));
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокМОТП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.ВведенВОборотВозвращен"));
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокМОТП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.Нанесен"));
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокМОТП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.НанесенОплачен"));
		
		ПараметрыСканирования.ВариантПолученияМРЦ             = "Вычисление";
		ПараметрыСканирования.ОтборПоВидуПродукции            = Истина;
		ПараметрыСканирования.ДопустимыПроверкиСеройЗоныМОТП  = Истина;
		ПараметрыСканирования.СохранятьКодыМаркировкиВПулИСМП = Истина;
		
		// Для обогащения информации об ошибке при выводе пользователю
		ПараметрыСканирования.ВидОперацииИСМП = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ОтгрузкаПродажа");
		
	КонецЕсли;
	
	Если ВключенаПоддержкаИСМП Тогда
		
		ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборот"));
		ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборотПриВозврате"));
		
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборот"));
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборотПриВозврате"));
		
		ПараметрыСканирования.ОтборПоВидуПродукции            = Истина;
		ПараметрыСканирования.СохранятьКодыМаркировкиВПулИСМП = Истина;
		
		ПараметрыСканирования.ПоддерживаетсяОбъемноСортовойУчет  = Истина;
		ПараметрыСканирования.ЗапрашиватьКоличествоМерногоТовара = ИнтеграцияИСМПКлиентСерверПовтИсп.НастройкиСканированияКодовМаркировки().ЗапрашиватьКоличествоМерногоТовара;
		// Для обогащения информации об ошибке при выводе пользователю
		ПараметрыСканирования.ВидОперацииИСМП                 = ВидОперацииИСМП;
		
	КонецЕсли;
	
	Если ВключенаПоддержкаИСМП Или ВключенаПоддержкаМОТП Тогда
		НастроитьПараметрыСканированияПоРаздельномуКонтролю(ПараметрыСканирования, ВидПродукции);
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Заполняет библиотечные настройки параметров сканирования для документов оптовового возврата товара.
//   Применимо для настроек МОТП, ИСМП.
//
// Параметры:
//   ПараметрыСканирования - см. ШтрихкодированиеИСКлиентСервер.БазовыеПараметрыСканирования
//   ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции ИС.
// Возвращаемое значение:
//   Булево - параметры сканирования дополнены спецификой ИС МП.
Функция ЗаполнитьПараметрыСканированияДокументаОптовогоВозврата(ПараметрыСканирования, ВидПродукции) Экспорт
	
	Если Не ИнтеграцияИСМПКлиентСерверПовтИсп.ВестиУчетМаркируемойПродукции() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВидОперацииИСМП = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ПриемкаПродажа");
	
	ВключенаПоддержкаМОТП = ШтрихкодированиеМОТПКлиентСервер.ВключитьПоддержкуТабачнойПродукции(ПараметрыСканирования, ВидПродукции,, ВидОперацииИСМП);
	ВключенаПоддержкаИСМП = ВключитьПоддержкуПродукцииИСМП(ПараметрыСканирования, ВидПродукции);
	
	Если ВключенаПоддержкаМОТП Тогда
		
		ПараметрыСканирования.ДопустимыеСтатусыМОТП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.ВведенВОборот"));
		ПараметрыСканирования.ДопустимыеСтатусыМОТП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.ВведенВОборотВозвращен"));
		
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокМОТП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.ВведенВОборот"));
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокМОТП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.ВведенВОборотВозвращен"));
		
		ПараметрыСканирования.ДопустимыПроверкиСеройЗоныМОТП  = Истина;
		ПараметрыСканирования.СохранятьКодыМаркировкиВПулИСМП = Истина;
		ПараметрыСканирования.ОтборПоВидуПродукции            = Истина;
		
		// Для обогащения информации об ошибке при выводе пользователю
		ПараметрыСканирования.ВидОперацииИСМП = ВидОперацииИСМП;
		
	КонецЕсли;
	
	Если ВключенаПоддержкаИСМП Тогда
		
		ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборот"));
		ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборотПриВозврате"));
		ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборота"));
		ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборотаРозничнаяПродажа"));
		ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборотаПоДоговоруРассрочки"));
		ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ОжидаетДоставки"));
		
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборот"));
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборотПриВозврате"));
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборота"));
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборотаРозничнаяПродажа"));
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборотаПоДоговоруРассрочки"));
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ОжидаетДоставки"));
		
		ПараметрыСканирования.СохранятьКодыМаркировкиВПулИСМП = Истина;
		ПараметрыСканирования.ОтборПоВидуПродукции            = Истина;
		
		ПараметрыСканирования.ПоддерживаетсяОбъемноСортовойУчет  = Истина;
		ПараметрыСканирования.ЗапрашиватьКоличествоМерногоТовара = ИнтеграцияИСМПКлиентСерверПовтИсп.НастройкиСканированияКодовМаркировки().ЗапрашиватьКоличествоМерногоТовара;
		// Для обогащения информации об ошибке при выводе пользователю
		ПараметрыСканирования.ВидОперацииИСМП = ВидОперацииИСМП;
		
	КонецЕсли;
	
	Если ВключенаПоддержкаИСМП Или ВключенаПоддержкаМОТП Тогда
		НастроитьПараметрыСканированияПоРаздельномуКонтролю(ПараметрыСканирования, ВидПродукции);
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Заполняет библиотечные настройки параметров сканирования для документов закупки (приобретения) товара.
//   Применимо для настроек МОТП, ИСМП.
//
// Параметры:
//   ПараметрыСканирования - см. ШтрихкодированиеИСКлиентСервер.БазовыеПараметрыСканирования
//   ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции ИС.
// Возвращаемое значение:
//   Булево - параметры сканирования дополнены спецификой ИС МП.
Функция ЗаполнитьПараметрыСканированияДокументаЗакупки(ПараметрыСканирования, ВидПродукции) Экспорт
	
	Если Не ИнтеграцияИСМПКлиентСерверПовтИсп.ВестиУчетМаркируемойПродукции() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВидОперацииИСМП = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.Приемка");
	
	ВключенаПоддержкаМОТП = ШтрихкодированиеМОТПКлиентСервер.ВключитьПоддержкуТабачнойПродукции(ПараметрыСканирования, ВидПродукции,, ВидОперацииИСМП);
	ВключенаПоддержкаИСМП = ВключитьПоддержкуПродукцииИСМП(ПараметрыСканирования, ВидПродукции);
	
	Если ВключенаПоддержкаМОТП Тогда
		
		ПараметрыСканирования.ДопустимыеСтатусыМОТП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.ВведенВОборот"));
		ПараметрыСканирования.ДопустимыеСтатусыМОТП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.ВведенВОборотВозвращен"));
		ПараметрыСканирования.ДопустимыеСтатусыМОТП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.Нанесен"));
		ПараметрыСканирования.ДопустимыеСтатусыМОТП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.НанесенОплачен"));
		ПараметрыСканирования.ДопустимыеСтатусыМОТП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.НанесенНеОплачен"));
		
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокМОТП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.ВведенВОборот"));
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокМОТП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.ВведенВОборотВозвращен"));
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокМОТП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.Нанесен"));
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокМОТП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.НанесенОплачен"));
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокМОТП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.НанесенНеОплачен"));
		
		ПараметрыСканирования.ОтборПоВидуПродукции           = Истина;
		ПараметрыСканирования.ДопустимыПроверкиСеройЗоныМОТП = Истина;
		
		// Для обогащения информации об ошибке при выводе пользователю
		ПараметрыСканирования.ВидОперацииИСМП = ВидОперацииИСМП;
		
	КонецЕсли;
	
	Если ВключенаПоддержкаИСМП Тогда
		
		ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборот"));
		ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборотПриВозврате"));
		ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ОжидаетДоставки"));
		
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборот"));
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборотПриВозврате"));
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ОжидаетДоставки"));
		
		ПараметрыСканирования.ОтборПоВидуПродукции = Истина;
		
		ПараметрыСканирования.ПоддерживаетсяОбъемноСортовойУчет  = Истина;
		ПараметрыСканирования.ЗапрашиватьКоличествоМерногоТовара = ИнтеграцияИСМПКлиентСерверПовтИсп.НастройкиСканированияКодовМаркировки().ЗапрашиватьКоличествоМерногоТовара;
		// Для обогащения информации об ошибке при выводе пользователю
		ПараметрыСканирования.ВидОперацииИСМП = ВидОперацииИСМП;
		
	КонецЕсли;
	
	Если ВключенаПоддержкаИСМП Или ВключенаПоддержкаМОТП Тогда
		НастроитьПараметрыСканированияПоРаздельномуКонтролю(ПараметрыСканирования, ВидПродукции);
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ДополнитьПараметрамиСканированияИСМП(ПараметрыСканирования) Экспорт
	
	Если ПараметрыСканирования.Свойство("ЭтоАгрегацияКодовМаркировкиИСМП") Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыСканирования.Вставить("ВыполняетсяЗагрузкаДанныхВФормуПроверкиИПодбораИСМП", Ложь);
	
	ПараметрыСканирования.Вставить("ОпределениеВидаПродукцииИСМП",     Ложь);
	ПараметрыСканирования.Вставить("СохранятьКодыМаркировкиВПулИСМП",  Ложь);
	ПараметрыСканирования.Вставить("ТребоватьПолныйКодМаркировкиИСМП", Ложь);
	ПараметрыСканирования.Вставить("ЗаписыватьЛогЗапросовИСМП",        Ложь);
	
	ПараметрыСканирования.Вставить("ВидОперацииИСМП",                   Неопределено);
	ПараметрыСканирования.Вставить("ЭтоПроверкаКодовМаркировкиИСМП",    Ложь);
	ПараметрыСканирования.Вставить("ЭтоАгрегацияКодовМаркировкиИСМП",   Ложь);
	ПараметрыСканирования.Вставить("ЭтоПечатьКодаМаркировкиИзПулаИСМП", Ложь);
	ПараметрыСканирования.Вставить("ЭтоОтчетПроизводственнойЛинии",     Ложь);
	ПараметрыСканирования.Вставить("ОперацияНанесенияТолькоДляНаборов", Ложь);
	
	ПараметрыСканирования.Вставить("ЗапрашиватьДанныеНеизвестныхУпаковокИСМП", Ложь);
	ПараметрыСканирования.Вставить("ЗапрашиватьДанныеСервисаИСМП",             Ложь);
	
	ПараметрыСканирования.Вставить("КонтролироватьСтатусыКодовМаркировкиИСМП",    Ложь);
	ПараметрыСканирования.Вставить("КонтролироватьВладельцевКодовМаркировкиИСМП", Ложь);
	ПараметрыСканирования.Вставить("ТребуетсяПроверкаСредствамиККТ",              Ложь);
	ПараметрыСканирования.Вставить("ККТФФД12ИСМП",                                Неопределено);
	ПараметрыСканирования.Вставить("НомерФискальногоНакопителя",                  "");
	
	ПараметрыСканирования.Вставить("ТребуетсяЧастичноеВыбытие",                Ложь);
	ПараметрыСканирования.Вставить("ИмяКолонкиЧастичноеВыбытиеКоличество",     Неопределено);
	ПараметрыСканирования.Вставить("ИмяКолонкиЧастичноеВыбытиеВариантУчета",   Неопределено);
	ПараметрыСканирования.Вставить("ИмяКолонкиЧастичноеВыбытиеНоменклатура",   Неопределено);
	ПараметрыСканирования.Вставить("ИмяКолонкиЧастичноеВыбытиеХарактеристика", Неопределено);
	
	ПараметрыСканирования.Вставить("ИмяКолонкиРазрешительныйРежимИдентификаторЗапросаГИСМТ", Неопределено);
	ПараметрыСканирования.Вставить("ИмяКолонкиРазрешительныйРежимДатаЗапросаГИСМТ",          Неопределено);
	
	ПараметрыСканирования.Вставить("ПоддерживаетсяОбъемноСортовойУчет",        Ложь);
	ПараметрыСканирования.Вставить("ЗаполнятьGTINПотребительскихУпаковок",     Ложь);
	
	ПараметрыСканирования.Вставить("ТолькоВесоваяПродукция", Ложь);
	
	ИнтеграцияИСМПКлиентСервер.УстановитьПараметрСканированияЗапрашиватьДанныеНеизвестныхУпаковокИСМП(ПараметрыСканирования);
	ИнтеграцияИСМПКлиентСервер.УстановитьПараметрСканированияЗапрашиватьДанныеСервисаИСМП(ПараметрыСканирования);
	
	НастройкиСканированияКодовМаркировки = ИнтеграцияИСМПКлиентСерверПовтИсп.НастройкиСканированияКодовМаркировки();
	
	ПараметрыСканирования.КонтролироватьСтандартнуюВложенность      = НастройкиСканированияКодовМаркировки.КонтролироватьСтандартнуюВложенность;
	ПараметрыСканирования.ПропускатьСтрокиСОшибкамиПриЗагрузкеИзТСД = НастройкиСканированияКодовМаркировки.ПропускатьСтрокиСОшибкамиПриЗагрузкеИзТСД;
	ПараметрыСканирования.ПроверятьАлфавитКодовМаркировки           = НастройкиСканированияКодовМаркировки.ПроверятьАлфавитКодовМаркировки;
	
	ПараметрыСканирования.ПроверятьСтруктуруКодовМаркировки = НастройкиСканированияКодовМаркировки.ПроверятьСтруктуруКодовМаркировки;
	
	// Только для табачной продукции
	Если ПараметрыСканирования.Свойство("ПроверятьПотребительскиеУпаковкиНаВхождениеВСеруюЗонуМОТП") Тогда
		ПараметрыСканирования.ПроверятьПотребительскиеУпаковкиНаВхождениеВСеруюЗонуМОТП =
			НастройкиСканированияКодовМаркировки.ПроверятьПотребительскиеУпаковкиНаВхождениеВСеруюЗонуМОТП;
	КонецЕсли;
	Если ПараметрыСканирования.Свойство("ПроверятьЛогистическиеИГрупповыеУпаковкиНаСодержаниеСерыхКодовМОТП") Тогда
		ПараметрыСканирования.ПроверятьЛогистическиеИГрупповыеУпаковкиНаСодержаниеСерыхКодовМОТП =
			НастройкиСканированияКодовМаркировки.ПроверятьЛогистическиеИГрупповыеУпаковкиНаСодержаниеСерыхКодовМОТП;
	КонецЕсли;
	Если ПараметрыСканирования.Свойство("ДатаПроизводстваНачалаКонтроляСтатусовКодовМаркировкиМОТП") Тогда
		ПараметрыСканирования.ДатаПроизводстваНачалаКонтроляСтатусовКодовМаркировкиМОТП =
			НастройкиСканированияКодовМаркировки.ДатаПроизводстваНачалаКонтроляСтатусовКодовМаркировкиМОТП;
	КонецЕсли;
	
КонецПроцедуры

// Включает поддержку маркируемой продукции, дополняя базовые параметры сканирования, параметрами необходимыми для обработки
// штрихкодов ИСМП (кроме табака).
//
// Параметры:
//   ПараметрыСканирования - Структура - (См. ШтрихкодированиеИСКлиент.ПараметрыСканирования), дополняются параметрами:
//    * ЗапрашиватьДанныеСервисаИСМП - Булево, Строка - Истина, если требуется разрешить запрашивать данные в ГИС МТ, "ПоУмолчанию" - если проверяется по ФО с учетом исключений
//    * ДопустимыеСтатусыИСМП - Массив из ПеречислениеСсылка.СтатусыКодовМаркировкиИСМП - Статусы, которые разрешены.
//    * КонтролироватьРасхожденияСИСМП - Булево - Истина, если требуется сравнить данные, хранящиеся в справочнике
//          Штрихкоды упаковок товаров с данными сервиса ИС МП и отобразить расхождения.
//   ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции
//   АнализироватьУчитываемыеВидыПродукции - Булево - Анализировать учитываемые виды продукции
//
// Возвращаемое значение:
//  Булево - параметры сканирования штрихкодов обуви добавлены
//
Функция ВключитьПоддержкуПродукцииИСМП(ПараметрыСканирования, ВидПродукции = Неопределено, АнализироватьУчитываемыеВидыПродукции = Истина) Экспорт
	
	Если ЗначениеЗаполнено(ВидПродукции)
		И Не ИнтеграцияИСКлиентСервер.ЭтоПродукцияИСМП(ВидПродукции) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если АнализироватьУчитываемыеВидыПродукции Тогда
		ВидыПродукцииИСМП = ИнтеграцияИСМПКлиентСерверПовтИсп.УчитываемыеВидыМаркируемойПродукции(, Ложь);
		Если ВидыПродукцииИСМП.Количество() = 0 Тогда
			Возврат Ложь;
		КонецЕсли;
	Иначе
		ВидыПродукцииИСМП = ИнтеграцияИСКлиентСервер.ВидыПродукцииИСМП(Ложь);
	КонецЕсли;
	
	Если ВидПродукции = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПараметрыСканирования.ДопустимыеВидыПродукции, ВидыПродукцииИСМП, Истина);
	ИначеЕсли ВидыПродукцииИСМП.Найти(ВидПродукции) = Неопределено Тогда
		Возврат Ложь;
	Иначе
		ПараметрыСканирования.ДопустимыеВидыПродукции.Добавить(ВидПродукции);
	КонецЕсли;
	
	ПараметрыСканирования.Вставить("ДопустимыеСтатусыИСМП",                                Новый Массив);
	ПараметрыСканирования.Вставить("ДопустимыеСтатусыУпаковокИСМП",                        Новый Массив);
	ПараметрыСканирования.Вставить("ДопустимыеСтатусыНаборовИСМП",                         Неопределено);
	ПараметрыСканирования.Вставить("КонтролироватьРасхождениеСоставаУпаковокМеждуИБиИСМП", Ложь);
	ПараметрыСканирования.Вставить("ЭтоМаркировкаОстатков",                                Неопределено);
	ПараметрыСканирования.Вставить("ЗаполнятьДанныеВЕТИС",                                 Ложь);
	ПараметрыСканирования.Вставить("ЗаполнятьСрокГодности",                                Ложь);
	ПараметрыСканирования.Вставить("КонтролироватьОкончаниеСрокаГодности",                 Ложь);
	ПараметрыСканирования.Вставить("ИндивидуализироватьКИЗ",                               Ложь);
	ПараметрыСканирования.Вставить("ДатаДокумента",                                        '00010101');
	
	ДополнитьПараметрамиСканированияИСМП(ПараметрыСканирования);
	
	Возврат Истина;
	
КонецФункции

Процедура ДополнитьПараметрыЗаполненияТабличнойЧастиТовары(ПараметрыЗаполнения) Экспорт
	
	ПараметрыЗаполнения.Вставить("ЗаполнитьGTIN", Истина);
	
КонецПроцедуры

// Заполняет параметры сканирования по контексту.
// 
// Параметры:
//  Контекст - ФормаКлиентскогоПриложения, ДокументСсылка - источник заполнения параметров сканирования
//  ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС - по данному параметру будет проиходить отбор заполнения
//  ПараметрыСканирования - См. ШтрихкодированиеИСКлиент.ПараметрыСканирования
//  ПараметрыРежимаИсправленияОшибок - Структура - Параметры режима исправления ошибок
Процедура ЗаполнитьПараметрыСканирования(Контекст, ВидПродукции, ПараметрыСканирования, ПараметрыРежимаИсправленияОшибок = Неопределено) Экспорт
	
	Если ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.МаркировкаТоваровИСМП") Тогда
		
		ЗаполнитьПараметрыСканированияМаркировкаТоваров(Контекст, ПараметрыСканирования, ПараметрыРежимаИсправленияОшибок);
		
	ИначеЕсли ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.ВыводИзОборотаИСМП") Тогда
		
		ЗаполнитьПараметрыСканированияВыводаИзОборота(Контекст, ПараметрыСканирования);
		
	ИначеЕсли ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.ВозвратВОборотИСМП") Тогда
		
		ЗаполнитьПараметрыСканированияВозвратаВОборот(Контекст, ПараметрыСканирования);
	
	ИначеЕсли ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.СписаниеКодовМаркировкиИСМП") Тогда
		
		ЗаполнитьПараметрыСканированияСписанияКодовМаркировки(Контекст, ПараметрыСканирования);
		
	ИначеЕсли ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.ПеремаркировкаТоваровИСМП") Тогда
		
		ЗаполнитьПараметрыСканированияПеремаркировкаТоваров(Контекст, ПараметрыСканирования);
		
	ИначеЕсли ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.ОтгрузкаТоваровИСМП") Тогда 
		
		ЗаполнитьПараметрыСканированияОтгрузкаТоваровИСМП(Контекст, ПараметрыСканирования);
	
	ИначеЕсли ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.ПриемкаТоваровИСМП") Тогда 
		
		ЗаполнитьПараметрыСканированияПриемкаТоваровИСМП(Контекст, ПараметрыСканирования);
		
	ИначеЕсли ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.ЗаказНаЭмиссиюКодовМаркировкиСУЗ") Тогда 
		
		ЗаполнитьПараметрыСканированияЗаказНаЭмиссиюКодовМаркировкиСУЗ(Контекст, ПараметрыСканирования);
		
	ИначеЕсли ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Обработка.ПроверкаИПодборПродукцииИСМП") Тогда 
		
		ВключитьПоддержкуПродукцииИСМП(ПараметрыСканирования);
		
		ПараметрыСканирования.ИмяТабличнойЧастиШтрихкодыУпаковок = Неопределено;
		ПараметрыСканирования.ИмяКолонкиШтрихкодУпаковки         = Неопределено;
		
		ПараметрыСканирования.ТолькоМаркируемаяПродукция = Истина;
	
	ИначеЕсли ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Обработка.ПроверкаКодовМаркировкиИСМП") Тогда 
		
		ЗаполнитьПараметрыСканированияПроверкаКодовМаркировки(Контекст, ПараметрыСканирования);
		
	ИначеЕсли ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.УточнениеСведенийОКодахМаркировкиИСМП") Тогда
		
		ЗаполнитьПараметрыСканированияУточнениеСведенийОКодахМаркировкиИСМП(Контекст, ПараметрыСканирования);
		
	ИначеЕсли ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.ОтчетОбИмпортеЕГАИС") Тогда
		
		ЗаполнитьПараметрыСканированияОтчетОбИмпортеЕГАИС(ПараметрыСканирования, Контекст);
		
	ИначеЕсли ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.ОтчетОПроизводствеЕГАИС") Тогда
		
		ЗаполнитьПараметрыСканированияОтчетОПроизводствеЕГАИС(ПараметрыСканирования, Контекст);
	
	ИначеЕсли ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "РегистрСведений.КегиНаОборудованииРозливаИСМП") Тогда
		
		ЗаполнитьПараметрыСканированияПодключенияКОборудованиюРозлива(Контекст, ПараметрыСканирования);
		
	ИначеЕсли ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.ОтчетИСМП") Тогда
		
		ЗаполнитьПараметрыСканированияОтчетИСМП(Контекст, ПараметрыСканирования);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура НастроитьПараметрыСканированияПоРаздельномуКонтролю(ПараметрыСканирования, ВидПродукции) Экспорт
	
	ЗапрашиватьДанныеСервиса = ИнтеграцияИСМПКлиентСерверПовтИсп.ЗапрашиватьДанныеСервиса();
	
	Если ЗапрашиватьДанныеСервиса Тогда
	
		ПараметрыСканирования.КонтролироватьСтатусыКодовМаркировкиИСМП = НастройкаПараметровСканированияСлужебныйКлиентСерверПовтИсп.КонтролироватьСтатусыКодовМаркировки(
			ВидПродукции,
			ПараметрыСканирования.ВидОперацииИСМП);
		
		ПараметрыСканирования.КонтролироватьВладельцевКодовМаркировкиИСМП = НастройкаПараметровСканированияСлужебныйКлиентСерверПовтИсп.КонтролироватьВладельцевКодовМаркировки(
			ВидПродукции,
			ПараметрыСканирования.ВидОперацииИСМП);
		
	КонецЕсли;
	
КонецПроцедуры

// Формирует новую структуру данных описания расширенного представления ошибки.
// 
// Возвращаемое значение:
// 	Структура:
// * Статус               - Неопределено, ПеречислениеСсылка.СтатусыКодовМаркировкиИСМП - Ошибочный статус.
// * ВидОперацииИСМП      - Неопределено, ПеречислениеСсылка.ВидыОперацийИСМП           - Вид операции.
// * СпособВводаВОборот   - Неопределено, ПеречислениеСсылка.СпособыВводаВОборотСУЗ     - Способ ввода в оборот.
// * Владелец             - Булево - Признак ошибки владельца.
// * ВозможноИгнорировать - Булево - Признак продолжения операциия после подтверждения пользователя.
// * ЗаголовокПродолжить  - Неопределено, Строка    - Заголовок кнопки игнорирования ошибки.
// * ДанныеПроверкиНаККТ  - Неопределено, Структура - Данные проверки ККТ.
// * ОбщаяОшибка          - Булево                  - Признак общей ошибки.
// * ТокенАвторизацииГИСМТ - Булево - Признак отсутствия токена авторизации для получения идентификаторов ГИС МТ
// * ЧастичноеВыбытиеНастроитьКарточкуНоменклатуры - Булево - Вывод подсказки настройки карточки номенклатуры.
// * ЧастичноеВыбытиеНоменклатура                  - Булево, Неопределено - Гиперссылкка настройки номенклатуры.
// * ЗагрузитьCDNПлощадки                          - Булево - Вывод подсказки загрузки CDN-площадок
// * ЗапретитьОтключениеКонтролейСтатусовИВладельцев - Булево - Истина, если продукция подлежит розничному контролю и отключить контроли нельзя
Функция ПараметрыРасширенногоОписанияОшибки() Экспорт
	
	ВозвращаемоеЗначение = Новый Структура();
	
	ВозвращаемоеЗначение.Вставить("Статус",                                          Неопределено);
	ВозвращаемоеЗначение.Вставить("Владелец",                                        Ложь);
	ВозвращаемоеЗначение.Вставить("ВидОперацииИСМП",                                 Неопределено);
	ВозвращаемоеЗначение.Вставить("ОбщаяОшибка",                                     Ложь);
	ВозвращаемоеЗначение.Вставить("ВозможноИгнорировать",                            Ложь);
	ВозвращаемоеЗначение.Вставить("ДанныеПроверкиНаККТ",                             Неопределено);
	ВозвращаемоеЗначение.Вставить("ЗаголовокПродолжить",                             Неопределено);
	ВозвращаемоеЗначение.Вставить("СпособВводаВОборот",                              Неопределено);
	ВозвращаемоеЗначение.Вставить("ТокенАвторизацииГИСМТ",                           Ложь);
	ВозвращаемоеЗначение.Вставить("ЧастичноеВыбытиеНастроитьКарточкуНоменклатуры",   Ложь);
	ВозвращаемоеЗначение.Вставить("ЧастичноеВыбытиеНоменклатура",                    Неопределено);
	ВозвращаемоеЗначение.Вставить("ЗагрузитьCDNПлощадки",                            Ложь);
	ВозвращаемоеЗначение.Вставить("ЗапретитьОтключениеКонтролейСтатусовИВладельцев", Ложь);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

#Область ПроверкаСредствамиККТ

Функция ДанныеПредставленияРезультатовПроверкиСредствамиККТ(СтрокаДанных, ДополнятьТекстомОшибки = Истина) Экспорт
	
	ВозвращаемоеЗначение  = Новый Структура();
	ВозвращаемоеЗначение.Вставить("ЕстьОшибки",                   Ложь);
	ВозвращаемоеЗначение.Вставить("ТребуетсяПолныйКодМаркировки", Ложь);
	ВозвращаемоеЗначение.Вставить("ОписаниеОшибок",               "");
	ВозвращаемоеЗначение.Вставить("ПредставлениеВЧеке",           Неопределено);
	
	ПредставленияВЧеке  = ПредставлениеКодаМаркировкиВЧеке();
	МассивТекстовОшибки = Новый Массив;
	
	Если СтрокаДанных.ТребуетсяПолныйКодМаркировки Тогда
		
		ТекстОшибки = НСтр("ru = 'Отсутствует полный код маркировки'");
		МассивТекстовОшибки.Добавить(ТекстОшибки);
		ВозвращаемоеЗначение.ТребуетсяПолныйКодМаркировки = Истина;
		
		ДополнитьПредставлениеВЧеке(ВозвращаемоеЗначение, ПредставленияВЧеке.Отсутствует);
		
	ИначеЕсли СтрокаДанных.ВидУпаковки <> ПредопределенноеЗначение("Перечисление.ВидыУпаковокИС.ОбъемноСортовойУчет") Тогда
		
		ОшибочныеКодыПроверки = КодыРезультатаПроверкиДляОтображенияОшибки();
		ДанныеОписания        = ОшибочныеКодыПроверки.Получить(СтрокаДанных.ПредставлениеРезультатаПроверки);
		
		Если ДанныеОписания <> Неопределено Тогда
			
			ВозвращаемоеЗначение.ПредставлениеВЧеке = ДанныеОписания.ПредставлениеВЧеке;
			МассивТекстовОшибки.Добавить(ДанныеОписания.Описание);
			
		КонецЕсли;
	
		Если СтрокаДанных.КодМаркировкиПроверен
			И Не СтрокаДанных.РезультатПроверки
			И ДанныеОписания = Неопределено Тогда
			
			МассивТекстовОшибки.Добавить(НСтр("ru = 'Результат проверки КП КМ фискальным накопителем с использованием ключа проверки КП отрицательный.'"));
			ДополнитьПредставлениеВЧеке(ВозвращаемоеЗначение, ПредставленияВЧеке.ММинус);
			
		КонецЕсли;
		
		Если СтрокаДанных.КодОбработкиЗапроса = "1" Тогда
			МассивТекстовОшибки.Добавить(НСтр("ru = 'Запрос проверки статуса ОИСМ имеет некорректный формат.'"));
			ДополнитьПредставлениеВЧеке(ВозвращаемоеЗначение, ПредставленияВЧеке.ММинус);
		ИначеЕсли СтрокаДанных.КодОбработкиЗапроса = "2" Тогда
			МассивТекстовОшибки.Добавить(НСтр("ru = 'Указанный в запросе код маркировки имеет некорректный формат (не распознан).'"));
			ДополнитьПредставлениеВЧеке(ВозвращаемоеЗначение, ПредставленияВЧеке.ММинус);
		КонецЕсли;
		
		Если Не СтрокаДанных.РезультаПроверкиОИСМ
			И ДанныеОписания = Неопределено Тогда
			
			МассивТекстовОшибки.Добавить(НСтр("ru = 'Проверка статуса товара ОИСМ завершилась с отрицательным результатом.'"));
			ДополнитьПредставлениеВЧеке(ВозвращаемоеЗначение, ПредставленияВЧеке.М);
			
		КонецЕсли;
		
		Если СтрокаДанных.СтатусТовара = ПредопределенноеЗначение("Перечисление.ОтветОИСМОСтатусеТовара.ОборотТовараПриостановлен")
			Или СтрокаДанных.СтатусТовара = ПредопределенноеЗначение("Перечисление.ОтветОИСМОСтатусеТовара.ПланируемыйСтатусТовараНекорректен") Тогда
			
			МассивТекстовОшибки.Добавить(Строка(СтрокаДанных.СтатусТовара));
			ДополнитьПредставлениеВЧеке(ВозвращаемоеЗначение, ПредставленияВЧеке.ММинус);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если МассивТекстовОшибки.Количество() Тогда
		
		ВозвращаемоеЗначение.ЕстьОшибки = Истина;
		Если ДополнятьТекстомОшибки
			И ЗначениеЗаполнено(СтрокаДанных.ТекстОшибки) Тогда
			МассивТекстовОшибки.Добавить(СтрокаДанных.ТекстОшибки);
		КонецЕсли;
		
	КонецЕсли;
	
	ВозвращаемоеЗначение.ОписаниеОшибок = СтрСоединить(МассивТекстовОшибки, Символы.ПС);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Конструктор элемента данных для проверки кода маркировки средствами ККТ.
// 
// Параметры:
// 
// Возвращаемое значение:
//  Структура - Новый элемент данных проверки средствами ККТ:
// * КодМаркировки                  - Строка - Код маркировки.
// * ПолныйКодМаркировки            - Строка - Полный код маркировки.
// * ВидПродукции                   - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции
// * ПланируемыйСтатусТовара        - ПеречислениеСсылка.ПланируемыйСтатусМаркируемогоТовара - Планируемый статус.
// * ВидПродукции                   - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции.
// * ВидУпаковки                    - ПеречислениеСсылка.ВидыУпаковокИС  - Вид упаковки.
// * ШтрихкодУпаковки               - Неопределено, СправочникСсылка.ШтрихкодыУпаковокТоваров - Ссылка на штрихкод, используется в сценариях сохранения результатов проверки.
// * ПредставлениеНоменклатуры      - Строка, Неопределено - Представление позиции для отображения пользователю. Если не заполнено - будет определяться по ШтрихкодУпаковки.
// * ПолученРезультатЗапросаКМ      - Булево - Служебный. Для блокировки повторного оповещения.
// * ТекстОшибки                    - Строка, Неопределено - Текст ошибки распределения.
// * Количество                     - Число                - Количество штук или иной мере (вес, объем и.т.д.).
// * ЧастичноеВыбытиеКоличество     - Число, Неопределено  - Числитель частичного выбытия.
// * ЕмкостьПотребительскойУпаковки - Число, Неопределено  - Знаменатель частичного выбытия.
// * КодЕдиницыИзмерения            - Строка, Неопределено - Код единицы измерения частично выбытия
// * КоличествоПотребительскихУпаковок - Число - Количество потребительских упаковок
// * РазрешительныйРежимИдентификаторЗапросаГИСМТ - ОпределяемыйТип.УникальныйИдентификаторИС - Идентификатор запроса ГИС МТ
// * РазрешительныйРежимДатаЗапросаГИСМТ          - Строка - Дата получения идентификатора ГИС МТ в формате timestamp
// * РазрешительныйРежимАдресСервера              - Строка - адрес сервера разрешительного запроса
// * РазрешительныйРежимТелоЗапросаJSON           - Строка - тело разрешительного запроса
// * РазрешительныйРежимТелоОтветаJSON            - Строка - тело ответа разрешительного запроса
// * РазрешительныйРежимКодОтвета                 - Строка - код ответа разрешительного запроса
Функция НовыйЭлементДанныхПроверкиСредствамиККТ() Экспорт
	
	ВозвращаемоеЗначение = Новый Структура();
	ВозвращаемоеЗначение.Вставить("КодМаркировки",                                "");
	ВозвращаемоеЗначение.Вставить("ПолныйКодМаркировки",                          "");
	ВозвращаемоеЗначение.Вставить("ВидПродукции",                                 Неопределено);
	ВозвращаемоеЗначение.Вставить("ПолученРезультатЗапросаКМ",                    Ложь); // Блокировка от повторного оповещения
	ВозвращаемоеЗначение.Вставить("ПланируемыйСтатусТовара",                      Неопределено);
	ВозвращаемоеЗначение.Вставить("ВидУпаковки",                                  Неопределено);
	ВозвращаемоеЗначение.Вставить("ШтрихкодУпаковки",                             Неопределено);
	ВозвращаемоеЗначение.Вставить("СоставКодаМаркировки",                         Неопределено);
	ВозвращаемоеЗначение.Вставить("ПредставлениеНоменклатуры",                    Неопределено);
	ВозвращаемоеЗначение.Вставить("ИдентификаторЗапроса",                         СокрЛП(Новый УникальныйИдентификатор));
	ВозвращаемоеЗначение.Вставить("ИдентификаторЭлемента",                        СокрЛП(Новый УникальныйИдентификатор));
	ВозвращаемоеЗначение.Вставить("ТребуетВзвешивания",                           Ложь);
	ВозвращаемоеЗначение.Вставить("Количество",                                   0);
	ВозвращаемоеЗначение.Вставить("ЧастичноеВыбытиеКоличество",                   Неопределено);
	ВозвращаемоеЗначение.Вставить("ЕмкостьПотребительскойУпаковки",               Неопределено);
	ВозвращаемоеЗначение.Вставить("КодЕдиницыИзмерения",                          Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                                  Неопределено);
	ВозвращаемоеЗначение.Вставить("КоличествоПотребительскихУпаковок",            0);
	ВозвращаемоеЗначение.Вставить("РазрешительныйРежимИдентификаторЗапросаГИСМТ", "");
	ВозвращаемоеЗначение.Вставить("РазрешительныйРежимДатаЗапросаГИСМТ",          "");
	ВозвращаемоеЗначение.Вставить("РазрешительныйРежимАдресСервера",              "");
	ВозвращаемоеЗначение.Вставить("РазрешительныйРежимТелоЗапросаJSON",           "");
	ВозвращаемоеЗначение.Вставить("РазрешительныйРежимТелоОтветаJSON",            "");
	ВозвращаемоеЗначение.Вставить("РазрешительныйРежимКодОтвета",                 "");
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция НовыйЭлементПроверкиСредствамиККТПоДаннымШтрихкода(ДанныеШтрихкода) Экспорт
	
	ЭлементПроверки  = НовыйЭлементДанныхПроверкиСредствамиККТ();
	ЗаполнитьЗначенияСвойств(ЭлементПроверки, ДанныеШтрихкода);
	ЭлементПроверки.КодМаркировки = ДанныеШтрихкода.НормализованныйШтрихкод;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеШтрихкода, "ЧастичноеВыбытие")
		И ДанныеШтрихкода.ЧастичноеВыбытие Тогда
		ЭлементПроверки.ЕмкостьПотребительскойУпаковки = ДанныеШтрихкода.ЕмкостьПотребительскойУпаковки;
		ЭлементПроверки.ЧастичноеВыбытиеКоличество     = ЭлементПроверки.Количество;
		Если ЭлементПроверки.Количество < 1 Тогда
			ЭлементПроверки.ЧастичноеВыбытиеКоличество = Окр(ЭлементПроверки.ЕмкостьПотребительскойУпаковки * ЭлементПроверки.Количество);
		КонецЕсли;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеШтрихкода, "РазрешительныйРежимДатаЗапросаГИСМТ") Тогда
		ЭлементПроверки.РазрешительныйРежимДатаЗапросаГИСМТ = ДанныеШтрихкода.РазрешительныйРежимДатаЗапросаГИСМТ;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеШтрихкода, "РазрешительныйРежимИдентификаторЗапросаГИСМТ") Тогда
		ЭлементПроверки.РазрешительныйРежимИдентификаторЗапросаГИСМТ = ДанныеШтрихкода.РазрешительныйРежимИдентификаторЗапросаГИСМТ;
	КонецЕсли;
	
	Возврат ЭлементПроверки;
	
КонецФункции

// Представление кода маркировки в чеке.
// 
// Возвращаемое значение:
//  Структура - Представление кода маркировки в чеке:
// * Отсутствует - Строка - "[М] отсутствует".
// * МПлюс       - Строка - "[М+]".
// * ММинус      - Строка - "[М-]".
// * М           - Строка - "[М]".
Функция ПредставлениеКодаМаркировкиВЧеке() Экспорт
	
	ВозвращаемоеЗначение = Новый Структура();
	ВозвращаемоеЗначение.Вставить("Отсутствует", "[М] отсутствует");
	ВозвращаемоеЗначение.Вставить("МПлюс",       "[М+]");
	ВозвращаемоеЗначение.Вставить("ММинус",      "[М-]");
	ВозвращаемоеЗначение.Вставить("М",           "[М]");
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

#КонецОбласти

Функция ТребуетсяУточнениеДанных(КодМаркировкиДляУточнения) Экспорт
	
	ПараметрыСканирования = КодМаркировкиДляУточнения.ПараметрыСканирования;
	РезультатОбработкиШтрихкода = КодМаркировкиДляУточнения.РезультатОбработкиШтрихкода;
	ДанныеШтрихкода = РезультатОбработкиШтрихкода.ДанныеШтрихкода;
	
	Если РезультатОбработкиШтрихкода.ТребуетсяВыборСерии
			И Не ЗначениеЗаполнено(ДанныеШтрихкода.Серия) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если ПараметрыСканирования.ЗаполнятьДанныеВЕТИС = Истина
			И Не ЗначениеЗаполнено(ДанныеШтрихкода.ИдентификаторПроисхожденияВЕТИС) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если ПараметрыСканирования.ЗаполнятьСрокГодности = Истина
			И Не ЗначениеЗаполнено(ДанныеШтрихкода.ГоденДо) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Процедура ЗаполнитьДопустимыеНачальныеСтатусыОтчетОНанесении(Статусы) Экспорт
	
	Статусы.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Эмитирован"));
	
КонецПроцедуры

Процедура ЗаполнитьОжидаемыеСтатусыПослеПередачиОтчетаОНанесении(Статусы) Экспорт
	
	Статусы.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Нанесен"));
	
КонецПроцедуры

Функция ПараметрыОткрытияФормыУточненияПодобраннойПродукцииИСМП() Экспорт
	
	ПараметрыОткрытияФормы = Новый Структура;
	
	ПараметрыОткрытияФормы.Вставить("ПараметрыСканирования", Неопределено);
	ПараметрыОткрытияФормы.Вставить("ВидПродукции",          Неопределено);
	ПараметрыОткрытияФормы.Вставить("Номенклатура",          Неопределено);
	ПараметрыОткрытияФормы.Вставить("Характеристика",        Неопределено);
	ПараметрыОткрытияФормы.Вставить("Серия",                 Неопределено);
	ПараметрыОткрытияФормы.Вставить("GTIN",                  Неопределено);
	ПараметрыОткрытияФормы.Вставить("КодыGTIN",              Неопределено);
	
	ПараметрыОткрытияФормы.Вставить("ГоденДо",                          '00010101');
	ПараметрыОткрытияФормы.Вставить("ИдентификаторыПроисхожденияВЕТИС", Неопределено);
	ПараметрыОткрытияФормы.Вставить("СкоропортящаясяПродукция",         Ложь);
	
	ПараметрыОткрытияФормы.Вставить("КоличествоПотребительскихУпаковок",                     0);
	ПараметрыОткрытияФормы.Вставить("КоличествоПотребительскихУпаковокТребующихВзвешивания", 0);
	ПараметрыОткрытияФормы.Вставить("КоличествоПодобрано",                                   0);
	ПараметрыОткрытияФормы.Вставить("КоличествоПодобраноВзвешено",                           0);
	ПараметрыОткрытияФормы.Вставить("КоличествоПодобраноВзвешеноОСУ",                        0);
	ПараметрыОткрытияФормы.Вставить("КоличествоПотребительскихУпаковокОСУ",                  0);
	
	ПараметрыОткрытияФормы.Вставить("УточнениеДанныхОТовареДопустимо",    Ложь);
	ПараметрыОткрытияФормы.Вставить("УточнениеКоличестваТовараДопустимо", Ложь);
	ПараметрыОткрытияФормы.Вставить("УточнениеКоличестваОСУДопустимо",    Ложь);
	
	
	ПараметрыОткрытияФормы.Вставить("ДопустимаПроизвольнаяЕдиницаУчета", Ложь);
	ПараметрыОткрытияФормы.Вставить("ЭтоДобавлениеОСУ",                  Ложь);
	
	Возврат ПараметрыОткрытияФормы;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЗаполнениеПараметровСканирования

// Дополняются свойства параметров сканирования и заполняются значения для документа "Маркировка товаров ИСМП".
//
// Параметры:
//  Контекст - ФормаКлиентскогоПриложения, ДокументСсылка, СправочникСсылка - Контекст.
//  ПараметрыСканирования - См. ШтрихкодированиеИСКлиент.ПараметрыСканирования.
//  ПараметрыРежимаИсправленияОшибок - Структура - Параметры режима исправления ошибок.
Процедура ЗаполнитьПараметрыСканированияМаркировкаТоваров(Контекст, ПараметрыСканирования, ПараметрыРежимаИсправленияОшибок = Неопределено)
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "Объект") Тогда
		ИсточникДанных = Контекст.Объект;
	Иначе
		ИсточникДанных = Контекст;
	КонецЕсли;
	
	ВидПродукции = ИсточникДанных.ВидПродукции;
	Операция     = ИсточникДанных.Операция;
	
	ПараметрыСканирования.ИмяТабличнойЧастиШтрихкодыУпаковок = "ШтрихкодыУпаковок";
	ПараметрыСканирования.ИмяКолонкиШтрихкодУпаковки         = "ШтрихкодУпаковки";
	
	ПараметрыСканирования.СозданиеНаборов = Истина;
	
	Если Не ВключитьПоддержкуПродукцииИСМП(ПараметрыСканирования, ВидПродукции) Тогда
		Возврат;
	КонецЕсли;
	
	ДоступнаПечатьЭтикеток = (ВидПродукции <> ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха"));
	
	ПараметрыСканирования.ДоступнаПечатьЭтикеток                         = ДоступнаПечатьЭтикеток;
	ПараметрыСканирования.ИспользуютсяДанныеВыбораПоМаркируемойПродукции = Истина;
	ПараметрыСканирования.ТолькоМаркируемаяПродукция                     = Истина;
	ПараметрыСканирования.КонтрольПустыхУпаковок                         = Ложь;
	ПараметрыСканирования.ПоддерживаютсяОперацииАгрегации                = Истина;
	ПараметрыСканирования.КонтролироватьСтандартнуюВложенность           = Ложь;
	
	ПараметрыСканирования.КонтрольРасхожденийСДокументомОснованием       = Истина;
	ПараметрыСканирования.ВозможнаЗагрузкаТСД                            = Истина;
	ПараметрыСканирования.РазрешенаОбработкаКодовСПустойНоменклатурой    = Истина;
	
	ПараметрыСканирования.Организация       = ИсточникДанных.Организация;
	ПараметрыСканирования.Владелец          = ИсточникДанных.Организация;
	ПараметрыСканирования.ДокументОснование = ИсточникДанных.ДокументОснование;
	
	ОперацииМаркировки = ИнтеграцияИСМПСлужебныйКлиентСервер.ОперацииМаркировки(Операция);
	ЭтоВводВОборотИмпортСФТС = (Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВводВОборотИмпортСФТС"));
	ЭтоЗаполнениеДекларацииПоКодамУпаковок = (ЭтоВводВОборотИмпортСФТС
		И ИсточникДанных.ВариантВыполненияАгрегации = ПредопределенноеЗначение("Перечисление.ВариантыЗаполненияДекларацииИСМП.КодыУпаковок"));
	ЭтоАгрегацияПередВводомВОборот = (Не ЭтоВводВОборотИмпортСФТС
		И ИсточникДанных.ВариантВыполненияАгрегации = ПредопределенноеЗначение("Перечисление.ВариантыВыполненияАгрегацииИСМП.ПередВводомВОборот"));
	ПрисутствуетОперацияНанесения = ОперацииМаркировки.ЭтоОперацияНанесения Или ЗначениеЗаполнено(ИсточникДанных.ОперацияНанесения);
	
	// Для обогащения информации об ошибке при выводе пользователю
	ПараметрыСканирования.ВидОперацииИСМП = Операция;
	
	ПараметрыСканирования.ДопустимыйСпособВводаВОборот =
		СпособВводаВОборотСУЗПоВидуОперации(Операция);
	
	ДальнейшееДействие = Неопределено;
	Если ПараметрыРежимаИсправленияОшибок <> Неопределено Тогда
		ДальнейшееДействие = ПараметрыРежимаИсправленияОшибок.ДальнейшееДействие;
	КонецЕсли;
	
	Если ОперацииМаркировки.ЭтоАгрегация Тогда
		ПараметрыСканирования.ЭтоАгрегацияКодовМаркировкиИСМП = Истина;
	КонецЕсли;
	
	Если ОперацииМаркировки.ЭтоМаркировкаОстатков Тогда
		ПараметрыСканирования.ЭтоМаркировкаОстатков = Истина;
	Иначе
		ПараметрыСканирования.ЭтоМаркировкаОстатков = Ложь;
	КонецЕсли;
	
	Если ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха")
		И ОперацииМаркировки.ЭтоПриемкаКИЗ Тогда
		ПараметрыСканирования.СопоставлятьНоменклатуру = Ложь;
	КонецЕсли;
	
	ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(Неопределено);
	ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Неопределен"));
	
	Если Не ОперацииМаркировки.ЭтоАгрегация
		И (ДальнейшееДействие = ПредопределенноеЗначение("Перечисление.ДальнейшиеДействияПоВзаимодействиюИСМП.ВыполнитеАгрегацию")
			Или ДальнейшееДействие = ПредопределенноеЗначение("Перечисление.ДальнейшиеДействияПоВзаимодействиюИСМП.ВыполнитеАгрегациюСИсправлениями")) Тогда
		
		Если ЭтоЗаполнениеДекларацииПоКодамУпаковок Или ЭтоАгрегацияПередВводомВОборот Тогда
			
			ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Нанесен"));
			ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Нанесен"));
			
		Иначе
			
			ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборот"));
			ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборот"));
			
		КонецЕсли;
		
	ИначеЕсли ОперацииМаркировки.ЭтоАгрегация Тогда
		
		ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
			ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборот"));
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(
			ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборот"));
		
		Если ПараметрыСканирования.ДопустимыеСтатусыНаборовИСМП = Неопределено Тогда
			ПараметрыСканирования.ДопустимыеСтатусыНаборовИСМП = Новый Массив;
		КонецЕсли;
		
		ПараметрыСканирования.ДопустимыеСтатусыНаборовИСМП.Добавить(
			ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Нанесен"));
		ПараметрыСканирования.ДопустимыеСтатусыНаборовИСМП.Добавить(
			ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборот"));
		
	ИначеЕсли ДальнейшееДействие = ПредопределенноеЗначение("Перечисление.ДальнейшиеДействияПоВзаимодействиюИСМП.ПередайтеДанныеСИсправлениями") Тогда
		
		Если ИсточникДанных.ОперацияНанесенияТолькоДляНаборов Тогда
			
			Если ПараметрыСканирования.ДопустимыеСтатусыНаборовИСМП = Неопределено Тогда
				ПараметрыСканирования.ДопустимыеСтатусыНаборовИСМП = Новый Массив;
			КонецЕсли;
			
			ПараметрыСканирования.ДопустимыеСтатусыНаборовИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Нанесен"));
			
			ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборот"));
			
		Иначе
			
			Если ЭтоЗаполнениеДекларацииПоКодамУпаковок Или ЭтоАгрегацияПередВводомВОборот Тогда
				ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Нанесен"));
			КонецЕсли;
			
			ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Нанесен"));
			
		КонецЕсли;
		
	Иначе
		
		Если ИсточникДанных.ОперацияНанесенияТолькоДляНаборов Тогда
			
			Если ПараметрыСканирования.ДопустимыеСтатусыНаборовИСМП = Неопределено Тогда
				ПараметрыСканирования.ДопустимыеСтатусыНаборовИСМП = Новый Массив;
			КонецЕсли;
			
			ПараметрыСканирования.ДопустимыеСтатусыНаборовИСМП.Добавить(
				ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Эмитирован"));
			
			ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
				ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборот"));
			
		Иначе
			
			Если ПрисутствуетОперацияНанесения Или ОперацииМаркировки.ЭтоПриемкаКИЗ Тогда
				
				ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
					ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Эмитирован"));
				ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(
					ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Эмитирован"));
					
				Если ОперацииМаркировки.ЭтоПриемкаКИЗ Тогда
					ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
						ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.НеИндивидуализирован"));
					ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
						ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Индивидуализирован"));
				КонецЕсли;
				
			Иначе
				ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
					ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Нанесен"));
				ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(
					ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Нанесен"));
				ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
					ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборота"));
				ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(
					ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборота"));
			КонецЕсли;
			
		КонецЕсли;
		
		Если ОперацииМаркировки.ЭтоИмпортСФТС Тогда
			ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
				ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ОжидаетПодтверждениеФТС"));
			ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(
				ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ОжидаетПодтверждениеФТС"));
		КонецЕсли;
		
	КонецЕсли;
	
	Если ИнтеграцияИСКлиентСервер.ЭтоПродукцияПодконтрольнаяВЕТИС(ВидПродукции) Тогда
		ИнтеграцияИСМПВЕТИСКлиентСервер.ДополнитьПараметрыСканированияМаркировкаТоваров(Операция, ПараметрыСканирования);
	КонецЕсли;
	
	Если ПрисутствуетОперацияНанесения Тогда
		
		ПараметрыСканирования.ОперацияНанесенияТолькоДляНаборов = ИсточникДанных.ОперацияНанесенияТолькоДляНаборов;
		ПараметрыСканирования.СохранятьКодыМаркировкиВПулИСМП   = Истина;
		ПараметрыСканирования.ТребоватьПолныйКодМаркировкиИСМП  = Истина;
		
		Если ИнтеграцияИСМПКлиентСервер.ЭтоВидПродукцииСоСрокамиГодности(ВидПродукции) Тогда
			ПараметрыСканирования.ЗаполнятьСрокГодности = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	НастроитьПараметрыСканированияПоРаздельномуКонтролю(ПараметрыСканирования, ВидПродукции);
	
	ПараметрыСканирования.ИндивидуализироватьКИЗ = ОперацииМаркировки.ЭтоИндивидуализацияКИЗ
		Или ИсточникДанных.ОперацияНанесения = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ИндивидуализацияКИЗ");
	
	Если ПрисутствуетОперацияНанесения Тогда
		ПараметрыСканирования.ЗапрашиватьКоличествоМерногоТовара = Истина;
	Иначе
		ПараметрыСканирования.ЗапрашиватьКоличествоМерногоТовара = ИнтеграцияИСМПКлиентСерверПовтИсп.НастройкиСканированияКодовМаркировки().ЗапрашиватьКоличествоМерногоТовара;
	КонецЕсли;
	
КонецПроцедуры

// Дополняются свойства параметров сканирования и заполняются значения для документа "Вывод из оборота ИСМП".
//
// Параметры:
//  Контекст - ФормаКлиентскогоПриложения, ДокументСсылка, СправочникСсылка - Контекст.
//  ПараметрыСканирования - Структура - (См. ШтрихкодированиеИСКлиент.ПараметрыСканирования).
//  ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции ИС.
Процедура ЗаполнитьПараметрыСканированияВыводаИзОборота(Контекст, ПараметрыСканирования)
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "Объект") Тогда
		ИсточникДанных = Контекст.Объект;
	Иначе
		ИсточникДанных = Контекст;
	КонецЕсли;
	
	ВидПродукции = ИсточникДанных.ВидПродукции;
	Операция     = ИсточникДанных.Операция;
	
	ПараметрыСканирования.ИмяТабличнойЧастиШтрихкодыУпаковок = "ШтрихкодыУпаковок";
	ПараметрыСканирования.ИмяКолонкиШтрихкодУпаковки         = "ШтрихкодУпаковки";
	
	Если Не ВключитьПоддержкуПродукцииИСМП(ПараметрыСканирования, ВидПродукции) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыСканирования.ИспользуютсяДанныеВыбораПоМаркируемойПродукции = Истина;
	ПараметрыСканирования.ТолькоМаркируемаяПродукция                     = Истина;
	ИнтеграцияИСМПКлиентСервер.УстановитьПараметрСканированияЗапрашиватьДанныеНеизвестныхУпаковокИСМП(ПараметрыСканирования, Истина);
	
	ПараметрыСканирования.ВозможнаЗагрузкаТСД = Истина;
	ПараметрыСканирования.Организация         = ИсточникДанных.Организация;
	
	ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборот"));
	ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборотПриВозврате"));
	
	ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборот"));
	ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборотПриВозврате"));
	
	// Для обогащения информации об ошибке при выводе пользователю
	ПараметрыСканирования.ВидОперацииИСМП = Операция;
	
	НастроитьПараметрыСканированияПоРаздельномуКонтролю(ПараметрыСканирования, ВидПродукции);
	
	ПараметрыСканирования.РазрешеноЗапрашиватьКодМаркировки = (Не ИсточникДанных.ОбъемноСортовойУчет);
	
КонецПроцедуры

// Дополняются свойства параметров сканирования и заполняются значения для документа "Списание кодов маркировки ИСМП".
//
// Параметры:
//  Контекст - ФормаКлиентскогоПриложения, ДокументСсылка, СправочникСсылка - Контекст.
//  ПараметрыСканирования - Структура - (См. ШтрихкодированиеИСКлиент.ПараметрыСканирования).
//  ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции ИС.
Процедура ЗаполнитьПараметрыСканированияСписанияКодовМаркировки(Контекст, ПараметрыСканирования)
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "Объект") Тогда
		ИсточникДанных = Контекст.Объект;
	Иначе
		ИсточникДанных = Контекст;
	КонецЕсли;
	
	ВидПродукции = ИсточникДанных.ВидПродукции;
	
	ПараметрыСканирования.ИмяТабличнойЧастиШтрихкодыУпаковок = "ШтрихкодыУпаковок";
	ПараметрыСканирования.ИмяКолонкиШтрихкодУпаковки         = "ШтрихкодУпаковки";
	
	Если Не ВключитьПоддержкуПродукцииИСМП(ПараметрыСканирования, ВидПродукции) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыСканирования.ТолькоМаркируемаяПродукция                  = Истина;
	ПараметрыСканирования.КонтрольПустыхУпаковок                      = Ложь;
	ПараметрыСканирования.ПоддерживаютсяОперацииАгрегации             = Ложь;
	ПараметрыСканирования.КонтролироватьСтандартнуюВложенность        = Ложь;
	ПараметрыСканирования.РазрешенаОбработкаКодовСПустойНоменклатурой = Истина;
	
	ПараметрыСканирования.ИспользуютсяДанныеВыбораПоМаркируемойПродукции = Истина;
	ИнтеграцияИСМПКлиентСервер.УстановитьПараметрСканированияЗапрашиватьДанныеНеизвестныхУпаковокИСМП(ПараметрыСканирования, Истина);
	
	ПараметрыСканирования.Организация = ИсточникДанных.Организация;
	
	Если ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха") Тогда
		
		ПараметрыСканирования.ВидОперацииИСМП = ИсточникДанных.Операция;
		Если ИсточникДанных.Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.СписаниеЭмитированныхКодовМаркировкиПриПоступлении") Тогда
			ПараметрыСканирования.СопоставлятьНоменклатуру = Ложь;
			ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
				ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Эмитирован"));
		Иначе
			ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
				ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Нанесен"));
		КонецЕсли;
		
		ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
			ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.НеИндивидуализирован"));
		ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
			ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Индивидуализирован"));
		
	Иначе
		// Для обогащения информации об ошибке при выводе пользователю
		ПараметрыСканирования.ВидОперацииИСМП = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.СписаниеЭмитированныхКодовМаркировки");
		
		ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
			ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Нанесен"));
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(
			ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Нанесен"));
		
		ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
			ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Эмитирован"));
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(
			ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Эмитирован"));
		
	КонецЕсли;
	
	ПараметрыСканирования.СохранятьКодыМаркировкиВПулИСМП  = Истина;
	ПараметрыСканирования.ТребоватьПолныйКодМаркировкиИСМП = Истина;
	
	НастроитьПараметрыСканированияПоРаздельномуКонтролю(ПараметрыСканирования, ВидПродукции);
	
КонецПроцедуры

// Дополняются свойства параметров сканирования и заполняются значения для документа "Перемаркировка ИСМП".
//
// Параметры:
//  Контекст - ФормаКлиентскогоПриложения, ДокументСсылка, СправочникСсылка - Контекст.
//  ПараметрыСканирования - Структура - (См. ШтрихкодированиеИСКлиент.ПараметрыСканирования).
//  ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции ИС.
Процедура ЗаполнитьПараметрыСканированияПеремаркировкаТоваров(Контекст, ПараметрыСканирования)
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "Объект") Тогда
		ИсточникДанных = Контекст.Объект;
	Иначе
		ИсточникДанных = Контекст;
	КонецЕсли;
	
	ВидПродукции = ИсточникДанных.ВидПродукции;
	
	ПараметрыСканирования.ИмяТабличнойЧастиШтрихкодыУпаковок = "Товары";
	ПараметрыСканирования.ИмяКолонкиШтрихкодУпаковки         = "КодМаркировки, НовыйКодМаркировки";
	
	Если Не ВключитьПоддержкуПродукцииИСМП(ПараметрыСканирования, ВидПродукции) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыСканирования.ТолькоМаркируемаяПродукция = Истина;
	
	ПараметрыСканирования.Организация = ИсточникДанных.Организация;
	
	ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборот"));
	ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборота"));
	ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Нанесен"));
	
	// Для обогащения информации об ошибке при выводе пользователю
	ПараметрыСканирования.ВидОперацииИСМП = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.Перемаркировка");
	
	ПараметрыСканирования.ИндивидуализироватьКИЗ = ИсточникДанных.ТребуетсяИндивидуализацияКИЗ;
	
	НастроитьПараметрыСканированияПоРаздельномуКонтролю(ПараметрыСканирования, ВидПродукции);
	
КонецПроцедуры

// Дополняются свойства параметров сканирования и заполняются значения для документа "Уведомление об отгрузке маркированных товаровИСМП ИСМП".
//
// Параметры:
//  Контекст - ФормаКлиентскогоПриложения, ДокументСсылка, СправочникСсылка - Контекст.
//  ПараметрыСканирования - Структура - (См. ШтрихкодированиеИСКлиент.ПараметрыСканирования).
//  ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции ИС.
Процедура ЗаполнитьПараметрыСканированияОтгрузкаТоваровИСМП(Контекст, ПараметрыСканирования)
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "Объект") Тогда
		ИсточникДанных = Контекст.Объект;
	Иначе
		ИсточникДанных = Контекст;
	КонецЕсли;
	
	ВидПродукции = ИсточникДанных.ВидПродукции;
	Операция     = ИсточникДанных.Операция;
	
	ПараметрыСканирования.ИмяТабличнойЧастиШтрихкодыУпаковок = "ШтрихкодыУпаковок";
	ПараметрыСканирования.ИмяКолонкиШтрихкодУпаковки         = "ШтрихкодУпаковки";
	
	Если Не ВключитьПоддержкуПродукцииИСМП(ПараметрыСканирования, ВидПродукции) Тогда
		Возврат;
	КонецЕсли;
	
	Если Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ОтгрузкаЕАЭССПризнаниемКИ") Тогда
		ПараметрыСканирования.ДопустимыйСпособВводаВОборот =
			ШтрихкодированиеИСМПКлиентСервер.СпособВводаВОборотСУЗПоВидуОперации(Операция);
	КонецЕсли;
	
	ПараметрыСканирования.ИспользуютсяДанныеВыбораПоМаркируемойПродукции = Истина;
	ПараметрыСканирования.ТолькоМаркируемаяПродукция                     = Истина;
	ИнтеграцияИСМПКлиентСервер.УстановитьПараметрСканированияЗапрашиватьДанныеНеизвестныхУпаковокИСМП(ПараметрыСканирования, Истина);
	
	Если ЗначениеЗаполнено(ИсточникДанных.ДатаОтгрузки) Тогда
		ПараметрыСканирования.ДатаДокумента = ИсточникДанных.ДатаОтгрузки;
	Иначе
		ПараметрыСканирования.ДатаДокумента = ИсточникДанных.Дата;
	КонецЕсли;
	
	ПараметрыСканирования.ВозможнаЗагрузкаТСД = Истина;
	ПараметрыСканирования.Организация         = ИсточникДанных.Организация;
	
	ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборот"));
	ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборотПриВозврате"));
	ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ОжидаетПередачуСобственнику"));
	
	ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборот"));
	ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборотПриВозврате"));
	ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ОжидаетПередачуСобственнику"));
	
	// Для обогащения информации об ошибке при выводе пользователю
	ПараметрыСканирования.ВидОперацииИСМП = Операция;
	
	Если Не ЗначениеЗаполнено(ВидПродукции) И ИнтеграцияИСКлиентСервер.ПрисутствуетМолочнаяПродукция(ПараметрыСканирования.ДопустимыеВидыПродукции)
			Или ИнтеграцияИСКлиентСервер.ЭтоМолочнаяПродукцияИСМП(ВидПродукции) Тогда
		ПараметрыСканирования.КонтролироватьОкончаниеСрокаГодности = Истина;
	КонецЕсли;
	
	ПараметрыСканирования.РазрешеноЗапрашиватьКодМаркировки = (Операция <> ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ОтгрузкаВЕАЭСПриОСУ"));
	
КонецПроцедуры

// Дополняются свойства параметров сканирования и заполняются значения для документа "Уведомление об отгрузке маркированных товаровИСМП ИСМП".
//
// Параметры:
//  Контекст - ФормаКлиентскогоПриложения, ДокументСсылка, СправочникСсылка - Контекст.
//  ПараметрыСканирования - (См. ШтрихкодированиеИСКлиент.ПараметрыСканирования).
Процедура ЗаполнитьПараметрыСканированияПриемкаТоваровИСМП(Контекст, ПараметрыСканирования)
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "Объект") Тогда
		ИсточникДанных = Контекст.Объект;
	Иначе
		ИсточникДанных = Контекст;
	КонецЕсли;
	
	ВидПродукции = ИсточникДанных.ВидПродукции;
	Операция     = ИсточникДанных.Операция;
	
	ПараметрыСканирования.ИмяТабличнойЧастиШтрихкодыУпаковок = "ШтрихкодыУпаковок";
	ПараметрыСканирования.ИмяКолонкиШтрихкодУпаковки         = "ШтрихкодУпаковки";
	
	Если Не ВключитьПоддержкуПродукцииИСМП(ПараметрыСканирования, ВидПродукции) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыСканирования.ИспользуютсяДанныеВыбораПоМаркируемойПродукции = Истина;
	ПараметрыСканирования.ТолькоМаркируемаяПродукция                     = Истина;
	
	ИнтеграцияИСМПКлиентСервер.УстановитьПараметрСканированияЗапрашиватьДанныеНеизвестныхУпаковокИСМП(ПараметрыСканирования, Истина);
	ИнтеграцияИСМПКлиентСервер.УстановитьПараметрСканированияЗапрашиватьДанныеСервисаИСМП(ПараметрыСканирования, Истина);
	
	ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборот"));
	ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборотПриВозврате"));
	ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ОжидаетДоставки"));
	
	ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборот"));
	ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборотПриВозврате"));
	ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ОжидаетДоставки"));
	
	ПараметрыСканирования.СопоставлятьНоменклатуру  = (Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ПриемкаИзЕАЭСПриОСУ"));
	ПараметрыСканирования.СоздаватьШтрихкодУпаковки = Ложь;
	ПараметрыСканирования.СсылкаНаОбъект            = Неопределено;
	
	Если ИнтеграцияИСМПСлужебныйКлиентСервер.ЭтоПриемкаИзСтранЕАЭС(Операция) Тогда
		ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Нанесен"));
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Нанесен"));
	КонецЕсли;
	
	// Для обогащения информации об ошибке при выводе пользователю
	ПараметрыСканирования.ВидОперацииИСМП                   = Операция;
	ПараметрыСканирования.ПоддерживаетсяОбъемноСортовойУчет = (Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ПриемкаИзЕАЭСПриОСУ"));
	
	НастроитьПараметрыСканированияПоРаздельномуКонтролю(ПараметрыСканирования, ВидПродукции);
	
КонецПроцедуры


// Дополняются свойства параметров сканирования и заполняются значения для документа "Возврат в оборот ИСМП".
//
// Параметры:
//  Контекст - ФормаКлиентскогоПриложения, ДокументСсылка, СправочникСсылка - Контекст.
//  ПараметрыСканирования - См. ШтрихкодированиеИСКлиент.ПараметрыСканирования.
Процедура ЗаполнитьПараметрыСканированияВозвратаВОборот(Контекст, ПараметрыСканирования)
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "Объект") Тогда
		ИсточникДанных = Контекст.Объект;
	Иначе
		ИсточникДанных = Контекст;
	КонецЕсли;
	
	ВидПродукции = ИсточникДанных.ВидПродукции;
	Операция     = ИсточникДанных.Операция;
	
	ПараметрыСканирования.ИмяТабличнойЧастиШтрихкодыУпаковок = "Товары";
	ПараметрыСканирования.ИмяКолонкиШтрихкодУпаковки         = "КодМаркировки";
	
	Если Не ВключитьПоддержкуПродукцииИСМП(ПараметрыСканирования, ВидПродукции) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыСканирования.ТолькоМаркируемаяПродукция = Истина;
	
	ПараметрыСканирования.Организация = ИсточникДанных.Организация;
	
	ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборота"));
	ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборотаВозвращенФизическомуЛицу"));
	ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборотаВРезультатеСписания"));
	ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборотаИспользованДляСобственныхНуждПредприятия"));
	ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборотаКонфискация"));
	ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборотаЛиквидацияПредприятия"));
	ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборотаПоДоговоруРассрочки"));
	ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборотаПриУтратеИлиПовреждении"));
	ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборотаРозничнаяПродажа"));
	ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборотаЭкспортированВСтраныЕАЭС"));
	ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборотаЭкспортированЗаПределыСтранЕАЭС"));
	
	ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборота"));
	ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборотаВозвращенФизическомуЛицу"));
	ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборотаВРезультатеСписания"));
	ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборотаИспользованДляСобственныхНуждПредприятия"));
	ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборотаКонфискация"));
	ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборотаЛиквидацияПредприятия"));
	ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборотаПоДоговоруРассрочки"));
	ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборотаПриУтратеИлиПовреждении"));
	ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборотаРозничнаяПродажа"));
	ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборотаЭкспортированВСтраныЕАЭС"));
	ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборотаЭкспортированЗаПределыСтранЕАЭС"));
	
	// Для обогащения информации об ошибке при выводе пользователю
	ПараметрыСканирования.ВидОперацииИСМП = Операция;
	
	НастроитьПараметрыСканированияПоРаздельномуКонтролю(ПараметрыСканирования, ВидПродукции);
	
КонецПроцедуры

// Дополняются свойства параметров сканирования и заполняются значения для документа "Отчет ИСМП".
//
// Параметры:
//  Контекст - ФормаКлиентскогоПриложения, ДокументСсылка, СправочникСсылка - Контекст.
//  ПараметрыСканирования - См. ШтрихкодированиеИСКлиент.ПараметрыСканирования.
Процедура ЗаполнитьПараметрыСканированияОтчетИСМП(Контекст, ПараметрыСканирования)
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "Объект") Тогда
		ИсточникДанных = Контекст.Объект;
	Иначе
		ИсточникДанных = Контекст;
	КонецЕсли;
	ВидПродукции = ИсточникДанных.ВидПродукции;
	
	ПараметрыСканирования.ИмяТабличнойЧастиШтрихкодыУпаковок  = Неопределено;
	ПараметрыСканирования.ИмяКолонкиШтрихкодУпаковки          = Неопределено;
	
	Если Не ВключитьПоддержкуПродукцииИСМП(ПараметрыСканирования, ВидПродукции) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыСканирования.СоздаватьШтрихкодУпаковки           = Ложь;
	ПараметрыСканирования.ТолькоМаркируемаяПродукция          = Истина;
	ПараметрыСканирования.КонтрольУникальностиКодовМаркировки = Ложь;
	ПараметрыСканирования.РазрешенаОбработкаБезУказанияМарки  = Истина;
	ПараметрыСканирования.РазрешеноЗапрашиватьКодМаркировки   = Ложь;
	ПараметрыСканирования.ЗапрашиватьКоличествоМерногоТовара  = Ложь;
	
	ИнтеграцияИСМПКлиентСервер.УстановитьПараметрСканированияЗапрашиватьДанныеНеизвестныхУпаковокИСМП(ПараметрыСканирования, Ложь);
	ИнтеграцияИСМПКлиентСервер.УстановитьПараметрСканированияЗапрашиватьДанныеСервисаИСМП(ПараметрыСканирования, Ложь);
	
	ПараметрыСканирования.Организация = ИсточникДанных.Организация;
	ПараметрыСканирования.Владелец    = Неопределено;
	ПараметрыСканирования.СозданиеНаборов = Ложь;
	
	// Для обогащения информации об ошибке при выводе пользователю
	ПараметрыСканирования.ВидОперацииИСМП = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ОтчетИСМПСписокКИНаБалансе");
	
	НастроитьПараметрыСканированияПоРаздельномуКонтролю(ПараметрыСканирования, ВидПродукции);
	
КонецПроцедуры

// Дополняются свойства параметров сканирования и заполняются значения для документа "Заказ на эмиссию кодов маркировки СУЗ".
//
// Параметры:
//  Контекст - ФормаКлиентскогоПриложения, ДокументСсылка, СправочникСсылка - Контекст.
//  ПараметрыСканирования - Структура - (См. ШтрихкодированиеИСКлиент.ПараметрыСканирования).
//  ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции ИС.
Процедура ЗаполнитьПараметрыСканированияЗаказНаЭмиссиюКодовМаркировкиСУЗ(Контекст, ПараметрыСканирования)
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "Объект") Тогда
		ИсточникДанных = Контекст.Объект;
	Иначе
		ИсточникДанных = Контекст;
	КонецЕсли;
	ВидПродукции = ИсточникДанных.ВидПродукции;
	
	ПараметрыСканирования.ИмяТабличнойЧастиШтрихкодыУпаковок  = Неопределено;
	ПараметрыСканирования.ИмяКолонкиШтрихкодУпаковки          = Неопределено;
	
	Если Не ВключитьПоддержкуПродукцииИСМП(ПараметрыСканирования, ВидПродукции) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыСканирования.СоздаватьШтрихкодУпаковки           = Ложь;
	ПараметрыСканирования.ТолькоМаркируемаяПродукция          = Истина;
	ПараметрыСканирования.КонтрольУникальностиКодовМаркировки = Ложь;
	ПараметрыСканирования.РазрешенаОбработкаБезУказанияМарки  = Истина;
	ПараметрыСканирования.РазрешеноЗапрашиватьКодМаркировки   = Ложь;
	ПараметрыСканирования.ЗапрашиватьКоличествоМерногоТовара  = Ложь;
	
	ИнтеграцияИСМПКлиентСервер.УстановитьПараметрСканированияЗапрашиватьДанныеНеизвестныхУпаковокИСМП(ПараметрыСканирования, Ложь);
	ИнтеграцияИСМПКлиентСервер.УстановитьПараметрСканированияЗапрашиватьДанныеСервисаИСМП(ПараметрыСканирования, Ложь);
	
	ПараметрыСканирования.Организация = ИсточникДанных.Организация;
	ПараметрыСканирования.Владелец    = Неопределено;
	ПараметрыСканирования.СозданиеНаборов = Истина;
	
	// Для обогащения информации об ошибке при выводе пользователю
	ПараметрыСканирования.ВидОперацииИСМП = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ЗаказНаЭмиссиюКодовМаркировки");
	
	НастроитьПараметрыСканированияПоРаздельномуКонтролю(ПараметрыСканирования, ВидПродукции);
	
КонецПроцедуры

// Дополняются свойства параметров сканирования и заполняются значения для РМ "Проверка кодов маркировки".
//
// Параметры:
//  Контекст - ФормаКлиентскогоПриложения, ДокументСсылка, СправочникСсылка - Контекст.
//  ПараметрыСканирования - См. ШтрихкодированиеИСКлиент.ПараметрыСканирования.
Процедура ЗаполнитьПараметрыСканированияПроверкаКодовМаркировки(Контекст, ПараметрыСканирования)
	
	Если Не ВключитьПоддержкуПродукцииИСМП(ПараметрыСканирования,, Ложь) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыСканирования.ДопустимыеВидыПродукции.Очистить();
	ПараметрыСканирования.РазрешенаОбработкаНеНайденныхЛогистическихУпаковок = Истина;
	ПараметрыСканирования.Владелец                                           = Неопределено;
	ПараметрыСканирования.ИмяТабличнойЧастиШтрихкодыУпаковок                 = Неопределено;
	ПараметрыСканирования.ВозможнаЗагрузкаТСД                                = Истина;
	ПараметрыСканирования.РазрешенаОбработкаКодовСПустойНоменклатурой        = Истина;
	ПараметрыСканирования.ИспользуютсяДанныеВыбораПоМаркируемойПродукции     = Истина;
	ПараметрыСканирования.СоздаватьШтрихкодУпаковки                          = Ложь;
	ПараметрыСканирования.СопоставлятьНоменклатуру                           = Ложь;
	ПараметрыСканирования.Организация                                        = Контекст.Организация;
	ПараметрыСканирования.ЗаполнятьСрокГодности                              = Истина;
	
	ПараметрыСканирования.ЭтоПроверкаКодовМаркировкиИСМП = Истина;
	ПараметрыСканирования.ОпределениеВидаПродукцииИСМП   = Истина;
	ПараметрыСканирования.ЗаписыватьЛогЗапросовИСМП      = Истина;
	ПараметрыСканирования.ТребуетсяЧастичноеВыбытие      = Истина;
	ПараметрыСканирования.СозданиеНаборов                = Истина;
	
	ИнтеграцияИСМПКлиентСервер.УстановитьПараметрСканированияЗапрашиватьДанныеНеизвестныхУпаковокИСМП(ПараметрыСканирования, Истина);
	ИнтеграцияИСМПКлиентСервер.УстановитьПараметрСканированияЗапрашиватьДанныеСервисаИСМП(ПараметрыСканирования, Истина);
	
КонецПроцедуры

// Дополняются свойства параметров сканирования и заполняются значения для документа "Вывод из оборота ИСМП".
//
// Параметры:
//  Контекст - ФормаКлиентскогоПриложения, ДокументСсылка, СправочникСсылка - Контекст.
//  ПараметрыСканирования - Структура - (См. ШтрихкодированиеИСКлиент.ПараметрыСканирования).
//  ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции ИС.
Процедура ЗаполнитьПараметрыСканированияУточнениеСведенийОКодахМаркировкиИСМП(Контекст, ПараметрыСканирования)
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "Объект") Тогда
		ИсточникДанных = Контекст.Объект;
	Иначе
		ИсточникДанных = Контекст;
	КонецЕсли;
	
	ВидПродукции = ИсточникДанных.ВидПродукции;
	Операция     = ИсточникДанных.Операция;
	
	ПараметрыСканирования.ИмяТабличнойЧастиШтрихкодыУпаковок = "ШтрихкодыУпаковок";
	ПараметрыСканирования.ИмяКолонкиШтрихкодУпаковки         = "ШтрихкодУпаковки";
	
	Если Не ВключитьПоддержкуПродукцииИСМП(ПараметрыСканирования, ВидПродукции) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыСканирования.КонтрольПустыхУпаковок               = Ложь;
	ПараметрыСканирования.ПоддерживаютсяОперацииАгрегации      = Ложь;
	ПараметрыСканирования.КонтролироватьСтандартнуюВложенность = Ложь;
	
	ПараметрыСканирования.ИспользуютсяДанныеВыбораПоМаркируемойПродукции = Истина;
	ПараметрыСканирования.ТолькоМаркируемаяПродукция                     = Истина;
	
	ПараметрыСканирования.ВозможнаЗагрузкаТСД = Истина;
	ПараметрыСканирования.Организация         = ИсточникДанных.Организация;
	
	ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборот"));
	
	ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборот"));
	
	Если Операция <> ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.КорректировкаСведенийКМСрокГодностиВСД") Тогда
		ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
			ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Нанесен"));
		ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(
			ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Нанесен"));
	КонецЕсли;
	
	ПараметрыСканирования.ОтборПоВидуПродукции = Истина;
	
	Если ИнтеграцияИСКлиентСервер.ЭтоПродукцияПодконтрольнаяВЕТИС(ВидПродукции) Тогда
		ИнтеграцияИСМПВЕТИСКлиентСервер.ДополнитьПараметрыСканированияУточнениеСведенийОКодах(Операция, ПараметрыСканирования);
	КонецЕсли;
	
	ПараметрыСканирования.СохранятьКодыМаркировкиВПулИСМП   = Ложь;
	ПараметрыСканирования.ТребоватьПолныйКодМаркировкиИСМП  = Ложь;
	
	Если (Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.КорректировкаСведенийКМСрокГодности")
		И ИнтеграцияИСМПКлиентСервер.ЭтоВидПродукцииСоСрокамиГодности(ВидПродукции))
		Или Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.КорректировкаСведенийКМСрокГодностиВСД") Тогда
		ПараметрыСканирования.ЗаполнятьСрокГодности = Истина;
	КонецЕсли;
	
	Если Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.КорректировкаСведенийКМФактическийВес")
		Или Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ОтчетОПеревзвешивании") Тогда
		ПараметрыСканирования.ТолькоВесоваяПродукция             = Истина;
		ПараметрыСканирования.ЗапрашиватьКоличествоМерногоТовара = Истина;
		ПараметрыСканирования.СохранятьКодыМаркировкиВПулИСМП    = Истина;
		ПараметрыСканирования.ТребоватьПолныйКодМаркировкиИСМП   = Истина;
	КонецЕсли;
	
	// Для обогащения информации об ошибке при выводе пользователю
	ПараметрыСканирования.ВидОперацииИСМП = Операция;
	
	НастроитьПараметрыСканированияПоРаздельномуКонтролю(ПараметрыСканирования, ВидПродукции);
	
КонецПроцедуры

// Дополняются свойства параметров сканирования и заполняются значения для регистра "Кеги на оборудовании розлива ИС МП".
//
// Параметры:
//  Контекст - ФормаКлиентскогоПриложения, ДокументСсылка, СправочникСсылка - Контекст.
//  ПараметрыСканирования - Структура - (См. ШтрихкодированиеИСКлиент.ПараметрыСканирования).
//  ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции ИС.
Процедура ЗаполнитьПараметрыСканированияПодключенияКОборудованиюРозлива(Контекст, ПараметрыСканирования)
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "Объект") Тогда
		ИсточникДанных = Контекст.Объект;
	Иначе
		ИсточникДанных = Контекст;
	КонецЕсли;
	
	ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Пиво");
	Операция     = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ПодключениеКОборудованиюРозлива");
	
	Если Не ВключитьПоддержкуПродукцииИСМП(ПараметрыСканирования, ВидПродукции) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыСканирования.ВидОперацииИСМП                                = Операция;
	ПараметрыСканирования.ТолькоМаркируемаяПродукция                     = Истина;
	ПараметрыСканирования.СоздаватьШтрихкодУпаковки                      = Истина;
	ПараметрыСканирования.ТребоватьПолныйКодМаркировкиИСМП               = Истина;
	ПараметрыСканирования.СохранятьКодыМаркировкиВПулИСМП                = Истина;
	ПараметрыСканирования.РазрешеноЗапрашиватьКодМаркировки              = Истина;
	
	ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборот"));
	ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборотПриВозврате"));
	ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборота"));
	ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(
		ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ПодключенКОборудованиюРозлива"));
	
	НастроитьПараметрыСканированияПоРаздельномуКонтролю(ПараметрыСканирования, ВидПродукции);
	
	ПараметрыСканирования.ДопустимыеВидыПродукции = Новый Массив();
	ПараметрыСканирования.ДопустимыеВидыПродукции.Добавить(ВидПродукции);
	
КонецПроцедуры

#Область ПроизводствоМаркировкаПива

Процедура ЗаполнитьПараметрыСканированияОтчетОбИмпортеЕГАИС(ПараметрыСканирования, Контекст)
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "Объект") Тогда
		ИсточникДанных = Контекст.Объект;
		Организация = Контекст.Организация;
	Иначе
		ИсточникДанных = Контекст;
		Организация = Неопределено;
	КонецЕсли;
	
	ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Пиво");
	Если Не ВключитьПоддержкуПродукцииИСМП(ПараметрыСканирования, ВидПродукции) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыСканирования.ИмяТабличнойЧастиШтрихкодыУпаковок = "ШтрихкодыУпаковок";
	ПараметрыСканирования.ИмяКолонкиШтрихкодУпаковки         = "ШтрихкодУпаковки";
	
	ПараметрыСканирования.ДоступнаПечатьЭтикеток                         = Ложь;
	ПараметрыСканирования.ИспользуютсяДанныеВыбораПоМаркируемойПродукции = Истина;
	ПараметрыСканирования.ТолькоМаркируемаяПродукция                     = Истина;
	ПараметрыСканирования.КонтролироватьСтандартнуюВложенность           = Ложь;
	ПараметрыСканирования.СохранятьКодыМаркировкиВПулИСМП                = Истина;
	
	ПараметрыСканирования.КонтрольРасхожденийСДокументомОснованием       = Истина;
	ПараметрыСканирования.ВозможнаЗагрузкаТСД                            = Истина;
	
	ПараметрыСканирования.Организация       = Организация;
	ПараметрыСканирования.Владелец          = Организация;
	ПараметрыСканирования.ДокументОснование = ИсточникДанных.ДокументОснование;
	// Для обогащения информации об ошибке при выводе пользователю
	ПараметрыСканирования.ВидОперацииИСМП = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВводВОборотТрансграничнаяТорговля");
	
	ПараметрыСканирования.ДопустимыйСпособВводаВОборот = СпособВводаВОборотСУЗПоВидуОперации(ПараметрыСканирования.ВидОперацииИСМП);
	
	ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Нанесен"));
	ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Нанесен"));
	
	НастроитьПараметрыСканированияПоРаздельномуКонтролю(ПараметрыСканирования, ВидПродукции);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыСканированияОтчетОПроизводствеЕГАИС(ПараметрыСканирования, Контекст)
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "Объект") Тогда
		ИсточникДанных = Контекст.Объект;
		Организация = Контекст.Организация;
	Иначе
		ИсточникДанных = Контекст;
		Организация = Неопределено;
	КонецЕсли;
	
	ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Пиво");
	Если Не ВключитьПоддержкуПродукцииИСМП(ПараметрыСканирования, ВидПродукции) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыСканирования.ИмяТабличнойЧастиШтрихкодыУпаковок = "ШтрихкодыУпаковок";
	ПараметрыСканирования.ИмяКолонкиШтрихкодУпаковки         = "ШтрихкодУпаковки";
	
	ПараметрыСканирования.ДоступнаПечатьЭтикеток                         = Ложь;
	ПараметрыСканирования.ИспользуютсяДанныеВыбораПоМаркируемойПродукции = Истина;
	ПараметрыСканирования.ТолькоМаркируемаяПродукция                     = Истина;
	ПараметрыСканирования.КонтролироватьСтандартнуюВложенность           = Ложь;
	ПараметрыСканирования.СохранятьКодыМаркировкиВПулИСМП                = Истина;
	
	ПараметрыСканирования.КонтрольРасхожденийСДокументомОснованием       = Истина;
	ПараметрыСканирования.ВозможнаЗагрузкаТСД                            = Истина;
	
	ПараметрыСканирования.Организация       = Организация;
	ПараметрыСканирования.Владелец          = Организация;
	ПараметрыСканирования.ДокументОснование = ИсточникДанных.ДокументОснование;
	
	// Для обогащения информации об ошибке при выводе пользователю
	ПараметрыСканирования.ВидОперацииИСМП = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВводВОборотТрансграничнаяТорговля");
	
	ПараметрыСканирования.ДопустимыйСпособВводаВОборот = СпособВводаВОборотСУЗПоВидуОперации(ПараметрыСканирования.ВидОперацииИСМП);
	
	ПараметрыСканирования.ДопустимыеСтатусыИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Нанесен"));
	ПараметрыСканирования.ДопустимыеСтатусыУпаковокИСМП.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Нанесен"));
	
	НастроитьПараметрыСканированияПоРаздельномуКонтролю(ПараметрыСканирования, ВидПродукции);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

Функция КодыРезультатаПроверкиДляОтображенияОшибки()
	
	ВозвращаемоеЗначение = Новый Соответствие();
	ПредставленияВЧеке   = ПредставлениеКодаМаркировкиВЧеке();
	
	ДобавитьОписаниеРезультатаПроверки(
		ВозвращаемоеЗначение,
		"00000000",
		ПредставленияВЧеке.М,
		НСтр("ru = 'Проверка КП КМ не выполнена, статус товара ОИСМ не проверен'"));
	ДобавитьОписаниеРезультатаПроверки(
		ВозвращаемоеЗначение,
		"00000001",
		ПредставленияВЧеке.ММинус,
		НСтр("ru = 'Проверка КП КМ выполнена в ФН с отрицательным результатом, статус товара ОИСМ не проверен'"));
	ДобавитьОписаниеРезультатаПроверки(
		ВозвращаемоеЗначение,
		"00000011",
		ПредставленияВЧеке.М,
		НСтр("ru = 'Проверка КП КМ выполнена с положительным результатом, статус товара ОИСМ не проверен'"));
	ДобавитьОписаниеРезультатаПроверки(
		ВозвращаемоеЗначение,
		"00010000",
		ПредставленияВЧеке.М,
		НСтр("ru = 'Проверка КП КМ не выполнена, статус товара ОИСМ не проверен (ККТ функционирует в автономном режиме)'"));
	ДобавитьОписаниеРезультатаПроверки(
		ВозвращаемоеЗначение,
		"00010001",
		ПредставленияВЧеке.ММинус,
		НСтр("ru = 'Проверка КП КМ выполнена в ФН с отрицательным результатом, статус товара ОИСМ не проверен (ККТ функционирует в автономном режиме)'"));
	ДобавитьОписаниеРезультатаПроверки(
		ВозвращаемоеЗначение,
		"_00010011",
		ПредставленияВЧеке.М,
		НСтр("ru = 'Проверка КП КМ выполнена в ФН с положительным результатом, статус товара ОИСМ не проверен (ККТ функционирует в автономном режиме)'"));
	ДобавитьОписаниеРезультатаПроверки(
		ВозвращаемоеЗначение,
		"00000101",
		ПредставленияВЧеке.ММинус,
		НСтр("ru = 'Проверка КП КМ выполнена с отрицательным результатом, статус товара у ОИСМ некорректен'"));
	ДобавитьОписаниеРезультатаПроверки(
		ВозвращаемоеЗначение,
		"00000111",
		ПредставленияВЧеке.ММинус,
		НСтр("ru = 'Проверка КП КМ выполнена с положительным результатом, статус товара у ОИСМ некорректен'"));
	ДобавитьОписаниеРезультатаПроверки(
		ВозвращаемоеЗначение,
		"_00001111",
		ПредставленияВЧеке.МПлюс,
		НСтр("ru = 'Проверка КП КМ выполнена с положительным результатом, статус товара у ОИСМ корректен'"));
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Процедура ДобавитьОписаниеРезультатаПроверки(Коллекция, Код, ПредставлениеВЧеке, Описание)
	
	ДанныеОписания = Новый Структура();
	ДанныеОписания.Вставить("ПредставлениеВЧеке", ПредставлениеВЧеке);
	ДанныеОписания.Вставить("Описание",           Описание);
	
	Коллекция.Вставить(Код, ДанныеОписания);
	
КонецПроцедуры

Процедура ДополнитьПредставлениеВЧеке(ПараметрыПредставления, Значение)
	
	Если Не ЗначениеЗаполнено(ПараметрыПредставления.ПредставлениеВЧеке) Тогда
		ПараметрыПредставления.ПредставлениеВЧеке = Значение;
	КонецЕсли;
	
КонецПроцедуры

// Новая структура установки дополнительных свойств при проверке ККТ.
// 
// Возвращаемое значение:
//  Структура - Новая структура установки дополнительных свойств при проверке ККТ:
// * Идентификатор        - УникальныйИдентификатор -
// * СтрокаТовары         - Структура, СтрокаТаблицыЗначений, СтрокаДереваЗначений - Исходная строка товаров с полями "Номенклатура", "Упаковка"
// * СтрокаНазначения     - см. ШтрихкодированиеИСМП.НовыйРезультатРаспределенияШтрихкодовПоТоварам, см. НовыйЭлементДанныхПроверкиСредствамиККТ -
//                          Коллекция свойств, в составе которых присутствует свойство: КодЕдиницыИзмерения.
// * ЭтоЧастичноеВыбытие  - Булево - Признак выбытия части потребительской упаковки.
// * ИсходнаяНоменклатура - Неопределено, ОпределяемыйТип.Номенклатура - Исходная номенклатура, если была выполнена подмена при частичном выбытии.
Функция НоваяСтруктураУстановкиДополнительныхСвойствПриПроверкеККТ() Экспорт
	
	ВозвращаемоеЗначение = Новый Структура();
	ВозвращаемоеЗначение.Вставить("Идентификатор",        Новый УникальныйИдентификатор());
	ВозвращаемоеЗначение.Вставить("СтрокаТовары",         Неопределено);
	ВозвращаемоеЗначение.Вставить("СтрокаНазначения",     Неопределено);
	ВозвращаемоеЗначение.Вставить("ЭтоЧастичноеВыбытие",  Ложь);
	ВозвращаемоеЗначение.Вставить("ИсходнаяНоменклатура", Неопределено);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Новый элемент обработки установки дополнительных свойств при проверки средствами ККТ.
// 
// Параметры:
//  ВходящаяСтруктураОбработки - см. НоваяСтруктураУстановкиДополнительныхСвойствПриПроверкеККТ
// 
// Возвращаемое значение:
//  Структура - Новый элемент обработки установки дополнительных свойств при проверке ККТ:
// * Идентификатор       - УникальныйИдентификатор - 
// * Номенклатура        - ОпределяемыйТип.Номенклатура -
// * Характеристика      - ОпределяемыйТип.ХарактеристикаНоменклатуры -
// * Упаковка            - ОпределяемыйТип.Упаковка -
// * ЭтоЧастичноеВыбытие - Булево - Признак выбытия части потребительской упаковки.
// * КодЕдиницыИзмерения - Строка - Код единицы измерения (тэг 2108).
Функция НовыйЭлементОбработкиУстановкиДополнительныхСвойствПриПроверкеККТ(ВходящаяСтруктураОбработки)
	
	ВозвращаемоеЗначение = Новый Структура();
	ВозвращаемоеЗначение.Вставить("Номенклатура",        Неопределено);
	ВозвращаемоеЗначение.Вставить("Характеристика",      Неопределено);
	ВозвращаемоеЗначение.Вставить("Упаковка",            Неопределено);
	ЗаполнитьЗначенияСвойств(ВозвращаемоеЗначение, ВходящаяСтруктураОбработки.СтрокаТовары);
	ВозвращаемоеЗначение.Вставить("КодЕдиницыИзмерения", Неопределено);
	ВозвращаемоеЗначение.Вставить("Идентификатор",       ВходящаяСтруктураОбработки.Идентификатор);
	ВозвращаемоеЗначение.Вставить("ЭтоЧастичноеВыбытие", ВходящаяСтруктураОбработки.ЭтоЧастичноеВыбытие);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Устарел. К удалению
Функция ЭтоСтрокаОбъемноСортовогоУчета(СтрокаДерева, ПараметрыСканирования = Неопределено) Экспорт
	
	Если (ПараметрыСканирования = Неопределено
		Или ПараметрыСканирования.ПоддерживаетсяОбъемноСортовойУчет)
		И СтрокаДерева.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИС.ОбъемноСортовойУчет")
		И ИнтеграцияИСМПКлиентСерверПовтИсп.ВидПродукцииПоддерживаетОбъемноСортовойУчет(СтрокаДерева.ВидПродукции) Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Вызывается при получении элеметнов проверки средставами ККТ для дозаполнения свойств прикладными данными.
// Используется для заполнения полей, которые будут переданы в методы проверки на ККТ, например, КодЕдиницыИзмерения.
// 
// Параметры:
//  ДанныеДляЗаполненияСвойств - Массив из см. НоваяСтруктураУстановкиДополнительныхСвойствПриПроверкеККТ - данные для обработки.
Процедура ПриУстановкеДополнительныхСвойствЭлеметовПроверкиСредствамиККТ(ДанныеДляЗаполненияСвойств) Экспорт
	
	Если ДанныеДляЗаполненияСвойств.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеОбработкиРезультата = Новый Соответствие();
	ДанныеОбработкиЗаполнения = Новый Массив();
	
	Для Каждого ЭлементДанных Из ДанныеДляЗаполненияСвойств Цикл
		ДанныеОбработкиРезультата.Вставить(ЭлементДанных.Идентификатор, ЭлементДанных.СтрокаНазначения);
		ДанныеОбработкиЗаполнения.Добавить(НовыйЭлементОбработкиУстановкиДополнительныхСвойствПриПроверкеККТ(ЭлементДанных));
	КонецЦикла;
	
	ШтрихкодированиеИСМПКлиентСерверПереопределяемый.ПриУстановкеДополнительныхСвойствЭлеметовПроверкиСредствамиККТ(ДанныеОбработкиЗаполнения);
	
	Для Каждого СтрокаДанныхОбработки Из ДанныеОбработкиЗаполнения Цикл
		СтрокаНазначения = ДанныеОбработкиРезультата[СтрокаДанныхОбработки.Идентификатор];
		СтрокаНазначения.КодЕдиницыИзмерения = СтрокаДанныхОбработки.КодЕдиницыИзмерения;
	КонецЦикла;
	
КонецПроцедуры

Функция РазрешеноЗапрашиватьСтатусыКодовЕАЭС(ПараметрыСканирования) Экспорт
	
	Если ПараметрыСканирования = Неопределено Тогда
		
		Возврат Ложь;
		
	Иначе
		
		Возврат ПараметрыСканирования.ВидОперацииИСМП = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВыводИзОборотаРозничнаяПродажа")
			Или ПараметрыСканирования.Свойство("ЭтоПроверкаКодовМаркировкиИСМП")
				И ПараметрыСканирования.ЭтоПроверкаКодовМаркировкиИСМП;
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти
