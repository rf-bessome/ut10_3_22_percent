////////////////////////////////////////////////////////////////////////////////
// ЭлектронныеДокументыКлиентСервер: механизм обмена электронными документами.
//
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Получает текстовое представление версии электронного документа.
//
// Параметры:
//  СсылкаНаВладельца - Ссылка на объект ИБ, состояние версии электронного документа которого необходимо получить.
//
Функция ПолучитьТекстСостоянияЭД(СсылкаНаВладельца, Форма = Неопределено) Экспорт
	
	Гиперссылка = Ложь;
	ТекстСостоянияЭД = ЭлектронныеДокументыСлужебныйВызовСервера.ТекстСостоянияЭД(СсылкаНаВладельца, Гиперссылка);
	
	Если НЕ Форма = Неопределено Тогда
		СтруктураПараметров = Новый Структура();
		СтруктураПараметров.Вставить("ТекстСостоянияЭДО", ТекстСостоянияЭД);
		СтруктураПараметров.Вставить("ВидОперации", "УстановкаГиперссылки");
		СтруктураПараметров.Вставить("ЗначениеПараметра", Гиперссылка);
		#Если ТолстыйКлиентОбычноеПриложение Тогда
			ЭлектронныеДокументыПереопределяемый.ИзменитьСвойстваЭлементовФормы(Форма, СтруктураПараметров);
		#Иначе
			ЭлектронныеДокументыСлужебныйВызовСервера.ИзменитьСвойстваЭлементовФормы(Форма, СтруктураПараметров);
		#КонецЕсли
	КонецЕсли;
	
	Возврат ТекстСостоянияЭД;
	
КонецФункции

// Формирует текст сообщения, подставляя значения
// параметров в шаблоны сообщений.
//
// Параметры
//  ВидПоля       - Строка - может принимать значения:
//                  Поле, Колонка, Список
//  ВидСообщения  - Строка - может принимать значения:
//                  Заполнение, Корректность
//  Параметр1     - Строка - имя поля
//  Параметр2     - Строка - номер строки
//  Параметр3     - Строка - имя списка
//  Параметр4     - Строка - текст сообщения о некорректности заполнения
//
// Возвращаемое значение:
//   Строка - текст сообщения
//
Функция ПолучитьТекстСообщения(ВидПоля = "Поле", ВидСообщения = "Заполнение",
	Параметр1 = "", Параметр2 = "",	Параметр3 = "", Параметр4 = "") Экспорт

	ТекстСообщения = "";

	Если ВРег(ВидПоля) = "ПОЛЕ" Тогда
		Если ВРег(ВидСообщения) = "ЗАПОЛНЕНИЕ" Тогда
			Шаблон = НСтр("ru = 'Поле ""%1"" не заполнено.'");
		ИначеЕсли ВРег(ВидСообщения) = "КОРРЕКТНОСТЬ" Тогда
			Шаблон = НСтр("ru = 'Поле ""%1"" заполнено некорректно.
                           |
                           |%4'");
		КонецЕсли;
	ИначеЕсли ВРег(ВидПоля) = "КОЛОНКА" Тогда
		Если ВРег(ВидСообщения) = "ЗАПОЛНЕНИЕ" Тогда
			Шаблон = НСтр("ru = 'Не заполнена колонка ""%1"" в строке %2 списка ""%3"".'");
		ИначеЕсли ВРег(ВидСообщения) = "КОРРЕКТНОСТЬ" Тогда
			Шаблон = НСтр("ru = 'Некорректно заполнена колонка ""%1"" в строке %2 списка ""%3"".
                           |
                           |%4'");
		КонецЕсли;
	ИначеЕсли ВРег(ВидПоля) = "СПИСОК" Тогда
		Если ВРег(ВидСообщения) = "ЗАПОЛНЕНИЕ" Тогда
			Шаблон = НСтр("ru = 'Не введено ни одной строки в список ""%3"".'");
		ИначеЕсли ВРег(ВидСообщения) = "КОРРЕКТНОСТЬ" Тогда
			Шаблон = НСтр("ru = 'Некорректно заполнен список ""%3"".
                           |
                           |%4'");
		КонецЕсли;
	КонецЕсли;

	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон, Параметр1, Параметр2, Параметр3, Параметр4);

КонецФункции

// Определяет отбор динамического списка в зависимости от наличия режима совместимости
//
// Параметры:
//  Список  - ДинамическийСписок - список, для которого надо определить отбор
//
// Возвращаемое значение:
//   Отбор   - требуемый отбор
//
Функция ОтборДинамическогоСписка(Список) Экспорт

	Если ОбщегоНазначенияКлиентСервер.ЭтоПлатформа83БезРежимаСовместимости() Тогда
		Возврат Список.КомпоновщикНастроек.Настройки.Отбор;
	Иначе
		Возврат Список.Отбор;
	КонецЕсли;

КонецФункции

// Заполняет текстовое представление версии электронного документа.
//
// Параметры:
//  ДокументСсылка - Ссылка на объект ИБ, состояние версии электронного документа которого необходимо получить.
//  ДекорацияСостояниеЭДО - Элемент формы "декорация", в заголовок которого пишется состояние ЭДО.
//
Процедура ЗаполнитьСостояниеЭДО(ДокументСсылка, ДекорацияСостояниеЭДО) Экспорт
	
	ЭлементФормыДоступность = Истина;
	ТекстСостоянияЭДО = ЭлектронныеДокументыСлужебныйВызовСервера.ТекстСостоянияЭД(ДокументСсылка, ЭлементФормыДоступность);
	
	ДекорацияСостояниеЭДО.Видимость = Не ПустаяСтрока(ТекстСостоянияЭДО);
	ДекорацияСостояниеЭДО.Заголовок = ТекстСостоянияЭДО;
	ДекорацияСостояниеЭДО.Доступность = ЭлементФормыДоступность;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЙ ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Определяет необходимость конкретного действия из перечня действий.
//
// Параметры:
//  ПереченьДействий - Строка, перечень действий, которые должны быть совершены с объектом
//  Действие - Строка, конкретное действие, которое нужно найти в перечне действий
// 
// Возвращаемое значение:
//  Булево - Если действие найдено - возвращается Истина, иначе Ложь
//
Функция ЕстьДействие(ПереченьДействий, Действие) Экспорт
	
	Если Найти(ПереченьДействий, Действие) > 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Возвращает текущую версию формата для синхронного обмена
// 
// Возвращаемое значение:
//  Строка - версия формата
//
Функция ВерсияФорматаСинхронногоОбмена() Экспорт
	
	Возврат "1.08";
	
КонецФункции

// Вызывается из ЭлектронныеДокументыВнутренний.СтруктураДанныхПодписи(...) и ЭлектронныеДокументыСлужебныйКлиент.РаспаковатьПакетЭДНаКлиенте(...).
// По двоичным данным подписи формируется структура данных сертификата.
//
// Параметры:
//   ДвоичныеДанныеПодписи - ДвоичныеДанные - данные электронной подписи.
//   МенеджерКриптографии  - МенеджерКриптографии.
//
// Возвращаемое значение:
//   Структура:
//      ДанныеСертификатаПолучены - Булево - Истина - данные сертификата получены.
//      ДвоичныеДанныеСертификата - ДвоичныеДанные - двоичные данные сертификата.
//      КомуВыданСертификат       - Строка - владелец сертификата.
//      Отпечаток                 - СтрокаBase64 - отпечаток сертификата в Base64.
//
Функция СтруктураДанныхСертификата(ДвоичныеДанныеДокумента, ДвоичныеДанныеПодписи, МенеджерКриптографии) Экспорт
	
	Структура = Новый Структура;
	ДанныеСертификатаПолучены = Ложь;
	ДатаПодписи = "";
	КомуВыданСертификат = "";
	Отпечаток = "";
	ДвоичныеДанныеСертификата = "";
	Если МенеджерКриптографии <> Неопределено Тогда
		
		Попытка
			Сертификат = Неопределено;
			МенеджерКриптографии.ПроверитьПодпись(ДвоичныеДанныеДокумента, ДвоичныеДанныеПодписи, Сертификат);
		Исключение
			ТекстСообщения = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("114");
			ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ВидОперации = НСтр("ru = 'проверка электронной подписи'");
			ЭлектронныеДокументыСлужебныйВызовСервера.ОбработатьИсключениеПоЭДНаСервере(ВидОперации,
				ПодробноеПредставлениеОшибки, ТекстСообщения);
		КонецПопытки;
		
		Если Сертификат = Неопределено Тогда
			Сертификаты = МенеджерКриптографии.ПолучитьСертификатыИзПодписи(ДвоичныеДанныеПодписи);
			Сертификат = СертификатПодписавшейСтороны(Сертификаты);
		КонецЕсли;
		
		Если Сертификат <> Неопределено Тогда
			КомуВыданСертификат = ЭлектроннаяЦифроваяПодписьКлиентСервер.ПолучитьПредставлениеПользователя(Сертификат.Субъект);
			Отпечаток = Base64Строка(Сертификат.Отпечаток);
			ДвоичныеДанныеСертификата = Сертификат.Выгрузить();
			ДанныеСертификатаПолучены = Истина;
		КонецЕсли;
	КонецЕсли;
	Структура.Вставить("ДанныеСертификатаПолучены", ДанныеСертификатаПолучены);
	Структура.Вставить("ДвоичныеДанныеСертификата", ДвоичныеДанныеСертификата);
	Структура.Вставить("КомуВыданСертификат",       КомуВыданСертификат);
	Структура.Вставить("Отпечаток",                 Отпечаток);
	
	Возврат Структура;
	
КонецФункции

// Ищет подменю ЭДО в форме документа или форме списка документов
//
Функция ПодменюЭДО(ЭлементыФормы) Экспорт
	
	Подменю = Неопределено;
	
	// Если подменю в командной панели формы
	Подменю = ЭлементыФормы.Найти("ЭД");
	Если Подменю = Неопределено Тогда
		Подменю = ЭлементыФормы.Найти("ФормаЭД");
	КонецЕсли;
	
	Возврат Подменю;
	
КонецФункции

// Обновляет пиктограмму подменю ЭДО формы документа или формы списка документов
//
Процедура ОбновитьПодменюЭДО(ПодменюЭДО, Картинка) Экспорт
	
	Если ПодменюЭДО = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ПодменюЭДО.Картинка = Картинка Тогда
		Возврат;
	КонецЕсли;
	
	ПодменюЭДО.Картинка = Картинка;
	Если Не Картинка = БиблиотекаКартинок.ЭмблемаСервиса1СЭДО Тогда
		ПодменюЭДО.Подсказка = НСтр("ru = 'В сервисе 1С-ЭДО есть новые документы'");
	Иначе
		ПодменюЭДО.Подсказка = НСтр("ru = 'Команды ЭДО'");
	КонецЕсли;
	
КонецПроцедуры

// Формирует список типов основных (неслужебных) документов.
// 
// Возвращаемое значение:
//  Массив - содержит элементы типа "ПеречислениеСсылка.ТипыЭлементовВерсииЭД".
//
Функция ОсновныеТипыЭД() Экспорт

	Результат = Новый Массив;
	Результат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.СЧФДОПУПД"));
	Результат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.СЧФУПД"));
	Результат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ДОПУПД"));
	Результат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.КСЧФДИСУКД"));
	Результат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.КСЧФУКД"));
	Результат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ДИСУКД"));
	Результат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ДОП"));
	Результат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ПервичныйЭД"));
	Результат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭСФ"));
	Результат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.СвРКУПД"));
	Результат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.СвЗКУПД"));
	Результат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.СвИСРКУПД"));
	Результат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.СвИСЗКУПД"));
	Если ОбщегоНазначенияКлиентСервер.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСГИСЭПД") Тогда
		МодульОбменСГИСЭПДКлиентСервер = ОбщегоНазначенияКлиентСервер.ОбщийМодуль("ОбменСГИСЭПДКлиентСервер");	
		МодульОбменСГИСЭПДКлиентСервер.ПриФормированииСпискаОсновныхТиповЭД(Результат);
	КонецЕсли;
	
	Возврат Результат;

КонецФункции 

////////////////////////////////////////////////////////////////////////////////
// Работа со служебными ЭД

// В функции выполняются действия по служебным ЭД (извещение о получении, уведомление об уточнении):
// формирование, утверждение, подписание, отправка.
//
// Параметры:
//  МассивЭД - массив - содержит ссылки на ЭД, по которым требуется сформировать служебные ЭД (электронные
//    документы, владельцы обрабатываемых служебных ЭД).
//  ВидЭД - перечисление - вид ЭД, которые надо обработать (может принимать значения: Извещение о получении
//    и уведомление об уточнении).
//  ТекстУведомления - строка - текст уведомления, введенный пользователем, отклонившим ЭД (имеет смысл,
//    только для ВидЭД = УведомлениеОбУточнении).
//
// Возвращаемое значение:
//  Булево - Истина - действия со служебными ЭД выполнены, иначе - Ложь.
//
Функция СформироватьПодписатьИОтправитьСлужебныеЭД(МассивЭД, ВидЭД, ТекстУведомления = "") Экспорт
	
	КолСформированных = 0;
	КолУтвержденных   = 0;
	КолПодписанных    = 0;
	КолПодготовленных = 0;
	КолОтправленных   = 0;
	// Структура соответствий содержит соответствия: соглашений и сертификатов подписи, соглашений и сертификатов авторизации,
	// сертификатов и структур параметров этих сертификатов (структура параметров сертификатов содержит: ссылку на сертификат,
	// признак "запомнить пароль к сертификату", пароль к сертификату, признак "отозван", отпечаток, файл сертификата, а так же
	// если этот сертификат используется для авторизации, то либо расшифрованный маркер, либо зашифрованный маркер или и то и другое).
	НемедленнаяОтправкаЭД = Неопределено;
	ВыполнятьАвторизациюНаСервере = Неопределено;
	ВыполнятьКриптооперацииНаСервере = Неопределено;
	ЭлектронныеДокументыСлужебныйВызовСервера.ИнициализироватьПеременные(ВыполнятьКриптооперацииНаСервере,
		ВыполнятьАвторизациюНаСервере, НемедленнаяОтправкаЭД);
	Если МассивЭД.Количество() > 0 И ВидЭД = ПредопределенноеЗначение("Перечисление.ВидыЭД.ИзвещениеОПолучении") Тогда
		ЭлектронныеДокументыСлужебныйВызовСервера.УдалитьИзМассиваНеОбрабатываемыеЭД(МассивЭД);
		ВыполнятьКриптооперацииНаСервере = ВыполнятьАвторизациюНаСервере;
	КонецЕсли;
	Если МассивЭД.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	МассивСлужебныхЭД = ЭлектронныеДокументыСлужебныйВызовСервера.СформироватьСлужебныеЭД(МассивЭД, ВидЭД, ТекстУведомления);

	ЕстьОбработанныеЭД = Ложь;
	Если ЗначениеЗаполнено(МассивСлужебныхЭД) Тогда
		СтМассивовСтруктурСертификатов = Новый Структура;
		Действия = "ПодписатьОтправить";
		ДопПараметры = Новый Структура;
		#Если Клиент Тогда
			ЭлектронныеДокументыСлужебныйКлиент.ОбработатьЭД(Новый Массив, Действия, ДопПараметры, МассивСлужебныхЭД);
		#Иначе
			СтруктураСоответствий = Неопределено;
			СтСоотвСоглашенийИМассивовЭД = ЭлектронныеДокументыСлужебныйВызовСервера.ВыполнитьДействияПоЭД(Новый Массив,
				Новый Массив, Действия, ДопПараметры, МассивСлужебныхЭД, СтруктураСоответствий, Истина);
		#КонецЕсли
		
		Если НЕ (ДопПараметры.Свойство("ЕстьОбработанныеЭД", ЕстьОбработанныеЭД) И ЕстьОбработанныеЭД = Истина) Тогда
			ЕстьОбработанныеЭД = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЕстьОбработанныеЭД;
	
КонецФункции

// Формирует текстовое представление рекламы по ЭДО.
//
// Параметры:
//
// ДополнительнаяИнформация - Структура - с полями:
// * Картинка - Картинка - картинка из библиотеки картинок;
// * Текст - Строка - форматированный текст надписи с навигационными ссылками.
//
// МассивСсылок- Массив - список ссылок на объекты.
Функция ПриВыводеНавигационнойСсылкиВФормеОбъектаИБ(ДополнительнаяИнформация, МассивСсылок) Экспорт
	
	Если Не ЗначениеЗаполнено(МассивСсылок) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ЭлектронныеДокументыСлужебныйВызовСервера.ЕстьПравоНастройкиЭДО(Ложь) Тогда
		Возврат "";
	КонецЕсли;
	
	ПараметрыЭД = Неопределено;
	Если Не ЭлектронныеДокументыСлужебныйВызовСервера.ОпределитьДействующуюНастройкуЭДО(МассивСсылок[0], ПараметрыЭД) Тогда
		
		ТекстНавигационнойСсылки = "";
		ЭлектронныеДокументыСлужебныйВызовСервера.ЗаполнитьТекстПриглашенияКЭДО(ТекстНавигационнойСсылки, ПараметрыЭД, МассивСсылок[0], Ложь);
		Если ЗначениеЗаполнено(ТекстНавигационнойСсылки) Тогда
			ДополнительнаяИнформация.Текст    = ТекстНавигационнойСсылки;
			ДополнительнаяИнформация.Картинка = БиблиотекаКартинок.ЭмблемаСервиса1СЭДО;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// НастройкиФормированияДокумента

Функция ТипРазделаДополнительныхПолейШапка() Экспорт
	Возврат "Шапка";
КонецФункции

Функция ТипРазделаДополнительныхПолейТаблица() Экспорт
	Возврат "Таблица";
КонецФункции

// Возвращает сертификат подписавшей стороны из коллекции сертификатов, извлеченной из данных подписи. 
// Поиск происходит с учетом того, что CN Субъекта и Издателя могут быть равны.
//
// Параметры:
//  СертификатыПодписи - Массив - Сертификаты, извлеченные из данных подписи, см. метод платформы ПолучитьСертификатыИзПодписи.
// 
// Возвращаемое значение:
// СертификатКриптографии, Неопределено - Сертификат, с помощью которого была произведена подпись.
//
Функция СертификатПодписавшейСтороны(Знач СертификатыПодписи) Экспорт
	
	Возврат НайтиСертификатПодписавшейСтороныРекурсивно(СертификатыПодписи);
	
КонецФункции

Функция НайтиСертификатПодписавшейСтороныРекурсивно(СертификатыПодписи) 
	
	КоличествоСертификатовПодписи = СертификатыПодписи.Количество();
	Если КоличествоСертификатовПодписи = 1 Тогда
		Возврат СертификатыПодписи[0];
	ИначеЕсли КоличествоСертификатовПодписи = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Пока СертификатыПодписи.Количество() Цикл
		ТекущийСертификат = СертификатыПодписи[0];
		ДочернийСертификат = ДочернийСертификат(СертификатыПодписи, ТекущийСертификат.Субъект.CN);
		Если ДочернийСертификат = Неопределено Тогда
			Возврат ТекущийСертификат;
		Иначе 
			СертификатыПодписи.Удалить(0);
			Возврат НайтиСертификатПодписавшейСтороныРекурсивно(СертификатыПодписи);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции 

Функция ДочернийСертификат(МассивСертификатов, Субъект) 
	
	Для каждого Сертификат Из МассивСертификатов Цикл
		Если Сертификат.Издатель.CN = Субъект Тогда
			Возврат Сертификат; 
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// МЧД

//Возвращает доверенность если она присутствует в соответствии
Функция ПолучитьЗначениеИзСоответствияДоверенностей(Сертификат, ЭД, Соответствие) Экспорт
	
	Если ТипЗнч(Соответствие) <> Тип("Соответствие") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Соответствие[Сертификат] <> Неопределено Тогда
		Возврат Соответствие[Сертификат][ЭД];
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

//Добавляет доверенность если в соответствие
Процедура ДобавитьЗначениеВСоответствиеДоверенностей(Сертификат, ЭД, Значение, Соотсветствие) Экспорт
	
	Если Соотсветствие[Сертификат] = Неопределено Тогда
		Соотсветствие.Вставить(Сертификат, Новый Соответствие);
	КонецЕсли;

	Соотсветствие[Сертификат].Вставить(ЭД, Значение);
	
КонецПроцедуры

Функция ДействияПриОтсутствииВхМЧД() Экспорт
	
	Действия = Новый Структура;
	Действия.Вставить("Спрашивать", "Спрашивать");
	Действия.Вставить("Доверять", "Доверять");
	Действия.Вставить("Требовать", "Требовать");
	
	Возврат Действия;
	
КонецФункции

// Возвращает новые данные подписи с учетом доверенности.
// 
// Возвращаемое значение:
//  Структура:
//   * СвойстваПодписи - Неопределено 
//   				   - См. НовыеДанныеПодписи
//   * ЭтоПодписьПоДоверенности - Булево
//   * СвойстваДоверенности - Неопределено,
//   						- см. МашиночитаемыеДоверенности.ОбщиеСвойстваДоверенности
//   * РезультатПроверкиПоМЧД - Неопределено, 
//   						  - см. МашиночитаемыеДоверенности.НовыйРезультатПроверкиПодписи
//   						  
//   * ПодписанныйОбъект - СправочникСсылка.СообщениеЭДОПрисоединенныеФайлы
Функция НовыеДанныеПодписиСУчетомДоверенности() Экспорт
	
	ДанныеПодписи = Новый Структура;
	ДанныеПодписи.Вставить("СвойстваПодписи", Неопределено);
	ДанныеПодписи.Вставить("ЭтоПодписьПоДоверенности", Ложь);
	ДанныеПодписи.Вставить("СвойстваДоверенности", Неопределено);
	ДанныеПодписи.Вставить("РезультатПроверкиПоМЧД", Неопределено);
	ДанныеПодписи.Вставить("ПодписанныйОбъект", ПредопределенноеЗначение("Справочник.ЭДПрисоединенныеФайлы.ПустаяСсылка"));

	Возврат ДанныеПодписи;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Работа с файлами

// Только для внутреннего использования
Процедура УстановитьТолькоЧтениеВсемФайламВКаталоге(Каталог, ТолькоЧтение) Экспорт
	
	НайденныеФайлы = НайтиФайлы(Каталог, "*.*");
	Для Каждого ФайлВКаталоге Из НайденныеФайлы Цикл 
		Если ФайлВКаталоге.Существует() Тогда
			ФайлВКаталоге.УстановитьТолькоЧтение(ТолькоЧтение);
			ФайлВКаталоге.УстановитьНевидимость(ТолькоЧтение);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры
	
	
	
