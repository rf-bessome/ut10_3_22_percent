////////////////////////////////////////////////////////////////////////////////
// ЭлектронныеДокументыПереопределяемый: механизм обмена электронными документами.
//
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Заполняет массив актуальными видами электронных документов для прикладного решения.
//
// Параметры:
//  Массив - виды актуальных ЭД.
//
Процедура ПолучитьАктуальныеВидыЭД(Массив) Экспорт
	
	Массив.Добавить(Перечисления.ВидыЭД.ТОРГ12Продавец);
	Массив.Добавить(Перечисления.ВидыЭД.ТОРГ12Покупатель);
	Массив.Добавить(Перечисления.ВидыЭД.ЗаказТовара);
	Массив.Добавить(Перечисления.ВидыЭД.ОтветНаЗаказ);
	Массив.Добавить(Перечисления.ВидыЭД.КаталогТоваров);
	Массив.Добавить(Перечисления.ВидыЭД.СчетФактура);
	Массив.Добавить(Перечисления.ВидыЭД.КорректировочныйСчетФактура);
	Массив.Добавить(Перечисления.ВидыЭД.Подтверждение);
	Массив.Добавить(Перечисления.ВидыЭД.ИзвещениеОПолучении);
	Массив.Добавить(Перечисления.ВидыЭД.УведомлениеОбУточнении);
	Массив.Добавить(Перечисления.ВидыЭД.АктЗаказчик);
	Массив.Добавить(Перечисления.ВидыЭД.АктИсполнитель);
	Массив.Добавить(Перечисления.ВидыЭД.СчетНаОплату);
	Массив.Добавить(Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель);
	Массив.Добавить(Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиПолучатель);
	Массив.Добавить(Перечисления.ВидыЭД.АктНаПередачуПрав);
	Массив.Добавить(Перечисления.ВидыЭд.АктСверкиВзаиморасчетов);
	
КонецПроцедуры

// Определяет параметры электронного документа по типу владельца.
//
// Параметры:
//  Источник - объекта либо ссылка документа/справочника-источника.
//  ПараметрыЭД - структура параметров источника, необходимых для определения
//                настроек обмена ЭД. Обязательные параметры: НаправлениеЭД, ВидЭД,
//                Контрагент, СоглашениеЭД или Организация.
//  ФорматCML - булево, если истина, то для формирования ЭД будут использоваться схемы CML (не ФНС),
//    в параметрах должны быть указаны соответствующие виды ЭД.
//
Процедура ЗаполнитьПараметрыЭДПоИсточнику(Источник, ПараметрыЭД, ФорматCML = Ложь) Экспорт
	
	ТипИсточника = ТипЗнч(Источник);
	
	Если ТипИсточника = Тип("ДокументСсылка.УдалитьПроизвольныйЭД")
		 ИЛИ ТипИсточника = Тип("ДокументОбъект.УдалитьПроизвольныйЭД") Тогда
		 
		ПараметрыЭД.ВидЭД =  Перечисления.ВидыЭД.ПроизвольныйЭД;
		ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.РеализацияТоваровУслуг") 
		 ИЛИ ТипИсточника = Тип("ДокументОбъект.РеализацияТоваровУслуг") Тогда
		 
		Если ФорматCML Тогда
			ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.ТОРГ12;
		Иначе
			Если Источник.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.АктВыполненныхРабот Тогда
				ПараметрыЭД.ВидЭД =  Перечисления.ВидыЭД.АктИсполнитель;
			ИначеЕсли Источник.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.АктНаПередачуПрав Тогда
				ПараметрыЭД.ВидЭД =  Перечисления.ВидыЭД.АктНаПередачуПрав;
			Иначе
				ПараметрыЭД.ВидЭД =  Перечисления.ВидыЭД.ТОРГ12Продавец;
			КонецЕсли;
		КонецЕсли;
		
		ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.ПоступлениеТоваровУслуг")
		 ИЛИ ТипИсточника = Тип("ДокументОбъект.ПоступлениеТоваровУслуг") Тогда
		 
		Если ФорматCML Тогда
			ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.ТОРГ12;
		Иначе
			ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Покупатель;
		КонецЕсли;
		ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.ЗаказПоставщику")
		 ИЛИ ТипИсточника = Тип("ДокументОбъект.ЗаказПоставщику") Тогда 
		 
		ПараметрыЭД.ВидЭД =  Перечисления.ВидыЭД.ЗаказТовара;
		ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.ЗаказПокупателя")
		 ИЛИ ТипИсточника = Тип("ДокументОбъект.ЗаказПокупателя") Тогда
		 
		ПараметрыЭД.ВидЭД =  Перечисления.ВидыЭД.ОтветНаЗаказ;
		ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;

	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.СчетФактураВыданный") 
		 ИЛИ ТипИсточника = Тип("ДокументОбъект.СчетФактураВыданный") Тогда
		 
		Если ЭтоКорректировочныйДокумент(Источник) Тогда
			ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.КорректировочныйСчетФактура;
		Иначе
			ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.СчетФактура;
		КонецЕсли;
		ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.СчетФактураПолученный") 
		 ИЛИ ТипИсточника = Тип("ДокументОбъект.СчетФактураПолученный") Тогда
		 
		Если ЭтоКорректировочныйДокумент(Источник) Тогда
			ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.КорректировочныйСчетФактура;
		Иначе
			ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.СчетФактура;
		КонецЕсли;
		ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Входящий;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.СчетНаОплатуПокупателю") 
		 ИЛИ ТипИсточника = Тип("ДокументОбъект.СчетНаОплатуПокупателю") Тогда
		 
		ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.СчетНаОплату;
		ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.СчетНаОплатуПоставщика") 
		 ИЛИ ТипИсточника = Тип("ДокументОбъект.СчетНаОплатуПоставщика") Тогда
		 
		ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.СчетНаОплату;
		ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Входящий;

	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.КорректировкаРеализации") 
		 ИЛИ ТипИсточника = Тип("ДокументОбъект.КорректировкаРеализации") Тогда
		
		ИсходныйИсправляемыйДокумент = УчетНДС.ПолучитьИсправляемыйДокументРеализации(Источник.ДокументРеализации, Истина);
		
		Если Источник.ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение
			 ИЛИ (ТипЗнч(Источник.ИсправляемыйДокументРеализации) = Тип("ДокументСсылка.КорректировкаРеализации") 
			     И Источник.ИсправляемыйДокументРеализации.ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение) Тогда
			 
			// Выполняется согласованное изменение или исправление согласованного изменения
			ПараметрыЭД.ВидЭД         = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель;
			ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
			
		ИначеЕсли ТипЗнч(ИсходныйИсправляемыйДокумент) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
			
			Если ИсходныйИсправляемыйДокумент.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.АктВыполненныхРабот Тогда
				// Выполняется исправление Акта выполненных работ
				ПараметрыЭД.ВидЭД         = Перечисления.ВидыЭД.АктИсполнитель;
				ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
			ИначеЕсли ИсходныйИсправляемыйДокумент.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.АктНаПередачуПрав Тогда
				// Выполняется исправление Акта на передачу прав
				ПараметрыЭД.ВидЭД         = Перечисления.ВидыЭД.АктНаПередачуПрав;
				ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
			Иначе
				// Выполняется исправление ТОРГ-12
				ПараметрыЭД.ВидЭД         = Перечисления.ВидыЭД.ТОРГ12Продавец;
				ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.КорректировкаПоступления") 
		 ИЛИ ТипИсточника = Тип("ДокументОбъект.КорректировкаПоступления") Тогда
		 
		ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
		Если Источник.ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.ИсправлениеОшибки Тогда
			ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Покупатель;
		ИначеЕсли Источник.ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение Тогда
			ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиПолучатель;
		КонецЕсли;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.АктСверкиВзаиморасчетов") 
		 ИЛИ ТипИсточника = Тип("ДокументОбъект.АктСверкиВзаиморасчетов") Тогда
		 
		ПараметрыЭД.ВидЭД =  Перечисления.ВидыЭД.АктСверкиВзаиморасчетов;
		ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
		
	КонецЕсли;
	
	Попытка
		ПараметрыЭД.Организация = Источник.Организация;
		Если Источник.Метаданные().Реквизиты.Найти("Контрагент") <> Неопределено Тогда
			ПараметрыЭД.Контрагент  = Источник.Контрагент;
		КонецЕсли;
		Если ТипЗнч(Источник) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
			Если ТипЗнч(Источник.ДокументОснование) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") Тогда
				ПараметрыЭД.Контрагент = Источник.ДокументОснование.Контрагент;
			КонецЕсли;
		КонецЕсли;

		ПараметрыЭД.ДоговорКонтрагента = Источник.ДоговорКонтрагента;
	Исключение
	КонецПопытки;
	
КонецПроцедуры



////////////////////////////////////////////////////////////////////////////////
// Аунтификация на пользовательском сайте.  

// Получение билета на сайт поддержки
// Параметры:
//  ПараметрыАутентификации	 - Структура 
//       * Логин
//       * Пароль
//	
//	Результат - Структура - Результат вызова
//       * Билет - Строка, билет на сайт поддержки
//       * ОшибкаНеверныйЛогинИлиПароль - Булево - Признак неверного логина или пароля
//       * СообщениеОбОшибке - Строка - Подробное описание ошибки для пользователя
//	
//  СтандартнаяОбработка – Булево – флаг стандартной обработки; если установить значение флага в Ложь,
//									то стандартная обработка получения БилетаНаСайтПоддержки не будет запрашивать билет
//
// Пример:
////////////////////////////////////////////////////////////////////////////////
//
// Получение билета методом БИП:
//
//
//		СтандартнаяОбработка = Ложь;  
//
//		МодульИнтернетПоддержкаПользователей = ОбщегоНазначения.ОбщийМодуль("ИнтернетПоддержкаПользователей");
//		УстановитьПривилегированныйРежим(Истина);
//		Результат = МодульИнтернетПоддержкаПользователей.ТикетАутентификацииНаПорталеПоддержки("1C-EDO");
//		УстановитьПривилегированныйРежим(Ложь);
//
//		Если ЗначениеЗаполнено(Результат.КодОшибки) Тогда
//			
//			Если Результат.КодОшибки = "НеверныйЛогинИлиПароль" Тогда
//				//Сбросим логин и пароль, пользователь должен будет ввести снова
//		    	Результат.ОшибкаНеверныйЛогинИлиПароль = Истина;
//			КонецЕсли;
//			
//			СообщениеОбОшибке = ""+Результат.КодОшибки+", "+Результат.СообщениеОбОшибке;
//		Иначе
//			Билет = Результат.Тикет;
//		КонецЕсли;     
//
////////////////////////////////////////////////////////////////////////////////
//
// Пример использования методов БСП и ВебСервиса, нужно следить за наличием обновленного файла cacert.pem в поставке платформы:
//
//	СтандартнаяОбработка = Ложь;
//	Попытка
//		ВебСервис = ОбщегоНазначения.WSПрокси(
//			"https://login.1c.ru/api/public/ticket?wsdl",
//			"http://api.cas.jasig.org/",
//			"TicketApiImplService",
//			"TicketApiImplPort",
//			ПараметрыАутентификации.Логин,
//			ПараметрыАутентификации.Пароль,
//			5,
//			Ложь);
//		
//		Билет = ВебСервис.getTicket(
//			ПараметрыАутентификации.Логин,
//			ПараметрыАутентификации.Пароль,
//			"1C-EDO");
//	Исключение
//		ИнформацияОбОшибке = ИнформацияОбОшибке();
//		КраткоеПредставлениеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
//		Если Найти(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке), "IncorrectLoginOrPasswordApiException") > 0 Тогда
//				//Сбросим логин и пароль, пользователь должен будет ввести снова
//		    	Результат.ОшибкаНеверныйЛогинИлиПароль = Истина;
//		КонецЕсли;
//		ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
//			НСтр("ru = 'Не удалось подключиться к пользовательскому сайту по причине:
//			           |%1'"),
//			КраткоеПредставлениеОшибки);
//		
//		СообщениеОбОшибке = ОписаниеОшибки;		
//	КонецПопытки;
//
Процедура БилетНаСайтПоддержки(ПараметрыАутентификации, Результат, СтандартнаяОбработка) Экспорт

КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// Определение соответствий объектов библиотеки ЭД и прикладного решения

// Определяет имя реквизита владельца справочника НоменклатураПоставщика.
//
// Параметры:
//  ИмяРеквизитаВладельца - строка - имя реквизита владельца.
//
Процедура ОпределитьИмяРеквизитаВладельцаНоменклатурыПоставщиков(ИмяРеквизитаВладельца) Экспорт
	
	ИмяРеквизитаВладельца = "Контрагент";	
	
КонецПроцедуры

// Определяет соответствие справочников библиотеки и прикладного решения,
// в случае различий в наименовании справочников.
//
// Параметры:
//  СоответствиеСправочников - Соответствие - список справочников.
//
Процедура ПолучитьСоответствиеСправочников(СоответствиеСправочников) Экспорт
	
	СоответствиеСправочников.Вставить("Организации", 				 "Организации");
	СоответствиеСправочников.Вставить("Контрагенты", 				 "Контрагенты");
	СоответствиеСправочников.Вставить("ДоговорыКонтрагентов", 		 "ДоговорыКонтрагентов");
	СоответствиеСправочников.Вставить("Партнеры",    				 "");
	СоответствиеСправочников.Вставить("Банки",       				 "Банки");
	СоответствиеСправочников.Вставить("ФизЛица",                     "ФизическиеЛица");
	
	СоответствиеСправочников.Вставить("Номенклатура",                "Номенклатура");
	СоответствиеСправочников.Вставить("ХарактеристикиНоменклатуры",  "ХарактеристикиНоменклатуры");
	СоответствиеСправочников.Вставить("ЕдиницыИзмерения",            "ЕдиницыИзмерения");
	СоответствиеСправочников.Вставить("НоменклатураПоставщиков",     "");
	СоответствиеСправочников.Вставить("Валюты",                      "Валюты");
	СоответствиеСправочников.Вставить("УпаковкиНоменклатуры",        "ЕдиницыИзмерения");
	СоответствиеСправочников.Вставить("БанковскиеСчетаОрганизаций",  "БанковскиеСчета");
	СоответствиеСправочников.Вставить("БанковскиеСчетаКонтрагентов", "БанковскиеСчета");
	
	СоответствиеСправочников.Вставить("Поставщик", 					 "Контрагенты");
	
	СоответствиеСправочников.Вставить("СтраныМира",					"КлассификаторСтранМира");
	
КонецПроцедуры

// Получает значение перечисления по имени объектов метаданных.
// 
// Параметры:
//  СоответствиеПеречислений - соответствие библиотечных и прикладных перечислений.
//
Процедура ПолучитьСоответствиеПеречислений(СоответствиеПеречислений) Экспорт
	
	СоответствиеПеречислений.Вставить("НДС", "СтавкиНДС");
	СоответствиеПеречислений.Вставить("ЮрФизЛицо", "ЮрФизЛицо");
	СоответствиеПеречислений.Вставить("ВариантыОплатыКлиентом", "");
	СоответствиеПеречислений.Вставить("ВариантыОплатыПоставщику", "");
	СоответствиеПеречислений.Вставить("ФормыОплаты", "");
	СоответствиеПеречислений.Вставить("СпособРасчета", "");
	
КонецПроцедуры

// В функции описана структура сопоставления имен переменных библиотеки,
// наименованиям объектов и реквизитов метаданных прикладного решения.
// 
// Параметры:
//  Ключ соответствия - имя переменной, используемой в коде библиотеки;
//  Значение соответствия - наименование объекта метаданных или реквизита объекта
//  в прикладном решении.
//
Процедура ПолучитьСоответствиеНаименованийОбъектовМДиРеквизитов(СоответствиеРеквизитовОбъекта) Экспорт
	
	СоответствиеРеквизитовОбъекта.Вставить("СчетФактураВыданныйВМетаданных",       "СчетФактураВыданный");
	СоответствиеРеквизитовОбъекта.Вставить("СчетФактураПолученныйВМетаданных",     "СчетФактураПолученный");
	СоответствиеРеквизитовОбъекта.Вставить("РеализацияТоваровУслугВМетаданных",    "РеализацияТоваровУслуг");
	СоответствиеРеквизитовОбъекта.Вставить("ПоступлениеТоваровУслугВМетаданных",   "ПоступлениеТоваровУслуг");
	СоответствиеРеквизитовОбъекта.Вставить("ДатаВыставленияВСчетеФактуреВыданном", "ДатаВыставления");
	СоответствиеРеквизитовОбъекта.Вставить("ДатаПолученияВСчетеФактуреПолученном", "Дата");
	СоответствиеРеквизитовОбъекта.Вставить("НомерСчета",                           "НомерСчета");
	
	СоответствиеРеквизитовОбъекта.Вставить("ИННКонтрагента",                       "ИНН");
	СоответствиеРеквизитовОбъекта.Вставить("КППКонтрагента",                       "КПП");
	СоответствиеРеквизитовОбъекта.Вставить("НаименованиеКонтрагента",              "Наименование");
	СоответствиеРеквизитовОбъекта.Вставить("НаименованиеКонтрагентаДляСообщенияПользователю", "НаименованиеПолное");
	СоответствиеРеквизитовОбъекта.Вставить("ВнешнийКодКонтрагента",                "Код");
	СоответствиеРеквизитовОбъекта.Вставить("ПартнерКонтрагента",                   "Партнер");
	
	СоответствиеРеквизитовОбъекта.Вставить("ИННОрганизации",                       "ИНН");
	СоответствиеРеквизитовОбъекта.Вставить("КППОрганизации",                       "КПП");
	СоответствиеРеквизитовОбъекта.Вставить("ОГРНОрганизации",                      "ОГРН");
	СоответствиеРеквизитовОбъекта.Вставить("НаименованиеОрганизации",              "НаименованиеПолное");
	СоответствиеРеквизитовОбъекта.Вставить("СокращенноеНаименованиеОрганизации",   "Наименование");

КонецПроцедуры

// Определяет соответствие функциональных опций библиотеки и прикладного решения,
// в случае различий в наименовании.
//
// Параметры:
//  СоответствиеФО - Соответствие - список функциональных опций.
//
Процедура ПолучитьСоответствиеФункциональныхОпций(СоответствиеФО) Экспорт
	
	СоответствиеФО.Вставить("ИспользоватьОбменЭД",                    "ИспользоватьОбменЭД");
	СоответствиеФО.Вставить("ИспользоватьЭлектронныеЦифровыеПодписи", "ИспользоватьЭлектронныеЦифровыеПодписи");
	СоответствиеФО.Вставить("ИспользоватьОбменЭДМеждуОрганизациями",  "ИспользоватьОбменЭДМеждуОрганизациями");
	СоответствиеФО.Вставить("ИспользоватьОбменЭДСБанками",            "ИспользоватьОбменЭДСБанками");
	
	СоответствиеФО.Вставить("ИспользоватьРучныеСкидкиВПродажах",         Неопределено);
	СоответствиеФО.Вставить("ИспользоватьАвтоматическиеСкидкиВПродажах", Неопределено);
	СоответствиеФО.Вставить("ИспользоватьРучныеСкидкиВЗакупках",         Неопределено);
	
КонецПроцедуры
// В процедуре указывается соответствие строковых представлений ставок НДС (используемые в БЭД)
// с прикладными значениями этих ставок.
//
// Параметры:
//   Соответствие - Соответствие - заполняемое соответствие ставок НДС.
//
// Пример:
//   Соответствие.Вставить("0",       Перечисления.СтавкиНДС.НДС0);
//   Соответствие.Вставить("10",      Перечисления.СтавкиНДС.НДС10);
//   Соответствие.Вставить("18",      Перечисления.СтавкиНДС.НДС18);
//   Соответствие.Вставить("10/110",  Перечисления.СтавкиНДС.НДС10_110);
//   Соответствие.Вставить("18/118",  Перечисления.СтавкиНДС.НДС18_118);
//   Соответствие.Вставить("без НДС", Перечисления.СтавкиНДС.БезНДС);
//   Соответствие.Вставить("20",      Перечисления.СтавкиНДС.НДС20);
//   Соответствие.Вставить("20/120",  Перечисления.СтавкиНДС.НДС20_120);
//
Процедура ЗаполнитьСоответствиеСтавокНДС(Соответствие) Экспорт
	
   Соответствие.Вставить("0",       Перечисления.СтавкиНДС.НДС0);
   Соответствие.Вставить("10",      Перечисления.СтавкиНДС.НДС10);
   Соответствие.Вставить("18",      Перечисления.СтавкиНДС.НДС18);
   //ТЕАЛ НДС ++
   Соответствие.Вставить("5",      Перечисления.СтавкиНДС.НДС5);
   Соответствие.Вставить("7",      Перечисления.СтавкиНДС.НДС7);
   Соответствие.Вставить("5/105",  Перечисления.СтавкиНДС.НДС5_105);
   Соответствие.Вставить("7/107",  Перечисления.СтавкиНДС.НДС7_107);
   Соответствие.Вставить("22",      Перечисления.СтавкиНДС.НДС22);
   Соответствие.Вставить("22/122",  Перечисления.СтавкиНДС.НДС22_122);
   //ТЕАЛ НДС --
   Соответствие.Вставить("10/110",  Перечисления.СтавкиНДС.НДС10_110);
   Соответствие.Вставить("18/118",  Перечисления.СтавкиНДС.НДС18_118);
   Соответствие.Вставить("без НДС", Перечисления.СтавкиНДС.БезНДС);
   Соответствие.Вставить("НДС исчисляется налоговым агентом", Перечисления.СтавкиНДС.ПустаяСсылка());
   Соответствие.Вставить("20",      Перечисления.СтавкиНДС.НДС20);
   Соответствие.Вставить("20/120",  Перечисления.СтавкиНДС.НДС20_120);
КонецПроцедуры

// Необходимо заполнить соответствие ставок и сумм НДС
//
Процедура ПолучитьСоответствиеСтавокНДС(СоответствиеСтавокНДС) Экспорт
	
	СоответствиеСтавокНДС = Новый Соответствие();
	
	СоответствиеСтавокНДС.Вставить(Перечисления.СтавкиНДС.НДС10,     0);
	СоответствиеСтавокНДС.Вставить(Перечисления.СтавкиНДС.НДС10_110, 0);
	СоответствиеСтавокНДС.Вставить(Перечисления.СтавкиНДС.НДС18,     0);
	СоответствиеСтавокНДС.Вставить(Перечисления.СтавкиНДС.НДС18_118, 0);
	СоответствиеСтавокНДС.Вставить(Перечисления.СтавкиНДС.НДС20,     0);
	СоответствиеСтавокНДС.Вставить(Перечисления.СтавкиНДС.НДС20_120, 0);
	//ТЕАЛ НДС ++
	СоответствиеСтавокНДС.Вставить(Перечисления.СтавкиНДС.НДС5,     0);
	СоответствиеСтавокНДС.Вставить(Перечисления.СтавкиНДС.НДС5_105, 0);
	СоответствиеСтавокНДС.Вставить(Перечисления.СтавкиНДС.НДС7,     0);
	СоответствиеСтавокНДС.Вставить(Перечисления.СтавкиНДС.НДС7_107, 0); 
	СоответствиеСтавокНДС.Вставить(Перечисления.СтавкиНДС.НДС22,     0);
	СоответствиеСтавокНДС.Вставить(Перечисления.СтавкиНДС.НДС22_122, 0);
	//ТЕАЛ НДС --
	СоответствиеСтавокНДС.Вставить(Перечисления.СтавкиНДС.НДС0,      0);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Настройка обмена

// Определяет, является ли объект корректировочным документом
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка.СчетФактураВыданный
//
// Возвращаемое значение:
//  Булево
//
Функция ЭтоКорректировочныйДокумент(СсылкаНаОбъект) Экспорт
	
	Результат = Ложь;
	
	Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		Результат = (СсылкаНаОбъект.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.Корректировочный);
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
		Результат = (СсылкаНаОбъект.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.Корректировочный);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Процедура возвращает признак использования справочника Партнеров в качестве
// дополнительной аналитики к справочнику Контрагенты.
//
// Параметры:
//  ИспользуетсяСправочникПартнеры - Булево - флаг использования в библиотеке справочника Партеры.
//
Процедура ДополнительнаяАналитикаКонтрагентовСправочникПартнеры(ИспользуетсяСправочникПартнеры) Экспорт

	ИспользуетсяСправочникПартнеры = Ложь;

КонецПроцедуры

// Процедура возвращает признак использования справочника "Характеристики номенклатуры" в качестве
// дополнительной аналитики к справочнику Номенклатура.
//
// Параметры:
//  ИспользуетсяСправочникХарактеристикиНоменклатуры - Булево - флаг использования в библиотеке справочника "Характеристики номенклатуры".
//
Процедура ДополнительнаяАналитикаСправочникХарактеристикиНоменклатуры(ИспользуетсяСправочникХарактеристикиНоменклатуры) Экспорт
	
	ИспользуетсяСправочникХарактеристикиНоменклатуры = глЗначениеПеременной("ИспользоватьХарактеристикиНоменклатуры");
	
КонецПроцедуры

// Процедура возвращает признак использования справочника "Упаковка номенклатуры" в качестве
// дополнительной аналитики к справочнику Номенклатура.
//
// Параметры:
//  ИспользуетсяСправочникУпаковкиНоменклатуры - Булево - флаг использования справочника "Упаковки номенклатуры".
//
Процедура ДополнительнаяАналитикаСправочникУпаковкиНоменклатуры(ИспользуетсяСправочникУпаковкиНоменклатуры) Экспорт
	
	ИспользуетсяСправочникУпаковкиНоменклатуры = Ложь;
	
КонецПроцедуры

// Получает текст запроса по настройкам обмена.
//
// Возвращаемое значение:
//  ТекстЗапроса - текст запроса.
//
Функция ПолучитьТекстНастроекОбменаПоСоглашению() Экспорт
	
	ТекстЗапроса = "";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Получает текст запроса по настройкам обмена с приоритетами.
//
// Возвращаемое значение:
//  ТекстЗапроса - текст запроса.
//
Функция ПолучитьТекстЗапросаНастроекОбменаСПриоритетами() Экспорт
	
	ТекстЗапроса = "";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Получает ключевые реквизиты объекта по текстовому представлению.
//
// Параметры:
//  ИмяОбъекта - Строка, текстовое представление объекта, ключевые реквизиты которого необходимо получить.
//
// Возвращаемое значение:
//  СтруктураКлючевыхРеквизитов - перечень параметров объекта.
//
Процедура ПолучитьСтруктуруКлючевыхРеквизитовОбъекта(ИмяОбъекта, СтруктураКлючевыхРеквизитов) Экспорт
	                           
	Если ИмяОбъекта = "Документ.УдалитьПроизвольныйЭД" Тогда
		// шапка
		СтрокаРеквизитовОбъекта = ("Дата, Номер, Организация, Контрагент, Текст");
		СтруктураКлючевыхРеквизитов.Вставить("Шапка", СтрокаРеквизитовОбъекта);
	ИначеЕсли ИмяОбъекта = "Документ.РеализацияТоваровУслуг" Тогда
		// шапка
		СтрокаРеквизитовОбъекта = ("Дата, Номер, Организация, Контрагент, ВалютаДокумента, СуммаДокумента, УчитыватьНДС, ВидОперации");
		СтруктураКлючевыхРеквизитов.Вставить("Шапка", СтрокаРеквизитовОбъекта);
		// ТЧ
		СтрокаРеквизитовОбъекта = ("Номенклатура, ХарактеристикаНоменклатуры, ЕдиницаИзмерения, Количество, Цена, Сумма, СуммаНДС, СтавкаНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Товары", СтрокаРеквизитовОбъекта);
		СтрокаРеквизитовОбъекта = ("Номенклатура, Содержание, Количество, Цена, Сумма, СуммаНДС, СтавкаНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Услуги", СтрокаРеквизитовОбъекта);
	ИначеЕсли ИмяОбъекта = "Документ.ПоступлениеТоваровУслуг" Тогда
		// шапка
		СтрокаРеквизитовОбъекта = ("Дата, Номер, Организация, Контрагент, ВалютаДокумента, СуммаДокумента, УчитыватьНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Шапка", СтрокаРеквизитовОбъекта);
		// ТЧ
		СтрокаРеквизитовОбъекта = ("Номенклатура, ХарактеристикаНоменклатуры, ЕдиницаИзмерения, Количество, Цена, Сумма, СуммаНДС, СтавкаНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Товары", СтрокаРеквизитовОбъекта);
		СтрокаРеквизитовОбъекта = ("Номенклатура, Содержание, Количество, Цена, Сумма, СуммаНДС, СтавкаНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Услуги", СтрокаРеквизитовОбъекта);
	ИначеЕсли ИмяОбъекта = "Документ.СчетФактураВыданный" Тогда
		// шапка
		СтрокаРеквизитовОбъекта = ("Дата, Номер, Организация, СуммаДокумента, СуммаНДСДокумента");
		СтруктураКлючевыхРеквизитов.Вставить("Шапка", СтрокаРеквизитовОбъекта);
	ИначеЕсли ИмяОбъекта = "Документ.СчетФактураПолученный" Тогда
		// шапка
		СтрокаРеквизитовОбъекта = ("Дата, Номер, Организация, СуммаДокумента, СуммаНДСДокумента");
		СтруктураКлючевыхРеквизитов.Вставить("Шапка", СтрокаРеквизитовОбъекта);
	ИначеЕсли ИмяОбъекта = "Документ.ЗаказПоставщику" Тогда
		// шапка
		СтрокаРеквизитовОбъекта = ("Дата, Номер, Организация, Контрагент, ВалютаДокумента, СуммаДокумента, УчитыватьНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Шапка", СтрокаРеквизитовОбъекта);
		// ТЧ
		СтрокаРеквизитовОбъекта = ("Номенклатура, ХарактеристикаНоменклатуры, ЕдиницаИзмерения, Количество, Цена, Сумма, СуммаНДС, СтавкаНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Товары", СтрокаРеквизитовОбъекта);
		СтрокаРеквизитовОбъекта = ("Номенклатура, Содержание, Количество, Цена, Сумма, СуммаНДС, СтавкаНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Услуги", СтрокаРеквизитовОбъекта);
	ИначеЕсли ИмяОбъекта = "Документ.ЗаказПокупателя" Тогда
		// шапка
		СтрокаРеквизитовОбъекта = ("Дата, Номер, Организация, Контрагент, ВалютаДокумента, СуммаДокумента, УчитыватьНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Шапка", СтрокаРеквизитовОбъекта);
		// ТЧ
		СтрокаРеквизитовОбъекта = ("Номенклатура, ХарактеристикаНоменклатуры, ЕдиницаИзмерения, Количество, Цена, Сумма, СуммаНДС, СтавкаНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Товары", СтрокаРеквизитовОбъекта);
		СтрокаРеквизитовОбъекта = ("Номенклатура, Содержание, Количество, Цена, Сумма, СуммаНДС, СтавкаНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Услуги", СтрокаРеквизитовОбъекта);
	ИначеЕсли ИмяОбъекта = "Документ.СчетНаОплатуПокупателю" Тогда
		// шапка
		СтрокаРеквизитовОбъекта = ("Дата, Номер, Организация, Контрагент, ВалютаДокумента, СуммаДокумента, УчитыватьНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Шапка", СтрокаРеквизитовОбъекта);
		// ТЧ
		СтрокаРеквизитовОбъекта = ("Номенклатура, ХарактеристикаНоменклатуры, ЕдиницаИзмерения, Количество, Цена, Сумма, СуммаНДС, СтавкаНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Товары", СтрокаРеквизитовОбъекта);
		СтрокаРеквизитовОбъекта = ("Номенклатура, Содержание, Количество, Цена, Сумма, СуммаНДС, СтавкаНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Услуги", СтрокаРеквизитовОбъекта);
	ИначеЕсли ИмяОбъекта = "Документ.КорректировкаРеализации" Тогда
		// шапка
		СтрокаРеквизитовОбъекта = ("Дата, Номер, Организация, Контрагент, ВалютаДокумента, СуммаДокумента, УчитыватьНДС, ВидОперации");
		СтруктураКлючевыхРеквизитов.Вставить("Шапка", СтрокаРеквизитовОбъекта);
		// ТЧ
		СтрокаРеквизитовОбъекта = ("Номенклатура, ХарактеристикаНоменклатуры, ЕдиницаИзмерения, Количество, Цена, Сумма, СуммаНДС, СтавкаНДС, 
								|КоличествоДоИзменения, ЦенаДоИзменения, СуммаДоИзменения, СуммаНДСДоИзменения, СтавкаНДСДоИзменения, 
								|КоличествоДоКорректировки, ЦенаДоКорректировки, СуммаДоКорректировки, СуммаНДСДоКорректировки");
		СтруктураКлючевыхРеквизитов.Вставить("Товары", СтрокаРеквизитовОбъекта);
		СтрокаРеквизитовОбъекта = ("Номенклатура, Содержание, Количество, Цена, Сумма, СуммаНДС, СтавкаНДС, 
								|КоличествоДоИзменения, ЦенаДоИзменения, СуммаДоИзменения, СуммаНДСДоИзменения, СтавкаНДСДоИзменения, СодержаниеДоИзменения, 
								|КоличествоДоКорректировки, ЦенаДоКорректировки, СуммаДоКорректировки, СуммаНДСДоКорректировки");
		СтруктураКлючевыхРеквизитов.Вставить("Услуги", СтрокаРеквизитовОбъекта);
	ИначеЕсли ИмяОбъекта = "Документ.ОтчетКомитентуОПродажах" Тогда
		// шапка
		СтрокаРеквизитовОбъекта = ("Дата, Номер, Организация, Контрагент, ВалютаДокумента, СуммаДокумента, УдержатьКомиссионноеВознаграждение, 
								|СпособРасчетаКомиссионногоВознаграждения, СуммаВознаграждения, СтавкаНДСВознаграждения, ПроцентКомиссионногоВознаграждения");
		СтруктураКлючевыхРеквизитов.Вставить("Шапка", СтрокаРеквизитовОбъекта);
		СтрокаРеквизитовОбъекта = ("Номенклатура, ХарактеристикаНоменклатуры, ЕдиницаИзмерения, Количество, Цена, Сумма, СтавкаНДС, 
								|ЦенаПоступления, СуммаПоступления, СуммаВознаграждения, СуммаНДСВознаграждения, Покупатель, ДатаРеализации");
	КонецЕсли;
	
КонецПроцедуры

// Процедура возвращает данные для заполнения заявки на получение уникального
// идентификатора абонента, добавления сертификата абонента.
//
// Параметры:
// Организация - Произвольный - ссылка на элемент справочника Организации;
// ДанныеОрганизации - Структура - данные об организации:
//	* Индекс - Строка - почтовый индекс организации;
//	* Регион - Строка - код региона организации;
//	* Район - Строка - Район;
//	* Город - Строка - Город;
//	* НаселенныйПункт - Строка - населенный пункт расположения организации;
//	* Улица - Строка - Улица;
//	* Дом - Строка - Дом;
//	* Корпус - Строка - Корпус;
//	* Квартира - Строка - Квартира;
//	* Телефон - Строка - телефон организации;
//	* Наименование - Строка - наименование организации;
//	* ИНН - Строка - ИНН организации;
//	* КПП - Строка - КПП организации;
//	* ОГРН - Строка - ОГРН организации;
//	* КодИМНС - Строка - код ИМНС организации;
//	* ЮрФизЛицо - Строка - вид лица, возможные значения: "ЮрЛицо" или "ФизЛицо";
//	* Фамилия - Строка - фамилия руководителя;
//	* Имя - Строка - имя руководителя;
//	* Отчество - Строка - отчество руководителя;
////////////////////////////////////////////////////////////////////////////////
//
// Пример заполнения МЧД части
//	ДанныеОрганизации.Вставить("ЭтоЮридическоеЛицо", НЕ СвойстваОрганизации.ЮрФизЛицо = Перечисления._ДемоЮрФизЛицо.ИндивидуальныйПредприниматель);
//	ДанныеОрганизации.Вставить("Должность", "");
//	ДанныеОрганизации.Вставить("СНИЛС", "");
//	ДанныеОрганизации.Вставить("РуководительОснованиеПолномочий", "");	
//	ДанныеОрганизации.Вставить("РуководительИНН", "");	
//	ДанныеОрганизации.Вставить("ДатаРождения", "");	
//	ДанныеОрганизации.Вставить("Гражданство", Справочники.КлассификаторСтранМира.Россия);	
//	ДанныеОрганизации.Вставить("РуководительФизлицо", Справочники.ФизическиеЛица.ПустаяСсылка());	
//	Если ДанныеОрганизации.ЮрФизЛицо = "ЮрЛицо" Тогда
//		ДанныеОрганизации.ЭтоЮридическоеЛицо = Истина;
//		ДанныеОрганизации.Должность = "Директор";
//		ДанныеОрганизации.РуководительОснованиеПолномочий = "Устав";	
//	КонецЕсли;
//
Процедура ЗаполнитьРегистрационныеДанныеОрганизации(Организация, ДанныеОрганизации) Экспорт
	
	ДанныеОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, "Ссылка, Наименование, НаименованиеПолное, ИНН, КПП, ОГРН, КодИМНС, ЮрФизЛицо");
	Если ДанныеОрганизации.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицо") Тогда
		ДанныеОрганизации.ЮрФизЛицо = "ЮрЛицо";
	Иначе
		ДанныеОрганизации.ЮрФизЛицо = "ФизЛицо";
	КонецЕсли;
	
	Если Не ПустаяСтрока(ДанныеОрганизации.НаименованиеПолное) Тогда
		ДанныеОрганизации.Наименование = ДанныеОрганизации.НаименованиеПолное;
	КонецЕсли;
	ДанныеОрганизации.Удалить("НаименованиеПолное");
	
	СтруктураАдреса = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьАдресСтруктурой(ДанныеОрганизации, "Ссылка", "Факт");
	Если СтруктураАдреса.АдресРФ = Истина Тогда
		ДанныеОрганизации.Вставить("Индекс", СтруктураАдреса.Индекс);
		ДанныеОрганизации.Вставить("Регион", СтруктураАдреса.Регион);
		ДанныеОрганизации.Вставить("КодРегиона", СтруктураАдреса.КодРегион);
		ДанныеОрганизации.Вставить("Район", СтруктураАдреса.Район);
		ДанныеОрганизации.Вставить("Город", СтруктураАдреса.Город);
		ДанныеОрганизации.Вставить("НаселенныйПункт", СтруктураАдреса.НаселПункт);
		ДанныеОрганизации.Вставить("Улица", СтруктураАдреса.Улица);
		ДанныеОрганизации.Вставить("Дом", СтруктураАдреса.Дом);
		ДанныеОрганизации.Вставить("Корпус", СтруктураАдреса.Корпус);
		ДанныеОрганизации.Вставить("Квартира", СтруктураАдреса.Кварт);
	КонецЕсли;
	
	Сведения = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Организация, ТекущаяДатаСеанса());
	Если ТипЗнч(Сведения.Телефоны) = Тип("Строка") Тогда
		Телефон = Сведения.Телефоны;
	ИначеЕсли ТипЗнч(Сведения.Телефоны) = Тип("Массив") И Сведения.Телефоны.Количество() > 0 Тогда
		Телефон = Сведения.Телефоны[0];
	Иначе
		Телефон = "";
	КонецЕсли;
	Если Не ПустаяСтрока(Телефон) Тогда
		ДанныеОрганизации.Вставить("Телефон", Телефон);
	КонецЕсли;
	
	Руководитель = ОтветственноеЛицоОрганизации(ТекущаяДатаСеанса(), Организация, 
		ПредопределенноеЗначение("Перечисление.ОтветственныеЛицаОрганизации.Руководитель"));
	Если ТипЗнч(Руководитель.СтруктураФИО) = Тип("Структура") Тогда
		ДанныеОрганизации.Вставить("Фамилия", Руководитель.СтруктураФИО.Фамилия);
		ДанныеОрганизации.Вставить("Имя", Руководитель.СтруктураФИО.Имя);
		ДанныеОрганизации.Вставить("Отчество", Руководитель.СтруктураФИО.Отчество);
	КонецЕсли;
	
	// Данные МЧД
	ДанныеОрганизации.Вставить("ЭтоЮридическоеЛицо", НЕ ДанныеОрганизации.ЮрФизЛицо = "ФизЛицо");
	ДанныеОрганизации.Вставить("Должность", "");
	ДанныеОрганизации.Вставить("СНИЛС", "");
	ДанныеОрганизации.Вставить("РуководительОснованиеПолномочий", "");
	ДанныеОрганизации.Вставить("РуководительИНН", "");
	ДанныеОрганизации.Вставить("ДатаРождения", "");
	ДанныеОрганизации.Вставить("Гражданство", Справочники.КлассификаторСтранМира.Россия);
	ДанныеОрганизации.Вставить("РуководительФизлицо", Справочники.ФизическиеЛица.ПустаяСсылка());
	Если ДанныеОрганизации.ЮрФизЛицо = "ЮрЛицо" Тогда
		ДанныеОрганизации.ЭтоЮридическоеЛицо = Истина;
		ДанныеОрганизации.Должность = "Директор";
		ДанныеОрганизации.РуководительОснованиеПолномочий = "Устав";
	КонецЕсли;
	Если ЗначениеЗаполнено(Руководитель.ФизическоеЛицо) Тогда
		ДанныеРуководителя = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(Руководитель.ФизическоеЛицо, "ДатаРождения,ИНН");
		ДанныеОрганизации.РуководительФизлицо = Руководитель.ФизическоеЛицо;
		ДанныеОрганизации.ДатаРождения = ДанныеРуководителя.ДатаРождения;
		ДанныеОрганизации.РуководительИНН = ДанныеРуководителя.ИНН;
	КонецЕсли;
		
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// Формирование данных для электронных документов

// Работа с деревом данных ФНС

// Подготавливает данные для электронного документа типа УПД (информация продавца).
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на объект информационной базы, по которому необходимо 
//                                    создать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//
Процедура ЗаполнитьДанныеДляУПДИнформацияПродавцаФНС(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных) Экспорт
	
	Если ТипЗнч(СсылкаНаОбъект) <> Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		
		СтруктураОтбораСчетаФактуры = Новый Структура;
		СтруктураОтбораСчетаФактуры.Вставить("ПометкаУдаления", Ложь);
		СсылкаСчетаФактуры = УчетНДС.НайтиПодчиненныйСчетФактуру(СсылкаНаОбъект, , СтруктураОтбораСчетаФактуры);
		
		Если НЕ ЗначениеЗаполнено(СсылкаСчетаФактуры) Тогда
			ЗаполнитьДанныеДляДОПИнформацияПродавцаФНС(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных);
		Иначе
			ЗаполнитьДанныеДляДОПИнформацияПродавцаФНС(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных);
			ДополнитьУПД_ДаннымиПоСчетуФактуре(СсылкаСчетаФактуры, ДеревоДанных);
		КонецЕсли;

	Иначе
		
		ЗаполнитьДанныеДляСЧФИнформацияПродавцаФНС(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных);
		
	КонецЕсли;
	

КонецПроцедуры

// Подготавливает данные для электронного документа типа УПД (информация продавца).
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на объект информационной базы, по которому необходимо 
//                                    создать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//
Процедура ЗаполнитьДанныеДляДОПИнформацияПродавцаФНС(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных) Экспорт
	
	// Получим данные для заполнения дерева
	ДанныеДокумента = Новый Структура;
	ПодготовитьДанныеДляУПД_ПервичныйДокумент(СсылкаНаОбъект, ДанныеДокумента, СтруктураЭД.ВариантыЗаполненияПолей.ТоварКод);
	РеквизитыШапки = ДанныеДокумента.РеквизитыШапки;
	ТаблицаНоменклатуры =  ДанныеДокумента.ТаблицаНоменклатуры;
	
	// ОснованиеОтгрузки
	ОснованиеОтгрузки = Новый ТаблицаЗначений;
	ОснованиеОтгрузки.Колонки.Добавить("ДокументНаименование");
	ОснованиеОтгрузки.Колонки.Добавить("ДокументНомер");
	ОснованиеОтгрузки.Колонки.Добавить("ДокументДата");
	ОснованиеОтгрузки.Колонки.Добавить("ДокументДопСведения");
	СтрокаОснование = ОснованиеОтгрузки.Добавить();
	СтрокаОснование.ДокументНаименование = РеквизитыШапки.ДоговорНаименование;
	СтрокаОснование.ДокументНомер = РеквизитыШапки.ДоговорНомер;
	СтрокаОснование.ДокументДата = РеквизитыШапки.ДоговорДата;
	СтрокаОснование.ДокументДопСведения = "";
	ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ОснованиеОтгрузки, "ОснованиеОтгрузкиТоваров");	

	// СведенияОПродавце
	СведенияОПродавце = ПолучитьДанныеЮрФизЛица(РеквизитыШапки.Организация, РеквизитыШапки.БанковскийСчет);
	ЗаполнитьСведенияУчастникаОбмена(ДеревоДанных, СведенияОПродавце, "Юр", "СведенияОПродавце", , Истина);
	// СведенияОПокупателе
	РеквизитыКонтрагента = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(РеквизитыШапки.Контрагент, 
		"ГоловнойКонтрагент, ОбособленноеПодразделение, КПП");
	Если РеквизитыКонтрагента.ОбособленноеПодразделение И ЗначениеЗаполнено(РеквизитыКонтрагента.ГоловнойКонтрагент) Тогда
		СведенияОПокупателе = ПолучитьДанныеЮрФизЛица(РеквизитыКонтрагента.ГоловнойКонтрагент);
		СведенияОПокупателе.КПП = РеквизитыКонтрагента.КПП;
	Иначе
		СведенияОПокупателе = ПолучитьДанныеЮрФизЛица(РеквизитыШапки.Контрагент);
	КонецЕсли;
	ЗаполнитьСведенияУчастникаОбмена(ДеревоДанных, СведенияОПокупателе, "Юр", "СведенияОПокупателе", , Истина);

	// *ДополнительныеСведенияОбУчастниках
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДополнительныеСведенияОбУчастниках.ВалютаНаименование", 
		СокрЛП(РеквизитыШапки.ВалютаНаименование));
	
	// СведенияОТоварах
	ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаНоменклатуры, "СведенияОТоварах");
	
	// *ВсегоКОплате
	ИтогоСНалогом = ТаблицаНоменклатуры.Итог("СтоимостьТоваровСНалогом");
	ИтогоБезНалога = ТаблицаНоменклатуры.Итог("СтоимостьТоваровБезНалога");
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВсегоКОплате.ВсегоСтоимостьТоваровСНалогом", 
		ИтогоСНалогом);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВсегоКОплате.ВсегоСтоимостьТоваровБезНалога", 
		ИтогоБезНалога);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВсегоКОплате.ВсегоСуммаНалога", 
		ИтогоСНалогом - ИтогоБезНалога);
	
	// Реквизиты шапки
	ЗаполнитьДатуИНомер(ДеревоДанных, РеквизитыШапки, СсылкаНаОбъект, "НомерДокумента", "ДатаДокумента");
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СоставительДокументаНаименование", 
		СведенияОПродавце.ПолноеНаименование);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаКод", 
		СокрЛП(РеквизитыШапки.ВалютаКод));
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СодержаниеОперации", 
		"Товары переданы/Услуги оказаны");
	ТолькоУслуги = ТаблицаНоменклатуры.Найти(Ложь, "Услуга") = Неопределено;
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ТолькоУслуги", 
		ТолькоУслуги);
		
	Если Не ТолькоУслуги Тогда
		// СведенияОГрузоотправителе
		Если ЗначениеЗаполнено(РеквизитыШапки.Грузоотправитель) Тогда
			СведенияОГрузоотправителе = ПолучитьДанныеЮрФизЛица(РеквизитыШапки.Грузоотправитель);
			ЗаполнитьСведенияУчастникаОбмена(ДеревоДанных, СведенияОГрузоотправителе, "Факт", "СведенияОГрузоотправителе.Грузоотправитель", , Истина);
		КонецЕсли;
		// СведенияОГрузополучателе
		Если ЗначениеЗаполнено(РеквизитыШапки.Грузополучатель) Тогда
			СведенияОГрузополучателе = ПолучитьДанныеЮрФизЛица(РеквизитыШапки.Грузополучатель);
			ЗаполнитьСведенияУчастникаОбмена(ДеревоДанных, СведенияОГрузополучателе, "Факт", "СведенияОГрузополучателе", , Истина);
		КонецЕсли;
	КонецЕсли;
		
	СчетФактураОснования = УчетНДС.НайтиПодчиненныйСчетФактуру(РеквизитыШапки.ДокументОснование);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияСчетаФактуры", 
		СчетФактураОснования);
		
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВидОперации", 
		РеквизитыШапки.ВидОперации);
	//ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправления", 
	//	РеквизитыШапки.);
	//ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсправления", 
	//	РеквизитыШапки.);
		
КонецПроцедуры

// Подготавливает данные для электронного документа типа УПД (информация покупателя).
//
// Параметры:
//  СсылкаНаЭД - Ссылка - ссылка на ЭД, по которому необходимо сформировать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//
Процедура ЗаполнитьДанныеДляУПДИнформацииПокупателяФНС(СсылкаНаЭД, СтруктураЭД, ДеревоДанных) Экспорт
	
	Если Константы.ВводитьИнформациюОЛицахПринявшихТоварыВЭД.Получить() Тогда
		ПустоеПоступление = Документы.ПоступлениеТоваровУслуг.ПустаяСсылка();
		ДокументОснования = ПустоеПоступление;
	
		Для Каждого ДокументИБ Из СсылкаНаЭД.ВладелецФайла.ДокументыОснования Цикл
		
			Если ТипЗнч(ДокументИБ.ДокументОснование) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
				ДокументОснования = ДокументИБ.ДокументОснование;
				Прервать;
			КонецЕсли;
		
		КонецЦикла;
	
		Если ДокументОснования <> ПустоеПоступление Тогда
			Запрос = Новый Запрос;
			Запрос.Текст = 
				"ВЫБРАТЬ
				|	ЛицаПринявшиеТовары.ИныеСведения,
				|	ЛицаПринявшиеТовары.ТипЛицаПринявшегоТовар,
				|	ЛицаПринявшиеТовары.ФизЛицо,
				|	ЛицаПринявшиеТовары.Должность,
				|	ЛицаПринявшиеТовары.ОснованиеПолномочий,
				|	ЛицаПринявшиеТовары.НаименованиеОрганизации,
				|	ЛицаПринявшиеТовары.ДоверенностьНаПринятие
				|ИЗ
				|	РегистрСведений.ЛицаПринявшиеТовары КАК ЛицаПринявшиеТовары
				|ГДЕ
				|	ЛицаПринявшиеТовары.ДокументПриемки = &Регистратор";
			Запрос.УстановитьПараметр("Регистратор", ДокументОснования);
			ВыборкаПоРегистратору = Запрос.Выполнить().Выбрать();

			Если ВыборкаПоРегистратору.Следующий() Тогда
			
				Если ВыборкаПоРегистратору.ТипЛицаПринявшегоТовар = 1 Тогда
 					ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОЛицеПринявшемТовары", "РаботникОрганизацииПокупателя");
					ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОЛицеПринявшемТовары.РаботникОрганизацииПокупателя", Истина);
					ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОЛицеПринявшемТовары.РаботникОрганизацииПокупателя.Должность", ВыборкаПоРегистратору.Должность);
					ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОЛицеПринявшемТовары.РаботникОрганизацииПокупателя.ИныеСведения", ВыборкаПоРегистратору.ИныеСведения);
					ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОЛицеПринявшемТовары.РаботникОрганизацииПокупателя.ОснованиеПолномочий", ВыборкаПоРегистратору.ОснованиеПолномочий);
					СтруктураФИО = ФормированиеПечатныхФорм.ФамилияИмяОтчество(ВыборкаПоРегистратору.ФизЛицо, СтруктураЭД.ВладелецЭД.Дата);
					ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОЛицеПринявшемТовары.РаботникОрганизацииПокупателя.Фамилия", СтруктураФИО.Фамилия);
					ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОЛицеПринявшемТовары.РаботникОрганизацииПокупателя.Имя", СтруктураФИО.Имя);
					ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОЛицеПринявшемТовары.РаботникОрганизацииПокупателя.Отчество", СтруктураФИО.Отчество);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
			
	КонецЕсли;
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СоставительДокументаНаименование", 
		СтруктураЭД.Организация.НаименованиеПолное);
		
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СодержаниеОперации", 
		"Товары приняты/Услуги получены");

	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаПолученияТоваров", 
		СтруктураЭД.ВладелецЭД.Дата);

КонецПроцедуры

// Подготавливает данные для электронного документа типа УПД (информация продавца).
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на объект информационной базы, по которому необходимо 
//                                    создать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//
Процедура ЗаполнитьДанныеДляСЧФИнформацияПродавцаФНС(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных) Экспорт
	
	СтруктураРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаОбъект, "ДокументОснование, ВидСчетаФактуры, ИдентификаторГосКонтракта");
	
	Если (СтруктураЭД.Функция = "СЧФ" ИЛИ СтруктураЭД.Функция = "СЧФДОП")
		И (СтруктураРеквизитов.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАванс
		ИЛИ СтруктураРеквизитов.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитента) Тогда
		
		ДанныеДокумента = Неопределено;
		УчетнаяПолитика = Неопределено;
		СсылкаНаОбъект.ПолучитьОбъект().СобратьДанныеДляПечати(ДанныеДокумента, УчетнаяПолитика);
		
		Отказ = Ложь;	
		ЭлектронноеВзаимодействиеИСМППереопределяемый.ПроверитьМаркируемуюПродукциюДокумента(СсылкаНаОбъект, Отказ);
	
		// СведенияОПродавце
		БанковскийСчет = Неопределено;
		СведенияОПродавце = ПолучитьДанныеЮрФизЛица(ДанныеДокумента.Организация, БанковскийСчет);
		ЗаполнитьСведенияУчастникаОбмена(ДеревоДанных, СведенияОПродавце, "Юр", "СведенияОПродавце", , Истина);
		
		// СведенияОПокупателе
		РеквизитыКонтрагента = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ДанныеДокумента.Покупатель, 
			"ГоловнойКонтрагент, ОбособленноеПодразделение, КПП");
		Если РеквизитыКонтрагента.ОбособленноеПодразделение И ЗначениеЗаполнено(РеквизитыКонтрагента.ГоловнойКонтрагент) Тогда
			СведенияОПокупателе = ПолучитьДанныеЮрФизЛица(РеквизитыКонтрагента.ГоловнойКонтрагент);
			СведенияОПокупателе.КПП = РеквизитыКонтрагента.КПП;
		Иначе
			СведенияОПокупателе = ПолучитьДанныеЮрФизЛица(ДанныеДокумента.Покупатель);
		КонецЕсли;
		ЗаполнитьСведенияУчастникаОбмена(ДеревоДанных, СведенияОПокупателе, "Юр", "СведенияОПокупателе", , Истина);
		
		// Реквизиты шапки
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СоставительДокументаНаименование", 
			СведенияОПродавце.ПолноеНаименование);
			
		РеквизитыВалюты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеДокумента.Валюта, "Код, НаименованиеПолное");
		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаКод", СокрЛП(РеквизитыВалюты.Код));
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			"ДополнительныеСведенияОбУчастниках.ВалютаНаименование", СокрЛП(РеквизитыВалюты.НаименованиеПолное));
		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВидСчетаФактуры", "Авансовый");
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СодержаниеОперации", 
			НСтр("ru = 'Регистрация счет-фактуры на аванс'"));
		
		// СведенияОТоварах
		ТаблицаТоваров = Новый ТаблицаЗначений;
		ТаблицаТоваров.Колонки.Добавить("НомерСтроки");
		ТаблицаТоваров.Колонки.Добавить("ТоварИдентификатор", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(255)));
		ТаблицаТоваров.Колонки.Добавить("ТоварКод", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(255)));
		ТаблицаТоваров.Колонки.Добавить("ТоварНаименование");
		ТаблицаТоваров.Колонки.Добавить("СтоимостьТоваровБезНалога", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
		ТаблицаТоваров.Колонки.Добавить("НалоговаяСтавка");
		ТаблицаТоваров.Колонки.Добавить("СтоимостьТоваровСНалогом", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
		ТаблицаТоваров.Колонки.Добавить("СуммаНалога", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
		ТаблицаТоваров.Колонки.Добавить("Признак", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(1)));
		ТаблицаТоваров.Колонки.Добавить("СведенияОМаркировке");
		ТаблицаКодовМаркировки = Неопределено;
		ДанныеДокумента.Свойство("Маркировка", ТаблицаКодовМаркировки);
		ОбъектыСОграничениемДлиныКодов = Новый Массив;
		
		НомерСтроки = 1;
		ТолькоУслуги = Истина;
		ТекстОшибки = "";
		
		Для Каждого СтрокаТаблицы Из ДанныеДокумента.ТабличнаяЧасть Цикл
			
			НоваяСтрока = ТаблицаТоваров.Добавить();
			НоваяСтрока.НомерСтроки = НомерСтроки;
			НомерСтроки = НомерСтроки + 1;
			
			Если Не ЗначениеЗаполнено(СтрокаТаблицы.Товар) ИЛИ ТипЗнч(СтрокаТаблицы.Товар) = Тип("Строка") Тогда
				ТекстОшибки = "Для формирования электронного документа необходимо заполнить реквизит ""Номенклатура (обобщенное наименование)""";
				Прервать;
			КонецЕсли;

			НоваяСтрока.ТоварИдентификатор        = Строка(СтрокаТаблицы.Товар.УникальныйИдентификатор());
			НоваяСтрока.ТоварКод                  = СтрокаТаблицы.ТоварКод;
			НоваяСтрока.ТоварНаименование         = СтрокаТаблицы.ТоварНаименование;
			НоваяСтрока.НалоговаяСтавка           = СтрокаТаблицы.СтавкаНДС;
			НоваяСтрока.СуммаНалога               = СтрокаТаблицы.СуммаНДС;
			Если СтрокаТаблицы.СуммаВключаетНДС Тогда
				НоваяСтрока.СтоимостьТоваровСНалогом  = СтрокаТаблицы.Сумма;
				//НоваяСтрока.СтоимостьТоваровБезНалога = СтрокаТаблицы.Сумма - СтрокаТаблицы.СуммаНДС;
			Иначе
				НоваяСтрока.СтоимостьТоваровСНалогом  = СтрокаТаблицы.Сумма + СтрокаТаблицы.СуммаНДС;
				//НоваяСтрока.СтоимостьТоваровБезНалога = СтрокаТаблицы.Сумма;
			КонецЕсли;
			
			ПризнакТовараЕдиногоДокумента = "";
			Если ТипЗнч(СтрокаТаблицы.Товар) = Тип("СправочникСсылка.Номенклатура") Тогда
				Если ЗначениеЗаполнено(СтрокаТаблицы.Товар)
					И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТаблицы.Товар, "Услуга") Тогда
					ПризнакТовараЕдиногоДокумента = "3"; // Услуга
				Иначе
					ПризнакТовараЕдиногоДокумента = "1"; // Имущество
				КонецЕсли;
			Иначе
				ПризнакТовараЕдиногоДокумента = "5"; // Иное
			КонецЕсли;
			
			НоваяСтрока.Признак = ПризнакТовараЕдиногоДокумента;
			
			Если ТолькоУслуги
				И НоваяСтрока.Признак = "1" Тогда
				ТолькоУслуги = Ложь;
			КонецЕсли;
		
			ЭлектронноеВзаимодействиеИСМП.ЗаполнитьСведенияОМаркировке(НоваяСтрока, СтрокаТаблицы, ТаблицаКодовМаркировки, ОбъектыСОграничениемДлиныКодов);
		
		КонецЦикла;
		
		Отказ = Ложь;	
		ЭлектронноеВзаимодействиеИСМП.ПроверитьСведенияОМаркировке(ТаблицаКодовМаркировки, ОбъектыСОграничениемДлиныКодов, Отказ);
		Если Отказ Тогда
			ТекстОшибки = НСтр("ru = 'Ошибка при формирование ЭД. Неверное указание маркированной продукции. 
			|Не удалось распределить все штрихкоды упаковок для маркируемых товаров в табличной части.
			| Возможная причина: слишком большое количество кодов в строке, требуется разбиение строки'");
		КонецЕсли;
	
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			ОбщегоНазначения.СообщитьОбОшибке(ТекстОшибки);
		КонецЕсли;
		
		ИтогоСНалогом = ТаблицаТоваров.Итог("СтоимостьТоваровСНалогом");
		ИтогоБезНалога = ТаблицаТоваров.Итог("СтоимостьТоваровБезНалога");
		ИтогоСуммаНалога = ТаблицаТоваров.Итог("СуммаНалога");
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВсегоКОплате.ВсегоСтоимостьТоваровСНалогом", 
			ИтогоСНалогом);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВсегоКОплате.ВсегоСтоимостьТоваровБезНалога", 
			ИтогоБезНалога);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВсегоКОплате.ВсегоСуммаНалога", 
			ИтогоСуммаНалога);
		
		ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаТоваров, "СведенияОТоварах");
		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ТолькоУслуги", ТолькоУслуги);
		
		ПлатежныеДокументы = Новый ТаблицаЗначений();
		ПлатежныеДокументы.Колонки.Добавить("ДатаПРД");
		ПлатежныеДокументы.Колонки.Добавить("НомерПРД");
		
		Если ДанныеДокумента.ТаблицаДатОплат <> Неопределено Тогда
			Для Каждого ПлатежныйДокумент ИЗ ДанныеДокумента.ТаблицаДатОплат Цикл
				НовыйПлатежныйДокумент = ПлатежныеДокументы.Добавить();
				НовыйПлатежныйДокумент.ДатаПРД  = ПлатежныйДокумент.ДатаПлатежноРасчетногоДокумента;
				НовыйПлатежныйДокумент.НомерПРД = ПлатежныйДокумент.НомерПлатежноРасчетногоДокумента;
			КонецЦикла;
		КонецЕсли;
		ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ПлатежныеДокументы, "ПлатежноРасчетныеДокументы");
		
		Если Не ПустаяСтрока(СтруктураРеквизитов.ИдентификаторГосКонтракта) Тогда
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДополнительныеСведенияОбУчастниках.ИдентификаторГосКонтракта", 
				СтруктураРеквизитов.ИдентификаторГосКонтракта);
			ДопДанные = Новый ТаблицаЗначений;
			ДопДанные.Колонки.Добавить("Идентификатор");
			ДопДанные.Колонки.Добавить("Значение");
			СтрокаТаблицы = ДопДанные.Добавить();
			СтрокаТаблицы.Идентификатор = "ИдентификаторГосКонтракта";
			СтрокаТаблицы.Значение = СтруктураРеквизитов.ИдентификаторГосКонтракта;
			ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ДопДанные, "ДопДанныеСчетаФактуры.ТекстоваяИнформация");
		КонецЕсли;

	Иначе
		
		ЗаполнитьДанныеДляДОПИнформацияПродавцаФНС(СтруктураРеквизитов.ДокументОснование, СтруктураЭД, ДеревоДанных);
		
	КонецЕсли;
	
	ДополнитьУПД_ДаннымиПоСчетуФактуре(СсылкаНаОбъект, ДеревоДанных);
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа УКД (информация продавца).
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на объект информационной базы, по которому необходимо
//                                    создать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//
Процедура ЗаполнитьДанныеДляУКДИнформацияПродавцаФНС(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных) Экспорт
	
	Если ТипЗнч(СсылкаНаОбъект) <> Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		
		СтруктураОтбораСчетаФактуры = Новый Структура;
		СтруктураОтбораСчетаФактуры.Вставить("ПометкаУдаления", Ложь);
		СсылкаСчетаФактуры = УчетНДС.НайтиПодчиненныйСчетФактуру(СсылкаНаОбъект, , СтруктураОтбораСчетаФактуры);
		
		Если НЕ ЗначениеЗаполнено(СсылкаСчетаФактуры) Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'По документу ""%1"" не выписан счет-фактура или он помечен на удаление!'"), СсылкаНаОбъект);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Иначе
			ЗаполнитьДанныеДляДИСИнформацияПродавцаФНС(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных);
			ДополнитьУКД_ДаннымиПоСчетуФактуре(СсылкаСчетаФактуры, ДеревоДанных);
		КонецЕсли;
	
	Иначе
		
		ЗаполнитьДанныеДляКСЧФИнформацияПродавцаФНС(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных);
		
	КонецЕсли;
	
КонецПроцедуры


// Подготавливает данные для электронного документа типа УКД (информация продавца).
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на объект информационной базы, по которому необходимо
//                                    создать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//
Процедура ЗаполнитьДанныеДляДИСИнформацияПродавцаФНС(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных) Экспорт
	
	// Получим данные для заполнения дерева
	ДанныеДокумента = Новый Структура;
	ПодготовитьДанныеДляУКД_ПервичныйДокумент(СсылкаНаОбъект, ДанныеДокумента);
	РеквизитыШапки = ДанныеДокумента.РеквизитыШапки;
	ТаблицаНоменклатуры =  ДанныеДокумента.ТаблицаТоваров;

	// ОснованиеКорректировки
	ОснованиеКорректировки = Новый ТаблицаЗначений;
	ОснованиеКорректировки.Колонки.Добавить("ДокументНаименование");
	ОснованиеКорректировки.Колонки.Добавить("ДокументНомер");
	ОснованиеКорректировки.Колонки.Добавить("ДокументДата");
	ОснованиеКорректировки.Колонки.Добавить("ДокументДопСведения");
	Если ЗначениеЗаполнено(РеквизитыШапки.ДоговорНомер) Тогда
		СтрокаОснование = ОснованиеКорректировки.Добавить();
		СтрокаОснование.ДокументНаименование = РеквизитыШапки.ДоговорНаименование;
		СтрокаОснование.ДокументНомер = РеквизитыШапки.ДоговорНомер;
		СтрокаОснование.ДокументДата = РеквизитыШапки.ДоговорДата;
		СтрокаОснование.ДокументДопСведения = "";
	КонецЕсли;
	ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ОснованиеКорректировки, "ОснованиеКорректировки");	
	
	// СведенияОПродавце
	СведенияОПродавце   = ПолучитьДанныеЮрФизЛица(РеквизитыШапки.Организация, РеквизитыШапки.БанковскийСчет);
	ЗаполнитьСведенияУчастникаОбмена(ДеревоДанных, СведенияОПродавце,   "Юр", "СведенияОПродавце", , Истина);
	// СведенияОПокупателе
	РеквизитыКонтрагента = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(РеквизитыШапки.Контрагент, 
		"ГоловнойКонтрагент, ОбособленноеПодразделение, КПП");
	Если РеквизитыКонтрагента.ОбособленноеПодразделение И ЗначениеЗаполнено(РеквизитыКонтрагента.ГоловнойКонтрагент) Тогда
		СведенияОПокупателе = ПолучитьДанныеЮрФизЛица(РеквизитыКонтрагента.ГоловнойКонтрагент);
		СведенияОПокупателе.КПП = РеквизитыКонтрагента.КПП;
	Иначе
		СведенияОПокупателе = ПолучитьДанныеЮрФизЛица(РеквизитыШапки.Контрагент);
	КонецЕсли;
	ЗаполнитьСведенияУчастникаОбмена(ДеревоДанных, СведенияОПокупателе, "Юр", "СведенияОПокупателе", , Истина);

	// ДополнительныеСведенияОбУчастниках
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, 
		"ДополнительныеСведенияОбУчастниках.ВалютаНаименование", РеквизитыШапки.ВалютаДокумента.НаименованиеПолное);
	
	// СведенияОВыбытииМаркированныхТоваров
	Если РеквизитыШапки.ОперацияВыбытияМаркированныхТоваров <> 0 Тогда
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОВыбытииМаркированныхТоваров", РеквизитыШапки.ОперацияВыбытияМаркированныхТоваров);
	КонецЕсли;
	
	// СведенияОТоварах
	ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаНоменклатуры, "СведенияОТоварах");
	
	// ВсегоИзмененияСтоимости
	ВсегоСНалогомДоКорректировки = ТаблицаНоменклатуры.Итог("СтоимостьТоваровСНалогомДоКорректировки");
	ВсегоБезНалогаДоКорректировки = ТаблицаНоменклатуры.Итог("СтоимостьТоваровБезНалогаДоКорректировки");
	ВсегоНалогаДоКорректировки = ТаблицаНоменклатуры.Итог("СуммаНалогаДоКорректировки");
	ВсегоСНалогом = ТаблицаНоменклатуры.Итог("СтоимостьТоваровСНалогом");
	ВсегоБезНалога = ТаблицаНоменклатуры.Итог("СтоимостьТоваровБезНалога");
	ВсегоНалога = ТаблицаНоменклатуры.Итог("СуммаНалога");
	РазницаСНалогом = ВсегоСНалогом - ВсегоСНалогомДоКорректировки;
	РазницаБезНалога = ВсегоБезНалога - ВсегоБезНалогаДоКорректировки;
	РазницаНалога = ВсегоНалога - ВсегоНалогаДоКорректировки;
	Если РазницаСНалогом > 0 Тогда
		Суффикс1 = "Увеличение";
		Суффикс2 = "Уменьшение";
	Иначе
		Суффикс1 = "Уменьшение";
		Суффикс2 = "Увеличение";
	КонецЕсли;
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, 
		"ВсегоИзмененияСтоимости.ВсегоСтоимостьТоваровСНалогом"+Суффикс1, Макс(РазницаСНалогом,-РазницаСНалогом));
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, 
		"ВсегоИзмененияСтоимости.ВсегоСтоимостьТоваровБезНалога"+Суффикс1, Макс(РазницаБезНалога,-РазницаБезНалога));
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, 
		"ВсегоИзмененияСтоимости.ВсегоСуммаНалога"+Суффикс1, Макс(РазницаНалога,-РазницаНалога));
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, 
		"ВсегоИзмененияСтоимости.ВсегоСтоимостьТоваровСНалогом"+Суффикс2, 0);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, 
		"ВсегоИзмененияСтоимости.ВсегоСтоимостьТоваровБезНалога"+Суффикс2, 0);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, 
		"ВсегоИзмененияСтоимости.ВсегоСуммаНалога"+Суффикс2, 0);
	
	// РеквизитыШапки
	ЗаполнитьДатуИНомер(ДеревоДанных, РеквизитыШапки, СсылкаНаОбъект, "НомерДокумента", "ДатаДокумента");
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, 
		"ВидДокумента", РеквизитыШапки.ВидДокумента);
	
	ИсправляемыйДокумент = УчетНДС.ПолучитьИсправляемыйДокументРеализации(РеквизитыШапки.ДокументОснование);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсходногоДокумента", 
		ПолучитьПечатныйНомерДокумента(ИсправляемыйДокумент));
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсходногоДокумента", 
		ИсправляемыйДокумент.Дата);
		
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияСчетаФактуры", 
		СсылкаНаОбъект.ИсправляемыйДокументРеализации);
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СоставительДокументаНаименование", 
		СведенияОПродавце.ПолноеНаименование);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаКод", 
		СокрЛП(РеквизитыШапки.ВалютаДокумента.Код));
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СодержаниеОперации", 
		"Предложение по изменению стоимости");
	НомерНаПечатьСтрокой 	= ОбщегоНазначения.ПолучитьНомерНаПечать(ИсправляемыйДокумент);
	ДатаНаПечатьСтрокой 	= Формат(ИсправляемыйДокумент.Дата, "ДФ='дд ММММ гггг'") + " г.";
	
	Если ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, "РеквизитыПередаточныхДокументов") Тогда
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "РеквизитыПередаточныхДокументов", "Универсальный передаточный документ № " + НомерНаПечатьСтрокой + " от " +  ДатаНаПечатьСтрокой);
	КонецЕсли;
	
	// Передаточный документ
	Если ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, "ПередаточныйДокумент") Тогда
		ПередаточныйДокумент = Новый ТаблицаЗначений;
		ПередаточныйДокумент.Колонки.Добавить("ДокументНаименование");
		ПередаточныйДокумент.Колонки.Добавить("ДокументНомер");
		ПередаточныйДокумент.Колонки.Добавить("ДокументДата");
		ПередаточныйДокумент.Колонки.Добавить("ДокументДопСведения");
		
		СтрокаПД = ПередаточныйДокумент.Добавить();
		СтрокаПД.ДокументНаименование = "Универсальный передаточный документ";
		СтрокаПД.ДокументНомер        = НомерНаПечатьСтрокой;
		СтрокаПД.ДокументДата         = ИсправляемыйДокумент.Дата;
		СтрокаПД.ДокументДопСведения  = "";
		
		ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ПередаточныйДокумент, "ПередаточныйДокумент");
	КонецЕсли;

КонецПроцедуры

// Подготавливает данные для электронного документа типа УКД (информация покупателя).
//
// Параметры:
//  СсылкаНаЭД - Ссылка - ссылка на ЭД, по которому необходимо сформировать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//
Процедура ЗаполнитьДанныеДляУКДИнформацииПокупателяФНС(СсылкаНаЭД, СтруктураЭД, ДеревоДанных) Экспорт
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СоставительДокументаНаименование", 
		СтруктураЭД.Организация.НаименованиеПолное);
		
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СодержаниеОперации", 
		"Товары приняты/Услуги получены");

	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаСогласования", 
		СтруктураЭД.ВладелецЭД.Дата);

КонецПроцедуры


// Подготавливает данные для электронного документа типа УКД (информация продавца).
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на объект информационной базы, по которому необходимо
//                                    создать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//
Процедура ЗаполнитьДанныеДляКСЧФИнформацияПродавцаФНС(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных) Экспорт
	
	ЗаполнитьДанныеДляДИСИнформацияПродавцаФНС(СсылкаНаОбъект.ДокументОснование, СтруктураЭД, ДеревоДанных);
	
	ДополнитьУКД_ДаннымиПоСчетуФактуре(СсылкаНаОбъект, ДеревоДанных)
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа УКД(СвИСРК).
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на учетный документ, данными которого необходимо
//    заполнить электронный документ.
//  СтруктураЭД - Структура - данные для формирования электронного документа, см. СтруктураЭлектронногоДокумента.
//    Дополнительные поля:
//     * ТипЭлементаВерсииЭД - ПеречислениеСсылка.ТипыЭлементовВерсииЭД - тип элемента версии электронного документа,
//        определяющий вариант заполнения дерева данных. Возможные значения:
//         - КСЧФДИСУКД - корректировочный счет-фактура и документ о согласии покупателя на  изменение стоимости отгрузки;
//         - КСЧФУКД - корректировочный счет-фактура, применяемый при расчетах по налогу на добавленную стоимость;
//         - ДИСУКД - документ о согласии покупателя на изменение стоимости отгрузки.
//  ДеревоДанных - ДеревоЗначений - дерево данных из которого заполняется электронный документ,
//    см. макет Обработка.ОбменСКонтрагентами.УКД_ИнформацияПродавца_2020.
//
Процедура ЗаполнитьДанныеУКД0ЗакупкаИнформацияПродавцаФНС_2020(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных) Экспорт
	
	
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа УКД(СвИСРК).
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на учетный документ, данными которого необходимо
//    заполнить электронный документ.
//  СтруктураЭД - Структура - данные для формирования электронного документа, см. СтруктураЭлектронногоДокумента.
//    Дополнительные поля:
//     * ТипЭлементаВерсииЭД - ПеречислениеСсылка.ТипыЭлементовВерсииЭД - тип элемента версии электронного документа,
//        определяющий вариант заполнения дерева данных. Возможные значения:
//         - КСЧФДИСУКД - корректировочный счет-фактура и документ о согласии покупателя на  изменение стоимости отгрузки;
//         - КСЧФУКД - корректировочный счет-фактура, применяемый при расчетах по налогу на добавленную стоимость;
//         - ДИСУКД - документ о согласии покупателя на изменение стоимости отгрузки.
//  ДеревоДанных - ДеревоЗначений - дерево данных из которого заполняется электронный документ,
//    см. макет Обработка.ОбменСКонтрагентами.УКД_ИнформацияПродавца_2020.
//
Процедура ЗаполнитьДанныеУКД0ИнформацияПродавцаФНС_2020(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных) Экспорт
	
	
	
КонецПроцедуры


// Подготавливает данные для электронного документа типа УКД (информация продавца).
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на учетный документ, данными которого необходимо
//    заполнить электронный документ.
//  СтруктураЭД - Структура - данные для формирования электронного документа, см. СтруктураЭлектронногоДокумента.
//    Дополнительные поля:
//     * ТипЭлементаВерсииЭД - ПеречислениеСсылка.ТипыЭлементовВерсииЭД - тип элемента версии электронного документа,
//        определяющий вариант заполнения дерева данных. Возможные значения:
//         - КСЧФДИСУКД - корректировочный счет-фактура и документ о согласии покупателя на  изменение стоимости отгрузки;
//         - КСЧФУКД - корректировочный счет-фактура, применяемый при расчетах по налогу на добавленную стоимость;
//         - ДИСУКД - документ о согласии покупателя на изменение стоимости отгрузки.
//  ДеревоДанных - ДеревоЗначений - дерево данных из которого заполняется электронный документ,
//    см. макет Обработка.ОбменСКонтрагентами.УКД_ИнформацияПродавца_2020.
//  Отказ - Булево - если нужно отказаться от создания электронного документа, необходимо установить значение в Истина.
//                   После этого дальнейшие действия по формированию документа производиться не будут, поэтому
//                   нужно сформировать сообщения пользователю при необходимости самостоятельно.
//
Процедура ЗаполнитьДанныеУКДИнформацияПродавцаФНС_2020(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных) Экспорт
	
	Если ТипЗнч(СсылкаНаОбъект) <> Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		
		СтруктураОтбораСчетаФактуры = Новый Структура;
		СтруктураОтбораСчетаФактуры.Вставить("ПометкаУдаления", Ложь);
		СсылкаСчетаФактуры = УчетНДС.НайтиПодчиненныйСчетФактуру(СсылкаНаОбъект, , СтруктураОтбораСчетаФактуры);
		
		Если НЕ ЗначениеЗаполнено(СсылкаСчетаФактуры) Тогда
			ЗаполнитьДанныеДляДИСИнформацияПродавцаФНС(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных);
		Иначе
			ЗаполнитьДанныеДляДИСИнформацияПродавцаФНС(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных);
			ДополнитьУКД_ДаннымиПоСчетуФактуре(СсылкаСчетаФактуры, ДеревоДанных);
		КонецЕсли;
	
	Иначе
		
		ЗаполнитьДанныеДляКСЧФИнформацияПродавцаФНС(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных);
		
	КонецЕсли;
	
КонецПроцедуры
	

// Подготавливает данные для электронного документа типа УКД (информация покупателя).
//
// Параметры:
//  СсылкаНаЭД - Ссылка - ссылка на ЭД, по которому необходимо сформировать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//
Процедура ЗаполнитьДанныеУКДИнформацияПокупателяФНС_2020(СсылкаНаЭД, СтруктураЭД, ДеревоДанных) Экспорт
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СоставительДокументаНаименование", 
		СтруктураЭД.Организация.НаименованиеПолное);
		
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СодержаниеОперации", 
		"Товары приняты/Услуги получены");

	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаСогласования", 
		СтруктураЭД.ВладелецЭД.Дата);
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа Торг12 титул продавца.
//
// Параметры:
//  СсылкаНаОбъект - документСсылка - ссылка на объект информационной базы,
//  по которому необходимо создать электронный документ.
//  СтруктураЭД - структура - структура данных для формирования электронного документа.
//  ДеревоДанных - дерево значений, дерево данных заполнения электронного документа.
//
Процедура ЗаполнитьДанныеПоТорг12ПродавецФНС(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных) Экспорт
	
	ДанныеДокумента = ПолучитьДанныеРеализации(СсылкаНаОбъект);
	РеквизитыШапки = ДанныеДокумента.РеквизитыШапки;
	ТаблицаТоваров = ДанныеДокумента.ТаблицаТоваров;
	ТаблицаСН      = ДанныеДокумента.ТаблицаСН;
	
	ЗаполнитьДатуИНомер(ДеревоДанных, РеквизитыШапки, СсылкаНаОбъект, "НомерТоварнойНакладной", "ДатаТоварнойНакладной");

	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснования", РеквизитыШапки.ДокументОснование);
	
	ТаблицаОснование = Новый ТаблицаЗначений;
	ТаблицаОснование.Колонки.Добавить("ДокОснованиеНаименование");
	ТаблицаОснование.Колонки.Добавить("ДокОснованиеНомер");
	ТаблицаОснование.Колонки.Добавить("ДокОснованиеДата");
	СтрокаОснование = ТаблицаОснование.Добавить();
	СтрокаОснование.ДокОснованиеНаименование = РеквизитыШапки.ДоговорНаименование;
	СтрокаОснование.ДокОснованиеНомер = РеквизитыШапки.ДоговорНомер;
	СтрокаОснование.ДокОснованиеДата = РеквизитыШапки.ДоговорДата;
	ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаОснование, "Основание");
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВидОперации", РеквизитыШапки.ВидОперации);
	
	ВалютаРеглУчета = глЗначениеПеременной("ВалютаРегламентированногоУчета");
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаКод", ВалютаРеглУчета.Код);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаНаименование", ВалютаРеглУчета.НаименованиеПолное);
	
	Если ЗначениеЗаполнено(РеквизитыШапки.Сделка) Тогда
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументСделкиНаименование", Строка(ТипЗнч(РеквизитыШапки.Сделка)));
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументСделкиНомер", ОбщегоНазначения.ПолучитьНомерНаПечать(РеквизитыШапки.Сделка));
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументСделкиДата", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыШапки.Сделка, "Дата"));
	КонецЕсли;
	
	СведенияОПоставщике       = ПолучитьДанныеЮрФизЛица(РеквизитыШапки.Организация);
	СведенияОПокупателе       = ПолучитьДанныеЮрФизЛица(РеквизитыШапки.Контрагент);
	СведенияОГрузополучателе  = ПолучитьДанныеЮрФизЛица(РеквизитыШапки.Грузополучатель);
	СведенияОГрузоотправителе = ПолучитьДанныеЮрФизЛица(РеквизитыШапки.Грузоотправитель);

	ЗаполнитьСведенияУчастникаОбмена(ДеревоДанных, СведенияОПоставщике,       "Юр",   "Поставщик",);
	ЗаполнитьСведенияУчастникаОбмена(ДеревоДанных, СведенияОПокупателе,       "Юр",   "Плательщик",);
	ЗаполнитьСведенияУчастникаОбмена(ДеревоДанных, СведенияОГрузополучателе,  "Факт", "Грузополучатель",);
	ЗаполнитьСведенияУчастникаОбмена(ДеревоДанных, СведенияОГрузоотправителе, "Факт", "Грузоотправитель",);

	Если ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, "СведенияОЛицеПередавшемТовары.ИноеЛицо.ФЛ") Тогда
		Если ЗначениеЗаполнено(РеквизитыШапки.ОтпускПроизвел) Тогда
			ЗаполнитьРеквизитыФизЛица(ДеревоДанных,	РеквизитыШапки.ОтпускПроизвел.Наименование,
					"СведенияОЛицеПередавшемТовары.ИноеЛицо.ФЛ", "");
		КонецЕсли;
	КонецЕсли;

	ИтоговыеСуммы = СтруктураИтоговыеСуммы(ТаблицаТоваров, СтруктураЭД.ВидЭД);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
								"СведенияПоОтпускуГруза.ОтпущеноНаСумму", ИтоговыеСуммы.СуммаСНДС);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
								"СведенияПоОтпускуГруза.ОтпущеноНаСуммуПрописью", СуммаПрописью(ИтоговыеСуммы.СуммаСНДС));
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
								"СведенияПоОтпускуГруза.ДатаОтпуска", РеквизитыШапки.ДатаДокумента);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
								"ОбщиеСведенияОТоварнойНакладной.КоличествоПорядковыхНомеровЗаписей", ИтоговыеСуммы.КоличествоПорядковыхНомеровЗаписей);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
								"ОбщиеСведенияОТоварнойНакладной.ВсегоМест", ИтоговыеСуммы.КоличествоМест);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
								"ОбщиеСведенияОТоварнойНакладной.ВсегоМестПрописью", ЧислоПрописью(ИтоговыеСуммы.КоличествоМест, ,",,,,,,,,0"));
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
								"ОбщиеСведенияОТоварнойНакладной.МассаГрузаНетто", ИтоговыеСуммы.МассаНетто);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
								"ОбщиеСведенияОТоварнойНакладной.МассаГрузаБрутто", ИтоговыеСуммы.МассаБрутто);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
								"ВсегоПоНакладной.КоличествоМест", ИтоговыеСуммы.КоличествоМест);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
								"ВсегоПоНакладной.МассаБрутто", ИтоговыеСуммы.МассаБрутто);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
								"ВсегоПоНакладной.МассаНетто", ИтоговыеСуммы.МассаНетто);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
								"ВсегоПоНакладной.СуммаБезНДС", ИтоговыеСуммы.СуммаБезНДС);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
								"ВсегоПоНакладной.СуммаНДС", ИтоговыеСуммы.СуммаНДС);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
								"ВсегоПоНакладной.СуммаСНДС", ИтоговыеСуммы.СуммаСНДС);
	
	ТаблицаТоваров.Колонки.Добавить("СтавкаНДСЧислом");
	ТаблицаТоваров.Колонки.Добавить("ДопДанныеПодписанные");
	ТаблицаТоваров.Колонки.Добавить("ДопДанныеНеПодписанные");
	ТаблицаТоваров.Колонки.Добавить("ИдТовараУКонтрагента");
	Для Каждого СтрокаТоваров Из ТаблицаТоваров Цикл
		СтрокаТоваров.СтавкаНДСЧислом = ПолучитьСтавкуНДСЧислом(СтрокаТоваров.СтавкаНДС);
		СтруктураДопДанных = Новый Структура;
		
		ИДТовара = СтрокаТоваров.Номенклатура.УникальныйИдентификатор();
		ИДХарактеристики = ?(ЗначениеЗаполнено(СтрокаТоваров.Характеристика), СтрокаТоваров.Характеристика.УникальныйИдентификатор(), "");
		ИДУпаковки = "";

		СтрокаТоваров.ИдТовараУКонтрагента = Строка(ИДТовара)+"#"+Строка(ИДХарактеристики)+"#"+Строка(ИДУпаковки);
		
		СтруктураОтбораСН = Новый Структура("КлючСвязи", СтрокаТоваров.КлючСвязи);
		СтрокиСН = ТаблицаСН.НайтиСтроки(СтруктураОтбораСН);
		Если СтрокиСН.Количество() > 0 Тогда
			СерийныеНомера = Новый Массив();
			Для Каждого СтрокаСН Из СтрокиСН Цикл
				СерийныеНомера.Добавить(СтрокаСН.СерийныйНомер);
			КонецЦикла;
			СтруктураДопДанных.Вставить("СерийныеНомера", СерийныеНомера);
			СтрокаТоваров.ДопДанныеПодписанные = СтруктураДопДанных;
		КонецЕсли;
		
	КонецЦикла;
	ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаТоваров, "ТаблицаТоваров");
	
	СтруктураДопДанных = Новый Структура;
	Если ЗначениеЗаполнено(РеквизитыШапки.АдресДоставки) Тогда
		СтруктураДопДанных.Вставить("АдресДоставки", УправлениеКонтактнойИнформацией.ПолучитьПредставлениеАдресаПоСтрока(РеквизитыШапки.АдресДоставки));
	КонецЕсли;
    ОбщегоНазначенияЭД.ДобавитьДопДанныеВДерево(ДеревоДанных, СтруктураДопДанных, Истина);

КонецПроцедуры

// Подготавливает данные для электронного документа типа Торг12 титул покупателя.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  ДеревоДанных - дерево значений, дерево данных заполнения электронного документа.
//
Процедура ЗаполнитьДанныеПоТОРГ12ПокупательФНС(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных) Экспорт
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаПолученияГруза", ТекущаяДатаСеанса());
	
КонецПроцедуры

// Подготавливает данные титула исполнителя для электронного документа типа Акт выполненных работ
// формата 5.01.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  ДеревоДанных - дерево значений, дерево данных заполнения электронного документа.
//
Процедура ЗаполнитьДанныеПоАкт501ИсполнительФНС(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных) Экспорт
	
	ДанныеДокумента = ПодготовитьДанныеАктаОбОказанииУслуг(СсылкаНаОбъект);
	РеквизитыШапки = ДанныеДокумента.РеквизитыШапки;
	ТаблицаУслуг = ДанныеДокумента.ТаблицаУслуг;
	
	ЗаполнитьДатуИНомер(ДеревоДанных, РеквизитыШапки, СсылкаНаОбъект, "НомерАкта", "ДатаАкта");

	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВидОперации", РеквизитыШапки.ВидОперации);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснования", РеквизитыШапки.ДокументОснование);
	ТаблицаОснование = Новый ТаблицаЗначений;
	ТаблицаОснование.Колонки.Добавить("ДокОснованиеНаименование");
	ТаблицаОснование.Колонки.Добавить("ДокОснованиеНомер");
	ТаблицаОснование.Колонки.Добавить("ДокОснованиеДата");
	СтрокаОснование = ТаблицаОснование.Добавить();
	СтрокаОснование.ДокОснованиеНаименование = РеквизитыШапки.ДоговорНаименование;
	СтрокаОснование.ДокОснованиеНомер = РеквизитыШапки.ДоговорНомер;
	СтрокаОснование.ДокОснованиеДата = РеквизитыШапки.ДоговорДата;
	ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаОснование, "Основание");
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументСделкиНаименование", РеквизитыШапки.ДоговорНаименование);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументСделкиНомер", РеквизитыШапки.ДоговорНомер);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументСделкиДата", РеквизитыШапки.ДоговорДата);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаКод", РеквизитыШапки.ВалютаДокумента.Код);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаНаименование", РеквизитыШапки.ВалютаДокумента.НаименованиеПолное);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "Заголовок",
		"Мы, нижеподписавшиеся,  представитель ИСПОЛНИТЕЛЯ, с одной стороны и  представитель ЗАКАЗЧИКА с другой стороны,
		|составили настоящий акт в том, что ИСПОЛНИТЕЛЬ выполнил, а ЗАКАЗЧИК принял следующие работы (услуги):");

	СведенияОИсполнителе = ПолучитьДанныеЮрФизЛица(РеквизитыШапки.Исполнитель);
	СведенияОЗаказчике   = ПолучитьДанныеЮрФизЛица(РеквизитыШапки.Заказчик);
	ЗаполнитьСведенияУчастникаОбмена(ДеревоДанных, СведенияОИсполнителе, "Юр", "Исполнитель", ,Истина);
	ЗаполнитьСведенияУчастникаОбмена(ДеревоДанных, СведенияОЗаказчике,   "Юр", "Заказчик", ,Истина);
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияПоВыполнениюУслуг.ДатаИсполнения",
		РеквизитыШапки.ДатаДокумента);
	
	// Не заполняем эти поля, потому что у пользователя нет возможности их изменить	
	//ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ОписаниеУслуги.НачалоРабот",
	//		РеквизитыШапки.ДатаДокумента);
	//ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ОписаниеУслуги.КонецРабот",
	//		РеквизитыШапки.ДатаДокумента);
			
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ОписаниеУслуги.СуммаБезНДСИтого",
			ТаблицаУслуг.Итог("СуммаБезНДС"));
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ОписаниеУслуги.СуммаНДСИтого",
			ТаблицаУслуг.Итог("СуммаНДС"));
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ОписаниеУслуги.СуммаСНДСИтого",
			ТаблицаУслуг.Итог("СуммаСНДС"));
	
	ТаблицаУслуг.Колонки.Добавить("ДопДанныеПодписанные");
	ТаблицаУслуг.Колонки.Добавить("ДопДанныеНеПодписанные");
	ТаблицаУслуг.Колонки.Добавить("ИдТовараУКонтрагента");
	Для Каждого СтрокаТоваров Из ТаблицаУслуг Цикл
		
		ИДТовара = СтрокаТоваров.Номенклатура.УникальныйИдентификатор();
		ИДХарактеристики = ?(ЗначениеЗаполнено(СтрокаТоваров.Характеристика), СтрокаТоваров.Характеристика.УникальныйИдентификатор(), "");
		ИДУпаковки = "";
		
		СтрокаТоваров.ИдТовараУКонтрагента = Строка(ИДТовара)+"#"+Строка(ИДХарактеристики)+"#"+Строка(ИДУпаковки);
	КонецЦикла;
	ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаУслуг, "ТаблицаУслуг");

КонецПроцедуры

// Подготавливает данные титула заказчика для электронного документа типа Акт выполненных работ
// формата 5.01.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  ДеревоДанных - дерево значений, дерево данных заполнения электронного документа.
//
Процедура ЗаполнитьДанныеПоАкт501ЗаказчикФНС(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных) Экспорт
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияПоВыполнениюУслуг.ДатаЗаказа",
		ТекущаяДатаСеанса());
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа Счет-Фактура.
//
// Параметры:
//  СсылкаНаОбъект - документСсылка - ссылка на объект информационной базы,
//  по которому необходимо создать электронный документ.
//  СтруктураЭД - структура - структура данных для формирования электронного документа.
//  ДеревоДанных - дерево значений, дерево данных заполнения электронного документа.
//
Процедура ЗаполнитьДанныеПоСчетуФактуреФНС(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных) Экспорт
	
	// Вызывается для с/ф выставленного: "На реализацию", "На аванс", "Исправление".
	
	ДанныеДляФормированияЭД = Неопределено;
	УчетнаяПолитика = Неопределено;
	
	СсылкаНаОбъект.ПолучитьОбъект().СобратьДанныеДляПечати(ДанныеДляФормированияЭД, УчетнаяПолитика);
	
	ВалютаДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаОбъект, "ВалютаДокумента");
	Если ВалютаДокумента <> глЗначениеПеременной("ВалютаРегламентированногоУчета") Тогда
		ОкруглитьЧисловыеПоля(ДанныеДляФормированияЭД);
	КонецЕсли;
		
	РеквизитыСФ = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(СсылкаНаОбъект, 
		"Номер, Дата, Исправление, НомерИсправления, НомерИсходногоДокумента, ДатаИсходногоДокумента, ВидСчетаФактуры");
	РеквизитыДоговора = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(СсылкаНаОбъект.ДоговорКонтрагента, 
		"Наименование, Номер, Дата");
	
	// Заполним реквизиты шапки
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументСделкиНаименование", РеквизитыДоговора.Наименование);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументСделкиНомер", РеквизитыДоговора.Номер);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументСделкиДата", РеквизитыДоговора.Дата);
	
	МассивДокументовОснований = Новый Массив;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СчетФактураВыданныйДокументыОснования.ДокументОснование КАК ДокументОснование
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|ГДЕ
	|	СчетФактураВыданныйДокументыОснования.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъект);
	МассивДокументовОснований = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ДокументОснование");
	Если ЗначениеЗаполнено(МассивДокументовОснований) Тогда
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснования", МассивДокументовОснований);
	КонецЕсли;

	НомерСФ = ?(РеквизитыСФ.Исправление, РеквизитыСФ.НомерИсходногоДокумента, ПолучитьПечатныйНомерДокумента(СсылкаНаОбъект));
	ДатаСФ  = ?(РеквизитыСФ.Исправление, РеквизитыСФ.ДатаИсходногоДокумента,  ДанныеДляФормированияЭД.Дата);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерСчетаФактуры", НомерСФ);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаСчетаФактуры",  ДатаСФ);
	
	Если РеквизитыСФ.Исправление Тогда
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправления", РеквизитыСФ.НомерИсправления);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсправления",  РеквизитыСФ.Дата);
	КонецЕсли;

	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаКод", 
		глЗначениеПеременной("ВалютаРегламентированногоУчета").Код);
		
	Если РеквизитыСФ.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАванс Тогда
		ВидСчетаФактуры = "Авансовый";
	Иначе
		ВидСчетаФактуры = "Реализация";
	КонецЕсли;
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВидСчетаФактуры", ВидСчетаФактуры);
	
	СведенияОПоставщике = ПолучитьДанныеЮрФизЛица(ДанныеДляФормированияЭД.Поставщик);
	СведенияОПокупателе = ПолучитьДанныеЮрФизЛица(ДанныеДляФормированияЭД.Покупатель);
	Если ВидСчетаФактуры = "Реализация" Тогда
		Если ТипЗнч(ДанныеДляФормированияЭД.Покупатель) = Тип("СправочникСсылка.Контрагенты") И ЗначениеЗаполнено(ДанныеДляФормированияЭД.Покупатель) Тогда
			РеквизитыПокупателя = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ДанныеДляФормированияЭД.Покупатель, "ГоловнойКонтрагент, ОбособленноеПодразделение");
			Если РеквизитыПокупателя.ОбособленноеПодразделение И ЗначениеЗаполнено(РеквизитыПокупателя.ГоловнойКонтрагент) Тогда
				КПП = СведенияОПокупателе.КПП;
				СведенияОПокупателе = ПолучитьДанныеЮрФизЛица(РеквизитыПокупателя.ГоловнойКонтрагент);
				СведенияОПокупателе.КПП = КПП;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	ЗаполнитьСведенияУчастникаОбмена(ДеревоДанных, СведенияОПоставщике, "Юр", "Продавец");
	ЗаполнитьСведенияУчастникаОбмена(ДеревоДанных, СведенияОПокупателе, "Юр", "Покупатель");
	
	ДанныеДляФормированияЭД.ТаблицаДатОплат.Колонки.НомерПлатежноРасчетногоДокумента.Имя = "НомерПРД";
	ДанныеДляФормированияЭД.ТаблицаДатОплат.Колонки.ДатаПлатежноРасчетногоДокумента.Имя  = "ДатаПРД";
	ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ДанныеДляФормированияЭД.ТаблицаДатОплат, "ПлатежноРасчетныеДокументы");
	
	// Заполним таблицу товаров
	
	ТолькоУслуги = Истина;
	
	КолонкиТЧ = ДанныеДляФормированияЭД.ТабличнаяЧасть.Колонки;
	КолонкиТЧ.ТоварНаименование.Имя      = "НаименованиеНоменклатуры";
	КолонкиТЧ.СтранаПроисхожденияКод.Имя = "КодСтраныПроисхождения";
	КолонкиТЧ.ПредставлениеГТД.Имя       = "НомерТаможеннойДекларации";
	КолонкиТЧ.Товар.Имя     			 = "Номенклатура";
	
	КолонкиТЧ.Добавить("ИдТовараУКонтрагента", ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(200));
	КолонкиТЧ.Добавить("СуммаБезНДС", 		   ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15, 2));
	КолонкиТЧ.Добавить("СуммаСНДС",            ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15, 2));
	КолонкиТЧ.Добавить("Акциза",               ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(18));
	КолонкиТЧ.Добавить("ТаможеннаяДекларация");
	
	ТД = Новый ТаблицаЗначений;
	ТД.Колонки.Добавить("КодСтраныПроисхождения",    ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(3) );
	ТД.Колонки.Добавить("НомерТаможеннойДекларации", ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(29));
	СтрокаТД = ТД.Добавить();
	
	НомераСтрокСОшибками = "";
	МаксимальнаяДлинаНомераГТД = 29;
	Для Каждого Строка Из ДанныеДляФормированияЭД.ТабличнаяЧасть Цикл
		
		Если ВидСчетаФактуры = "Авансовый" И 
			(Не ЗначениеЗаполнено(Строка.Номенклатура) ИЛИ ТипЗнч(Строка.Номенклатура) = Тип("Строка")) Тогда
			ТекстОшибки = "Для формирования электронного документа необходимо заполнить реквизит ""Номенклатура (обобщенное наименование)""";
			Прервать;
		КонецЕсли;
		
		Если ТолькоУслуги
			И ((ТипЗнч(Строка.Номенклатура) = Тип("СправочникСсылка.Номенклатура") И НЕ Строка.Номенклатура.Услуга)) Тогда
			ТолькоУслуги = Ложь;
		КонецЕсли;
		
		Строка.СуммаБезНДС               = ?(Строка.СуммаВключаетНДС, Строка.Сумма - Строка.СуммаНДС, Строка.Сумма);
		Строка.СуммаСНДС                 = ?(Строка.СуммаВключаетНДС, Строка.Сумма, Строка.Сумма + Строка.СуммаНДС);
		Строка.Акциза                    = "без акциза";
		
		Если Строка.Количество = 0 Тогда
			Строка.Цена = 0;
		Иначе
			Строка.Цена = Окр(Строка.СуммаБезНДС / Строка.Количество, 2);
		КонецЕсли;
		
		Строка.НомерТаможеннойДекларации = СокрЛП(Строка.НомерТаможеннойДекларации);
		Если СтрДлина(Строка.НомерТаможеннойДекларации) > МаксимальнаяДлинаНомераГТД Тогда
			НомераСтрокСОшибками = НомераСтрокСОшибками + Строка(Строка.НомерСтроки) + " ,"
		Иначе
			
			Если ЗначениеЗаполнено(Строка.КодСтраныПроисхождения) И ЗначениеЗаполнено(Строка.НомерТаможеннойДекларации) Тогда
				ЗаполнитьЗначенияСвойств(СтрокаТД, Строка);
				Строка.ТаможеннаяДекларация = ТД.Скопировать();
			КонецЕсли;
			
		КонецЕсли;

		ИДТовара = Строка.Номенклатура.УникальныйИдентификатор();
		ИДХарактеристики = ?(ЗначениеЗаполнено(Строка.Характеристика), Строка.Характеристика.УникальныйИдентификатор(), "");
		ИДУпаковки = "";
		Строка.ИдТовараУКонтрагента = Строка(ИДТовара)+"#"+Строка(ИДХарактеристики)+"#"+Строка(ИДУпаковки);

	КонецЦикла;
	
	Если ЗначениеЗаполнено(НомераСтрокСОшибками) Тогда
		НомераСтрокСОшибками = Лев(НомераСтрокСОшибками, СтрДлина(НомераСтрокСОшибками)-1);
		ШаблонОшибки = НСтр("ru = 'Длина номера ГТД в строках %1 превышает %2 символов.'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОшибки, 
			НомераСтрокСОшибками, Строка(МаксимальнаяДлинаНомераГТД));
	КонецЕсли;
	
	ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ДанныеДляФормированияЭД.ТабличнаяЧасть, "ТаблицаТоваров");
	
	// Заполним оставшиеся реквизиты шапки
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ТолькоУслуги", ТолькоУслуги);
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВсегоКОплате.СуммаБезНДСВсего", 
		ДанныеДляФормированияЭД.ТабличнаяЧасть.Итог("СуммаБезНДС"));
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВсегоКОплате.СуммаСНДСВсего", 
		ДанныеДляФормированияЭД.ТабличнаяЧасть.Итог("СуммаСНДС"));
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВсегоКОплате.СуммаНДСВсего", 
		ДанныеДляФормированияЭД.ТабличнаяЧасть.Итог("СуммаНДС"));
		
	Если НЕ ТолькоУслуги Тогда
			
		Если ЗначениеЗаполнено(ДанныеДляФормированияЭД.Грузоотправитель) И ДанныеДляФормированияЭД.Грузоотправитель <> "он же" Тогда
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОГрузоотправителе", "Грузоотправитель");
			СведенияОГрузоотправителе = ПолучитьДанныеЮрФизЛица(ДанныеДляФормированияЭД.Грузоотправитель);
			ЗаполнитьСведенияУчастникаСФ(ДеревоДанных, СведенияОГрузоотправителе, "СведенияОГрузоотправителе.Грузоотправитель", "Факт");
		Иначе
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОГрузоотправителе", "ОнЖе");
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОГрузоотправителе.ОнЖе", Истина);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДанныеДляФормированияЭД.Грузополучатель) Тогда
			СведенияОГрузополучателе = ПолучитьДанныеЮрФизЛица(ДанныеДляФормированияЭД.Грузополучатель);
			ЗаполнитьСведенияУчастникаСФ(ДеревоДанных, СведенияОГрузополучателе, "Грузополучатель", "Факт");
		КонецЕсли;
		
	Иначе
		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОГрузоотправителе", "ОнЖе");
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОГрузоотправителе.ОнЖе", Истина);
		
	КонецЕсли;
	
	ИДГК = ДанныеДляФормированияЭД.ИдентификаторГосКонтракта;
	Заголовок = УчетНДС.ТекстЗаголовкаИДГК(ДатаСФ);
	Если ИДГК <> Заголовок Тогда
		ДопДанные = Новый Структура("ИдентификаторГосКонтракта", СокрЛП(Прав(ИДГК, СтрДлина(ИДГК) - СтрДлина(Заголовок))));
		ОбщегоНазначенияЭД.ДобавитьДопДанныеВДерево(ДеревоДанных, ДопДанные, Истина);
	КонецЕсли;
		
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		СтрокаСОшибкой = ДеревоДанных.Строки.Добавить();
		СтрокаСОшибкой.ПолныйПуть = "ТекстОшибки";
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ТекстОшибки", ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа Корректировочный Счет-Фактура.
//
// Параметры:
//  СсылкаНаОбъект - документСсылка - ссылка на объект информационной базы,
//  по которому необходимо создать электронный документ.
//  СтруктураЭД - структура - структура данных для формирования электронного документа.
//  ДеревоДанных - дерево значений, дерево данных заполнения электронного документа.
//
Процедура ЗаполнитьДанныеПоКорректировочномуСчетуФактуреФНС(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных) Экспорт
	
	// Вызывается для с/ф выставленного: "Корректировочный".
	
	ДанныеДляФормированияЭД = Неопределено;
	УчетнаяПолитика = Неопределено;
	
	СсылкаНаОбъект.ПолучитьОбъект().СобратьДанныеДляПечати(ДанныеДляФормированияЭД, УчетнаяПолитика);
	
	ВалютаДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаОбъект, "ВалютаДокумента");
	Если ВалютаДокумента <> глЗначениеПеременной("ВалютаРегламентированногоУчета") Тогда
		ОкруглитьЧисловыеПоля(ДанныеДляФормированияЭД);
	КонецЕсли;
		
	РеквизитыСФ = Новый Структура("Номер, Дата,
								|Исправление, НомерИсправления,
								|НомерИсправляемогоКорректировочногоДокумента, ДатаИсправляемогоКорректировочногоДокумента,
								|НомерИсходногоДокумента, ДатаИсходногоДокумента,
								|УчитыватьИсправлениеИсходногоДокумента, НомерИсправленияИсходногоДокумента, ДатаИсправленияИсходногоДокумента");
	ЗапросРеквизитовОснований = Новый Запрос();
	ЗапросРеквизитовОснований.Параметры.Вставить("Ссылка", СсылкаНаОбъект.Ссылка);
	ЗапросРеквизитовОснований.Текст = 
	"ВЫБРАТЬ
	|	СчетФактураВыданныйДокументыОснования.НомерИсходногоДокумента КАК НомерИсходногоДокумента,
	|	СчетФактураВыданныйДокументыОснования.ДатаИсходногоДокумента КАК ДатаИсходногоДокумента,
	|	СчетФактураВыданныйДокументыОснования.УчитыватьИсправлениеИсходногоДокумента КАК УчитыватьИсправлениеИсходногоДокумента,
	|	СчетФактураВыданныйДокументыОснования.НомерИсправленияИсходногоДокумента КАК НомерИсправленияИсходногоДокумента,
	|	СчетФактураВыданныйДокументыОснования.ДатаИсправленияИсходногоДокумента КАК ДатаИсправленияИсходногоДокумента,
	|	СчетФактураВыданный.Номер КАК Номер,
	|	СчетФактураВыданный.Дата КАК Дата,
	|	СчетФактураВыданный.Исправление КАК Исправление,
	|	СчетФактураВыданный.НомерИсправления КАК НомерИсправления,
	|	СчетФактураВыданный.НомерИсправляемогоКорректировочногоДокумента КАК НомерИсправляемогоКорректировочногоДокумента,
	|	СчетФактураВыданный.ДатаИсправляемогоКорректировочногоДокумента КАК ДатаИсправляемогоКорректировочногоДокумента
	|ИЗ
	|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ПО СчетФактураВыданный.Ссылка = СчетФактураВыданныйДокументыОснования.Ссылка
	|			И (СчетФактураВыданныйДокументыОснования.НомерСтроки = 1)
	|ГДЕ
	|	СчетФактураВыданныйДокументыОснования.Ссылка = &Ссылка";
	Выборка = ЗапросРеквизитовОснований.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(РеквизитыСФ, Выборка);
	КонецЕсли;
		
	// Заполним реквизиты шапки
	
	МассивДокументовОснований = Новый Массив;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СчетФактураВыданныйДокументыОснования.ДокументОснование КАК ДокументОснование
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|ГДЕ
	|	СчетФактураВыданныйДокументыОснования.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъект);
	МассивДокументовОснований = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ДокументОснование");
	Если ЗначениеЗаполнено(МассивДокументовОснований) Тогда
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснования", МассивДокументовОснований);
	КонецЕсли;

	Если РеквизитыСФ.Исправление Тогда
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерКорСчетаФактуры", РеквизитыСФ.НомерИсправляемогоКорректировочногоДокумента);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаКорСчетаФактуры",  РеквизитыСФ.ДатаИсправляемогоКорректировочногоДокумента);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправленияКорСчетаФактуры", РеквизитыСФ.НомерИсправления);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсправленияКорСчетаФактуры",  РеквизитыСФ.Дата);
	Иначе
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерКорСчетаФактуры", ПолучитьПечатныйНомерДокумента(СсылкаНаОбъект));
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаКорСчетаФактуры",  РеквизитыСФ.Дата);
	КонецЕсли;
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерСчетаФактуры", РеквизитыСФ.НомерИсходногоДокумента);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаСчетаФактуры",  РеквизитыСФ.ДатаИсходногоДокумента);
	
	Если РеквизитыСФ.УчитыватьИсправлениеИсходногоДокумента Тогда
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправления", РеквизитыСФ.НомерИсправленияИсходногоДокумента);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсправления",  РеквизитыСФ.ДатаИсправленияИсходногоДокумента);
	КонецЕсли;
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаКод", 
		глЗначениеПеременной("ВалютаРегламентированногоУчета").Код);
		
	СведенияОПоставщике = ПолучитьДанныеЮрФизЛица(ДанныеДляФормированияЭД.Поставщик);
	СведенияОПокупателе = ПолучитьДанныеЮрФизЛица(ДанныеДляФормированияЭД.Покупатель);
	Если ТипЗнч(ДанныеДляФормированияЭД.Покупатель) = Тип("СправочникСсылка.Контрагенты") И ЗначениеЗаполнено(ДанныеДляФормированияЭД.Покупатель) Тогда
		РеквизитыПокупателя = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ДанныеДляФормированияЭД.Покупатель, "ГоловнойКонтрагент, ОбособленноеПодразделение");
		Если РеквизитыПокупателя.ОбособленноеПодразделение И ЗначениеЗаполнено(РеквизитыПокупателя.ГоловнойКонтрагент) Тогда
			КПП = СведенияОПокупателе.КПП;
			СведенияОПокупателе = ПолучитьДанныеЮрФизЛица(РеквизитыПокупателя.ГоловнойКонтрагент);
			СведенияОПокупателе.КПП = КПП;
		КонецЕсли;
	КонецЕсли;
	ЗаполнитьСведенияУчастникаОбмена(ДеревоДанных, СведенияОПоставщике, "Юр", "Продавец");
	ЗаполнитьСведенияУчастникаОбмена(ДеревоДанных, СведенияОПокупателе, "Юр", "Покупатель");
	
	// Заполним таблицу товаров
	
	ТолькоУслуги = Истина;
	
	КолонкиТЧ = ДанныеДляФормированияЭД.ТабличнаяЧасть.Колонки;
	
	КолонкиТЧ.НаименованиеТовара.Имя = "НаименованиеНоменклатуры";
	КолонкиТЧ.СтавкаНДС.Имя     	 = "Ставка";
	
	КолонкиТЧ.Добавить("ИдТовараУКонтрагента",  ОбщегоНазначения.ПолучитьОписаниеТиповстроки(200));
	КолонкиТЧ.Добавить("ЕдиницаИзмеренияКодДо", ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(3));
	
	КолонкиДляПереименования = Новый Структура ("Количество,Цена,СуммаНДС,СтоимостьБезНДС,СтоимостьСНДС",
		"Количество","Цена","СуммаНДС","СуммаБезНДС","СуммаСНДС");
	Для Каждого ИмяКолонки Из КолонкиДляПереименования Цикл
		КолонкиТЧ[ИмяКолонки.Ключ+"ДоИзменения"].Имя    = ИмяКолонки.Значение+"До";
		КолонкиТЧ[ИмяКолонки.Ключ+"ПослеИзменения"].Имя = ИмяКолонки.Значение;
	КонецЦикла;
	
	КолонкиДляДобавления = "Акциза,СтавкаНДС";
	ИменаКолонокДляДобавления = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(КолонкиДляДобавления);
	Для Каждого ИмяКолонки Из ИменаКолонокДляДобавления Цикл
		КолонкиТЧ.Добавить(ИмяКолонки);
		КолонкиТЧ.Добавить(ИмяКолонки+"До");
	КонецЦикла;
	
	Для Каждого Строка Из ДанныеДляФормированияЭД.ТабличнаяЧасть Цикл
		
		Если ТолькоУслуги
			И ((ТипЗнч(Строка.Номенклатура) = Тип("СправочникСсылка.Номенклатура") И НЕ Строка.Номенклатура.Услуга)) Тогда
			ТолькоУслуги = Ложь;
		КонецЕсли;
		
		Строка.Акциза                    = "без акциза";
		Строка.АкцизаДо                  = "без акциза";
		
		Если Строка.Количество = 0 Тогда
			Строка.Цена = 0;
		Иначе
			Строка.Цена = Окр(Строка.СуммаБезНДС / Строка.Количество, 2);
		КонецЕсли;
		
		Строка.СтавкаНДС = Строка.Ставка;
		Строка.СтавкаНДСДо = Строка.Ставка;
		
		Строка.ЕдиницаИзмеренияКодДо = Строка.ЕдиницаИзмеренияКод;
		
		ИДТовара = Строка.Номенклатура.УникальныйИдентификатор();
		ИДХарактеристики = ?(ЗначениеЗаполнено(Строка.Характеристика), Строка.Характеристика.УникальныйИдентификатор(), "");
		ИДУпаковки = "";
		Строка.ИдТовараУКонтрагента = Строка(ИДТовара)+"#"+Строка(ИДХарактеристики)+"#"+Строка(ИДУпаковки);

	КонецЦикла;
	
	ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ДанныеДляФормированияЭД.ТабличнаяЧасть, "ТаблицаТоваров");
	
	// Заполним оставшиеся реквизиты шапки
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ТолькоУслуги", ТолькоУслуги);

	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВсегоКОплате.СуммаСНДСВсего", 
		ДанныеДляФормированияЭД.ТабличнаяЧасть.Итог("СуммаСНДС"));
		
	ИДГК = ДанныеДляФормированияЭД.ИдентификаторГосКонтракта;
	Заголовок = УчетНДС.ТекстЗаголовкаИДГК(РеквизитыСФ.Дата);
	Если ИДГК <> Заголовок Тогда
		ДопДанные = Новый Структура("ИдентификаторГосКонтракта", СокрЛП(Прав(ИДГК, СтрДлина(ИДГК) - СтрДлина(Заголовок))));
		ОбщегоНазначенияЭД.ДобавитьДопДанныеВДерево(ДеревоДанных, ДопДанные, Истина);
	КонецЕсли;
	
КонецПроцедуры

// Заполняет данные для электронного документа типа СоглашениеОбИзмененииСтоимостиОтправитель.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  ДеревоДанных - дерево значений, дерево данных заполнения электронного документа.
//
Процедура ЗаполнитьДанныеПоКорректировочномуДокументу(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных) Экспорт
	
	ДанныеДокумента = ПолучитьДанныеСогласованногоИзменения(СсылкаНаОбъект);
	РеквизитыШапки  = ДанныеДокумента.РеквизитыШапки;
	
	ИсходныйИсправляемыйДокумент = УчетНДС.ПолучитьИсправляемыйДокументРеализации(РеквизитыШапки.ДокументОснование, Истина);
	ИсходныйИсправляемыйДокументМетаданные = ИсходныйИсправляемыйДокумент.Метаданные();
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерТоварнойНакладной", ПолучитьПечатныйНомерДокумента(СсылкаНаОбъект));
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаТоварнойНакладной",  РеквизитыШапки.ДатаДокумента);

	// Выводим общие реквизиты шапки
	СведенияОПоставщике       = ПолучитьДанныеЮрФизЛица(РеквизитыШапки.Организация);
	СведенияОПокупателе       = ПолучитьДанныеЮрФизЛица(РеквизитыШапки.Контрагент);
	СведенияОГрузополучателе  = ПолучитьДанныеЮрФизЛица(РеквизитыШапки.Грузополучатель);
	СведенияОГрузоотправителе = ПолучитьДанныеЮрФизЛица(РеквизитыШапки.Грузоотправитель);
	
	ЗаполнитьСведенияУчастникаОбмена(ДеревоДанных, СведенияОПоставщике,       	    , "Поставщик");
	ЗаполнитьСведенияУчастникаОбмена(ДеревоДанных, СведенияОГрузоотправителе, "Факт", "Грузоотправитель");
	
	ЗаполнитьСведенияУчастникаОбмена(ДеревоДанных, СведенияОПокупателе,       		, "Плательщик");
	ЗаполнитьСведенияУчастникаОбмена(ДеревоДанных, СведенияОГрузополучателе,  "Факт", "Грузополучатель");

	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокОснованиеНаименование",  РеквизитыШапки.ДоговорНаименование);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокОснованиеНомер", 		РеквизитыШапки.ДоговорНомер);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокОснованиеДата", 			РеквизитыШапки.ДоговорДата);
	
	ДокументыОснования = Новый Массив;
	ДокументыОснования.Добавить(РеквизитыШапки.ДокументОснование);
	СФоснования = УчетНДС.НайтиПодчиненныйСчетФактуру(РеквизитыШапки.ДокументОснование);
	Если ЗначениеЗаполнено(СФоснования) Тогда
		ДокументыОснования.Добавить(СФоснования);
	КонецЕсли;

	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснования",        ДокументыОснования);

	СтруктураДопДанных = Новый Структура;
	СтруктураДопДанных.Вставить("ВалютаКод",   РеквизитыШапки.ВалютаКод);
	СтруктураДопДанных.Вставить("ВидОперации", РеквизитыШапки.ВидОперации);
	ОбщегоНазначенияЭД.ДобавитьДопДанныеВДерево(ДеревоДанных, СтруктураДопДанных, Истина);

	// Табличные части {
	ТаблицаТоваров = ДанныеДокумента.ТаблицаТоваров.Скопировать();
	ТаблицаТоваров.Колонки.Добавить("ИдТовараУКонтрагента");
	ТаблицаТоваров.Колонки.Добавить("ДопДанныеПодписанные");
	ТаблицаТоваров.Колонки.Добавить("ДопДанныеНеПодписанные");
	ОбработатьТаблицуТоваров(ТаблицаТоваров, Истина, "ИдТовараУКонтрагента");
	ИтоговыеСуммы = СтруктураИтоговыеСуммы(ТаблицаТоваров, СтруктураЭД.ВидЭД);
	ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаТоваров, "ТаблицаТоваров");
	// Табличные части {
	
	// Сведения по отпуску груза {
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
								"СведенияПоОтпускуГруза.ОтпущеноНаСумму",
								ИтоговыеСуммы.СуммаСНДС);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
								"СведенияПоОтпускуГруза.ОтпущеноНаСуммуПрописью",
								СуммаПрописью(ИтоговыеСуммы.СуммаСНДС));
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
								"СведенияПоОтпускуГруза.ДатаОтпуска",
								РеквизитыШапки.ДатаДокумента);
	// Сведения по отпуску груза }

	// Всего по накладной {
	Для Каждого Итог Из ИтоговыеСуммы Цикл
		Если Итог.Ключ <> "КоличествоПорядковыхНомеровЗаписей" Тогда
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВсегоПоНакладной."+Итог.Ключ, Итог.Значение);
		КонецЕсли;
	КонецЦикла;
	// Всего по накладной }
	
	// Общие cведения о товарной накладной {
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
								"ОбщиеСведенияОТоварнойНакладной.ВсегоМест",
								ИтоговыеСуммы.КоличествоМест);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
								"ОбщиеСведенияОТоварнойНакладной.ВсегоМестПрописью",
								ЧислоПрописью(ИтоговыеСуммы.КоличествоМест, ,",,,,,,,,0"));
								
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
								"ОбщиеСведенияОТоварнойНакладной.МассаГрузаНетто",
								ИтоговыеСуммы.МассаНетто);
								
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
								"ОбщиеСведенияОТоварнойНакладной.МассаГрузаБрутто",
								ИтоговыеСуммы.МассаБрутто);
	// Общие cведения о товарной накладной }
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа Торг12 титул покупателя.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  СтруктураПараметров - структура, параметры для заполнения.
//
Процедура ЗаполнитьДанныеПоКорректировочномуДокументуПолучатель(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных) Экспорт
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаПолученияГруза", ТекущаяДатаСеанса());
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа ПередачаТоваровПродавец.
//
// Параметры:
//  СсылкаНаЭД   - Ссылка на ЭД, по которому необходимо сформировать электронный документ,
//  СтруктураЭД  - Структура, структура данных для формирования электронного документа.
//  ДеревоДанных - Дерево значений, дерево данных заполнения электронного документа.
//
Процедура ЗаполнитьДанныеПередачаТоваровПродавец(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных) Экспорт
	
	ЗаполнитьДанныеПоТорг12ПродавецФНС(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных)
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа ПередачаТоваровПродавец.
//
// Параметры:
//  СсылкаНаЭД   - Ссылка на ЭД, по которому необходимо сформировать электронный документ,
//  СтруктураЭД  - Структура, структура данных для формирования электронного документа.
//  ДеревоДанных - Дерево значений, дерево данных заполнения электронного документа.
//
Процедура ЗаполнитьДанныеПередачаРаботИсполнитель(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных) Экспорт
	
	ЗаполнитьДанныеПоАкт501ИсполнительФНС(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных)
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Акт о расхождениях ФНС

// Подготавливает данные для электронного документа вида "Акт о расхождениях".
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на объект информационной базы, по которому необходимо 
//                                    создать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//  Отказ - Булево - если нужно отказаться от создания электронного документа, необходимо установить значение в Истина.
//                   После этого дальнейшие действия по формированию документа производиться не будут, поэтому
//                   нужно сформировать сообщения пользователю при необходимости самостоятельно.
//
Процедура ЗаполнитьДанныеДляАктОРасхождениях(Знач СсылкаНаОбъект, Знач СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа Акт сверки взаиморасчетов (информация получателя).
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на объект информационной базы, по которому необходимо 
//                                    создать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//
Процедура ЗаполнитьАктОРасхожденияхПоДаннымДопСведений_ФНС_2019(ДеревоДанных, СсылкаНаВладельца, Записывать = Истина, СпособОбработки = Неопределено, ОписаниеОшибки = "") Экспорт
	
	
	
КонецПроцедуры

// Подготавливает данные для электронного документа вида "Акт о расхождениях".
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на объект информационной базы, по которому необходимо 
//                                    создать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//  Отказ - Булево - если нужно отказаться от создания электронного документа, необходимо установить значение в Истина.
//                   После этого дальнейшие действия по формированию документа производиться не будут, поэтому
//                   нужно сформировать сообщения пользователю при необходимости самостоятельно.
//
Процедура ЗаполнитьДанныеАктОРасхождениях_ДополнительныеСведения(Знач СсылкаНаОбъект, Знач СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	
	
КонецПроцедуры


// Работа со деревом данных CML

// Заполняет данные для электронного документа типа Акт на передачу прав.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  ДеревоДанных - дерево значений, дерево данных заполнения электронного документа.
//
Процедура ЗаполнитьДанныеПоАктуНаПередачуПрав(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных) Экспорт
	
	Данные = Документы.РеализацияТоваровУслуг.ПолучитьДанныеДляПечатиАктаНаПередачуПрав(СсылкаНаОбъект, глЗначениеПеременной("ВалютаРегламентированногоУчета"));
	
	Сч = Данные.Строки.Количество() - 1;
	Пока Сч >= 0 Цикл
		СтрокаТЧ = Данные.Строки[Сч];
		Если СтрокаТЧ.Количество = 0 ИЛИ СтрокаТЧ.Сумма = 0 Тогда
			Данные.Строки.Удалить(Сч);
		КонецЕсли;
		Сч = Сч - 1;
	КонецЦикла;
	
	ЗаполнитьДатуИНомер(ДеревоДанных, Данные, СсылкаНаОбъект, "Номер", "Дата");

	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВидОперации", Данные.ВидОперации);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснования", Данные.ДокументыОснования);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "Валюта", Данные.ВалютаДанных.Код);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "Курс", "1");
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "Сумма", Данные.Строки.Итог("СуммаСНДС"));
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "КодПартнера", Данные.КодПартнера);
	Если ЗначениеЗаполнено(Данные.ОтсрочкаПлатежаДней) И ЗначениеЗаполнено(Данные.ПроцентШтрафаЗаДеньПросрочки) Тогда
		ШаблонУсловий = "Внимание! При отгрузке на условиях отсрочки платежа, срок отсрочки составляет %1 календарных дней от даты накладной.
		| В случае несвоевременной оплаты может начисляться штраф в размере %2% за каждый календарный день просрочки.";
		ОсобыеУсловия = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонУсловий, Строка(Данные.ОтсрочкаПлатежаДней),
			Строка(Данные.ПроцентШтрафаЗаДеньПросрочки));
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ОсобыеУсловия", ОсобыеУсловия);
	КонецЕсли;
	
	ЗаполнитьСведенияУчастникаОбмена(ДеревоДанных, Данные.СведенияОЛицензиаре,  "Факт", "Лицензиар",  "Произвольный");
	
	ЗаполнитьСведенияУчастникаОбмена(ДеревоДанных, Данные.СведенияОЛицензиате,  "Факт", "Лицензиат",  "Произвольный");
	
	ЗаполнитьСведенияУчастникаОбмена(ДеревоДанных, Данные.СведенияОПлательщике, "Юр",   "Плательщик", "Произвольный");
	
	ИмяКолонкиИД = "ИдТовараУКонтрагента";
	КолонкаИД = Данные.Строки.Колонки.Найти(ИмяКолонкиИД);
	Если КолонкаИД = Неопределено Тогда
		Данные.Строки.Колонки.Добавить(ИмяКолонкиИД);
	КонецЕсли;
	ОбработатьТаблицуТоваров(Данные.Строки, Ложь, ИмяКолонкиИД);
	
	Данные.Строки.Колонки.Добавить("Свойства");
	тзСвойства = Новый ТаблицаЗначений;
	тзСвойства.Колонки.Добавить("Наименование");
	тзСвойства.Колонки.Добавить("Значение");
	Для Каждого СтрокаДанных Из Данные.Строки Цикл
		тзПараметры = ЭлектронныеДокументыВнутренний.ПолучитьПараметрыДокументовОснований(СтрокаДанных.ДокументОснование);
		Если тзПараметры.Количество() = 0 Тогда
			// ЭД не сформирован
			Продолжить;
		КонецЕсли;
		тзСвойства.Очистить();
		СтрокаТЗ = тзСвойства.Добавить();
		СтрокаТЗ.Наименование = "ИДЭД";
		СтрокаТЗ.Значение = тзПараметры[0].Наименование;
		СтрокаТЗ = тзСвойства.Добавить();
		СтрокаТЗ.Наименование = "ВидЭД";
		СтрокаТЗ.Значение = тзПараметры[0].ВидЭД;
		СтрокаТЗ = тзСвойства.Добавить();
		СтрокаТЗ.Наименование = "НомерДокументаОтправителя";
		СтрокаТЗ.Значение = тзПараметры[0].НомерДокументаОтправителя;
		СтрокаТЗ = тзСвойства.Добавить();
		СтрокаТЗ.Наименование = "ДатаДокументаОтправителя";
		СтрокаТЗ.Значение = тзПараметры[0].ДатаДокументаОтправителя;
		СтрокаДанных.Свойства = тзСвойства.Скопировать();
	КонецЦикла;
	ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, Данные.Строки, "Товары");
	
	СтруктураЭД.Вставить("СуммаДокумента", Данные.Строки.Итог("СуммаСНДС"));
КонецПроцедуры

// Подготавливает данные для электронного документа типа Реквизиты организации формата CML 2.
//
// Параметры: 
// СсылкаНаОбъект - СправочникСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
// СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  ДеревоДокумента - дерево значений - дерево значений, соответствующее макету РеквизитыОрганизации обработки ЭлектронныеДокументы.
//
Процедура ЗаполнитьДанныеРеквизитыОрганизации(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных) Экспорт
	
	СведенияОбОрганизации = ПолучитьДанныеЮрФизЛица(СсылкаНаОбъект);
	
	ЗаполнитьСведенияУчастникаОбмена(ДеревоДанных, СведенияОбОрганизации, "Юр", "Организация");
	
	Руководитель = ОтветственноеЛицоОрганизации(ТекущаяДатаСеанса(), СсылкаНаОбъект, 
		Перечисления.ОтветственныеЛицаОрганизации.Руководитель);
	
	Если ТипЗнч(Руководитель.СтруктураФИО) = Тип("Структура") Тогда
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "Организация.Руководитель.Фамилия", 
			Руководитель.СтруктураФИО.Фамилия);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "Организация.Руководитель.Имя", 
			Руководитель.СтруктураФИО.Имя);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "Организация.Руководитель.Отчество", 
			Руководитель.СтруктураФИО.Отчество);
	КонецЕсли;
		
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "Организация.Руководитель.Должность", Руководитель.Должность);
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаФормирования", ТекущаяДатаСеанса());
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "Наименование", "Реквизиты "+СведенияОбОрганизации.ОфициальноеНаименование);

КонецПроцедуры

// Подготавливает данные для электронного документа типа КаталогТоваров.
//
// Параметры:
//  Организация - СправочникСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  ТоварыКаталога - Массив, список товаров для заполнения каталога.
//  ДеревоДанных - дерево значений, дерево данных заполнения электронного документа.
//
Процедура ЗаполнитьДанныеПоКаталогуТоваровCML(Организация, ТоварыКаталога, ДеревоДанных) Экспорт
	
	СведенияОВладельце = ПолучитьДанныеЮрФизЛица(Организация);
	ЗаполнитьСведенияУчастникаОбмена(ДеревоДанных, СведенияОВладельце, "Факт", "Владелец", "Произвольный");
	
	ТаблицаТоваров = ТоварыКаталога.СкопироватьКолонки();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТоварыКаталога.Номенклатура,
	|	ТоварыКаталога.ХарактеристикаНоменклатуры,
	|	ТоварыКаталога.ЕдиницаИзмерения
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ
	|	&ТоварыКаталога КАК ТоварыКаталога
	|;
	|
	|//////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Номенклатура.Артикул								 				КАК Артикул,
	|	ВЫРАЗИТЬ(ТаблицаТоваров.Номенклатура.НаименованиеПолное КАК Строка(255))		КАК Наименование,
	|	ТаблицаТоваров.ХарактеристикаНоменклатуры.Наименование 							КАК НаименованиеХарактеристики,
	|	ТаблицаТоваров.Номенклатура														КАК Номенклатура,
	|	ТаблицаТоваров.ХарактеристикаНоменклатуры 										КАК Характеристика,
	|	ТаблицаТоваров.Номенклатура.БазоваяЕдиницаИзмерения 							КАК БазоваяЕдиница,
	|	ТаблицаТоваров.Номенклатура.БазоваяЕдиницаИзмерения.Код 						КАК БазоваяЕдиницаКод,
	|	ТаблицаТоваров.Номенклатура.БазоваяЕдиницаИзмерения.Наименование 				КАК БазоваяЕдиницаНаименование,
	|	ТаблицаТоваров.Номенклатура.БазоваяЕдиницаИзмерения.НаименованиеПолное 			КАК БазоваяЕдиницаНаименованиеПолное,
	|	ТаблицаТоваров.Номенклатура.БазоваяЕдиницаИзмерения.МеждународноеСокращение		КАК БазоваяЕдиницаМеждународноеСокращение,
	|	ТаблицаТоваров.ЕдиницаИзмерения.Код 											КАК УпаковкаКод,
	|	ТаблицаТоваров.ЕдиницаИзмерения.Наименование 									КАК УпаковкаНаименование,
	|	ТаблицаТоваров.ЕдиницаИзмерения.Код												КАК УпаковкаПоОКЕИ
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров";
	
	Запрос.УстановитьПараметр("ТоварыКаталога", ТоварыКаталога);
	Результат = Запрос.Выполнить().Выгрузить();
	ИмяКолонкиИД = "ИдТовараУКонтрагента";
	Результат.Колонки.Добавить(ИмяКолонкиИД);
	ОбработатьТаблицуТоваров(Результат, Ложь, ИмяКолонкиИД);
	
	ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, Результат, "Товары");
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа ПрайсЛист формата CML 2.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  ДеревоДокумента - Дерево значений - Дерево значений, соответствующее макету ПрайсЛист обработки ЭлектронныеДокументы.
//
Процедура ЗаполнитьДанныеПоПрайсЛисту(СсылкаНаОбъект, СтруктураЭД, ДеревоДокумента) Экспорт
	
	// не используется
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа Счет формата CML 2.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  Дерево документа - дерево значений - дерево значений, соответствующее макету СчетНаОплату обработки ЭлектронныеДокументы.
//
Процедура ЗаполнитьДанныеПоСчету(СсылкаНаОбъект, СтруктураЭД, ДеревоДокумента) Экспорт
	
	РеквизитыДокумента = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(СсылкаНаОбъект, "Дата, Номер, 
		| Организация, Контрагент, СуммаВключаетНДС, ВалютаДокумента, КурсВзаиморасчетов, СтруктурнаяЕдиница, ДатаОплаты");
	
	СтрокаВыборкиПоляСодержания    = ОбработкаТабличныхЧастей.ПолучитьЧастьЗапросаДляВыбораСодержания("СчетНаОплатуПокупателю");

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СчетНаОплатуПокупателю.Номенклатура.Код                                        КАК КодТовара,
	|	СчетНаОплатуПокупателю.Номенклатура.Артикул			                           КАК Артикул,
	|	ВЫРАЗИТЬ(СчетНаОплатуПокупателю.Номенклатура.НаименованиеПолное КАК СТРОКА(255))КАК Наименование,
	|	СчетНаОплатуПокупателю.Номенклатура                                            КАК Номенклатура,
	|	СчетНаОплатуПокупателю.ХарактеристикаНоменклатуры                              КАК Характеристика,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения                    КАК БазоваяЕдиница,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения.Код                КАК БазоваяЕдиницаКод,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения.Наименование       КАК БазоваяЕдиницаНаименование,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения.НаименованиеПолное КАК БазоваяЕдиницаНаименованиеПолное,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения.МеждународноеСокращение КАК БазоваяЕдиницаМеждународноеСокращение,
	|	СчетНаОплатуПокупателю.ЕдиницаИзмерения.ЕдиницаПоКлассификатору                КАК ЕдиницаИзмерения,
	|	СчетНаОплатуПокупателю.ЕдиницаИзмерения.ЕдиницаПоКлассификатору                КАК Упаковка,
	|	СчетНаОплатуПокупателю.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код            КАК ЕдиницаИзмеренияКодПоОКЕИ,
	|	СчетНаОплатуПокупателю.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Наименование   КАК ЕдиницаИзмеренияНаименование,
	|	СчетНаОплатуПокупателю.ЕдиницаИзмерения.Коэффициент					           КАК ЕдиницаИзмеренияКоэффициент,
	|	СчетНаОплатуПокупателю.Цена,
	|	ВЫБОР
	|		КОГДА СчетНаОплатуПокупателю.Ссылка.СуммаВключаетНДС
	|			ТОГДА СчетНаОплатуПокупателю.Сумма - СчетНаОплатуПокупателю.СуммаНДС
	|		ИНАЧЕ СчетНаОплатуПокупателю.Сумма
	|	КОНЕЦ КАК СуммаБезНДС,
	|	ВЫБОР
	|		КОГДА СчетНаОплатуПокупателю.Ссылка.СуммаВключаетНДС
	|			ТОГДА СчетНаОплатуПокупателю.Сумма
	|		ИНАЧЕ СчетНаОплатуПокупателю.Сумма + СчетНаОплатуПокупателю.СуммаНДС
	|	КОНЕЦ КАК СуммаСНДС,
	|	СчетНаОплатуПокупателю.Сумма КАК Сумма,
	|	СчетНаОплатуПокупателю.СтавкаНДС,
	|	СчетНаОплатуПокупателю.СуммаНДС,
	|	СчетНаОплатуПокупателю.Количество,
	|	СчетНаОплатуПокупателю.Ссылка.СуммаВключаетНДС КАК НДСУчтеноВСумме
	|ИЗ
	|	Документ.СчетНаОплатуПокупателю.Товары КАК СчетНаОплатуПокупателю
	|ГДЕ
	|	СчетНаОплатуПокупателю.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетНаОплатуПокупателю.Номенклатура.Код,
	|	СчетНаОплатуПокупателю.Номенклатура.Артикул,
	|	ВЫРАЗИТЬ(" + СтрокаВыборкиПоляСодержания + " КАК Строка(255)),
	|	СчетНаОплатуПокупателю.Номенклатура,
	|	"""",
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения.Код,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения.Наименование,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения.НаименованиеПолное,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения.МеждународноеСокращение,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения.Код,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения.Наименование,
	|   1,
	|	СчетНаОплатуПокупателю.Цена,
	|	ВЫБОР
	|		КОГДА СчетНаОплатуПокупателю.Ссылка.СуммаВключаетНДС
	|			ТОГДА СчетНаОплатуПокупателю.Сумма - СчетНаОплатуПокупателю.СуммаНДС
	|		ИНАЧЕ СчетНаОплатуПокупателю.Сумма
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА СчетНаОплатуПокупателю.Ссылка.СуммаВключаетНДС
	|			ТОГДА СчетНаОплатуПокупателю.Сумма
	|		ИНАЧЕ СчетНаОплатуПокупателю.Сумма + СчетНаОплатуПокупателю.СуммаНДС
	|	КОНЕЦ,
	|	СчетНаОплатуПокупателю.Сумма,
	|	СчетНаОплатуПокупателю.СтавкаНДС,
	|	СчетНаОплатуПокупателю.СуммаНДС,
	|	СчетНаОплатуПокупателю.Количество,
	|	СчетНаОплатуПокупателю.Ссылка.СуммаВключаетНДС
	|ИЗ
	|	Документ.СчетНаОплатуПокупателю.Услуги КАК СчетНаОплатуПокупателю
	|ГДЕ
	|	СчетНаОплатуПокупателю.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъект);
	ТаблицаТоваров = Запрос.Выполнить().Выгрузить();
	ИмяКолонкиИД = "ИдТовараУКонтрагента";
	ТаблицаТоваров.Колонки.Добавить(ИмяКолонкиИД);
	ОбработатьТаблицуТоваров(ТаблицаТоваров, Ложь, ИмяКолонкиИД);
	
	// Заполняем дерево
	СведенияОПродавце   = ПолучитьДанныеЮрФизЛица(РеквизитыДокумента.Организация);
	СведенияОПокупателе = ПолучитьДанныеЮрФизЛица(РеквизитыДокумента.Контрагент);
	ЗаполнитьСведенияУчастникаОбмена(ДеревоДокумента, СведенияОПродавце,   "Юр", "Продавец");
	ЗаполнитьСведенияУчастникаОбмена(ДеревоДокумента, СведенияОПокупателе, "Юр", "Покупатель");
	
	СуммаДокумента = ТаблицаТоваров.Итог("Сумма");
	Если Не РеквизитыДокумента.СуммаВключаетНДС Тогда
		СуммаДокумента  = СуммаДокумента + ТаблицаТоваров.Итог("СуммаНДС");
	КонецЕсли;
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "Валюта", РеквизитыДокумента.ВалютаДокумента.Код);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "Курс", РеквизитыДокумента.КурсВзаиморасчетов);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "Сумма", СуммаДокумента);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "СрокПлатежа", РеквизитыДокумента.ДатаОплаты);
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогоПоДокументу.Сумма", 
		СуммаДокумента);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогоПоДокументу.СуммаНДС", 
		ТаблицаТоваров.Итог("СуммаНДС"));
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогоПоДокументу.ЦенаВключаетНДС", 
		РеквизитыДокумента.СуммаВключаетНДС);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогоПоДокументу.СуммаСкидки", 0);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогоПоДокументу.СуммаБезСкидки", 
		СуммаДокумента);

	ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДокумента, ТаблицаТоваров, "Товары");
	
	Если ЗначениеЗаполнено(РеквизитыДокумента.СтруктурнаяЕдиница)
		И ТипЗнч(РеквизитыДокумента.СтруктурнаяЕдиница) = Тип("СправочникСсылка.БанковскиеСчета") Тогда
		
		РасчетныйСчет  = РеквизитыДокумента.СтруктурнаяЕдиница;
		РеквизитыБанка = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(РасчетныйСчет.Банк, 
			"Наименование, КоррСчет, Код");
			
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РасчетныйСчет.НомерСчета", РасчетныйСчет.НомерСчета);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РасчетныйСчет.Банк.БИК", РеквизитыБанка.Код);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РасчетныйСчет.Банк.СчетКорреспондентский", РеквизитыБанка.КоррСчет);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РасчетныйСчет.Банк.Наименование", РеквизитыБанка.Наименование);
		
		Если ЗначениеЗаполнено(РасчетныйСчет.БанкДляРасчетов) Тогда
			
			РеквизитыБанкаДляРасчетов = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(РасчетныйСчет.БанкДляРасчетов, 
				"Наименование, КоррСчет, Код");
			
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РасчетныйСчет.БанкКорреспондент.БИК", РеквизитыБанкаДляРасчетов.Код);
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РасчетныйСчет.БанкКорреспондент.СчетКорреспондентский", РеквизитыБанкаДляРасчетов.КоррСчет);
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РасчетныйСчет.БанкКорреспондент.Наименование", РеквизитыБанкаДляРасчетов.Наименование);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ИтоговаяСтрока = НСтр("ru='Всего наименований %Количество%, на сумму %Сумма%'");
	ИтоговаяСтрока = СтрЗаменить(ИтоговаяСтрока, "%Количество%", ТаблицаТоваров.Количество());
	ИтоговаяСтрока = СтрЗаменить(ИтоговаяСтрока, "%Сумма%",		 ОбщегоНазначения.ФорматСумм(СсылкаНаОбъект.СуммаДокумента, СсылкаНаОбъект.ВалютаДокумента));
	СуммаПрописью  = ОбщегоНазначения.СформироватьСуммуПрописью(СсылкаНаОбъект.СуммаДокумента, СсылкаНаОбъект.ВалютаДокумента);
	ИтоговаяСтрока = ИтоговаяСтрока + Символы.ПС + СуммаПрописью;
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогиПрописью", ИтоговаяСтрока);
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа ЗаказТоваров формата CML 2.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  ДеревоДокумента - дерево значений - дерево значений, соответствующее макету ЗаказТовара обработки ЭлектронныеДокументы.
//
Процедура ЗаполнитьДанныеПоЗаказуТоваров(СсылкаНаОбъект, СтруктураЭД, ДеревоДокумента) Экспорт
	
	РеквизитыДокумента = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(СсылкаНаОбъект, "Дата, Номер, ДоговорКонтрагента,
		| Организация, Контрагент, СуммаВключаетНДС, ВалютаДокумента, КурсВзаиморасчетов, СтруктурнаяЕдиница, ДатаОплаты");

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(НоменклатураПоставщика.Идентификатор, НЕОПРЕДЕЛЕНО) КАК ИдТовараУКонтрагента,
	|	ЕСТЬNULL(НоменклатураПоставщика.АртикулНоменклатурыКонтрагента, """") КАК Артикул,
	|	ЕСТЬNULL(ВЫРАЗИТЬ(НоменклатураПоставщика.НаименованиеНоменклатурыКонтрагента КАК Строка(255)), """") КАК Наименование,
	|	ДокЗаказ.Номенклатура КАК Номенклатура,
	|	ДокЗаказ.ХарактеристикаНоменклатуры КАК Характеристика,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения КАК БазоваяЕдиница,
	|	ДокЗаказ.Количество КАК Количество,
	|	ДокЗаказ.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код КАК ЕдиницаИзмеренияКодПоОКЕИ,
	|	ДокЗаказ.ЕдиницаИзмерения.ЕдиницаПоКлассификатору КАК Упаковка,
	|	ВЫБОР
	|		КОГДА ДокЗаказ.Ссылка.СуммаВключаетНДС
	|			ТОГДА ДокЗаказ.Сумма - ДокЗаказ.СуммаНДС
	|		ИНАЧЕ ДокЗаказ.Сумма
	|	КОНЕЦ КАК СуммаБезНДС,
	|	ДокЗаказ.СтавкаНДС КАК СтавкаНДС,
	|	ДокЗаказ.СуммаНДС КАК СуммаНДС,
	|	ДокЗаказ.Цена КАК Цена,
	|	0 КАК СуммаСкидки,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.Код КАК БазоваяЕдиницаКод,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.Наименование КАК БазоваяЕдиницаНаименование,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.НаименованиеПолное КАК БазоваяЕдиницаНаименованиеПолное,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.МеждународноеСокращение КАК БазоваяЕдиницаМеждународноеСокращение,
	|	ДокЗаказ.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код КАК УпаковкаКод,
	|	ДокЗаказ.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Наименование КАК УпаковкаНаименование,
	|	ДокЗаказ.ЕдиницаИзмерения.Коэффициент КАК ЕдиницаИзмеренияКоэффициент,
	|	ВЫБОР
	|		КОГДА ДокЗаказ.Ссылка.СуммаВключаетНДС
	|			ТОГДА ДокЗаказ.Сумма
	|		ИНАЧЕ ДокЗаказ.Сумма + ДокЗаказ.СуммаНДС
	|	КОНЕЦ КАК СуммаСНДС,
	|	ВЫРАЗИТЬ(ДокЗаказ.Номенклатура.ДополнительноеОписаниеНоменклатуры КАК СТРОКА(1000)) КАК Описание,
	|	ДокЗаказ.Ссылка.СуммаВключаетНДС КАК НДСУчтеноВСумме,
	|	ДокЗаказ.Сумма КАК Сумма
	|ИЗ
	|	Документ.ЗаказПоставщику.Товары КАК ДокЗаказ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураКонтрагентов КАК НоменклатураПоставщика
	|		ПО (НоменклатураПоставщика.Контрагент = ДокЗаказ.Ссылка.Контрагент)
	|			И (НоменклатураПоставщика.Номенклатура = ДокЗаказ.Номенклатура)
	|			И (НоменклатураПоставщика.ХарактеристикаНоменклатуры = ДокЗаказ.ХарактеристикаНоменклатуры)
	|ГДЕ
	|	ДокЗаказ.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(НоменклатураПоставщика.Идентификатор, НЕОПРЕДЕЛЕНО),
	|	ЕСТЬNULL(НоменклатураПоставщика.АртикулНоменклатурыКонтрагента, """"),
	|	ЕСТЬNULL(ВЫРАЗИТЬ(НоменклатураПоставщика.НаименованиеНоменклатурыКонтрагента КАК Строка(255)), """"),
	|	ДокЗаказ.Номенклатура,
	|	НЕОПРЕДЕЛЕНО,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения,
	|	ДокЗаказ.Количество,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.Код,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА ДокЗаказ.Ссылка.СуммаВключаетНДС
	|			ТОГДА ДокЗаказ.Сумма - ДокЗаказ.СуммаНДС
	|		ИНАЧЕ ДокЗаказ.Сумма
	|	КОНЕЦ,
	|	ДокЗаказ.СтавкаНДС,
	|	ДокЗаказ.СуммаНДС,
	|	ДокЗаказ.Цена,
	|	0,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.Код,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.Наименование,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.НаименованиеПолное,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.МеждународноеСокращение,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.Код,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.Наименование,
	|	1,
	|	ВЫБОР
	|		КОГДА ДокЗаказ.Ссылка.СуммаВключаетНДС
	|			ТОГДА ДокЗаказ.Сумма
	|		ИНАЧЕ ДокЗаказ.Сумма + ДокЗаказ.СуммаНДС
	|	КОНЕЦ,
	|	ДокЗаказ.Содержание,
	|	ДокЗаказ.Ссылка.СуммаВключаетНДС,
	|	ДокЗаказ.Сумма
	|ИЗ
	|	Документ.ЗаказПоставщику.Услуги КАК ДокЗаказ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураКонтрагентов КАК НоменклатураПоставщика
	|		ПО (НоменклатураПоставщика.Контрагент = ДокЗаказ.Ссылка.Контрагент)
	|			И (НоменклатураПоставщика.Номенклатура = ДокЗаказ.Номенклатура)
	|			И (НоменклатураПоставщика.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	|ГДЕ
	|	ДокЗаказ.Ссылка = &Ссылка";
		
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъект);
	ТаблицаТоваров = Запрос.Выполнить().Выгрузить();
	
	ЕстьОшибка = Ложь;
	Для Каждого СтрокаТовара Из ТаблицаТоваров Цикл
		
		Если СтрокаТовара.ИдТовараУКонтрагента = Неопределено Тогда
			ЕстьОшибка = Истина;
			ОбщегоНазначения.СообщитьОбОшибке(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось сопоставить номенклатуру ""%1"" с номенклатурой поставщика'"),
				Строка(СтрокаТовара.Номенклатура) + ?(ЗначениеЗаполнено(СтрокаТовара.Характеристика), "("+СтрокаТовара.Характеристика+")", "")));
		КонецЕсли;
			
	КонецЦикла;
	
	Если ЕстьОшибка Тогда
		Возврат;
	КонецЕсли;
	
	СведенияОПродавце   = ПолучитьДанныеЮрФизЛица(РеквизитыДокумента.Контрагент);
	СведенияОПокупателе = ПолучитьДанныеЮрФизЛица(РеквизитыДокумента.Организация);
	ЗаполнитьСведенияУчастникаОбмена(ДеревоДокумента, СведенияОПродавце,   "Юр", "Продавец");
	ЗаполнитьСведенияУчастникаОбмена(ДеревоДокумента, СведенияОПокупателе, "Юр", "Покупатель");
	
	СуммаДокумента = ТаблицаТоваров.Итог("СуммаСНДС");
	Если Не РеквизитыДокумента.СуммаВключаетНДС Тогда
		СуммаДокумента  = СуммаДокумента + ТаблицаТоваров.Итог("СуммаНДС");
	КонецЕсли;
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ДоговорНомер", РеквизитыДокумента.ДоговорКонтрагента.Номер);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ДоговорДата", РеквизитыДокумента.ДоговорКонтрагента.Дата);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "Валюта", РеквизитыДокумента.ВалютаДокумента.Код);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "Курс", РеквизитыДокумента.КурсВзаиморасчетов);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "Сумма", СуммаДокумента);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "СрокПлатежа", РеквизитыДокумента.ДатаОплаты);
	Если ТипЗнч(РеквизитыДокумента.СтруктурнаяЕдиница) = Тип("СправочникСсылка.БанковскиеСчета")
		И ЗначениеЗаполнено(РеквизитыДокумента.СтруктурнаяЕдиница) Тогда
		ЗаполнитьБанковскийСчет(ДеревоДокумента, РеквизитыДокумента.СтруктурнаяЕдиница);
	КонецЕсли;
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогоПоДокументу.СуммаИтог", 
		СуммаДокумента);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогоПоДокументу.СуммаНалогаИтог", 
		ТаблицаТоваров.Итог("СуммаНДС"));
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогоПоДокументу.ЦенаВключаетНДС", 
		РеквизитыДокумента.СуммаВключаетНДС);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогоПоДокументу.СуммаСкидкиИтог", 0);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогоПоДокументу.СуммаБезСкидкиИтог", 
		СуммаДокумента);

	ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДокумента, ТаблицаТоваров, "Товары");

	ИтоговаяСтрока = НСтр("ru='Всего наименований %Количество%, на сумму %Сумма%'");
	ИтоговаяСтрока = СтрЗаменить(ИтоговаяСтрока, "%Количество%", ТаблицаТоваров.Количество());
	ИтоговаяСтрока = СтрЗаменить(ИтоговаяСтрока, "%Сумма%",		 ОбщегоНазначения.ФорматСумм(СсылкаНаОбъект.СуммаДокумента, СсылкаНаОбъект.ВалютаДокумента));
	СуммаПрописью  = ОбщегоНазначения.СформироватьСуммуПрописью(СсылкаНаОбъект.СуммаДокумента, СсылкаНаОбъект.ВалютаДокумента);
	ИтоговаяСтрока = ИтоговаяСтрока + Символы.ПС + СуммаПрописью;
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогиПрописью", ИтоговаяСтрока);
	
	СтруктураЭД.Вставить("СуммаДокумента", СуммаДокумента);
КонецПроцедуры

// Подготавливает данные для электронного документа типа ОтветНаЗаказ формата CML 2.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  ДеревоДокумента - дерево значений - дерево значений, соответствующее макету ОтветНаЗаказ обработки ЭлектронныеДокументы.
//
Процедура ЗаполнитьДанныеПоОтветуНаЗаказ(СсылкаНаОбъект, СтруктураЭД, ДеревоДокумента) Экспорт
	
	РеквизитыДокумента = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(СсылкаНаОбъект, "Дата, Номер, ДоговорКонтрагента,
		| Организация, Контрагент, СуммаВключаетНДС, ВалютаДокумента, КурсВзаиморасчетов, СтруктурнаяЕдиница, ДатаОплаты,
		| Грузополучатель");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокЗаказ.Номенклатура.Код КАК ИД,
	|	ДокЗаказ.Номенклатура.Артикул КАК Артикул,
	|	ВЫРАЗИТЬ(ДокЗаказ.Номенклатура.НаименованиеПолное КАК Строка(255))КАК Наименование,
	|	ДокЗаказ.Номенклатура КАК Номенклатура,
	|	ДокЗаказ.ХарактеристикаНоменклатуры КАК Характеристика,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения КАК БазоваяЕдиница,
	|	ДокЗаказ.Количество КАК Количество,
	|	ДокЗаказ.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код КАК УпаковкаПоОКЕИ,
	|	ДокЗаказ.ЕдиницаИзмерения.ЕдиницаПоКлассификатору КАК Упаковка,
	|	ДокЗаказ.СтавкаНДС КАК СтавкаНДС,
	|	ДокЗаказ.СуммаНДС КАК СуммаНДС,
	|	ДокЗаказ.Цена КАК Цена,
	|	0 КАК СуммаСкидки,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.Код КАК БазоваяЕдиницаКод,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.Наименование КАК БазоваяЕдиницаНаименование,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.НаименованиеПолное КАК БазоваяЕдиницаНаименованиеПолное,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.МеждународноеСокращение КАК БазоваяЕдиницаМеждународноеСокращение,
	|	ДокЗаказ.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код КАК ЕдиницаИзмеренияКодПоОКЕИ,
	|	ДокЗаказ.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Наименование КАК ЕдиницаИзмеренияНаименование,
	|	ДокЗаказ.ЕдиницаИзмерения.Коэффициент КАК ЕдиницаИзмеренияКоэффициент,
	|	ВЫБОР
	|		КОГДА ДокЗаказ.Ссылка.СуммаВключаетНДС
	|			ТОГДА ДокЗаказ.Сумма
	|		ИНАЧЕ ДокЗаказ.Сумма + ДокЗаказ.СуммаНДС
	|	КОНЕЦ КАК СуммаСНДС,
	|	ВЫБОР
	|		КОГДА ДокЗаказ.Ссылка.СуммаВключаетНДС
	|			ТОГДА ДокЗаказ.Сумма - ДокЗаказ.СуммаНДС
	|		ИНАЧЕ ДокЗаказ.Сумма
	|	КОНЕЦ КАК СуммаБезНДС,
	|	ВЫРАЗИТЬ(ДокЗаказ.Номенклатура.ДополнительноеОписаниеНоменклатуры КАК СТРОКА(1000)) КАК Описание,
	|	ДокЗаказ.Ссылка.СуммаВключаетНДС КАК НДСУчтеноВСумме,
	|	ДокЗаказ.Сумма КАК Сумма
	|ИЗ
	|	Документ.ЗаказПокупателя.Товары КАК ДокЗаказ
	|ГДЕ
	|	ДокЗаказ.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокЗаказ.Номенклатура.Код,
	|	ДокЗаказ.Номенклатура.Артикул,
	|	ВЫРАЗИТЬ(ДокЗаказ.Номенклатура.НаименованиеПолное КАК Строка(255)),
	|	ДокЗаказ.Номенклатура,
	|	НЕОПРЕДЕЛЕНО,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения,
	|	ДокЗаказ.Количество,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.Код,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения,
	|	ДокЗаказ.СтавкаНДС,
	|	ДокЗаказ.СуммаНДС,
	|	ДокЗаказ.Цена,
	|	0,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.Код,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.Наименование,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.НаименованиеПолное,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.МеждународноеСокращение,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.Код,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.Наименование,
	|	1,
	|	ВЫБОР
	|		КОГДА ДокЗаказ.Ссылка.СуммаВключаетНДС
	|			ТОГДА ДокЗаказ.Сумма
	|		ИНАЧЕ ДокЗаказ.Сумма + ДокЗаказ.СуммаНДС
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ДокЗаказ.Ссылка.СуммаВключаетНДС
	|			ТОГДА ДокЗаказ.Сумма - ДокЗаказ.СуммаНДС
	|		ИНАЧЕ ДокЗаказ.Сумма
	|	КОНЕЦ,
	|	ДокЗаказ.Содержание,
	|	ДокЗаказ.Ссылка.СуммаВключаетНДС,
	|	ДокЗаказ.Сумма
	|ИЗ
	|	Документ.ЗаказПокупателя.Услуги КАК ДокЗаказ
	|ГДЕ
	|	ДокЗаказ.Ссылка = &Ссылка";
		
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъект);
	ТаблицаТоваров = Запрос.Выполнить().Выгрузить();
	ИмяКолонкиИД = "ИдТовараУКонтрагента";
	ТаблицаТоваров.Колонки.Добавить(ИмяКолонкиИД);
	ОбработатьТаблицуТоваров(ТаблицаТоваров, Ложь, ИмяКолонкиИД);

	// Заполняем дерево
	СведенияОПродавце   = ПолучитьДанныеЮрФизЛица(РеквизитыДокумента.Организация);
	СведенияОПокупателе = ПолучитьДанныеЮрФизЛица(РеквизитыДокумента.Контрагент);
	ЗаполнитьСведенияУчастникаОбмена(ДеревоДокумента, СведенияОПродавце,   "Юр", "Продавец");
	ЗаполнитьСведенияУчастникаОбмена(ДеревоДокумента, СведенияОПокупателе, "Юр", "Покупатель");
	Если ЗначениеЗаполнено(РеквизитыДокумента.Грузополучатель) Тогда
		СведенияОГрузополучателе = ПолучитьДанныеЮрФизЛица(РеквизитыДокумента.Грузополучатель);
		ЗаполнитьСведенияУчастникаОбмена(ДеревоДокумента, СведенияОГрузополучателе, "Факт", "Получатель");
	КонецЕсли;

	СуммаДокумента = ТаблицаТоваров.Итог("СуммаСНДС");
	Если Не РеквизитыДокумента.СуммаВключаетНДС Тогда
		СуммаДокумента  = СуммаДокумента + ТаблицаТоваров.Итог("СуммаНДС");
	КонецЕсли;
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ДоговорНомер", РеквизитыДокумента.ДоговорКонтрагента.Номер);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ДоговорДата", РеквизитыДокумента.ДоговорКонтрагента.Дата);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "Валюта", РеквизитыДокумента.ВалютаДокумента.Код);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "Курс", РеквизитыДокумента.КурсВзаиморасчетов);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "Сумма", СуммаДокумента);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "СрокПлатежа", РеквизитыДокумента.ДатаОплаты);
	Если ТипЗнч(РеквизитыДокумента.СтруктурнаяЕдиница) = Тип("СправочникСсылка.БанковскиеСчета")
		И ЗначениеЗаполнено(РеквизитыДокумента.СтруктурнаяЕдиница) Тогда
		ЗаполнитьБанковскийСчет(ДеревоДокумента, РеквизитыДокумента.СтруктурнаяЕдиница);
	КонецЕсли;

	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогоПоДокументу.СуммаИтог", 
		СуммаДокумента);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогоПоДокументу.СуммаНалогаИтог", 
		ТаблицаТоваров.Итог("СуммаНДС"));
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогоПоДокументу.ЦенаВключаетНДС", 
		РеквизитыДокумента.СуммаВключаетНДС);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогоПоДокументу.СуммаСкидкиИтог", 0);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогоПоДокументу.СуммаБезСкидкиИтог", 
		СуммаДокумента);

	ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДокумента, ТаблицаТоваров, "Товары");

	ИтоговаяСтрока = НСтр("ru='Всего наименований %Количество%, на сумму %Сумма%'");
	ИтоговаяСтрока = СтрЗаменить(ИтоговаяСтрока, "%Количество%", ТаблицаТоваров.Количество());
	ИтоговаяСтрока = СтрЗаменить(ИтоговаяСтрока, "%Сумма%",		 ОбщегоНазначения.ФорматСумм(СсылкаНаОбъект.СуммаДокумента, СсылкаНаОбъект.ВалютаДокумента));
	СуммаПрописью  = ОбщегоНазначения.СформироватьСуммуПрописью(СсылкаНаОбъект.СуммаДокумента, СсылкаНаОбъект.ВалютаДокумента);
	ИтоговаяСтрока = ИтоговаяСтрока + Символы.ПС + СуммаПрописью;
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогиПрописью", ИтоговаяСтрока);

	СтруктураЭД.Вставить("СуммаДокумента", СуммаДокумента);
КонецПроцедуры

// Подготавливает данные для электронного документа типа ОтчетКомитенту формата CML 2.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  ДеревоДокумента - дерево значений - дерево значений, соответствующее макету ОтчетКомиссионераОПродажах обработки ЭлектронныеДокументы.
//
// Особенность:
//  Параметр ДополнительныеРеквизитыДляТаблицыТоваров в общей структуре параметров предназначен для заполнения
//  колонки ДополнительныеРеквизиты в таблице товаров.
//
Процедура ЗаполнитьДанныеПоОтчетуОПродажахКомиссионногоТовара(СсылкаНаОбъект, СтруктураЭД, ДеревоДокумента) Экспорт
	
	
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа ОтчетКомитентуОСписании формата CML 2.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  ДеревоДокумента - дерево значений - дерево значений, соответствующее макету ОтчетКомиссионераОСписании обработки ЭлектронныеДокументы.
//
Процедура ЗаполнитьДанныеПоОтчетуОСписанииКомиссионногоТовара(СсылкаНаОбъект, СтруктураЭД, ДеревоДокумента) Экспорт
 
	

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// УСТАРЕВШИЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Устаревшие: Работа со структурой данных ФНС

// Устаревшая процедура, вместо этой процедуры следует использовать ЗаполнитьДанныеПоТорг12ПродавецФНС.
// Подготавливает данные для электронного документа типа Торг12 титул продавца.
//
// Параметры:
//  СсылкаНаОбъект - документСсылка - ссылка на объект информационной базы,
//  по которому необходимо создать электронный документ.
//  СтруктураЭД - структура - структура данных для формирования электронного документа.
//  СтруктураПараметров - структура - параметры заполнения электронного документа.
//
Процедура ПодготовитьДанныеПоТорг12(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметров) Экспорт
	
	ДанныеДокумента = ПолучитьДанныеРеализации(СсылкаНаОбъект);
	РеквизитыШапки  = ДанныеДокумента.РеквизитыШапки;
	
	Если ДанныеДокумента.ТаблицаТоваров.Количество() = 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Документ не содержит данных для формирования ЭД ""%1""'"),
			СтруктураЭД.ВидЭД);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	// Сформируем таблицу товаров
	НомерСтроки = 1;
	Для каждого ДанныеСтрокиТоваров Из ДанныеДокумента.ТаблицаТоваров Цикл
		ДобавитьСтрокуТаблицуДанных(СтруктураПараметров.ТаблицаТоваров, ДанныеСтрокиТоваров, СтруктураПараметров, "Товары", НомерСтроки);
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
		
	// Рассчитаем итоговые показатели
	ИтоговыеСуммы = СтруктураИтоговыеСуммы(СтруктураПараметров.ТаблицаТоваров, СтруктураЭД.ВидЭД);
	
	ЗаполнитьЗначенияСвойств(СтруктураПараметров.ВсегоПоНакладной, ИтоговыеСуммы);
	
	СтруктураПараметров.НомерТоварнойНакладной   = ПолучитьПечатныйНомерДокумента(СсылкаНаОбъект);
	СтруктураПараметров.ДатаТоварнойНакладной    = РеквизитыШапки.ДатаДокумента;
	СтруктураПараметров.ВидОперации              = РеквизитыШапки.ВидОперации;
	
	// Передадим документы основания
	Если ЗначениеЗаполнено(РеквизитыШапки.ДокументОснование) Тогда
		МассивДокументовОснований = Новый Массив;
		МассивДокументовОснований.Добавить(РеквизитыШапки.ДокументОснование);
		СтруктураПараметров.ДокументыОснования = МассивДокументовОснований;
	КонецЕсли;
	
	// Заполним данные по договору
	СтруктураПараметров.ДокОснованиеНаименование = РеквизитыШапки.ДоговорНаименование;
	СтруктураПараметров.ДокОснованиеНомер        = РеквизитыШапки.ДоговорНомер;
	СтруктураПараметров.ДокОснованиеДата         = РеквизитыШапки.ДоговорДата;
	
	ЗаполнитьРеквизитыУчастниковТОРГ12(РеквизитыШапки, СтруктураПараметров);
	
	// Получим данные по ответственным лица
	Руководитель = ОтветственноеЛицоОрганизации(
		РеквизитыШапки.ДатаДокумента, 
		РеквизитыШапки.Организация, 
		Перечисления.ОтветственныеЛицаОрганизации.Руководитель);
		
	ГлавныйБухгалтер = ОтветственноеЛицоОрганизации(
		РеквизитыШапки.ДатаДокумента, 
		РеквизитыШапки.Организация, 
		Перечисления.ОтветственныеЛицаОрганизации.ГлавныйБухгалтер);
	
	// Заполним данные по подписанту
	ЭтоФизЛицо = ЭтоФизЛицо(СтруктураЭД.Организация);
	ДанныеЮрФизЛица = ПолучитьДанныеЮрФизЛица(СтруктураЭД.Организация);
	ФИО       = "";
	Должность = Неопределено;
	
	Если ЭтоФизЛицо Тогда
		ФИО = ДанныеЮрФизЛица.ПолноеНаименование;
		ДанныеСвидетельстваОРегистрацииИП(СтруктураЭД.Организация, СтруктураПараметров.Подписант.СвидетельствоОРегистрацииИП);
	Иначе
		ФИО       = Руководитель.ФИО;
		Должность = Руководитель.Должность;
	КонецЕсли;
	
	СтруктураПараметров.Подписант.ЭтоФизЛицо = ЭтоФизЛицо;
	ЗаполнитьФИОиДолжность(СтруктураПараметров.Подписант, ФИО, Должность);
	СтруктураПараметров.Подписант.ИНН = ДанныеЮрФизЛица.ИНН;
	
	// Заполним сведения по отпуску товара
	СведенияПоОтпускуГруза = СтруктураПараметров.СведенияПоОтпускуГруза;
	СведенияПоОтпускуГруза.ДатаОтпуска             = РеквизитыШапки.ДатаДокумента;
	СведенияПоОтпускуГруза.ОтпущеноНаСумму         = ИтоговыеСуммы.СуммаСНДС;
	СведенияПоОтпускуГруза.ОтпущеноНаСуммуПрописью = СуммаПрописью(ИтоговыеСуммы.СуммаСНДС);
	
	// Заполним общие сведения по товароной накладной
	СтруктураПараметров.ОбщиеСведенияОТоварнойНакладной.Вставить("КоличествоПорядковыхНомеровЗаписей", 
		ИтоговыеСуммы.КоличествоПорядковыхНомеровЗаписей);
	СтруктураПараметров.ОбщиеСведенияОТоварнойНакладной.Вставить("КоличествоПорядковыхНомеровЗаписейПрописью", 
		ЧислоПрописью(ИтоговыеСуммы.КоличествоПорядковыхНомеровЗаписей, ,",,,,,,,,0"));
	СтруктураПараметров.ОбщиеСведенияОТоварнойНакладной.Вставить("ВсегоМест", ИтоговыеСуммы.КоличествоМест);
	СтруктураПараметров.ОбщиеСведенияОТоварнойНакладной.Вставить("ВсегоМестПрописью", ЧислоПрописью(ИтоговыеСуммы.КоличествоМест, ,",,,,,,,,0"));
	СтруктураПараметров.ОбщиеСведенияОТоварнойНакладной.Вставить("МассаГрузаНетто", ИтоговыеСуммы.МассаНетто);
	СтруктураПараметров.ОбщиеСведенияОТоварнойНакладной.Вставить("МассаГрузаБрутто", ИтоговыеСуммы.МассаБрутто);
	
	// Бухгалтер
	Бухгалтер = СведенияПоОтпускуГруза.Бухгалтер;
	ЗаполнитьФИОиДолжность(Бухгалтер, ГлавныйБухгалтер.ФИО, ГлавныйБухгалтер.Должность);
	
	// Отпуск разрешил
	ОтпускРазрешил = СведенияПоОтпускуГруза.ОтпускРазрешил;
	Если НЕ ЗначениеЗаполнено(РеквизитыШапки.ОтпускРазрешил) Тогда
		ФИО       = Руководитель.ФИО;
		Должность = Руководитель.Должность;
	Иначе
		ФИО       = ФормированиеПечатныхФорм.ФамилияИмяОтчество(РеквизитыШапки.ОтпускРазрешил, РеквизитыШапки.ДатаДокумента);
		//Должность = ПолныеПрава.СведенияОСотруднике(РеквизитыШапки.ОтпускРазрешил, РеквизитыШапки.ДатаДокумента, РеквизитыШапки.Организация).Должность;
		Должность = ФормированиеПечатныхФорм.ДолжностьОтветственногоЛицаОрганизации(РеквизитыШапки.ОтпускРазрешил, РеквизитыШапки.ДатаДокумента, РеквизитыШапки.Организация).Должность;
	КонецЕсли;
	ЗаполнитьФИОиДолжность(ОтпускРазрешил, ФИО, Должность);
	
	// Отпуск произвел
	ОтпускПроизвел = СведенияПоОтпускуГруза.ОтпускПроизвел;
	Если НЕ ЗначениеЗаполнено(РеквизитыШапки.ОтпускПроизвел) Тогда
		ФИО       = ФормированиеПечатныхФорм.ФамилияИмяОтчество(РеквизитыШапки.ОтветственноеЛицо, РеквизитыШапки.ДатаДокумента);
		//Должность = ПолныеПрава.СведенияОСотруднике(РеквизитыШапки.ОтветственноеЛицо, РеквизитыШапки.ДатаДокумента, РеквизитыШапки.Организация).Должность;
		Должность = ФормированиеПечатныхФорм.ДолжностьОтветственногоЛицаОрганизации(РеквизитыШапки.ОтветственноеЛицо, РеквизитыШапки.ДатаДокумента, РеквизитыШапки.Организация).Должность;
	Иначе
		ФИО       = ФормированиеПечатныхФорм.ФамилияИмяОтчество(РеквизитыШапки.ОтпускПроизвел, РеквизитыШапки.ДатаДокумента);
		//Должность = ПолныеПрава.СведенияОСотруднике(РеквизитыШапки.ОтпускПроизвел, РеквизитыШапки.ДатаДокумента, РеквизитыШапки.Организация).Должность;
		Должность = ФормированиеПечатныхФорм.ДолжностьОтветственногоЛицаОрганизации(РеквизитыШапки.ОтпускПроизвел, РеквизитыШапки.ДатаДокумента, РеквизитыШапки.Организация).Должность;
	КонецЕсли;
	ЗаполнитьФИОиДолжность(ОтпускПроизвел, ФИО, Должность);
	
КонецПроцедуры

// Устаревшая процедура, вместо этой процедуры следует использовать ЗаполнитьДанныеПоТОРГ12ПокупательФНС.
// Подготавливает данные для электронного документа типа Торг12 титул покупателя.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  СтруктураПараметров - структура - параметры заполнения электронного документа.
//
Процедура ПодготовитьДанныеПоТорг12Покупатель(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметров) Экспорт
	
	ЭтоФизЛицо = ЭтоФизЛицо(СтруктураЭД.Организация);
	
	ДанныеЮрФизЛица = ПолучитьДанныеЮрФизЛица(СтруктураЭД.Организация);
	ФИО       = "";
	Должность = Неопределено;
	
	Если ЭтоФизЛицо Тогда
		ФИО = ДанныеЮрФизЛица.ПолноеНаименование;
		ДанныеСвидетельстваОРегистрацииИП(СтруктураЭД.Организация, СтруктураПараметров.Подписант.СвидетельствоОРегистрацииИП);
	Иначе
		Руководитель = ОтветственноеЛицоОрганизации(
			СтруктураЭД.ДатаЭД, 
			СтруктураЭД.Организация, 
			Перечисления.ОтветственныеЛицаОрганизации.Руководитель);
		
		ФИО       = Руководитель.ФИО;
		Должность = Руководитель.Должность;
		
	КонецЕсли;
	
	СтруктураПараметров.Подписант.ЭтоФизЛицо = ЭтоФизЛицо;
	ЗаполнитьФИОиДолжность(СтруктураПараметров.Подписант, ФИО, Должность);
	СтруктураПараметров.Подписант.ИНН = ДанныеЮрФизЛица.ИНН;
	
КонецПроцедуры

// Устаревшая процедура, вместо этой процедуры следует использовать ЗаполнитьДанныеПоАкт501ИсполнительФНС.
// Подготавливает данные титула исполнителя для электронного документа типа Акт выполненных работ
// формата 5.01.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  СтруктураПараметров - структура - параметры заполнения электронного документа.
//
Процедура ПодготовитьДанныеПоАкт501(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметров) Экспорт

	ДанныеДокумента = ПодготовитьДанныеАктаОбОказанииУслуг(СсылкаНаОбъект);
	РеквизитыШапки = ДанныеДокумента.РеквизитыШапки;
	
	Если ДанныеДокумента.ТаблицаУслуг.Количество() = 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Документ не содержит данных для формирования ЭД ""%1""'"),
			СтруктураЭД.ВидЭД);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	СтруктураПараметров.НомерАкта    = ПолучитьПечатныйНомерДокумента(СсылкаНаОбъект);
	СтруктураПараметров.ДатаАкта     = РеквизитыШапки.ДатаДокумента;
	СтруктураПараметров.ВидОперации  = РеквизитыШапки.ВидОперации;
	ЗаполнитьРеквизитыУчастниковАкт501(РеквизитыШапки, СтруктураПараметров);
	
	// Передадим документы основания
	Если ЗначениеЗаполнено(РеквизитыШапки.ДокументОснование) Тогда
		МассивДокументовОснований = Новый Массив;
		МассивДокументовОснований.Добавить(РеквизитыШапки.ДокументОснование);
		СтруктураПараметров.ДокументыОснования = МассивДокументовОснований;
	КонецЕсли;
	
	// Сформируем таблицу услуг
	НомерСтроки = 1;
	Для каждого ДанныеСтрокиУслуг Из ДанныеДокумента.ТаблицаУслуг Цикл
		ДобавитьСтрокуТаблицуДанных(СтруктураПараметров.ТаблицаУслуг, ДанныеСтрокиУслуг, СтруктураПараметров, "Услуги", НомерСтроки);
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	
	// Заполним таблицу описаний услуг, вкладывая туда таблицу с услугами.
	НоваяСтрока = СтруктураПараметров.ТаблицаОписанийУслуг.Добавить();
	
	// Не заполняем эти поля, потому что у пользователя нет возможности их изменить
	//НоваяСтрока.НачалоРабот      = РеквизитыШапки.Дата;
	//НоваяСтрока.КонецРабот       = РеквизитыШапки.Дата;
	
	НоваяСтрока.СуммаБезНДСИтого = СтруктураПараметров.ТаблицаУслуг.Итог("СуммаБезНДС");
	НоваяСтрока.СуммаНДСИтого    = СтруктураПараметров.ТаблицаУслуг.Итог("СуммаНДС");
	НоваяСтрока.СуммаСНДСИтого   = СтруктураПараметров.ТаблицаУслуг.Итог("СуммаСНДС");
	НоваяСтрока.Услуги           = СтруктураПараметров.ТаблицаУслуг;
	
	Руководитель = ОтветственноеЛицоОрганизации(
		РеквизитыШапки.Дата, 
		РеквизитыШапки.Исполнитель, 
		Перечисления.ОтветственныеЛицаОрганизации.Руководитель);
		
	СтруктураПараметров.СведенияПоВыполнениюУслуг.ДатаИсполнения = РеквизитыШапки.Дата;
	ЗаполнитьФИОиДолжность(СтруктураПараметров.СведенияПоВыполнениюУслуг.ПодписьИсполнителя, Руководитель.ФИО, Руководитель.Должность);
	
	// Заполним данные по подписанту
	ЭтоФизЛицо = ЭтоФизЛицо(РеквизитыШапки.Исполнитель);
	ДанныеЮрФизЛица = ПолучитьДанныеЮрФизЛица(РеквизитыШапки.Исполнитель);
	ФИО       = "";
	Должность = Неопределено;
	
	Если ЭтоФизЛицо Тогда
		ФИО = ДанныеЮрФизЛица.ПолноеНаименование;
		ДанныеСвидетельстваОРегистрацииИП(РеквизитыШапки.Исполнитель, СтруктураПараметров.Подписант.СвидетельствоОРегистрацииИП);
	Иначе
		ФИО       = Руководитель.ФИО;
		Должность = Руководитель.Должность;
	КонецЕсли;
	
	СтруктураПараметров.Подписант.ЭтоФизЛицо = ЭтоФизЛицо;
	ЗаполнитьФИОиДолжность(СтруктураПараметров.Подписант, ФИО, Должность);
	СтруктураПараметров.Подписант.ИНН = ДанныеЮрФизЛица.ИНН;
	
КонецПроцедуры

// Устаревшая процедура, вместо этой процедуры следует использовать ЗаполнитьДанныеПоАкт501ЗаказчикФНС.
// Подготавливает данные титула заказчика для электронного документа типа Акт выполненных работ
// формата 5.01.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  СтруктураПараметров - структура - параметры заполнения электронного документа.
//
Процедура ПодготовитьДанныеПоАкт501Заказчик(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметров) Экспорт

	// Заполним данные по подписанту
	ЭтоФизЛицо = ЭтоФизЛицо(СтруктураЭД.Организация);
	ДанныеЮрФизЛица = ПолучитьДанныеЮрФизЛица(СтруктураЭД.Организация);
	ФИО       = "";
	Должность = Неопределено;
	
	Если ЭтоФизЛицо Тогда
		ФИО = ДанныеЮрФизЛица.ПолноеНаименование;
		ДанныеСвидетельстваОРегистрацииИП(СтруктураЭД.Организация, СтруктураПараметров.Подписант.СвидетельствоОРегистрацииИП);
	Иначе
		Руководитель = ОтветственноеЛицоОрганизации(
			СтруктураЭД.ДатаЭД, 
			СтруктураЭД.Организация, 
			Перечисления.ОтветственныеЛицаОрганизации.Руководитель);
		
		ФИО       = Руководитель.ФИО;
		Должность = Руководитель.Должность;
		
	КонецЕсли;
	
	СтруктураПараметров.Подписант.ЭтоФизЛицо = ЭтоФизЛицо;
	ЗаполнитьФИОиДолжность(СтруктураПараметров.Подписант, ФИО, Должность);
	СтруктураПараметров.Подписант.ИНН = ДанныеЮрФизЛица.ИНН;
	
	СтруктураПараметров.ДатаЗаказа = ТекущаяДатаСеанса();
	СтруктураПараметров.Претензия = "Претензий нет";
	
КонецПроцедуры

// Устаревшая процедура, вместо этой процедуры следует использовать ЗаполнитьДанныеПоСчетуФактуреФНС.
// Подготавливает данные для электронного документа типа СчетФактураВыданный.
//
// Параметры:
//  СсылкаНаОбъект - документСсылка - ссылка на объект информационной базы, по которому необходимо создать электронный документ;
//  СтруктураЭД - структура - структура данных для формирования электронного документа;
//  СтруктураПараметров - структура - параметры заполнения электронного документа.
//
Процедура ПодготовитьДанныеПоСчетФактуре(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметров) Экспорт
	
	ДанныеДляФормированияЭД = Неопределено;
	УчетнаяПолитика = Неопределено;
	
	СсылкаНаОбъект.ПолучитьОбъект().СобратьДанныеДляПечати(ДанныеДляФормированияЭД, УчетнаяПолитика);
	
	Если ТипЗнч(ДанныеДляФормированияЭД) = Тип("Соответствие") Тогда
		СтруктураПараметров.Вставить("ДанныеПодготовлены", Ложь);
		Возврат;
	ИначеЕсли ДанныеДляФормированияЭД = Неопределено Тогда
		СтруктураПараметров.Вставить("ДанныеПодготовлены", Ложь);
		Возврат;
	КонецЕсли;
	
	ВалютаДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаОбъект, "ВалютаДокумента");
	Если ВалютаДокумента <> глЗначениеПеременной("ВалютаРегламентированногоУчета") Тогда
		ОкруглитьЧисловыеПоля(ДанныеДляФормированияЭД);
	КонецЕсли;
		
	РеквизитыСФ = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(СсылкаНаОбъект, 
		"Номер, Дата, Исправление, НомерИсправления, 
		|НомерИсходногоДокумента, ДатаИсходногоДокумента");
		
	СведенияОПокупателе = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(ДанныеДляФормированияЭД.Покупатель, РеквизитыСФ.Дата);
	СведенияОПоставщике  = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(ДанныеДляФормированияЭД.Поставщик, РеквизитыСФ.Дата);
	
	Если ЭтоКорректировочныйДокумент(СсылкаНаОбъект) Тогда
		
		// Подготовим параметры для заполнения шапки
		
		Если РеквизитыСФ.Исправление Тогда
			СтруктураПараметров.НомерКоррСчФ            = РеквизитыСФ.НомерИсправляемогоКорректировочногоДокумента;
			СтруктураПараметров.ДатаКоррСчФ             = РеквизитыСФ.ДатаИсправляемогоКорректировочногоДокумента;
			СтруктураПараметров.НомерИсправленияКоррСчФ = РеквизитыСФ.НомерИсправления;
			СтруктураПараметров.ДатаИсправленияКоррСчФ  = РеквизитыСФ.Дата;
		Иначе
			СтруктураПараметров.НомерКоррСчФ = ПолучитьПечатныйНомерДокумента(СсылкаНаОбъект);
			СтруктураПараметров.ДатаКоррСчФ  = РеквизитыСФ.Дата;
		КонецЕсли;
		
		СтруктураПараметров.НомерСчФ = РеквизитыСФ.НомерИсходногоДокумента;
		СтруктураПараметров.ДатаСчФ  = РеквизитыСФ.ДатаИсходногоДокумента;
		
		Если РеквизитыСФ.УчитыватьИсправлениеИсходногоДокумента Тогда
			СтруктураПараметров.НомерИсправленияИсходногоСчФ = РеквизитыСФ.НомерИсправленияИсходногоДокумента;
			СтруктураПараметров.ДатаИсправленияИсходногоСчФ  = РеквизитыСФ.ДатаИсправленияИсходногоДокумента;
		КонецЕсли;
		
		// Подготовим таблицу товаров
		НомерСтроки = 1;
		Для каждого Строка Из ДанныеДляФормированияЭД.ТабличнаяЧасть Цикл
			
			НоваяСтрока = СтруктураПараметров.ТаблицаТоваров.Добавить();
			НоваяСтрока.НомерСтроки              = НомерСтроки;
			НоваяСтрока.НаименованиеНоменклатуры = Строка.НаименованиеТовара;
			НоваяСтрока.ЕдиницаИзмеренияКодДо    = Строка.ЕдиницаИзмеренияКод;
			НоваяСтрока.ЕдиницаИзмеренияКод      = Строка.ЕдиницаИзмеренияКод;
			НоваяСтрока.КоличествоДо             = Строка.КоличествоДоИзменения;
			НоваяСтрока.Количество               = Строка.КоличествоПослеИзменения;
			НоваяСтрока.ЦенаДо                   = Строка.ЦенаДоИзменения;
			НоваяСтрока.Цена                     = Строка.ЦенаПослеИзменения;
			НоваяСтрока.СуммаБезНДСДо            = Строка.СтоимостьБезНДСДоИзменения;
			НоваяСтрока.СуммаБезНДС              = Строка.СтоимостьБезНДСПослеИзменения;
			НоваяСтрока.СуммаБезНДСУвеличение    = Строка.РазницаБезНДСУвеличение;
			НоваяСтрока.СуммаБезНДСУменьшение    = Строка.РазницаБезНДСУменьшение;
			НоваяСтрока.СуммаДо                  = Строка.СтоимостьСНДСДоИзменения;
			НоваяСтрока.Сумма                    = Строка.СтоимостьСНДСПослеИзменения;
			НоваяСтрока.СуммаУвеличение          = Строка.РазницаСНДСУвеличение;
			НоваяСтрока.СуммаУменьшение          = Строка.РазницаСНДСУменьшение;
			НоваяСтрока.СтавкаНДСДо              = ПредставлениеСтавкиНДС(Строка.СтавкаНДС);
			НоваяСтрока.СтавкаНДС                = ПредставлениеСтавкиНДС(Строка.СтавкаНДС);
			НоваяСтрока.СтавкаНДСТипДо           = ТипСтавкиНДС(Строка.СтавкаНДС);
			НоваяСтрока.СтавкаНДСТип             = ТипСтавкиНДС(Строка.СтавкаНДС);
			НоваяСтрока.СуммаНДСДо               = Строка.СуммаНДСДоИзменения;
			НоваяСтрока.СуммаНДС                 = Строка.СуммаНДСПослеИзменения;
			НоваяСтрока.СуммаНДСУвеличение       = Строка.РазницаНДСУвеличение;
			НоваяСтрока.СуммаНДСУменьшение       = Строка.РазницаНДСУменьшение;
			НоваяСтрока.АкцизаДо                 = "без акциза";
			НоваяСтрока.Акциза                   = "без акциза";
			
			НомерСтроки = НомерСтроки + 1;
			
		КонецЦикла;
		
		СтруктураПараметров.СуммаБезНДСВсегоУвеличение = СтруктураПараметров.ТаблицаТоваров.Итог("СуммаБезНДСУвеличение");
		СтруктураПараметров.СуммаСНДСВсегоУвеличение   = СтруктураПараметров.ТаблицаТоваров.Итог("СуммаУвеличение");
		СтруктураПараметров.СуммаНДСУвеличение         = СтруктураПараметров.ТаблицаТоваров.Итог("СуммаНДСУвеличение");
		
		СтруктураПараметров.СуммаБезНДСВсегоУменьшение = СтруктураПараметров.ТаблицаТоваров.Итог("СуммаБезНДСУменьшение");
		СтруктураПараметров.СуммаСНДСВсегоУменьшение   = СтруктураПараметров.ТаблицаТоваров.Итог("СуммаУменьшение");
		СтруктураПараметров.СуммаНДСУменьшение         = СтруктураПараметров.ТаблицаТоваров.Итог("СуммаНДСУменьшение");
		
	Иначе
		
		// Подготовим параметры для заполнения шапки
		
		СтруктураПараметров.НомерСчФ = ?(РеквизитыСФ.Исправление, РеквизитыСФ.НомерИсходногоДокумента, ПолучитьПечатныйНомерДокумента(СсылкаНаОбъект));
		СтруктураПараметров.ДатаСчФ  = ?(РеквизитыСФ.Исправление, РеквизитыСФ.ДатаИсходногоДокумента,  ДанныеДляФормированияЭД.Дата);
		
		Если РеквизитыСФ.Исправление Тогда
			СтруктураПараметров.НомерИсправленияИсходногоСчФ = РеквизитыСФ.НомерИсправления;
			СтруктураПараметров.ДатаИсправленияИсходногоСчФ  = РеквизитыСФ.Дата;
		КонецЕсли;

		// Подготовим таблицу товаров
		
		ТолькоУслуги = Истина;
		
		НомерСтроки = 1;
		Для каждого Строка Из ДанныеДляФормированияЭД.ТабличнаяЧасть Цикл
			
			Если ТолькоУслуги
				 И ((ТипЗнч(Строка.Товар) = Тип("СправочникСсылка.Номенклатура") И НЕ Строка.Товар.Услуга)) Тогда
				ТолькоУслуги = Ложь;
			КонецЕсли;
			
			НоваяСтрока = СтруктураПараметров.ТаблицаТоваров.Добавить();
			
			НоваяСтрока.НомерСтроки               = НомерСтроки;
			НоваяСтрока.НаименованиеНоменклатуры  = Строка.ТоварНаименование;
			НоваяСтрока.ЕдиницаИзмеренияКод       = Строка.ЕдиницаИзмеренияКод;
			НоваяСтрока.Количество                = Строка.Количество;
			Если Строка.Количество = 0 Тогда
				НоваяСтрока.Цена = 0;
			Иначе
				НоваяСтрока.Цена = Окр(?(Строка.СуммаВключаетНДС, Строка.Сумма - Строка.СуммаНДС, Строка.Сумма) / Строка.Количество, 2);
			КонецЕсли;
			НоваяСтрока.СуммаБезНДС               = ?(Строка.СуммаВключаетНДС, Строка.Сумма - Строка.СуммаНДС, Строка.Сумма);
			НоваяСтрока.Сумма                     = ?(Строка.СуммаВключаетНДС, Строка.Сумма, Строка.Сумма + Строка.СуммаНДС);
			НоваяСтрока.СтавкаНДС                 = ПредставлениеСтавкиНДС(Строка.СтавкаНДС);
			НоваяСтрока.СтавкаНДСТип              = ТипСтавкиНДС(Строка.СтавкаНДС);
			НоваяСтрока.СуммаНДС                  = Строка.СуммаНДС;
			НоваяСтрока.КодСтраныПроисхождения    = ?(ЗначениеЗаполнено(Строка.СтранаПроисхожденияКод), Строка.СтранаПроисхожденияКод, "");
			НоваяСтрока.НомерТаможеннойДекларации = ?(ЗначениеЗаполнено(Строка.НомерГТД), СокрЛП(Строка(Строка.НомерГТД)), "");
			Если СтрДлина(НоваяСтрока.НомерТаможеннойДекларации) > 29 Тогда
				ШаблонОшибки = НСтр("ru = 'Длина номера ГТД в строке %1 превышает 29 символов. Электронный документ не сформирован.'");
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОшибки, Строка(НомерСтроки));
				ОбщегоНазначения.СообщитьОбОшибке(ТекстОшибки);
				СтруктураПараметров.Вставить("ДанныеПодготовлены", Ложь);
			КонецЕсли;
			НоваяСтрока.Акциза                    = "без акциза";
			
			НомерСтроки = НомерСтроки + 1;
			
		КонецЦикла;
		
		СтруктураПараметров.СуммаБезНДСВсего = СтруктураПараметров.ТаблицаТоваров.Итог("СуммаБезНДС");
		СтруктураПараметров.СуммаСНДСВсего   = СтруктураПараметров.ТаблицаТоваров.Итог("Сумма");
		СтруктураПараметров.СуммаНДСВсего    = СтруктураПараметров.ТаблицаТоваров.Итог("СуммаНДС");
		СтруктураПараметров.ТолькоУслуги     = ТолькоУслуги;
		
		Если НЕ ТолькоУслуги Тогда
			
			// Сведения по грузоотправителю и грузополучаетелю
			Грузоотправитель = ?(ДанныеДляФормированияЭД.Грузоотправитель = "он же", 
					СтруктураЭД.Организация, 
					ДанныеДляФормированияЭД.Грузоотправитель);
			ЗаполнитьСведенияУчастникаОбмена(СтруктураПараметров.СведенияОГрузоотправителе.Грузоотправитель, ПолучитьДанныеЮрФизЛица(Грузоотправитель));
			
			Грузополучатель = ?(ДанныеДляФормированияЭД.Грузополучатель = "он же", 
				СтруктураЭД.Контрагент, 
				ДанныеДляФормированияЭД.Грузополучатель);
			ЗаполнитьСведенияУчастникаОбмена(СтруктураПараметров.Грузополучатель, ПолучитьДанныеЮрФизЛица(Грузополучатель));
			
		КонецЕсли;
		
		// Таблица оплат заполняется только для не корректировочного счета-фактуры
		Для каждого Строка Из ДанныеДляФормированияЭД.ТаблицаДатОплат Цикл
			
			НоваяСтрока = СтруктураПараметров.ПлатежныеДокументы.Добавить();
			
			НоваяСтрока.ДатаПРД  = Строка.ДатаПлатежноРасчетногоДокумента;
			НоваяСтрока.НомерПРД = Строка.НомерПлатежноРасчетногоДокумента;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ЭтоФизЛицо = ЭтоФизЛицо(СтруктураЭД.Организация);
	
	СтруктураПараметров.КодВалюты      = глЗначениеПеременной("ВалютаРегламентированногоУчета").Код;
	
	СведенияОПоставщике = ПолучитьДанныеЮрФизЛица(СтруктураЭД.Организация);
	СведенияОПокупателе = ПолучитьДанныеЮрФизЛица(СтруктураЭД.Контрагент);
	
	ЗаполнитьСведенияУчастникаОбмена(СтруктураПараметров.Организация, СведенияОПоставщике);
	ЗаполнитьСведенияУчастникаОбмена(СтруктураПараметров.Контрагент, СведенияОПокупателе);
	
	// Заполним документы-основания
	МассивДокументовОснований = Новый Массив;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СчетФактураВыданныйДокументыОснования.ДокументОснование КАК ДокументОснование
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|ГДЕ
	|	СчетФактураВыданныйДокументыОснования.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъект);
	
	МассивДокументовОснований = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ДокументОснование");
	
	Если ЗначениеЗаполнено(МассивДокументовОснований) Тогда
		СтруктураПараметров.ДокументыОснования = МассивДокументовОснований;
	КонецЕсли;
	
	// Заполним данные по подписанту
	ЭтоФизЛицо = ЭтоФизЛицо(СтруктураЭД.Организация);
	ДанныеЮрФизЛица = ПолучитьДанныеЮрФизЛица(СтруктураЭД.Организация);
	ФИО       = "";
	Должность = Неопределено;
	
	Если ЭтоФизЛицо Тогда
		ФИО = ДанныеЮрФизЛица.ПолноеНаименование;
		ДанныеСвидетельстваОРегистрацииИП(ДанныеДляФормированияЭД.Организация, СтруктураПараметров.Подписант.СвидетельствоОРегистрацииИП);
	Иначе
		Руководитель = ОтветственноеЛицоОрганизации(
			РеквизитыСФ.Дата, 
			ДанныеДляФормированияЭД.Организация, 
			Перечисления.ОтветственныеЛицаОрганизации.Руководитель);
		
		ФИО       = Руководитель.ФИО;
		Должность = Руководитель.Должность;
	КонецЕсли;
	
	СтруктураПараметров.Подписант.ЭтоФизЛицо = ЭтоФизЛицо;
	ЗаполнитьФИОиДолжность(СтруктураПараметров.Подписант, ФИО, Должность);
	СтруктураПараметров.Подписант.ИНН = ДанныеЮрФизЛица.ИНН;
	
КонецПроцедуры

// Устаревшая процедура, вместо этой процедуры следует использовать ЗаполнитьДанныеПоКорректировочномуСчетуФактуреФНС.
// Подготавливает данные для электронного документа типа КорректировочныйСчетФактураВыданный.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//
Процедура ПодготовитьДанныеПоКорректировочномуСчетуФактуре(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметров) Экспорт
	
	ДанныеДляФормированияЭД = Неопределено;
	УчетнаяПолитика = Неопределено;
	
	СсылкаНаОбъект.ПолучитьОбъект().СобратьДанныеДляПечати(ДанныеДляФормированияЭД, УчетнаяПолитика);
	
	Если ТипЗнч(ДанныеДляФормированияЭД) = Тип("Соответствие") Тогда
		СтруктураПараметров.Вставить("ДанныеПодготовлены", Ложь);
		Возврат;
	ИначеЕсли ДанныеДляФормированияЭД = Неопределено Тогда
		СтруктураПараметров.Вставить("ДанныеПодготовлены", Ложь);
		Возврат;
	КонецЕсли;
	
	ВалютаДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаОбъект, "ВалютаДокумента");
	Если ВалютаДокумента <> глЗначениеПеременной("ВалютаРегламентированногоУчета") Тогда
		ОкруглитьЧисловыеПоля(ДанныеДляФормированияЭД);
	КонецЕсли;
		
	РеквизитыСФ = Новый Структура("Номер, Дата,
								|Исправление, НомерИсправления,
								|НомерИсправляемогоКорректировочногоДокумента, ДатаИсправляемогоКорректировочногоДокумента,
								|НомерИсходногоДокумента, ДатаИсходногоДокумента,
								|УчитыватьИсправлениеИсходногоДокумента, НомерИсправленияИсходногоДокумента, ДатаИсправленияИсходногоДокумента");
	
	ЗапросРеквизитовОснований = Новый Запрос();
	ЗапросРеквизитовОснований.Параметры.Вставить("Ссылка", СсылкаНаОбъект.Ссылка);
	ЗапросРеквизитовОснований.Текст = 
	"ВЫБРАТЬ
	|	СчетФактураВыданныйДокументыОснования.НомерИсходногоДокумента КАК НомерИсходногоДокумента,
	|	СчетФактураВыданныйДокументыОснования.ДатаИсходногоДокумента КАК ДатаИсходногоДокумента,
	|	СчетФактураВыданныйДокументыОснования.УчитыватьИсправлениеИсходногоДокумента КАК УчитыватьИсправлениеИсходногоДокумента,
	|	СчетФактураВыданныйДокументыОснования.НомерИсправленияИсходногоДокумента КАК НомерИсправленияИсходногоДокумента,
	|	СчетФактураВыданныйДокументыОснования.ДатаИсправленияИсходногоДокумента КАК ДатаИсправленияИсходногоДокумента,
	|	СчетФактураВыданный.Номер КАК Номер,
	|	СчетФактураВыданный.Дата КАК Дата,
	|	СчетФактураВыданный.Исправление КАК Исправление,
	|	СчетФактураВыданный.НомерИсправления КАК НомерИсправления,
	|	СчетФактураВыданный.НомерИсправляемогоКорректировочногоДокумента КАК НомерИсправляемогоКорректировочногоДокумента,
	|	СчетФактураВыданный.ДатаИсправляемогоКорректировочногоДокумента КАК ДатаИсправляемогоКорректировочногоДокумента
	|ИЗ
	|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ПО СчетФактураВыданный.Ссылка = СчетФактураВыданныйДокументыОснования.Ссылка
	|			И (СчетФактураВыданныйДокументыОснования.НомерСтроки = 1)
	|ГДЕ
	|	СчетФактураВыданныйДокументыОснования.Ссылка = &Ссылка";
	Выборка = ЗапросРеквизитовОснований.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(РеквизитыСФ, Выборка);
	КонецЕсли;
	
	СведенияОПокупателе = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(ДанныеДляФормированияЭД.Покупатель, РеквизитыСФ.Дата);
	СведенияОПоставщике  = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(ДанныеДляФормированияЭД.Поставщик, РеквизитыСФ.Дата);
	
	Если РеквизитыСФ.Исправление Тогда
		СтруктураПараметров.НомерКоррСчФ            = РеквизитыСФ.НомерИсправляемогоКорректировочногоДокумента;
		СтруктураПараметров.ДатаКоррСчФ             = РеквизитыСФ.ДатаИсправляемогоКорректировочногоДокумента;
		СтруктураПараметров.НомерИсправленияКоррСчФ = РеквизитыСФ.НомерИсправления;
		СтруктураПараметров.ДатаИсправленияКоррСчФ  = РеквизитыСФ.Дата;
	Иначе
		СтруктураПараметров.НомерКоррСчФ = ПолучитьПечатныйНомерДокумента(СсылкаНаОбъект);
		СтруктураПараметров.ДатаКоррСчФ  = РеквизитыСФ.Дата;
	КонецЕсли;
	
	СтруктураПараметров.НомерСчФ = РеквизитыСФ.НомерИсходногоДокумента;
	СтруктураПараметров.ДатаСчФ  = РеквизитыСФ.ДатаИсходногоДокумента;
	
	Если РеквизитыСФ.УчитыватьИсправлениеИсходногоДокумента Тогда
		СтруктураПараметров.НомерИсправленияИсходногоСчФ = РеквизитыСФ.НомерИсправленияИсходногоДокумента;
		СтруктураПараметров.ДатаИсправленияИсходногоСчФ  = РеквизитыСФ.ДатаИсправленияИсходногоДокумента;
	КонецЕсли;
	
	// Подготовим таблицу товаров
	НомерСтроки = 1;
	Для каждого Строка Из ДанныеДляФормированияЭД.ТабличнаяЧасть Цикл
		
		НоваяСтрока = СтруктураПараметров.ТаблицаТоваров.Добавить();
		НоваяСтрока.НомерСтроки              = НомерСтроки;
		НоваяСтрока.НаименованиеНоменклатуры = Строка.НаименованиеТовара;
		НоваяСтрока.ЕдиницаИзмеренияКодДо    = Строка.ЕдиницаИзмеренияКод;
		НоваяСтрока.ЕдиницаИзмеренияКод      = Строка.ЕдиницаИзмеренияКод;
		НоваяСтрока.КоличествоДо             = Строка.КоличествоДоИзменения;
		НоваяСтрока.Количество               = Строка.КоличествоПослеИзменения;
		НоваяСтрока.ЦенаДо                   = Строка.ЦенаДоИзменения;
		НоваяСтрока.Цена                     = Строка.ЦенаПослеИзменения;
		НоваяСтрока.СуммаБезНДСДо            = Строка.СтоимостьБезНДСДоИзменения;
		НоваяСтрока.СуммаБезНДС              = Строка.СтоимостьБезНДСПослеИзменения;
		НоваяСтрока.СуммаБезНДСУвеличение    = Строка.РазницаБезНДСУвеличение;
		НоваяСтрока.СуммаБезНДСУменьшение    = Строка.РазницаБезНДСУменьшение;
		НоваяСтрока.СуммаДо                  = Строка.СтоимостьСНДСДоИзменения;
		НоваяСтрока.Сумма                    = Строка.СтоимостьСНДСПослеИзменения;
		НоваяСтрока.СуммаУвеличение          = Строка.РазницаСНДСУвеличение;
		НоваяСтрока.СуммаУменьшение          = Строка.РазницаСНДСУменьшение;
		НоваяСтрока.СтавкаНДСДо              = ПредставлениеСтавкиНДС(Строка.СтавкаНДС);
		НоваяСтрока.СтавкаНДС                = ПредставлениеСтавкиНДС(Строка.СтавкаНДС);
		НоваяСтрока.СтавкаНДСТипДо           = ТипСтавкиНДС(Строка.СтавкаНДС);
		НоваяСтрока.СтавкаНДСТип             = ТипСтавкиНДС(Строка.СтавкаНДС);
		НоваяСтрока.СуммаНДСДо               = Строка.СуммаНДСДоИзменения;
		НоваяСтрока.СуммаНДС                 = Строка.СуммаНДСПослеИзменения;
		НоваяСтрока.СуммаНДСУвеличение       = Строка.РазницаНДСУвеличение;
		НоваяСтрока.СуммаНДСУменьшение       = Строка.РазницаНДСУменьшение;
		НоваяСтрока.АкцизаДо                 = "без акциза";
		НоваяСтрока.Акциза                   = "без акциза";
		
		НомерСтроки = НомерСтроки + 1;
		
	КонецЦикла;
	
	СтруктураПараметров.СуммаБезНДСВсегоУвеличение = СтруктураПараметров.ТаблицаТоваров.Итог("СуммаБезНДСУвеличение");
	СтруктураПараметров.СуммаСНДСВсегоУвеличение   = СтруктураПараметров.ТаблицаТоваров.Итог("СуммаУвеличение");
	СтруктураПараметров.СуммаНДСУвеличение         = СтруктураПараметров.ТаблицаТоваров.Итог("СуммаНДСУвеличение");
	
	СтруктураПараметров.СуммаБезНДСВсегоУменьшение = СтруктураПараметров.ТаблицаТоваров.Итог("СуммаБезНДСУменьшение");
	СтруктураПараметров.СуммаСНДСВсегоУменьшение   = СтруктураПараметров.ТаблицаТоваров.Итог("СуммаУменьшение");
	СтруктураПараметров.СуммаНДСУменьшение         = СтруктураПараметров.ТаблицаТоваров.Итог("СуммаНДСУменьшение");
	

	ЭтоФизЛицо = ЭтоФизЛицо(СтруктураЭД.Организация);
	
	СтруктураПараметров.КодВалюты      = глЗначениеПеременной("ВалютаРегламентированногоУчета").Код;
	
	СведенияОПоставщике = ПолучитьДанныеЮрФизЛица(СтруктураЭД.Организация);
	СведенияОПокупателе = ПолучитьДанныеЮрФизЛица(СтруктураЭД.Контрагент);
	
	ЗаполнитьСведенияУчастникаОбмена(СтруктураПараметров.Организация, СведенияОПоставщике);
	ЗаполнитьСведенияУчастникаОбмена(СтруктураПараметров.Контрагент, СведенияОПокупателе);
	
	// Заполним документы-основания
	МассивДокументовОснований = Новый Массив;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СчетФактураВыданныйДокументыОснования.ДокументОснование КАК ДокументОснование
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|ГДЕ
	|	СчетФактураВыданныйДокументыОснования.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъект);
	
	МассивДокументовОснований = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ДокументОснование");
	
	Если ЗначениеЗаполнено(МассивДокументовОснований) Тогда
		СтруктураПараметров.ДокументыОснования = МассивДокументовОснований;
	КонецЕсли;
	
	// Заполним данные по подписанту
	ЭтоФизЛицо = ЭтоФизЛицо(СтруктураЭД.Организация);
	ДанныеЮрФизЛица = ПолучитьДанныеЮрФизЛица(СтруктураЭД.Организация);
	ФИО       = "";
	Должность = Неопределено;
	
	Если ЭтоФизЛицо Тогда
		ФИО = ДанныеЮрФизЛица.ПолноеНаименование;
		ДанныеСвидетельстваОРегистрацииИП(ДанныеДляФормированияЭД.Организация, СтруктураПараметров.Подписант.СвидетельствоОРегистрацииИП);
	Иначе
		Руководитель = ОтветственноеЛицоОрганизации(
			РеквизитыСФ.Дата, 
			ДанныеДляФормированияЭД.Организация, 
			Перечисления.ОтветственныеЛицаОрганизации.Руководитель);
		
		ФИО       = Руководитель.ФИО;
		Должность = Руководитель.Должность;
	КонецЕсли;
	
	СтруктураПараметров.Подписант.ЭтоФизЛицо = ЭтоФизЛицо;
	ЗаполнитьФИОиДолжность(СтруктураПараметров.Подписант, ФИО, Должность);
	СтруктураПараметров.Подписант.ИНН = ДанныеЮрФизЛица.ИНН;
	
КонецПроцедуры

// Устаревшие: Работа со структурой данных CML

// Устаревшая процедура, вместо этой процедуры следует использовать ЗаполнитьДанныеПоКаталогуТоваровCML.
// Подготавливает данные для электронного документа типа КаталогТоваров.
//
// Параметры: 
// СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
// ТоварыКаталога - Массив, список товаров для заполнения каталога.
// СтруктураЭД - Структура, структура данных для формирования электронного документа.
// СтруктураПараметров - структура - параметры заполнения электронного документа.
//
Процедура ПодготовитьДанныеПоКаталогуТоваров(СсылкаНаОбъект, ТоварыКаталога, СтруктураЭД, СтруктураПараметров) Экспорт
	
	ТаблицаТоваров = СтруктураПараметров.ТаблицаТоваров.СкопироватьКолонки();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТоварыКаталога.Номенклатура,
	|	ТоварыКаталога.ХарактеристикаНоменклатуры,
	|	ТоварыКаталога.ЕдиницаИзмерения
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ
	|	&ТоварыКаталога КАК ТоварыКаталога
	|;
	|
	|//////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Номенклатура.Артикул								 				КАК Артикул,
	|	ТаблицаТоваров.Номенклатура.НаименованиеПолное 									КАК Наименование,
	|	ТаблицаТоваров.ХарактеристикаНоменклатуры.Наименование 							КАК НаименованиеХарактеристики,
	|	ТаблицаТоваров.Номенклатура														КАК Номенклатура,
	|	ТаблицаТоваров.ХарактеристикаНоменклатуры 										КАК Характеристика,
	|	ТаблицаТоваров.Номенклатура.БазоваяЕдиницаИзмерения 							КАК БазоваяЕдиница,
	|	ТаблицаТоваров.Номенклатура.БазоваяЕдиницаИзмерения.Код 						КАК БазоваяЕдиницаКод,
	|	ТаблицаТоваров.Номенклатура.БазоваяЕдиницаИзмерения.Наименование 				КАК БазоваяЕдиницаНаименование,
	|	ТаблицаТоваров.Номенклатура.БазоваяЕдиницаИзмерения.НаименованиеПолное 			КАК БазоваяЕдиницаНаименованиеПолное,
	|	ТаблицаТоваров.Номенклатура.БазоваяЕдиницаИзмерения.МеждународноеСокращение		КАК БазоваяЕдиницаМеждународноеСокращение,
	|	ТаблицаТоваров.ЕдиницаИзмерения.Код 											КАК УпаковкаКод,
	|	ТаблицаТоваров.ЕдиницаИзмерения.Наименование 									КАК УпаковкаНаименование,
	|	ТаблицаТоваров.ЕдиницаИзмерения.Код												КАК УпаковкаПоОКЕИ
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров";
	
	Запрос.УстановитьПараметр("ТоварыКаталога", ТоварыКаталога);
	
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(Запрос.Выполнить().Выгрузить(), ТаблицаТоваров);
	ОбработатьТаблицуТоваров(ТаблицаТоваров);
	
	СтруктураПараметров.Вставить("Исполнитель", 		СтруктураЭД.Отправитель);
	СтруктураПараметров.Вставить("ВерсияСхемы", 		"4.02");
	СтруктураПараметров.Вставить("ТаблицаТоваров", 		ТаблицаТоваров);
	СтруктураПараметров.Вставить("Организация", 		СтруктураЭД.Организация);
	СтруктураПараметров.Вставить("Контрагент", 			СтруктураЭД.Контрагент);
	СтруктураПараметров.Вставить("Ид", 					СтруктураЭД.НомерЭД);
	СтруктураПараметров.Вставить("ДатаФормирования",	ТекущаяДатаСеанса());
	СтруктураПараметров.Вставить("ВидЭД", 				СтруктураЭД.ВидЭД);
	СтруктураПараметров.Вставить("НаправлениеЭД", 		СтруктураЭД.НаправлениеЭД);
	
КонецПроцедуры

// Устаревшая процедура, вместо этой процедуры следует использовать ЗаполнитьДанныеПоПрайсЛисту.
// Подготавливает данные для электронного документа типа ПрайсЛист.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  СтруктураПараметров - структура - параметры заполнения электронного документа.
//
Процедура ПодготовитьДанныеПоПрайсЛисту(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметров) Экспорт
	
	
	
КонецПроцедуры

// Устаревшая процедура, вместо этой процедуры следует использовать ЗаполнитьДанныеПоСчету.
// Подготавливает данные для электронного документа типа Счет.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  СтруктураПараметров - структура - параметры заполнения электронного документа.
//
Процедура ПодготовитьДанныеПоСчету(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметров) Экспорт

	ОписаниеТиповКоличество = Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(0, 4));
	ОписаниеТиповСумма      = Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(18, 2));
	
	ТаблицаТоваров = Новый ТаблицаЗначений;
	ТаблицаТоваров.Колонки.Добавить("Ид");
	ТаблицаТоваров.Колонки.Добавить("Артикул");
	ТаблицаТоваров.Колонки.Добавить("Наименование");
	ТаблицаТоваров.Колонки.Добавить("Номенклатура");
	ТаблицаТоваров.Колонки.Добавить("Характеристика");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиница");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаКод");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаНаименование");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаНаименованиеПолное");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаМеждународноеСокращение");
	ТаблицаТоваров.Колонки.Добавить("Количество",    ОписаниеТиповКоличество);
	ТаблицаТоваров.Колонки.Добавить("Упаковка");
	ТаблицаТоваров.Колонки.Добавить("УпаковкаКод");
	ТаблицаТоваров.Колонки.Добавить("УпаковкаНаименование");
	ТаблицаТоваров.Колонки.Добавить("Коэффициент");
	ТаблицаТоваров.Колонки.Добавить("СуммаБезНДС",   ОписаниеТиповСумма);
	ТаблицаТоваров.Колонки.Добавить("СуммаСНДС",     ОписаниеТиповСумма);
	ТаблицаТоваров.Колонки.Добавить("СтавкаНДС");
	ТаблицаТоваров.Колонки.Добавить("СуммаНДС",      ОписаниеТиповСумма);
	ТаблицаТоваров.Колонки.Добавить("Цена",          ОписаниеТиповСумма);
	ТаблицаТоваров.Колонки.Добавить("СуммаСкидки",   ОписаниеТиповСумма);
	ТаблицаТоваров.Колонки.Добавить("Сумма",         ОписаниеТиповСумма);
	ТаблицаТоваров.Колонки.Добавить("КодТовара");
	
	РеквизитыДокумента = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(СсылкаНаОбъект, 
		"Дата, Номер, Организация, СуммаВключаетНДС, ВалютаДокумента, КурсВзаиморасчетов, СтруктурнаяЕдиница, ДатаОплаты");
	
	СтрокаВыборкиПоляСодержания    = ОбработкаТабличныхЧастей.ПолучитьЧастьЗапросаДляВыбораСодержания("СчетНаОплатуПокупателю");

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СчетНаОплатуПокупателю.Номенклатура.Код                                        КАК КодТовара,
	|	СчетНаОплатуПокупателю.Номенклатура.Артикул			                           КАК Артикул,
	|	СчетНаОплатуПокупателю.Номенклатура.НаименованиеПолное                         КАК Наименование,
	|	СчетНаОплатуПокупателю.Номенклатура                                            КАК Номенклатура,
	|	СчетНаОплатуПокупателю.ХарактеристикаНоменклатуры                              КАК Характеристика,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения                    КАК БазоваяЕдиница,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения.Код                КАК БазоваяЕдиницаКод,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения.Наименование       КАК БазоваяЕдиницаНаименование,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения.НаименованиеПолное КАК БазоваяЕдиницаНаименованиеПолное,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения.МеждународноеСокращение КАК БазоваяЕдиницаМеждународноеСокращение,
	|	СчетНаОплатуПокупателю.ЕдиницаИзмерения.ЕдиницаПоКлассификатору                КАК ЕдиницаИзмерения,
	|	СчетНаОплатуПокупателю.ЕдиницаИзмерения.ЕдиницаПоКлассификатору                КАК Упаковка,
	|	СчетНаОплатуПокупателю.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код            КАК УпаковкаКод,
	|	СчетНаОплатуПокупателю.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Наименование   КАК УпаковкаНаименование,
	|	СчетНаОплатуПокупателю.ЕдиницаИзмерения.Коэффициент					           КАК Коэфиициент,
	|	СчетНаОплатуПокупателю.Цена,
	|	СчетНаОплатуПокупателю.Сумма,
	|	СчетНаОплатуПокупателю.СтавкаНДС,
	|	СчетНаОплатуПокупателю.СуммаНДС,
	|	СчетНаОплатуПокупателю.Количество
	|ИЗ
	|	Документ.СчетНаОплатуПокупателю.Товары КАК СчетНаОплатуПокупателю
	|ГДЕ
	|	СчетНаОплатуПокупателю.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетНаОплатуПокупателю.Номенклатура.Код,
	|	СчетНаОплатуПокупателю.Номенклатура.Артикул,
	|	" + СтрокаВыборкиПоляСодержания + ",
	|	СчетНаОплатуПокупателю.Номенклатура,
	|	"""",
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения.Код,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения.Наименование,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения.НаименованиеПолное,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения.МеждународноеСокращение,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения.Код,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения.Наименование,
	|   1,
	|	СчетНаОплатуПокупателю.Цена,
	|	СчетНаОплатуПокупателю.Сумма,
	|	СчетНаОплатуПокупателю.СтавкаНДС,
	|	СчетНаОплатуПокупателю.СуммаНДС,
	|	СчетНаОплатуПокупателю.Количество
	|ИЗ
	|	Документ.СчетНаОплатуПокупателю.Услуги КАК СчетНаОплатуПокупателю
	|ГДЕ
	|	СчетНаОплатуПокупателю.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъект);
	
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(Запрос.Выполнить().Выгрузить(), ТаблицаТоваров);
	ОбработатьТаблицуТоваров(ТаблицаТоваров);
	
	Для каждого СтрокаТаблицы из ТаблицаТоваров Цикл
		
		СуммаСНДС   = Окр((СтрокаТаблицы.Сумма + ?(РеквизитыДокумента.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС)), 2);
		СуммаБезНДС = СуммаСНДС - СтрокаТаблицы.СуммаНДС;
		
		СтрокаТаблицы.СуммаБезНДС = СуммаБезНДС;
		СтрокаТаблицы.СуммаСНДС   = СуммаСНДС;
		
	КонецЦикла;

	// получение данных по таблицам товаров
	СтруктураПараметров.Вставить("Исполнитель",      СтруктураЭД.Отправитель);
	СтруктураПараметров.Вставить("ВерсияСхемы",      "4.02");
	СтруктураПараметров.Вставить("ТаблицаТоваров",   ТаблицаТоваров);
	СтруктураПараметров.Вставить("Организация",      СтруктураЭД.Организация);
	СтруктураПараметров.Вставить("Контрагент",       СтруктураЭД.Контрагент);
	СтруктураПараметров.Вставить("Ид",               СтруктураЭД.НомерЭД);
	СтруктураПараметров.Вставить("ДатаФормирования", ТекущаяДатаСеанса());
	СтруктураПараметров.Вставить("Номер",            СтруктураЭД.НомерДокументаОтправителя);
	СтруктураПараметров.Вставить("Дата",             СтруктураЭД.ДатаДокументаОтправителя);
	СтруктураПараметров.Вставить("Валюта",           РеквизитыДокумента.ВалютаДокумента);
	СтруктураПараметров.Вставить("Курс",             РеквизитыДокумента.КурсВзаиморасчетов);
	СтруктураПараметров.Вставить("Сумма",            ТаблицаТоваров.Итог("СуммаСНДС"));
	СтруктураПараметров.Вставить("ЦенаВключаетНДС",  РеквизитыДокумента.СуммаВключаетНДС);
	СтруктураПараметров.Вставить("СуммаНДС",         ТаблицаТоваров.Итог("СуммаНДС"));
	СтруктураПараметров.Вставить("ВидЭД",            СтруктураЭД.ВидЭД);
	СтруктураПараметров.Вставить("НаправлениеЭД",    СтруктураЭД.НаправлениеЭД);
	
	СтруктураПараметров.Вставить("СрокПлатежа", РеквизитыДокумента.ДатаОплаты);
	СтруктураПараметров.Вставить("НазначениеПлатежа", "");
	
	Если ЗначениеЗаполнено(РеквизитыДокумента.СтруктурнаяЕдиница)
		И ТипЗнч(РеквизитыДокумента.СтруктурнаяЕдиница) = Тип("СправочникСсылка.БанковскиеСчета") Тогда
		
		РасчетныйСчет  = РеквизитыДокумента.СтруктурнаяЕдиница;
		РеквизитыБанка = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(РасчетныйСчет.Банк, 
			"Наименование, КоррСчет, Код");
		
		РасчетныйСчетСтруктура = Новый Структура;
		РасчетныйСчетСтруктура.Вставить("НомерСчета", РасчетныйСчет.НомерСчета);
		РасчетныйСчетСтруктура.Вставить("Банк",       РеквизитыБанка.Наименование);
		РасчетныйСчетСтруктура.Вставить("КоррСчет",   РеквизитыБанка.КоррСчет);
		РасчетныйСчетСтруктура.Вставить("БИК",        РеквизитыБанка.Код);
		СтруктураПараметров.Вставить("РасчетныйСчет", РасчетныйСчетСтруктура);
		
		Если ЗначениеЗаполнено(РасчетныйСчет.БанкДляРасчетов) Тогда
			
			РеквизитыБанкаДляРасчетов = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(РасчетныйСчет.БанкДляРасчетов, 
				"Наименование, КоррСчет, Код");
			
			БанкКорреспондентСтруктура = Новый Структура;
			БанкКорреспондентСтруктура.Вставить("Банк",     РеквизитыБанкаДляРасчетов.Наименование);
			БанкКорреспондентСтруктура.Вставить("КоррСчет", РеквизитыБанкаДляРасчетов.КоррСчет);
			БанкКорреспондентСтруктура.Вставить("БИК",      РеквизитыБанкаДляРасчетов.Код);
			СтруктураПараметров.Вставить("БанкКорреспондент", БанкКорреспондентСтруктура);
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Получим данные по ответственным лица
	Руководитель = ОтветственноеЛицоОрганизации(
		РеквизитыДокумента.Дата, 
		РеквизитыДокумента.Организация, 
		Перечисления.ОтветственныеЛицаОрганизации.Руководитель);
		
	ГлавныйБухгалтер = ОтветственноеЛицоОрганизации(
		РеквизитыДокумента.Дата, 
		РеквизитыДокумента.Организация, 
		Перечисления.ОтветственныеЛицаОрганизации.ГлавныйБухгалтер);
	
	СтруктураПараметров.Вставить("Руководитель", Руководитель.ФИО);
	СтруктураПараметров.Вставить("Бухгалтер",    ГлавныйБухгалтер.ФИО);
	
	ИтоговаяСтрока = НСтр("ru='Всего наименований %Количество%, на сумму %Сумма%'");
	ИтоговаяСтрока = СтрЗаменить(ИтоговаяСтрока, "%Количество%", ТаблицаТоваров.Количество());
	ИтоговаяСтрока = СтрЗаменить(ИтоговаяСтрока, "%Сумма%",		 ОбщегоНазначения.ФорматСумм(СсылкаНаОбъект.СуммаДокумента, СсылкаНаОбъект.ВалютаДокумента));
	СуммаПрописью  = ОбщегоНазначения.СформироватьСуммуПрописью(СсылкаНаОбъект.СуммаДокумента, СсылкаНаОбъект.ВалютаДокумента);
	ИтоговаяСтрока = ИтоговаяСтрока + Символы.ПС + СуммаПрописью;
	СтруктураПараметров.Вставить("ИтогиПрописью", ИтоговаяСтрока);
	
	СтруктураПараметров.Вставить("ОбязательныеПоля", "Организация, Контрагент, Ид, ДатаФормирования,
		|Номер, Дата, ВидЭД, НаправлениеЭД, РасчетныйСчет, ТаблицаТоваров");
	СтруктураПараметров.Вставить("ОбязательныеПоляТаблицыЗначений", "Ид, Наименование, БазоваяЕдиницаКод, УпаковкаКод");
	
КонецПроцедуры

// Устаревшая процедура, вместо этой процедуры следует использовать ЗаполнитьДанныеПоЗаказуТоваров.
// Подготавливает данные для электронного документа типа ЗаказТоваров.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  СтруктураПараметров - структура - параметры заполнения электронного документа.
//
Процедура ПодготовитьДанныеПоЗаказуТоваров(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметров) Экспорт
	
	ТаблицаТоваров = СтруктураПараметров.ТаблицаТоваров.СкопироватьКолонки();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(НоменклатураПоставщика.Идентификатор, НЕОПРЕДЕЛЕНО) КАК Ид,
	|	ЕСТЬNULL(НоменклатураПоставщика.АртикулНоменклатурыКонтрагента, """") КАК Артикул,
	|	ЕСТЬNULL(НоменклатураПоставщика.НаименованиеНоменклатурыКонтрагента, """") КАК Наименование,
	|	ДокЗаказ.Номенклатура КАК Номенклатура,
	|	ДокЗаказ.ХарактеристикаНоменклатуры КАК Характеристика,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения КАК БазоваяЕдиница,
	|	ДокЗаказ.Количество КАК Количество,
	|	ДокЗаказ.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код КАК УпаковкаПоОКЕИ,
	|	ДокЗаказ.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Наименование КАК Упаковка,
	|	ВЫБОР
	|		КОГДА ДокЗаказ.Ссылка.СуммаВключаетНДС
	|			ТОГДА ДокЗаказ.Сумма - ДокЗаказ.СуммаНДС
	|		ИНАЧЕ ДокЗаказ.Сумма
	|	КОНЕЦ КАК СуммаБезНДС,
	|	ДокЗаказ.СтавкаНДС КАК СтавкаНДС,
	|	ДокЗаказ.СуммаНДС КАК СуммаНДС,
	|	ДокЗаказ.Цена КАК Цена,
	|	0 КАК СуммаСкидки,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.Код КАК БазоваяЕдиницаКод,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.Наименование КАК БазоваяЕдиницаНаименование,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.НаименованиеПолное КАК БазоваяЕдиницаНаименованиеПолное,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.МеждународноеСокращение КАК БазоваяЕдиницаМеждународноеСокращение,
	|	ДокЗаказ.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код КАК УпаковкаКод,
	|	ДокЗаказ.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Наименование КАК УпаковкаНаименование,
	|	ДокЗаказ.ЕдиницаИзмерения.Коэффициент КАК Коэффициент,
	|	ВЫБОР
	|		КОГДА ДокЗаказ.Ссылка.СуммаВключаетНДС
	|			ТОГДА ДокЗаказ.Сумма
	|		ИНАЧЕ ДокЗаказ.Сумма + ДокЗаказ.СуммаНДС
	|	КОНЕЦ КАК СуммаСНДС,
	|	ДокЗаказ.Сумма КАК Сумма,
	|	ВЫРАЗИТЬ(ДокЗаказ.Номенклатура.ДополнительноеОписаниеНоменклатуры КАК СТРОКА(1000)) КАК Описание
	|ИЗ
	|	Документ.ЗаказПоставщику.Товары КАК ДокЗаказ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураКонтрагентов КАК НоменклатураПоставщика
	|		ПО (НоменклатураПоставщика.Контрагент = ДокЗаказ.Ссылка.Контрагент)
	|			И (НоменклатураПоставщика.Номенклатура = ДокЗаказ.Номенклатура)
	|			И (НоменклатураПоставщика.ХарактеристикаНоменклатуры = ДокЗаказ.ХарактеристикаНоменклатуры)
	|ГДЕ
	|	ДокЗаказ.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(НоменклатураПоставщика.Идентификатор, НЕОПРЕДЕЛЕНО),
	|	ЕСТЬNULL(НоменклатураПоставщика.АртикулНоменклатурыКонтрагента, """"),
	|	ЕСТЬNULL(НоменклатураПоставщика.НаименованиеНоменклатурыКонтрагента, """"),
	|	ДокЗаказ.Номенклатура,
	|	НЕОПРЕДЕЛЕНО,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения,
	|	ДокЗаказ.Количество,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.Код,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.Наименование,
	|	ВЫБОР
	|		КОГДА ДокЗаказ.Ссылка.СуммаВключаетНДС
	|			ТОГДА ДокЗаказ.Сумма - ДокЗаказ.СуммаНДС
	|		ИНАЧЕ ДокЗаказ.Сумма
	|	КОНЕЦ,
	|	ДокЗаказ.СтавкаНДС,
	|	ДокЗаказ.СуммаНДС,
	|	ДокЗаказ.Цена,
	|	0,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.Код,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.Наименование,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.НаименованиеПолное,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.МеждународноеСокращение,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.Код,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.Наименование,
	|	1,
	|	ВЫБОР
	|		КОГДА ДокЗаказ.Ссылка.СуммаВключаетНДС
	|			ТОГДА ДокЗаказ.Сумма
	|		ИНАЧЕ ДокЗаказ.Сумма + ДокЗаказ.СуммаНДС
	|	КОНЕЦ,
	|	ДокЗаказ.Сумма,
	|	ДокЗаказ.Содержание
	|ИЗ
	|	Документ.ЗаказПоставщику.Услуги КАК ДокЗаказ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураКонтрагентов КАК НоменклатураПоставщика
	|		ПО (НоменклатураПоставщика.Контрагент = ДокЗаказ.Ссылка.Контрагент)
	|			И (НоменклатураПоставщика.Номенклатура = ДокЗаказ.Номенклатура)
	|			И (НоменклатураПоставщика.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	|ГДЕ
	|	ДокЗаказ.Ссылка = &Ссылка";
		
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъект);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Ид = Неопределено Тогда
			ОбщегоНазначения.СообщитьОбОшибке(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось сопоставить номенклатуру ""%1"" с номенклатурой поставщика'"),
				Строка(Выборка.Номенклатура) + ?(ЗначениеЗаполнено(Выборка.Характеристика), "("+Выборка.Характеристика+")", "")));
		КонецЕсли;
			
		НоваяСтрока = ТаблицаТоваров.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
	КонецЦикла;
	
	СтруктураПараметров.Вставить("Исполнитель",				СтруктураЭД.Отправитель);
	СтруктураПараметров.Вставить("ВерсияСхемы", 			"4.02");
	СтруктураПараметров.Вставить("Роль", 					"Покупатель");
	СтруктураПараметров.Вставить("ТаблицаТоваров", 			ТаблицаТоваров);
	СтруктураПараметров.Вставить("Организация", 			СтруктураЭД.Организация);
	СтруктураПараметров.Вставить("Контрагент", 				СтруктураЭД.Контрагент);
	СтруктураПараметров.Вставить("Ид", 						СтруктураЭД.НомерЭД);
	СтруктураПараметров.Вставить("ДатаФормирования",		ТекущаяДатаСеанса());
	СтруктураПараметров.Вставить("Номер", 					СтруктураЭД.НомерДокументаОтправителя);
	СтруктураПараметров.Вставить("Дата", 					СтруктураЭД.ДатаДокументаОтправителя);
	СтруктураПараметров.Вставить("Валюта",					СсылкаНаОбъект.ВалютаДокумента);
	СтруктураПараметров.Вставить("Курс", 					СсылкаНаОбъект.КурсВзаиморасчетов);
	СтруктураПараметров.Вставить("Сумма",					СсылкаНаОбъект.СуммаДокумента);
	СтруктураПараметров.Вставить("ЦенаВключаетНДС",			СсылкаНаОбъект.СуммаВключаетНДС);
	СтруктураПараметров.Вставить("СуммаНДС", 				СсылкаНаОбъект.Товары.Итог("СуммаНДС")+СсылкаНаОбъект.Услуги.Итог("СуммаНДС"));
	СтруктураПараметров.Вставить("ВидЭД", 					СтруктураЭД.ВидЭД);
	СтруктураПараметров.Вставить("НаправлениеЭД",			СтруктураЭД.НаправлениеЭД);
	СтруктураПараметров.Вставить("Комментарий", 			СсылкаНаОбъект.Комментарий);
	
	ИтоговаяСтрока = НСтр("ru='Всего наименований %Количество%, на сумму %Сумма%'");
	ИтоговаяСтрока = СтрЗаменить(ИтоговаяСтрока, "%Количество%", ТаблицаТоваров.Количество());
	ИтоговаяСтрока = СтрЗаменить(ИтоговаяСтрока, "%Сумма%",		 ОбщегоНазначения.ФорматСумм(СсылкаНаОбъект.СуммаДокумента, СсылкаНаОбъект.ВалютаДокумента));
	СуммаПрописью  = ОбщегоНазначения.СформироватьСуммуПрописью(СсылкаНаОбъект.СуммаДокумента, СсылкаНаОбъект.ВалютаДокумента);
	ИтоговаяСтрока = ИтоговаяСтрока + Символы.ПС + СуммаПрописью;
	СтруктураПараметров.Вставить("ИтогиПрописью", ИтоговаяСтрока);
	
КонецПроцедуры

// Устаревшая процедура, вместо этой процедуры следует использовать ЗаполнитьДанныеПоОтветуНаЗаказ.
// Подготавливает данные для электронного документа типа ОтветНаЗаказ.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  СтруктураПараметров - структура - параметры заполнения электронного документа.
//
// УСТАРЕВШАЯ ПРОЦЕДУРА
Процедура ПодготовитьДанныеПоОтветуНаЗаказ(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметров) Экспорт
	
	ТаблицаТоваров = СтруктураПараметров.ТаблицаТоваров.СкопироватьКолонки();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокЗаказ.Номенклатура.Код КАК ИД,
	|	ДокЗаказ.Номенклатура.Артикул КАК Артикул,
	|	ДокЗаказ.Номенклатура.НаименованиеПолное КАК Наименование,
	|	ДокЗаказ.Номенклатура КАК Номенклатура,
	|	ДокЗаказ.ХарактеристикаНоменклатуры КАК Характеристика,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения КАК БазоваяЕдиница,
	|	ДокЗаказ.Количество КАК Количество,
	|	ДокЗаказ.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код КАК УпаковкаПоОКЕИ,
	|	ДокЗаказ.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Наименование КАК Упаковка,
	|	ДокЗаказ.СтавкаНДС КАК СтавкаНДС,
	|	ДокЗаказ.СуммаНДС КАК СуммаНДС,
	|	ДокЗаказ.Цена КАК Цена,
	|	0 КАК СуммаСкидки,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.Код КАК БазоваяЕдиницаКод,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.Наименование КАК БазоваяЕдиницаНаименование,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.НаименованиеПолное КАК БазоваяЕдиницаНаименованиеПолное,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.МеждународноеСокращение КАК БазоваяЕдиницаМеждународноеСокращение,
	|	ДокЗаказ.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код КАК УпаковкаКод,
	|	ДокЗаказ.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Наименование КАК УпаковкаНаименование,
	|	ДокЗаказ.ЕдиницаИзмерения.Коэффициент КАК Коэффициент,
	|	ВЫБОР
	|		КОГДА ДокЗаказ.Ссылка.СуммаВключаетНДС
	|			ТОГДА ДокЗаказ.Сумма
	|		ИНАЧЕ ДокЗаказ.Сумма - ДокЗаказ.СуммаНДС
	|	КОНЕЦ КАК СуммаСНДС,
	|	ДокЗаказ.Сумма КАК Сумма,
	|	ВЫРАЗИТЬ(ДокЗаказ.Номенклатура.ДополнительноеОписаниеНоменклатуры КАК СТРОКА(1000)) КАК Описание
	|ИЗ
	|	Документ.ЗаказПокупателя.Товары КАК ДокЗаказ
	|ГДЕ
	|	ДокЗаказ.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокЗаказ.Номенклатура.Код,
	|	ДокЗаказ.Номенклатура.Артикул,
	|	ДокЗаказ.Номенклатура.НаименованиеПолное,
	|	ДокЗаказ.Номенклатура,
	|	НЕОПРЕДЕЛЕНО,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения,
	|	ДокЗаказ.Количество,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.Код,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.Наименование,
	|	ДокЗаказ.СтавкаНДС,
	|	ДокЗаказ.СуммаНДС,
	|	ДокЗаказ.Цена,
	|	0,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.Код КАК БазоваяЕдиницаКод,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.Наименование КАК БазоваяЕдиницаНаименование,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.НаименованиеПолное КАК БазоваяЕдиницаНаименованиеПолное,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.МеждународноеСокращение КАК БазоваяЕдиницаМеждународноеСокращение,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.Код,
	|	ДокЗаказ.Номенклатура.БазоваяЕдиницаИзмерения.Наименование,
	|	1,
	|	ВЫБОР
	|		КОГДА ДокЗаказ.Ссылка.СуммаВключаетНДС
	|			ТОГДА ДокЗаказ.Сумма
	|		ИНАЧЕ ДокЗаказ.Сумма - ДокЗаказ.СуммаНДС
	|	КОНЕЦ,
	|	ДокЗаказ.Сумма,
	|	ДокЗаказ.Содержание
	|ИЗ
	|	Документ.ЗаказПокупателя.Услуги КАК ДокЗаказ
	|ГДЕ
	|	ДокЗаказ.Ссылка = &Ссылка";
		
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъект);
	
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(Запрос.Выполнить().Выгрузить(), ТаблицаТоваров);
	ОбработатьТаблицуТоваров(ТаблицаТоваров);
	
	СтруктураПараметров.Вставить("Исполнитель",				СтруктураЭД.Отправитель);
	СтруктураПараметров.Вставить("ВерсияСхемы", 			"4.02");
	СтруктураПараметров.Вставить("ТаблицаТоваров", 			ТаблицаТоваров);
	СтруктураПараметров.Вставить("Роль", 					"Продавец");
	СтруктураПараметров.Вставить("Организация", 			СтруктураЭД.Организация);
	СтруктураПараметров.Вставить("Контрагент", 				СтруктураЭД.Контрагент);
	СтруктураПараметров.Вставить("Ид", 						СтруктураЭД.НомерЭД);
	СтруктураПараметров.Вставить("ДатаФормирования",		ТекущаяДатаСеанса());
	СтруктураПараметров.Вставить("Номер", 					СтруктураЭД.НомерДокументаОтправителя);
	СтруктураПараметров.Вставить("Дата", 					СтруктураЭД.ДатаДокументаОтправителя);
	СтруктураПараметров.Вставить("Валюта",					СсылкаНаОбъект.ВалютаДокумента);
	СтруктураПараметров.Вставить("Курс", 					СсылкаНаОбъект.КурсВзаиморасчетов);
	СтруктураПараметров.Вставить("Сумма",					СсылкаНаОбъект.СуммаДокумента);
	СтруктураПараметров.Вставить("ЦенаВключаетНДС",			СсылкаНаОбъект.СуммаВключаетНДС);
	СтруктураПараметров.Вставить("СуммаНДС", 				СсылкаНаОбъект.Товары.Итог("СуммаНДС")+СсылкаНаОбъект.Услуги.Итог("СуммаНДС"));
	СтруктураПараметров.Вставить("ВидЭД", 					СтруктураЭД.ВидЭД);
	СтруктураПараметров.Вставить("НаправлениеЭД",			СтруктураЭД.НаправлениеЭД);
	СтруктураПараметров.Вставить("Комментарий", 			СсылкаНаОбъект.Комментарий);
	
	ИтоговаяСтрока = НСтр("ru='Всего наименований %Количество%, на сумму %Сумма%'");
	ИтоговаяСтрока = СтрЗаменить(ИтоговаяСтрока, "%Количество%", ТаблицаТоваров.Количество());
	ИтоговаяСтрока = СтрЗаменить(ИтоговаяСтрока, "%Сумма%",		 ОбщегоНазначения.ФорматСумм(СсылкаНаОбъект.СуммаДокумента, СсылкаНаОбъект.ВалютаДокумента));
	СуммаПрописью  = ОбщегоНазначения.СформироватьСуммуПрописью(СсылкаНаОбъект.СуммаДокумента, СсылкаНаОбъект.ВалютаДокумента);
	ИтоговаяСтрока = ИтоговаяСтрока + Символы.ПС + СуммаПрописью;
	СтруктураПараметров.Вставить("ИтогиПрописью", ИтоговаяСтрока);
	
КонецПроцедуры

// Устаревшая процедура, вместо этой процедуры следует использовать ЗаполнитьДанныеПоОтчетуОПродажахКомиссионногоТовара. 
// Подготавливает данные для электронного документа типа ОтчетКомитенту.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  СтруктураПараметров - структура, параметры для заполнения.
//
// Особенность:
//  Параметр ДополнительныеРеквизитыДляТаблицыТоваров в общей структуре параметров предназначен для заполнения
//  колонки ДополнительныеРеквизиты в таблице товаров.
//
Процедура ПодготовитьДанныеПоОтчетуОПродажахКомиссионногоТовара(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметров) Экспорт
	
	
	
КонецПроцедуры

// Устаревшая процедура, вместо этой процедуры следует использовать ЗаполнитьДанныеПоОтчетуОСписанииКомиссионногоТовара.
// Подготавливает данные для электронного документа типа ОтчетКомитентуОСписании.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  СтруктураПараметров - структура, параметры для заполнения.
//
Процедура ПодготовитьДанныеПоОтчетуОСписанииКомиссионногоТовара(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметров) Экспорт
	
	
	
КонецПроцедуры

// Устаревшая процедура, будет удалена при переходе на новую редакцию БЭД.
// Подготавливает данные для электронного документа типа Накладная.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  СтруктураПараметров - структура - параметры заполнения электронного документа.
//
Процедура ПодготовитьДанныеПоНакладной(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметров) Экспорт
	
	ТаблицаТоваров = СтруктураПараметров.ТаблицаТоваров.СкопироватьКолонки();
	
	ДанныеДокумента = ПолучитьДанныеРеализации(СсылкаНаОбъект);
	РеквизитыШапки  = ДанныеДокумента.РеквизитыШапки;
	
	// Сформируем таблицу товаров
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ДанныеДокумента.ТаблицаТоваров, ТаблицаТоваров);
	ОбработатьТаблицуТоваров(ТаблицаТоваров);
	
	СтруктураПараметров.Вставить("Исполнитель", 		СтруктураЭД.Отправитель);
	СтруктураПараметров.Вставить("ВерсияСхемы", 		"4.02");
	СтруктураПараметров.Вставить("ТаблицаТоваров", 		ТаблицаТоваров);
	СтруктураПараметров.Вставить("Организация", 		СтруктураЭД.Организация);
	СтруктураПараметров.Вставить("Контрагент", 			СтруктураЭД.Контрагент);
	СтруктураПараметров.Вставить("Ид", 					СтруктураЭД.НомерЭД);
	СтруктураПараметров.Вставить("ДатаФормирования", 	ТекущаяДатаСеанса());
	СтруктураПараметров.Вставить("Номер", 				СтруктураЭД.НомерДокументаОтправителя);
	СтруктураПараметров.Вставить("Дата", 				СтруктураЭД.ДатаДокументаОтправителя);
	СтруктураПараметров.Вставить("СуммаСНДСИтого",		ТаблицаТоваров.Итог("СуммаСНДС"));
	СтруктураПараметров.Вставить("ВидЭД", 				СтруктураЭД.ВидЭД);
	СтруктураПараметров.Вставить("НаправлениеЭД", 		СтруктураЭД.НаправлениеЭД);
	СтруктураПараметров.Вставить("Грузоотправитель", 	РеквизитыШапки.Грузоотправитель);
	СтруктураПараметров.Вставить("ТипГрузоотправителя", "Организация");
	СтруктураПараметров.Вставить("Грузополучатель",		РеквизитыШапки.Грузополучатель);
	СтруктураПараметров.Вставить("ЦенаВключаетНДС",		РеквизитыШапки.СуммаВключаетНДС);
	СтруктураПараметров.Вставить("Валюта",				РеквизитыШапки.ВалютаДокумента);
	СтруктураПараметров.Вставить("Курс",				РеквизитыШапки.Курс);
	СтруктураПараметров.Вставить("Кратность",			РеквизитыШапки.Кратность);
	
	// заполнение спец.реквизитов для связки заказ-поступление
	Если ТипЗнч(РеквизитыШапки.Сделка) = Тип("ДокументСсылка.ЗаказПокупателя")
		И ЗначениеЗаполнено(РеквизитыШапки.Сделка) Тогда
		
		ЗаказПокупателя = РеквизитыШапки.Сделка;
		
		СтруктураПараметров.Вставить("НомерПоДаннымПоставщика",	ЗаказПокупателя.Номер);
		СтруктураПараметров.Вставить("ДатаПоДаннымПоставщика", 	ЗаказПокупателя.Дата);
		
	КонецЕсли;
	
КонецПроцедуры

// Устаревшая процедура, будет удалена при переходе на новую редакцию БЭД.
// Подготавливает данные для электронного документа типа АктВыполненияРабот.
//
// Параметры: 
// СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
// СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  СтруктураПараметров - структура - параметры заполнения электронного документа.
//
Процедура ПодготовитьДанныеПоАктуВыполненныхРабот(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметров) Экспорт

	
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Поиск и создание документов

// Сохраняет данные из электронного документа в объект ИБ.
//
// Параметры:
//  СтрокаДляЗагрузки - строка параметров для загрузки,
//  ДеревоРазбора     - ДеревоЗначений, структура параметров документа ИБ.
//  СсылкаНаВладельца - Ссылка на объект ИБ, владельца электронного документа.
//
// Возвращаемое значение:
//  НайденныйОбъект - ссылка объекта.
//
Функция СохранитьДанныеОбъектаВБД(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца = Неопределено, 
	Записывать = Истина, ПараметрыПакетаЭД = Неопределено) Экспорт
	
	НайденныйОбъект = Неопределено;
	
	Если СтрокаДляЗагрузки.ВидЭД = Перечисления.ВидыЭД.ТОРГ12 ИЛИ 
		СтрокаДляЗагрузки.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Продавец ИЛИ
		СтрокаДляЗагрузки.ВидЭД = Перечисления.ВидыЭД.АктИсполнитель ИЛИ
		СтрокаДляЗагрузки.ВидЭД = Перечисления.ВидыЭД.АктНаПередачуПрав Тогда
		
		ВидОперации = ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "ВидОперации", Истина, ДеревоРазбора);
		
		Если ВидОперации = Перечисления.ВидыОперацийЭД.Исправление Тогда
			НайденныйОбъект = НайтиСоздатьКорректировкуПоступления(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца);
		Иначе
			НайденныйОбъект = НайтиСоздатьПоступлениеТоваровУслуг(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца);
		КонецЕсли;

	ИначеЕсли СтрокаДляЗагрузки.ВидЭД = Перечисления.ВидыЭД.СчетНаОплату Тогда
		НайденныйОбъект = НайтиСоздатьСчетНаОплатуПоставщика(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца);
		
	ИначеЕсли СтрокаДляЗагрузки.ВидЭД = Перечисления.ВидыЭД.ЗаказТовара Тогда
		НайденныйОбъект = НайтиСоздатьЗаказПокупателя(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца);
		
	ИначеЕсли СтрокаДляЗагрузки.ВидЭД = Перечисления.ВидыЭД.ОтветНаЗаказ Тогда
		НайденныйОбъект = НайтиСоздатьЗаказПоставщику(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца);
		
	ИначеЕсли СтрокаДляЗагрузки.ВидЭД = Перечисления.ВидыЭД.СчетФактура ИЛИ 
			  СтрокаДляЗагрузки.ВидЭД = Перечисления.ВидыЭД.КорректировочныйСчетФактура Тогда
		НайденныйОбъект = НайтиСоздатьСчетФактуру(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца);
		
	ИначеЕсли СтрокаДляЗагрузки.ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель Тогда
		НайденныйОбъект = НайтиСоздатьКорректировкуПоступления(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца);
		
	ИначеЕсли ВРег(СтрокаДляЗагрузки.ВидЭД) = ВРег("РеквизитыОрганизации") Тогда
		НайденныйОбъект = НайтиСоздатьКонтрагента(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца);
		
	КонецЕсли;
	
	Если НайденныйОбъект <> Неопределено Тогда
		ЗаполнитьДополнительнуюИнформацию(НайденныйОбъект, ПараметрыПакетаЭД);
		
		Если Записывать Тогда
			Попытка
				НайденныйОбъект.Записать();
				НайденныйОбъект = НайденныйОбъект.Ссылка;
			Исключение
				ЗаписьЖурналаРегистрации(НСтр("ru = 'Заполнение на основании ЭД'"),
					УровеньЖурналаРегистрации.Ошибка, 
					НайденныйОбъект.Метаданные(), 
					, 
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			КонецПопытки;
		КонецЕсли;
		
	КонецЕсли;

	Возврат НайденныйОбъект;
	
КонецФункции

// Сохраняет данные из электронного документа в объекты ИБ.
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - структура параметров документа ИБ.
//  СсылкиНаВладельцев - Массив - документы информационной базы, созданные ранее по входящему электронному документу.
//  Записывать - Булево - признак необходимости записывать объект ИБ.
//  СпособОбработки - Строка - способ сохранения данных в информационной базе.
//
Процедура НайтиСоздатьУниверсальныйПередаточныйДокумент(ДеревоДанных, СсылкиНаВладельцев = Неопределено,
	Записывать = Истина, СпособОбработки = "") Экспорт
	
	ПервичныйДокумент = Неопределено;
	СчетФактура = Неопределено;
	
	Если СсылкиНаВладельцев <> Неопределено Тогда
		Для Каждого Ссылка Из СсылкиНаВладельцев Цикл
			Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
				СчетФактура = Ссылка;
			Иначе
				ПервичныйДокумент = Ссылка;
			КонецЕсли;
			
		КонецЦикла;
	
	КонецЕсли;
	
	ДокументыУчета = Новый Массив;	
	СкорректироватьСсылкиНаВладельцев(ПервичныйДокумент, СчетФактура);
	
	НайтиСоздатьУПДДокументОПередаче(ДеревоДанных, ПервичныйДокумент, Записывать, СпособОбработки);
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияСчетаФактуры", 
		ПервичныйДокумент);
	ДокументыУчета.Добавить(ПервичныйДокумент);
	
	НайтиСоздатьУПДСчетФактуру(ДеревоДанных, СчетФактура, Записывать, СпособОбработки);
	
	ДокументыУчета.Добавить(СчетФактура);
	
	СсылкиНаВладельцев = ДокументыУчета;
	
КонецПроцедуры

// Сохраняет данные из электронного документа в объекты ИБ.
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - структура параметров документа ИБ.
//  СсылкаНаВладельца - Ссылка - ссылка на объект ИБ, владельца электронного документа.
//  Записывать - Булево - признак необходимости записывать объект ИБ.
//  СпособОбработки - Строка - способ сохранения данных в информационной базе.
//
Процедура НайтиСоздатьУПДДокументОПередаче(ДеревоДанных, СсылкаНаВладельца = Неопределено, Записывать = Истина, СпособОбработки = "") Экспорт
	
	ВидОперации = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВидОперации");
		
	Если ВидОперации = Перечисления.ВидыОперацийЭД.Исправление Тогда
		ИмяТипаДокумента = "КорректировкаПоступления";
	Иначе
		ИмяТипаДокумента = "ПоступлениеТоваровУслуг";
	КонецЕсли;
	
	ДанныеДокумента = Новый Структура("ВидДокумента", "УПДДокументОПередаче");
	ПрочитатьДанныеУниверсальногоДокумента(ДеревоДанных, ДанныеДокумента, ИмяТипаДокумента);
	
	Попытка
		Если ЗначениеЗаполнено(СсылкаНаВладельца) Тогда // получены изменения по существующему документу
			Если СсылкаНаВладельца.Проведен Тогда 
				ВызватьИсключение НСтр("ru = 'Заполнение на основании ЭД возможно только для непроведенного документа'");
			КонецЕсли;
			ДокументОбъект = СсылкаНаВладельца.ПолучитьОбъект();
		Иначе  // создаем новый
			УстановитьПривилегированныйРежим(Истина);
			ДокументОбъект = Документы[ИмяТипаДокумента].СоздатьДокумент();
			ДанныеДокумента.Шапка.Свойство("Организация", ДокументОбъект.Организация);
			ДанныеДокумента.Шапка.Свойство("Контрагент", ДокументОбъект.Контрагент);
			ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ДокументОбъект, глЗначениеПеременной("глТекущийПользователь"), , "Покупка");
			ДокументОбъект.Дата = ТекущаяДатаСеанса();
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ДокументОбъект, ДанныеДокумента.Шапка);
		Если ВидОперации = Перечисления.ВидыОперацийЭД.Исправление Тогда
			Если ЗначениеЗаполнено(ДокументОбъект.ДокументПоступления) Тогда
				Попытка
					ДокументОбъект.Заполнить(ДокументОбъект.ДокументПоступления);
					ЗаполнитьТабличныеЧастиКорректировкиПоступления(ДокументОбъект, ДанныеДокумента);
				Исключение
					ДокументОбъект.Товары.Загрузить(ДанныеДокумента.Товары);
					ДокументОбъект.Услуги.Загрузить(ДанныеДокумента.Услуги);
				КонецПопытки;
			Иначе
				ДокументОбъект.Товары.Загрузить(ДанныеДокумента.Товары);
				ДокументОбъект.Услуги.Загрузить(ДанныеДокумента.Услуги);
			КонецЕсли;
		Иначе
			ДокументОбъект.Товары.Загрузить(ДанныеДокумента.Товары);
			ДокументОбъект.Услуги.Загрузить(ДанныеДокумента.Услуги);
		КонецЕсли;
		ДокументОбъект.СерийныеНомера.Загрузить(ДанныеДокумента.СерийныеНомера);
		ПересчитатьСуммы(ДокументОбъект);
		ДокументОбъект.ОбменДанными.Загрузка = Истина;
		ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
		СсылкаНаВладельца = ДокументОбъект.Ссылка;
	Исключение
		ОписаниеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Заполнение на основании ЭД'"),
			УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы[ИмяТипаДокумента], 
			СсылкаНаВладельца, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение ОписаниеОшибки;
	КонецПопытки;

КонецПроцедуры

// Сохраняет данные из электронного документа в объекты ИБ.
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - структура параметров документа ИБ.
//  СсылкаНаВладельца - Ссылка - ссылка на объект ИБ, владельца электронного документа.
//  Записывать - Булево - признак необходимости записывать объект ИБ.
//  СпособОбработки - Строка - способ сохранения данных в информационной базе.
//
Процедура НайтиСоздатьУПДСчетФактуру(ДеревоДанных, СсылкаНаВладельца = Неопределено, Записывать = Истина, СпособОбработки = "") Экспорт
	
	ИмяТипаДокумента = "СчетФактураПолученный";
	
	ДанныеДокумента = Новый Структура("ВидДокумента", "УПДСчетФактура");
	ПрочитатьДанныеУниверсальногоДокумента(ДеревоДанных, ДанныеДокумента, ИмяТипаДокумента);

	Попытка
		Если ЗначениеЗаполнено(СсылкаНаВладельца) Тогда // получены изменения по существующему документу
			Если СсылкаНаВладельца.Проведен Тогда 
				ВызватьИсключение НСтр("ru = 'Заполнение на основании ЭД возможно только для непроведенного документа'");
			КонецЕсли;
			ДокументОбъект = СсылкаНаВладельца.ПолучитьОбъект();
		Иначе  // создаем новый
			УстановитьПривилегированныйРежим(Истина);
			ДокументОбъект = Документы[ИмяТипаДокумента].СоздатьДокумент();
			ДанныеДокумента.Шапка.Свойство("Организация", ДокументОбъект.Организация);
			ДанныеДокумента.Шапка.Свойство("Контрагент", ДокументОбъект.Контрагент);
			ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ДокументОбъект, глЗначениеПеременной("глТекущийПользователь"), , "Покупка");
			ДокументОбъект.Дата = ТекущаяДатаСеанса();
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ДокументОбъект, ДанныеДокумента.Шапка);
		ДокументОбъект.ДокументыОснования.Загрузить(ДанныеДокумента.ДокументыОснования);
		
		ВидСчетаФактуры = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВидСчетаФактуры");
		
		Если ВидСчетаФактуры = "Авансовый" Тогда
			ДокументОбъект.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.НаАванс;
			
			ДокументОбъект.Авансы.Очистить();
			СведенияОТоварах = ДеревоДанных.Строки.Найти("СведенияОТоварах", "ПолныйПуть");
			Для Каждого СведенияОТоваре Из СведенияОТоварах.Строки Цикл
				НоваяСтрока = ДокументОбъект.Авансы.Добавить();
				НоваяСтрока.Сумма     = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(СведенияОТоваре,
					"СведенияОТоварах.НомерСтроки.СтоимостьТоваровСНалогом");
				НоваяСтрока.СуммаНДС  = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(СведенияОТоваре,
					"СведенияОТоварах.НомерСтроки.СуммаНалога");
				НоваяСтрока.СтавкаНДС = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(СведенияОТоваре,
					"СведенияОТоварах.НомерСтроки.НалоговаяСтавка");
			КонецЦикла;
			
		Иначе
			ДокументОбъект.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.НаПоступление;
		КонецЕсли;
		
		ДокументОбъект.СуммаДокумента = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных,
			"ВсегоКОплате.ВсегоСтоимостьТоваровСНалогом");
		ДокументОбъект.СуммаНДСДокумента = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных,
			"ВсегоКОплате.ВсегоСуммаНалога");
		
		Если ДокументОбъект.ДокументыОснования.Количество() = 0 Тогда
			
			Если НЕ ЗначениеЗаполнено(ДокументОбъект.ДоговорКонтрагента) Тогда
				СписокВидовДоговоров = Новый СписокЗначений;
				СписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком);
				СписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером);
				СписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомитентом);
				УправлениеВзаиморасчетами.УстановитьДоговорКонтрагента(
					ДокументОбъект.ДоговорКонтрагента, ДокументОбъект.Контрагент, ДокументОбъект.Организация, СписокВидовДоговоров);
			КонецЕсли;
			
			ДокументОбъект.ДокументыОснования.Добавить();
		КонецЕсли;
		
		ДокументОбъект.КодСпособаПолучения = 2;
		ДокументОбъект.УстановитьКодВидаОперации();
		
		Если ДокументОбъект.ДокументыОснования.Количество() > 0 Тогда
			ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
			СсылкаНаВладельца = ДокументОбъект.Ссылка;
		КонецЕсли;
		
	Исключение
		ОписаниеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Заполнение на основании ЭД'"),
			УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы[ИмяТипаДокумента], 
			СсылкаНаВладельца, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение ОписаниеОшибки;
	КонецПопытки;

КонецПроцедуры

// Сохраняет данные из электронного документа в объекты ИБ.
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - структура параметров документа ИБ.
//  СсылкиНаВладельцев - Массив - документы информационной базы, созданные ранее по входящему электронному документу.
//  Записывать - Булево - признак необходимости записывать объект ИБ.
//  СпособОбработки - Строка - способ сохранения данных в информационной базе.
//
Процедура НайтиСоздатьУниверсальныйКорректировочныйДокумент(ДеревоДанных, СсылкиНаВладельцев = Неопределено, Записывать = Истина, СпособОбработки = "") Экспорт
	
	ПервичныйДокумент = Неопределено;
	СчетФактура = Неопределено;
	
	Если СсылкиНаВладельцев <> Неопределено Тогда
		Для Каждого Ссылка Из СсылкиНаВладельцев Цикл
			Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
				СчетФактура = Ссылка;
			Иначе
				ПервичныйДокумент = Ссылка;
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		ДокументыУчета = Новый Массив;	
	КонецЕсли;
	
	СкорректироватьСсылкиНаВладельцев(ПервичныйДокумент, СчетФактура);

	НайтиСоздатьУКДДокументОбИзмененииСтоимости(ДеревоДанных, ПервичныйДокумент, Записывать, СпособОбработки);
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияСчетаФактуры", 
		ПервичныйДокумент);
		
	Если СсылкиНаВладельцев = Неопределено Тогда
		ДокументыУчета.Добавить(ПервичныйДокумент);
	КонецЕсли;
	
	НайтиСоздатьУКДСчетФактуру(ДеревоДанных, СчетФактура, Записывать, СпособОбработки);
	
	Если СсылкиНаВладельцев = Неопределено Тогда
		ДокументыУчета.Добавить(СчетФактура);
	
		СсылкиНаВладельцев = ДокументыУчета;
	Иначе
		
		Если ТипЗнч(СсылкиНаВладельцев) = Тип("Массив") Тогда
			
			Для Каждого СсылкаНаВладельца Из СсылкиНаВладельцев Цикл
				
				Если ТипЗнч(СсылкаНаВладельца) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
					СсылкаНаВладельца =  СчетФактура;
				Иначе
					СсылкаНаВладельца =  ПервичныйДокумент;
				КонецЕсли;
				
			КонецЦикла;
			
		Иначе
			СсылкиНаВладельцев.ПервичныйДокумент = ПервичныйДокумент;
			СсылкиНаВладельцев.СчетФактура = СчетФактура;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Сохраняет данные из электронного документа в объекты ИБ.
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - структура параметров документа ИБ.
//  СсылкаНаВладельца - Ссылка - ссылка на объект ИБ, владельца электронного документа.
//  Записывать - Булево - признак необходимости записывать объект ИБ.
//  СпособОбработки - Строка - способ сохранения данных в информационной базе.
//
Процедура НайтиСоздатьУКДДокументОбИзмененииСтоимости(ДеревоДанных, СсылкаНаВладельца = Неопределено, Записывать = Истина, СпособОбработки = "") Экспорт
	
	ИмяТипаДокумента = "КорректировкаПоступления";
	
	ДанныеДокумента = Новый Структура("ВидДокумента", "УКДДокументОбИзмененииСтоимости");
	ПрочитатьДанныеУниверсальногоДокумента(ДеревоДанных, ДанныеДокумента, ИмяТипаДокумента);
	
	Попытка
		Если ЗначениеЗаполнено(СсылкаНаВладельца) Тогда // получены изменения по существующему документу
			Если СсылкаНаВладельца.Проведен Тогда 
				ВызватьИсключение НСтр("ru = 'Заполнение на основании ЭД возможно только для непроведенного документа'");
			КонецЕсли;
			ДокументОбъект = СсылкаНаВладельца.ПолучитьОбъект();
		Иначе  // создаем новый
			УстановитьПривилегированныйРежим(Истина);
			ДокументОбъект = Документы[ИмяТипаДокумента].СоздатьДокумент();
			ДанныеДокумента.Шапка.Свойство("Организация", ДокументОбъект.Организация);
			ДанныеДокумента.Шапка.Свойство("Контрагент", ДокументОбъект.Контрагент);
			ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ДокументОбъект, глЗначениеПеременной("глТекущийПользователь"), , "Покупка");
			ДокументОбъект.Дата = ТекущаяДатаСеанса();
			ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ДокументОбъект, ДанныеДокумента.Шапка);
		Если ЗначениеЗаполнено(ДокументОбъект.ДокументПоступления) Тогда
			Попытка
				ДокументОбъект.Заполнить(ДокументОбъект.ДокументПоступления);
				ЗаполнитьТабличныеЧастиКорректировкиПоступления(ДокументОбъект, ДанныеДокумента);
			Исключение
				ДокументОбъект.Товары.Загрузить(ДанныеДокумента.Товары);
				ДокументОбъект.Услуги.Загрузить(ДанныеДокумента.Услуги);
			КонецПопытки;
		Иначе
			ДокументОбъект.Товары.Загрузить(ДанныеДокумента.Товары);
			ДокументОбъект.Услуги.Загрузить(ДанныеДокумента.Услуги);
		КонецЕсли;
		ДокументОбъект.СведенияПрослеживаемости.Загрузить(ДанныеДокумента.СведенияОПрослеживаемости);
		ДокументОбъект.СерийныеНомера.Загрузить(ДанныеДокумента.СерийныеНомера);
		ПересчитатьСуммы(ДокументОбъект);
		ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
		СсылкаНаВладельца = ДокументОбъект.Ссылка;
	Исключение
		ОписаниеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Заполнение на основании ЭД'"),
			УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы[ИмяТипаДокумента], 
			СсылкаНаВладельца, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение ОписаниеОшибки;
	КонецПопытки;

КонецПроцедуры

// Сохраняет данные из электронного документа в объекты ИБ.
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - структура параметров документа ИБ.
//  СсылкаНаВладельца - Ссылка - ссылка на объект ИБ, владельца электронного документа.
//  Записывать - Булево - признак необходимости записывать объект ИБ.
//  СпособОбработки - Строка - способ сохранения данных в информационной базе.
//
Процедура НайтиСоздатьУКДСчетФактуру(ДеревоДанных, СсылкаНаВладельца = Неопределено, Записывать = Истина, СпособОбработки = "") Экспорт
	
	ИмяТипаДокумента = "СчетФактураПолученный";
	
	ДанныеДокумента = Новый Структура("ВидДокумента", "УКДСчетФактура");
	ПрочитатьДанныеУниверсальногоДокумента(ДеревоДанных, ДанныеДокумента, ИмяТипаДокумента);

	Попытка
		Если ЗначениеЗаполнено(СсылкаНаВладельца) Тогда // получены изменения по существующему документу
			Если СсылкаНаВладельца.Проведен Тогда 
				ВызватьИсключение НСтр("ru = 'Заполнение на основании ЭД возможно только для непроведенного документа'");
			КонецЕсли;
			ДокументОбъект = СсылкаНаВладельца.ПолучитьОбъект();
		Иначе  // создаем новый
			УстановитьПривилегированныйРежим(Истина);
			ДокументОбъект = Документы[ИмяТипаДокумента].СоздатьДокумент();
			ДанныеДокумента.Шапка.Свойство("Организация", ДокументОбъект.Организация);
			ДанныеДокумента.Шапка.Свойство("Контрагент", ДокументОбъект.Контрагент);
			ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ДокументОбъект, глЗначениеПеременной("глТекущийПользователь"), , "Покупка");
			ДокументОбъект.Дата = ТекущаяДатаСеанса();
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ДокументОбъект, ДанныеДокумента.Шапка);
		ДокументОбъект.ДокументыОснования.Загрузить(ДанныеДокумента.ДокументыОснования);
		
		ДокументОбъект.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.Корректировочный;
		ДокументОбъект.КодСпособаПолучения = 2;
		ДокументОбъект.УстановитьКодВидаОперации();
		
		ДокументОбъект.ДокументыОснования.Очистить();
		Для Каждого ДокументОснование Из ДанныеДокумента.ДокументыОснования Цикл
			Если ТипЗнч(ДокументОснование.ДокументОснование) = Тип("ДокументСсылка.СчетФактураПолученный")
					Или (ДокументОбъект.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.Корректировочный 
						И ТипЗнч(ДокументОснование.ДокументОснование) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг")) Тогда
				Продолжить;
			КонецЕсли;
			ДокументОбъект.ДокументыОснования.Добавить().ДокументОснование = ДокументОснование.ДокументОснование;
		КонецЦикла;
		
		Если ДокументОбъект.ДокументыОснования.Количество() > 0 Тогда
			ОснованиеСчетаФактуры = ДокументОбъект.ДокументыОснования[0];
			ОснованиеСчетаФактуры.НомерИсходногоДокумента = ДанныеДокумента.Шапка.НомерИсходногоДокумента;
			ОснованиеСчетаФактуры.ДатаИсходногоДокумента  = ДанныеДокумента.Шапка.ДатаИсходногоДокумента;
			Если ДанныеДокумента.Свойство("УчитыватьИсправлениеИсходногоДокумента")
				И ДанныеДокумента.УчитыватьИсправлениеИсходногоДокумента Тогда
				ОснованиеСчетаФактуры.НомерИсправленияИсходногоДокумента = ДанныеДокумента.НомерИсправленияИсходногоДокумента;
				ОснованиеСчетаФактуры.ДатаИсправленияИсходногоДокумента  = ДанныеДокумента.ДатаИсправленияИсходногоДокумента;
			КонецЕсли;
		КонецЕсли;
		
		Если ДокументОбъект.ДокументыОснования.Количество() > 0 Тогда
			ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
			СсылкаНаВладельца = ДокументОбъект.Ссылка;
		КонецЕсли;
		
	Исключение
		ОписаниеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Заполнение на основании ЭД'"),
			УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы[ИмяТипаДокумента], 
			СсылкаНаВладельца, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение ОписаниеОшибки;
	КонецПопытки;

КонецПроцедуры

// Сохраняет данные из электронного документа в объекты ИБ.
//
// Параметры:
//  ТипЭлементаВерсииЭД - ПеречислениеСсылка.ТипыЭлементовВерсииЭД - тип элемента версии электронного документа,
//     определяющий вариант заполнения дерева данных. Возможные значения:
//      - КСЧФДИСУКД - корректировочный счет-фактура и документ о согласии покупателя на  изменение стоимости отгрузки;
//      - КСЧФУКД - корректировочный счет-фактура, применяемый при расчетах по налогу на добавленную стоимость;
//      - ДИСУКД - документ о согласии покупателя на изменение стоимости отгрузки.
//  ДеревоДанных - ДеревоЗначений - дерево данных из которого заполняется учетный документ,
//    см. макет Обработка.ОбменСКонтрагентами.УКД_ИнформацияПродавца_2020.
//  СсылкиНаВладельцев - Массив - документы информационной базы, созданные ранее по входящему электронному документу.
//  Записывать - Булево - признак необходимости записывать объект ИБ.
//  СпособОбработки - Структура - способы сохранения данных в информационной базе.
//     - ПервичныйДокумент - Строка - способ сохранения первичного документа.
//     - СчетФактура - Строка - способ сохранения счета-фактуры.
//
Процедура НайтиСоздатьУКД_2020(ТипЭлементаВерсииЭД, ДеревоДанных, СсылкиНаВладельцев = Неопределено, 
	Записывать = Истина, СпособОбработки = Неопределено) Экспорт
	
	Если ТипЭлементаВерсииЭД = Перечисления.ТипыЭлементовВерсииЭД.КСЧФДИСУКД Тогда
	
		ПервичныйДокумент = Неопределено;
		СчетФактура = Неопределено;
		
		Если СсылкиНаВладельцев <> Неопределено Тогда
			Для Каждого Ссылка Из СсылкиНаВладельцев Цикл
				Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
					СчетФактура = Ссылка;
				Иначе
					ПервичныйДокумент = Ссылка;
				КонецЕсли;
				
			КонецЦикла;
			
		Иначе
			ДокументыУчета = Новый Массив;	
		КонецЕсли;
		
		СкорректироватьСсылкиНаВладельцев(ПервичныйДокумент, СчетФактура);





		НайтиСоздатьУКДДокументОбИзмененииСтоимости(ДеревоДанных, ПервичныйДокумент, Записывать, СпособОбработки);



		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияСчетаФактуры", 
			ПервичныйДокумент);










			
		Если СсылкиНаВладельцев = Неопределено Тогда
			ДокументыУчета.Добавить(ПервичныйДокумент);
		КонецЕсли;
		
		НайтиСоздатьУКДСчетФактуру(ДеревоДанных, СчетФактура, Записывать, СпособОбработки);
		
		Если СсылкиНаВладельцев = Неопределено Тогда
			ДокументыУчета.Добавить(СчетФактура);
		
			СсылкиНаВладельцев = ДокументыУчета;
		Иначе
			Если ТипЗнч(СсылкиНаВладельцев) = Тип("Массив") Тогда
				
				Для Каждого СсылкаНаВладельца Из СсылкиНаВладельцев Цикл
					
					Если ТипЗнч(СсылкаНаВладельца) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
						СсылкаНаВладельца =  СчетФактура;
					Иначе
						СсылкаНаВладельца =  ПервичныйДокумент;
					КонецЕсли;
					
				КонецЦикла;
				
			Иначе
				СсылкиНаВладельцев.ПервичныйДокумент = ПервичныйДокумент;
				СсылкиНаВладельцев.СчетФактура = СчетФактура;
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ТипЭлементаВерсииЭД = Перечисления.ТипыЭлементовВерсииЭД.ДИСУКД Тогда
		
		Если СсылкиНаВладельцев <> Неопределено Тогда
			ПервичныйДокумент = СсылкиНаВладельцев;
		Иначе
			ПервичныйДокумент = Неопределено;
		КонецЕсли;
		
		СкорректироватьСсылкиНаВладельцев(ПервичныйДокумент, СчетФактура);

		НайтиСоздатьУКДДокументОбИзмененииСтоимости(ДеревоДанных, ПервичныйДокумент, Записывать, СпособОбработки);
		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияСчетаФактуры", 
			ПервичныйДокумент);
			
		Если ЗначениеЗаполнено(ПервичныйДокумент) Тогда
			СсылкиНаВладельцев = ПервичныйДокумент;
		КонецЕсли;
		
	Иначе
		
		ПервичныйДокумент = Неопределено;
		Если СсылкиНаВладельцев <> Неопределено Тогда
			СчетФактура = СсылкиНаВладельцев;
		Иначе
			СчетФактура = Неопределено;
		КонецЕсли;
		
		СкорректироватьСсылкиНаВладельцев(ПервичныйДокумент, СчетФактура);
		НайтиСоздатьУКДСчетФактуру(ДеревоДанных, СчетФактура, Записывать, СпособОбработки);
		
		Если ЗначениеЗаполнено(СчетФактура) Тогда
			СсылкиНаВладельцев = СчетФактура;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Сохраняет данные из электронного документа в объекты ИБ.
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - структура параметров документа ИБ.
//  СсылкаНаВладельца - Ссылка - ссылка на объект ИБ, владельца электронного документа.
//  Записывать - Булево - признак необходимости записывать объект ИБ.
//  СпособОбработки - Строка - способ сохранения данных в информационной базе.
//  ОписаниеОшибки - Строка - описание ошибки создания учетного документа.
//
Процедура НайтиСоздатьКорректировкуСведенийОРеализации(ДеревоДанных, СсылкаНаВладельца = Неопределено, Записывать = Истина, СпособОбработки = "", ОписаниеОшибки = "") Экспорт
	
	
	
КонецПроцедуры

// Сохраняет данные из электронного документа в объекты ИБ.
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - структура параметров документа ИБ.
//  СсылкаНаВладельца - Ссылка - ссылка на объект ИБ, владельца электронного документа.
//  Записывать - Булево - признак необходимости записывать объект ИБ.
//  СпособОбработки - Строка - способ сохранения данных в информационной базе.
//  ОписаниеОшибки - Строка - описание ошибки создания учетного документа.
//
Процедура НайтиСоздатьСведенияОРеализации(ДеревоДанных, СсылкаНаВладельца = Неопределено, Записывать = Истина, СпособОбработки = "", ОписаниеОшибки = "") Экспорт
	
	
	
КонецПроцедуры

// Сохраняет данные из электронного документа в объекты ИБ.
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - структура параметров документа ИБ.
//  СсылкаНаВладельца - Ссылка - ссылка на объект ИБ, владельца электронного документа.
//  Записывать - Булево - признак необходимости записывать объект ИБ.
//  СпособОбработки - Строка - способ сохранения данных в информационной базе.
//  ОписаниеОшибки - Строка - описание ошибки создания учетного документа.
//
Процедура НайтиСоздатьКорректировкуСведенийОЗакупке(ДеревоДанных, СсылкаНаВладельца = Неопределено, Записывать = Истина, СпособОбработки = "", ОписаниеОшибки = "") Экспорт
	
	
	
КонецПроцедуры

// Сохраняет данные из электронного документа в объекты ИБ.
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - структура параметров документа ИБ.
//  СсылкаНаВладельца - Ссылка - ссылка на объект ИБ, владельца электронного документа.
//  Записывать - Булево - признак необходимости записывать объект ИБ.
//  СпособОбработки - Строка - способ сохранения данных в информационной базе.
//  ОписаниеОшибки - Строка - описание ошибки создания учетного документа.
//
Процедура НайтиСоздатьСведенияОЗакупке(ДеревоДанных, СсылкаНаВладельца = Неопределено, Записывать = Истина, СпособОбработки = "", ОписаниеОшибки = "") Экспорт
	
	
	
КонецПроцедуры

// Сохраняет данные из электронного документа в объекты ИБ (Акт о расхождениях, ФНС, версия 5.01).
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - структура параметров документа ИБ.
//  ДокументУчета - Ссылка - ссылка на объект ИБ, владельца электронного документа.
//  СпособОбработки - Строка - способ сохранения данных в информационной базе.
//  ОписаниеОшибки - Строка - описание ошибки создания учетного документа.
//
Процедура НайтиСоздатьАктОРасхождениях_ФНС_2019(ДеревоДанных, ДокументУчета = Неопределено, СпособОбработки = "", ОписаниеОшибки = "") Экспорт
	
	
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// УПД_2019

// Подготавливает данные для электронного документа типа УПД (информация продавца).
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на объект информационной базы, по которому необходимо 
//                                    создать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//  Отказ - Булево - если нужно отказаться от создания электронного документа, необходимо установить значение в Истина.
//                   После этого дальнейшие действия по формированию документа производиться не будут, поэтому
//                   нужно сформировать сообщения пользователю при необходимости самостоятельно.
//
Процедура ЗаполнитьДанныеДляСвЗКИнформацияПродавцаФНС_2019(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа УПД (информация продавца).
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на объект информационной базы, по которому необходимо 
//                                    создать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//  Отказ - Булево - если нужно отказаться от создания электронного документа, необходимо установить значение в Истина.
//                   После этого дальнейшие действия по формированию документа производиться не будут, поэтому
//                   нужно сформировать сообщения пользователю при необходимости самостоятельно.
//
Процедура ЗаполнитьДанныеДляСвРКИнформацияПродавцаФНС_2019(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// УПД_2019

// Подготавливает данные для электронного документа типа УПД (информация продавца).
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на объект информационной базы, по которому необходимо 
//                                    создать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//  Отказ - Булево - если нужно отказаться от создания электронного документа, необходимо установить значение в Истина.
//                   После этого дальнейшие действия по формированию документа производиться не будут, поэтому
//                   нужно сформировать сообщения пользователю при необходимости самостоятельно.
//
Процедура ЗаполнитьДанныеДляУПДИнформацияПродавцаФНС_2019(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		СчетФактура = СсылкаНаОбъект;
		ЗаполнитьДанныеДляСЧФИнформацияПродавцаФНС_2019(СчетФактура, СтруктураЭД, ДеревоДанных, Отказ);		
	Иначе
		СтруктураОтбораСчетаФактуры = Новый Структура;
		СтруктураОтбораСчетаФактуры.Вставить("ПометкаУдаления", Ложь);
		СсылкаСчетаФактуры = УчетНДС.НайтиПодчиненныйСчетФактуру(СсылкаНаОбъект, , СтруктураОтбораСчетаФактуры);
		
		Если НЕ ЗначениеЗаполнено(СсылкаСчетаФактуры) Тогда
			ЗаполнитьДанныеДляДОПИнформацияПродавцаФНС_2019(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ);
		Иначе
			ЗаполнитьДанныеДляДОПИнформацияПродавцаФНС_2019(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ);
			ДополнитьУПД_ДаннымиПоСчетуФактуре(СсылкаСчетаФактуры, ДеревоДанных);
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа УПД (информация продавца).
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на объект информационной базы, по которому необходимо 
//                                    создать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//  Отказ - Булево - если нужно отказаться от создания электронного документа, необходимо установить значение в Истина.
//                   После этого дальнейшие действия по формированию документа производиться не будут, поэтому
//                   нужно сформировать сообщения пользователю при необходимости самостоятельно.
//
Процедура ЗаполнитьДанныеДляДОПИнформацияПродавцаФНС_2019(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	// Получим данные для заполнения дерева
	ДанныеДокумента = Новый Структура;
	ДанныеДокумента.Вставить("Функция", СтруктураЭД.Функция);
	ДанныеДокумента.Вставить("НомерСФ");
	ДанныеДокумента.Вставить("ДатаСФ");
	// Данные СФ
	СчетФактураОснования = УчетНДС.НайтиПодчиненныйСчетФактуру(СсылкаНаОбъект);
	РеквизитыСФ = Новый Структура;
	Если ЗначениеЗаполнено(СчетФактураОснования) Тогда
		РеквизитыСФ = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(СчетФактураОснования, 
			"КППКонтрагента, Дата, НомерИсходногоДокумента, ДатаИсходногоДокумента");
		Если СтруктураЭД.Функция = "СЧФДОП" Тогда
			ДанныеДокумента.Вставить("НомерСФ", ПолучитьПечатныйНомерДокумента(СчетФактураОснования));
			ДанныеДокумента.Вставить("ДатаСФ",  РеквизитыСФ.Дата);
			Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
				Если СчетФактураОснования.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаРеализацию Тогда
					// исправительный счет-фактура
					ДанныеДокумента.НомерСФ = РеквизитыСФ.НомерИсходногоДокумента;
					ДанныеДокумента.ДатаСФ  = РеквизитыСФ.ДатаИсходногоДокумента;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ПодготовитьДанныеДляУПД_ПервичныйДокумент(СсылкаНаОбъект, ДанныеДокумента, СтруктураЭД.ВариантыЗаполненияПолей.ТоварКод);
	РеквизитыШапки = ДанныеДокумента.РеквизитыШапки;
	ТаблицаНоменклатуры =  ДанныеДокумента.ТаблицаНоменклатуры;
	
	
	// ОснованиеОтгрузки
	ОснованиеОтгрузки = Новый ТаблицаЗначений;
	ОснованиеОтгрузки.Колонки.Добавить("ДокументНаименование");
	ОснованиеОтгрузки.Колонки.Добавить("ДокументНомер");
	ОснованиеОтгрузки.Колонки.Добавить("ДокументДата");
	ОснованиеОтгрузки.Колонки.Добавить("ДокументДопСведения");
	
	ВедениеВзаиморасчетовПоСчетамЗаказам = ДанныеДокумента.РеквизитыШапки.ВедениеВзаиморасчетов = Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоЗаказам 
		Или ДанныеДокумента.РеквизитыШапки.ВедениеВзаиморасчетов = Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоСчетам;
		
	Если ВедениеВзаиморасчетовПоСчетамЗаказам И ЗначениеЗаполнено(РеквизитыШапки.ДокументОснование) Тогда
		СтрокаОснование = ОснованиеОтгрузки.Добавить();
		СтрокаОснование.ДокументНаименование = РеквизитыШапки.ДокументОснование.Метаданные().Синоним;
		СтрокаОснование.ДокументНомер = ОбщегоНазначения.ПолучитьНомерНаПечать(РеквизитыШапки.ДокументОснование);
		СтрокаОснование.ДокументДата = РеквизитыШапки.ДокументОснованиеДата;
		СтрокаОснование.ДокументДопСведения = "";
	Иначе
		Если ЗначениеЗаполнено(РеквизитыШапки.ДоговорНомер) Тогда
			СтрокаОснование = ОснованиеОтгрузки.Добавить();
			СтрокаОснование.ДокументНаименование = РеквизитыШапки.ДоговорНаименование;
			СтрокаОснование.ДокументНомер = РеквизитыШапки.ДоговорНомер;
			СтрокаОснование.ДокументДата = РеквизитыШапки.ДоговорДата;
			СтрокаОснование.ДокументДопСведения = "";
		КонецЕсли;
	КонецЕсли;
	ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ОснованиеОтгрузки, "ОснованиеОтгрузкиТоваров");	
	
	// СведенияОПродавце
	СведенияОПродавце = ПолучитьДанныеЮрФизЛица_2019(РеквизитыШапки.Организация, РеквизитыШапки.БанковскийСчет);
	ТаблицаПродавцов = ЭлектронныеДокументыСлужебный.ДанныеЭлементаДереваЭлектронногоДокумента(ДеревоДанных, "СведенияОПродавце");
	ТаблицаПродавцов.Колонки.Добавить("СведенияОбУчастнике");
	ДанныеПродавца = ПолучитьДанныеУчастникаУПД(СведенияОПродавце, "Юр");
	СтрокаПродавца = ТаблицаПродавцов.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаПродавца, ДанныеПродавца);
	ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаПродавцов, "СведенияОПродавце");
	// СведенияОПокупателе
	РеквизитыКонтрагента = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(РеквизитыШапки.Контрагент, 
		"ГоловнойКонтрагент, ОбособленноеПодразделение, КПП");
	Если РеквизитыКонтрагента.ОбособленноеПодразделение И ЗначениеЗаполнено(РеквизитыКонтрагента.ГоловнойКонтрагент) Тогда
		СведенияОПокупателе = ПолучитьДанныеЮрФизЛица_2019(РеквизитыКонтрагента.ГоловнойКонтрагент);
		СведенияОПокупателе.КПП = РеквизитыКонтрагента.КПП;
	Иначе
		СведенияОПокупателе = ПолучитьДанныеЮрФизЛица_2019(РеквизитыШапки.Контрагент);
	КонецЕсли;
	Если РеквизитыСФ.Свойство("КППКонтрагента") Тогда
		СведенияОПокупателе.КПП = ?(ЗначениеЗаполнено(РеквизитыСФ.КППКонтрагента), РеквизитыСФ.КППКонтрагента, СведенияОПокупателе.КПП);
	КонецЕсли;

	ТаблицаПокупателей = ЭлектронныеДокументыСлужебный.ДанныеЭлементаДереваЭлектронногоДокумента(ДеревоДанных, "СведенияОПокупателе");
	ТаблицаПокупателей.Колонки.Добавить("СведенияОбУчастнике");
	ДанныеПокупателя = ПолучитьДанныеУчастникаУПД(СведенияОПокупателе, "Юр");
	СтрокаПокупателя = ТаблицаПокупателей.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаПокупателя, ДанныеПокупателя);
	ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаПокупателей, "СведенияОПокупателе");
	
	// *ДополнительныеСведенияОбУчастниках
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДополнительныеСведенияОбУчастниках.ВалютаНаименование", 
		СокрЛП(РеквизитыШапки.ВалютаНаименование));
		
	// СведенияОВыбытииМаркированныхТоваров
	Если РеквизитыШапки.ОперацияВыбытияМаркированныхТоваров <> 0 Тогда
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОВыбытииМаркированныхТоваров", РеквизитыШапки.ОперацияВыбытияМаркированныхТоваров);
	КонецЕсли;
	
	// СведенияОТоварах
	ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаНоменклатуры, "СведенияОТоварах");
	
	// *ВсегоКОплате
	ИтогоСНалогом = ТаблицаНоменклатуры.Итог("СтоимостьТоваровСНалогом");
	ИтогоБезНалога = ТаблицаНоменклатуры.Итог("СтоимостьТоваровБезНалога");
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВсегоКОплате.ВсегоСтоимостьТоваровСНалогом", 
		ИтогоСНалогом);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВсегоКОплате.ВсегоСтоимостьТоваровБезНалога", 
		ИтогоБезНалога);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВсегоКОплате.ВсегоСуммаНалога", 
		ИтогоСНалогом - ИтогоБезНалога);
	
	// Реквизиты шапки
	ЗаполнитьДатуИНомер(ДеревоДанных, РеквизитыШапки, СсылкаНаОбъект, "НомерДокумента", "ДатаДокумента");
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СоставительДокументаНаименование", 
		СведенияОПродавце.ПолноеНаименование);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаКод", 
		СокрЛП(РеквизитыШапки.ВалютаКод));
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СодержаниеОперации", 
		"Товары переданы/Услуги оказаны");
	ТолькоУслуги = ТаблицаНоменклатуры.Найти(Ложь, "Услуга") = Неопределено;
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ТолькоУслуги", 
		ТолькоУслуги);
	
	Если Не ТолькоУслуги Тогда	
		// СведенияОГрузоотправителе
		Если ЗначениеЗаполнено(РеквизитыШапки.Грузоотправитель) Тогда
			ТаблицаГрузоотправителей = ЭлектронныеДокументыСлужебный.ДанныеЭлементаДереваЭлектронногоДокумента(ДеревоДанных, "СведенияОГрузоотправителе");
			ТаблицаГрузоотправителей.Колонки.Добавить("СведенияОбУчастнике");
			СведенияОГрузоотправителе = ПолучитьДанныеЮрФизЛица_2019(РеквизитыШапки.Грузоотправитель);
			ДанныеГрузоотправителя = ПолучитьДанныеУчастникаУПД(СведенияОГрузоотправителе, "Факт");
			СтрокаГрузоотправителя = ТаблицаГрузоотправителей.Добавить();
			Грузоотправитель = ЭлектронныеДокументыСлужебный.ДанныеЭлементаДереваЭлектронногоДокумента(ДеревоДанных, "СведенияОГрузоотправителе.НомерСтроки.Грузоотправитель");
			ЗаполнитьЗначенияСвойств(Грузоотправитель, ДанныеГрузоотправителя);
			СтрокаГрузоотправителя.Грузоотправитель = Грузоотправитель;
			ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаГрузоотправителей, "СведенияОГрузоотправителе");
		КонецЕсли;
		// СведенияОГрузополучателе
		Если ЗначениеЗаполнено(РеквизитыШапки.Грузополучатель) Тогда
			ТаблицаГрузополучателей = ЭлектронныеДокументыСлужебный.ДанныеЭлементаДереваЭлектронногоДокумента(ДеревоДанных, "СведенияОГрузополучателе");
			ТаблицаГрузополучателей.Колонки.Добавить("СведенияОбУчастнике");
			СведенияОГрузополучателе = ПолучитьДанныеЮрФизЛица_2019(РеквизитыШапки.Грузополучатель);
			СведенияОГрузополучателе.Вставить("АдресДоставки", РеквизитыШапки.АдресДоставки);
			ДанныеГрузополучателя = ПолучитьДанныеУчастникаУПД(СведенияОГрузополучателе, "Факт");
			СтрокаГрузополучателя = ТаблицаГрузополучателей.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаГрузополучателя, ДанныеГрузополучателя);
			ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаГрузополучателей, "СведенияОГрузополучателе");
		КонецЕсли;
	КонецЕсли;

	СчетФактураОснования = УчетНДС.НайтиПодчиненныйСчетФактуру(РеквизитыШапки.ДокументОснование);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияСчетаФактуры", 
		СчетФактураОснования);
		
	// ВидОперации
	ВидОперации = РеквизитыШапки.ВидОперации;
	Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		Если СсылкаНаОбъект.ДоговорКонтрагента.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером Тогда
			ВидОперации = Перечисления.ВидыОперацийЭД.Комиссия;
		КонецЕсли;
	КонецЕсли;
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВидОперации", 
		ВидОперации);
		
	ЗаполнитьОбстоятельстваФормированияУПД(ДеревоДанных, СтруктураЭД, РеквизитыШапки);
	
	Если ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, "ДокументОбОтгрузке") Тогда
		
		ЭтоУПД = Не СтруктураЭД.Свойство("ЭтоСФ");
		СтрокаДокументОтгрузки = ПолучитьСведенияОДокументеОтгрузки(СсылкаНаОбъект, ДанныеДокумента, ЭтоУПД);
		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументОбОтгрузке", СтрокаДокументОтгрузки);
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьСведенияОДокументеОтгрузки(СсылкаНаОбъект, ДанныеДокумента, ЭтоУПД = Ложь) Экспорт
		
	ТаблицаДокумента = ДанныеДокумента.ТаблицаНоменклатуры;
	РеквизитыШапки   = ДанныеДокумента.РеквизитыШапки;
	
	Если ДанныеДокумента.Функция = "СЧФДОП" И ЗначениеЗаполнено(ДанныеДокумента.НомерСФ) Тогда
		НомерНаПечать = ДанныеДокумента.НомерСФ;
		ДатаНаПечать = Формат(ДанныеДокумента.ДатаСФ, "ДЛФ='Д'") + " г.";
	Иначе
		НомерНаПечать = ПолучитьПечатныйНомерДокумента(СсылкаНаОбъект);
		ДатаНаПечать = Формат(РеквизитыШапки.ДатаДокумента, "ДЛФ='Д'") + " г.";
	КонецЕсли;
	
	ПредыдущийДокумент = Неопределено;
	ПерваяСтрокаПоДокументуОтгрузки = 1;
	ПоследняяСтрокаПоДокументуОтгрузки = 1;
	ШаблонПодстрокиДокументаОбОтгрузке = "№ п/п %1 №%2 от %3";
	ДокументыОбОтгрузке = "";
	Если ЭтоУПД Тогда
		ПоследняяСтрокаПоДокументуОтгрузки = ТаблицаДокумента.Количество();
		Если ПерваяСтрокаПоДокументуОтгрузки = ПоследняяСтрокаПоДокументуОтгрузки Тогда
			ДиапазонСтрок = "" + ПерваяСтрокаПоДокументуОтгрузки;
		Иначе
			ДиапазонСтрок = "" + ПерваяСтрокаПоДокументуОтгрузки + "-" + ПоследняяСтрокаПоДокументуОтгрузки;
		КонецЕсли;
		ДокументыОбОтгрузке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПодстрокиДокументаОбОтгрузке,
				ДиапазонСтрок,
				НомерНаПечать,
				ДатаНаПечать);
				
		Возврат ДокументыОбОтгрузке;
	КонецЕсли;
	Для ИндексСтроки = 0 По ТаблицаДокумента.Количество() - 1 Цикл
		ЭтоПоследняяСтрока = (ИндексСтроки = ТаблицаДокумента.Количество() - 1);
		ТекущийНомерСтроки = ИндексСтроки + 1;
		ПоследняяСтрокаПоДокументуОтгрузки = ТекущийНомерСтроки;
		
		// Для 1-й строки установим предыдущий документ.
		Если Не ЗначениеЗаполнено(ПредыдущийДокумент) Тогда
			// Запоминаем первый документ отгрузки.
			ПредыдущийДокумент = ТаблицаДокумента[ИндексСтроки].Ссылка;
		КонецЕсли;
		
		Если ТаблицаДокумента[ИндексСтроки].Ссылка <> ПредыдущийДокумент Тогда
			// Выводим предыдущий документ.
			ПоследняяСтрокаПоДокументуОтгрузки = ТекущийНомерСтроки - 1;
			
			Если (ПоследняяСтрокаПоДокументуОтгрузки - ПерваяСтрокаПоДокументуОтгрузки) > 0 Тогда
				ДиапазонСтрок = "" + ПерваяСтрокаПоДокументуОтгрузки + "-" + ПоследняяСтрокаПоДокументуОтгрузки;
			Иначе
				ДиапазонСтрок = ПерваяСтрокаПоДокументуОтгрузки;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(ТаблицаДокумента[ИндексСтроки-1].Ссылка) Тогда
				Продолжить;
			КонецЕсли;
			
			Если ТипЗнч(ТаблицаДокумента[ИндексСтроки-1].Ссылка) = Тип("ДокументСсылка.КорректировкаРеализации")
				И ЗначениеЗаполнено(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТаблицаДокумента[ИндексСтроки-1].Ссылка, "ИсправляемыйДокументРеализации")) Тогда
				ДокументОтгрузкиСсылка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТаблицаДокумента[ИндексСтроки-1].Ссылка, "ИсправляемыйДокументРеализации");
			Иначе
				ДокументОтгрузкиСсылка = ТаблицаДокумента[ИндексСтроки-1].Ссылка;
			КонецЕсли;
			
			ДатаДокументаОтгрузки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОтгрузкиСсылка, "Дата");
			ДокументОтгрузки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПодстрокиДокументаОбОтгрузке,
				ДиапазонСтрок,
				Строка(ОбщегоНазначения.ПолучитьНомерНаПечать(ДокументОтгрузкиСсылка)),
				Формат(ДатаДокументаОтгрузки, "ДЛФ='Д'") + " г.");
			Если НЕ ЗначениеЗаполнено(ДокументыОбОтгрузке) Тогда
				ДокументыОбОтгрузке = ДокументОтгрузки;
			Иначе
				ДокументыОбОтгрузке = ДокументыОбОтгрузке + ";" + ДокументОтгрузки;
			КонецЕсли;
			
			ПерваяСтрокаПоДокументуОтгрузки = ТекущийНомерСтроки;
			ПоследняяСтрокаПоДокументуОтгрузки    = ТекущийНомерСтроки;
			ПредыдущийДокумент = ТаблицаДокумента[ИндексСтроки].Ссылка;
		КонецЕсли;
		
		Если ЭтоПоследняяСтрока Тогда
			// Выводим последний документ.
			Если (ПоследняяСтрокаПоДокументуОтгрузки - ПерваяСтрокаПоДокументуОтгрузки) > 0 Тогда
				ДиапазонСтрок = "" + ПерваяСтрокаПоДокументуОтгрузки + "-" + ПоследняяСтрокаПоДокументуОтгрузки;
			Иначе
				ДиапазонСтрок = ПерваяСтрокаПоДокументуОтгрузки;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ТаблицаДокумента[ИндексСтроки].Ссылка) Тогда
				Продолжить;
			КонецЕсли;
			
			Если ТипЗнч(ТаблицаДокумента[ИндексСтроки].Ссылка) = Тип("ДокументСсылка.КорректировкаРеализации")
				И ЗначениеЗаполнено(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТаблицаДокумента[ИндексСтроки].Ссылка, "ИсправляемыйДокументРеализации")) Тогда
				ДокументОтгрузкиСсылка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТаблицаДокумента[ИндексСтроки].Ссылка, "ИсправляемыйДокументРеализации");
			Иначе
				ДокументОтгрузкиСсылка = ТаблицаДокумента[ИндексСтроки].Ссылка;
			КонецЕсли;
			
			ДатаДокументаОтгрузки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОтгрузкиСсылка, "Дата");
			ДокументОтгрузки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПодстрокиДокументаОбОтгрузке,
				ДиапазонСтрок,
				Строка(ОбщегоНазначения.ПолучитьНомерНаПечать(ДокументОтгрузкиСсылка)),
				Формат(ДатаДокументаОтгрузки, "ДЛФ='Д'") + " г.");
			Если НЕ ЗначениеЗаполнено(ДокументыОбОтгрузке) Тогда
				ДокументыОбОтгрузке = ДокументОтгрузки;
			Иначе
				ДокументыОбОтгрузке = ДокументыОбОтгрузке + ";" + ДокументОтгрузки;
			КонецЕсли;
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат ДокументыОбОтгрузке;
	
КонецФункции

// Подготавливает данные для электронного документа типа УПД (информация продавца).
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на объект информационной базы, по которому необходимо 
//                                    создать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//  Отказ - Булево - если нужно отказаться от создания электронного документа, необходимо установить значение в Истина.
//                   После этого дальнейшие действия по формированию документа производиться не будут, поэтому
//                   нужно сформировать сообщения пользователю при необходимости самостоятельно.
//
Процедура ЗаполнитьДанныеДляСЧФИнформацияПродавцаФНС_2019(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	Если ТипЗнч(СсылкаНаОбъект) <> Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаОбъект, "ДокументОснование, ВидСчетаФактуры, ИдентификаторГосКонтракта");
	СтруктураЭД.Вставить("ЭтоСФ", Истина);
	
	Если (СтруктураЭД.Функция = "СЧФ" ИЛИ СтруктураЭД.Функция = "СЧФДОП")
		И (СтруктураРеквизитов.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАванс
		ИЛИ СтруктураРеквизитов.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитента)
		ИЛИ ТипЗнч(СтруктураРеквизитов.ДокументОснование) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") Тогда
		
		ДанныеДокумента = Неопределено;
		УчетнаяПолитика = Неопределено;
		СсылкаНаОбъект.ПолучитьОбъект().СобратьДанныеДляПечати(ДанныеДокумента, УчетнаяПолитика);
		
		// СведенияОПродавце
		БанковскийСчет = Неопределено;
		СведенияОПродавце = ПолучитьДанныеЮрФизЛица_2019(ДанныеДокумента.Организация, БанковскийСчет);
		ТаблицаПродавцов = ЭлектронныеДокументыСлужебный.ДанныеЭлементаДереваЭлектронногоДокумента(ДеревоДанных, "СведенияОПродавце");
		ТаблицаПродавцов.Колонки.Добавить("СведенияОбУчастнике");
		ДанныеПродавца = ПолучитьДанныеУчастникаУПД(СведенияОПродавце, "Юр");
		СтрокаПродавца = ТаблицаПродавцов.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПродавца, ДанныеПродавца);
		ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаПродавцов, "СведенияОПродавце");
		
		// СведенияОПокупателе
		РеквизитыКонтрагента = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ДанныеДокумента.Покупатель, 
			"ГоловнойКонтрагент, ОбособленноеПодразделение, КПП");
		Если РеквизитыКонтрагента.ОбособленноеПодразделение И ЗначениеЗаполнено(РеквизитыКонтрагента.ГоловнойКонтрагент) Тогда
			СведенияОПокупателе = ПолучитьДанныеЮрФизЛица_2019(РеквизитыКонтрагента.ГоловнойКонтрагент);
			СведенияОПокупателе.КПП = РеквизитыКонтрагента.КПП;
		Иначе
			СведенияОПокупателе = ПолучитьДанныеЮрФизЛица_2019(ДанныеДокумента.Покупатель);
		КонецЕсли;
		ТаблицаПокупателей = ЭлектронныеДокументыСлужебный.ДанныеЭлементаДереваЭлектронногоДокумента(ДеревоДанных, "СведенияОПокупателе");
		ТаблицаПокупателей.Колонки.Добавить("СведенияОбУчастнике");
		ДанныеПокупателя = ПолучитьДанныеУчастникаУПД(СведенияОПокупателе, "Юр");
		СтрокаПокупателя = ТаблицаПокупателей.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПокупателя, ДанныеПокупателя);
		ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаПокупателей, "СведенияОПокупателе");
		
		// СведенияОГрузоотправителе
		Если ЗначениеЗаполнено(ДанныеДокумента.Грузоотправитель) Тогда
			Если ДанныеДокумента.Грузоотправитель = "он же" Тогда
				ТаблицаГрузоотправителей = ЭлектронныеДокументыСлужебный.ДанныеЭлементаДереваЭлектронногоДокумента(
					ДеревоДанных, "СведенияОГрузоотправителе");
				ТаблицаГрузоотправителей.Колонки.Добавить("СведенияОбУчастнике");
				ТаблицаГрузоотправителей.Добавить().ОнЖе = Истина;
				ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаГрузоотправителей, "СведенияОГрузоотправителе");
			Иначе
				ТаблицаГрузоотправителей = ЭлектронныеДокументыСлужебный.ДанныеЭлементаДереваЭлектронногоДокумента(ДеревоДанных, "СведенияОГрузоотправителе");
				ТаблицаГрузоотправителей.Колонки.Добавить("СведенияОбУчастнике");
				СведенияОГрузоотправителе = ПолучитьДанныеЮрФизЛица_2019(ДанныеДокумента.Грузоотправитель);
				ДанныеГрузоотправителя = ПолучитьДанныеУчастникаУПД(СведенияОГрузоотправителе, "Факт");
				СтрокаГрузоотправителя = ТаблицаГрузоотправителей.Добавить();
				Грузоотправитель = ЭлектронныеДокументыСлужебный.ДанныеЭлементаДереваЭлектронногоДокумента(ДеревоДанных, "СведенияОГрузоотправителе.НомерСтроки.Грузоотправитель");
				ЗаполнитьЗначенияСвойств(Грузоотправитель, ДанныеГрузоотправителя);
				СтрокаГрузоотправителя.Грузоотправитель = Грузоотправитель;
				ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаГрузоотправителей, "СведенияОГрузоотправителе");
			КонецЕсли;
		КонецЕсли;
		// СведенияОГрузополучателе
		Если ЗначениеЗаполнено(ДанныеДокумента.Грузополучатель) Тогда
			ТаблицаГрузополучателей = ЭлектронныеДокументыСлужебный.ДанныеЭлементаДереваЭлектронногоДокумента(ДеревоДанных, "СведенияОГрузополучателе");
			ТаблицаГрузополучателей.Колонки.Добавить("СведенияОбУчастнике");
			СведенияОГрузополучателе = ПолучитьДанныеЮрФизЛица_2019(ДанныеДокумента.Грузополучатель);
			ДанныеГрузополучателя = ПолучитьДанныеУчастникаУПД(СведенияОГрузополучателе, "Факт");
			СтрокаГрузополучателя = ТаблицаГрузополучателей.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаГрузополучателя, ДанныеГрузополучателя);
			ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаГрузополучателей, "СведенияОГрузополучателе");
		КонецЕсли;
		
		// Реквизиты шапки
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СоставительДокументаНаименование", 
			СведенияОПродавце.ПолноеНаименование);
			
		РеквизитыВалюты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеДокумента.Валюта, "Код, НаименованиеПолное");
		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаКод", СокрЛП(РеквизитыВалюты.Код));
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			"ДополнительныеСведенияОбУчастниках.ВалютаНаименование", СокрЛП(РеквизитыВалюты.НаименованиеПолное));
		
		Если СтруктураРеквизитов.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАванс
			ИЛИ СтруктураРеквизитов.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитента Тогда
			
			ВидСчетаФактуры = "Авансовый";
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВидСчетаФактуры", "Авансовый");
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СодержаниеОперации", 
				НСтр("ru = 'Регистрация счет-фактуры на аванс'"));
			
		Иначе
			
			ВидСчетаФактуры = "Реализация";
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВидСчетаФактуры", "Реализация");
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СодержаниеОперации", 
				"Товары переданы/Услуги оказаны");
			
		КонецЕсли;
		
		// СведенияОТоварах
		ТаблицаТоваров = Новый ТаблицаЗначений;
		ТаблицаТоваров.Колонки.Добавить("НомерСтроки");
		ТаблицаТоваров.Колонки.Добавить("ТоварИдентификатор", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(255)));
		ТаблицаТоваров.Колонки.Добавить("ТоварКод", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(255)));
		ТаблицаТоваров.Колонки.Добавить("ТоварНаименование");
		ТаблицаТоваров.Колонки.Добавить("СтоимостьТоваровБезНалога", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
		ТаблицаТоваров.Колонки.Добавить("НалоговаяСтавка");
		ТаблицаТоваров.Колонки.Добавить("СтоимостьТоваровСНалогом", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
		ТаблицаТоваров.Колонки.Добавить("СуммаНалога", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
		ТаблицаТоваров.Колонки.Добавить("Признак", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(1)));
		
		НомерСтроки = 1;
		ТолькоУслуги = Истина;
		ТекстОшибки = "";
		
		Для Каждого СтрокаТаблицы Из ДанныеДокумента.ТабличнаяЧасть Цикл
			
			НоваяСтрока = ТаблицаТоваров.Добавить();
			НоваяСтрока.НомерСтроки = НомерСтроки;
			НомерСтроки = НомерСтроки + 1;
			
			Если ВидСчетаФактуры = "Авансовый" И 
				(Не ЗначениеЗаполнено(СтрокаТаблицы.Товар) ИЛИ ТипЗнч(СтрокаТаблицы.Товар) = Тип("Строка")) Тогда
				ТекстОшибки = "Для формирования электронного документа необходимо заполнить реквизит ""Номенклатура (обобщенное наименование)""";
				Прервать;
			КонецЕсли;

			НоваяСтрока.ТоварИдентификатор        = Строка(СтрокаТаблицы.Товар.УникальныйИдентификатор());
			НоваяСтрока.ТоварКод                  = СтрокаТаблицы.ТоварКод;
			НоваяСтрока.ТоварНаименование         = СтрокаТаблицы.ТоварНаименование;
			НоваяСтрока.НалоговаяСтавка           = СтрокаТаблицы.СтавкаНДС;
			НоваяСтрока.СуммаНалога               = СтрокаТаблицы.СуммаНДС;
			Если СтрокаТаблицы.СуммаВключаетНДС Тогда
				НоваяСтрока.СтоимостьТоваровСНалогом  = СтрокаТаблицы.Сумма;
				Если Не ВидСчетаФактуры = "Авансовый" Тогда
					НоваяСтрока.СтоимостьТоваровБезНалога = СтрокаТаблицы.Сумма - СтрокаТаблицы.СуммаНДС;
				КонецЕсли;
			Иначе
				НоваяСтрока.СтоимостьТоваровСНалогом  = СтрокаТаблицы.Сумма + СтрокаТаблицы.СуммаНДС;
				Если Не ВидСчетаФактуры = "Авансовый" Тогда
					НоваяСтрока.СтоимостьТоваровБезНалога = СтрокаТаблицы.Сумма;
				КонецЕсли;
			КонецЕсли;
			
			ПризнакТовараЕдиногоДокумента = "";
			Если ТипЗнч(СтрокаТаблицы.Товар) = Тип("СправочникСсылка.Номенклатура") Тогда
				Если ЗначениеЗаполнено(СтрокаТаблицы.Товар)
					И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТаблицы.Товар, "Услуга") Тогда
					ПризнакТовараЕдиногоДокумента = "3"; // Услуга
				Иначе
					ПризнакТовараЕдиногоДокумента = "1"; // Имущество
				КонецЕсли;
			Иначе
				ПризнакТовараЕдиногоДокумента = "5"; // Иное
			КонецЕсли;
			
			НоваяСтрока.Признак = ПризнакТовараЕдиногоДокумента;
			
			Если ТолькоУслуги
				И НоваяСтрока.Признак = "1" Тогда
				ТолькоУслуги = Ложь;
			КонецЕсли;
		
		КонецЦикла;
		
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			ОбщегоНазначения.СообщитьОбОшибке(ТекстОшибки);
		КонецЕсли;
		
		ИтогоСНалогом = ТаблицаТоваров.Итог("СтоимостьТоваровСНалогом");
		ИтогоБезНалога = ТаблицаТоваров.Итог("СтоимостьТоваровБезНалога");
		ИтогоСуммаНалога = ТаблицаТоваров.Итог("СуммаНалога");
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВсегоКОплате.ВсегоСтоимостьТоваровСНалогом", 
			ИтогоСНалогом);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВсегоКОплате.ВсегоСтоимостьТоваровБезНалога", 
			ИтогоБезНалога);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВсегоКОплате.ВсегоСуммаНалога", 
			ИтогоСуммаНалога);
		
		ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаТоваров, "СведенияОТоварах");
		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ТолькоУслуги", ТолькоУслуги);
		
		ПлатежныеДокументы = Новый ТаблицаЗначений();
		ПлатежныеДокументы.Колонки.Добавить("ДатаПРД");
		ПлатежныеДокументы.Колонки.Добавить("НомерПРД");
		
		Если ДанныеДокумента.ТаблицаДатОплат <> Неопределено Тогда
			Для Каждого ПлатежныйДокумент ИЗ ДанныеДокумента.ТаблицаДатОплат Цикл
				НовыйПлатежныйДокумент = ПлатежныеДокументы.Добавить();
				НовыйПлатежныйДокумент.ДатаПРД  = ПлатежныйДокумент.ДатаПлатежноРасчетногоДокумента;
				НовыйПлатежныйДокумент.НомерПРД = ПлатежныйДокумент.НомерПлатежноРасчетногоДокумента;
			КонецЦикла;
		КонецЕсли;
		ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ПлатежныеДокументы, "ПлатежноРасчетныеДокументы");
		
		Если Не ПустаяСтрока(СтруктураРеквизитов.ИдентификаторГосКонтракта) Тогда
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДополнительныеСведенияОбУчастниках.ИдентификаторГосКонтракта", 
				СтруктураРеквизитов.ИдентификаторГосКонтракта);
			ДопДанные = Новый ТаблицаЗначений;
			ДопДанные.Колонки.Добавить("Идентификатор");
			ДопДанные.Колонки.Добавить("Значение");
			СтрокаТаблицы = ДопДанные.Добавить();
			СтрокаТаблицы.Идентификатор = "ИдентификаторГосКонтракта";
			СтрокаТаблицы.Значение = СтруктураРеквизитов.ИдентификаторГосКонтракта;
			ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ДопДанные, "ДопДанныеСчетаФактуры.ТекстоваяИнформация");
		КонецЕсли;

	Иначе
		
		ЭтоСводныйСчетФактура    = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаОбъект, "СводныйКомиссионный");
		СтруктураЭД.Вставить("ЭтоСводныйСчетФактура", ЭтоСводныйСчетФактура);
		СтруктураЭД.Вставить("ЭтоСчетФактураНаАванс", ЭтоСчетФактураНаАванс(СсылкаНаОбъект));
		
		ЗаполнитьДанныеДляДОПИнформацияПродавцаФНС_2019(СтруктураРеквизитов.ДокументОснование, СтруктураЭД, ДеревоДанных, Отказ);
		ОбстоятельстваФормированияСФ = "1";
		
	КонецЕсли;
	
	ДополнитьУПД_ДаннымиПоСчетуФактуре(СсылкаНаОбъект, ДеревоДанных);
	
КонецПроцедуры

Функция ЭтоСчетФактураНаАванс(СчетФактура)
	
	Если СчетФактура = Неопределено Или ТипЗнч(СчетФактура) <> Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВидСчетаФактуры = СчетФактура.ВидСчетаФактуры;

	Возврат ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАванс
		ИЛИ ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитента;
	
КонецФункции

// Подготавливает данные для электронного документа типа УПД (информация покупателя).
//
// Параметры:
//  СсылкаНаЭД - Ссылка - ссылка на ЭД, по которому необходимо сформировать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//  Отказ - Булево - если нужно отказаться от создания электронного документа, необходимо установить значение в Истина.
//                   После этого дальнейшие действия по формированию документа производиться не будут, поэтому
//                   нужно сформировать сообщения пользователю при необходимости самостоятельно.
//
Процедура ЗаполнитьДанныеДляУПДИнформацииПокупателяФНС_2019(СсылкаНаЭД, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	ДокументОснования = Документы.ПоступлениеТоваровУслуг.ПустаяСсылка();
	Для Каждого ДокументИБ Из СсылкаНаЭД.ВладелецФайла.ДокументыОснования Цикл
		
		Если ТипЗнч(ДокументИБ.ДокументОснование) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
			ДокументОснования = ДокументИБ.ДокументОснование;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если Константы.ВводитьИнформациюОЛицахПринявшихТоварыВЭД.Получить() Тогда
		
		Если ДокументОснования <> Документы.ПоступлениеТоваровУслуг.ПустаяСсылка() Тогда
			Запрос = Новый Запрос;
			Запрос.Текст = 
				"ВЫБРАТЬ
				|	ЛицаПринявшиеТовары.ИныеСведения,
				|	ЛицаПринявшиеТовары.ТипЛицаПринявшегоТовар,
				|	ЛицаПринявшиеТовары.ФизЛицо,
				|	ЛицаПринявшиеТовары.Должность,
				|	ЛицаПринявшиеТовары.ОснованиеПолномочий,
				|	ЛицаПринявшиеТовары.НаименованиеОрганизации,
				|	ЛицаПринявшиеТовары.ДоверенностьНаПринятие
				|ИЗ
				|	РегистрСведений.ЛицаПринявшиеТовары КАК ЛицаПринявшиеТовары
				|ГДЕ
				|	ЛицаПринявшиеТовары.ДокументПриемки = &Регистратор";
			Запрос.УстановитьПараметр("Регистратор", ДокументОснования);
			ВыборкаПоРегистратору = Запрос.Выполнить().Выбрать();

			Если ВыборкаПоРегистратору.Следующий() Тогда
			
				Если ВыборкаПоРегистратору.ТипЛицаПринявшегоТовар = 1 Тогда
 					ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОЛицеПринявшемТовары", "РаботникОрганизацииПокупателя");
					ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОЛицеПринявшемТовары.РаботникОрганизацииПокупателя", Истина);
					ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОЛицеПринявшемТовары.РаботникОрганизацииПокупателя.Должность", ВыборкаПоРегистратору.Должность);
					ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОЛицеПринявшемТовары.РаботникОрганизацииПокупателя.ИныеСведения", ВыборкаПоРегистратору.ИныеСведения);
					ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОЛицеПринявшемТовары.РаботникОрганизацииПокупателя.ОснованиеПолномочий", ВыборкаПоРегистратору.ОснованиеПолномочий);
					СтруктураФИО = ФормированиеПечатныхФорм.ФамилияИмяОтчество(ВыборкаПоРегистратору.ФизЛицо, СтруктураЭД.ВладелецЭД.Дата);
					ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОЛицеПринявшемТовары.РаботникОрганизацииПокупателя.Фамилия", СтруктураФИО.Фамилия);
					ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОЛицеПринявшемТовары.РаботникОрганизацииПокупателя.Имя", СтруктураФИО.Имя);
					ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОЛицеПринявшемТовары.РаботникОрганизацииПокупателя.Отчество", СтруктураФИО.Отчество);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
			
	КонецЕсли;
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СоставительДокументаНаименование", 
		СтруктураЭД.Организация.НаименованиеПолное);
		
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОПринятииТоваров.СодержаниеОперации", 
		"Товары приняты/Услуги получены");

	Если ЗначениеЗаполнено(ДокументОснования) Тогда
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОПринятииТоваров.ДатаПолученияТоваров", 
			?(ЗначениеЗаполнено(ДокументОснования.ДатаПриема), ДокументОснования.ДатаПриема, СтруктураЭД.ВладелецЭД.Дата));
	Иначе
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОПринятииТоваров.ДатаПолученияТоваров", 
			СтруктураЭД.ВладелецЭД.Дата);
	КонецЕсли;
		
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОПринятииТоваров.КодИтога", "1");
				
КонецПроцедуры

// Сохраняет данные из электронного документа в объекты ИБ (УПД версии 2019).
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - структура параметров документа ИБ.
//  СсылкиНаВладельцев - Массив - документы информационной базы, созданные ранее по входящему электронному документу.
//  СпособОбработки - Структура - способы сохранения данных в информационной базе.
//     * ПервичныйДокумент - Строка - способ сохранения первичного документа.
//     * СчетФактура       - Строка - способ сохранения счета-фактуры.
//  ОписаниеОшибки - Строка - описание ошибки создания учетного документа.
//
Процедура НайтиСоздатьУниверсальныйПередаточныйДокумент_2019(ДеревоДанных, СсылкиНаВладельцев = Неопределено, СпособОбработки = Неопределено, ОписаниеОшибки = "") Экспорт
	
	ПервичныйДокумент = Неопределено;
	СчетФактура = Неопределено;
	
	Если СсылкиНаВладельцев <> Неопределено Тогда
		Для Каждого Ссылка Из СсылкиНаВладельцев Цикл
			Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
				СчетФактура = Ссылка;
			Иначе
				ПервичныйДокумент = Ссылка;
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	ДокументыУчета = Новый Массив;	
	СкорректироватьСсылкиНаВладельцев(ПервичныйДокумент, СчетФактура);
	
	НайтиСоздатьУПДДокументОПередаче_2019(ДеревоДанных, ПервичныйДокумент, , СпособОбработки, );
	ДокументыУчета.Добавить(ПервичныйДокумент);
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияСчетаФактуры", 
		ПервичныйДокумент);
		
	НайтиСоздатьУПДСчетФактуру_2019(ДеревоДанных, СчетФактура, , СпособОбработки);
	ДокументыУчета.Добавить(СчетФактура);
	
	СсылкиНаВладельцев = ДокументыУчета;
		
КонецПроцедуры

// Сохраняет данные из электронного документа в объекты ИБ (УПД-ДОП версии 2019).
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - структура параметров документа ИБ.
//  СсылкаНаВладельца - Ссылка - ссылка на объект ИБ, владельца электронного документа.
//  Записывать - Булево - признак необходимости записывать объект ИБ.
//  СпособОбработки - Строка - способ сохранения данных в информационной базе.
//  ОписаниеОшибки - Строка - описание ошибки создания учетного документа.
//
Процедура НайтиСоздатьУПДДокументОПередаче_2019(ДеревоДанных, СсылкаНаВладельца, Записывать, СпособОбработки, ОписаниеОшибки) Экспорт
	
	ВидОперации = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВидОперации");
		
	Если ВидОперации = Перечисления.ВидыОперацийЭД.Исправление Тогда
		ИмяТипаДокумента = "КорректировкаПоступления";
	Иначе
		ИмяТипаДокумента = "ПоступлениеТоваровУслуг";
	КонецЕсли;
	
	ДанныеДокумента = Новый Структура("ВидДокумента", "УПДДокументОПередаче");
	ПрочитатьДанныеУниверсальногоДокумента_2019(ДеревоДанных, ДанныеДокумента, ИмяТипаДокумента);
	
	Попытка
		Если ЗначениеЗаполнено(СсылкаНаВладельца) Тогда // получены изменения по существующему документу
			Если СсылкаНаВладельца.Проведен Тогда 
				ВызватьИсключение НСтр("ru = 'Заполнение на основании ЭД возможно только для непроведенного документа'");
			КонецЕсли;
			ДокументОбъект = СсылкаНаВладельца.ПолучитьОбъект();
		Иначе  // создаем новый
			УстановитьПривилегированныйРежим(Истина);
			ДокументОбъект = Документы[ИмяТипаДокумента].СоздатьДокумент();
			ДанныеДокумента.Шапка.Свойство("Организация", ДокументОбъект.Организация);
			ДанныеДокумента.Шапка.Свойство("Контрагент", ДокументОбъект.Контрагент);
			ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ДокументОбъект, глЗначениеПеременной("глТекущийПользователь"), , "Покупка");
			ДокументОбъект.Дата = ТекущаяДатаСеанса();
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ДокументОбъект, ДанныеДокумента.Шапка);
		Если ВидОперации = Перечисления.ВидыОперацийЭД.Исправление Тогда
			Если ЗначениеЗаполнено(ДокументОбъект.ДокументПоступления) Тогда
				Попытка
					ДокументОбъект.Заполнить(ДокументОбъект.ДокументПоступления);
					ЗаполнитьТабличныеЧастиКорректировкиПоступления(ДокументОбъект, ДанныеДокумента);
				Исключение
					ДокументОбъект.Товары.Загрузить(ДанныеДокумента.Товары);
					ДокументОбъект.Услуги.Загрузить(ДанныеДокумента.Услуги);
				КонецПопытки;
			Иначе
				ДокументОбъект.Товары.Загрузить(ДанныеДокумента.Товары);
				ДокументОбъект.Услуги.Загрузить(ДанныеДокумента.Услуги);
			КонецЕсли;
		Иначе
			ДокументОбъект.Товары.Загрузить(ДанныеДокумента.Товары);
			ДокументОбъект.Услуги.Загрузить(ДанныеДокумента.Услуги);
		КонецЕсли;
		ДокументОбъект.СерийныеНомера.Загрузить(ДанныеДокумента.СерийныеНомера);
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДокументОбъект, "ШтрихкодыУпаковок") Тогда
			ДокументОбъект.ШтрихкодыУпаковок.Загрузить(ДанныеДокумента.ШтрихкодыУпаковок);
		КонецЕсли;
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДокументОбъект, "СведенияПрослеживаемости") Тогда
			ДокументОбъект.СведенияПрослеживаемости.Загрузить(ДанныеДокумента.СведенияОПрослеживаемости);
		КонецЕсли;
		ПересчитатьСуммы(ДокументОбъект);
		ДокументОбъект.ОбменДанными.Загрузка = Истина;
		ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
		СсылкаНаВладельца = ДокументОбъект.Ссылка;
	Исключение
		ОписаниеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Заполнение на основании ЭД'"),
			УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы[ИмяТипаДокумента], 
			СсылкаНаВладельца, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение ОписаниеОшибки;
	КонецПопытки;
	
КонецПроцедуры

// Сохраняет данные из электронного документа в объекты ИБ (УПД-СЧФ версия 2019).
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - структура параметров документа ИБ.
//  СсылкаНаВладельца - Ссылка - ссылка на объект ИБ, владельца электронного документа.
//  СпособОбработки - Строка - способ сохранения данных в информационной базе.
//  ОписаниеОшибки - Строка - описание ошибки создания учетного документа.
//
Процедура НайтиСоздатьУПДСчетФактуру_2019(ДеревоДанных, СсылкаНаВладельца = Неопределено, СпособОбработки = "", ОписаниеОшибки = "") Экспорт
	
	ИмяТипаДокумента = "СчетФактураПолученный";
	
	ДанныеДокумента = Новый Структура("ВидДокумента", "УПДСчетФактура");
	ПрочитатьДанныеУниверсальногоДокумента_2019(ДеревоДанных, ДанныеДокумента, ИмяТипаДокумента);

	Попытка
		Если ЗначениеЗаполнено(СсылкаНаВладельца) Тогда // получены изменения по существующему документу
			Если СсылкаНаВладельца.Проведен Тогда 
				ВызватьИсключение НСтр("ru = 'Заполнение на основании ЭД возможно только для непроведенного документа'");
			КонецЕсли;
			ДокументОбъект = СсылкаНаВладельца.ПолучитьОбъект();
		Иначе  // создаем новый
			УстановитьПривилегированныйРежим(Истина);
			ДокументОбъект = Документы[ИмяТипаДокумента].СоздатьДокумент();
			ДанныеДокумента.Шапка.Свойство("Организация", ДокументОбъект.Организация);
			ДанныеДокумента.Шапка.Свойство("Контрагент", ДокументОбъект.Контрагент);
			ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ДокументОбъект, глЗначениеПеременной("глТекущийПользователь"), , "Покупка");
			ДокументОбъект.Дата = ТекущаяДатаСеанса();
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ДокументОбъект, ДанныеДокумента.Шапка);
		
		ДокументОбъект.ДокументыОснования.Загрузить(ДанныеДокумента.ДокументыОснования);
		
		ВидСчетаФактуры = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВидСчетаФактуры");
		
		Если ВидСчетаФактуры = "Авансовый" Тогда
			ДокументОбъект.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.НаАванс;
			
			ДокументОбъект.Авансы.Очистить();
			СведенияОТоварах = ДеревоДанных.Строки.Найти("СведенияОТоварах", "ПолныйПуть");
			Для Каждого СведенияОТоваре Из СведенияОТоварах.Строки Цикл
				НоваяСтрока = ДокументОбъект.Авансы.Добавить();
				НоваяСтрока.Сумма     = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(СведенияОТоваре,
					"СведенияОТоварах.НомерСтроки.СтоимостьТоваровСНалогом");
				НоваяСтрока.СуммаНДС  = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(СведенияОТоваре,
					"СведенияОТоварах.НомерСтроки.СуммаНалога");
				НоваяСтрока.СтавкаНДС = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(СведенияОТоваре,
					"СведенияОТоварах.НомерСтроки.НалоговаяСтавка");
			КонецЦикла;
			
		Иначе
			ДокументОбъект.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.НаПоступление;
		КонецЕсли;
			
		ДокументОбъект.СуммаНДСДокумента = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных,
			"ВсегоКОплате.ВсегоСуммаНалога");
			
		Если ДокументОбъект.ДокументыОснования.Количество() = 0 Тогда
			
			Если НЕ ЗначениеЗаполнено(ДокументОбъект.ДоговорКонтрагента) Тогда
				СписокВидовДоговоров = Новый СписокЗначений;
				СписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком);
				СписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером);
				СписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомитентом);
				УправлениеВзаиморасчетами.УстановитьДоговорКонтрагента(
					ДокументОбъект.ДоговорКонтрагента, ДокументОбъект.Контрагент, ДокументОбъект.Организация, СписокВидовДоговоров);
			КонецЕсли;
			
			ДокументОбъект.ДокументыОснования.Добавить();
		КонецЕсли;
			
		ДокументОбъект.КодСпособаПолучения = 2;
		ДокументОбъект.УстановитьКодВидаОперации();
		ДокументОбъект.СуммаДокумента = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных,
				"ВсегоКОплате.ВсегоСтоимостьТоваровСНалогом");
		
		
		Если ДокументОбъект.ДокументыОснования.Количество() > 0 Тогда
			ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
			СсылкаНаВладельца = ДокументОбъект.Ссылка;
		КонецЕсли;
			
	Исключение
		ОписаниеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Заполнение на основании ЭД'"),
			УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы[ИмяТипаДокумента], 
			СсылкаНаВладельца, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение ОписаниеОшибки;
	КонецПопытки;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Счет на оплату ФНС

// Сохраняет данные из электронного документа в объекты ИБ.
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - структура параметров документа ИБ.
//  СсылкаНаВладельца - Ссылка - ссылка на объект ИБ, владельца электронного документа.
//  Записывать - Булево - признак необходимости записывать объект ИБ.
//  СпособОбработки - Строка - способ сохранения данных в информационной базе.
//  ОписаниеОшибки - Строка - описание ошибки создания учетного документа.
//
Процедура НайтиСоздатьСчетНаОплату101(ДеревоДанных, СсылкаНаВладельца = Неопределено, Записывать = Истина, СпособОбработки = "", ОписаниеОшибки = "") Экспорт
	
	УстановитьПривилегированныйРежим(Истина);

	ДанныеДляЗагрузки = ПодготовитьСтруктуруДляСчетаНаОплату_1_01(ДеревоДанных, ОписаниеОшибки);
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСчетНаОплатуПоставщика(СсылкаНаВладельца, ДанныеДляЗагрузки, СпособОбработки, ОписаниеОшибки);
	
КонецПроцедуры

Функция ОписаниеДанныхПриЗагрузкеСчетаНаОплату_1_01()
	
	ОписаниеОбъекта = Новый Структура;
	// Общие реквизиты шапки.
	ОписаниеОбъекта.Вставить("НомерВходящегоДокумента", "");
	ОписаниеОбъекта.Вставить("ДатаВходящегоДокумента", Дата(1, 1, 1));
	ОписаниеОбъекта.Вставить("НомерВходящегоДокументаЭлектронногоОбмена", "");
	ОписаниеОбъекта.Вставить("ДатаВходящегоДокументаЭлектронногоОбмена", Дата(1, 1, 1));
	ОписаниеОбъекта.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	ОписаниеОбъекта.Вставить("Контрагент", Справочники.Контрагенты.ПустаяСсылка());
	ОписаниеОбъекта.Вставить("СтруктурнаяЕдиница", Справочники.БанковскиеСчета.ПустаяСсылка());
	
	// Финансовые реквизиты шапки.
	ОписаниеОбъекта.Вставить("ВалютаДокумента", Справочники.Валюты.ПустаяСсылка());
	ОписаниеОбъекта.Вставить("Курс", 0);
	ОписаниеОбъекта.Вставить("ДатаОплаты", Дата(1, 1, 1));
	ОписаниеОбъекта.Вставить("ДатаПоступления", Дата(1, 1, 1));
	ОписаниеОбъекта.Вставить("СтоимостьБезНДС", 0);
	ОписаниеОбъекта.Вставить("СтоимостьСНДС", 0);
	ОписаниеОбъекта.Вставить("СуммаНалога", 0);
	ОписаниеОбъекта.Вставить("СуммаСкидки", 0);
	ОписаниеОбъекта.Вставить("СуммаДокумента", 0);
	ОписаниеОбъекта.Вставить("НазначениеПлатежа", "");
	ОписаниеОбъекта.Вставить("ИдентификаторПлатежа", "");
	ОписаниеОбъекта.Вставить("УчитыватьНДС", Ложь);
	ОписаниеОбъекта.Вставить("СуммаВключаетНДС", Ложь);
	ОписаниеОбъекта.Вставить("КратностьВзаиморасчетов", 1);
	
	// Реквизиты шапки, не присутствующие в формате обмена.
	ОписаниеОбъекта.Вставить("Склад", Справочники.Склады.ПустаяСсылка());
	ОписаниеОбъекта.Вставить("Ответственный", Справочники.Пользователи.ПустаяСсылка());
	
	// Служебные реквизиты
	ОписаниеОбъекта.Вставить("ОблагаетсяНДСУПокупателя", Ложь);
	
	// Табличная часть товары
	ТаблицаТовары = Новый ТаблицаЗначений();
	ТаблицаТовары.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"));
	ТаблицаТовары.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаТовары.Колонки.Добавить("ХарактеристикаНоменклатуры", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ТаблицаТовары.Колонки.Добавить("ЕдиницаИзмерения", Новый ОписаниеТипов("СправочникСсылка.ЕдиницыИзмерения"));
	ТаблицаТовары.Колонки.Добавить("Признак", Новый ОписаниеТипов("Строка"));
	ТаблицаТовары.Колонки.Добавить("Содержание", Новый ОписаниеТипов("Строка"));
	
	СписокТипов = Новый Массив;
	СписокТипов.Добавить(Тип("Строка"));
	СписокТипов.Добавить(Тип("ПеречислениеСсылка.СтавкиНДС"));
	ТаблицаТовары.Колонки.Добавить("СтавкаНДС", Новый ОписаниеТипов(СписокТипов));
	ТаблицаТовары.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число"));
	ТаблицаТовары.Колонки.Добавить("Коэффициент", Новый ОписаниеТипов("Число"));
	ТаблицаТовары.Колонки.Добавить("Цена", Новый ОписаниеТипов("Число"));
	ТаблицаТовары.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число"));
	ТаблицаТовары.Колонки.Добавить("СуммаНДС", Новый ОписаниеТипов("Число"));
	
	ОписаниеОбъекта.Вставить("Товары", ТаблицаТовары);

	Возврат ОписаниеОбъекта;
	
КонецФункции

Процедура ЗаполнитьСчетНаОплатуПоставщика(ДокументУчета, ДанныеДляЗагрузки, ИмяДокумента, ОписаниеОшибки)
	
	РежимЗаписи      = РежимЗаписиДокумента.Запись;
	
	Попытка
		
		Если ЗначениеЗаполнено(ДокументУчета) Тогда // получены изменения по существующему документу
			
			ДокументОбъект = ДокументУчета.ПолучитьОбъект();
			ДокументОбъект.Товары.Очистить();
			ДокументОбъект.Услуги.Очистить();
			
		Иначе // попробуем найти по РеквизитыИБ и РеквизитыИБКонтрагента
			
			УстановитьПривилегированныйРежим(Истина);
			
			ДокументОбъект = Документы.СчетНаОплатуПоставщика.СоздатьДокумент();
			ДокументОбъект.Дата = ТекущаяДатаСеанса();
			ДокументОбъект.Организация = ДанныеДляЗагрузки.Организация;
			ДокументОбъект.Заполнить(ДанныеДляЗагрузки);
			
		КонецЕсли;
		
		ДокументОбъект.ОбменДанными.Загрузка = Истина;
		
		// Заполним значения реквизитов шапки
		ПерезаполнениеЗначенийРеквизитовШапки(ДокументОбъект, ДанныеДляЗагрузки);
		
		Для Каждого СтрокаТовары Из ДанныеДляЗагрузки.Товары Цикл
			Если СтрокаТовары.Признак = "2" Или СтрокаТовары.Признак = "3" Тогда
				// Услуги или Работы
				НоваяСтрока = ДокументОбъект.Услуги.Добавить();
			Иначе
				НоваяСтрока = ДокументОбъект.Товары.Добавить();
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТовары);
		КонецЦикла;
		
		ДокументОбъект.СуммаДокумента = УчетНДС.ПолучитьСуммуДокументаСНДС(ДокументОбъект);
		
		Если ДокументОбъект.ЭтоНовый() Тогда
			ДокументОбъект.УстановитьНовыйНомер();
		КонецЕсли;
		
		ДокументОбъект.Записать(РежимЗаписи);
		Если Не ЗначениеЗаполнено(ДокументУчета) Тогда
			ДокументУчета = ДокументОбъект.Ссылка;
		КонецЕсли;
		
	Исключение
		
		ОписаниеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначения.СообщитьОбОшибке(ОписаниеОшибки);
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Заполнение на основании ЭД'"), 
			УровеньЖурналаРегистрации.Ошибка, 
			Метаданные.Документы.ЗаказПокупателя, 
			ДокументУчета, 
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

Функция ПодготовитьСтруктуруДляСчетаНаОплату_1_01(ДеревоДанных, ОписаниеОшибки)
	
	ОписаниеОбъекта = ОписаниеДанныхПриЗагрузкеСчетаНаОплату_1_01();
	
	ДанныеЭД = ДанныеВходящегоЭД(ДеревоДанных);
	
	// Реквизиты шапки
	ОписаниеОбъекта.НомерВходящегоДокумента = ДанныеЭД.НомерДокумента;
	ОписаниеОбъекта.ДатаВходящегоДокумента = ДанныеЭД.ДатаДокумента;
	
	ПрочитатьДанныеУчастника(ДеревоДанных, ОписаниеОбъекта, "Покупатель", "Организация", "Организации");
	ПрочитатьДанныеУчастника(ДеревоДанных, ОписаниеОбъекта, "Продавец", "Контрагент", "Контрагенты");
	
	// Валюта
	Валюта = НайтиСсылкуНаОбъект("Валюты",ДанныеЭД.ДенежнаяЕдиница.КодВалюты);
	ОписаниеОбъекта.ВалютаДокумента = Валюта;
	ОписаниеОбъекта.Курс = ДанныеЭД.ДенежнаяЕдиница.КурсВалюты;
	Если Не ЗначениеЗаполнено(Валюта) Тогда
		ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'В базе данных не удалось найти валюту с кодом ""%1""'"),
			ДанныеЭД.ДенежнаяЕдиница.КодВалюты);
	КонецЕсли;

	// Банковский счет организации
	НомерСчетаОрганизации = ДанныеЭД.Плательщик.БанковскиеРеквизиты.НомерСчета;
	БанковскийСчетОрганизации = НайтиСсылкуНаОбъектПоРеквизиту(
		"БанковскиеСчета", "НомерСчета", НомерСчетаОрганизации, ОписаниеОбъекта.Организация);
	Если ЗначениеЗаполнено(БанковскийСчетОрганизации) Тогда
		ОписаниеОбъекта.БанковскийСчет = БанковскийСчетОрганизации;
	Иначе
		БанковскийСчетОрганизации = ОписаниеОбъекта.Организация.ОсновнойБанковскийСчет;
	КонецЕсли;
	
	// Дополнительные сведения
	ОписаниеОбъекта.ДатаОплаты = ДанныеЭД.ДополнительныеСведения.ОграничениеПоДатеОплаты;
	ОписаниеОбъекта.ДатаПоступления = ДанныеЭД.ДополнительныеСведения.ДатаНачалаПоставки;
	ОписаниеОбъекта.НазначениеПлатежа = ДанныеЭД.ДополнительныеСведения.НазначениеПлатежа;
	ОписаниеОбъекта.ИдентификаторПлатежа = ДанныеЭД.ДополнительныеСведения.ИнформацияДляОплаты;
	
	// Суммы по документу
	ОписаниеОбъекта.СтоимостьБезНДС = ДанныеЭД.ВсегоКОплате.СтоимостьСНДС;
	ОписаниеОбъекта.СтоимостьСНДС = ДанныеЭД.ВсегоКОплате.СтоимостьСНДС;
	ОписаниеОбъекта.СуммаНалога = ДанныеЭД.ВсегоКОплате.СуммаНалога.СуммаНалога;
	ОписаниеОбъекта.СуммаСкидки = ДанныеЭД.ВсегоКОплате.СуммаСкидки;
	ОписаниеОбъекта.СуммаДокумента = ДанныеЭД.ВсегоКОплате.СтоимостьСНДС;
	
	// Товары
	Идентификатор = "";
	Для Каждого СведенияОТоваре Из ДанныеЭД.ТаблицаТоваров Цикл
		НоваяСтрока = ОписаниеОбъекта.Товары.Добавить();
		
		Если СведенияОТоваре.Сопоставление <> Неопределено Тогда
			
			Идентификатор = СведенияОТоваре.Сопоставление.Идентификатор;
			
			Если Не ЗначениеЗаполнено(Идентификатор) Тогда
				Идентификатор = ИдентификаторТовараПоСтроке(ВРег(СтрЗаменить(СведенияОТоваре.Наименование, " ", "")) + "###");
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Идентификатор) Тогда
				РеквизитыПоиска = Новый Структура("Владелец, Идентификатор", ОписаниеОбъекта.Контрагент, Идентификатор);
				РезультатПоиска = ПолучитьДанныеТовараПоНоменклатуреПоставщика(РеквизитыПоиска);
				Если ЗначениеЗаполнено(РезультатПоиска.Номенклатура) Тогда
					НоваяСтрока.Номенклатура = РезультатПоиска.Номенклатура;
				КонецЕсли;
				Если ЗначениеЗаполнено(РезультатПоиска.ХарактеристикаНоменклатуры) Тогда
					НоваяСтрока.ХарактеристикаНоменклатуры = РезультатПоиска.ХарактеристикаНоменклатуры;
				КонецЕсли;
			КонецЕсли;
			
			Номенклатура = СведенияОТоваре.Сопоставление.НоменклатураИБ;
			Если ЗначениеЗаполнено(Номенклатура) Тогда
				НоваяСтрока.Номенклатура = Номенклатура;
			КонецЕсли;
			
			Характеристика = СведенияОТоваре.Сопоставление.ХарактеристикаИБ;
			Если ЗначениеЗаполнено(Характеристика) Тогда
				ИспользованиеХарактеристик = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НоваяСтрока.Номенклатура, "ВестиУчетПоХарактеристикам");
				Если ИспользованиеХарактеристик Тогда
					НоваяСтрока.Характеристика = Характеристика;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НоваяСтрока.Номенклатура) Тогда
			ЕдиницаПоКлассификатору = НайтиСсылкуНаОбъект("ЕдиницыИзмерения", СведенияОТоваре.Сопоставление.ЕдиницаИзмеренияКод);
			Если ЗначениеЗаполнено(ЕдиницаПоКлассификатору) Тогда
				ЕдиницаИзмерения = НайтиСсылкуНаОбъектПоРеквизиту("ЕдиницыИзмерения", "ЕдиницаПоКлассификатору", 
					ЕдиницаПоКлассификатору, НоваяСтрока.Номенклатура);
				Если ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
					НоваяСтрока.ЕдиницаИзмерения = ЕдиницаИзмерения;
					Если ЕдиницаИзмерения.Коэффициент <> 0 Тогда
						НоваяСтрока.Коэффициент = ЕдиницаИзмерения.Коэффициент;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НоваяСтрока.Номенклатура) И НоваяСтрока.Номенклатура.Услуга Тогда
			НоваяСтрока.Содержание = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			НоваяСтрока.Номенклатура, "НаименованиеПолное");
		КонецЕсли;
		
		НоваяСтрока.Признак = СведенияОТоваре.ДополнительныеСведения.Признак;
		
		НоваяСтрока.Сумма    = СведенияОТоваре.СтоимостьСНДС;
		НоваяСтрока.СуммаНДС = СведенияОТоваре.СуммаНДС.СуммаНалога;
		
		НоваяСтрока.СтавкаНДС = ?(
			СведенияОТоваре.СтавкаНДС = "НДС исчисляется налоговым агентом",
			Перечисления.СтавкиНДС.БезНДС,
			СведенияОТоваре.СтавкаНДС);
			
		Если Не ОписаниеОбъекта.ОблагаетсяНДСУПокупателя И СведенияОТоваре.СтавкаНДС = "НДС исчисляется налоговым агентом" Тогда
			ОписаниеОбъекта.ОблагаетсяНДСУПокупателя = Истина;
		КонецЕсли;
		
		НоваяСтрока.Количество = СведенияОТоваре.Количество;
		НоваяСтрока.Цена = (НоваяСтрока.Сумма) / ?(НоваяСтрока.Количество = 0, 1, НоваяСтрока.Количество);
		
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(ОписаниеОбъекта.Ответственный) Тогда
		ОписаниеОбъекта.Ответственный = ПользователиКлиентСервер.АвторизованныйПользователь();
	КонецЕсли;
	
	ОписаниеОбъекта.УчитыватьНДС     = Истина;
	ОписаниеОбъекта.СуммаВключаетНДС = Истина;
	
	Возврат ОписаниеОбъекта;
	
КонецФункции


// Подготавливает данные для электронного документа типа Счет формата ФНС 1.01.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  Дерево документа - дерево значений - дерево значений, соответствующее макету СчетНаОплату обработки ЭлектронныеДокументы.
//
Процедура ЗаполнитьДанныеПоСчету101(СсылкаНаОбъект, СтруктураЭД, ДеревоДокумента) Экспорт
	
	Основания = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СсылкаНаОбъект);
	
	ДанныеДокументов = Документы.СчетНаОплатуПокупателю.ДанныеДокументовДляСчетаНаОплату_1_01(Основания);
	
	ДанныеШапки = ДанныеДокументов.РезультатПоШапке.Выбрать();
	ДанныеШапки.Следующий();
	
	ТоварыДокумента = ДанныеДокументов.РезультатПоТабличнойЧасти.Выгрузить();
	
	// Заполнение по данным шапки документа.
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "Функция", "0");
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "НомерДокумента", ДанныеШапки.НомерДокумента);
	
	// Заполнение сведений об основаниях.
	ОснованияСчета = ЭлектронныеДокументыСлужебный.ДанныеЭлементаДереваЭлектронногоДокумента(ДеревоДокумента, "Основания");
	Если ЗначениеЗаполнено(ДанныеШапки.ДокументОснование) Тогда
		
		РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеШапки.ДокументОснование, "Номер, Дата");
		
		НоваяСтрока = ОснованияСчета.Добавить();
		НоваяСтрока.Наименование  = ДанныеШапки.ДокументОснованиеНаименование;
		НоваяСтрока.Номер         = ?(ЗначениеЗаполнено(РеквизитыОснования.Номер),
													РеквизитыОснования.Номер,
													НСтр("ru = 'Без номера'"));
		НоваяСтрока.Дата          = РеквизитыОснования.Дата;
		НоваяСтрока.Идентификатор = Строка(ДанныеШапки.ДокументОснование.УникальныйИдентификатор());
		
	КонецЕсли;
	ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДокумента, ОснованияСчета, "Основания");
	
	ДатаДокумента = ДанныеШапки.ДатаДокумента;
	
	// Заполнение сведений об участниках операции.
	ПолучательДенежныхСредств = ПолучитьДанныеЮрФизЛица(ДанныеШапки.ОрганизацияПолучатель, ДанныеШапки.БанковскийСчет);
	
	ЗаполнитьРеквизитыУчастникаСчетаНаОплату_1_01(ДеревоДокумента, ПолучательДенежныхСредств, "ПолучательДенежныхСредств", "Юр");
	
	Покупатель = ПолучитьДанныеЮрФизЛица(ДанныеШапки.Контрагент,
											Неопределено);
	
	ЗаполнитьРеквизитыУчастникаСчетаНаОплату_1_01(ДеревоДокумента, Покупатель, "Плательщик", "Юр");
	ЗаполнитьРеквизитыУчастникаСчетаНаОплату_1_01(ДеревоДокумента, Покупатель, "Покупатель", "Юр");
	
	Продавец = ПолучитьДанныеЮрФизЛица(ДанныеШапки.Организация,
											Неопределено);
											
	ЗаполнитьРеквизитыУчастникаСчетаНаОплату_1_01(ДеревоДокумента, Продавец, "Продавец", "Юр");
	
	Если ЗначениеЗаполнено(ДанныеШапки.Грузоотправитель) Тогда
		Грузоотправитель = ПолучитьДанныеЮрФизЛица(ДанныеШапки.Грузоотправитель,
												Неопределено);
												
		ЗаполнитьРеквизитыУчастникаСчетаНаОплату_1_01(ДеревоДокумента, Грузоотправитель, "Грузоотправитель", "Факт");
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеШапки.Грузополучатель) Тогда
		Грузополучатель = ПолучитьДанныеЮрФизЛица(ДанныеШапки.Грузополучатель,
												Неопределено);
												
		ЗаполнитьРеквизитыУчастникаСчетаНаОплату_1_01(ДеревоДокумента, Грузополучатель, "Грузополучатель", "Факт");
		
	КонецЕсли;
	
	// Сведения о товарах.
	ДокументБезНДС = Истина;
	
	ТаблицаТоваров = ТаблицаТоваровСчетаНаОплату_1_01(ДеревоДокумента,
														ДанныеШапки,
														ТоварыДокумента,
														ДокументБезНДС);


	ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДокумента, ТаблицаТоваров, "ТаблицаТоваров");
	
	// Заполнение показателей оплаты.
	ДанныеСуммыНалога = ЭлектронныеДокументыСлужебный.ДанныеЭлементаДереваЭлектронногоДокумента(ДеревоДокумента,
																							"ВсегоКОплате.СуммаНалога");
	
	ДанныеСуммыНалога.СуммаНалога = ТаблицаТоваров.Итог("СуммаНалога");
	
	Если ДокументБезНДС Тогда
		ДанныеСуммыНалога.БезНДС = НСтр("ru = 'без НДС'");
	КонецЕсли;
	
	КоличествоТоваров = ТаблицаТоваров.Итог("Количество");
	
	ВсегоКОплате = ЭлектронныеДокументыСлужебный.ДанныеЭлементаДереваЭлектронногоДокумента(ДеревоДокумента, "ВсегоКОплате");
	ВсегоКОплате.СтоимостьБезНДС = ТаблицаТоваров.Итог("СтоимостьБезНДС");
	ВсегоКОплате.СтоимостьСНДС   = ТаблицаТоваров.Итог("СтоимостьСНДС");
	ВсегоКОплате.Количество      = ?(КоличествоТоваров = 0,
										1,
										КоличествоТоваров);
	ВсегоКОплате.СуммаСкидки     = ТаблицаТоваров.Итог("СуммаСкидки");
	ВсегоКОплате.СуммаНалога     = ДанныеСуммыНалога;
		
	ОбщегоНазначенияЭД.ЗагрузитьСтруктуруВГруппуДерева(ДеревоДокумента, ВсегоКОплате, "ВсегоКОплате");
	СтрокаТаблицы = ДеревоДокумента.Строки.Найти("ВсегоКОплате", "ПолныйПуть", Истина);
	СтрокаТаблицы.Значение = Истина;
	
	// Заполнение дополнительных сведений счета на оплату.
	ДополнительныеСведения = ЭлектронныеДокументыСлужебный.ДанныеЭлементаДереваЭлектронногоДокумента(
								ДеревоДокумента,
								"ДополнительныеСведения");
	ЗаполнитьЗначенияСвойств(ДополнительныеСведения, ДанныеШапки);
	
	ОбщегоНазначенияЭД.ЗагрузитьСтруктуруВГруппуДерева(ДеревоДокумента, ДополнительныеСведения, "ДополнительныеСведения");
	
	СтрокаТаблицы = ДеревоДокумента.Строки.Найти("ДополнительныеСведения", "ПолныйПуть", Истина);
	СтрокаТаблицы.Значение = Истина;
	
	// Заполнение сведений о денежной единице.
	ДенежнаяЕдиница = ЭлектронныеДокументыСлужебный.ДанныеЭлементаДереваЭлектронногоДокумента(ДеревоДокумента, "ДенежнаяЕдиница");
	ЗаполнитьЗначенияСвойств(ДенежнаяЕдиница, ДанныеШапки);
	
	КурсВалюты = МодульВалютногоУчета.ПолучитьКурсВалюты(ДанныеШапки.Валюта,
															ДатаДокумента);
	
	ДенежнаяЕдиница.КурсВалюты = ?(ЗначениеЗаполнено(КурсВалюты.Курс), КурсВалюты.Курс, 1);
	ДенежнаяЕдиница.СуммаНалога = ДанныеСуммыНалога;
	
	ОбщегоНазначенияЭД.ЗагрузитьСтруктуруВГруппуДерева(ДеревоДокумента, ДенежнаяЕдиница, "ДенежнаяЕдиница");
	СтрокаТаблицы = ДеревоДокумента.Строки.Найти("ДенежнаяЕдиница", "ПолныйПуть", Истина);
	СтрокаТаблицы.Значение = Истина;
	
	ВсегоНаименований = ?(КоличествоТоваров = 0,
							1,
							ТаблицаТоваров.Количество());
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ВсегоНаименований", ВсегоНаименований);
	
КонецПроцедуры

Функция ТаблицаТоваровСчетаНаОплату_1_01(Данные, ДанныеШапки, ТоварыДокумента, ДокументБезНДС)
	
	ТаблицаТоваров = ЭлектронныеДокументыСлужебный.ДанныеЭлементаДереваЭлектронногоДокумента(Данные, "ТаблицаТоваров");
	ТаблицаТоваров.Колонки.Добавить("СуммаНалога", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19, 2)));
	
	СоответствиеСтавокНДС = Новый Соответствие;
	ЗаполнитьСоответствиеСтавокНДС(СоответствиеСтавокНДС);
	
	ИспользоватьНаборы = ТоварыДокумента.Колонки.Найти("ЭтоНабор") <> Неопределено;
	
	СоответствиеСтавокНДСКонтрагента = Новый Соответствие;
	ТаблицаСопоставления             = Неопределено;
	ШтрихкодыКомбинаций              = Неопределено;
	ШтрихкодыТоваров                 = Неопределено;
	
	ЗаполнениеКодаТовара = Константы.ДополнительнаяКолонкаПечатныхФормДокументов.Получить();
	
	Для Каждого СтрокаТовары Из ТоварыДокумента Цикл
				
		НоваяСтрокаТоваров = ТаблицаТоваров.Добавить();
		НоваяСтрокаТоваров.Наименование = СтрокаТовары.Наименование + 
											?(ЗначениеЗаполнено(СтрокаТовары.Характеристика), + " (" + СтрокаТовары.Характеристика + ")", "");
		
		НоваяСтрокаТоваров.КодЕдиницыИзмерения          = СокрЛП(СтрокаТовары.ЕдиницаИзмеренияКодПоОКЕИ);
		НоваяСтрокаТоваров.НаименованиеЕдиницыИзмерения = СокрЛП(СтрокаТовары.ЕдиницаИзмеренияНаименование);
		
		НоваяСтрокаТоваров.Количество = СтрокаТовары.Количество;
		
		СуммаСкидки = Ценообразование.ПолучитьСуммуСкидки(СтрокаТовары.СтоимостьСНДС, СтрокаТовары.Скидка);
		
		НоваяСтрокаТоваров.ЦенаЗаЕдиницуИзмерения   = Окр(СтрокаТовары.Цена, 2);
		НоваяСтрокаТоваров.СтоимостьБезНДС          = Окр(СтрокаТовары.СтоимостьБезНДС, 2);
		НоваяСтрокаТоваров.СуммаСкидки              = СуммаСкидки;
		
		СтоимостьСНДСБезСкидки   = СтрокаТовары.СтоимостьСНДС + СуммаСкидки;
		СтоимостьБезНДСБезСкидки = СтрокаТовары.СтоимостьБезНДС + СуммаСкидки;
		
		ЦенаБезСкидки  = СтоимостьБезНДСБезСкидки / СтрокаТовары.Количество;
		
		НоваяСтрокаТоваров.ЦенаБезСкидки            = Окр(ЦенаБезСкидки, 2);
		НоваяСтрокаТоваров.СтоимостьБезНДСБезСкидки = Окр(СтоимостьБезНДСБезСкидки, 2);
		НоваяСтрокаТоваров.СтоимостьСНДС            = Окр(СтрокаТовары.СтоимостьСНДС, 2);
		НоваяСтрокаТоваров.СтоимостьСНДСБезСкидки   = Окр(СтоимостьСНДСБезСкидки, 2);
		
		ДанныеСуммыНДС = ЭлектронныеДокументыСлужебный.ДанныеЭлементаДереваЭлектронногоДокумента(
		Данные,
		"ТаблицаТоваров.НомерСтроки.СуммаНДС");
		ДанныеСуммыНДСБезСкидки = ЭлектронныеДокументыСлужебный.ДанныеЭлементаДереваЭлектронногоДокумента(
		Данные,
		"ТаблицаТоваров.НомерСтроки.СуммаНДСБезСкидки");
		
		//Если Не ДанныеШапки.ОперацияОблагаетсяНДСУПокупателя Тогда
			Если СтрокаТовары.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС Тогда
				ДанныеСуммыНДС.БезНДС          = НСтр("ru = 'без НДС'");
				ДанныеСуммыНДСБезСкидки.БезНДС = НСтр("ru = 'без НДС'");
			Иначе
				ДокументБезНДС = Ложь;
				
				ДанныеСуммыНДС.СуммаНалога = Окр(СтрокаТовары.СуммаНДС, 2);
				
				Если НоваяСтрокаТоваров.СуммаСкидки <> 0 Тогда
					
					ДанныеСуммыНДСБезСкидки.СуммаНалога =
					Окр(УчетНДС.РассчитатьСуммуНДС(СтоимостьБезНДСБезСкидки, СтрокаТовары.УчитыватьНДС,
													СтрокаТовары.СуммаВключаетНДС, УчетНДС.ПолучитьСтавкуНДС(СтрокаТовары.СтавкаНДС)),
					2);
				Иначе
					ДанныеСуммыНДСБезСкидки.СуммаНалога = ДанныеСуммыНДС.СуммаНалога;
				КонецЕсли;
			КонецЕсли;
			
			НоваяСтрокаТоваров.СтавкаНДС         = ПредставлениеСтавкиНДССчетаНаОплату_1_01(СтрокаТовары.СтавкаНДС, СоответствиеСтавокНДС);
			НоваяСтрокаТоваров.СуммаНДС          = ДанныеСуммыНДС;
			НоваяСтрокаТоваров.СуммаНДСБезСкидки = ДанныеСуммыНДСБезСкидки;
			НоваяСтрокаТоваров.СуммаНалога       = ДанныеСуммыНДС.СуммаНалога;
		//Иначе
		//	ДокументБезНДС = Ложь;
		//	
		//	НоваяСтрокаТоваров.СтавкаНДС         = НСтр("ru = 'НДС исчисляется налоговым агентом'");
		//	НоваяСтрокаТоваров.СуммаНДС          = ДанныеСуммыНДС;
		//	НоваяСтрокаТоваров.СуммаНДСБезСкидки = ДанныеСуммыНДСБезСкидки;
		//КонецЕсли;
		
		// Заполнение дополнительных сведений товара.
		ДополнительныеСведения = ЭлектронныеДокументыСлужебный.ДанныеЭлементаДереваЭлектронногоДокумента(
		Данные,
		"ТаблицаТоваров.НомерСтроки.ДополнительныеСведения");
		ЗаполнитьЗначенияСвойств(ДополнительныеСведения, СтрокаТовары);
		
		ДополнительныеСведения.Признак = ПризнакТовара(СтрокаТовары.Номенклатура);
		ДополнительныеСведения.КодВидаТовара = СтрокаТовары.КодВидаТовара;
		
		// Данные сопоставления.
		ШтрихкодыТоваров = ШтрихкодыНоменклатуры(СтрокаТовары.Номенклатура);
		
		Если ЗаполнениеКодаТовара = "Артикул" Тогда
			НоваяСтрокаТоваров.ТоварКод = СтрокаТовары.Артикул;
		ИначеЕсли ЗаполнениеКодаТовара = "Штрихкод" Тогда
			НоваяСтрокаТоваров.ТоварКод = ?(ЗначениеЗаполнено(ШтрихкодыТоваров), ШтрихкодыТоваров[0], "");
		КонецЕсли;
		
		ИДТовара = СтрокаТовары.Номенклатура.УникальныйИдентификатор();
		ИДХарактеристики = ?(ЗначениеЗаполнено(СтрокаТовары.Характеристика), СтрокаТовары.Характеристика.УникальныйИдентификатор(), "");
		ИДУпаковки = "";
		ИдТовараУКонтрагента = Строка(ИДТовара)+"#"+Строка(ИДХарактеристики)+"#"+Строка(ИДУпаковки);
		
		Сопоставление = СопоставлениеНоменклатуры();
		Сопоставление.Идентификатор       = ИдТовараУКонтрагента;
		Сопоставление.Наименование        = СтрокаТовары.Наименование;
		Сопоставление.Характеристика      = СтрокаТовары.ХарактеристикаНаименование;
		Сопоставление.ЕдиницаИзмерения    = СтрокаТовары.ЕдиницаИзмеренияНаименование;
		Сопоставление.ЕдиницаИзмеренияКод = СтрокаТовары.ЕдиницаИзмеренияКодПоОКЕИ;
		Сопоставление.Артикул             = СтрокаТовары.Артикул;
		Сопоставление.СтавкаНДС           = СтрокаТовары.СтавкаНДС;
		Сопоставление.НоменклатураИБ      = СтрокаТовары.Номенклатура;
		Сопоставление.ХарактеристикаИБ    = СтрокаТовары.Характеристика;
		Сопоставление.УпаковкаИБ          = СтрокаТовары.ЕдиницаИзмерения;
		
		Если ЗначениеЗаполнено(ШтрихкодыТоваров) Тогда
			Сопоставление.Штрихкод = ШтрихкодыТоваров;
		КонецЕсли;
		
		НоваяСтрокаТоваров.Сопоставление = Сопоставление;
				
		НоваяСтрокаТоваров.ДополнительныеСведения = ДополнительныеСведения;
		
	КонецЦикла;
		
	
	Возврат ТаблицаТоваров;
	
КонецФункции

// Возвращает строковое представление прикладного значения ставки НДС.
//
// Параметры:
//	СтавкаНДС - Перечисление.СтавкиНДС - значение ставки НДС.
//	СоответствиеСтавокНДС - см. ЗаполнитьСоответствиеСтавокНДС.
//
// Возвращаемое значение:
//	Строка - строковое представление прикладного значения ставки НДС.
//
Функция ПредставлениеСтавкиНДССчетаНаОплату_1_01(СтавкаНДС, СоответствиеСтавокНДС)
	
	Для Каждого КлючИЗначение Из СоответствиеСтавокНДС Цикл
		Если КлючИЗначение.Значение = СтавкаНДС Тогда
			ПредставлениеСтавки = КлючИЗначение.Ключ;
			
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ПредставлениеСтавки = "0"
		Или ПредставлениеСтавки = "10"
		Или ПредставлениеСтавки = "18"
		Или ПредставлениеСтавки = "20" Тогда
		
		ПредставлениеСтавки = ПредставлениеСтавки + "%";
		
	КонецЕсли;
	
	Возврат ПредставлениеСтавки;
	
КонецФункции

Функция ЭтоИндивидуальныйПредприниматель(ПолноеНаименование)
	
	Возврат (ВРЕГ(Лев(ПолноеНаименование,3))="ИП ")
				Или (ВРЕГ(Лев(ПолноеНаименование, СтрДлина("Индивидуальный предприниматель")))="ИНДИВИДУАЛЬНЫЙ ПРЕДПРИНИМАТЕЛЬ");
	
КонецФункции

Процедура ЗаполнитьРеквизитыУчастникаСчетаНаОплату_1_01(ДеревоДанных,
														СведенияОбУчастнике,
														ВидУчастника,
														ВидАдреса = "Юр")
	
	// Заполнение сведений о коде по ОКПО.
	Если СведенияОбУчастнике.Свойство("КодПоОКПО")
		И ЗначениеЗаполнено(СведенияОбУчастнике.КодПоОКПО) Тогда
		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			ВидУчастника + ".КодОКПО",
			СведенияОбУчастнике.КодПоОКПО);
		
	КонецЕсли;
		
	Нерезидент = (ТипЗнч(СведенияОбУчастнике.Ссылка) = Тип("СправочникСсылка.Контрагенты") И СведенияОбУчастнике.Ссылка.НеЯвляетсяРезидентом);
	
	ИндивидуальныйПредприниматель = ЭтоИндивидуальныйПредприниматель(СведенияОбУчастнике.ПолноеНаименование);
	
	// Заполнение сведений о типе участника.
	Если Нерезидент Тогда
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			ВидУчастника + ".ТипУчастника.ИЛ.НаименованиеОрганизации",
			СведенияОбУчастнике.ПолноеНаименование);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			ВидУчастника + ".ТипУчастника.ИЛ.ИдентификаторОрганизации",
			СведенияОбУчастнике.НалоговыйНомерВСтранеРегистрации);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			ВидУчастника + ".ТипУчастника.ИЛ.ОКСМ",
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СведенияОбУчастнике.СтранаРегистрации, "Код"));
	ИначеЕсли СведенияОбУчастнике.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			ВидУчастника + ".ТипУчастника.ЮЛ.НаименованиеОрганизации",
			СведенияОбУчастнике.ПолноеНаименование);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			ВидУчастника + ".ТипУчастника.ЮЛ.ИНН",
			СведенияОбУчастнике.ИНН);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			ВидУчастника + ".ТипУчастника.ЮЛ.КПП",
			СведенияОбУчастнике.КПП);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			ВидУчастника + ".ТипУчастника.ЮЛ.ОГРН",
			СведенияОбУчастнике.ОГРН);
	ИначеЕсли ИндивидуальныйПредприниматель Тогда
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			ВидУчастника + ".ТипУчастника.ИП.ИНН",
			СведенияОбУчастнике.ИНН);
		
		Если ЗначениеЗаполнено(СведенияОбУчастнике.Свидетельство) Тогда
			Свидетельство = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Свидетельство №%1 от %2'"),
								СведенияОбУчастнике.СвидетельствоСерияНомер,
								Формат(СведенияОбУчастнике.СвидетельствоДатаВыдачи, "ДЛФ=D"));
			
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
				ДеревоДанных,
				ВидУчастника + ".ТипУчастника.ИП.СвидетельствоГосударственнойРегистрации",
				Свидетельство);
		КонецЕсли;
		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			ВидУчастника + ".ТипУчастника.ИП.ОГРН",
			СведенияОбУчастнике.ОГРН);
		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			ВидУчастника + ".ТипУчастника.ИП.ФИО.Фамилия",
			СведенияОбУчастнике.Фамилия);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			ВидУчастника + ".ТипУчастника.ИП.ФИО.Имя",
			СведенияОбУчастнике.Имя);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			ВидУчастника + ".ТипУчастника.ИП.ФИО.Отчество",
			СведенияОбУчастнике.Отчество);
	Иначе
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			ВидУчастника + ".ТипУчастника.ФЛ.ИНН",
			СведенияОбУчастнике.ИНН);
		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			ВидУчастника + ".ТипУчастника.ФЛ.ФИО.Фамилия",
			СведенияОбУчастнике.Фамилия);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			ВидУчастника + ".ТипУчастника.ФЛ.ФИО.Имя",
			СведенияОбУчастнике.Имя);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			ВидУчастника + ".ТипУчастника.ФЛ.ФИО.Отчество",
			СведенияОбУчастнике.Отчество);
	КонецЕсли;
	
	// Заполнение сведений о банке.
	Если СведенияОбУчастнике.Свойство("НомерСчета")
		И ЗначениеЗаполнено(СведенияОбУчастнике.НомерСчета) Тогда
		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			ВидУчастника + ".БанковскиеРеквизиты.НомерСчета",
			СведенияОбУчастнике.НомерСчета);
		
		Если СведенияОбУчастнике.Свойство("Банк")
			И ЗначениеЗаполнено(СведенияОбУчастнике.Банк) Тогда
			
			Если ТипЗнч(СведенияОбУчастнике.Банк) = Тип("Строка") Тогда
				Банк = СведенияОбУчастнике.Банк;
			Иначе
				Банк = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СведенияОбУчастнике.Банк, "Наименование");
			КонецЕсли;
			
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
				ДеревоДанных,
				ВидУчастника + ".БанковскиеРеквизиты.НаименованиеБанка",
				Банк);
			
		КонецЕсли;
		
		Если СведенияОбУчастнике.Свойство("БИК")
			И ЗначениеЗаполнено(СведенияОбУчастнике.БИК) Тогда
			
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
				ДеревоДанных,
				ВидУчастника + ".БанковскиеРеквизиты.БИКБанка",
				СведенияОбУчастнике.БИК);
		КонецЕсли;
		
		Если СведенияОбУчастнике.Свойство("КоррСчет")
			И ЗначениеЗаполнено(СведенияОбУчастнике.КоррСчет) Тогда
			
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
				ДеревоДанных,
				ВидУчастника + ".БанковскиеРеквизиты.КорреспондентскийСчетБанка",
				СведенияОбУчастнике.КоррСчет);
		КонецЕсли;
		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			ВидУчастника + ".БанковскиеРеквизиты.РасчетныйСчет",
			СведенияОбУчастнике.НомерСчета);
		
	КонецЕсли;
		
	АдресУчастника = Новый Структура;
	
	АдресУчастника = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьАдресСтруктурой(СведенияОбУчастнике, "Ссылка", ВидАдреса);
	
	Если АдресУчастника.Свойство("АдресРФ") И АдресУчастника.АдресРФ Тогда		
		ЗаполнитьАдресВДереве(ДеревоДанных, АдресУчастника, "АдресРФ", ВидУчастника);
	Иначе
		ЗаполнитьАдресВДереве(ДеревоДанных, АдресУчастника, "АдресИнформация", ВидУчастника);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СведенияОбУчастнике.Телефоны) Тогда
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			ВидУчастника + ".КонтактныеСведения.Телефоны.НомерСтроки.Телефон",
			СведенияОбУчастнике.Телефоны);
	КонецЕсли;
	
КонецПроцедуры

Функция ПризнакТовара(Номенклатура)
	
	Признак = "";
	Если ТипЗнч(Номенклатура) = Тип("СправочникСсылка.Номенклатура") Тогда
		Если ЗначениеЗаполнено(Номенклатура)
			И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "Услуга") Тогда
			Признак = "3"; // Услуга
		Иначе
			Признак = "1"; // Имущество
		КонецЕсли;
	Иначе
		Признак = "5"; // Иное
	КонецЕсли;
	
	Возврат Признак;
			
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Акт сверки взаиморасчетов ФНС

Функция ДанныеВходящегоЭД(ДеревоДанных)
	
	Результат = Новый Структура;
	Для каждого СтрокаДерева Из ДеревоДанных.Строки Цикл
		Путь = СтрокаДерева.ПолныйПуть;
		ДанныеСтроки = ЭлектронныеДокументыСлужебный.ДанныеЭлементаДереваЭлектронногоДокумента(ДеревоДанных, Путь);
		Результат.Вставить(Путь, ДанныеСтроки);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ПодготовитьСтруктуруДляАктСверкиВзаиморасчетовВходящий(ДеревоДанных, ОписаниеОшибки = "")
	
	ДанныеЭД = ДанныеВходящегоЭД(ДеревоДанных);
	
	Шапка = Новый Структура;
	
	ПрочитатьДанныеУчастника(ДеревоДанных, Шапка, "Получатель", "Организация", "Организации");
	ПрочитатьДанныеУчастника(ДеревоДанных, Шапка, "Отправитель", "Контрагент", "Контрагенты");
	
	Шапка.Вставить("ЭтоЭлектронныйДокумент",  Истина);
	Шапка.Вставить("ЭтоДокументКонтрагента",  Истина);
	Шапка.Вставить("РазбитьПоДоговорам",      Истина);
	Шапка.Вставить("ДатаНачала",              ДанныеЭД.ДатаНачалаПериодаСверки);
	Шапка.Вставить("ДатаОкончания",           ДанныеЭД.ДатаОкончанияПериодаСверки);
	Шапка.Вставить("НомерВходящегоДокумента", ДанныеЭД.НомерДокумента);
	Шапка.Вставить("ДатаВходящегоДокумента",  ДанныеЭД.ДатаДокумента);
	
	Валюта = НайтиСсылкуНаОбъект("Валюты", ДанныеЭД.КодВалюты);
	Шапка.Вставить("ВалютаДокумента", Валюта);
	Шапка.Вставить("ОстатокНаНачалоПоДаннымКонтрагента",
		ДанныеЭД.СведенияОтправителя.СальдоНаНачалоПериодаДебет - ДанныеЭД.СведенияОтправителя.СальдоНаНачалоПериодаКредит);
	
	ТаблицаПоДаннымКонтрагента = Новый ТаблицаЗначений;
	Для Каждого Реквизит Из Метаданные.Документы.АктСверкиВзаиморасчетов.ТабличныеЧасти.ПоДаннымКонтрагента.Реквизиты Цикл
		ТаблицаПоДаннымКонтрагента.Колонки.Добавить(Реквизит.Имя, Реквизит.Тип);
	КонецЦикла;
	
	Для Каждого СтрокаДоговора Из ДанныеЭД.СведенияОтправителя.СведенияПоДоговорам Цикл
		Для Каждого СтрокаДокумента Из СтрокаДоговора.СведенияПоДокументам Цикл
			Для Каждого СтрокаОперации Из СтрокаДокумента.СведенияПоОперациям Цикл
				
				СтрокаПоДаннымКонтрагента  = ТаблицаПоДаннымКонтрагента.Добавить();
				СтрокаПоДаннымКонтрагента.ДоговорКонтрагента = ДоговорПоСтрокеАктаСверки(Шапка, СтрокаДоговора);
				СтрокаПоДаннымКонтрагента.Дата = СтрокаОперации.ДатаОперации;
				СтрокаПоДаннымКонтрагента.Дебет = СтрокаОперации.СуммаДебет;
				СтрокаПоДаннымКонтрагента.Кредит = СтрокаОперации.СуммаКредит;
				СтрокаПоДаннымКонтрагента.Представление = СтрокаОперации.НаименованиеОперации + " (" +
															СтрокаДокумента.НомерДокумента + " от " + СтрокаДокумента.ДатаДокумента + ")";
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	ДанныеДляЗаполнения = Новый Структура();
	ДанныеДляЗаполнения.Вставить("Шапка", Шапка);
	ДанныеДляЗаполнения.Вставить("ПоДаннымКонтрагента", ТаблицаПоДаннымКонтрагента);
	
	Возврат ДанныеДляЗаполнения;
	
КонецФункции

Функция ДоговорПоСтрокеАктаСверки(ДанныеДокумента, РеквизитыДоговора)
	
	ОтборПоВалюте = Новый Структура("ЗначениеОтбора", ДанныеДокумента.ВалютаДокумента);
	СтруктураПараметров = Новый Структура("ВалютаВзаиморасчетов", ОтборПоВалюте);
	
	СписокВидовДоговоров = Новый СписокЗначений;
	
	Если РеквизитыДоговора.ОписаниеДоговора = "С покупателем" Тогда
		СписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком);
	ИначеЕсли РеквизитыДоговора.ОписаниеДоговора = "С поставщиком" Тогда
		СписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПокупателем);
	Иначе
		СписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком);
		СписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПокупателем);
	КонецЕсли;
		
	ДоговорКонтрагента = Справочники.Контрагенты.ПустаяСсылка();
	
	УправлениеВзаиморасчетами.УстановитьДоговорКонтрагента(
		ДоговорКонтрагента, ДанныеДокумента.Контрагент, ДанныеДокумента.Организация, СписокВидовДоговоров);
		
	Возврат ДоговорКонтрагента;
	
КонецФункции

// Сохраняет данные из электронного документа в объекты ИБ. Акт сверки взаиморасчетов ФНС 
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - структура параметров документа ИБ.
//  СсылкаНаВладельца - Ссылка - ссылка на объект ИБ, владельца электронного документа.
//  Записывать - Булево - признак необходимости записывать объект ИБ.
//  СпособОбработки - Строка - способ сохранения данных в информационной базе.
//
Процедура НайтиСоздатьАктСверкиВзаиморасчетов(ДеревоДанных, СсылкаНаВладельца = Неопределено, Записывать = Истина, СпособОбработки = Неопределено, ОписаниеОшибки = "") Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеДляЗаполнения = ПодготовитьСтруктуруДляАктСверкиВзаиморасчетовВходящий(ДеревоДанных, ОписаниеОшибки);
	
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		Возврат;
	КонецЕсли;
	
	Шапка = ДанныеДляЗаполнения.Шапка;
	РежимЗаписи = РежимЗаписиДокумента.Запись;
	
	Если ЗначениеЗаполнено(СсылкаНаВладельца) Тогда // получены изменения по существующему документу
		ДокументОбъект = СсылкаНаВладельца.ПолучитьОбъект();
		Если ДокументОбъект.Проведен Тогда 
			РежимЗаписи = РежимЗаписиДокумента.Проведение;
		КонецЕсли;
	Иначе
		ДокументОбъект = Документы.АктСверкиВзаиморасчетов.СоздатьДокумент();
		ДокументОбъект.Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ДокументОбъект, Шапка);
	Документы.АктСверкиВзаиморасчетов.ЗаполнитьАктСверкиПоДаннымИБ(ДокументОбъект);
	ДокументОбъект.ПоДаннымКонтрагента.Загрузить(ДанныеДляЗаполнения.ПоДаннымКонтрагента); // Данные входящего электронного документа
	
	// Расхождение
	ОстатокНаКонец = ДокументОбъект.ОстатокНаНачало + ДокументОбъект.ПоДаннымОрганизации.Итог("Дебет") - ДокументОбъект.ПоДаннымОрганизации.Итог("Кредит");
	ОстатокНаКонецКонтрагент = - ДокументОбъект.ОстатокНаНачало + ДокументОбъект.ПоДаннымКонтрагента.Итог("Дебет") - ДокументОбъект.ПоДаннымКонтрагента.Итог("Кредит");
	
	Если ДокументОбъект.Расхождение <> ОстатокНаКонец + ОстатокНаКонецКонтрагент Тогда
		ДокументОбъект.Расхождение = ОстатокНаКонец + ОстатокНаКонецКонтрагент;
	КонецЕсли;
	
	СверкаСогласована = Не ДокументОбъект.Расхождение;

	ДокументОбъект.Записать(РежимЗаписи);
	Если Не ЗначениеЗаполнено(СсылкаНаВладельца) Тогда
		СсылкаНаВладельца = ДокументОбъект.Ссылка;
	КонецЕсли;
		
КонецПроцедуры 

Функция ПодготовитьСтруктуруДляАктСверкиВзаиморасчетовПолученОтветныйТитул(ДеревоДанных, СсылкаНаВладельца, ОписаниеОшибки)
	
	ДанныеДляЗаполнения = Новый Структура();
	
	Если Не ЗначениеЗаполнено(СсылкаНаВладельца) Тогда
		Возврат ДанныеДляЗаполнения;
	КонецЕсли;
	ПараметрыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаВладельца, 
		"ВалютаДокумента,Организация,Контрагент,ПоДаннымОрганизации,ОстатокНаНачало");
	
	ДанныеЭД = ДанныеВходящегоЭД(ДеревоДанных);
	
	Шапка = Новый Структура;
	
	Если ДанныеЭД.ПризнакНаличияРазногласий Тогда
		Шапка.Вставить("СверкаСогласована", Ложь);
		Шапка.Вставить("ОстатокНаНачалоПоДаннымКонтрагента",
			ДанныеЭД.СведенияПолучателя.СальдоНаНачалоПериодаДебет - ДанныеЭД.СведенияПолучателя.СальдоНаНачалоПериодаКредит);
	Иначе
		Шапка.Вставить("СверкаСогласована", Истина);
		Шапка.Вставить("ОстатокНаНачалоПоДаннымКонтрагента", - ПараметрыОснования.ОстатокНаНачало);
		ТаблицаПоДаннымКонтрагента = Документы.АктСверкиВзаиморасчетов.ТаблицаПоДаннымКонтрагентаНаОсновеТаблицыПоДаннымОрганизации(
			ПараметрыОснования.ПоДаннымОрганизации.Выгрузить());
		ДанныеДляЗаполнения.Вставить("Шапка", Шапка);
		ДанныеДляЗаполнения.Вставить("ПоДаннымКонтрагента", ТаблицаПоДаннымКонтрагента);
		ДанныеДляЗаполнения.Вставить("Основание", СсылкаНаВладельца);
		
		Возврат ДанныеДляЗаполнения;
		
	КонецЕсли;
	
	ТаблицаПоДаннымКонтрагента = Новый ТаблицаЗначений;
	Для Каждого Реквизит Из Метаданные.Документы.АктСверкиВзаиморасчетов.ТабличныеЧасти.ПоДаннымКонтрагента.Реквизиты Цикл
		ТаблицаПоДаннымКонтрагента.Колонки.Добавить(Реквизит.Имя, Реквизит.Тип);
	КонецЦикла;
	
	Для Каждого СтрокаДоговора Из ДанныеЭД.СведенияПолучателя.СведенияПоДоговорам Цикл
		Для Каждого СтрокаДокумента Из СтрокаДоговора.СведенияПоДокументам Цикл
			Для Каждого СтрокаОперации Из СтрокаДокумента.СведенияПоОперациям Цикл
				
				СтрокаПоДаннымКонтрагента  = ТаблицаПоДаннымКонтрагента.Добавить();
				СтрокаПоДаннымКонтрагента.ДоговорКонтрагента = ДоговорПоСтрокеАктаСверки(ПараметрыОснования, СтрокаДоговора);
				СтрокаПоДаннымКонтрагента.Дата = СтрокаОперации.ДатаОперации;
				СтрокаПоДаннымКонтрагента.Дебет = СтрокаОперации.СуммаДебет;
				СтрокаПоДаннымКонтрагента.Кредит = СтрокаОперации.СуммаКредит;
				СтрокаПоДаннымКонтрагента.Представление = СтрокаОперации.НаименованиеОперации + " (" + СтрокаДокумента.НомерДокумента + " от " + СтрокаДокумента.ДатаДокумента + ")";
				
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	ДанныеДляЗаполнения.Вставить("Шапка", Шапка);
	ДанныеДляЗаполнения.Вставить("ПоДаннымКонтрагента", ТаблицаПоДаннымКонтрагента);
	ДанныеДляЗаполнения.Вставить("Основание", СсылкаНаВладельца);
	
	Возврат ДанныеДляЗаполнения;
	
КонецФункции

Процедура ЗаполнитьДокументАктСверкиВзаиморасчетовИсходящийОтветнымТитулом(ДанныеДляЗаполнения)
	
	Если ДанныеДляЗаполнения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Шапка = ДанныеДляЗаполнения.Шапка;
	РежимЗаписи = РежимЗаписиДокумента.Запись;
	ДокументУчета = ДанныеДляЗаполнения.Основание;
	ДокументОбъект = ДокументУчета.ПолучитьОбъект();
	Если ДокументОбъект.Проведен Тогда 
		РежимЗаписи = РежимЗаписиДокумента.Проведение;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ДокументОбъект, Шапка);
	СверкаСогласована = Шапка.СверкаСогласована;
	
	ДокументОбъект.ПоДаннымКонтрагента.Загрузить(ДанныеДляЗаполнения.ПоДаннымКонтрагента); // Данные входящего электронного документа
	
	ДокументОбъект.Записать(РежимЗаписи);
	Если Не ЗначениеЗаполнено(ДокументУчета) Тогда
		ДокументУчета = ДокументОбъект.Ссылка;
	КонецЕсли;
		
КонецПроцедуры

// Сохраняет данные из электронного документа в объекты ИБ. Акт сверки взаиморасчетов ФНС. Заполняет информацию получателя 
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - структура параметров документа ИБ.
//  СсылкаНаВладельца - Ссылка - ссылка на объект ИБ, владельца электронного документа.
//  Записывать - Булево - признак необходимости записывать объект ИБ.
//  СпособОбработки - Строка - способ сохранения данных в информационной базе.
//
Процедура ЗаполнитьАктСверкиВзаиморасчетовПоДаннымПолучателя(ДеревоДанных, СсылкаНаВладельца, Записывать = Истина, СпособОбработки = Неопределено, ОписаниеОшибки = "") Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеДляЗаполнения = ПодготовитьСтруктуруДляАктСверкиВзаиморасчетовПолученОтветныйТитул(ДеревоДанных, СсылкаНаВладельца, ОписаниеОшибки);
	ЗаполнитьДокументАктСверкиВзаиморасчетовИсходящийОтветнымТитулом(ДанныеДляЗаполнения);
	
КонецПроцедуры

Функция НовыйТаблицаСведенияПоДоговорам()
	
	СведенияПоДоговорам = Новый ТаблицаЗначений;
	СведенияПоДоговорам.Колонки.Добавить("НомерСтроки");
	СведенияПоДоговорам.Колонки.Добавить("НомерДоговора", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(50)));
	СведенияПоДоговорам.Колонки.Добавить("ДатаДоговора");
	СведенияПоДоговорам.Колонки.Добавить("ОписаниеДоговора", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(500)));
	СведенияПоДоговорам.Колонки.Добавить("СведенияПоДокументам");
	
	Возврат СведенияПоДоговорам;

КонецФункции

Функция НовыйТаблицаСведенияПоДокументам()
	
	СведенияПоДокументам = Новый ТаблицаЗначений;
	СведенияПоДокументам.Колонки.Добавить("НомерСтроки");
	СведенияПоДокументам.Колонки.Добавить("НаименованиеДокумента", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(100)));
	СведенияПоДокументам.Колонки.Добавить("НомерДокумента", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(50)));
	СведенияПоДокументам.Колонки.Добавить("ДатаДокумента");
	СведенияПоДокументам.Колонки.Добавить("СведенияПоОперациям");
	
	Возврат СведенияПоДокументам;

КонецФункции

Функция НовыйТаблицаСведенияПоОперациям()
	
	СведенияПоОперациям = Новый ТаблицаЗначений;
	СведенияПоОперациям.Колонки.Добавить("НомерСтроки");
	СведенияПоОперациям.Колонки.Добавить("ПорядковыйНомерСтроки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(6, 0)));
	СведенияПоОперациям.Колонки.Добавить("ДатаОперации");
	СведенияПоОперациям.Колонки.Добавить("НаименованиеОперации", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(500)));
	СведенияПоОперациям.Колонки.Добавить("СуммаДебет", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	СведенияПоОперациям.Колонки.Добавить("СуммаКредит", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	
	Возврат СведенияПоОперациям;
	
КонецФункции

Функция ПолучитьНаименованиеОперации(Представление)
	
	НаименованиеОперации = Представление;
	
	Поз = СтрНайти(Представление, "(");
	Если Поз <> 0 Тогда
		НаименованиеОперации = Лев(Представление, Поз-1);
	КонецЕсли;
	
	Возврат НаименованиеОперации;
	
КонецФункции

// Подготавливает данные для электронного документа типа Акт сверки взаиморасчетов (информация отправителя).
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на объект информационной базы, по которому необходимо 
//                                    создать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//
Процедура ЗаполнитьДанныеДляАктСверкиВзаиморасчетов(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных) Экспорт
	
	Если Не ДоступенЭДОДляЭДАктСверкиВзаиморасчетов(СсылкаНаОбъект) Тогда
		ТекстСообщения = НСтр("ru = 'Электронный документ ""Акт сверки расчетов с контрагентом"" недоступен.'");
		СообщитьОбОшибкеПриФормированииЭД(СсылкаНаОбъект, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ПараметрыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаОбъект, "ЭтоЭлектронныйДокумент,ЭтоДокументКонтрагента");
	
	Если Не ПараметрыОснования.ЭтоЭлектронныйДокумент Тогда
		ТекстСообщения = НСтр("ru = 'Не установлен признак составления в электронном виде.'");
		СообщитьОбОшибкеПриФормированииЭД(СсылкаНаОбъект, ТекстСообщения);
		Возврат;
	ИначеЕсли ПараметрыОснования.ЭтоДокументКонтрагента Тогда
		ТекстСообщения = НСтр("ru = 'Для входящего документа недоступно формирование электронного документа.'");
		СообщитьОбОшибкеПриФормированииЭД(СсылкаНаОбъект, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъект);
	Запрос.Текст = Документы.АктСверкиВзаиморасчетов.ТекстЗапросаДляФормированияЭлектронногоДокументаДанныеОтправителя();
	Результат = Запрос.ВыполнитьПакет();
	ТаблицаРеквизитыДокумента = Результат[0].Выгрузить();
	Если ТаблицаРеквизитыДокумента.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Нет данных для формирования электронного документа.'");
		СообщитьОбОшибкеПриФормированииЭД(СсылкаНаОбъект, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	РеквизитыДокумента = ТаблицаРеквизитыДокумента[0];
		
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ДатаНачалаПериодаСверки", РеквизитыДокумента.ДатаНачала);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ДатаОкончанияПериодаСверки", РеквизитыДокумента.ДатаОкончания);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"НомерДокумента", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(РеквизитыДокумента.Номер, Истина, Ложь));
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"КодВалюты", РеквизитыДокумента.КодВалюты, ТекстОшибкиНекорректнаяВалюта());
	
	СведенияОбОтправителе = ПолучитьДанныеЮрФизЛица_2019(РеквизитыДокумента.Организация);    
	ЗаполнитьДанныеУчастника(ДеревоДанных, СведенияОбОтправителе, "Отправитель","Юр", "");
	
	СведенияОПолучателе = ПолучитьДанныеЮрФизЛица_2019(РеквизитыДокумента.Контрагент);
	ЗаполнитьДанныеУчастника(ДеревоДанных, СведенияОПолучателе, "Получатель","Юр", "");
	
	ТаблицаИтоги = Результат[1].Выгрузить();
	СальдоНаНачалоПериодаДебет = 0;
	СальдоНаНачалоПериодаКредит = 0;
	Если РеквизитыДокумента.ОстатокНаНачало > 0 Тогда
		СальдоНаНачалоПериодаДебет = РеквизитыДокумента.ОстатокНаНачало;
	Иначе
		СальдоНаНачалоПериодаКредит = - РеквизитыДокумента.ОстатокНаНачало;
	КонецЕсли;
	ОборотПоДебету   = ТаблицаИтоги.Итог("Дебет");
	ОборотПоКредиту = ТаблицаИтоги.Итог("Кредит");
	СальдоНаКонецПериодаДебет = СальдоНаНачалоПериодаДебет + ОборотПоДебету;
	СальдоНаКонецПериодаКредит = СальдоНаНачалоПериодаКредит + ОборотПоКредиту;
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"СведенияОтправителя.СальдоНаНачалоПериодаДебет", СальдоНаНачалоПериодаДебет);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"СведенияОтправителя.СальдоНаНачалоПериодаКредит", СальдоНаНачалоПериодаКредит);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"СведенияОтправителя.ОборотПоДебету", ОборотПоДебету);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"СведенияОтправителя.ОборотПоКредиту", ОборотПоКредиту);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"СведенияОтправителя.СальдоНаКонецПериодаДебет", СальдоНаКонецПериодаДебет);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"СведенияОтправителя.СальдоНаКонецПериодаКредит", СальдоНаКонецПериодаКредит);
	
	СведенияПоДоговорам = НовыйТаблицаСведенияПоДоговорам();
	НомерСтрокиПоДоговору = 0;
	
	МассивПрефиксов = ОбщегоНазначения.СформироватьМассивПрефиксовДляРИБИОрганизации(РеквизитыДокумента.Организация);
	
	ВыборкаПоДоговорам = Результат[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоДоговорам.Следующий() Цикл
		
		НомерСтрокиПоДоговору = НомерСтрокиПоДоговору + 1;
		НомерСтрокиПоДокументам = 0;

		СтрокаПоДоговору = СведенияПоДоговорам.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПоДоговору, ВыборкаПоДоговорам);
		СтрокаПоДоговору.НомерСтроки = НомерСтрокиПоДоговору;
		СтрокаПоДоговору.ОписаниеДоговора = Строка(ВыборкаПоДоговорам.ВидДоговора);
		
		ТаблицаПоДокументам = НовыйТаблицаСведенияПоДокументам();
		СтрокаПоДоговору.СведенияПоДокументам = ТаблицаПоДокументам;
		
		ВыборкаПоДокументам = ВыборкаПоДоговорам.Выбрать();
		Пока ВыборкаПоДокументам.Следующий() Цикл

			НомерСтрокиПоДокументам = НомерСтрокиПоДокументам + 1;
			
			СтрокаПоДокументу = ТаблицаПоДокументам.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаПоДокументу, ВыборкаПоДокументам);
			СтрокаПоДокументу.НомерСтроки = НомерСтрокиПоДокументам;
			Если ЗначениеЗаполнено(ВыборкаПоДокументам.НомерВходящегоДокумента) Тогда
				СтрокаПоДокументу.НомерДокумента = ВыборкаПоДокументам.НомерВходящегоДокумента;
				СтрокаПоДокументу.ДатаДокумента = Формат(ВыборкаПоДокументам.ДатаВходящегоДокумента, "ДФ=dd.MM.yyyy");
			Иначе
				СтрокаПоДокументу.НомерДокумента = ОбщегоНазначения.ПолучитьНомерНаПечать(ВыборкаПоДокументам.ДокументРегистратор,МассивПрефиксов);
				СтрокаПоДокументу.ДатаДокумента = Формат(ВыборкаПоДокументам.ДатаОперации, "ДФ=dd.MM.yyyy");
			КонецЕсли;
			
			ТаблицаПоОперациям = НовыйТаблицаСведенияПоОперациям();
			СтрокаПоДокументу.СведенияПоОперациям = ТаблицаПоОперациям;
			
			СтрокаПоОперациям = ТаблицаПоОперациям.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаПоОперациям, ВыборкаПоДокументам);
			СтрокаПоОперациям.НомерСтроки = 1;
			СтрокаПоОперациям.ПорядковыйНомерСтроки = 1;
			СтрокаПоОперациям.НаименованиеОперации = ПолучитьНаименованиеОперации(ВыборкаПоДокументам.Представление);
			
		КонецЦикла;
		
	КонецЦикла;
	
	ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, СведенияПоДоговорам, "СведенияОтправителя.СведенияПоДоговорам");
	
КонецПроцедуры 

Функция ДоступенЭДОДляЭДАктСверкиВзаиморасчетов(СсылкаНаОбъект)
	
	Возврат ЗначениеЗаполнено(СсылкаНаОбъект) И ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.АктСверкиВзаиморасчетов");
	
КонецФункции

// Подготавливает данные для электронного документа типа Акт сверки взаиморасчетов (информация получателя).
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на объект информационной базы, по которому необходимо 
//                                    создать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//
Процедура ЗаполнитьДанныеДляАктСверкиВзаиморасчетовИнформацияПолучателя(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных) Экспорт
	
	Если Не ЗначениеЗаполнено(СсылкаНаОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ДоступенЭДОДляЭДАктСверкиВзаиморасчетов(СсылкаНаОбъект) Тогда
		ТекстСообщения = НСтр("ru = 'Электронный документ ""Акт сверки расчетов с контрагентом"" недоступен.'");
		СообщитьОбОшибкеПриФормированииЭД(СсылкаНаОбъект, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ПараметрыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаОбъект, "ЭтоЭлектронныйДокумент,ЭтоДокументКонтрагента");
	
	Если Не ПараметрыОснования.ЭтоЭлектронныйДокумент Тогда
		ТекстСообщения = НСтр("ru = 'Не установлен признак составления в электронном виде.'");
		СообщитьОбОшибкеПриФормированииЭД(СсылкаНаОбъект, ТекстСообщения);
		Возврат;
	ИначеЕсли Не ПараметрыОснования.ЭтоДокументКонтрагента Тогда
		ТекстСообщения = НСтр("ru = 'Формирование ответного титула доступно только для полученного электронного документа.'");
		СообщитьОбОшибкеПриФормированииЭД(СсылкаНаОбъект, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъект);
	Запрос.Текст = Документы.АктСверкиВзаиморасчетов.ТекстЗапросаДляФормированияЭлектронногоДокументаДанныеПолучателя();
	Результат = Запрос.ВыполнитьПакет();
	ТаблицаРеквизитыДокумента = Результат[0].Выгрузить();
	Если ТаблицаРеквизитыДокумента.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Нет данных для формирования электронного документа.'");
		СообщитьОбОшибкеПриФормированииЭД(СсылкаНаОбъект, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	РеквизитыДокумента = ТаблицаРеквизитыДокумента[0];
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ПризнакНаличияРазногласий", РеквизитыДокумента.ЕстьРасхождения И Не РеквизитыДокумента.СверкаСогласована);
	
	ТаблицаИтоги = Результат[1].Выгрузить();
	
	СальдоНаНачалоПериодаДебет = 0;
	СальдоНаНачалоПериодаКредит = 0;
	Если РеквизитыДокумента.ОстатокНаНачало > 0 Тогда
		СальдоНаНачалоПериодаДебет = РеквизитыДокумента.ОстатокНаНачало;
	Иначе
		СальдоНаНачалоПериодаКредит = - РеквизитыДокумента.ОстатокНаНачало;
	КонецЕсли;
	ОборотПоДебету   = ТаблицаИтоги.Итог("Дебет");
	ОборотПоКредиту = ТаблицаИтоги.Итог("Кредит");
	СальдоНаКонецПериодаДебет = СальдоНаНачалоПериодаДебет + ОборотПоДебету;
	СальдоНаКонецПериодаКредит = СальдоНаНачалоПериодаКредит + ОборотПоКредиту;
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"СведенияПолучателя.СальдоНаНачалоПериодаДебет", СальдоНаНачалоПериодаДебет);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"СведенияПолучателя.СальдоНаНачалоПериодаКредит", СальдоНаНачалоПериодаКредит);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"СведенияПолучателя.ОборотПоДебету", ОборотПоДебету);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"СведенияПолучателя.ОборотПоКредиту", ОборотПоКредиту);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"СведенияПолучателя.СальдоНаКонецПериодаДебет", СальдоНаКонецПериодаДебет);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"СведенияПолучателя.СальдоНаКонецПериодаКредит", СальдоНаКонецПериодаКредит);
	
	СведенияПоДоговорам = НовыйТаблицаСведенияПоДоговорам();
	НомерСтрокиПоДоговору = 0;
	
	МассивПрефиксов = ОбщегоНазначения.СформироватьМассивПрефиксовДляРИБИОрганизации(РеквизитыДокумента.Организация);
	
	ВыборкаПоДоговорам = Результат[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоДоговорам.Следующий() Цикл
		
		НомерСтрокиПоДоговору = НомерСтрокиПоДоговору + 1;
		НомерСтрокиПоДокументам = 0;

		СтрокаПоДоговору = СведенияПоДоговорам.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПоДоговору, ВыборкаПоДоговорам);
		СтрокаПоДоговору.НомерСтроки = НомерСтрокиПоДоговору;
		СтрокаПоДоговору.ОписаниеДоговора = Строка(ВыборкаПоДоговорам.ВидДоговора);
		
		ТаблицаПоДокументам = НовыйТаблицаСведенияПоДокументам();
		СтрокаПоДоговору.СведенияПоДокументам = ТаблицаПоДокументам;
		
		ВыборкаПоДокументам = ВыборкаПоДоговорам.Выбрать();
		Пока ВыборкаПоДокументам.Следующий() Цикл

			НомерСтрокиПоДокументам = НомерСтрокиПоДокументам + 1;
			
			СтрокаПоДокументу = ТаблицаПоДокументам.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаПоДокументу, ВыборкаПоДокументам);
			СтрокаПоДокументу.НомерСтроки = НомерСтрокиПоДокументам;
			Если ЗначениеЗаполнено(ВыборкаПоДокументам.НомерВходящегоДокумента) Тогда
				СтрокаПоДокументу.НомерДокумента = ВыборкаПоДокументам.НомерВходящегоДокумента;
				СтрокаПоДокументу.ДатаДокумента = Формат(ВыборкаПоДокументам.ДатаВходящегоДокумента, "ДФ=dd.MM.yyyy");
			Иначе
				СтрокаПоДокументу.НомерДокумента = ОбщегоНазначения.ПолучитьНомерНаПечать(ВыборкаПоДокументам.ДокументРегистратор,МассивПрефиксов);
				СтрокаПоДокументу.ДатаДокумента = Формат(ВыборкаПоДокументам.ДатаОперации, "ДФ=dd.MM.yyyy");
			КонецЕсли;
			
			ТаблицаПоОперациям = НовыйТаблицаСведенияПоОперациям();
			СтрокаПоДокументу.СведенияПоОперациям = ТаблицаПоОперациям;
			
			СтрокаПоОперациям = ТаблицаПоОперациям.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаПоОперациям, ВыборкаПоДокументам);
			СтрокаПоОперациям.НомерСтроки = 1;
			СтрокаПоОперациям.ПорядковыйНомерСтроки = 1;
			СтрокаПоОперациям.НаименованиеОперации = ПолучитьНаименованиеОперации(ВыборкаПоДокументам.Представление);
			
		КонецЦикла;
		
	КонецЦикла;
	
	ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, СведенияПоДоговорам, "СведенияПолучателя.СведенияПоДоговорам");
	
КонецПроцедуры 

////////////////////////////////////////////////////////////////////////////////
// Создание элементов справочников

// Создает объект в ИБ по дереву параметров.
//
// Параметры:
//  СтрокаОбъекта - Структура параметров записываемого объекта,
//  ДеревоРазбора - ДеревоЗначений, результат разбора электронного документа.
//
// Возвращаемое значение:
//  НовыйЭлемент - ссылка на новый элемент в информационной базе.
//
Функция СоздатьОбъектВБД(СтрокаОбъекта, ДеревоРазбора) Экспорт
	
	НовыйЭлемент = Неопределено;
	
	
	
	Возврат НовыйЭлемент;
	
КонецФункции

////////////////////////
// Создание элементов справочников

// Находит ссылку на объект ИБ по типу, ИД и дополнительным реквизитам
// 
// Параметры:
//  ТипОбъекта - Строка, идентификатор типа объекта, который необходимо найти,
//  ИДОбъекта - Строка, идентификатор объекта заданного типа,
//  ДополнительныеРеквизиты - Структура, набор дополнительных полей объекта для поиска.
//
Функция НайтиСсылкуНаОбъект(ТипОбъекта, ИдОбъекта = "", ДополнительныеРеквизиты = Неопределено, ИДЭД = Неопределено) Экспорт
	
	Результат = Неопределено;
	
	Если ТипОбъекта = "Валюты" 
		 ИЛИ ТипОбъекта = "Банки" Тогда
		Результат = НайтиСсылкуНаОбъектПоРеквизиту(ТипОбъекта, "Код", ИдОбъекта);
		
	ИначеЕсли ТипОбъекта = "БанковскиеСчетаКонтрагентов"
		 ИЛИ ТипОбъекта = "БанковскиеСчетаОрганизаций" Тогда
		
		Владелец = ""; 
		НомерСчета = "";
		Если ДополнительныеРеквизиты.Свойство("Владелец", Владелец) Тогда
			Если ДополнительныеРеквизиты.Свойство("НомерСчета", НомерСчета) Тогда
				Результат = НайтиСсылкуНаОбъектПоРеквизиту("БанковскиеСчета", "НомерСчета", НомерСчета, Владелец);
			Иначе
				Результат = НайтиСсылкуНаОбъектПоРеквизиту("БанковскиеСчета", "НомерСчета", ИдОбъекта, Владелец);
			КонецЕсли;
		Иначе
			Результат = НайтиСсылкуНаОбъектПоРеквизиту("БанковскиеСчета", "НомерСчета", ИдОбъекта);
		КонецЕсли;
		
	ИначеЕсли ТипОбъекта = "ЕдиницыИзмерения" Тогда
		Результат = НайтиСсылкуНаОбъектПоРеквизиту("КлассификаторЕдиницИзмерения", "Код", ИдОбъекта);
		
	ИначеЕсли ТипОбъекта = "ДоговорыКонтрагентов" Тогда
		Владелец = "";
		НомерДоговора = "";
		ДатаДоговора = "";
		
		Если ДополнительныеРеквизиты.Свойство("Владелец", Владелец)
			И ДополнительныеРеквизиты.Свойство("НомерДоговора", НомерДоговора) Тогда
			Если ДополнительныеРеквизиты.Свойство("ДатаДоговора", ДатаДоговора) Тогда
				Результат = СсылкаНаДоговорПоНомеруИДате(Владелец, НомерДоговора, ДатаДоговора);
			Иначе
				Результат = НайтиСсылкуНаОбъектПоРеквизиту("ДоговорыКонтрагентов", "Номер", НомерДоговора, Владелец);
			КонецЕсли;
		КонецЕсли;

	ИначеЕсли (ТипОбъекта = "Контрагенты" ИЛИ ТипОбъекта = "Организации") И ЗначениеЗаполнено(ДополнительныеРеквизиты) Тогда
		ПараметрПоиска = "";
		ИНН = "";
		КПП = "";
		Если ДополнительныеРеквизиты.Свойство("ИНН") Тогда 
			ИНН = ДополнительныеРеквизиты.ИНН;
		КонецЕсли;
		Если ДополнительныеРеквизиты.Свойство("КПП") Тогда 
			КПП = ДополнительныеРеквизиты.КПП;
		КонецЕсли;
		Если ЗначениеЗаполнено(Строка(ИНН)+Строка(КПП)) Тогда // по ИНН+КПП
			Результат = СсылкаНаОбъектПоИННКПП(ТипОбъекта, ИНН, КПП); 
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Результат) И ДополнительныеРеквизиты.Свойство("Наименование", ПараметрПоиска) Тогда // по Наименованию
			Результат = НайтиСсылкуНаОбъектПоРеквизиту(ТипОбъекта, "Наименование", ПараметрПоиска); 
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Результат) И ДополнительныеРеквизиты.Свойство("ПолноеНаименование", ПараметрПоиска) Тогда // по Полному наименованию
			Результат = НайтиСсылкуНаОбъектПоРеквизиту(ТипОбъекта, "НаименованиеПолное", ПараметрПоиска); 
		КонецЕсли;
		
	ИначеЕсли ТипОбъекта = "НоменклатураПоставщиков" И ЗначениеЗаполнено(ДополнительныеРеквизиты) Тогда
		
		Владелец = "";
		
		Если ДополнительныеРеквизиты.Свойство("Владелец", Владелец) Тогда // есть Владелец
			
			Результат = Новый Структура(
				"Владелец, Идентификатор, Код, Артикул, Наименование", 
				Владелец, "", "", "", "");
			
			Параметр = "";
			Если ДополнительныеРеквизиты.Свойство("Идентификатор", Параметр) Тогда // по Идентификатору
				Результат.Идентификатор = Параметр;
			КонецЕсли;
			
			Если ДополнительныеРеквизиты.Свойство("Код", Параметр) Тогда // по Коду
				Результат.Код = Параметр;
			КонецЕсли;
			
			Если ДополнительныеРеквизиты.Свойство("Артикул", Параметр) Тогда  // по Артикулу
				Результат.Артикул = Параметр;
			КонецЕсли;
			
			Если ДополнительныеРеквизиты.Свойство("Наименование", Параметр) Тогда // по Наименованию
				Результат.Наименование = Параметр;
			КонецЕсли;
			
			
		КонецЕсли;
		
	ИначеЕсли ТипОбъекта = "Номенклатура" Тогда
		
		Код = "";
		
		Если ЗначениеЗаполнено(ИдОбъекта) Тогда
			// Если задан Ид, то будем искать по коду
			Результат = НайтиСсылкуНаОбъектПоРеквизиту(ТипОбъекта, "Код", ИдОбъекта);
			
		ИначеЕсли ЗначениеЗаполнено(ДополнительныеРеквизиты) И ДополнительныеРеквизиты.Свойство("Код", Код) Тогда
			
			Результат = НайтиСсылкуНаОбъектПоРеквизиту(ТипОбъекта, "Код", Код);
			
		ИначеЕсли ЗначениеЗаполнено(ДополнительныеРеквизиты) Тогда
			
			ДанныеТовара = ПолучитьДанныеТовараПоНоменклатуреПоставщика(ДополнительныеРеквизиты);
			Результат = ДанныеТовара.Номенклатура;
			
		КонецЕсли;
		
	ИначеЕсли ТипОбъекта = "ВидыКонтактнойИнформации" Тогда
		Попытка
			Результат = Справочники[ТипОбъекта][ИдОбъекта];
		Исключение
			Результат = Справочники[ТипОбъекта].НайтиПоНаименованию(ИдОбъекта, Истина);
		КонецПопытки;
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

// Находит ссылку на справочник по переданному реквизиту.
//
// Параметры:
//  ИмяСправочника - Строка, имя справочника, объект которого надо найти,
//  ИмяРеквизита - Строка, имя реквизита, по которому будет проведен поиск,
//  ЗначРеквизита - произвольное значение, значение реквизита, по которому будет проведен поиск,
//  Владелец - Ссылка на владельца для поиска в иерархическом справочнике.
//
Функция НайтиСсылкуНаОбъектПоРеквизиту(ИмяСправочника, ИмяРеквизита, ЗначРеквизита, Владелец = Неопределено, ИскатьПомеченныеНаУдаление = Ложь) Экспорт
	
	Результат = Неопределено;
	
	Если ИмяСправочника = "НоменклатураПоставщиков" Тогда
		
		// Справочник отсутствует в системе - вернем структуру содержащую значение реквизита
		Результат = Новый Структура(ИмяРеквизита, ЗначРеквизита);
		
	Иначе
		
		ОбъектМетаданных = Метаданные.Справочники[ИмяСправочника];
		Если НЕ ОбщегоНазначения.ЭтоСтандартныйРеквизит(ОбъектМетаданных.СтандартныеРеквизиты, ИмяРеквизита) Тогда
			// это не стандартный реквизит
			Если НЕ ОбъектМетаданных.Реквизиты.Найти(ИмяРеквизита) <> Неопределено Тогда 
				// это не обычный реквизит
				Возврат Неопределено;
			Иначе
				ОписаниеРеквизита = ОбъектМетаданных.Реквизиты.Найти(ИмяРеквизита);
			КонецЕсли;
		Иначе
			ОписаниеРеквизита = ОбъектМетаданных.СтандартныеРеквизиты[ИмяРеквизита];
		КонецЕсли;
		
		СпособСравнения = " = ";
		Если ОписаниеРеквизита.Тип.Типы().Найти(Тип("Строка")) <> Неопределено И ОписаниеРеквизита.Тип.Типы().Количество() = 1 Тогда
			Если ОписаниеРеквизита.Тип.КвалификаторыСтроки.Длина = 0 Тогда
				СпособСравнения = " ПОДОБНО ";
			КонецЕсли;
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ИскСправочник.Ссылка КАК Ссылка
		|
		|ИЗ
		|	Справочник."+ИмяСправочника+" КАК ИскСправочник
		|ГДЕ
		|	
		|	ИскСправочник."+ИмяРеквизита+СпособСравнения+"&ЗначРеквизита";
		
		Если ЗначениеЗаполнено(Владелец) Тогда
			Запрос.Текст = Запрос.Текст + " И ИскСправочник.Владелец = &Владелец";
			Запрос.УстановитьПараметр("Владелец", 	Владелец);
		КонецЕсли;
		Запрос.УстановитьПараметр("ЗначРеквизита", ЗначРеквизита);
		
		Если Не ИскатьПомеченныеНаУдаление Тогда
			Запрос.Текст = Запрос.Текст + " И Не ИскСправочник.ПометкаУдаления";
		КонецЕсли;
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Результат = Выборка.Ссылка;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Находит элемент справочника по реквизитам ИНН и КПП
// Если элемент не найден возвращаем Неопределено
// Параметры:
//  ТипОбъекта - Строка, имя справочника в метаданных;
//  ИНН - строка;
//  КПП - строка;
//  Организация - Организация, ссылка на элемент справочника организации
//
// Возвращаемое значение:
//  Результат - ссылка на справочник или неопределено
//
Функция СсылкаНаОбъектПоИННКПП(ТипОбъекта, ИНН, КПП, Организация = Неопределено) Экспорт
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Контрагенты.Ссылка
	|ИЗ
	|	Справочник."+ТипОбъекта+" КАК Контрагенты
	|ГДЕ
	|	Не Контрагенты.ПометкаУдаления И
	|	Контрагенты.ИНН = &ИНН";

	ИННпараметр = ИНН;
	Если ИННпараметр = Неопределено Тогда
		ИННпараметр = "";
	КонецЕсли;
	КППпараметр = КПП;
	Если КППпараметр = Неопределено Тогда
		КППпараметр = "";
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ИНН", ИННпараметр);
	Если ЗначениеЗаполнено(КППпараметр) Тогда
		Запрос.Текст = Запрос.Текст + " И
			|	Контрагенты.КПП = &КПП";
		Запрос.УстановитьПараметр("КПП", КППпараметр);
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Заполняет реквизиты объекта данными из структуры реквизитов.
//
// Параметры:
//  СтруктураРеквизитов - структура - перечень значений реквизитов
//
// Возвращаемое значение:
//  Контрагент.Ссылка - ссылка на справочник контрагентов
//
Функция ЗаполнитьРеквизитыКонтрагента(СтруктураРеквизитов) Экспорт
	
	Результат = Неопределено;
	
	Если ЗначениеЗаполнено(СтруктураРеквизитов.Контрагент) Тогда
		Контрагент = СтруктураРеквизитов.Контрагент.ПолучитьОбъект();
	Иначе	
		Контрагент = Справочники.Контрагенты.СоздатьЭлемент();
	КонецЕсли;
	
	Контрагент.Наименование = СтруктураРеквизитов.Наименование;
	ИНН_КПП = СтруктураРеквизитов.ИНН_КПП;
	Контрагент.ИНН = Сред(ИНН_КПП,1,Найти(ИНН_КПП,"/")-1);
	Контрагент.КПП = Сред(ИНН_КПП,Найти(ИНН_КПП,"/")+1);
	Контрагент.Записать();
	
	Возврат Результат;
	
КонецФункции

// Заполняет структуру реквизитов товара
//
// Параметры:
//  РеквизитыНоменклатуры - Структура, содержащая параметры поиска 
//  СтруктураВозврата - Структура содержащая ссылки на номенклатуру, характеристику, упаковку
//  ИД - идентификатор обмена ЭД
//
Процедура ПолучитьРеквизитыТовара(РеквизитыНоменклатуры, СтруктураВозврата, ИД = Неопределено) Экспорт
	
	ДанныеТовара = ПолучитьДанныеТовараПоНоменклатуреПоставщика(РеквизитыНоменклатуры);
	Если Не ЗначениеЗаполнено(ДанныеТовара.Номенклатура) И РеквизитыНоменклатуры.Свойство("НоменклатураПоставщика") Тогда
		ДанныеТовара = ПолучитьДанныеТовараПоНоменклатуреПоставщика(РеквизитыНоменклатуры.НоменклатураПоставщика);
	КонецЕсли;
	
	СтруктураВозврата.Номенклатура   = ДанныеТовара.Номенклатура;
	СтруктураВозврата.Характеристика = ДанныеТовара.ХарактеристикаНоменклатуры;
	
КонецПроцедуры

// Возвращает ИД контрагента.
//
// Параметры
//  Контрагент – ссылка на контрагента (Организация или Контрагент)
//  ВидКонтрагента - строка, определяющая вид контрагента
//
// Возвращаемое значение:
// ИдКонтрагента - строка - значение ИдКонтрагента
//
Функция ПолучитьИДКонтрагента(Контрагент, ВидКонтрагента) Экспорт
	
	ИдКонтрагента = "";
	
	Если ЗначениеЗаполнено(Контрагент) Тогда
		Если ВРег(ВидКонтрагента) = ВРег("Организация") Тогда
			ИдКонтрагента = Контрагент.ИНН+"_"+Контрагент.КПП;
		ИначеЕсли ВРег(ВидКонтрагента) = ВРег("Контрагент") Тогда
			ИдКонтрагента = Контрагент.ИНН+"_"+Контрагент.КПП;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ИдКонтрагента;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Сопоставление номенклатуры

// Формирует тест запроса для получения таблицы сопоставления номенклатуры
//
// Параметры:
//  ТекстЗапроса - Строка - текст запроса
//
Процедура ТекстЗапросаСопоставленияНоменклатуры(ТекстЗапроса) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаИнформацияОТоваре.Ид 				КАК Идентификатор,
	|	ТаблицаИнформацияОТоваре.Артикул 			КАК АртикулНоменклатурыКонтрагента,
	|	ТаблицаИнформацияОТоваре.Наименование 		КАК НаименованиеНоменклатурыКонтрагента,
	|	ТаблицаИнформацияОТоваре.БазоваяЕдиницаКод 	КАК БазоваяЕдиницаКод,
	|	ТаблицаИнформацияОТоваре.Описание 			КАК Описание
	|ПОМЕСТИТЬ ТаблицаИнформацияОТоваре
	|ИЗ
	|	&ТаблицаИнформацияОТоваре КАК ТаблицаИнформацияОТоваре
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаИнформацияОТоваре.Идентификатор,
	|	ТаблицаИнформацияОТоваре.АртикулНоменклатурыКонтрагента,
	|	ТаблицаИнформацияОТоваре.НаименованиеНоменклатурыКонтрагента,
	|	ЕСТЬNULL(КлассификаторЕдиницИзмерения.Ссылка, НЕОПРЕДЕЛЕНО) КАК ЕдиницаНоменклатурыКонтрагента,
	|	ТаблицаИнформацияОТоваре.Описание
	|ИЗ
	|	ТаблицаИнформацияОТоваре КАК ТаблицаИнформацияОТоваре
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РегистрСведений.НоменклатураКонтрагентов КАК НоменклатураКонтрагентов
	|	ПО 
	|		НоменклатураКонтрагентов.Контрагент = &Контрагент
	|		И НоменклатураКонтрагентов.Идентификатор = ТаблицаИнформацияОТоваре.Идентификатор
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Справочник.КлассификаторЕдиницИзмерения КАК КлассификаторЕдиницИзмерения
	|	ПО 
	|		ТаблицаИнформацияОТоваре.БазоваяЕдиницаКод = КлассификаторЕдиницИзмерения.Код
	|	
	|ГДЕ
	|	НоменклатураКонтрагентов.Номенклатура ЕСТЬ NULL";
	
КонецПроцедуры

// Сохраняет результат ручного сопоставления Номенклатуры в БД
//
// Параметры:
//  ТаблицаСопоставления -таблицаЗначений, содержащая данные сопоставления
//  Контрагент - СправочникСсылка.Контрагенты
//  Отказ - Булево, признак ошибки
//
Процедура ЗаписатьСопоставлениеНоменклатуры(ТаблицаСопоставления, Контрагент, Отказ) Экспорт
	
	НаборЗаписей = РегистрыСведений.НоменклатураКонтрагентов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Контрагент.Установить(Контрагент);
	
	Для Каждого Строка Из ТаблицаСопоставления Цикл
		
		Если ЗначениеЗаполнено(Строка.Номенклатура) Тогда
			
			Запись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, Строка);
			
			Запись.Контрагент = Контрагент;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если НаборЗаписей.Количество() > 0 Тогда
		Попытка
			НаборЗаписей.Записать(Ложь);
		Исключение
			ОбщегоНазначения.СообщитьОбОшибке(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()), Отказ);
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

// Возвращает структуру для открытия формы сопоставления номенклатуры
//
// Параметры:
//  СсылкаНаЭД - СправочникСсылка.ЭДПрисоединенныеФайлы
//
// Возвращаемое значение:
//  Структура, содержащая ИмяФормы и ПараметрыОткрытияФормы
//
Функция ПолучитьПараметрыФормыСопоставленияНоменклатуры(СсылкаНаЭД) Экспорт
	
	СтруктураПараметров = Новый Структура();
	
	Если (ТипЗнч(СсылкаНаЭД) = Тип("ДокументСсылка.ЭлектронныйДокументВходящий")
		    И (СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ТОРГ12
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Продавец
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ОтветНаЗаказ
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.КаталогТоваров
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.АктИсполнитель
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.СчетНаОплату
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.АктНаПередачуПрав)) 
		 ИЛИ (ТипЗнч(СсылкаНаЭД) = Тип("ДокументСсылка.ЭлектронныйДокументИсходящий")
			И (СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Покупатель
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.АктЗаказчик
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиПолучатель)) 
		 ИЛИ (ТипЗнч(СсылкаНаЭД) = Тип("ДокументСсылка.ЭлектронныйДокументВходящий")
		 	И СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.СчетФактура
			И СсылкаНаЭД.ТипЭлементаВерсииЭД = Перечисления.ТипыЭлементовВерсииЭД.СЧФДОПУПД) Тогда
		
		СтруктураПараметров.Вставить("ИмяФормы", "ОбщаяФорма.СопоставлениеДанныхПоНоменклатуре");
		ПараметрыОткрытияФормы = Новый Структура("ЭлектронныйДокумент, НеОткрыватьФормуПриОтсутствииНесопоставленнойНоменклатуры", 
			СсылкаНаЭД, Истина);
		СтруктураПараметров.Вставить("ПараметрыОткрытияФормы", ПараметрыОткрытияФормы);
		СтруктураПараметров.Вставить("СопоставлятьНоменклатуру", Ложь);
		
	КонецЕсли;
	
	Возврат СтруктураПараметров;
	
КонецФункции

// Производит заполнение реквизитов формы переданными значениями 
//
// Параметры:
//  ДанныеФормы - Данные управляемой формы;
//  ЗначениеЗаполнения - ссылка данные во временном хранилище.
//
Процедура ЗаполнитьИсточник(ДанныеФормы, ЗначениеЗаполнения) Экспорт
	
	
	
КонецПроцедуры

// Заполняет адрес хранилища с таблицей значений - каталога товаров
//
// Параметры:
//  АдресВоВременномХранилище - адрес хранения каталога товаров;
//  ИдентификаторФормы - уникальный  идентификатор формы, вызвавшей функцию.
//
Процедура ПоместитьКаталогТоваровВоВременноеХранилище(АдресВоВременномХранилище, ИдентификаторФормы) Экспорт
	
	
	
КонецПроцедуры

// Получает значение перечисления по имени перечисления и представлению в библиотеке.
// 
// Параметры:
//  ИмяПеречисления - Строка, наименование перечисления.
//  ПредставлениеПеречисления - Строка, наименование значения перечисления.
//  НайденноеЗначение - значение искомого перечисления.
//
Процедура ПолучитьЗначениеПеречисления(ИмяПеречисления, ПредставлениеПеречисления, НайденноеЗначение) Экспорт
	
	Если ИмяПеречисления = "СтавкиНДС" Тогда
		
		НайденноеЗначение = ЗначениеПеречисленияСтавкаНДС(ПредставлениеПеречисления);
		
	Иначе
		
		Для Каждого ЭлПеречисления Из Метаданные.Перечисления[ИмяПеречисления].ЗначенияПеречисления Цикл
			Если Найти(Врег(ЭлПеречисления.Синоним), Врег(ПредставлениеПеречисления)) > 0
				 ИЛИ Найти(Врег(ЭлПеречисления.Имя), Врег(ПредставлениеПеречисления)) > 0 Тогда
				 
				НайденноеЗначение = Перечисления[ИмяПеречисления][ЭлПеречисления.Имя];
				Прервать;
			КонецЕсли;
		КонецЦикла;

	КонецЕсли;
	
КонецПроцедуры

// Получает данные о физическом (юридическом) лице по ссылке.
//
// Параметры:
//  ЮрФизЛицо - Ссылка на элемент справочника, по которому надо получить данные.
//
Функция ПолучитьДанныеЮрФизЛица(ЮрФизЛицо, БанковскийСчет = Неопределено) Экспорт
	
	Сведения = Новый Структура;
	
	Сведения = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(ЮрФизЛицо, ТекущаяДатаСеанса(), , БанковскийСчет);
	Свидетельство = "";
	СвидетельствоСерияНомер = "";
	СвидетельствоДатаВыдачи = "";
	 
	Если ТипЗнч(ЮрФизЛицо) = Тип("СправочникСсылка.Организации") Тогда
		
		Если ЮрФизЛицо.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
			
			ПолноеНаименование = Сведения.ПолноеНаименование;
			Если ВРЕГ(Лев(ПолноеНаименование,3))="ИП " Тогда
				ПолноеНаименование = Прав(ПолноеНаименование, СтрДлина(ПолноеНаименование)-3);
			ИначеЕсли ВРЕГ(Лев(ПолноеНаименование, СтрДлина("Индивидуальный предприниматель")))="ИНДИВИДУАЛЬНЫЙ ПРЕДПРИНИМАТЕЛЬ" Тогда
				ПолноеНаименование = Прав(ПолноеНаименование, СтрДлина(ПолноеНаименование)-СтрДлина("Индивидуальный предприниматель"));
			КонецЕсли;
			
			Фамилия  = "";
			Имя      = "";
			Отчество = "";
			ПолноеНаименование = СокрЛП(ПолноеНаименование);
			
			ОбщегоНазначения.ФамилияИнициалыФизЛица(ПолноеНаименование, Фамилия, Имя, Отчество);
			Сведения.Вставить("Фамилия",  Фамилия);
			Сведения.Вставить("Имя",      Имя);
			Сведения.Вставить("Отчество", Отчество);
			
			Если Не ПустаяСтрока(ЮрФизЛицо.СвидетельствоСерияНомер) Тогда
				Свидетельство = "Свидетельство " + ЮрФизЛицо.СвидетельствоСерияНомер + " от " + Формат(ЮрФизЛицо.СвидетельствоДатаВыдачи, "ДЛФ=D");
				СвидетельствоСерияНомер = ЮрФизЛицо.СвидетельствоСерияНомер;
				СвидетельствоДатаВыдачи = ЮрФизЛицо.СвидетельствоДатаВыдачи;
			КонецЕсли;

		КонецЕсли;
		
	Иначе
		Если ЮрФизЛицо.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
			
			ПолноеНаименование = Сведения.ПолноеНаименование;
			Если ВРЕГ(Лев(ПолноеНаименование,3))="ИП " Тогда
				ПолноеНаименование = Прав(ПолноеНаименование, СтрДлина(ПолноеНаименование)-3);
			ИначеЕсли ВРЕГ(Лев(ПолноеНаименование, СтрДлина("Индивидуальный предприниматель")))="ИНДИВИДУАЛЬНЫЙ ПРЕДПРИНИМАТЕЛЬ" Тогда
				ПолноеНаименование = Прав(ПолноеНаименование, СтрДлина(ПолноеНаименование)-СтрДлина("Индивидуальный предприниматель"));
			КонецЕсли;
			
			Фамилия  = "";
			Имя      = "";
			Отчество = "";
			ПолноеНаименование = СокрЛП(ПолноеНаименование);
			
			ОбщегоНазначения.ФамилияИнициалыФизЛица(ПолноеНаименование, Фамилия, Имя, Отчество);
			Сведения.Вставить("Фамилия",  Фамилия);
			Сведения.Вставить("Имя",      Имя);
			Сведения.Вставить("Отчество", Отчество);
			
		КонецЕсли;
		Сведения.Вставить("КодОКОПФ", ЮрФизЛицо.ОКОПФ);
	КонецЕсли;
	
	Сведения.Вставить("Ссылка",    ЮрФизЛицо);
	Сведения.Вставить("ЮрФизЛицо", ЮрФизЛицо.ЮрФизЛицо);
	Сведения.Вставить("ОфициальноеНаименование", Сведения.ПолноеНаименование);
	Если Сведения.Банк = "" Тогда
		Сведения.Банк = Справочники.Банки.ПустаяСсылка();
	КонецЕсли;
	Сведения.Вставить("Свидетельство", Свидетельство);
	Сведения.Вставить("СвидетельствоСерияНомер", СвидетельствоСерияНомер);
	Сведения.Вставить("СвидетельствоДатаВыдачи", СвидетельствоДатаВыдачи);
	
	Возврат Сведения;
	
КонецФункции

// Получает данные свидетельства о регистрации ИП по ссылке.
//
// Параметры:
//  ИП - Ссылка на элемент справочника - по которому нужно получить данные;
//  Сведения - Строка - сведения о регистрации индивидуального предпринимателя.
//
Процедура ДанныеСвидетельстваОРегистрацииИП(ИП, Сведения) Экспорт
	
	Реквизиты = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ИП, "СвидетельствоДатаВыдачи, СвидетельствоСерияНомер");
	
	Сведения = "Свидетельство " + Реквизиты.СвидетельствоСерияНомер + " от " +  Формат(Реквизиты.СвидетельствоДатаВыдачи,"ДФ=dd.MM.yyyy");
	
КонецПроцедуры

// Получает контактную информацию организации по ссылке
//
// Параметры:
//  Организация - ссылка на элемент справочника Организации, по которой нужно получить данные.
//
Функция ПолучитьКонтактнуюИнформацию(Организация) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РегистрСведенийКонтактнаяИнформация.Вид,
	|	ВЫРАЗИТЬ(РегистрСведенийКонтактнаяИнформация.Представление КАК СТРОКА(1000)) КАК Значение,
	|	ВЫРАЗИТЬ(РегистрСведенийКонтактнаяИнформация.Комментарий КАК СТРОКА(1000)) КАК Комментарий
	|ИЗ
	|	РегистрСведений.КонтактнаяИнформация КАК РегистрСведенийКонтактнаяИнформация
	|ГДЕ
	|	РегистрСведенийКонтактнаяИнформация.Объект = &Объект
	|	И (РегистрСведенийКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ТелефонОрганизации)
	|		ИЛИ РегистрСведенийКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ФаксОрганизации))";
	
	Запрос.УстановитьПараметр("Объект", Организация);
	
	ТабЗн = Запрос.Выполнить().Выгрузить();
	Для Каждого стр Из ТабЗн Цикл
		стр.Значение    = СокрП(стр.Значение);
		стр.Комментарий = СокрП(стр.Комментарий);
	КонецЦикла;
	
	Возврат ТабЗн;
	
	//Возврат Новый ТаблицаЗначений;
	
КонецФункции

// Возвращает название региона по коду
//
// Параметры:
//  КодРегиона - Строка, содержащая двухсимвольный код региона
//
// Возвращаемое значение:
//  Строка - название региона.
//
Функция НазваниеРегиона(КодРегиона) Экспорт
	
	Если НЕ ЗначениеЗаполнено(КодРегиона) Тогда
		Возврат "";
	КонецЕсли;
	
	КодРегионаЧисло = Число(КодРегиона);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	АдресныйКлассификатор.КодРегионаВКоде,
	|	АдресныйКлассификатор.ТипАдресногоЭлемента,
	|	АдресныйКлассификатор.Наименование,
	|	АдресныйКлассификатор.Код
	|ИЗ
	|	РегистрСведений.АдресныйКлассификатор КАК АдресныйКлассификатор
	|ГДЕ
	|	АдресныйКлассификатор.ТипАдресногоЭлемента = &ТипАдресногоЭлемента
	|	И АдресныйКлассификатор.КодРегионаВКоде = &КодРегионаВКоде";
	Запрос.УстановитьПараметр("КодРегионаВКоде", КодРегионаЧисло);
	Запрос.УстановитьПараметр("ТипАдресногоЭлемента", 1);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Возврат УправлениеКонтактнойИнформацией.ПолучитьПолноеНазвание(Выборка.Код);
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

// Получает структуру, содержащую информацию о юридическом адресе контрагента.
//
// Параметры:
//  СтруктураАдреса     - структура - содержит ссылки на элементы справочника;
//  СтруктураПараметров - структура - содержит ссылки на элементы справочника;
//  ВидКонтрагента      - строка - имя метаданных справочника;
//  ВидАдреса           - Строка - "Факт" или "Юр";
//  ТекстОшибки         - Строка - описание ошибки;
//
Процедура ПолучитьАдресСтруктурой(СтруктураАдреса, СтруктураПараметров, ВидКонтрагента, ВидАдреса, ТекстОшибки) Экспорт
	
	Если НЕ ЗначениеЗаполнено(СтруктураПараметров[ВидКонтрагента]) Тогда
		Возврат;
	КонецЕсли; 
	Если ТипЗнч(СтруктураПараметров[ВидКонтрагента]) = Тип("СправочникСсылка.Организации") Тогда
		Объект = СтруктураПараметров[ВидКонтрагента];
		ВидАдресаОрганизации = ?(ВидАдреса = "Юр", Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации, Справочники.ВидыКонтактнойИнформации.ФактАдресОрганизации);
	Иначе
		Объект = СтруктураПараметров[ВидКонтрагента];
		ВидАдресаОрганизации = ?(ВидАдреса = "Юр", Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента, Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента);
	КонецЕсли;
	
	Данные = Новый Структура("Объект, Тип, Вид", Объект, Перечисления.ТипыКонтактнойИнформации.Адрес, ВидАдресаОрганизации);
	Адрес = РегистрыСведений.КонтактнаяИнформация.Получить(Данные);
	
	ПроизвольныйАдрес = (ВРЕГ(УправлениеКонтактнойИнформацией.ПолучитьПредставлениеАдреса(Адрес)) <> ВРЕГ(Адрес.Представление));
	Если ПроизвольныйАдрес Тогда
		СтруктураАдреса.Вставить("АдресРФ", Ложь);
		//Попытаемся найти код страны
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Наименование", Адрес.Поле1);
		Запрос.Текст = "
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	КлассификаторСтранМира.Код КАК КодСтраны
		|ИЗ
		|	Справочник.КлассификаторСтранМира КАК КлассификаторСтранМира
		|
		|ГДЕ
		|	КлассификаторСтранМира.Наименование = &Наименование
		|";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			СтруктураАдреса.Вставить("КодСтраны", Выборка.КодСтраны);
			ТекущееПредставлениеАдреса = Адрес.Представление;
			СтруктураАдреса.Вставить("АдресСтрокой", УправлениеКонтактнойИнформацией.ПолучитьПредставлениеАдресаЗаПределамиРФБезСтраны(Адрес.Поле1, ТекущееПредставлениеАдреса));
		Иначе
			// Если страна в адресе не указана, то адрес не пройдет проверку.
			СтруктураАдреса.Вставить("КодСтраны", "");
			СтруктураАдреса.Вставить("АдресСтрокой", Адрес.Представление);
		КонецЕсли;
	Иначе
		Если (Адрес.ТипДома = Перечисления.ТипыДомов.Владение) Или ((Адрес.ТипКорпуса <> Перечисления.ТипыКорпусов.Корпус) И (ЗначениеЗаполнено(Адрес.ТипКорпуса))) Или 
			((Адрес.ТипКвартиры <> Перечисления.ТипыКвартир.Квартира) И (ЗначениеЗаполнено(Адрес.ТипКвартиры))) Тогда 
			СтруктураАдреса.Вставить("АдресРФ", Ложь);
			СтруктураАдреса.Вставить("КодСтраны", Справочники.КлассификаторСтранМира.Россия.Код);
			СтруктураАдреса.Вставить("АдресСтрокой", Адрес.Представление);
		Иначе
			СтруктураАдреса.Вставить("АдресРФ", Истина);
			СтруктураАдреса.Вставить("Индекс", СокрЛП(Адрес.Поле1));
			СтруктураАдреса.Вставить("КодСтраны", Справочники.КлассификаторСтранМира.Россия.Код);
		
			СтруктураКлассификатора = УправлениеКонтактнойИнформацией.ВернутьСтрокуАдресногоКлассификатораПоАдреснымЭлементам(Адрес.Поле2, "", "", "", "");
			СтруктураАдреса.Вставить("КодРегион", Формат(СтруктураКлассификатора.КодРегионаВКоде, "ЧЦ=2; ЧВН=; ЧГ="));
			СтруктураАдреса.Вставить("Регион", Адрес.Поле2);
		
			СтруктураАдреса.Вставить("Район", СокрЛП(Адрес.Поле3));
			СтруктураАдреса.Вставить("Город", СокрЛП(Адрес.Поле4));
			СтруктураАдреса.Вставить("НаселПункт", СокрЛП(Адрес.Поле5));
			СтруктураАдреса.Вставить("Улица", СокрЛП(Адрес.Поле6));
			СтруктураАдреса.Вставить("Дом", СокрЛП(Адрес.Поле7));
			СтруктураАдреса.Вставить("Корпус", СокрЛП(Адрес.Поле8));
			СтруктураАдреса.Вставить("Кварт", СокрЛП(Адрес.Поле9));
			СтруктураАдреса.Вставить("АдресСтрокой", Адрес.Представление);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Получает адрес электронной почты контрагента.
//
// Параметры:
//  Контрагент - справочник - ссылка на элемент справочника контрагенты,
//                            адрес которого надо получить.
//
// Возвращаемое значение:
//  АдресЭП - адрес электронной почты.
//
Функция АдресЭлектроннойПочтыКонтрагента(Контрагент) Экспорт
	
	АдресЭП = "";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КонтактнаяИнформация.Представление 	КАК АдресЭП
	|	
	|ИЗ
	|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|	
	|ГДЕ
	|	КонтактнаяИнформация.Объект = &Объект
	|	И КонтактнаяИнформация.Тип = &Тип
	|	И КонтактнаяИнформация.ЗначениеПоУмолчанию";
	
	Запрос.УстановитьПараметр("Тип", 	Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
	Запрос.УстановитьПараметр("Объект", Контрагент);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда 
		АдресЭП = Выборка.АдресЭП;
	КонецЕсли;
	
	Возврат АдресЭП;
	
КонецФункции

// Получает печатный номер документа.
//
// Параметры:
//  СсылкаНаОбъект - документссылка - ссылка на документ информационной базы.
//
// Возвращаемое значение:
//  НомерОбъекта - номер документа.
//
Функция ПолучитьПечатныйНомерДокумента(СсылкаНаОбъект) Экспорт
	
	Если ОбщегоНазначения.ЗначениеСсылочногоТипа(СсылкаНаОбъект) Тогда
		Возврат ОбщегоНазначения.ПолучитьНомерНаПечать(СсылкаНаОбъект);
	Иначе
		Возврат СсылкаНаОбъект;
	КонецЕсли;
	
КонецФункции

// Получает банковские счета.
//
// Параметры:
//  Организация - СправочникСсылка.Организация - ссылка на организацию.
//  Банк - СправочникСсылка - ссылка на элемент справочника с банками
//
// Возвращаемое значение:
//  Таблица - таблица значений с перечнем банковских счетов.
//
Функция ПолучитьБанковскиеСчета(Организация, Банк = Неопределено) Экспорт
	
	Таблица = Новый ТаблицаЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	БанковскиеСчета.Ссылка КАК БанковскийСчет
	|ИЗ
	|	Справочник.БанковскиеСчета КАК БанковскиеСчета
	|ГДЕ
	|	БанковскиеСчета.Владелец = &Организация
	|	//ОтборПоБанку И БанковскиеСчета.Банк = &Банк";
	
	Если ЗначениеЗаполнено(Банк) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//ОтборПоБанку", "");
		Запрос.УстановитьПараметр("Банк", Банк);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Таблица = Запрос.Выполнить().Выгрузить();
	
	Возврат Таблица;
	
КонецФункции

// Получает банковские реквизиты.
//
// Параметры:
//  МассивСчетов - массив - список банковских счетов.
//
// Возвращаемое значение:
//  Таблица - перечень банковских реквизитов.
//
Функция ПолучитьБанковскиеРеквизиты(МассивСчетов) Экспорт
	
	Таблица = Новый ТаблицаЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	БанковскиеСчета.Ссылка							КАК Ссылка,
	|	БанковскиеСчета.НомерСчета 						КАК РасчетныйСчет,
	|	БанковскиеСчета.Банк.КоррСчет 					КАК КорреспондентскийСчет,
	|	БанковскиеСчета.Банк.Код 						КАК БИК,
	|	БанковскиеСчета.Банк.Наименование 				КАК Банк,
	|	БанковскиеСчета.БанкДляРасчетов.Наименование 	КАК БанкДляРасчетов,
	|	БанковскиеСчета.БанкДляРасчетов.Код 			КАК БанкДляРасчетовБИК,
	|	БанковскиеСчета.БанкДляРасчетов.КоррСчет 		КАК БанкДляРасчетовКоррСчет
	|ИЗ
	|	Справочник.БанковскиеСчета КАК БанковскиеСчета
	|ГДЕ
	|	БанковскиеСчета.Ссылка В(&МассивСчетов)";
	Запрос.УстановитьПараметр("МассивСчетов", МассивСчетов);
	
	Таблица = Запрос.Выполнить().Выгрузить();
	
	Возврат Таблица;
	
КонецФункции

// В процедуре выполняется заполнение реквизитов (дата выставления, признак выставления,
// дата получения, признак получения) документов счета-фактуры, по ключевым событиям,
// описанным в приказе от 25 апреля 2011 г. N 50н.: получение ПДО, ПДП, ИП, ПДОИП.
//
// Параметры:
//  ВладелецЭД - документ-ссылка, ссылка на документ ИБ счет-фактура выданный/полученный.
//  ЭД - справочник-ссылка, ссылка на элемент справочника ЭДПрисоединенныеФайлы.
//
Процедура ЗаполнитьРеквизитыЭСФ(ВладелецЭД, ЭД) Экспорт
	
	Если ЭД.ТипЭлементаВерсииЭД = Перечисления.ТипыЭлементовВерсииЭД.ПДПЭСФ Тогда
		
		//  Датой выставления покупателю счета-фактуры в электронном виде по телекоммуникационным
		// каналам связи считается дата поступления файла счета-фактуры Оператору ЭДО от продавца, 
		// указанная в подтверждении (ПДПЭСФ) этого Оператора ЭДО.  ПРИКАЗ от 25 апреля 2011 г. N 50н
		
		ЭСФ = ВладелецЭД.ПолучитьОбъект();
		Если ЭСФ.Дата >= '20130608' Тогда
			ЭСФ.ДатаВыставления = ЭД.ДатаДокументаОтправителя;
			ЭСФ.КодСпособаВыставления = 2;
			ЭСФ.Выставлен = Истина;
			ЭСФ.Записать(?(ЭСФ.Проведен, РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись));
		КонецЕсли;
		
	ИначеЕсли ЭД.ТипЭлементаВерсииЭД = Перечисления.ТипыЭлементовВерсииЭД.ПДОЭСФ Тогда
		
		//  Датой получения покупателем счета-фактуры в электронном виде по телекоммуникационным 
		// каналам связи считается дата направления покупателю Оператором ЭДО файла счета-фактуры продавца, 
		// указанная в подтверждении (ПДОЭСФ) Оператора ЭДО.  ПРИКАЗ от 25 апреля 2011 г. N 50н
		
		ЭСФ = ВладелецЭД.ПолучитьОбъект();
		Если КонецДня(ЭСФ.Дата) <> КонецДня(ЭД.ДатаДокументаОтправителя)
			ИЛИ ЭСФ.КодСпособаПолучения <> 2 Тогда
			ДатаДоИзменения = ЭСФ.Дата;
			ЭСФ.Дата = ЭД.ДатаДокументаОтправителя + (ЭСФ.Дата - НачалоДня(ЭСФ.Дата)) ;
			РазностьЛет = Год(ДатаДоИзменения) - Год(ЭД.ДатаДокументаОтправителя);
			Если РазностьЛет <> 0 Тогда
				ЭСФ.УстановитьНовыйНомер();
			КонецЕсли;
			ЭСФ.КодСпособаПолучения = 2;
			ЭСФ.Записать(?(ЭСФ.Проведен, РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись));
		КонецЕсли;
		
	ИначеЕсли ЭД.ТипЭлементаВерсииЭД = Перечисления.ТипыЭлементовВерсииЭД.ИПЭСФ Тогда
		
		// Счет-фактура в электронном виде считается выставленным, если продавцу поступило 
		// соответствующее подтверждение (ПДПЭСФ) Оператора ЭДО, при наличии у продавца извещения покупателя 
		// о получении счета-фактуры (ИПЭСФ), подписанного ЭЦП покупателя и полученного через Оператора ЭДО.
		// ПРИКАЗ от 25 апреля 2011 г. N 50н
		
		СвойстваЭСФ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВладелецЭД, "Выставлен, КодСпособаВыставления, ДатаВыставления");
		ДатаВыставленияЭСФ = ЭлектронныеДокументы.ДатаВыставленияСчетФактуры(ВладелецЭД.Ссылка);
		Если ЗначениеЗаполнено(ДатаВыставленияЭСФ) И (НЕ СвойстваЭСФ.Выставлен ИЛИ СвойстваЭСФ.КодСпособаВыставления <> 2
				ИЛИ СвойстваЭСФ.ДатаВыставления <> ДатаВыставленияЭСФ) Тогда
			ЭСФ = ВладелецЭД.ПолучитьОбъект();
			ЭСФ.ДатаВыставления = ДатаВыставленияЭСФ;
			ЭСФ.КодСпособаВыставления = 2;
			ЭСФ.Выставлен = Истина;
			ЭСФ.Записать(?(ЭСФ.Проведен, РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись));
		КонецЕсли;
		
	ИначеЕсли ЭД.ТипЭлементаВерсииЭД = Перечисления.ТипыЭлементовВерсииЭД.ПДП_ИП_ЭСФ Тогда
		
		//  Счет-фактура в электронном виде считается полученным покупателем, если ему поступило 
		// соответствующее подтверждение (ПДОЭСФ) Оператора ЭДО, при наличии извещения покупателя 
		// о получении счета-фактуры (ИПЭСФ), подписанного ЭЦП покупателя и подтвержденного (ПДП_ИП_ЭСФ)
		// Оператором ЭДО.  ПРИКАЗ от 25 апреля 2011 г. N 50н
		
		СвойстваЭСФ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВладелецЭД, "КодСпособаПолучения");
		Если СвойстваЭСФ.КодСпособаПолучения <> 2 Тогда
			ЭСФ = ВладелецЭД.ПолучитьОбъект();
			ЭСФ.КодСпособаПолучения = 2;
			ЭСФ.Записать(?(ЭСФ.Проведен, РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись));
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Устарела. Будет удалена при переходе на новую редакцию БЭД.
// Выполняет заполнение структуры параметров подписанта для ЭД вида извещение о получении.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  СтруктураПараметровПодписанта - структура - параметры заполнения подписанта электронного документа.
//
Процедура ЗаполнитьСтруктуруДанныхПодписанта(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметровПодписанта) Экспорт
	
	
	
КонецПроцедуры

// Получает должность подписанта по ФИО.
//
// Параметры:
//  ФИО - строка - фамилия, имя и отчество подписанта
//  Должность - Строка - наименование должности подписанта.
//
Процедура ДолжностьПодписанта(ФИО, Организация, Должность) Экспорт
	
	Должность = "Директор";
	
КонецПроцедуры

// Заполняет таблицу реквизитов контрагента для приглашения к обмену.
//
// Параметры:
//  ТаблицаРеквизитов - таблица значений с полями: Участник, Наименование, ИНН, КПП, АдресЭП, ВнешнийКод,
//  НаименованиеДляСообщенияПользователю.
//    Наименование - передается Оператору ЭДО,
//    НаименованиеДляСообщенияПользователю - выводится в сообщении пользователю ИБ.
//  МассивКонтрагентов - массив ссылок на участников-контрагентов.
//
Процедура ЗаполнитьРеквизитыКонтрагентовДляПриглашенияКОбмену(ТаблицаРеквизитов, МассивКонтрагентов, СоглашениеЭД) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Контрагенты.Ссылка                           КАК Участник,
	|	Контрагенты.НаименованиеПолное               КАК Наименование,
	|	Контрагенты.ИНН                              КАК ИНН,
	|	Контрагенты.КПП                              КАК КПП,
	|	Контрагенты.Код                              КАК ВнешнийКод,
	|	УчастникиОбменовЭДЧерезОператоровЭДО.АдресЭП КАК АдресЭП,
	|	Контрагенты.Наименование                     КАК НаименованиеДляСообщенияПользователю
	|ИЗ
	|	РегистрСведений.УчастникиОбменовЭДЧерезОператоровЭДО КАК УчастникиОбменовЭДЧерезОператоровЭДО
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО УчастникиОбменовЭДЧерезОператоровЭДО.Участник = Контрагенты.Ссылка
	|ГДЕ
	|	УчастникиОбменовЭДЧерезОператоровЭДО.Участник В (&СписокУчастников)
	|	И УчастникиОбменовЭДЧерезОператоровЭДО.СоглашениеОбИспользованииЭД = &СоглашениеЭД";
	Запрос.УстановитьПараметр("СписокУчастников", МассивКонтрагентов);
	Запрос.УстановитьПараметр("СоглашениеЭД",     СоглашениеЭД);
	
	ТЗ = Запрос.Выполнить().Выгрузить();
	
	ТаблицаРеквизитов = ТЗ.Скопировать();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Просмотр электронных документов

// Возвращает текстовое описание организации.
//
// Параметры:
//  СведенияОКонтрагенте - Структура, сведения об организации, по которой надо составить описание.
//  Список - Строка, список запрашиваемых параметров организации.
//  СПрефиксом - Булево, признак вывода префикса параметра организации.
//
Функция ОписаниеОрганизации(СведенияОКонтрагенте, Список = "", СПрефиксом = Истина) Экспорт
	
	Результат = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОКонтрагенте, Список, СПрефиксом);
	
	Возврат Результат;
	
КонецФункции

// Формирует текст НДС по этапу оплаты
//
// Параметры:
//  СоответствиеСтавокНДС - Соответствие - соответствие, полученное с помощью функции ПолучитьСоответствиеСтавокНДС()
//  ПроцентПлатежа       - Число - Процент платежа по этапу
//
// Возвращаемое значение:
//  ТекстНДС - Строка - описание ставки НДС
//
Функция СформироватьТекстНДСЭтапаОплаты(СоответствиеСтавокНДС, ПроцентПлатежа) Экспорт
	
	ТекстНДС = "";
	
	Если СоответствиеСтавокНДС.Количество() > 0 Тогда
		
		Для Каждого ТекСтавкаНДС Из СоответствиеСтавокНДС Цикл
			
			Если ТекСтавкаНДС.Значение <> 0 Тогда
				
				ТекстНДС = ТекстНДС + ?(ПустаяСтрока(ТекстНДС), НСтр("ru='НДС(%СтавкаНДС%) %СуммаНДС%'"), НСтр("ru=', НДС(%СтавкаНДС%) %СуммаНДС%'"));
				ТекстНДС = СтрЗаменить(ТекстНДС, "%СтавкаНДС%", ТекСтавкаНДС.Ключ);
				ТекстНДС = СтрЗаменить(ТекстНДС, "%СуммаНДС%",  Формат(ТекСтавкаНДС.Значение / 100 * ПроцентПлатежа, "ЧЦ=15; ЧДЦ=2"));
			
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстНДС) Тогда
		ТекстНДС = НСтр("ru='В т.ч. '") + ТекстНДС;
	Иначе
		ТекстНДС = НСтр("ru='Без налога (НДС)'");
	КонецЕсли;
	
	Возврат ТекстНДС;
	
КонецФункции

// Возвращает текстовое представление суммы.
//
// Параметры:
//  СуммаКПрописи - Число, сумма, по которой надо получить представление.
//  КодВалюты - Число, код используемой валюты.
//  ЧН - Строка, параметр нулевого значения числа.
//  ЧРГ - Строка, разделитель групп целой части числа.
//
Функция ФорматСумм(СуммаКПрописи, КодВалюты = Неопределено, ЧН = "", ЧРГ = "") Экспорт
	
	Валюта = НайтиСсылкуНаОбъект("Валюты",КодВалюты);
	
	Результат = ОбщегоНазначения.ФорматСумм(СуммаКПрописи, Валюта,  ЧН, ЧРГ);
	
	Возврат Результат;
	
КонецФункции

// Возвращает сумму прописью.
//
// Параметры:
//  СуммаЧислом - Число, преобразуемая сумма.
//  КодВалюты - Число, код используемой валюты.
//
Функция СуммаПрописью(СуммаЧислом, КодВалюты = Неопределено) Экспорт
	
	Если КодВалюты = Неопределено Тогда
		Валюта = глЗначениеПеременной("ВалютаРегламентированногоУчета");
	Иначе
		Валюта = НайтиСсылкуНаОбъект("Валюты",КодВалюты);
		
		Если Не ЗначениеЗаполнено(Валюта) Тогда
			Валюта = глЗначениеПеременной("ВалютаРегламентированногоУчета");
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ОбщегоНазначения.СформироватьСуммуПрописью(СуммаЧислом, Валюта);
	
КонецФункции

// Формирует текст НДС по ставке для печатной формы счета и заказа
//
// Параметры:
//  СтавкаНДС       - ПеречислениеСсылка.СтавкиНДС - ставка НДС, для которой необходимо сформировать текст
//  ЦенаВключаетНДС - Булево - Признак включения НДС в цену
//
// Возвращаемое значение:
//  Строка
//
Функция ТекстНДСПоСтавке(СтавкаНДС, ЦенаВключаетНДС) Экспорт
	
	ТекстНДСПоСтавке = ?(ЦенаВключаетНДС, НСтр("ru='В т.ч. НДС (%СтавкаНДС%):'"), НСтр("ru='НДС (%СтавкаНДС%):'"));
	ТекстНДСПоСтавке = СтрЗаменить(ТекстНДСПоСтавке, "%СтавкаНДС%", СтавкаНДС);
	
	Возврат ТекстНДСПоСтавке;
	
КонецФункции

// Возвращает ответственного за электронный документооборот по данному соглашению
//
// Параметры:
//  Контрагент - СправочникСсылка.Контрагенты, ссылка на контрагента, по которому надо получить ответственного.
//  Соглашение - СправочникСсылка.СоглашениеОбИспользованииЭД, ссылка на соглашение, по которому надо найти ответственного.
//
Функция ПолучитьОтветственногоПоЭД(Контрагент, Соглашение) Экспорт
	
	ОтветственныйПоЭД = Справочники.Пользователи.ПустаяСсылка();
	
	Если ТипЗнч(Контрагент) = Тип("СправочникСсылка.Контрагенты") Тогда
		ОтветственныйПоЭД = Контрагент.ОсновнойМенеджерПокупателя;
	КонецЕсли;
	
	Возврат ОтветственныйПоЭД;
	
КонецФункции

// Возвращает признак физ. лица.
//
// Параметры:
//  ДанныеКонтрагента - ссылка на элемент справочника.
//
Функция ЭтоФизЛицо(ДанныеКонтрагента) Экспорт
	
	Если ДанныеКонтрагента.Метаданные().Реквизиты.Найти("ЮрФизЛицо") = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЮрФизЛицо = ДанныеКонтрагента.ЮрФизЛицо;
	
	Если ТипЗнч(ЮрФизЛицо) <> Тип("ПеречислениеСсылка.ЮрФизЛицо") Тогда
		Возврат Ложь;
	КонецЕсли;
		
	ЭтоФизЛицо = Ложь;
	Если ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
		ЭтоФизЛицо = Истина;
	КонецЕсли;
	
	Возврат ЭтоФизЛицо;
	
КонецФункции

// Функция возвращает, нужно ли вы водить данные о скидках в печатную форму документа
//
Функция НужноВыводитьСкидки(Знач Товары, ИспользоватьСкидки) Экспорт
	
	
	
	Возврат Ложь;
	
КонецФункции

// Получает имя дополнительной колонки.
//
// Возвращаемое значение:
//  ИмяКолонки - строка колонки.
//
Функция ИмяДополнительнойКолонки() Экспорт
	
	Результат = "";
	
	ДопКолонка = Константы.ДополнительнаяКолонкаПечатныхФормДокументов.Получить();
	Если ДопКолонка = Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул Тогда
		Результат = "Артикул";
	ИначеЕсли ДопКолонка = Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.Код Тогда
		Результат = "Код";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает числовое значение ставки НДС по значению перечисления
//
// Параметры:
//  СтавкаНДС - ПеречислениеСсылка.СтавкиНДС - значение перечисления СтавкиНДС
//
// Возвращаемое значение:
//  Число - Значение ставки НДС числом
//  Если СтавкаНДС = 0%, то число = О;
//  Если СтавкаНДС = БезНДС, то число = Неопределено.
//
Функция ПолучитьСтавкуНДСЧислом(Знач СтавкаНДС) Экспорт
	
	Если СтавкаНДС = Перечисления.СтавкиНДС.БезНДС Тогда
		Результат = Неопределено;
	Иначе
		Результат = УчетНДС.ПолучитьСтавкуНДС(СтавкаНДС);
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

// Функция преобразует из числового представления ставки НДС в значение перечисления.
//
// Параметры:
//  СтавкаЧислом - Число - Ставка НДС числом.
//
// Возвращаемое значение:
//  СтавкаНДС - Значение перечисление
//  Если СтавкаЧислом = О, то СтавкаНДС = 0%;
//  Если СтавкаЧислом = Неопределено, то СтавкаНДС = БезНДС.
//
Функция ЗначениеПеречисленияСтавкаНДС(СтавкаЧислом) Экспорт
	
	ЗначениеНДС = Неопределено;
	
	Если ТипЗнч(СтавкаЧислом) = Тип("Строка") Тогда
		ПредставлениеСтавкиНДС = СтрЗаменить(СтавкаЧислом, " ", "");
	ИначеЕсли ТипЗнч(СтавкаЧислом) = Тип("Число") Тогда 
		ПредставлениеСтавкиНДС = СтрЗаменить(Строка(СтавкаЧислом), " ", "");
	Иначе // неправильный тип
		ПредставлениеСтавкиНДС = Неопределено;
	КонецЕсли;
	
	Если ПредставлениеСтавкиНДС = Неопределено 
		ИЛИ ВРег(ПредставлениеСтавкиНДС) = "БЕЗНДС" Тогда
		ЗначениеНДС = Перечисления.СтавкиНДС.БезНДС;
		
	ИначеЕсли Найти("0#0%", ПредставлениеСтавкиНДС) > 0 Тогда
		ЗначениеНДС = Перечисления.СтавкиНДС.НДС0;
		
	ИначеЕсли Найти("10#0.1#0,1#0.10#0,10#10%", ПредставлениеСтавкиНДС) > 0 Тогда
		ЗначениеНДС = Перечисления.СтавкиНДС.НДС10;
		
	ИначеЕсли Найти("20#0.2#0,2#0.20#0,20#20%", ПредставлениеСтавкиНДС) > 0 Тогда
		ЗначениеНДС = Перечисления.СтавкиНДС.НДС20;
		
	ИначеЕсли Найти("18#0.18#0,18#0.18#0,18#18%", ПредставлениеСтавкиНДС) > 0 Тогда
		ЗначениеНДС = Перечисления.СтавкиНДС.НДС18;
		
	ИначеЕсли Найти("10/110#10%/110%", ПредставлениеСтавкиНДС) > 0 Тогда
		ЗначениеНДС = Перечисления.СтавкиНДС.НДС10_110;
		
	ИначеЕсли Найти("18/118#18%/118%", ПредставлениеСтавкиНДС) > 0 Тогда
		ЗначениеНДС = Перечисления.СтавкиНДС.НДС18_118;
		
	ИначеЕсли Найти("20/120#20%/120%", ПредставлениеСтавкиНДС) > 0 Тогда
		ЗначениеНДС = Перечисления.СтавкиНДС.НДС20_120; 
	//ТЕАЛ НДС ++	
	ИначеЕсли Найти("5#0.05#0,05#0.050#0,050#5%", ПредставлениеСтавкиНДС) > 0 Тогда
		ЗначениеНДС = Перечисления.СтавкиНДС.НДС5;
		
	ИначеЕсли Найти("7#0.07#0,07#0.070#0,070#7%", ПредставлениеСтавкиНДС) > 0 Тогда
		ЗначениеНДС = Перечисления.СтавкиНДС.НДС7; 
		
    ИначеЕсли Найти("7/107#7%/107%", ПредставлениеСтавкиНДС) > 0 Тогда
		ЗначениеНДС = Перечисления.СтавкиНДС.НДС7_107;
		
	ИначеЕсли Найти("5/105#5%/105%", ПредставлениеСтавкиНДС) > 0 Тогда
		ЗначениеНДС = Перечисления.СтавкиНДС.НДС5_105; 
		
	ИначеЕсли Найти("22#0.22#0,22#0.22#0,22#22%", ПредставлениеСтавкиНДС) > 0 Тогда
		ЗначениеНДС = Перечисления.СтавкиНДС.НДС22;  
		
	ИначеЕсли Найти("22/122#22%/122%", ПредставлениеСтавкиНДС) > 0 Тогда
		ЗначениеНДС = Перечисления.СтавкиНДС.НДС22_122;
	
	//ТЕАЛ НДС --	
	КонецЕсли;
	
	Возврат ЗначениеНДС;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Переопределение поведения электронных документов

// Данное событие возникает при изменении элемента справочника ЭДПрисоединенныеФайлы
// Предназначено для переопределения или добавления изменяемых реквизитов электронного документа
//
// Параметры:
//  Объект - СправочникСсылка.ЭДПрисоединенныеФайлы - изменяемый объект
//  СтруктураПараметров - Структура, содержит структуру изменяемых реквизитов
//
Процедура ПриИзмененииПрисоединенногоФайла(Объект, СтруктураПараметров) Экспорт
	
	Если Не ЗначениеЗаполнено(Объект.Ответственный) И Не СтруктураПараметров.Свойство("Ответственный") Тогда
		СтруктураПараметров.Вставить("Ответственный", Пользователи.АвторизованныйПользователь());
	КонецЕсли;
	
КонецПроцедуры

// Выполняет дополнительную обработку электронного документа, которому назначили статус "Утвержден".
// 
// Параметры:
//  ЭлектронныйДокумент - ссылка на присоединенный файл.
//
Процедура НазначенСтатусУтвержден(ЭлектронныйДокумент) Экспорт
	
КонецПроцедуры

// Выполняет дополнительную обработку электронного документа, которому назначили статус "Подписан".
// 
// Параметры:
//  ЭлектронныйДокумент - ссылка на присоединенный файл.
//
Процедура НазначенСтатусПодписан(ЭлектронныйДокумент) Экспорт
	
	
КонецПроцедуры

// Проверяет, готовность документов ИБ для формирования ЭД, и удаляет из массива не готовые документы
//
// Параметры
//  ДокументыМассив - Массив   - ссылки на документы, которые должны быть проверены перед формированием ЭД.
//
Процедура ПроверитьГотовностьИсточников(ДокументыМассив, ФормаИсточник = Неопределено) Экспорт
	
	ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияТипаИзМассива(ДокументыМассив, Тип("СтрокаГруппировкиДинамическогоСписка"));
	
	ДокументыНеВыставляются = Новый Массив();
	ДокументыНеВыставляютсяВЭлектронномВиде = Новый Массив();
	СводныеКомиссионныеСчетаФактуры = Новый Массив();
	СводныеКорректировочныеСчетаФактуры = Новый Массив();
	ДокументыКорректировкиПоСогласованиюСторон = Новый Массив();
	ДокументыИсправленияКорректировкиПоСогласованиюСторон = Новый Массив();
	ДокументыКорректировкиБезСФ = Новый Массив();
	
	Для каждого Документ из ДокументыМассив Цикл
		
		Если ТипЗнч(Документ) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда 
			Если Документ.СчетФактураНеВыставляется Тогда
				// На основании счетов-фактур с признаком СчетФактураНеВыставляется ЭД формировать не требуется
				ДокументыНеВыставляются.Добавить(Документ);
			ИначеЕсли Документ.СводныйКомиссионный Тогда
				СводныеКомиссионныеСчетаФактуры.Добавить(Документ);
			ИначеЕсли Документ.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.Корректировочный
				И Документ.ДокументыОснования.Количество()>1 Тогда
				СводныеКорректировочныеСчетаФактуры.Добавить(Документ);
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
			
			// На основании док. "Корректировка реализации", осуществляющего исправление/согласованное изменение 
			// документа отличного от док. "РеализацияТоваровУслуг", ЭД не формируется
			
			ИсходныйИсправляемыйДокумент = УчетНДС.ПолучитьИсправляемыйДокументРеализации(Документ.ДокументРеализации, Истина);
			
			Если ТипЗнч(ИсходныйИсправляемыйДокумент) <> Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
				 
				ДокументыНеВыставляютсяВЭлектронномВиде.Добавить(Документ);
				
			КонецЕсли;
			
		
			НастройкиОбмена = ЭлектронныеДокументыСлужебный.ОпределитьНастройкиОбменаЭДПоИсточнику(Документ,
																								   ,
																								   ,
																								   ,
																								   );
			Если ЗначениеЗаполнено(НастройкиОбмена) Тогда
				Если НастройкиОбмена.ВерсияФормата = "ФНС 5.01 (УКД:Корректировочный счет-фактура)" Тогда
				
					Если Не ЗначениеЗаполнено(УчетНДС.НайтиПодчиненныйСчетФактуру(Документ, "СчетФактураВыданный")) Тогда
					
						ДокументыКорректировкиБезСФ.Добавить(Документ);
					
					КонецЕсли;
					
				КонецЕсли;
			КонецЕсли;

		КонецЕсли;
		
	КонецЦикла;
	
	УдалитьДокументыНеподходящиеДляФормированияЭД(
		ДокументыМассив,
		ДокументыНеВыставляются,
		НСтр("ru = 'Документ ""%1"" не выставляется. Электронный документ не сформирован.'"));
		
	УдалитьДокументыНеподходящиеДляФормированияЭД(
		ДокументыМассив,
		ДокументыНеВыставляютсяВЭлектронномВиде,
		НСтр("ru = 'Документ ""%1"" не выставляется в электронном виде. Электронный документ не сформирован.'"));
		
	УдалитьДокументыНеподходящиеДляФормированияЭД(
		ДокументыМассив,
		СводныеКомиссионныеСчетаФактуры,
		НСтр("ru = 'Для документа ""%1"" установлен признак ""Сводный"". Отправка таких счетов-фактур не поддерживается.'"));
		
	УдалитьДокументыНеподходящиеДляФормированияЭД(
		ДокументыМассив,
		СводныеКорректировочныеСчетаФактуры,
		НСтр("ru = 'Корректировочный счета-фактура ""%1"" содержит несколько документов-оснований. Отправка таких счетов-фактур не поддерживается.'"));

	УдалитьДокументыНеподходящиеДляФормированияЭД(
		ДокументыМассив,
		ДокументыКорректировкиБезСФ,
		НСтр("ru = 'По документу ""%1"" нужно выписать счет-фактуру.'"));
		
	// Перед формированием ЭД документы ИБ должны быть проведены
	МассивПроводимыхДокументов = Новый Массив;
	МассивТиповНепроводимыхДокументов = Новый Массив;
	Для каждого Элемент из ДокументыМассив Цикл
		ИмяДокумента = Элемент.Метаданные().ПолноеИмя();
		Если Метаданные.Документы.Содержит(Метаданные.НайтиПоПолномуИмени(ИмяДокумента)) Тогда
			
			Если Элемент.Метаданные().Проведение = Метаданные.СвойстваОбъектов.Проведение.Запретить Тогда
				Если МассивТиповНепроводимыхДокументов.Найти(ТипЗнч(Элемент)) = Неопределено Тогда
					МассивТиповНепроводимыхДокументов.Добавить(ТипЗнч(Элемент));
				КонецЕсли;
			КонецЕсли;
			
			МассивПроводимыхДокументов.Добавить(Элемент)
		КонецЕсли;
	КонецЦикла;
	
	Для каждого ТипНепроводногоДокумента ИЗ МассивТиповНепроводимыхДокументов Цикл
		ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияТипаИзМассива(МассивПроводимыхДокументов, ТипНепроводногоДокумента);
	КонецЦикла;
	
	МассивНепроведенныхДокументов = ОбщегоНазначения.ПроверитьПроведенностьДокументов(МассивПроводимыхДокументов);
	
	КоличествоНепроведенныхДокументов = МассивНепроведенныхДокументов.Количество();
	Если КоличествоНепроведенныхДокументов > 0 Тогда
		
		#Если Клиент Тогда
		Если КоличествоНепроведенныхДокументов = 1 Тогда
			ТекстВопроса = НСтр("ru = 'Для того чтобы сформировать электронную версию документа, его необходимо предварительно провести. Выполнить проведение документа и продолжить?'");
		Иначе
			ТекстВопроса = НСтр("ru = 'Для того чтобы сформировать электронные версии документов, их необходимо предварительно провести. Выполнить проведение документов и продолжить?'");
		КонецЕсли;
		
		КодОтвета = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Если КодОтвета = КодВозвратаДиалога.Да Тогда
			ДанныеОНепроведенныхДокументах = ОбщегоНазначенияВызовСервера.ПровестиДокументы(МассивНепроведенныхДокументов);
			
			ШаблонСообщения = НСтр("ru = 'Документ %1 не проведен: %2 Формирование ЭД невозможно.'");
			НепроведенныеДокументы = Новый Массив;
			Для Каждого ИнформацияОДокументе Из ДанныеОНепроведенныхДокументах Цикл
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, Строка(ИнформацияОДокументе.Ссылка), 
				ИнформацияОДокументе.ОписаниеОшибки), ИнформацияОДокументе.Ссылка);
				НепроведенныеДокументы.Добавить(ИнформацияОДокументе.Ссылка);		
			КонецЦикла;
			
			ДокументыМассив = ОбщегоНазначенияКлиентСервер.СократитьМассив(ДокументыМассив, НепроведенныеДокументы);
		Иначе
			ШаблонСообщения = НСтр("ru = 'Документ %1 не проведен. Электронный документ не сформирован.'");
			УдалитьДокументыНеподходящиеДляФормированияЭД(
				ДокументыМассив,
				МассивНепроведенныхДокументов,
				ШаблонСообщения);
		КонецЕсли;
		#Иначе
		ШаблонСообщения = НСтр("ru = 'Документ %1 не проведен. Электронный документ не сформирован.'");
		УдалитьДокументыНеподходящиеДляФормированияЭД(
			ДокументыМассив,
			МассивНепроведенныхДокументов,
			ШаблонСообщения);
		#КонецЕсли
	КонецЕсли;
	
	// Проверка возможности формирования единого документа
	МассивНаУдаление = Новый Массив();
	Для Каждого Элемент Из ДокументыМассив Цикл
		Если ТипЗнч(Элемент) = Тип("ДокументСсылка.ЭлектроннаяТранспортнаяНакладная") Тогда
			Продолжить
		КонецЕсли;
		Отказ = Ложь;
		ПроверитьВозможностьФормированияЕдиногоДокумента(Элемент, Отказ);
		Если Отказ Тогда
			МассивНаУдаление.Добавить(Элемент);
		КонецЕсли;
	КонецЦикла;

	Для Каждого Элемент Из МассивНаУдаление Цикл
		ЭлементНаУдаление = ДокументыМассив.Найти(Элемент);
		Если ЭлементНаУдаление <> Неопределено Тогда
			ДокументыМассив.Удалить(ДокументыМассив.Найти(Элемент));
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

// Проверяет все ли необходимые подписи установлены перед отправкой контрагенту.
// 
// Параметры:
//  ЭлектронныйДокумент - ссылка на присоединенный файл.
//  ФлагПодписанПолностью - булево - признак полностью подписанного документа.
//
Процедура ЭлектронныйДокументПолностьюПодписан(ЭлектронныйДокумент, ФлагПодписанПолностью) Экспорт
	
	
КонецПроцедуры

// Проверяет выполняются ли необходимые автоматические условия для утверждения документа.
//
// Параметры:
//  ЭлектронныйДокумент - ссылка на присоединенный файл.
//
Функция ЭлектронныйДокументГотовКУтверждению(ЭлектронныйДокумент) Экспорт
	
	Возврат Истина;
	
КонецФункции

// Устаревшие: прочее

// Устарела. Следует использовать ПередЗаписьюВладельцаЭлектронногоДокумента
// Определяет возможно ли редактировать объект информационной базы
//
// Параметры
//  <СсылкаНаОбъект>  - <любая ссылка> - ссылка на проверяемый объект
/// <РедактированиеРазрешено> - <Булево>   - возвращает разрешено или нет редактирование
//
Функция ПроверитьВозможностьРедактированияОбъекта(СсылкаНаОбъект, РедактированиеРазрешено) Экспорт

	
	
КонецФункции

// Выполняется проверка возможности корректного чтения Пакета ЭД.
// Необходимость данной проверки возникает при работе с данными внешней информационной базы (через com-соединение).
//
// Параметры:
//  ПакетЭД - ДокументСсылка.ПакетЭД - исследуемый пакет электронных документов.
//  ЧтениеПакетаВозможно - булево/неопределено - Ложь - чтение пакета не будет выполняться, во всех остальных случаях,
//    (включая пустое значение) пакет будет прочитан.
//
Процедура ОпределитьВозможностьЧтенияДвоичныхДанныхПакетаЭД(ПакетЭД, ЧтениеПакетаВозможно) Экспорт
	
	
	
КонецПроцедуры

// Проверяет на корректность заполнения параметров электронного документа.
//
// Параметры:
//  ПараметрыЭД - структура - перечень параметров ЭД.
//
// Возвращаемое значение:
//  Булево - Истина если правильно заполнен объект выгрузки
//
Функция ПроверитьПравильностьЗаполненияОбъекта(ПараметрыЭД) Экспорт
	
	Возврат Истина;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Работа с правами

// Проверяет наличие прав обрабатывать электронный документы.
//
// Возвращаемое значение:
//  Булево - истина или ложь, в зависимости от установленных прав.
//
Функция ЕстьПравоОбработкиЭД() Экспорт
	
	Результат = (ПользователиБСП.ЭтоПолноправныйПользователь() ИЛИ ПользователиБСП.РолиДоступны("ВыполнениеОбменаЭД"));
	
	Возврат Результат;
	
КонецФункции

// Проверяет наличие прав читать электронный документы.
//
// Возвращаемое значение:
//  Булево - истина или ложь, в зависимости от установленных прав.
//
Функция ЕстьПравоЧтенияЭД() Экспорт
	
	Результат = (ПользователиБСП.ЭтоПолноправныйПользователь() ИЛИ ПользователиБСП.РолиДоступны("ЧтениеЭД"));
	
	Возврат Результат;
	
КонецФункции

// Проверяет наличие прав на открытие журнала регистрации.
//
// Возвращаемое значение:
//  Булево - истина или ложь, в зависимости от установленных прав.
//
Функция ЕстьПравоОткрытияЖурналаРегистрации() Экспорт
	
	Результат = ПользователиБСП.ЭтоПолноправныйПользователь();
	
	Возврат Результат;
	
КонецФункции

// Проверяет наличие прав на настройку параметров электронных документов.
//
// Возвращаемое значение:
//  Булево - истина или ложь, в зависимости от установленных прав.
//
Функция ЕстьПравоНастройкиПараметровЭД() Экспорт
	
	Результат = (ПользователиБСП.ЭтоПолноправныйПользователь() ИЛИ ПользователиБСП.РолиДоступны("НастройкаПараметровЭД"));
	
	Возврат Результат;
	
КонецФункции

// Проверяет наличие прав подписания электронных документов электронной подписью.
//
// Возвращаемое значение:
//  Булево - истина или ложь, в зависимости от установленных прав.
//
Функция ЕстьПравоПодписанияЭД() Экспорт
	
	Результат = ЕстьПравоОбработкиЭД();
	
	Возврат Результат;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Работа с элементами форм

// Изменяет поведение элементов управляемой или обычной формы.
//
// Параметры:
//  Форма - <Управляемая или обычная форма> - управляемая или обычная форма для изменения.
//  СтруктураПараметров - <Структура> - параметры процедуры
//
Процедура ИзменитьСвойстваЭлементовФормы(Форма, СтруктураПараметров) Экспорт
	
	Если СтруктураПараметров.Свойство("ВидОперации")
		И СтруктураПараметров.Свойство("ЗначениеПараметра") Тогда
		Если ВРег(СтруктураПараметров.ВидОперации) = ВРег("УстановкаГиперссылки")
			И СтруктураПараметров.Свойство("ТекстСостоянияЭДО") Тогда
			// Зададим особые условия.
			Если Найти(СтруктураПараметров.ТекстСостоянияЭДО, "Не сформирован") > 0 Тогда
				СтруктураПараметров.ЗначениеПараметра = Ложь;
			КонецЕсли;
			// Определим элемент формы.
			НайденныйЭлементФормы = Неопределено;
			Если ТипЗнч(Форма) = Тип("УправляемаяФорма") Тогда // только для управляемой формы
				НайденныйЭлементФормы = Форма.Элементы.Найти("ТекстСостоянияЭД");
				
				// Заполним свойство найденного элемента.
				Если НЕ НайденныйЭлементФормы = Неопределено
					И НайденныйЭлементФормы.Вид = ВидПоляФормы.ПолеНадписи Тогда
					НайденныйЭлементФормы.Гиперссылка = СтруктураПараметров.ЗначениеПараметра;
				КонецЕсли;
			Иначе // для обычной формы
				НайденныйЭлементФормы = Форма.ЭлементыФормы.Найти("ТекстСостоянияЭД");
				
				// Заполним свойство найденного элемента.
				Если НЕ НайденныйЭлементФормы = Неопределено
					И ТипЗнч(НайденныйЭлементФормы) = Тип("Надпись") Тогда
					НайденныйЭлементФормы.Гиперссылка = СтруктураПараметров.ЗначениеПараметра;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Переопределение сообщений пользователю

// Переопределяет выводимое сообщение об ошибке
// КодОшибки - строка
// ТекстОшибки - строка
Процедура ИзменитьСообщениеОбОшибке(КодОшибки, ТекстОшибки) Экспорт
	
	Если КодОшибки = "100" ИЛИ КодОшибки = "110" Тогда
		ТекстОшибки = "Проверьте общие настройки криптографии."
	КонецЕсли;
	
КонецПроцедуры

// Определяет текст сообщения о необходимости настройки системы в зависимости от вида операции.
//
// Параметры:
//  ВидОперации    - строка - признак выполняемой операции;
//  ТекстСообщения - строка - текст сообщения.
//
Процедура ТекстСообщенияОНеобходимостиНастройкиСистемы(ВидОперации, ТекстСообщения) Экспорт
	
	Если ВРег(ВидОперации) = "РАБОТАСЭД" Тогда
		ТекстСообщения = НСтр("ru = 'Для работы с электронными документами необходимо в форме ""Настройка параметров учета""
		|на закладке ""Электронные документы"" включить использование обмена электронными документами.'");
	ИначеЕсли ВРег(ВидОперации) = "ПОДПИСАНИЕЭД" Тогда
		ТекстСообщения = НСтр("ru = 'Для возможности подписания ЭД необходимо в форме ""Настройка параметров учета""
		|на закладке ""Электронные документы"" включить опцию использования электронных цифровых подписей.'");
	ИначеЕсли ВРег(ВидОперации) = "НАСТРОЙКАКРИПТОГРАФИИ" Тогда
		ТекстСообщения = НСтр("ru = 'Для возможности настройки криптографии необходимо в форме ""Настройка параметров учета""
		|на закладке ""Электронные документы"" включить опцию использования электронных цифровых подписей.'");
	ИначеЕсли ВРег(ВидОперации) = "РАБОТАС1ССЕТЬ" Тогда
		ТекстСообщения = НСтр("ru = 'Для работы с данным видом обмена необходимо в форме ""Настройка параметров учета""
		|на закладке ""Электронные документы"" включить использование обмена через 1С:Сеть.'");
	ИначеЕсли ВРег(ВидОперации) = "РАБОТАСCOMMERCEML" Тогда
		ТекстСообщения = НСтр("ru = 'Для работы с данным видом обмена необходимо в форме ""Настройка параметров учета""
		|на закладке ""Электронные документы"" включить использование обмена в формате CommerceML.'");
	ИначеЕсли ВРег(ВидОперации) = "ДОСТУП" Тогда
		ТекстСообщения = НСтр("ru = 'Нарушение прав доступа'");
	Иначе
		ТекстСообщения = НСтр("ru='Операция не может быть выполнена'");
	КонецЕсли;
	
КонецПроцедуры

// Переопределяет сообщение о нехватке прав доступа
//
// Параметры:
//  ТекстСообщения - строка сообщения
//
Процедура ПодготовитьТекстСообщенияОНарушенииПравДоступа(ТекстСообщения) Экспорт
	
	// При необходимости можно переопределить или дополнить текст сообщения
	
КонецПроцедуры

// Получает таблицу соответствий параметров для типов метаданных их пользовательским представлениям.
//
// Параметры:
//  ТаблицаСоответствия - таблица - соответствие параметров для типов метаданных их пользовательским представлениям
//  содержит следующие колонки: ТипИсточника, Параметр, Представление.
//
Процедура ПолучитьТаблицуСоответствияПараметровПользовательскимПредставлениям(ТаблицаСоответствия) Экспорт
	
	Макет                   = Обработки.ЭлектронныеДокументы.ПолучитьМакет("ПользовательскоеПредставлениеОбязательныхПолей");
	ОбластьДокументов       = Макет.ПолучитьОбласть("ОбязательныеПоля");
	ОбластьДокументовВысота = ОбластьДокументов.ВысотаТаблицы;

	Для НСтр = 1 По ОбластьДокументовВысота Цикл
		
		НоваяСтрока = ТаблицаСоответствия.Добавить();
		НоваяСтрока.ТипИсточника  = Тип(СокрЛП(ОбластьДокументов.Область(НСтр, 1).Текст));
		НоваяСтрока.Параметр      = СокрЛП(ОбластьДокументов.Область(НСтр, 2).Текст);
		НоваяСтрока.Представление = СокрЛП(ОбластьДокументов.Область(НСтр, 3).Текст);
	КонецЦикла;
	
КонецПроцедуры

// Заполняет соответствие кодов реквизитов схем электронных документов их пользовательскому представлению.
//
// Параметры:
//  СоответствиеВозврата - Соответствие, исходное соответствие для заполнения.
//
Процедура СоответствиеКодовРеквизитовИПредставлений(СоответствиеВозврата) Экспорт
	
	Макет = Обработки.ЭлектронныеДокументы.ПолучитьМакет("ПрикладноеПредставлениеРеквизитов");
	ВысотаТаблицы = Макет.ВысотаТаблицы;
	Для НСтр = 1 По ВысотаТаблицы Цикл
		СоответствиеВозврата.Вставить(СокрЛП(Макет.Область(НСтр, 1).Текст), СокрЛП(Макет.Область(НСтр,2).Текст));
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Общие процедуры и функции

// Создает таблицу порядка создания типов при загрузке электронного документа.
//
// Возвращаемое значение:
//  Таблица - таблица значений.
//
Функция ЗаполнитьТаблицуПорядкаСозданияТиповОбъектов() Экспорт
	
	Таблица = Новый ТаблицаЗначений;
	
	Таблица.Колонки.Добавить("ТипОбъекта");
	Таблица.Колонки.Добавить("Порядок");
	
	Возврат Таблица;
	
КонецФункции

// При необходимости, в функции можно определить каталог для временных файлов,
// отличный от устанавливаемого по умолчанию в библиотеке ЭД.
//
// Параметры:
//  ТекущийКаталог - путь к каталогу временных файлов.
//
Процедура ТекущийКаталогВременныхФайлов(ТекущийКаталог) Экспорт
	
	ТекущийКаталог = КаталогВременныхФайлов();
	
КонецПроцедуры

// Получает имя временного файла.
//
// Параметры:
//  ИмяВременногоФайла - Строка - имя временного файла;
//  Расширение - Строка - расширение для временного файла.
//
Процедура ТекущееИмяВременногоФайла(ИмяВременногоФайла, Расширение = "") Экспорт
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла(Расширение);
	
КонецПроцедуры

// Разбирает из переданной строки фамилию, имя и отчество.
//
// Параметры
//  ПолноеНаименование - строка с наименованием;
//  Фамилия - строка с фамилией;
//  Имя - строка с именем;
//  Отчество - строка с отчеством.
//
Процедура РазобратьНаименованиеФизЛица(ПолноеНаименование, Фамилия = " ", Имя = " ", Отчество = " ") Экспорт
	
	ФИО = ПолноеНаименование;
	
	Если ВРЕГ(Лев(ФИО,3)) = "ИП " Тогда
		ФИО = Прав(ФИО, СтрДлина(ФИО)-3);
	ИначеЕсли ВРЕГ(Лев(ФИО, СтрДлина("Индивидуальный предприниматель")))="ИНДИВИДУАЛЬНЫЙ ПРЕДПРИНИМАТЕЛЬ" Тогда
		ФИО = Прав(ФИО, СтрДлина(ФИО)-СтрДлина("Индивидуальный предприниматель"));
	КонецЕсли;
	ОбщегоНазначения.ФамилияИнициалыФизЛица(ФИО, Фамилия, Имя, Отчество);
	
КонецПроцедуры

// Осуществляет разбор файла с реквизитами контрагента
// в ней можно внести изменения в структуру возвращаемых данных
//
// Параметры:
//  СсылкаНаФайл - адрес хранилища файла с реквизитами контрагента;
//  СтруктураВозврата - Структура - перечень параметров;
//  ОшибкаРазбора - текст, описание ошибки.
//
// Возвращаемое значение:
//  РезультатРазбора - Булево - Истина - разбор файла выполнен; Ложь - разбор файла не выполнялся.
//
Процедура РазобратьФайлРеквизитовКонтрагента(СсылкаНаФайл, СтруктураВозврата, РезультатРазбора, ОшибкаРазбора) Экспорт
	// не используется
КонецПроцедуры

// Возвращает структуру, содержащую значения реквизитов прочитанные из информационной базы
// по ссылке на объект.
// 
// Если не задан альтернативный алгоритм получения значений реквизитов (процедура пуста), то используется функция БСП:
// ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, ИменаРеквизитов).
// 
// Параметры:
//  Ссылка       - ссылка на объект, - элемент справочника, документ, ...
//  ИменаРеквизитов - Строка или Структура - если Строка, то имена реквизитов, 
//               перечисленные через запятую, в формате требований к свойствам структуры.
//               Например, "Код, Наименование, Родитель".
//               Если Структура, то в качестве ключа передается псевдоним поля для
//               возвращаемой структуры с результатом, а в качестве значения (опционально) 
//               - фактическое имя поля в таблице. 
//               Если значение не определено, то имя поля берется из ключа.
//  СтруктураДанных - содержит список свойств, как список имен в строке
//                 ИменаРеквизитов, со значениям реквизитов, прочитанных
//                 из информационной базы.
// 
Процедура ПолучитьСтруктуруЗначенийРеквизитов(Ссылка, ИменаРеквизитов, СтруктураДанных) Экспорт
	
	
	
КонецПроцедуры

// Определяет Счета-Фактуры по документам основаниям.
// 
// Параметры:
//  МассивОснований - Массив - массив ссылок на документы основания.
//  МассивСФ - Массив - массив для заполнения ссылками на счета-фактуры.
//  НаправлениеЭД - ПеречислениеСсылка.НаправленияЭД - направление электронного документа.
// 
Процедура ЗаполнитьСчетаФактурыПоДокументамОснования(МассивОснований, МассивСФ, НаправлениеЭД) Экспорт
	
	
	
КонецПроцедуры

Процедура СообщитьОбОшибкеПриФормированииЭД(Документ, ТекстОшибки)

	ШаблонСообщения = НСтр("ru = 'Не удалось сформировать электронный документ на основании документа ""%1"". %2'");
	
	ОбщегоНазначения.СообщитьПользователю(СтрШаблон(ШаблонСообщения, Документ, ТекстОшибки),,,,);

КонецПроцедуры

Функция ТекстОшибкиНекорректнаяВалюта()
	
	Возврат НСтр("ru = 'Не удалось заполнить код валюты документа. Проверьте, что:
		|	- в документе указана валюта,
		|	- для нее заполнен код по Общероссийскому классификатору валют.'");
	
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// Механизм однократной сделки

// Выполняет заполнение списка документов по виду электронного документа.
//
// Параметры:
//  ВидЭД           - Перечисления   - вид электронного документа;
//  СписокВозврата  - СписокЗначений - список ссылок на документы информационной базы.
//
Процедура СписокТиповДокументовПоВидуЭД(ВидЭД, СписокВозврата) Экспорт
	
	ВидыЭД = Перечисления.ВидыЭД;
	ИмяДокумента = "";
	
	Если ВидЭД = ВидыЭД.ТОРГ12 ИЛИ ВидЭД = ВидыЭД.ТОРГ12Продавец ИЛИ ВидЭД = ВидыЭД.АктИсполнитель
		ИЛИ ВидЭД = ВидыЭД.АктВыполненныхРабот ИЛИ ВидЭД = ВидыЭД.АктНаПередачуПрав Тогда
		ИмяДокумента = "ПоступлениеТоваровУслуг";
	ИначеЕсли ВидЭД = ВидыЭД.СчетФактура ИЛИ ВидЭД = ВидыЭД.КорректировочныйСчетФактура Тогда
		ИмяДокумента = "СчетФактураПолученный";
	ИначеЕсли ВидЭД = Перечисления.ВидыЭД.СчетНаОплату Тогда
		ИмяДокумента = "СчетНаОплатуПоставщика";
	ИначеЕсли ВидЭД = ВидыЭД.ЗаказТовара Тогда
		ИмяДокумента = "ЗаказПокупателя";
	ИначеЕсли ВидЭД = ВидыЭД.ОтветНаЗаказ Тогда
		ИмяДокумента = "ЗаказПоставщику";
	ИначеЕсли ВидЭД = ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель Тогда
		ИмяДокумента = "КорректировкаПоступления";
	ИначеЕсли ВидЭД = ВидыЭД.АктСверкиВзаиморасчетов Тогда
		ИмяДокумента = "АктСверкиВзаиморасчетов";
	ИначеЕсли ВРег(ВидЭД) = ВРег("РеквизитыОрганизации") Тогда
		СписокВозврата.Добавить(Справочники.Контрагенты.ПустаяСсылка(),
									Метаданные.Справочники.Контрагенты.Представление());
	КонецЕсли;
	
	Если ИмяДокумента <> "" Тогда
		СписокВозврата.Добавить(Документы[ИмяДокумента].ПустаяСсылка(), Метаданные.Документы[ИмяДокумента].Представление());
	КонецЕсли;
	
КонецПроцедуры

// Задает имя файла "по умолчанию", с которым будет предложено пользователю сохранить
// ЭД при выгрузке по сценарию "Однократной сделки".
//
// Параметры:
//  ВладелецЭД - ссылка на документ ИБ, на основании которого формируется и выгружается ЭД,
//  НаименованиеФайла - Строка - имя файла.
//
Процедура ЗадатьИмяСохраняемогоФайлаПриБыстромОбмене(ВладелецЭД, НаименованиеФайла) Экспорт
	
КонецПроцедуры

// Получает реквизиты элемнта справочника "Организации", для выгрузки в xml-файл.
//
// Параметры:
//  Организация - СправочникСсылка.Организации - элемент справочника организации;
//  СтруктураВозврата - структура - перечень параметров организации.
//
Процедура ПолучитьРеквизитыОрганизацииДляВыгрузкиВФайл(Организация, СтруктураВозврата) Экспорт
	
	ЗаполнитьЗначенияСвойств(СтруктураВозврата, 
		ОбщегоНазначения.ПолучитьЗначенияРеквизитов(Организация, "Наименование, НаименованиеПолное, ИНН, КПП, ЮрФизЛицо"));
	
КонецПроцедуры

// Механизм выгрузки электронных документов в файлы для отправки в ФНС через сервис 1С-Отчетность

// Функция должна возвращать дату и номер документов оснований (договоров) по массиву ссылок.
// Параметры функции:
// Парам 1 -  массив ссылок на документы ИБ
// (в качестве возможных значений следует принимать те типы документов,
// на основании которых в данном прикладном решении формируется электронный документ вида «Акт приемки-сдачи работ (услуг)»)
//
// Параметры:
//  МассивСсылок - массив ссылок на документы ИБ;
//  ВозвращаемоеСоответствие - Соответствие со следующими свойствами:
//    ключ соответствия - ссылка на выгружаемый документ ИБ, взятая из входящего параметра
//    значение соответствия - Структура, с полями:
//    НомерДоговора, тип: Строка
//    ДатаДоговора, тип: Дата
// В случае, если требуемые реквизиты у договора не заполнены или при невозможности получения данных реквизитов, помещать пустые значения указанных типов.
//
Процедура ПолучитьНомерДатаДоговораДокументов(МассивСсылок, ВозвращаемоеСоответствие) Экспорт
	
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Механизм прямого обмена между организациями

////////////////////////////////////////////////////////////////////////////////
// Подготавливает данные для электронного документа типа ПередачаТоваровМеждуОрганизациями.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  СтруктураПараметров - структура, параметры для заполнения.
//
Процедура ПодготовитьДанныеПоПередачеТоваровМеждуОрганизациями(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметров) Экспорт
	
	
		
КонецПроцедуры

// Подготавливает данные для электронного документа типа ВозвратТоваровМеждуОрганизациями.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  СтруктураПараметров - структура, параметры для заполнения.
//
Процедура ПодготовитьДанныеПоВозвратуТоваровМеждуОрганизациями(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметров) Экспорт
	
	
		
КонецПроцедуры

// Используется для получения номеров счетов в виде массив строк
//
// Параметры:
//  Организация - <СправочникСсылка.Организации> - отбор по организации.
//  Банк - <СправочникСсылка.КлассификаторБанковРФ> - отбор по банку.
//  МассивНомеровБанковскихСчетов - массив возврата, в элементах строки с номерами счетов
//
Процедура ПолучитьНомераБанковскихСчетов(Организация, Банк, МассивНомеровБанковскихСчетов) Экспорт
	
	
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа Платежное поручение.
//
// Параметры: 
// СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
// СтруктураЭД - Структура, структура данных для формирования электронного документа.
// СтруктураПараметров - структура, параметры для заполнения.
//
Процедура ПодготовитьДанныеПоПлатежномуПоручению(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметров) Экспорт
		
	
	
КонецПроцедуры

// Заполняет соответствие исходящих видов электронных документов и представлений документов информационной базы,
// на основании которых они формируются.
//
// Параметры:
//  СоответствиеВидовЭДДокументамИБ - Соответствие - перечень видов электронных документов.
//    - Соответствие - с свойствами:
//    * Ключ             - Перечисление.ВидыЭД - вид электронного документа.
//    * Значение         - Строка - представление документа информационной базы (хоз. операции).
//
Процедура СоответствиеИсходящихВидовЭДДокументамИБ(СоответствиеВидовЭДДокументамИБ) Экспорт
	
	СоответствиеВидовЭДДокументамИБ.Вставить(Перечисления.ВидыЭД.ТОРГ12Продавец,
		"Реализация товаров и услуг (продажа, комиссия); Корректировка реализации (исправление)");
	СоответствиеВидовЭДДокументамИБ.Вставить(Перечисления.ВидыЭД.АктИсполнитель,
		"Реализация товаров и услуг (акт выполненных работ); Корректировка реализации (исправление)");
	СоответствиеВидовЭДДокументамИБ.Вставить(Перечисления.ВидыЭД.АктНаПередачуПрав,
		"Реализация товаров и услуг (акт на передачу прав); Корректировка реализации (исправление)");
	СоответствиеВидовЭДДокументамИБ.Вставить(Перечисления.ВидыЭД.СчетНаОплату,
		"Счет на оплату покупателю");
	СоответствиеВидовЭДДокументамИБ.Вставить(Перечисления.ВидыЭД.ЗаказТовара,
		"Заказ поставщику");
	СоответствиеВидовЭДДокументамИБ.Вставить(Перечисления.ВидыЭД.ОтветНаЗаказ,
		"Заказ покупателя");
	СоответствиеВидовЭДДокументамИБ.Вставить(Перечисления.ВидыЭД.СчетФактура,
		"Счет-фактура выданный (на реализацию)");
	СоответствиеВидовЭДДокументамИБ.Вставить(Перечисления.ВидыЭД.КорректировочныйСчетФактура,
		"Счет-фактура выданный (корректировочный)");
	СоответствиеВидовЭДДокументамИБ.Вставить(Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель,
		"Корректировка реализации (корректировка по согласованию сторон)");
	СоответствиеВидовЭДДокументамИБ.Вставить(Перечисления.ВидыЭД.КаталогТоваров,
		"Настройка ЭДО");
	СоответствиеВидовЭДДокументамИБ.Вставить(Перечисления.ВидыЭД.АктСверкиВзаиморасчетов,
		"Акт сверки взаиморасчетов");

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Поиск и создание документов

// Поиск и создание документа передачи товаров.
//
// Параметры:
//  ДеревоДанных		 - ДеревоЗначений - дерево данных электронного документа.
//  СсылкаНаВладельца	 - ДокументСсылка - ссылка на документ учета.
//  Записывать			 - Булево - признак записи документа.
//
Процедура НайтиСоздатьДокументПередачаТоваров(ДеревоДанных, СсылкаНаВладельца, Записывать = Истина, СпособОбработки = "") Экспорт
	
	ВидОперации = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВидОперации");
		
	Если ВидОперации = Перечисления.ВидыОперацийЭД.Исправление Тогда
		ИмяТипаДокумента = "КорректировкаПоступления";
	Иначе
		ИмяТипаДокумента = "ПоступлениеТоваровУслуг";
	КонецЕсли;
	
	ДанныеДокумента = Новый Структура("ВидДокумента", "ПередачаТоваров");
	ПрочитатьДанныеДокументаПередачи(ДеревоДанных, ДанныеДокумента, ИмяТипаДокумента);

	Попытка
		Если ЗначениеЗаполнено(СсылкаНаВладельца) Тогда // получены изменения по существующему документу
			Если СсылкаНаВладельца.Проведен Тогда 
				ВызватьИсключение НСтр("ru = 'Заполнение на основании ЭД возможно только для непроведенного документа'");
			КонецЕсли;
			ДокументОбъект = СсылкаНаВладельца.ПолучитьОбъект();
		Иначе  // создаем новый
			УстановитьПривилегированныйРежим(Истина);
			ДокументОбъект = Документы[ИмяТипаДокумента].СоздатьДокумент();
			ДанныеДокумента.Шапка.Свойство("Организация", ДокументОбъект.Организация);
			ДанныеДокумента.Шапка.Свойство("Контрагент", ДокументОбъект.Контрагент);
			ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ДокументОбъект, глЗначениеПеременной("глТекущийПользователь"), , "Покупка");
			ДокументОбъект.Дата = ТекущаяДатаСеанса();
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ДокументОбъект, ДанныеДокумента.Шапка);
		Если ВидОперации = Перечисления.ВидыОперацийЭД.Исправление Тогда
			Если ЗначениеЗаполнено(ДокументОбъект.ДокументПоступления) Тогда
				Попытка
					ДокументОбъект.Заполнить(ДокументОбъект.ДокументПоступления);
					ЗаполнитьТабличныеЧастиКорректировкиПоступления(ДокументОбъект, ДанныеДокумента);
				Исключение
					ДокументОбъект.Товары.Загрузить(ДанныеДокумента.Товары);
					ДокументОбъект.Услуги.Загрузить(ДанныеДокумента.Услуги);
				КонецПопытки;
			КонецЕсли;
		Иначе
			ДокументОбъект.Товары.Загрузить(ДанныеДокумента.Товары);
			ДокументОбъект.Услуги.Загрузить(ДанныеДокумента.Услуги);
		КонецЕсли;
		ДокументОбъект.СерийныеНомера.Загрузить(ДанныеДокумента.СерийныеНомера);
		Если ДокументОбъект.ЭтоНовый() Тогда
			ДокументОбъект.УстановитьНовыйНомер();
			ДокументОбъект.Дата = ТекущаяДатаСеанса();
		КонецЕсли;
		ПересчитатьСуммы(ДокументОбъект);
		Если Записывать Тогда
			ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
			СсылкаНаВладельца = ДокументОбъект.Ссылка;
		Иначе // если функция вызвана из формы "Обработки.ОбменСКонтрагентами.ФормаЗагрузкиПросмотраЭД"
			СсылкаНаВладельца = ДокументОбъект;
		КонецЕсли;
	Исключение
		ОписаниеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Заполнение на основании ЭД'"),
			УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы[ИмяТипаДокумента], 
			СсылкаНаВладельца, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение ОписаниеОшибки;
	КонецПопытки;

КонецПроцедуры

// Поиск и создание документа передачи результатов работ.
//
// Параметры:
//  ДеревоДанных		 - ДеревоЗначений - дерево данных электронного документа.
//  СсылкаНаВладельца	 - ДокументСсылка - ссылка на документ учета.
//  Записывать			 - Булево - признак записи документа.
//
Процедура НайтиСоздатьДокументПередачаРезультатовРабот(ДеревоДанных, СсылкаНаВладельца, Записывать = Истина, СпособОбработки = "") Экспорт
	
	ВидОперации = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВидОперации");
		
	Если ВидОперации = Перечисления.ВидыОперацийЭД.Исправление Тогда
		ИмяТипаДокумента = "КорректировкаПоступления";
	Иначе
		ИмяТипаДокумента = "ПоступлениеТоваровУслуг";
	КонецЕсли;
	
	ДанныеДокумента = Новый Структура("ВидДокумента", "ПередачаРезультатовРабот");
	ПрочитатьДанныеДокументаПередачи(ДеревоДанных, ДанныеДокумента, ИмяТипаДокумента);

	Попытка
		Если ЗначениеЗаполнено(СсылкаНаВладельца) Тогда // получены изменения по существующему документу
			Если СсылкаНаВладельца.Проведен Тогда 
				ВызватьИсключение НСтр("ru = 'Заполнение на основании ЭД возможно только для непроведенного документа'");
			КонецЕсли;
			ДокументОбъект = СсылкаНаВладельца.ПолучитьОбъект();
		Иначе  // создаем новый
			УстановитьПривилегированныйРежим(Истина);
			ДокументОбъект = Документы[ИмяТипаДокумента].СоздатьДокумент();
			ДанныеДокумента.Шапка.Свойство("Организация", ДокументОбъект.Организация);
			ДанныеДокумента.Шапка.Свойство("Контрагент", ДокументОбъект.Контрагент);
			ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ДокументОбъект, глЗначениеПеременной("глТекущийПользователь"), , "Покупка");
			ДокументОбъект.Дата = ТекущаяДатаСеанса();
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ДокументОбъект, ДанныеДокумента.Шапка);
		Если ВидОперации = Перечисления.ВидыОперацийЭД.Исправление Тогда
			Если ЗначениеЗаполнено(ДокументОбъект.ДокументПоступления) Тогда
				Попытка
					ДокументОбъект.Заполнить(ДокументОбъект.ДокументПоступления);
					ЗаполнитьТабличныеЧастиКорректировкиПоступления(ДокументОбъект, ДанныеДокумента);
				Исключение
					ДокументОбъект.Товары.Загрузить(ДанныеДокумента.Товары);
					ДокументОбъект.Услуги.Загрузить(ДанныеДокумента.Услуги);
				КонецПопытки;
			КонецЕсли;
		Иначе
			ДокументОбъект.Товары.Загрузить(ДанныеДокумента.Товары);
			ДокументОбъект.Услуги.Загрузить(ДанныеДокумента.Услуги);
		КонецЕсли;
		Если ДокументОбъект.ЭтоНовый() Тогда
			ДокументОбъект.УстановитьНовыйНомер();
			ДокументОбъект.Дата = ТекущаяДатаСеанса();
		КонецЕсли;
		ПересчитатьСуммы(ДокументОбъект);
		ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
		СсылкаНаВладельца = ДокументОбъект.Ссылка;
	Исключение
		ОписаниеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Заполнение на основании ЭД'"),
			УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы[ИмяТипаДокумента], 
			СсылкаНаВладельца, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение ОписаниеОшибки;
	КонецПопытки;

КонецПроцедуры


Функция ПолучитьОбъектТипаCML(Тип, ВерсияСхемы = "4.01") Экспорт
	
	
	
КонецФункции

// Вызывается при изменении состояния документооборота на ОбменЗавершен, ОбменЗавершенСИсправлением.
//
// Параметры:
//  ОснованияЭлектронногоДокумента - ДокументСсылка - ссылка на документ основания электронного документа.
//
Процедура УстановленоСостояниеОбменЗавершен(ОснованияЭлектронногоДокумента) Экспорт
		
КонецПроцедуры

// Выполняет проверку на возможность записи владельца электронного документа,
// необходимость создания электронного документа на основе владельца.
//
// Параметры
//  Объект - ДокументОбъект - Учетный документ ЭД
//  ИзменилисьКлючевыеРеквизиты - Булево - Признак изменения ключевых реквизитов владельца электронного документа.
//                                         Если Истина, то текущая версия электронного документа становится неактуальной.
//                                         По умолчанию для нового документа Истина, иначе Ложь.
//  СостояниеЭлектронногоДокумента - Перечисление.СостоянияВерсийЭД - состояние текущей версии электронного документа.
//  ПодлежитОбменуЭД - Булево - Признак участия документа в ЭДО.
//  Отказ - Булево - Признак отказа от записи владельца электронного документа.
//
Процедура ПередЗаписьюВладельцаЭлектронногоДокумента(Объект, ИзменилисьКлючевыеРеквизиты,
	Знач СостояниеЭлектронногоДокумента, ПодлежитОбменуЭД, Отказ) Экспорт
	
	// ПРОВЕРКА МОДИФИЦИРОВАННОСТИ КЛЮЧЕВЫХ РЕКВИЗИТОВ
	
	ИмяОбъекта = Объект.Метаданные().ПолноеИмя();
	СтруктураКлючевыхРеквизитов = Новый Структура;
	
	Если ИмяОбъекта = "Документ.РеализацияТоваровУслуг" Тогда

		// Реквизиты объекта
		СтрокаРеквизитовОбъекта = ("Дата, Номер, Организация, Контрагент");
		СтруктураКлючевыхРеквизитов.Вставить("Шапка", СтрокаРеквизитовОбъекта);
		
		// Табличная часть
		СтрокаРеквизитовОбъекта = ("Номенклатура, ХарактеристикаНоменклатуры, Количество, Цена, Сумма, СтавкаНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Товары", СтрокаРеквизитовОбъекта);
		СтрокаРеквизитовОбъекта = ("Номенклатура, Количество, Цена, Сумма, СтавкаНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Услуги", СтрокаРеквизитовОбъекта);
		
	ИначеЕсли ИмяОбъекта = "Документ.УдалитьПроизвольныйЭД" Тогда

		// Реквизиты объекта
		СтрокаРеквизитовОбъекта = ("Дата, Номер, Организация, Контрагент, Текст");
		СтруктураКлючевыхРеквизитов.Вставить("Шапка", СтрокаРеквизитовОбъекта);
		
	ИначеЕсли ИмяОбъекта = "Документ.ЗаказПоставщику" Тогда

		// Реквизиты объекта
		СтрокаРеквизитовОбъекта = ("Дата, Номер, Организация, Контрагент");
		СтруктураКлючевыхРеквизитов.Вставить("Шапка", СтрокаРеквизитовОбъекта);
		
		// Табличная часть
		СтрокаРеквизитовОбъекта = ("Номенклатура, ХарактеристикаНоменклатуры, Количество, Цена, Сумма, СтавкаНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Товары", СтрокаРеквизитовОбъекта);
		СтрокаРеквизитовОбъекта = ("Номенклатура, Содержание, Количество, Цена, Сумма, СтавкаНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Услуги", СтрокаРеквизитовОбъекта);
		
	ИначеЕсли ИмяОбъекта = "Документ.ЗаказПокупателя" Тогда  // для сравнения при загрузке

		// Реквизиты объекта
		СтрокаРеквизитовОбъекта = ("Организация, Контрагент");
		СтруктураКлючевыхРеквизитов.Вставить("Шапка", СтрокаРеквизитовОбъекта);
		
		// Табличная часть
		СтрокаРеквизитовОбъекта = ("Номенклатура, ХарактеристикаНоменклатуры, Количество, Цена, Сумма, СтавкаНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Товары", СтрокаРеквизитовОбъекта);
		СтрокаРеквизитовОбъекта = ("Номенклатура, Содержание, Количество, Цена, Сумма, СтавкаНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Услуги", СтрокаРеквизитовОбъекта);
		
	ИначеЕсли ИмяОбъекта = "Документ.СчетФактураВыданный" Тогда

		// Реквизиты объекта
		СтрокаРеквизитовОбъекта = ("Дата, Номер, Организация, СуммаДокумента, СуммаНДСДокумента");
		СтруктураКлючевыхРеквизитов.Вставить("Шапка", СтрокаРеквизитовОбъекта);
		
	ИначеЕсли ИмяОбъекта = "Документ.СчетНаОплатуПокупателю" Тогда

		// Реквизиты объекта
		СтрокаРеквизитовОбъекта = ("Дата, Номер, Организация, Контрагент");
		СтруктураКлючевыхРеквизитов.Вставить("Шапка", СтрокаРеквизитовОбъекта);
		
		// Табличная часть
		СтрокаРеквизитовОбъекта = ("Номенклатура, ХарактеристикаНоменклатуры, Количество, Цена, Сумма, СтавкаНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Товары", СтрокаРеквизитовОбъекта);
		СтрокаРеквизитовОбъекта = ("Номенклатура, Количество, Цена, Сумма, СтавкаНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Услуги", СтрокаРеквизитовОбъекта);
		
	ИначеЕсли ИмяОбъекта = "Документ.КорректировкаРеализации" Тогда

		// Реквизиты объекта
		СтрокаРеквизитовОбъекта = ("Дата, Номер, Организация, Контрагент");
		СтруктураКлючевыхРеквизитов.Вставить("Шапка", СтрокаРеквизитовОбъекта);
		
		// Табличная часть
		СтрокаРеквизитовОбъекта = ("Номенклатура, ХарактеристикаНоменклатуры, Количество, Цена, Сумма, СтавкаНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Товары", СтрокаРеквизитовОбъекта);
		СтрокаРеквизитовОбъекта = ("Номенклатура, Количество, Цена, Сумма, СтавкаНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Услуги", СтрокаРеквизитовОбъекта);
		
	ИначеЕсли ИмяОбъекта = "Документ.АктСверкиВзаиморасчетов" Тогда

		// Реквизиты объекта
		СтрокаРеквизитовОбъекта = ("Дата, Номер, Организация, Контрагент");
		СтруктураКлючевыхРеквизитов.Вставить("Шапка", СтрокаРеквизитовОбъекта);
		
		// Табличная часть
		СтрокаРеквизитовОбъекта = ("ДоговорКонтрагента, Сделка, Дебет, Кредит");
		СтруктураКлючевыхРеквизитов.Вставить("ПоДаннымОрганизации", СтрокаРеквизитовОбъекта);
		
	КонецЕсли;
	
	Если СтруктураКлючевыхРеквизитов.Количество() = 0 Тогда
		
		ШаблонСообщения = НСтр("ru = 'Не определена структура ключевых реквизитов для объекта %1.'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, ИмяОбъекта);
		ВызватьИсключение(ТекстСообщения);
		
	ИначеЕсли Не Объект.ЭтоНовый() Тогда
		
		// проверим ключевые реквизиты объекта на модификацию
		Для Каждого Реквизиты Из СтруктураКлючевыхРеквизитов Цикл
			Если Реквизиты.Ключ = "Шапка" Тогда
				МассивРеквизитов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Реквизиты.Значение, ", ");
				Для Каждого ИмяРеквизита Из МассивРеквизитов Цикл
					СтароеЗначение = Объект.Ссылка[ИмяРеквизита];
					НовоеЗначение  = Объект[ИмяРеквизита];
					Если ТипЗнч(СтароеЗначение) = Тип("Дата") Тогда
						СтароеЗначение = НачалоДня(СтароеЗначение);
						НовоеЗначение  = НачалоДня(НовоеЗначение);
					КонецЕсли;
					Если СтароеЗначение <> НовоеЗначение Тогда
						ИзменилисьКлючевыеРеквизиты = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			Иначе
				СтароеЗначение = Объект.Ссылка[Реквизиты.Ключ].Выгрузить();
				НовоеЗначение  = Объект[Реквизиты.Ключ].Выгрузить();
				ИзменилисьКлючевыеРеквизиты = НЕ ТаблицыРеквизитовОбъектовОдинаковые(
					СтароеЗначение,
					НовоеЗначение,
					Реквизиты.Значение);
			КонецЕсли;
			Если ИзменилисьКлючевыеРеквизиты Тогда
				Прервать
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	// ПРОВЕРКА ВОЗМОЖНОСТИ РЕДАКТИРОВАНИЯ ОБЪЕКТА
	
	РедактированиеРазрешено = Истина;
	Если ИзменилисьКлючевыеРеквизиты Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	1 КАК Поле
		|ИЗ
		|	РегистрСведений.СостоянияЭД КАК СостоянияЭД
		|ГДЕ
		|	СостоянияЭД.СсылкаНаОбъект = &СсылкаНаОбъект
		|	И НЕ СостоянияЭД.СостояниеВерсииЭД В (&СостояниеВерсииЭД)");
		
		СписокСостоянийЭД = Новый Массив;
		СписокСостоянийЭД.Добавить(Перечисления.СостоянияВерсийЭД.Отклонен);
		СписокСостоянийЭД.Добавить(Перечисления.СостоянияВерсийЭД.Аннулирован);
		СписокСостоянийЭД.Добавить(Перечисления.СостоянияВерсийЭД.ОжидаетсяАннулирование);
		СписокСостоянийЭД.Добавить(Перечисления.СостоянияВерсийЭД.НеСформирован);
		СписокСостоянийЭД.Добавить(Перечисления.СостоянияВерсийЭД.ЗакрытПринудительно);
		СписокСостоянийЭД.Добавить(Перечисления.СостоянияВерсийЭД.НаПодписи);
		
		Запрос.УстановитьПараметр("СсылкаНаОбъект",    Объект.Ссылка);
		Запрос.УстановитьПараметр("СостояниеВерсииЭД", СписокСостоянийЭД);
		
		РедактированиеРазрешено = Запрос.Выполнить().Пустой();
		Если Не РедактированиеРазрешено Тогда
			Отказ = Истина
		КонецЕсли;
		
	КонецЕсли;
	
	// ПРОВЕРКА НА НЕОБХОДИМОСТЬ СОЗДАНИЯ ЭД
	
	Если ТипЗнч(Объект) = Тип("ДокументОбъект.СчетФактураВыданный") Тогда
		// если СЧФ выставлен на бумаге, то ЭД не нужен
		ПодлежитОбменуЭД = Не (Объект.КодСпособаВыставления = 1)
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СостояниеЭлектронногоДокумента) Тогда
		ИзменилисьКлючевыеРеквизиты = Истина;
	КонецЕсли;
	
	МетаданныеОбъекта = Объект.Метаданные();
	
	Если ОбщегоНазначения.ЭтоДокумент(МетаданныеОбъекта) Тогда
		Если Объект.ПометкаУдаления Тогда
			ПодлежитОбменуЭД = Ложь;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры // ПередЗаписьюВладельцаЭлектронногоДокумента()

// Процедура - Переопределяет заполнение таблицы соответствия видов ЭД и способов обработки по имени профиля,
//			   который используется в форме настройки ЭДО на закладке "Входящие электронные документы".
//
// Параметры:
//  ТаблицаПрофиля	 - ТаблицаЗначений - таблица, содержащая соответствие видов электронных документов и способов обработки
//  Профиль			 - Строка - имя профиля настроек: "Автоматически", "Вручную", "Поставщик", "Покупатель".
//
Процедура ТаблицаПредопределенногоПрофиля(ТаблицаПрофиля,Профиль) Экспорт 
	
КонецПроцедуры

// Возвращает список имен документов доступных для создания при загрузке электронного документа.
//
// Параметры:
//  ВидЭД			 - Перечисление.ВидыЭД - вид электронного документа
//  СписокСпособовОбработки - Строка - список для добавления операций по отражению электронного документа.
//
Процедура СписокОперацийВидаЭД(ВидЭД,СписокСпособовОбработки) Экспорт 
	
	Если ВидЭД = Перечисления.ВидыЭД.ТОРГ12
		ИЛИ ВидЭД = Перечисления.ВидыЭД.ТОРГ12Продавец
		ИЛИ ВидЭД = Перечисления.ВидыЭД.АктИсполнитель Тогда
		
		СписокСпособовОбработки.Добавить("ПоступлениеТоваровУслуг", НСтр("ru = 'Поступление товаров услуг'"), Истина);
		
	ИначеЕсли ВидЭД = Перечисления.ВидыЭД.СчетФактура
		ИЛИ ВидЭД = Перечисления.ВидыЭД.КорректировочныйСчетФактура Тогда
		
		СписокСпособовОбработки.Добавить("СчетФактураПолученный", НСтр("ru = 'Счет-фактура полученный'"), Истина);
		СписокСпособовОбработки.Добавить("ВозвратТоваровОтПокупателя", НСтр("ru = 'Возврат товаров от покупателя'"), Истина);
		СписокСпособовОбработки.Добавить("КорректировкаПоступления", НСтр("ru = 'Корректировка поступления'"), Истина);
		
	ИначеЕсли ВидЭД = Перечисления.ВидыЭД.СчетНаОплату Тогда
		
		СписокСпособовОбработки.Добавить("СчетНаОплатуПоставщика", НСтр("ru = 'Счет на оплату поставщика'"), Истина);
		
	ИначеЕсли ВидЭД = Перечисления.ВидыЭД.ЗаказТовара Тогда
		
		СписокСпособовОбработки.Добавить("ЗаказПокупателя", НСтр("ru = 'Заказ покупателя'"), Истина);
		
	ИначеЕсли ВидЭД = Перечисления.ВидыЭД.ОтветНаЗаказ Тогда
		
		СписокСпособовОбработки.Добавить("ЗаказПоставщику", НСтр("ru = 'Заказ поставщику'"), Истина);
		
	ИначеЕсли ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель Тогда
		
		СписокСпособовОбработки.Добавить("КорректировкаПоступления", НСтр("ru = 'Корректировка поступления'"), Истина);
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура - переопределяет имя объекта метаданных для способа обработки входящего электронного документа,
//			   по-умолчанию совпадает с именем способа обработки.
//
// Параметры:
//  СпособОбработки - Строка - один из доступных способов обработки входящего документа
//  ИмяДокумента - Строка - переменная в которую будет помещено имя объекта метаданных, соответствующее переданному способу обработки.
//
Процедура ИмяДокументаПоСпособуОбработки(СпособОбработки,ИмяДокумента) Экспорт 
	
	
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Определяет является ли документ информационной базы счет-фактурой.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка - документ информационной базы.
//  Результат - Булево - является ли документ счет-фактурой.
//
Процедура ОпределитьДокументЯвляетсяСчетомФактурой(ДокументСсылка, Результат) Экспорт
	
	ТипДокумента = ТипЗнч(ДокументСсылка);
	Если ТипДокумента = Тип("ДокументСсылка.СчетФактураВыданный")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
		
		Результат = Истина;
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// НастройкиФормированияДокумента

// Выполняется при выводе формы настроек регламента ЭДО. Используется для получения возможных вариантов
// заполнения некоторых полей формализованных форматов.
//
// Параметры:
//  ВариантыЗаполненияПолей	 - Структура - описание полей и вариантов их заполнения. В качестве ключа задается идентификатор поля,
//     а в качестве значения - список значений возможных вариантов заполнения.
//     Возможные идентификаторы:
//       * УПД_ТоварКод - на входе содержит список значений: {"Код", "Внутренний код"}; {"Штрихкод", "Штрихкод"}
//       * ПередачаТоваров_ТоварКод - на входе содержит список значений: {"Код", "Внутренний код"}; {"Штрихкод", "Штрихкод"}
//
Процедура ПриОпределенииВариантовЗаполненияПолейЭлектронныхДокументов(ВариантыЗаполненияПолей) Экспорт
	
КонецПроцедуры


// Получает запросы, описывающие документы-источники данных для формирования электронного документа указанного вида.
// Запросы используются для указания пользователем формулы заполнения значений доп. полей по данным источника.
// Запрос должен включать в себя отбор по параметру Ссылка.
//
// Параметры:
//  Параметры - Структура - параметры электронного документа, для которого производится настройка.
//     * ВидЭлектронногоДокумента - ПеречислениеСсылка.ВидыЭД - вид электронного документа. Могут передаваться следующие виды документов:
//                                  АктИсполнитель, ТОРГ12Продавец, СчетФактура, КорректировочныйСчетФактура,
//                                  СоглашениеОбИзмененииСтоимостиОтправитель, АктНаПередачуПрав.
//     * ИспользоватьУПД - Булево - признак использования универсального передаточного документа.
//     * ИспользоватьУКД - Булево - признак использования универсального корректировочного документа.
//  ТекстЗапроса             - Строка - текст запроса, который должен быть использован в конструкторе доп. полей шапки.
//
Процедура ПолучитьЗапросКонструктораДополнительныхПолейШапки(Параметры, ТекстЗапроса) Экспорт

	Если (Параметры.ИспользоватьУПД И Параметры.ВидЭлектронногоДокумента = Перечисления.ВидыЭД.СчетФактура)
		ИЛИ (Параметры.ИспользоватьУКД И Параметры.ВидЭлектронногоДокумента = Перечисления.ВидыЭД.КорректировочныйСчетФактура) Тогда
		
		ТекстЗапроса = ЗапросКонструктораДополнительныхПолейШапкиУПД();
	
	ИначеЕсли Параметры.ВидЭлектронногоДокумента = Перечисления.ВидыЭД.СчетФактура
		ИЛИ Параметры.ВидЭлектронногоДокумента = Перечисления.ВидыЭД.КорректировочныйСчетФактура Тогда
		
		ТекстЗапроса = ЗапросКонструктораДополнительныхПолейШапкиСчетаФактуры();
		
	ИначеЕсли Параметры.ВидЭлектронногоДокумента = Перечисления.ВидыЭД.ТОРГ12Продавец
		ИЛИ Параметры.ВидЭлектронногоДокумента = Перечисления.ВидыЭД.АктИсполнитель
		ИЛИ Параметры.ВидЭлектронногоДокумента = Перечисления.ВидыЭД.АктНаПередачуПрав
		ИЛИ Параметры.ВидЭлектронногоДокумента = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель Тогда
		
		ТекстЗапроса = ЗапросКонструктораДополнительныхПолейШапкиПервичногоДокумента();
		
	КонецЕсли;

КонецПроцедуры

// Дополняет список типов колонки документов оснований
//
Функция ОписаниеТипаКолонкиДокументовОснований(ИсходноеОписаниеТипов) Экспорт
	Возврат Новый ОписаниеТипов(ИсходноеОписаниеТипов, "ДокументСсылка.ПлатежноеПоручениеВходящее");
КонецФункции

Функция ОпределитьНаименованиеСтраны(КодСтраны) Экспорт
	Результат = "";
	Если Не ПустаяСтрока(КодСтраны) Тогда
		Страна = Справочники.КлассификаторСтранМира.НайтиПоКоду(КодСтраны);
		Если ЗначениеЗаполнено(Страна) Тогда
			Результат = Страна.Наименование;
		КонецЕсли;
	КонецЕсли;
	Возврат Результат;
КонецФункции

Функция ТекстЗапросаТЧ_УПД_РеализацияИКорректировка(ЭтоКорректировка = Ложь)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	РеализацияТоваровУслуг.НомерСтроки КАК НомерСтроки,
	|	РеализацияТоваровУслуг.Номенклатура.Код КАК КодТовара,
	|	РеализацияТоваровУслуг.Номенклатура.Артикул КАК Артикул,
	|	РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
	|	РеализацияТоваровУслуг.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслуг.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
	|			ТОГДА РеализацияТоваровУслуг.КодТНВЭД.Код
	|		ИНАЧЕ """"
	|	 КОНЕЦ КАК КодВидаТовара,
	|	//КорректировкаРеализации ВЫБОР
	|	//КорректировкаРеализации	КОГДА РеализацияТоваровУслуг.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
	|	//КорректировкаРеализации		ТОГДА ВЫБОР
	|	//КорректировкаРеализации				КОГДА РеализацияТоваровУслуг.КодТНВЭДДоИзменения = ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка)
	|	//КорректировкаРеализации					ТОГДА РеализацияТоваровУслуг.КодТНВЭД.Код
	|	//КорректировкаРеализации				ИНАЧЕ РеализацияТоваровУслуг.КодТНВЭДДоИзменения.Код
	|	//КорректировкаРеализации			КОНЕЦ
	|	//КорректировкаРеализации	ИНАЧЕ """"
	|	//КорректировкаРеализации КОНЕЦ КАК КодВидаТовараДоИзменения,
	|	РеализацияТоваровУслуг.Количество КАК Количество,
	|	ВЫРАЗИТЬ(РеализацияТоваровУслуг.Сумма * &Курс / &Кратность КАК Число(15, 2)) КАК Сумма,
	|	ВЫРАЗИТЬ(РеализацияТоваровУслуг.Цена * &Курс / &Кратность КАК Число(15, 2)) КАК ЦенаЗаЕдиницуИзмерения,
	|	ПРЕДСТАВЛЕНИЕ(РеализацияТоваровУслуг.ХарактеристикаНоменклатуры) КАК НаименованиеХарактеристики,
	|	РеализацияТоваровУслуг.ХарактеристикаНоменклатуры КАК Характеристика,
	|	РеализацияТоваровУслуг.ЕдиницаИзмерения.ЕдиницаПоКлассификатору КАК ЕдиницаИзмерения,
	|	РеализацияТоваровУслуг.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код КАК ЕдиницаИзмеренияКод,
	|	РеализацияТоваровУслуг.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Наименование КАК ЕдиницаИзмеренияНаименование,
	|	РеализацияТоваровУслуг.Коэффициент КАК Коэффициент,
	|	РеализацияТоваровУслуг.СтавкаНДС КАК НалоговаяСтавка,
	|	ВЫРАЗИТЬ(РеализацияТоваровУслуг.СуммаНДС * &Курс / &Кратность КАК Число(15, 2)) КАК СуммаНалога,
	|	РеализацияТоваровУслуг.КоличествоМест КАК КоличествоМест,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.ЕдиницаИзмеренияМест.Коэффициент, 0) / РеализацияТоваровУслуг.Коэффициент КАК КоличествоВОдномМесте,
	|	РеализацияТоваровУслуг.Количество КАК МассаНетто,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслуг.КоличествоМест > 0
	|			ТОГДА РеализацияТоваровУслуг.КоличествоМест * РеализацияТоваровУслуг.ЕдиницаИзмеренияМест.Вес
	|		ИНАЧЕ РеализацияТоваровУслуг.Количество * РеализацияТоваровУслуг.ЕдиницаИзмерения.Вес
	|	КОНЕЦ КАК МассаБрутто,
	|	РеализацияТоваровУслуг.ЗаказПокупателя КАК ДокументОснование,
	|	РеализацияТоваровУслуг.КлючСвязи КАК КлючСвязи,
	|	ЛОЖЬ КАК Услуга,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслуг.ПрослеживаемыйТовар
	|			ТОГДА ВЫБОР
	|					КОГДА РеализацияТоваровУслуг.СтранаПроисхождения = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия)
	|						ТОГДА """"
	|					ИНАЧЕ РеализацияТоваровУслуг.СтранаПроисхождения.Код
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА РеализацияТоваровУслуг.СерияНоменклатуры.СтранаПроисхождения = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия)
	|					ТОГДА """"
	|				ИНАЧЕ РеализацияТоваровУслуг.СерияНоменклатуры.СтранаПроисхождения.Код
	|			КОНЕЦ
	|	КОНЕЦ КАК СтранаПроисхожденияКод,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслуг.ПрослеживаемыйТовар
	|			ТОГДА ВЫБОР
	|					КОГДА РеализацияТоваровУслуг.СтранаПроисхождения = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия)
	|						ТОГДА """"
	|					ИНАЧЕ РеализацияТоваровУслуг.СтранаПроисхождения.Наименование
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА РеализацияТоваровУслуг.СерияНоменклатуры.СтранаПроисхождения = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия)
	|					ТОГДА """"
	|				ИНАЧЕ РеализацияТоваровУслуг.СерияНоменклатуры.СтранаПроисхождения.Наименование
	|			КОНЕЦ
	|	КОНЕЦ КАК СтранаПроисхожденияНаименование,
	|	СерияНоменклатуры.НомерГТД.Код КАК ТаможеннаяДекларацияНомер,
	|	ВЫРАЗИТЬ(РеализацияТоваровУслуг.КодТНВЭД.ЕдиницаИзмерения.Код КАК Строка(4)) КАК ЕдиницаИзмеренияПрослеживаемостиКод,
	|	РеализацияТоваровУслуг.КодТНВЭД.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияПрослеживаемостиНаименование,
	|	РеализацияТоваровУслуг.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ИЗ
	|	//РеализацияТоваровУслуг Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслуг
	|	//КорректировкаРеализации Документ.КорректировкаРеализации.Товары КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка = &Ссылка
	|	//КорректировкаРеализации И (РеализацияТоваровУслуг.Сумма > 0 ИЛИ РеализацияТоваровУслуг.Количество > 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.НомерСтроки,
	|	РеализацияТоваровУслуг.Номенклатура.Код,
	|	РеализацияТоваровУслуг.Номенклатура.Артикул,
	|	ВЫБОР
	|		КОГДА НЕ РеализацияТоваровУслуг.Содержание ПОДОБНО """"
	|			ТОГДА ВЫРАЗИТЬ(РеализацияТоваровУслуг.Содержание КАК СТРОКА(1000))
	|		КОГДА НЕ РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА ВЫРАЗИТЬ(РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))
	|		ИНАЧЕ РеализацияТоваровУслуг.Номенклатура.Наименование
	|	КОНЕЦ,
	|	РеализацияТоваровУслуг.Номенклатура,
	|	"""",
	|	//КорректировкаРеализации """",
	|	РеализацияТоваровУслуг.Количество,
	|	ВЫРАЗИТЬ(РеализацияТоваровУслуг.Сумма * &Курс / &Кратность КАК Число(15, 2)) ,
	|	ВЫРАЗИТЬ(РеализацияТоваровУслуг.Цена * &Курс / &Кратность КАК Число(15, 2)) ,
	|	"""",
	|	НЕОПРЕДЕЛЕНО,
	|	РеализацияТоваровУслуг.Номенклатура.БазоваяЕдиницаИзмерения,
	|	РеализацияТоваровУслуг.Номенклатура.БазоваяЕдиницаИзмерения.Код,
	|	РеализацияТоваровУслуг.Номенклатура.БазоваяЕдиницаИзмерения.Наименование,
	|	1,
	|	РеализацияТоваровУслуг.СтавкаНДС,
	|	ВЫРАЗИТЬ(РеализацияТоваровУслуг.СуммаНДС * &Курс / &Кратность КАК Число(15, 2)) ,
	|	0,
	|	0,
	|	РеализацияТоваровУслуг.Количество,
	|	0,
	|	РеализацияТоваровУслуг.ЗаказПокупателя,
	|	-1,
	|	ИСТИНА,
	|	"""",
	|	"""",
	|	"""",
	|	"""",
	|	"""",
	|	""""
	|ИЗ
	|	//РеализацияТоваровУслуг Документ.РеализацияТоваровУслуг.Услуги КАК РеализацияТоваровУслуг
	|	//КорректировкаРеализации Документ.КорректировкаРеализации.Услуги КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка = &Ссылка
	|	//КорректировкаРеализации И (РеализацияТоваровУслуг.Сумма > 0 ИЛИ РеализацияТоваровУслуг.Количество > 0)
	|";
	
	Если ЭтоКорректировка Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//КорректировкаРеализации", "");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//РеализацияТоваровУслуг", "");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТЧ_УПД_ВозвратТоваровПоставщику(ИзНТТ = Ложь)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	УПДТЧ.НомерСтроки КАК НомерСтроки,
	|	УПДТЧ.Номенклатура.Код КАК ТоварКод,
	|	УПДТЧ.Номенклатура.Артикул КАК Артикул,
	|	УПДТЧ.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
	|	УПДТЧ.Номенклатура КАК Номенклатура,
	|	УПДТЧ.Количество КАК Количество,
	|	ВЫРАЗИТЬ(УПДТЧ.Сумма * &Курс / &Кратность КАК Число(15, 2)) КАК Сумма,
	|	ВЫРАЗИТЬ(УПДТЧ.Цена * &Курс / &Кратность КАК Число(15, 2)) КАК ЦенаЗаЕдиницуИзмерения,
	|	ПРЕДСТАВЛЕНИЕ(УПДТЧ.ХарактеристикаНоменклатуры) КАК НаименованиеХарактеристики,
	|	УПДТЧ.ХарактеристикаНоменклатуры КАК Характеристика,
	|	УПДТЧ.ЕдиницаИзмерения.ЕдиницаПоКлассификатору КАК ЕдиницаИзмерения,
	|	УПДТЧ.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код КАК ЕдиницаИзмеренияКод,
	|	УПДТЧ.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Наименование КАК ЕдиницаИзмеренияНаименование,
	|	УПДТЧ.Коэффициент КАК Коэффициент,
	|	УПДТЧ.СтавкаНДС КАК НалоговаяСтавка,
	|	ВЫРАЗИТЬ(УПДТЧ.СуммаНДС * &Курс / &Кратность КАК Число(15, 2)) КАК СуммаНалога,
	|	УПДТЧ.КоличествоМест КАК КоличествоМест,
	|	ЕСТЬNULL(УПДТЧ.ЕдиницаИзмеренияМест.Коэффициент, 0) / УПДТЧ.Коэффициент КАК КоличествоВОдномМесте,
	|	УПДТЧ.Количество КАК МассаНетто,
	|	ВЫБОР
	|		КОГДА УПДТЧ.КоличествоМест > 0
	|			ТОГДА УПДТЧ.КоличествоМест * УПДТЧ.ЕдиницаИзмеренияМест.Вес
	|		ИНАЧЕ УПДТЧ.Количество * УПДТЧ.ЕдиницаИзмерения.Вес
	|	КОНЕЦ КАК МассаБрутто,
	|	УПДТЧ.Заказ КАК ДокументОснование,
	|	УПДТЧ.КлючСвязи КАК КлючСвязи,
	|	ЛОЖЬ КАК Услуга,
	|	СерияНоменклатуры.СтранаПроисхождения.Код КАК СтранаПроисхожденияКод,
	|	СерияНоменклатуры.СтранаПроисхождения.Наименование КАК СтранаПроисхожденияНаименование,
	|	СерияНоменклатуры.НомерГТД.Код КАК ТаможеннаяДекларацияНомер
	|ИЗ
	|	Документ.ВозвратТоваровПоставщику.Товары КАК УПДТЧ
	|ГДЕ
	|	УПДТЧ.Ссылка = &Ссылка
	|";
	Если ИзНТТ Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Документ.ВозвратТоваровПоставщику", "Документ.ВозвратТоваровПоставщикуИзНТТ");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "УПДТЧ.Заказ", "ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаШапка_УПД()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	//РеализацияТоваровУслуг """" КАК ВидОперации,
	|	//КорректировкаРеализации ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЭД.Исправление) КАК ВидОперации,
	|	//ВозвратТоваровПоставщику """" КАК ВидОперации,
	|	//РеализацияТоваровУслуг ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка) КАК ИсправляемыйДокументРеализации,
	|	//КорректировкаРеализации ИсправляемыйДокументРеализации КАК ИсправляемыйДокументРеализации,
	|	//ВозвратТоваровПоставщику ЗНАЧЕНИЕ(Документ.ВозвратТоваровПоставщику.ПустаяСсылка) КАК ИсправляемыйДокументРеализации,
	|	//РеализацияТоваровУслуг Сделка КАК ДокументОснование,
	|	//РеализацияТоваровУслуг ВЫБОР КОГДА Сделка ССЫЛКА Документ.ЗаказПокупателя ТОГДА
	|	//РеализацияТоваровУслуг ВЫРАЗИТЬ(Сделка КАК Документ.ЗаказПокупателя).Представление
	|	//РеализацияТоваровУслуг КОГДА Сделка ССЫЛКА Документ.СчетНаОплатуПокупателю ТОГДА
	|	//РеализацияТоваровУслуг ВЫРАЗИТЬ(Сделка КАК Документ.СчетНаОплатуПокупателю).Представление КОНЕЦ КАК ДокументОснованиеНаименование,
	|	//РеализацияТоваровУслуг ВЫБОР КОГДА Сделка ССЫЛКА Документ.ЗаказПокупателя ТОГДА
	|	//РеализацияТоваровУслуг ВЫРАЗИТЬ(Сделка КАК Документ.ЗаказПокупателя).Номер
	|	//РеализацияТоваровУслуг КОГДА Сделка ССЫЛКА Документ.СчетНаОплатуПокупателю ТОГДА
	|	//РеализацияТоваровУслуг ВЫРАЗИТЬ(Сделка КАК Документ.СчетНаОплатуПокупателю).Номер КОНЕЦ КАК ДокументОснованиеНомер,
	|	//РеализацияТоваровУслуг ВЫБОР КОГДА Сделка ССЫЛКА Документ.ЗаказПокупателя ТОГДА
	|	//РеализацияТоваровУслуг ВЫРАЗИТЬ(Сделка КАК Документ.ЗаказПокупателя).Дата
	|	//РеализацияТоваровУслуг КОГДА Сделка ССЫЛКА Документ.СчетНаОплатуПокупателю ТОГДА
	|	//РеализацияТоваровУслуг ВЫРАЗИТЬ(Сделка КАК Документ.СчетНаОплатуПокупателю).Дата КОНЕЦ КАК ДокументОснованиеДата,
	|	//КорректировкаРеализации ДокументРеализации КАК ДокументОснование,
	|	//КорректировкаРеализации ВЫРАЗИТЬ(ДокументРеализации КАК Документ.РеализацияТоваровУслуг).Дата КАК ДокументОснованиеДата,
	|	//ВозвратТоваровПоставщику Сделка КАК ДокументОснование,
	|	//РеализацияТоваровУслуг ОперацияВыбытияМаркированныхТоваров КАК ОперацияВыбытияМаркированныхТоваров,
	|	//КорректировкаРеализации ОперацияВыбытияМаркированныхТоваров КАК ОперацияВыбытияМаркированныхТоваров,
	|	//ВозвратТоваровПоставщику 0 КАК ОперацияВыбытияМаркированныхТоваров,
	|	УПД.Номер КАК НомерДокумента,
	|	УПД.Дата КАК ДатаДокумента,
	|	УПД.Организация,
	|	УПД.Организация КАК ЮрФизЛицо,
	|	УПД.Организация КАК Поставщик,
	|	УПД.Контрагент КАК Контрагент,
	|	УПД.Организация КАК Руководители,
	|	//РеализацияТоваровУслуг УПД.АдресДоставки КАК АдресДоставки,
	|	//КорректировкаРеализации УПД.АдресДоставки КАК АдресДоставки,
	|	//ВозвратТоваровПоставщику """" КАК АдресДоставки,
	|	
	|	%ПолеГрузополучатель%
	|	
	|	%ПолеГрузоотправитель%
	|		
	|	УПД.БанковскийСчетОрганизации КАК БанковскийСчет,
	|	УПД.Контрагент КАК Покупатель,
	|	УПД.Контрагент КАК Плательщик,
	|	УПД.Сделка,
	|	УПД.ДоговорКонтрагента.Представление КАК ДоговорНаименование,
	|	УПД.ДоговорКонтрагента.Номер КАК ДоговорНомер,
	|	УПД.ДоговорКонтрагента.Дата КАК ДоговорДата,
	|	УПД.ДоговорКонтрагента.ВедениеВзаиморасчетов КАК ВедениеВзаиморасчетов,
	|	УПД.ДоговорКонтрагента.ВидДоговора КАК ВидДоговора,
	|	ОтветственныеЛица.ФизическоеЛицо КАК ОтветственноеЛицо,
	|	//РеализацияТоваровУслуг ОтпускРазрешил,
	|	//РеализацияТоваровУслуг ОтпускПроизвел,
	|	//КорректировкаРеализации ИсправляемыйДокументРеализации.ОтпускРазрешил КАК ОтпускРазрешил,
	|	//КорректировкаРеализации ИсправляемыйДокументРеализации.ОтпускПроизвел КАК ОтпускПроизвел,
	|   //ВозвратТоваровПоставщику ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК ОтпускРазрешил,
	|   //ВозвратТоваровПоставщику ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК ОтпускПроизвел,
	|	ПРЕДСТАВЛЕНИЕ(УПД.Подразделение) КАК Подразделение,
	|	&ВалютаРеглНаименование КАК ВалютаНаименование,
	|	УПД.КурсВзаиморасчетов КАК Курс,
	|	УПД.КратностьВзаиморасчетов КАК Кратность,
	|	УПД.УчитыватьНДС,
	|	УПД.СуммаВключаетНДС,
	|	&ВалютаРеглКод КАК ВалютаКод
	|ИЗ
	|	//РеализацияТоваровУслуг Документ.РеализацияТоваровУслуг КАК УПД
	|	//КорректировкаРеализации Документ.КорректировкаРеализации КАК УПД
	|	//ВозвратТоваровПоставщику Документ.ВозвратТоваровПоставщику КАК УПД
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтветственныеЛица.СрезПоследних(&ДатаСреза, СтруктурнаяЕдиница = &СтруктурнаяЕдиница) КАК ОтветственныеЛица
	|		ПО (ОтветственныеЛица.СтруктурнаяЕдиница = УПД.Склад)
	|ГДЕ
	|	УПД.Ссылка = &ТекущийДокумент";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ПодготовитьДанныеДляУПД_ПервичныйДокумент(СсылкаНаОбъект, ДанныеДокумента, ЗаполнениеКодаТовара)
	
	ДокументОбъект = СсылкаНаОбъект.ПолучитьОбъект();
	Если ДанныеДокумента = Неопределено Тогда
		ДанныеДокумента = Новый Структура();
	КонецЕсли;

	ВалютаРегламентированногоУчета = глЗначениеПеременной("ВалютаРегламентированногоУчета");
	
	// Шапка
	ЗапросШапка = Новый Запрос;
	ЗапросШапка.Текст = ТекстЗапросаШапка_УПД();
	
	ПолеГрузополучатель = "
	|	ВЫБОР
	|		КОГДА УПД.Грузополучатель = &ПустойКонтрагент
	|			ТОГДА УПД.Контрагент
	|		ИНАЧЕ УПД.Грузополучатель
	|	КОНЕЦ КАК Грузополучатель,";

	ПолеГрузоотправитель = "
	|	ВЫБОР
	|		КОГДА УПД.Грузоотправитель = &ПустойКонтрагент
	|			ТОГДА УПД.Организация
	|		ИНАЧЕ УПД.Грузоотправитель
	|	КОНЕЦ КАК Грузоотправитель,";
	
	// Табличные части
	ЗапросТЧ = Новый Запрос;
	
	ЗапросСН = Новый Запрос;
	ЗапросСН.Текст = 
	"ВЫБРАТЬ
	|	СерийныеНомера.КлючСвязи,
	|	СерийныеНомера.СерийныйНомер.Код КАК СерийныйНомер
	|ИЗ
	|	//РеализацияТоваровУслуг Документ.РеализацияТоваровУслуг.СерийныеНомера КАК СерийныеНомера
	|	//КорректировкаРеализации Документ.КорректировкаРеализации.СерийныеНомера КАК СерийныеНомера
	|	//ВозвратТоваровПоставщику Документ.ВозвратТоваровПоставщику.СерийныеНомера КАК СерийныеНомера
	|ГДЕ
	|	СерийныеНомера.Ссылка = &Ссылка";
	
	Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		ЗапросШапка.Текст = СтрЗаменить(ЗапросШапка.Текст, "//РеализацияТоваровУслуг", "");
		ЗапросТЧ.Текст = ТекстЗапросаТЧ_УПД_РеализацияИКорректировка();
		ЗапросСН.Текст = СтрЗаменить(ЗапросСН.Текст, "//РеализацияТоваровУслуг", "");
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
		ЗапросШапка.Текст = СтрЗаменить(ЗапросШапка.Текст, "//КорректировкаРеализации", "");
		ЗапросТЧ.Текст = ТекстЗапросаТЧ_УПД_РеализацияИКорректировка(Истина);
		ЗапросСН.Текст = СтрЗаменить(ЗапросСН.Текст, "//КорректировкаРеализации", "");
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
		ЗапросШапка.Текст = СтрЗаменить(ЗапросШапка.Текст, "//ВозвратТоваровПоставщику", "");
		ЗапросТЧ.Текст = ТекстЗапросаТЧ_УПД_ВозвратТоваровПоставщику();
		ЗапросСН.Текст = СтрЗаменить(ЗапросСН.Текст, "//ВозвратТоваровПоставщику", "");
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.ВозвратТоваровПоставщикуИзНТТ") Тогда
		ПолеГрузоотправитель = " &ПустойКонтрагент КАК Грузоотправитель, ";
		ЗапросШапка.Текст = СтрЗаменить(ЗапросШапка.Текст, "//ВозвратТоваровПоставщику", "");
		ЗапросШапка.Текст = СтрЗаменить(ЗапросШапка.Текст, "Документ.ВозвратТоваровПоставщику", "Документ.ВозвратТоваровПоставщикуИзНТТ");
		ЗапросТЧ.Текст = ТекстЗапросаТЧ_УПД_ВозвратТоваровПоставщику(Истина);
		ЗапросСН.Текст = СтрЗаменить(ЗапросСН.Текст, "//ВозвратТоваровПоставщику", "");
		ЗапросСН.Текст = СтрЗаменить(ЗапросСН.Текст, "Документ.ВозвратТоваровПоставщику", "Документ.ВозвратТоваровПоставщикуИзНТТ");
	КонецЕсли;

	ЗапросШапка.Текст = СтрЗаменить(ЗапросШапка.Текст, "%ПолеГрузополучатель%", ПолеГрузополучатель);
	ЗапросШапка.Текст = СтрЗаменить(ЗапросШапка.Текст, "%ПолеГрузоотправитель%", ПолеГрузоотправитель);

	ЗапросШапка.УстановитьПараметр("ДатаСреза",              СсылкаНаОбъект.Дата);
	ЗапросШапка.УстановитьПараметр("СтруктурнаяЕдиница",     СсылкаНаОбъект.Склад);
	ЗапросШапка.УстановитьПараметр("ТекущийДокумент",        СсылкаНаОбъект);
	ЗапросШапка.УстановитьПараметр("ПустойКонтрагент",       Справочники.Контрагенты.ПустаяСсылка());
	ЗапросШапка.УстановитьПараметр("Организация",            СсылкаНаОбъект.Организация);
	ЗапросШапка.УстановитьПараметр("Подразделение",          СсылкаНаОбъект.Подразделение);
	ЗапросШапка.УстановитьПараметр("ВалютаРеглНаименование", ВалютаРегламентированногоУчета.НаименованиеПолное);
	ЗапросШапка.УстановитьПараметр("ВалютаРеглКод",          ВалютаРегламентированногоУчета.Код);
	
	Шапка = ЗапросШапка.Выполнить().Выбрать();
	Шапка.Следующий();
	
	ДанныеДокумента.Вставить("РеквизитыШапки", Шапка);

	ЗапросТЧ.УстановитьПараметр("Ссылка", СсылкаНаОбъект);
	УниверсальныеМеханизмы.ОпределитьКурсыДокументаДляПечати(ДокументОбъект, ЗапросТЧ);
	
	ТаблицаНоменклатуры = ЗапросТЧ.Выполнить().Выгрузить();
	ТаблицаНоменклатуры.Колонки.Добавить("ИдТовараУКонтрагента");
	ТаблицаНоменклатуры.Колонки.Добавить("ТоварИдентификатор");
	ТаблицаНоменклатуры.Колонки.Добавить("СтоимостьТоваровСНалогом");
	ТаблицаНоменклатуры.Колонки.Добавить("СтоимостьТоваровБезНалога");
	ТаблицаНоменклатуры.Колонки.Добавить("ТекстоваяИнформация");
	ТаблицаНоменклатуры.Колонки.Добавить("СведенияОТаможеннойДекларации");
	ТаблицаНоменклатуры.Колонки.Добавить("Признак");
	ТаблицаНоменклатуры.Колонки.Добавить("Сопоставление");
	ТаблицаНоменклатуры.Колонки.Добавить("ДопДанныеПодписанные");
	ТаблицаНоменклатуры.Колонки.Добавить("ТоварКод", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(255)));
	ТаблицаНоменклатуры.Колонки.Добавить("ТоварАртикул", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(255)));
	ТаблицаНоменклатуры.Колонки.Добавить("СведенияОПрослеживаемости");
	ТаблицаНоменклатуры.Колонки.Добавить("СведенияОДокументеОтгрузки");
	ТаблицаНоменклатуры.Колонки.Добавить("Ссылка");
	
    // Получим таблицу серийных номеров
	ЗапросСН.УстановитьПараметр("Ссылка", СсылкаНаОбъект);
	
	ТаблицаСН = ЗапросСН.Выполнить().Выгрузить();
	
	// Сведения о прослеживаемости
	СведенияОПрослеживаемости = ПолучитьСведенияОПрослеживаемости(СсылкаНаОбъект, ДанныеДокумента.РеквизитыШапки);
	ДанныеПрослеживаемости   = НовыйДанныеПрослеживаемости(СсылкаНаОбъект, ДанныеДокумента.РеквизитыШапки);

	ТД = Новый ТаблицаЗначений;
	ТД.Колонки.Добавить("СтранаПроисхожденияКод");
	ТД.Колонки.Добавить("ТаможеннаяДекларацияНомер");

	Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.ВозвратТоваровПоставщику") ИЛИ
		ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.ВозвратТоваровПоставщикуИзНТТ") Тогда
		ВидОперацииДокументаИБ = Перечисления.ВидыОперацийРеализацияТоваров.ПродажаКомиссия;
	Иначе
		ВидОперацииДокументаИБ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаОбъект, "ВидОперации");
		Если ВидОперацииДокументаИБ <> Перечисления.ВидыОперацийРеализацияТоваров.АктНаПередачуПрав Тогда
			Если ВидОперацииДокументаИБ = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.ИсправлениеОшибки Тогда
				ДокументРеализации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаОбъект, "ДокументРеализации");
				Если ДокументРеализации <> Неопределено И ТипЗнч(ДокументРеализации) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
					ВидОперацииДокументаИБ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументРеализации, "ВидОперации");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Маркировка = Неопределено;
	Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.РеализацияТоваровУслуг")
		Или ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.КорректировкаРеализации")
		Или ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.ВозвратТоваровПоставщику")Тогда
		Запрос = Новый Запрос(СтрШаблон("ВЫБРАТЬ Т.Ссылка, Т.ШтрихкодУпаковки КАК Штрихкод Из Документ.%1.ШтрихкодыУпаковок КАК Т ГДЕ Т.Ссылка = &Ссылка", СсылкаНаОбъект.Метаданные().Имя));
		Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъект);
		ТаблицаКодовМаркировки = Запрос.Выполнить().Выгрузить();
		Запрос = Новый Запрос(СтрШаблон("ВЫБРАТЬ Т.Ссылка КАК Ссылка,
							|	Т.Номенклатура КАК Номенклатура,
							|	Т.ХарактеристикаНоменклатуры КАК Характеристика,
							|	СУММА(Т.Количество * Т.Коэффициент) КАК Количество
							|Из Документ.%1.Товары КАК Т
							|ГДЕ Т.Ссылка = &Ссылка
							|СГРУППИРОВАТЬ ПО
							|	Т.Ссылка,
							|   Т.Номенклатура,
							|	Т.ХарактеристикаНоменклатуры", СсылкаНаОбъект.Метаданные().Имя));
		Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъект);
		ТаблицаТоварыДокумента = Запрос.Выполнить().Выгрузить();
		ТаблицаКодовМаркировки = ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимоеИКодыОСУ(ТаблицаКодовМаркировки, ТаблицаТоварыДокумента, ШтрихкодированиеИС.ПараметрыСканирования(СсылкаНаОбъект));
		ТаблицаКодовМаркировки.Колонки.Удалить("Ссылка");
		ТаблицаКодовМаркировки.Колонки.Добавить("Ссылка");
	КонецЕсли;
	ТаблицаНоменклатуры.Колонки.Добавить("СведенияОМаркировке");
	
	Если СсылкаНаОбъект.Метаданные().Реквизиты.Найти("ДоговорКонтрагента") <> Неопределено Тогда
		
		Если СсылкаНаОбъект.ДоговорКонтрагента.ВалютаВзаиморасчетов <> ВалютаРегламентированногоУчета Тогда

			СтруктураШапкиДокумента = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(СсылкаНаОбъект, "Организация, Контрагент, ДоговорКонтрагента");
			СуммаВзаиморасчетов = УчетНДСФормированиеДвижений.ПолучитьСуммуВзаиморасчетов(Документы.СчетФактураВыданный.ПустаяСсылка(), СсылкаНаОбъект, СтруктураШапкиДокумента);
			ТаблицаНоменклатуры.Колонки.Добавить("СуммаБезНДС");
			МассивРаспределения = Новый Массив;
			
			Для Каждого СтрокаСчетаФактуры Из ТаблицаНоменклатуры Цикл
				СуммаСНДС = СтрокаСчетаФактуры.Сумма + ?(Шапка.СуммаВключаетНДС, 0, СтрокаСчетаФактуры.СуммаНалога);
				СуммаБезНДС = СуммаСНДС - СтрокаСчетаФактуры.СуммаНалога;
				МассивРаспределения.Добавить(СуммаСНДС);
				СтрокаСчетаФактуры.СуммаБезНДС = СуммаБезНДС;
			КонецЦикла;
	
			ТаблицаНоменклатуры.Колонки.Добавить("СуммаРублевая");
			УчетНДС.РаспределитьСуммуПоСтолбцу(МассивРаспределения, СуммаВзаиморасчетов, ТаблицаНоменклатуры, "СуммаРублевая");
			РасчетСуммыНДСПоСтавке = УчетНДС.РасчетНДСвРубляхПоСтавкеДокумента(СсылкаНаОбъект.Дата);
	
			Для Каждого СтрокаСчетаФактуры Из ТаблицаНоменклатуры Цикл
				Если РасчетСуммыНДСПоСтавке Тогда
					//Выделение суммы НДС, Расчет суммы без НДС
					ЗначениеСтавкиНДС = УчетНДС.ПолучитьСтавкуНДС(СтрокаСчетаФактуры.НалоговаяСтавка);
					СтрокаСчетаФактуры.СуммаНалога = ?(ЗначениеСтавкиНДС = 0, 0, Окр(СтрокаСчетаФактуры.СуммаРублевая * ЗначениеСтавкиНДС/(100+ЗначениеСтавкиНДС),2));
					СтрокаСчетаФактуры.Сумма = СтрокаСчетаФактуры.СуммаРублевая - СтрокаСчетаФактуры.СуммаНалога;
			
				Иначе
					МассивРаспределения.Очистить();
					МассивРаспределения.Добавить(СтрокаСчетаФактуры.СуммаБезНДС);
					МассивРаспределения.Добавить(СтрокаСчетаФактуры.СуммаНалога);
					МассивРаспределенныхСумм = ОбщегоНазначения.РаспределитьПропорционально(СтрокаСчетаФактуры.СуммаРублевая, МассивРаспределения);
					Если МассивРаспределенныхСумм <> Неопределено Тогда
						СтрокаСчетаФактуры.Сумма = МассивРаспределенныхСумм[0];
						СтрокаСчетаФактуры.СуммаНалога = МассивРаспределенныхСумм[1];
					КонецЕсли;
				КонецЕсли;
		
				Если Шапка.СуммаВключаетНДС Тогда
					СтрокаСчетаФактуры.Сумма = СтрокаСчетаФактуры.Сумма+СтрокаСчетаФактуры.СуммаНалога;
				КонецЕсли;

			КонецЦикла;
			
			ТаблицаНоменклатуры.Колонки.Удалить(ТаблицаНоменклатуры.Колонки.СуммаБезНДС);
			ТаблицаНоменклатуры.Колонки.Удалить(ТаблицаНоменклатуры.Колонки.СуммаРублевая);
		КонецЕсли;
		
	КонецЕсли;
	
	Для Каждого СтрокаНоменклатуры Из ТаблицаНоменклатуры Цикл
		
		// ТоварНаименование
		Если Не ПустаяСтрока(СтрокаНоменклатуры.НаименованиеХарактеристики) Тогда
			СтрокаНоменклатуры.ТоварНаименование = СтрокаНоменклатуры.ТоварНаименование +
				" (" + СтрокаНоменклатуры.НаименованиеХарактеристики + ")";
		КонецЕсли;
		
		СтрокаНоменклатуры.ТоварКод = СокрЛП(СтрокаНоменклатуры.КодТовара);
		СтрокаНоменклатуры.ТоварАртикул = СокрЛП(СтрокаНоменклатуры.Артикул);
		
		// ИдТовараУКонтрагента
		ИДТовара = СтрокаНоменклатуры.Номенклатура.УникальныйИдентификатор();
		ИДХарактеристики = ?(ЗначениеЗаполнено(СтрокаНоменклатуры.Характеристика), СтрокаНоменклатуры.Характеристика.УникальныйИдентификатор(), "");
		ИДУпаковки = "";
		СтрокаНоменклатуры.ИдТовараУКонтрагента = Строка(ИДТовара)+"#"+Строка(ИДХарактеристики)+"#"+Строка(ИДУпаковки);
		// ТоварИдентификатор
		СтрокаНоменклатуры.ТоварИдентификатор = Строка(ИДТовара);
		// СтоимостьТоваровСНалогом
		Если Шапка.СуммаВключаетНДС Тогда
			СтрокаНоменклатуры.СтоимостьТоваровСНалогом  = СтрокаНоменклатуры.Сумма;
			СтрокаНоменклатуры.СтоимостьТоваровБезНалога = СтрокаНоменклатуры.Сумма - СтрокаНоменклатуры.СуммаНалога;
		Иначе
			СтрокаНоменклатуры.СтоимостьТоваровСНалогом = СтрокаНоменклатуры.Сумма + СтрокаНоменклатуры.СуммаНалога;
			СтрокаНоменклатуры.СтоимостьТоваровБезНалога = СтрокаНоменклатуры.Сумма;
		КонецЕсли;
		Если СтрокаНоменклатуры.Количество <> 0 Тогда
			СтрокаНоменклатуры.ЦенаЗаЕдиницуИзмерения = Окр(СтрокаНоменклатуры.СтоимостьТоваровБезНалога/СтрокаНоменклатуры.Количество,2);
		КонецЕсли;
		// Серийные номера
		СтруктураОтбораСН = Новый Структура("КлючСвязи", СтрокаНоменклатуры.КлючСвязи);
		СтрокиСН = ТаблицаСН.НайтиСтроки(СтруктураОтбораСН);
		Если СтрокиСН.Количество() > 0 Тогда
			СерийныеНомера = Новый ТаблицаЗначений;
			СерийныеНомера.Колонки.Добавить("Идентификатор");
			СерийныеНомера.Колонки.Добавить("Значение");
			Для Каждого СтрокаСН Из СтрокиСН Цикл
				СтрокаТаблицы = СерийныеНомера.Добавить();
				СтрокаТаблицы.Идентификатор = "СерийныйНомер";
				СтрокаТаблицы.Значение = СтрокаСН.СерийныйНомер;
			КонецЦикла;
			СтрокаНоменклатуры.ТекстоваяИнформация = СерийныеНомера.Скопировать();
		КонецЕсли;
		
		// Сведения о прослеживаемости
		Если СведенияОПрослеживаемости <> Неопределено Тогда
			СтруктураОтбора = Новый Структура("ИдентификаторСтроки", СтрокаНоменклатуры.ИдентификаторСтроки);
			СтрокиПрослеживаемости = СведенияОПрослеживаемости.НайтиСтроки(СтруктураОтбора);
			Если СтрокиПрослеживаемости.Количество() > 0 Тогда
				ДанныеПрослеживаемости.Очистить();
				Для Каждого Строка Из СтрокиПрослеживаемости Цикл
					НовСтрока = ДанныеПрослеживаемости.Добавить();
					ЗаполнитьЗначенияСвойств(НовСтрока, Строка);
					
					НовСтрока.НомерТовара = СокрЛП(Строка.НомерТовара);
					
					Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
						НовСтрока.Количество = Строка.КоличествоТовара;
						НовСтрока.КоличествоУчетное = Строка.КоличествоТовараУчетноеПослеИзменения;
					КонецЕсли;
					
					НовСтрока.ЕдиницаИзмеренияКод = СтрокаНоменклатуры.ЕдиницаИзмеренияПрослеживаемостиКод;
					НовСтрока.ЕдиницаИзмеренияНаименование = СтрокаНоменклатуры.ЕдиницаИзмеренияПрослеживаемостиНаименование;
					НовСтрока.СтоимостьБезНДС = ?(ЗначениеЗаполнено(Строка.СтоимостьБезНалога),
													Строка.СтоимостьБезНалога,
													НовСтрока.Количество * СтрокаНоменклатуры.ЦенаЗаЕдиницуИзмерения);
				КонецЦикла;
				СтрокаНоменклатуры.СведенияОПрослеживаемости = ДанныеПрослеживаемости.Скопировать();
			КонецЕсли;
		КонецЕсли;

		Если ЗначениеЗаполнено(СтрокаНоменклатуры.ТаможеннаяДекларацияНомер)
			Или ЗначениеЗаполнено(СтрокаНоменклатуры.СтранаПроисхожденияКод)Тогда
			ТД.Очистить();
			СтрокаТД = ТД.Добавить();
			СтрокаТД.СтранаПроисхожденияКод = СокрЛП(СтрокаНоменклатуры.СтранаПроисхожденияКод);
			СтрокаТД.ТаможеннаяДекларацияНомер = СокрЛП(СтрокаНоменклатуры.ТаможеннаяДекларацияНомер);
			СтрокаНоменклатуры.СведенияОТаможеннойДекларации = ТД.Скопировать();
		Иначе
			СтрокаНоменклатуры.СтранаПроисхожденияКод = "";
			СтрокаНоменклатуры.СтранаПроисхожденияНаименование = "";
		КонецЕсли;
		
		Если ВидОперацииДокументаИБ = Перечисления.ВидыОперацийРеализацияТоваров.АктНаПередачуПрав Тогда
			СтрокаНоменклатуры.Признак = "4";
		Иначе
			СтрокаНоменклатуры.Признак = ?(СтрокаНоменклатуры.Услуга, "3", "1");
		КонецЕсли;
		
		Штрихкоды = ШтрихкодыНоменклатуры(СтрокаНоменклатуры.Номенклатура);
		
		Если ЗаполнениеКодаТовара = "Артикул" Тогда
			СтрокаНоменклатуры.ТоварКод = СтрокаНоменклатуры.Артикул;
		ИначеЕсли ЗаполнениеКодаТовара = "Штрихкод" Тогда
			СтрокаНоменклатуры.ТоварКод = ?(ЗначениеЗаполнено(Штрихкоды), Штрихкоды[0], "");
		КонецЕсли;
		
		Сопоставление = СопоставлениеНоменклатуры();
		Сопоставление.Идентификатор       = СтрокаНоменклатуры.ИдТовараУКонтрагента;
		Сопоставление.Наименование        = СтрокаНоменклатуры.ТоварНаименование;
		Сопоставление.Характеристика      = СтрокаНоменклатуры.НаименованиеХарактеристики;
		Сопоставление.ЕдиницаИзмерения    = СтрокаНоменклатуры.ЕдиницаИзмеренияНаименование;
		Сопоставление.ЕдиницаИзмеренияКод = СтрокаНоменклатуры.ЕдиницаИзмеренияКод;
		Сопоставление.Артикул             = СтрокаНоменклатуры.Артикул;
		Сопоставление.СтавкаНДС           = СтрокаНоменклатуры.НалоговаяСтавка;
		Сопоставление.НоменклатураИБ      = СтрокаНоменклатуры.Номенклатура;
		Сопоставление.ХарактеристикаИБ    = СтрокаНоменклатуры.Характеристика;
		Сопоставление.УпаковкаИБ          = СтрокаНоменклатуры.ЕдиницаИзмерения;
		
		Если ЗначениеЗаполнено(Штрихкоды) Тогда
			Сопоставление.Штрихкод = Штрихкоды;
		КонецЕсли;
		
		СтрокаНоменклатуры.Сопоставление = Сопоставление;
		
		ДопДанныеПодписанные = Новый Соответствие;
		ДопДанныеПодписанные.Вставить("ИД", СтрокаНоменклатуры.ИдТовараУКонтрагента);
		СтрокаНоменклатуры.ДопДанныеПодписанные = ДопДанныеПодписанные;
		
		Если ТаблицаКодовМаркировки<>Неопределено Тогда 
			ЭлектронноеВзаимодействиеИСМП.ЗаполнитьСведенияОМаркировке_2019(СтрокаНоменклатуры, СтрокаНоменклатуры, ТаблицаКодовМаркировки);
		КонецЕсли;
		
		// Сведения о документе отгрузки
		СведенияОДокументеОтгрузки = Новый Структура("Номер,Дата");
		Если ДанныеДокумента.Свойство("НомерСФ") Тогда
			СведенияОДокументеОтгрузки.Номер = ДанныеДокумента.НомерСФ;
			СведенияОДокументеОтгрузки.Дата = ДанныеДокумента.ДатаСФ;
		Иначе
			СведенияОДокументеОтгрузки.Номер = ПолучитьПечатныйНомерДокумента(СсылкаНаОбъект);
			СведенияОДокументеОтгрузки.Дата  = Шапка.ДатаДокумента;
		КонецЕсли;
		СтрокаНоменклатуры.СведенияОДокументеОтгрузки = СведенияОДокументеОтгрузки;
		
		СтрокаНоменклатуры.Ссылка = СсылкаНаОбъект;
		
	КонецЦикла;
	
	ДанныеДокумента.Вставить("ТаблицаНоменклатуры", ТаблицаНоменклатуры);

КонецПроцедуры

Функция ШтрихкодыНоменклатуры(Номенклатура)
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ 
	|	ШтрихкодыНоменклатуры.Штрихкод КАК Штрихкод
	|ИЗ
	|	РегистрСведений.Штрихкоды КАК ШтрихкодыНоменклатуры
	|ГДЕ
	|	ВЫРАЗИТЬ(ШтрихкодыНоменклатуры.Владелец КАК Справочник.Номенклатура) = &Номенклатура
	|
	|УПОРЯДОЧИТЬ ПО
	|	Штрихкод";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Результат = ОбщегоНазначения.ВыгрузитьКолонку(РезультатЗапроса.Выгрузить(), "Штрихкод");
	КонецЕсли; 
	
	Возврат Результат;
	
КонецФункции

Функция СопоставлениеНоменклатуры()
	
	Сопоставление = Новый Структура;
	Сопоставление.Вставить("Идентификатор");
	Сопоставление.Вставить("Наименование");
	Сопоставление.Вставить("Характеристика");
	Сопоставление.Вставить("ЕдиницаИзмерения");
	Сопоставление.Вставить("ЕдиницаИзмеренияКод");
	Сопоставление.Вставить("Артикул");
	Сопоставление.Вставить("СтавкаНДС");
	Сопоставление.Вставить("Штрихкод");
	Сопоставление.Вставить("НоменклатураИБ");
	Сопоставление.Вставить("ХарактеристикаИБ");
	Сопоставление.Вставить("УпаковкаИБ");
	
	Возврат Сопоставление;
	
КонецФункции

Функция НовыйДанныеПрослеживаемости(СсылкаНаОбъект, ДанныеДокумента)
	
	ДанныеПрослеживаемости = Новый ТаблицаЗначений;

	Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.КорректировкаРеализации") И ДанныеДокумента.ВидОперации <> Перечисления.ВидыОперацийЭД.Исправление Тогда
		ДанныеПрослеживаемости.Колонки.Добавить("НомерТовара", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(29)));
		ДанныеПрослеживаемости.Колонки.Добавить("ЕдиницаИзмеренияКод", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(4)));
		ДанныеПрослеживаемости.Колонки.Добавить("КоличествоТовараДо", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(26,11)));
		ДанныеПрослеживаемости.Колонки.Добавить("КоличествоТовара", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(26,11)));
		ДанныеПрослеживаемости.Колонки.Добавить("КоличествоТовараУвеличение", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(26,11)));
		ДанныеПрослеживаемости.Колонки.Добавить("КоличествоТовараУменьшение", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(26,11)));
		ДанныеПрослеживаемости.Колонки.Добавить("КоличествоТовараУчетноеДоИзменения",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
		ДанныеПрослеживаемости.Колонки.Добавить("КоличествоТовараУчетноеПослеИзменения", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
		ДанныеПрослеживаемости.Колонки.Добавить("КоличествоТовараУчетноеУвеличение",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
		ДанныеПрослеживаемости.Колонки.Добавить("КоличествоТовараУчетноеУменьшение", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
		ДанныеПрослеживаемости.Колонки.Добавить("СтоимостьБезНДСДоИзменения",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
		ДанныеПрослеживаемости.Колонки.Добавить("СтоимостьБезНДСПослеИзменения",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
		ДанныеПрослеживаемости.Колонки.Добавить("СтоимостьБезНДСУвеличение",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
		ДанныеПрослеживаемости.Колонки.Добавить("СтоимостьБезНДСУменьшение",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
	Иначе
		ДанныеПрослеживаемости.Колонки.Добавить("НомерТовара", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(29)));
		ДанныеПрослеживаемости.Колонки.Добавить("ЕдиницаИзмеренияКод", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(4)));
		ДанныеПрослеживаемости.Колонки.Добавить("ЕдиницаИзмеренияНаименование", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(255)));
		ДанныеПрослеживаемости.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(26,11)));
		ДанныеПрослеживаемости.Колонки.Добавить("КоличествоУчетное",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
		ДанныеПрослеживаемости.Колонки.Добавить("СтоимостьБезНДС",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
	КонецЕсли;
	
	Возврат ДанныеПрослеживаемости;
	
КонецФункции

Функция ПолучитьСведенияОПрослеживаемости(Ссылка, ДанныеДокумента)
	
	Запрос = Новый Запрос;
	
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		
		Запрос.Текст = "ВЫБРАТЬ
		               |	РеализацияТоваровУслугСведенияПрослеживаемости.КоличествоПрослеживаемости КАК Количество,
		               |	РеализацияТоваровУслугСведенияПрослеживаемости.Количество КАК КоличествоУчетное,
		               |	ВЫРАЗИТЬ(РеализацияТоваровУслугСведенияПрослеживаемости.РНПТ.Код КАК СТРОКА(29)) КАК НомерТовара,
		               |	РеализацияТоваровУслугСведенияПрослеживаемости.ИдентификаторСтроки КАК ИдентификаторСтроки,
		               |	РеализацияТоваровУслугСведенияПрослеживаемости.Сумма КАК СтоимостьБезНалога
		               |ИЗ
		               |	Документ.РеализацияТоваровУслуг.СведенияПрослеживаемости КАК РеализацияТоваровУслугСведенияПрослеживаемости
		               |ГДЕ
		               |	РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка = &Ссылка";
		
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
		Запрос.Текст = "ВЫБРАТЬ
		               |	ВЫРАЗИТЬ(СведенияПрослеживаемостиПосле.РНПТ.Код КАК СТРОКА(29)) КАК НомерТовара,
		               |	СведенияПрослеживаемостиПосле.ИдентификаторСтроки КАК ИдентификаторСтроки,
		               |	СведенияПрослеживаемостиПосле.Количество КАК КоличествоТовараУчетноеПослеИзменения,
		               |	СведенияПрослеживаемостиПосле.КоличествоПрослеживаемости КАК КоличествоТовара,
		               |	СведенияПрослеживаемостиПосле.РНПТ КАК РНПТ,
		               |	СведенияПрослеживаемостиДо.Количество КАК КоличествоТовараУчетноеДоИзменения,
		               |	СведенияПрослеживаемостиДо.КоличествоПрослеживаемости КАК КоличествоТовараДо,
		               |	СведенияПрослеживаемостиДо.Сумма КАК СтоимостьБезНДСДоИзменения,
		               |	СведенияПрослеживаемостиПосле.Сумма КАК СтоимостьБезНДСПослеИзменения
		               |ИЗ
		               |	Документ.КорректировкаРеализации.СведенияПрослеживаемости КАК СведенияПрослеживаемостиПосле
		               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.СведенияПрослеживаемости КАК СведенияПрослеживаемостиДо
		               |		ПО СведенияПрослеживаемостиПосле.Ссылка.ДокументРеализации = СведенияПрослеживаемостиДо.Ссылка
		               |			И СведенияПрослеживаемостиПосле.РНПТ = СведенияПрослеживаемостиДо.РНПТ
		               |			И СведенияПрослеживаемостиПосле.ИдентификаторСтроки = СведенияПрослеживаемостиДо.ИдентификаторСтроки
		               |ГДЕ
		               |	СведенияПрослеживаемостиПосле.Ссылка = &Ссылка";
		
		ВидДокументаОснования = ?(ТипЗнч(ДанныеДокумента.ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваровУслуг"), "РеализацияТоваровУслуг", "КорректировкаРеализации");
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "РеализацияТоваровУслуг", ВидДокументаОснования);
		
	Иначе
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура ДополнитьУПД_ДаннымиПоСчетуФактуре(СсылкаСчетаФактуры, ДеревоДанных)
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СсылкаСчетаФактуры", 
		СсылкаСчетаФактуры);
		
	РеквизитыСФ = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(СсылкаСчетаФактуры, "
		|Номер, Дата, Исправление, НомерИсправления, НомерИсходногоДокумента, ДатаИсходногоДокумента, 
		|ВидСчетаФактуры, ИдентификаторГосКонтракта");
	НомерСФ = ?(РеквизитыСФ.Исправление, РеквизитыСФ.НомерИсходногоДокумента, ПолучитьПечатныйНомерДокумента(СсылкаСчетаФактуры));
	ДатаСФ  = ?(РеквизитыСФ.Исправление, РеквизитыСФ.ДатаИсходногоДокумента,  РеквизитыСФ.Дата);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерДокумента", НомерСФ);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаДокумента",  ДатаСФ);
	Если РеквизитыСФ.Исправление Тогда
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправления", РеквизитыСФ.НомерИсправления);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсправления",  РеквизитыСФ.Дата);
	Иначе
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправления", "");
	КонецЕсли;
	
	ДокументыОснования = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияСчетаФактуры");
	МассивОснований = СсылкаСчетаФактуры.ДокументыОснования.ВыгрузитьКолонку("ДокументОснование");
	Если ЗначениеЗаполнено(ДокументыОснования) Тогда
		ДокументыОснованияТип = ТипЗнч(ДокументыОснования);
		Если ДокументыОснованияТип = Тип("Массив") Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивОснований, ДокументыОснования);
		ИначеЕсли ОбщегоНазначения.ЭтоСсылка(ДокументыОснованияТип) Тогда
			МассивОснований.Добавить(ДокументыОснования);
		КонецЕсли;
	КонецЕсли;
	
	МассивОснований = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивОснований);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияСчетаФактуры", МассивОснований);
	
	ПРДвДереве = ОбщегоНазначенияЭД.ДанныеДерева(ДеревоДанных, "ПлатежноРасчетныеДокументы");
	Если ПРДвДереве.Количество() = 1 И ПустаяСтрока(ПРДвДереве[0].НомерПРД) И ПустаяСтрока(ПРДвДереве[0].ДатаПРД) Тогда
		ПРД = СсылкаСчетаФактуры.ДатаНомерДокументовОплаты.Выгрузить();
		ПРД.Колонки.НомерПлатежноРасчетногоДокумента.Имя = "НомерПРД";
		ПРД.Колонки.ДатаПлатежноРасчетногоДокумента.Имя  = "ДатаПРД";
		Сч = ПРД.Количество() - 1;
		Пока Сч >= 0 Цикл
			Если ПустаяСтрока(ПРД[Сч].НомерПРД) ИЛИ ПустаяСтрока(ПРД[Сч].ДатаПРД) Тогда
				ПРД.Удалить(Сч)
			КонецЕсли;
			Сч = Сч - 1;
		КонецЦикла;
		Если ПРД.Количество() > 0 Тогда
			ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ПРД, "ПлатежноРасчетныеДокументы");
		КонецЕсли;
	КонецЕсли;
	
	Если Не ПустаяСтрока(РеквизитыСФ.ИдентификаторГосКонтракта) Тогда
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДополнительныеСведенияОбУчастниках.ИдентификаторГосКонтракта", 
			РеквизитыСФ.ИдентификаторГосКонтракта);
		ДопДанные = Новый ТаблицаЗначений;
		ДопДанные.Колонки.Добавить("Идентификатор");
		ДопДанные.Колонки.Добавить("Значение");
		СтрокаТаблицы = ДопДанные.Добавить();
		СтрокаТаблицы.Идентификатор = "ИдентификаторГосКонтракта";
		СтрокаТаблицы.Значение = РеквизитыСФ.ИдентификаторГосКонтракта;
		ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ДопДанные, "ДопДанныеСчетаФактуры.ТекстоваяИнформация");
	КонецЕсли;

КонецПроцедуры


Функция ПолучитьТаблицуКодовМаркировкиДокумента(ДокументСсылка)
	
	ТаблицаКодовМаркировки = Неопределено;
		
	Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.РеализацияТоваровУслуг")
		Или ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.КорректировкаРеализации")
		Или ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВозвратТоваровПоставщику")Тогда
		Запрос = Новый Запрос(СтрШаблон("ВЫБРАТЬ Т.Ссылка, Т.ШтрихкодУпаковки КАК Штрихкод Из Документ.%1.ШтрихкодыУпаковок КАК Т ГДЕ Т.Ссылка = &Ссылка", ДокументСсылка.Метаданные().Имя));
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
		ТаблицаКодовМаркировки = Запрос.Выполнить().Выгрузить();
		Запрос = Новый Запрос(СтрШаблон("ВЫБРАТЬ Т.Ссылка КАК Ссылка,
							|	Т.Номенклатура КАК Номенклатура,
							|	Т.ХарактеристикаНоменклатуры КАК Характеристика,
							|	СУММА(Т.Количество * Т.Коэффициент) КАК Количество
							|Из Документ.%1.Товары КАК Т
							|ГДЕ Т.Ссылка = &Ссылка
							|СГРУППИРОВАТЬ ПО
							|	Т.Ссылка,
							|   Т.Номенклатура,
							|	Т.ХарактеристикаНоменклатуры", ДокументСсылка.Метаданные().Имя));
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
		ТаблицаТоварыДокумента = Запрос.Выполнить().Выгрузить();
		ТаблицаКодовМаркировки = ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимоеИКодыОСУ(ТаблицаКодовМаркировки, ТаблицаТоварыДокумента, ШтрихкодированиеИС.ПараметрыСканирования(ДокументСсылка));
		ТаблицаКодовМаркировки.Колонки.Удалить("Ссылка");
		ТаблицаКодовМаркировки.Колонки.Добавить("Ссылка");
	КонецЕсли;
	
	Возврат ТаблицаКодовМаркировки;
	
КонецФункции

Процедура ПодготовитьДанныеДляУКД_ПервичныйДокумент(СсылкаНаОбъект, ДанныеДокумента)
	
	ДокументОбъект = СсылкаНаОбъект.ПолучитьОбъект();
	Если ДанныеДокумента = Неопределено Тогда
		ДанныеДокумента = Новый Структура();
	КонецЕсли;

	ДанныеДокумента = ПолучитьДанныеСогласованногоИзменения(СсылкаНаОбъект);
	
	КолонкиТаблицы = ДанныеДокумента.ТаблицаТоваров.Колонки;
	КолонкиТаблицы.Наименование.Имя = "ТоварНаименование";
	КолонкиТаблицы.КодТовара.Имя = "ТоварКод";
	КолонкиТаблицы.СтавкаНДСДоКорректировки.Имя = "НалоговаяСтавкаДоКорректировки";
	КолонкиТаблицы.СтавкаНДС.Имя = "НалоговаяСтавка";
	КолонкиТаблицы.СуммаНДСДоКорректировки.Имя = "СуммаНалогаДоКорректировки";
	КолонкиТаблицы.СуммаНДС.Имя = "СуммаНалога";
	КолонкиТаблицы.СуммаСНДСДоКорректировки.Имя = "СтоимостьТоваровСНалогомДоКорректировки";
	КолонкиТаблицы.СуммаСНДС.Имя = "СтоимостьТоваровСНалогом";
	КолонкиТаблицы.СуммаБезНДСДоКорректировки.Имя = "СтоимостьТоваровБезНалогаДоКорректировки";
	КолонкиТаблицы.СуммаБезНДС.Имя = "СтоимостьТоваровБезНалога";
	КолонкиТаблицы.ЦенаДоКорректировки.Имя = "ЦенаЗаЕдиницуИзмеренияДоКорректировки";
	КолонкиТаблицы.Цена.Имя = "ЦенаЗаЕдиницуИзмерения";
	КолонкиТаблицы.МассаНеттоДоКорректировки.Имя = "КоличествоДоКорректировки";
	КолонкиТаблицы.МассаНетто.Имя = "Количество";
	
	КолонкиТаблицы.Добавить("СуммаАкцизаДоКорректировки");
	КолонкиТаблицы.Добавить("СуммаАкциза");
	КолонкиТаблицы.Добавить("ЕдиницаИзмеренияКодДоКорректировки");
	КолонкиТаблицы.Добавить("ЕдиницаИзмеренияКод");
	КолонкиТаблицы.Добавить("НаименованиеЕдиницыИзмеренияДоКорректировки");
	КолонкиТаблицы.Добавить("НаименованиеЕдиницыИзмеренияПослеКорректировки");
	КолонкиТаблицы.Добавить("ИдТовараУКонтрагента");
	КолонкиТаблицы.Добавить("ТоварИдентификатор");
	КолонкиТаблицы.Добавить("СтоимостьТоваровСНалогомУвеличение");
	КолонкиТаблицы.Добавить("СтоимостьТоваровСНалогомУменьшение");
	КолонкиТаблицы.Добавить("СтоимостьТоваровБезНалогаУвеличение");
	КолонкиТаблицы.Добавить("СтоимостьТоваровБезНалогаУменьшение");
	КолонкиТаблицы.Добавить("СуммаНалогаУвеличение");
	КолонкиТаблицы.Добавить("СуммаНалогаУменьшение");
	КолонкиТаблицы.Добавить("СуммаАкцизаУвеличение");
	КолонкиТаблицы.Добавить("СуммаАкцизаУменьшение");
	КолонкиТаблицы.Добавить("СведенияОТаможеннойДекларации");
	
	//+УКД
	КолонкиТаблицы.Добавить("СведенияОМаркировкеДо");
	КолонкиТаблицы.Добавить("СведенияОМаркировке");
	
	// сведения о маркировке до корректировки
	ТаблицаКодовМаркировкиДо = ПолучитьТаблицуКодовМаркировкиДокумента(СсылкаНаОбъект.ДокументРеализации);
	
	// сведения о маркировке
	ТаблицаКодовМаркировкиПосле = ПолучитьТаблицуКодовМаркировкиДокумента(СсылкаНаОбъект);
	
	КолонкиТаблицы.Добавить("Сопоставление");
	
	КолонкиТаблицы.Добавить("СведенияОПрослеживаемости");
	ДанныеПрослеживаемости = НовыйДанныеПрослеживаемости(СсылкаНаОбъект, ДанныеДокумента.РеквизитыШапки);
	СведенияОПрослеживаемости = ПолучитьСведенияОПрослеживаемости(СсылкаНаОбъект, ДанныеДокумента.РеквизитыШапки);
	
	ТД = Новый ТаблицаЗначений;
	ТД.Колонки.Добавить("СтранаПроисхожденияКод");
	ТД.Колонки.Добавить("ТаможеннаяДекларацияНомер");

	Для Каждого СтрокаНоменклатуры Из ДанныеДокумента.ТаблицаТоваров Цикл
		
		// ТоварНаименование
		Если Не ПустаяСтрока(СтрокаНоменклатуры.НаименованиеХарактеристики) Тогда
			СтрокаНоменклатуры.ТоварНаименование = СтрокаНоменклатуры.ТоварНаименование +
				" (" + СтрокаНоменклатуры.НаименованиеХарактеристики + ")";
		КонецЕсли;
		// ИдТовараУКонтрагента
		ИДТовара = СтрокаНоменклатуры.Номенклатура.УникальныйИдентификатор();
		ИДХарактеристики = ?(ЗначениеЗаполнено(СтрокаНоменклатуры.Характеристика), СтрокаНоменклатуры.Характеристика.УникальныйИдентификатор(), "");
		ИДУпаковки = "";
		СтрокаНоменклатуры.ИдТовараУКонтрагента = Строка(ИДТовара)+"#"+Строка(ИДХарактеристики)+"#"+Строка(ИДУпаковки);
		// ТоварИдентификатор
		СтрокаНоменклатуры.ТоварИдентификатор = Строка(ИДТовара);
		// СуммаАкциза
		СтрокаНоменклатуры.СуммаАкцизаДоКорректировки = 0;
		СтрокаНоменклатуры.СуммаАкциза = 0;
		СтрокаНоменклатуры.СуммаАкцизаУвеличение = 0;
		СтрокаНоменклатуры.СуммаАкцизаУменьшение = 0;
		// Увеличение/Уменьшение
		РазницаСНалогом = СтрокаНоменклатуры.СтоимостьТоваровСНалогом - СтрокаНоменклатуры.СтоимостьТоваровСНалогомДоКорректировки;
		РазницаБезНалога = СтрокаНоменклатуры.СтоимостьТоваровБезНалога - СтрокаНоменклатуры.СтоимостьТоваровБезНалогаДоКорректировки;
		РазницаНалога = СтрокаНоменклатуры.СуммаНалога - СтрокаНоменклатуры.СуммаНалогаДоКорректировки;
		Если РазницаСНалогом > 0 Тогда
			Суффикс1 = "Увеличение";
			Суффикс2 = "Уменьшение";
		Иначе
			Суффикс1 = "Уменьшение";
			Суффикс2 = "Увеличение";
		КонецЕсли;
		СтрокаНоменклатуры["СтоимостьТоваровСНалогом"+Суффикс1] = Макс(РазницаСНалогом,-РазницаСНалогом);
		СтрокаНоменклатуры["СтоимостьТоваровБезНалога"+Суффикс1] = Макс(РазницаБезНалога,-РазницаБезНалога);
		СтрокаНоменклатуры["СуммаНалога"+Суффикс1] = Макс(РазницаНалога,-РазницаНалога);
		СтрокаНоменклатуры["СтоимостьТоваровСНалогом"+Суффикс2] = 0;
		СтрокаНоменклатуры["СтоимостьТоваровБезНалога"+Суффикс2] = 0;
		СтрокаНоменклатуры["СуммаНалога"+Суффикс2] = 0;
		Если СтрокаНоменклатуры.КоличествоДоКорректировки <> 0 Тогда
			СтрокаНоменклатуры.ЦенаЗаЕдиницуИзмеренияДоКорректировки =
				Окр(СтрокаНоменклатуры.СтоимостьТоваровБезНалогаДоКорректировки/СтрокаНоменклатуры.КоличествоДоКорректировки,2);
		КонецЕсли;
		Если СтрокаНоменклатуры.Количество <> 0 Тогда
			СтрокаНоменклатуры.ЦенаЗаЕдиницуИзмерения =
				Окр(СтрокаНоменклатуры.СтоимостьТоваровБезНалога/СтрокаНоменклатуры.Количество,2);
		Иначе
			СтрокаНоменклатуры.ЦенаЗаЕдиницуИзмерения = СтрокаНоменклатуры.ЦенаЗаЕдиницуИзмеренияДоКорректировки;
		КонецЕсли;
		// Единицы измерения
		СтрокаНоменклатуры.ЕдиницаИзмеренияКодДоКорректировки = СокрЛП(СтрокаНоменклатуры.УпаковкаКодДоКорректировки);
		СтрокаНоменклатуры.ЕдиницаИзмеренияКод = СокрЛП(СтрокаНоменклатуры.УпаковкаКод);
		
		СтрокаНоменклатуры.НаименованиеЕдиницыИзмеренияДоКорректировки = СокрЛП(СтрокаНоменклатуры.УпаковкаНаименованиеДоКорректировки);
		СтрокаНоменклатуры.НаименованиеЕдиницыИзмеренияПослеКорректировки = СокрЛП(СтрокаНоменклатуры.УпаковкаНаименование);
		
		Сопоставление = СопоставлениеНоменклатуры();
		Сопоставление.НоменклатураИБ   = СтрокаНоменклатуры.Номенклатура;
		Сопоставление.ХарактеристикаИБ = СтрокаНоменклатуры.Характеристика;
		Сопоставление.УпаковкаИБ       = СтрокаНоменклатуры.Упаковка;
		
		СтрокаНоменклатуры.Сопоставление = Сопоставление;
		
		// Сведения о прослеживаемости
		СтруктураОтбора = Новый Структура("ИдентификаторСтроки", СтрокаНоменклатуры.ИдентификаторСтроки);
		СтрокиПрослеживаемости = СведенияОПрослеживаемости.НайтиСтроки(СтруктураОтбора);
		Если СтрокиПрослеживаемости.Количество() > 0 Тогда
			ДанныеПрослеживаемости.Очистить();
			
			Для Каждого Строка Из СтрокиПрослеживаемости Цикл
				НовСтрока = ДанныеПрослеживаемости.Добавить();
				ЗаполнитьЗначенияСвойств(НовСтрока, Строка);
				
				Если Не ЗначениеЗаполнено(НовСтрока.СтоимостьБезНДСДоИзменения) Тогда
					НовСтрока.СтоимостьБезНДСДоИзменения = НовСтрока.КоличествоТовараДо * СтрокаНоменклатуры.ЦенаЗаЕдиницуИзмеренияДоКорректировки;
				КонецЕсли;
				
				Если Не ЗначениеЗаполнено(НовСтрока.СтоимостьБезНДСПослеИзменения) Тогда
					НовСтрока.СтоимостьБезНДСПослеИзменения = НовСтрока.КоличествоТовара * СтрокаНоменклатуры.ЦенаЗаЕдиницуИзмерения;
				КонецЕсли;
				
				РазницаКоличествоУчетное          = НовСтрока.КоличествоТовараУчетноеПослеИзменения - НовСтрока.КоличествоТовараУчетноеДоИзменения;
				РазницаКоличествоПрослеживаемости = НовСтрока.КоличествоТовара - НовСтрока.КоличествоТовараДо;
				РазницаСтоимостьБезНДС            = НовСтрока.СтоимостьБезНДСДоИзменения - НовСтрока.СтоимостьБезНДСПослеИзменения;
				
				НовСтрока["КоличествоТовараУчетное" + Суффикс1] = Макс(РазницаКоличествоУчетное, -РазницаКоличествоУчетное);
				НовСтрока["КоличествоТовараУчетное" + Суффикс2] = 0;
				НовСтрока["КоличествоТовара" + Суффикс1] = Макс(РазницаКоличествоПрослеживаемости, -РазницаКоличествоПрослеживаемости);
				НовСтрока["КоличествоТовара" + Суффикс2] = 0;
				НовСтрока["СтоимостьБезНДС" + Суффикс1] = Макс(РазницаСтоимостьБезНДС, -РазницаСтоимостьБезНДС);
				
				НовСтрока.ЕдиницаИзмеренияКод = СтрокаНоменклатуры.ЕдиницаИзмеренияПрослеживаемостиКод;
			КонецЦикла;
			СтрокаНоменклатуры.СведенияОПрослеживаемости = ДанныеПрослеживаемости.Скопировать();
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаНоменклатуры.ТаможеннаяДекларацияНомер) Или
			ЗначениеЗаполнено(СтрокаНоменклатуры.СтранаПроисхожденияКод)Тогда
			ТД.Очистить();
			СтрокаТД = ТД.Добавить();
			СтрокаТД.СтранаПроисхожденияКод = СокрЛП(СтрокаНоменклатуры.СтранаПроисхожденияКод);
			СтрокаТД.ТаможеннаяДекларацияНомер = ?(ЗначениеЗаполнено(СтрокаНоменклатуры.ТаможеннаяДекларацияНомер),СокрЛП(СтрокаНоменклатуры.ТаможеннаяДекларацияНомер), "-");
			СтрокаНоменклатуры.СведенияОТаможеннойДекларации = ТД.Скопировать();
		Иначе
			СтрокаНоменклатуры.СтранаПроисхожденияКод = "";
			СтрокаНоменклатуры.СтранаПроисхожденияНаименование = "";
		КонецЕсли;
		
		//+УКД
		ЭлектронноеВзаимодействиеИСМП.ЗаполнитьСведенияОМаркировкеУКД2020(СтрокаНоменклатуры, СтрокаНоменклатуры, ТаблицаКодовМаркировкиДо, ТаблицаКодовМаркировкиПосле);

	КонецЦикла;
	
КонецПроцедуры

Процедура ДополнитьУКД_ДаннымиПоСчетуФактуре(СсылкаСчетаФактуры, ДеревоДанных)
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СсылкаКорректировочногоСчетаФактуры", 
		СсылкаСчетаФактуры);
		
	РеквизитыСФ = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(СсылкаСчетаФактуры, "ИсправляемыйСчетФактура,
		|Номер, Дата, Исправление, НомерИсправления, НомерИсходногоДокумента, ДатаИсходногоДокумента, 
		|ВидСчетаФактуры, ИдентификаторГосКонтракта");
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерДокумента", ПолучитьПечатныйНомерДокумента(СсылкаСчетаФактуры));
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаДокумента",  РеквизитыСФ.Дата);

	Если ЗначениеЗаполнено(РеквизитыСФ.ИсправляемыйСчетФактура) Тогда
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсходногоДокумента", 
			ПолучитьПечатныйНомерДокумента(РеквизитыСФ.ИсправляемыйСчетФактура));
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсходногоДокумента", 
			РеквизитыСФ.ИсправляемыйСчетФактура.Дата);
	КонецЕсли;

	ДокументыОснования = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияСчетаФактуры");
	МассивОснований = СсылкаСчетаФактуры.ДокументыОснования.ВыгрузитьКолонку("ДокументОснование");
	Если ЗначениеЗаполнено(ДокументыОснования) Тогда
		ДокументыОснованияТип = ТипЗнч(ДокументыОснования);
		Если ДокументыОснованияТип = Тип("Массив") Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивОснований, ДокументыОснования, Истина);
		ИначеЕсли ОбщегоНазначения.ЭтоСсылка(ДокументыОснованияТип) Тогда
			МассивОснований.Добавить(ДокументыОснования);
		КонецЕсли;
	КонецЕсли;
	
	МассивОснований = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивОснований);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияСчетаФактуры", МассивОснований);
	
	Если Не ПустаяСтрока(РеквизитыСФ.ИдентификаторГосКонтракта) Тогда
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДополнительныеСведенияОбУчастниках.ИдентификаторГосКонтракта", 
			РеквизитыСФ.ИдентификаторГосКонтракта);
		ДопДанные = Новый ТаблицаЗначений;
		ДопДанные.Колонки.Добавить("Идентификатор");
		ДопДанные.Колонки.Добавить("Значение");
		СтрокаТаблицы = ДопДанные.Добавить();
		СтрокаТаблицы.Идентификатор = "ИдентификаторГосКонтракта";
		СтрокаТаблицы.Значение = РеквизитыСФ.ИдентификаторГосКонтракта;
		ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ДопДанные, "ДопДанныеСчетаФактуры.ТекстоваяИнформация");
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ВспомогательныеМетоды_УПД_УКД_2019

Процедура ДобавитьШтрихкодыТаблицуШтрихкодовУпаковок(ШтрихкодыУпаковок, СведенияОТоваре) Экспорт
	
	ШтрихкодыУпаковокСтрокиТЧ = Новый Массив;
	
	КодыУпаковок = СведенияОТоваре.Строки.Найти(
		"СведенияОТоварах.НомерСтроки.СведенияОМаркировке", "ПолныйПуть", Истина);
	
	Если КодыУпаковок <> Неопределено И ЗначениеЗаполнено(КодыУпаковок.Значение) Тогда
		
		КонтрольныеИдентификационныеЗнаки = КодыУпаковок.Строки.Найти(
			"СведенияОТоварах.НомерСтроки.СведенияОМаркировке.КонтрольныеИдентификационныеЗнаки", "ПолныйПуть", Истина);
		Для Каждого СтрокаКодаУпаковки Из КонтрольныеИдентификационныеЗнаки.Строки Цикл
			ШтрихкодУпаковки = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(СтрокаКодаУпаковки,
				"СведенияОТоварах.НомерСтроки.СведенияОМаркировке.КонтрольныеИдентификационныеЗнаки.НомерСтроки.Код");
			Если ЗначениеЗаполнено(ШтрихкодУпаковки) Тогда 
				ШтрихкодыУпаковокСтрокиТЧ.Добавить(ШтрихкодУпаковки);
			КонецЕсли;
		КонецЦикла;
		
		ИндивидуальныеУпаковки = КодыУпаковок.Строки.Найти(
			"СведенияОТоварах.НомерСтроки.СведенияОМаркировке.ИндивидуальныеУпаковки", "ПолныйПуть", Истина);
		Для Каждого СтрокаКодаУпаковки Из ИндивидуальныеУпаковки.Строки Цикл
			ШтрихкодУпаковки = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(СтрокаКодаУпаковки,
				"СведенияОТоварах.НомерСтроки.СведенияОМаркировке.ИндивидуальныеУпаковки.НомерСтроки.Код");
			Если ЗначениеЗаполнено(ШтрихкодУпаковки) Тогда 
				ШтрихкодыУпаковокСтрокиТЧ.Добавить(ШтрихкодУпаковки);
			КонецЕсли;
		КонецЦикла;
		
		ТранспортныеУпаковки = КодыУпаковок.Строки.Найти(
			"СведенияОТоварах.НомерСтроки.СведенияОМаркировке.ТранспортныеУпаковки", "ПолныйПуть", Истина);
		Для Каждого СтрокаКодаУпаковки Из ТранспортныеУпаковки.Строки Цикл
			ШтрихкодУпаковки = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(СтрокаКодаУпаковки,
				"СведенияОТоварах.НомерСтроки.СведенияОМаркировке.ТранспортныеУпаковки.НомерСтроки.Код");
			Если ЗначениеЗаполнено(ШтрихкодУпаковки) Тогда 
				ШтрихкодыУпаковокСтрокиТЧ.Добавить(ШтрихкодУпаковки);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Для Каждого ЗначениеШтрихкода Из ШтрихкодыУпаковокСтрокиТЧ Цикл
		
		НоваяСтрокаТЧШтрихкодыУпаковок = ШтрихкодыУпаковок.Добавить();
		НоваяСтрокаТЧШтрихкодыУпаковок.ЗначениеШтрихкода = ЗначениеШтрихкода;
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет соответствующий тип адреса переданными данными.
// Параметры:
//  СтрокаДерева - СтрокаДереваЗначений - Строка дерева, содержащая данные участника
//  АдресУчастника - Структура - содержит данные адреса участника обмена. Имена полей структуры должны совпадать с
//    именами полей структуры выбранного типа адреса:
//    Структурированный - "Индекс, КодРегион, Район, Город, НаселПункт, Улица, Дом, Корпус, Кварт";
//    Произвольный/Иностранный - "КодСтр, АдрТекст" (вынесены в разные элементы списка для того,
//      чтобы правильно заполнить ЭД).
//  ТипАдреса - Строка - один из 3-х вариантов: Структурированный, Произвольный, Иностранный.
//
Процедура ЗаполнитьАдресВДереве_2019(СтруктураАдресаСтрокиДереваДанных, АдресУчастника, ТипАдреса, ВидУчастника, ДеревоДанных, УД = Ложь)
	
	ЛокальнаяСтруктураАдресаСтрокиДереваДанных = Новый Структура;
	
	Если УД = Истина Тогда
		Если ТипАдреса = "Структурированный" Тогда
			ТипАдресаВДереве = "АдресРФ";
		ИначеЕсли ТипАдреса = "Иностранный" Тогда
			ТипАдресаВДереве = "АдресИнформация";
		ИначеЕсли ТипАдреса = "Произвольный" Тогда
			ТипАдресаВДереве = "АдресИнформация";
		КонецЕсли;
	Иначе
		ТипАдресаВДереве = ТипАдреса;
	КонецЕсли;
	
	Если ТипАдреса = "Произвольный" Тогда
		Адрес = "";
		Если Не АдресУчастника.Свойство("АдрТекст", Адрес) Тогда
			АдресУчастника.Свойство("АдресСтрокой", Адрес);
		КонецЕсли;
		ЛокальнаяСтруктураАдресаСтрокиДереваДанных.Вставить(ТипАдресаВДереве,Адрес);
	Иначе
		Если АдресУчастника.АдресРФ Тогда
			АдресУчастника.Удалить("АдресРФ");
			АдресУчастника.Удалить("КодСтраны");
			АдресУчастника.Удалить("АдресСтрокой");
			АдресУчастника.Удалить("Регион");
			Если УД = Истина Тогда
				Значение = Неопределено;
				Если АдресУчастника.Свойство("КодРегион", Значение) Тогда
					АдресУчастника.Удалить("КодРегион");
					АдресУчастника.Вставить("КодРегиона", Значение);
				КонецЕсли;
				Если АдресУчастника.Свойство("НаселПункт", Значение) Тогда
					АдресУчастника.Удалить("НаселПункт");
					АдресУчастника.Вставить("НаселенныйПункт", Значение);
				КонецЕсли;
				Если АдресУчастника.Свойство("Кварт", Значение) Тогда
					АдресУчастника.Удалить("Кварт");
					АдресУчастника.Вставить("Квартира", Значение);
				КонецЕсли;
			КонецЕсли;
		Иначе
			АдресУчастника.Удалить("АдресРФ");
			АдресУчастника.Удалить("Индекс");
			АдресУчастника.Удалить("КодРегион");
			АдресУчастника.Удалить("Регион");
			АдресУчастника.Удалить("Район");
			АдресУчастника.Удалить("Город");
			АдресУчастника.Удалить("НаселПункт");
			АдресУчастника.Удалить("Улица");
			АдресУчастника.Удалить("Дом");
			АдресУчастника.Удалить("Корпус");
			АдресУчастника.Удалить("Кварт");
		КонецЕсли;
		НеПроизвольныйАдрес = Новый Структура;
		Для Каждого Элемент Из АдресУчастника Цикл
			ИмяРеквизита = "";
			Если Элемент.Ключ = "КодСтраны" ИЛИ Элемент.Ключ = "КодСтр" Тогда
				
				Если ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, 
						ВидУчастника + ".Адрес." + ТипАдресаВДереве + "." + "КодСтраны") Тогда
					ИмяРеквизита = "КодСтраны";
				ИначеЕсли ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, 
						ВидУчастника + ".Адрес." + ТипАдресаВДереве + "." + "КодСтр") Тогда
					ИмяРеквизита = "КодСтр";
				КонецЕсли;
				
			ИначеЕсли Элемент.Ключ = "АдресСтрокой" ИЛИ Элемент.Ключ = "АдресТекст" ИЛИ Элемент.Ключ = "АдрТекст" Тогда
				
				Если ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, 
						ВидУчастника + ".Адрес." + ТипАдресаВДереве + "." + "АдресТекст") Тогда
					ИмяРеквизита = "АдресТекст";
				ИначеЕсли ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, 
						ВидУчастника + ".Адрес." + ТипАдресаВДереве + "." + "АдрТекст") Тогда
					ИмяРеквизита = "АдрТекст";
				КонецЕсли;
				
			Иначе
				ИмяРеквизита = Элемент.Ключ;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ИмяРеквизита) Тогда
				НеПроизвольныйАдрес.Вставить(ИмяРеквизита,Элемент.Значение);
			КонецЕсли;
			
		КонецЦикла;
		ЛокальнаяСтруктураАдресаСтрокиДереваДанных.Вставить(ТипАдресаВДереве,НеПроизвольныйАдрес);
	КонецЕсли;
	
	СтруктураАдресаСтрокиДереваДанных = ЛокальнаяСтруктураАдресаСтрокиДереваДанных;
КонецПроцедуры

// Получает данные о физическом (юридическом) лице по ссылке.
//
// Параметры:
//  ЮрФизЛицо - Ссылка на элемент справочника, по которому надо получить данные.
//
Функция ПолучитьДанныеЮрФизЛица_2019(ЮрФизЛицо, БанковскийСчет = Неопределено) Экспорт
	
	Сведения = Новый Структура;
	
	Сведения = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(ЮрФизЛицо, ТекущаяДатаСеанса(), , БанковскийСчет);
	Свидетельство = "";
	 
	Если ТипЗнч(ЮрФизЛицо) = Тип("СправочникСсылка.Организации") Тогда
		
		Если ЮрФизЛицо.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
			 
			Сведения.Вставить("ПолноеНаименование", ОбщегоНазначения.ПолучитьЗначениеРеквизита(
														ЮрФизЛицо, 
														"НаименованиеПолное"));
			
			ПолноеНаименование = Сведения.ПолноеНаименование;
			Если ВРЕГ(Лев(ПолноеНаименование,3))="ИП " Тогда
				ПолноеНаименование = Прав(ПолноеНаименование, СтрДлина(ПолноеНаименование)-3);
			ИначеЕсли ВРЕГ(Лев(ПолноеНаименование, СтрДлина("Индивидуальный предприниматель")))="ИНДИВИДУАЛЬНЫЙ ПРЕДПРИНИМАТЕЛЬ" Тогда
				ПолноеНаименование = Прав(ПолноеНаименование, СтрДлина(ПолноеНаименование)-СтрДлина("Индивидуальный предприниматель"));
			КонецЕсли;
			
			Фамилия  = "";
			Имя      = "";
			Отчество = "";
			ПолноеНаименование = СокрЛП(ПолноеНаименование);
			
			ОбщегоНазначения.ФамилияИнициалыФизЛица(ПолноеНаименование, Фамилия, Имя, Отчество);
			Сведения.Вставить("Фамилия",  Фамилия);
			Сведения.Вставить("Имя",      Имя);
			Сведения.Вставить("Отчество", Отчество);
			
			Если Не ПустаяСтрока(ЮрФизЛицо.СвидетельствоСерияНомер) Тогда
				Свидетельство = "Свидетельство " + ЮрФизЛицо.СвидетельствоСерияНомер + " от " + Формат(ЮрФизЛицо.СвидетельствоДатаВыдачи, "ДЛФ=D");
			КонецЕсли;

		КонецЕсли;
		
		Сведения.Вставить("КраткоеНаименование", ЮрФизЛицо.Наименование);
	Иначе
		Если ЮрФизЛицо.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
			
			ПолноеНаименование = Сведения.ПолноеНаименование;
			Если ВРЕГ(Лев(ПолноеНаименование,3))="ИП " Тогда
				ПолноеНаименование = Прав(ПолноеНаименование, СтрДлина(ПолноеНаименование)-3);
			ИначеЕсли ВРЕГ(Лев(ПолноеНаименование, СтрДлина("Индивидуальный предприниматель")))="ИНДИВИДУАЛЬНЫЙ ПРЕДПРИНИМАТЕЛЬ" Тогда
				ПолноеНаименование = Прав(ПолноеНаименование, СтрДлина(ПолноеНаименование)-СтрДлина("Индивидуальный предприниматель"));
			КонецЕсли;
			
			Фамилия  = "";
			Имя      = "";
			Отчество = "";
			ПолноеНаименование = СокрЛП(ПолноеНаименование);
			
			ОбщегоНазначения.ФамилияИнициалыФизЛица(ПолноеНаименование, Фамилия, Имя, Отчество);
			Сведения.Вставить("Фамилия",  Фамилия);
			Сведения.Вставить("Имя",      Имя);
			Сведения.Вставить("Отчество", Отчество);
			
		КонецЕсли;
		Сведения.Вставить("КодОКОПФ", ЮрФизЛицо.ОКОПФ);
		Сведения.Вставить("КраткоеНаименование", ЮрФизЛицо.Наименование);
	КонецЕсли;
	
	Сведения.Вставить("Ссылка",    ЮрФизЛицо);
	Сведения.Вставить("ЮрФизЛицо", ЮрФизЛицо.ЮрФизЛицо);
	Сведения.Вставить("ОфициальноеНаименование", Сведения.ПолноеНаименование);
	Сведения.Вставить("Свидетельство", Свидетельство);
	Возврат Сведения;
	
КонецФункции

Процедура ЗаполнитьСведенияУчастникаОбмена_2019(ГдеЗаполнять, СведенияОбУчастнике, ВидАдреса = "Юр", ВидУчастника = Неопределено, ТипАдреса = "", УД = Ложь)
	
	Если ТипЗнч(ГдеЗаполнять) = Тип("Структура") Тогда
		СтруктураУчастника = ГдеЗаполнять;
		СтруктураУчастника.Вставить("КодОКПО",                 Лев(СведенияОбУчастнике.КодПоОКПО, 10));
		СтруктураУчастника.Вставить("НаименованиеОрганизации", СведенияОбУчастнике.ПолноеНаименование);
		СтруктураУчастника.Вставить("ИНН",                     СведенияОбУчастнике.ИНН);
		СтруктураУчастника.Вставить("КПП",                     СведенияОбУчастнике.КПП);
		СтруктураУчастника.Вставить("КодОКОПФ",                СведенияОбУчастнике.КодОКОПФ);
		СтруктураУчастника.Вставить("ЭтоФизЛицо", СведенияОбУчастнике.ЮрФизЛицо <> Перечисления.ЮрФизЛицо.ЮрЛицо);
		Если СведенияОбУчастнике.ЮрФизЛицо <> Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
			ЗаполнитьФИОиДолжность(СтруктураУчастника, СведенияОбУчастнике.ПолноеНаименование);
		КонецЕсли;
		
		// Типы адресов представлены списком значений, в котором Представление элемента - описание типа (Структурированный,
		// Произвольный, Иностранный), значение элемента - структура описывающая поля адреса, пометка - указывает, из какого
		// элемента списка брать данные при заполнении ЭД.
		АдресУчастника = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьАдресСтруктурой(СведенияОбУчастнике, "Ссылка", ВидАдреса);
		ТипАдреса = ?(АдресУчастника.АдресРФ, "Структурированный", "Иностранный");
		ЗаполнитьАдресВСпискеТиповАдресов(СтруктураУчастника.Адрес, АдресУчастника, ТипАдреса);
		
		СтруктураУчастника.Вставить("Телефон",                 СведенияОбУчастнике.Телефоны);
		СтруктураУчастника.Вставить("Факс");
		
		СтруктураБанковскийСчет = СтруктураУчастника.БанковскийСчет;
		СтруктураБанковскийСчет.Вставить("БИК",         СведенияОбУчастнике.БИК);
		СтруктураБанковскийСчет.Вставить("НаимБанк",    СведенияОбУчастнике.Банк.Наименование);
		СтруктураБанковскийСчет.Вставить("НомерСчета ", СведенияОбУчастнике.НомерСчета);
	Иначе
		ДеревоДанных = ГдеЗаполнять;
		ТаблицаУчастников = Новый ТаблицаЗначений;
		ТаблицаУчастников.Колонки.Добавить("НомерСтроки");
		
		Если ВидУчастника <> "СведенияОГрузоотправителе.Грузоотправитель" Тогда
			ТаблицаУчастников.Колонки.Добавить("ТипУчастника");
			ТаблицаУчастников.Колонки.Добавить("Адрес");
		
			Если ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, ВидУчастника+"НомерСтроки.Контакт") Тогда
				ТаблицаУчастников.Колонки.Добавить("Контакт");
			ИначеЕсли ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, ВидУчастника+".КонтактныеСведения") Тогда
				ТаблицаУчастников.Колонки.Добавить("КонтактныеСведения");
			КонецЕсли;
		
			Если ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, ВидУчастника+".БанковскийСчет") Тогда
				ТаблицаУчастников.Колонки.Добавить("БанковскийСчет");
			ИначеЕсли ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, ВидУчастника+".БанковскиеРеквизиты") Тогда
				ТаблицаУчастников.Колонки.Добавить("БанковскиеРеквизиты");
			КонецЕсли;
		
			ТаблицаУчастников.Колонки.Добавить("КраткоеНаименование", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(255)));
			НоваяСтрока = ТаблицаУчастников.Добавить();
			НоваяСтрока.НомерСтроки = 1;
			ТипУчастника = Новый Структура;
		
			Если СведенияОбУчастнике.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
				ЮЛ = Новый Структура;
				ЮЛ.Вставить("ИНН",СведенияОбУчастнике.ИНН);
				ЮЛ.Вставить("КПП",СведенияОбУчастнике.КПП);
				ЮЛ.Вставить("НаименованиеОрганизации",СведенияОбУчастнике.ПолноеНаименование);
				ТипУчастника.Вставить("ЮЛ",ЮЛ);
				НоваяСтрока.ТипУчастника=ТипУчастника;
			Иначе
				Если УД = Истина Тогда
					ПрефиксРеквизитов = "ИП"
				Иначе
					ПрефиксРеквизитов = "ФЛ";
				КонецЕсли;
				ФЛ= Новый Структура;
				ФЛ.Вставить("ИНН",СведенияОбУчастнике.ИНН);
				Фамилия = ""; Имя = ""; Отчество = "";
				Если СведенияОбУчастнике.Свойство("ФИО") Тогда
					ЭлектронныеДокументы.ФамилияИнициалыФизЛица(СведенияОбУчастнике.ФИО, Фамилия, Имя, Отчество);
				Иначе
					ЭлектронныеДокументы.ФамилияИнициалыФизЛица(СведенияОбУчастнике.ПолноеНаименование, Фамилия, Имя, Отчество);
				КонецЕсли;
				ФЛ.Вставить("Фамилия", Фамилия);
				ФЛ.Вставить("Имя", Имя);
				ФЛ.Вставить("Отчество", Отчество);
				СтрокаРеквизита = ДеревоДанных.Строки.Найти(ВидУчастника + ".НомерСтроки."+ПрефиксРеквизитов + ".ПолноеНаименование", "ПолныйПуть", Истина);
				Если СтрокаРеквизита <> Неопределено Тогда
					ФЛ.Вставить("ПолноеНаименование", СведенияОбУчастнике.ПолноеНаименование);
				КонецЕсли;
				ТипУчастника.Вставить(ПрефиксРеквизитов, ФЛ);
				НоваяСтрока.ТипУчастника = ТипУчастника;
			КонецЕсли;
	
			Если СведенияОбУчастнике.Свойство("Ссылка") Тогда
				АдресУчастника = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьАдресСтруктурой(СведенияОбУчастнике, "Ссылка", ВидАдреса);
				Если ТипАдреса = "" Тогда
					ТипАдреса = ?(АдресУчастника.АдресРФ, "Структурированный", "Иностранный");
				КонецЕсли;
			Иначе
				Если ТипАдреса = "Произвольный" Тогда
					Если ВидАдреса = "Юр" Тогда
						АдресУчастника = Новый Структура("АдресСтрокой", СведенияОбУчастнике.ЮридическийАдрес);
					ИначеЕсли ВидАдреса = "Факт" Тогда
						АдресУчастника = Новый Структура("АдресСтрокой", СведенияОбУчастнике.ФактическийАдрес);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		
			ЗаполнитьАдресВДереве_2019(НоваяСтрока.Адрес, АдресУчастника, ТипАдреса, ВидУчастника, ДеревоДанных, УД);
			Контакт = Новый Структура;
		
			Если ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, ВидУчастника+"НомерСтроки.Контакт") Тогда
				Контакт.Вставить("Телефон", СведенияОбУчастнике.Телефоны);
				НоваяСтрока.Контакт = Контакт;
			ИначеЕсли ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, ВидУчастника+".КонтактныеСведения") Тогда
				Контакт.Вставить("Телефон", СведенияОбУчастнике.Телефоны);
				НоваяСтрока.КонтактныеСведения = Контакт;
			КонецЕсли;
		
			Если ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, ВидУчастника+".БанковскийСчет") Тогда
				НомерСчета = "";
				Если СведенияОбУчастнике.Свойство("НомерСчета", НомерСчета) И ЗначениеЗаполнено(НомерСчета) Тогда
					Банк = "";
					БИК = "";
					БанковскийСчет = Новый Структура;
					БанковскийСчет.Вставить("НомерСчета", НомерСчета);
					Если СведенияОбУчастнике.Свойство("Банк", Банк) И ЗначениеЗаполнено(Банк) Тогда
						БанковскийСчет.Вставить("НаимБанк", Банк.Наименование);
					КонецЕсли;
					Если СведенияОбУчастнике.Свойство("БИК", БИК) И ЗначениеЗаполнено(БИК) Тогда
						БанковскийСчет.Вставить("БИК", БИК);
					КонецЕсли;
					НоваяСтрока.БанковскийСчет = БанковскийСчет;
				КонецЕсли;
			ИначеЕсли ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, ВидУчастника+".БанковскиеРеквизиты") Тогда
				НомерСчета = "";
				Если СведенияОбУчастнике.Свойство("НомерСчета", НомерСчета) И ЗначениеЗаполнено(НомерСчета) Тогда
					Банк = "";
					БИК = "";
					КоррСчет = "";
					БанковскиеРеквизиты = Новый Структура;
					БанковскиеРеквизиты.Вставить("НомерСчета", НомерСчета);
					Если СведенияОбУчастнике.Свойство("Банк", Банк) И ЗначениеЗаполнено(Банк) Тогда
						БанковскиеРеквизиты.Вставить("НаименованиеБанка", Банк.Наименование);
					КонецЕсли;
					Если СведенияОбУчастнике.Свойство("БИК", БИК) И ЗначениеЗаполнено(БИК) Тогда
						БанковскиеРеквизиты.Вставить("БИКБанка", БИК);
					КонецЕсли;
					Если СведенияОбУчастнике.Свойство("КоррСчет", КоррСчет) И ЗначениеЗаполнено(КоррСчет) Тогда
						БанковскиеРеквизиты.Вставить("КорреспондентскийСчетБанка", КоррСчет);
					КонецЕсли;
					НоваяСтрока.БанковскиеРеквизиты = БанковскиеРеквизиты;
				КонецЕсли;
			КонецЕсли;
			НоваяСтрока.КраткоеНаименование = СведенияОбУчастнике.КраткоеНаименование;
			ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаУчастников, ВидУчастника);
			ГдеЗаполнять = ДеревоДанных;
		Иначе
			ТаблицаУчастников.Колонки.Добавить("Грузоотправитель");
			ТаблицаУчастников.Колонки.Добавить("ОнЖе");
			НоваяСтрока = ТаблицаУчастников.Добавить();
			НоваяСтрока.НомерСтроки = 1;
			Грузоотправитель = Новый Структура;
			ТипУчастника = Новый Структура;
		
			Если СведенияОбУчастнике.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
				ЮЛ = Новый Структура;
				ЮЛ.Вставить("ИНН",СведенияОбУчастнике.ИНН);
				ЮЛ.Вставить("КПП",СведенияОбУчастнике.КПП);
				ЮЛ.Вставить("НаименованиеОрганизации",СведенияОбУчастнике.ПолноеНаименование);
				ТипУчастника.Вставить("ЮЛ",ЮЛ);
			Иначе
				Если УД = Истина Тогда
					ПрефиксРеквизитов = "ИП"
				Иначе
					ПрефиксРеквизитов = "ФЛ";
				КонецЕсли;
				ФЛ= Новый Структура;
				ФЛ.Вставить("ИНН",СведенияОбУчастнике.ИНН);
				Фамилия = ""; Имя = ""; Отчество = "";
				Если СведенияОбУчастнике.Свойство("ФИО") Тогда
					ЭлектронныеДокументы.ФамилияИнициалыФизЛица(СведенияОбУчастнике.ФИО, Фамилия, Имя, Отчество);
				Иначе
					ЭлектронныеДокументы.ФамилияИнициалыФизЛица(СведенияОбУчастнике.ПолноеНаименование, Фамилия, Имя, Отчество);
				КонецЕсли;
				ФЛ.Вставить("Фамилия", Фамилия);
				ФЛ.Вставить("Имя", Имя);
				ФЛ.Вставить("Отчество", Отчество);
				СтрокаРеквизита = ДеревоДанных.Строки.Найти(ВидУчастника + ".НомерСтроки."+ПрефиксРеквизитов + ".ПолноеНаименование", "ПолныйПуть", Истина);
				Если СтрокаРеквизита <> Неопределено Тогда
					ФЛ.Вставить("ПолноеНаименование", СведенияОбУчастнике.ПолноеНаименование);
				КонецЕсли;
				ТипУчастника.Вставить(ПрефиксРеквизитов, ФЛ);
			КонецЕсли;
	
			Грузоотправитель.Вставить("ТипУчастника", ТипУчастника);
			
			Если СведенияОбУчастнике.Свойство("Ссылка") Тогда
				АдресУчастника = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьАдресСтруктурой(СведенияОбУчастнике, "Ссылка", ВидАдреса);
				Если ТипАдреса = "" Тогда
					ТипАдреса = ?(АдресУчастника.АдресРФ, "Структурированный", "Иностранный");
				КонецЕсли;
			Иначе
				Если ТипАдреса = "Произвольный" Тогда
					Если ВидАдреса = "Юр" Тогда
						АдресУчастника = Новый Структура("АдресСтрокой", СведенияОбУчастнике.ЮридическийАдрес);
					ИначеЕсли ВидАдреса = "Факт" Тогда
						АдресУчастника = Новый Структура("АдресСтрокой", СведенияОбУчастнике.ФактическийАдрес);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			АдресСтруктура = Новый Структура;
			ЗаполнитьАдресВДереве_2019(АдресСтруктура, АдресУчастника, ТипАдреса, ВидУчастника, ДеревоДанных, УД);
			Грузоотправитель.Вставить("Адрес", АдресСтруктура);
			Контакт = Новый Структура;
		
			Если ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, ВидУчастника+"НомерСтроки.Контакт") Тогда
				Контакт.Вставить("Телефон", СведенияОбУчастнике.Телефоны);
				Грузоотправитель.Вставить("Контакт", Контакт);
			ИначеЕсли ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, ВидУчастника+".КонтактныеСведения") Тогда
				Контакт.Вставить("Телефон", СведенияОбУчастнике.Телефоны);
				Грузоотправитель.Вставить("КонтактныеСведения", Контакт);
			КонецЕсли;
		
			Если ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, ВидУчастника+".БанковскийСчет") Тогда
				НомерСчета = "";
				Если СведенияОбУчастнике.Свойство("НомерСчета", НомерСчета) И ЗначениеЗаполнено(НомерСчета) Тогда
					Банк = "";
					БИК = "";
					БанковскийСчет = Новый Структура;
					БанковскийСчет.Вставить("НомерСчета", НомерСчета);
					Если СведенияОбУчастнике.Свойство("Банк", Банк) И ЗначениеЗаполнено(Банк) Тогда
						БанковскийСчет.Вставить("НаимБанк", Банк.Наименование);
					КонецЕсли;
					Если СведенияОбУчастнике.Свойство("БИК", БИК) И ЗначениеЗаполнено(БИК) Тогда
						БанковскийСчет.Вставить("БИК", БИК);
					КонецЕсли;
					Грузоотправитель.Вставить("БанковскийСчет", БанковскийСчет);
				КонецЕсли;
			ИначеЕсли ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, ВидУчастника+".БанковскиеРеквизиты") Тогда
				НомерСчета = "";
				Если СведенияОбУчастнике.Свойство("НомерСчета", НомерСчета) И ЗначениеЗаполнено(НомерСчета) Тогда
					Банк = "";
					БИК = "";
					КоррСчет = "";
					БанковскиеРеквизиты = Новый Структура;
					БанковскиеРеквизиты.Вставить("НомерСчета", НомерСчета);
					Если СведенияОбУчастнике.Свойство("Банк", Банк) И ЗначениеЗаполнено(Банк) Тогда
						БанковскиеРеквизиты.Вставить("НаименованиеБанка", Банк.Наименование);
					КонецЕсли;
					Если СведенияОбУчастнике.Свойство("БИК", БИК) И ЗначениеЗаполнено(БИК) Тогда
						БанковскиеРеквизиты.Вставить("БИКБанка", БИК);
					КонецЕсли;
					Если СведенияОбУчастнике.Свойство("КоррСчет", КоррСчет) И ЗначениеЗаполнено(КоррСчет) Тогда
						БанковскиеРеквизиты.Вставить("КорреспондентскийСчетБанка", КоррСчет);
					КонецЕсли;
					Грузоотправитель.Вставить("БанковскиеРеквизиты", БанковскиеРеквизиты);
				КонецЕсли;
			КонецЕсли;
			Грузоотправитель.Вставить("КраткоеНаименование", СведенияОбУчастнике.КраткоеНаименование);
			НоваяСтрока.Грузоотправитель = Грузоотправитель;
			ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаУчастников, "СведенияОГрузоотправителе");
			ГдеЗаполнять = ДеревоДанных;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПрочитатьНомерСчетаУчастника(ДеревоДанных)
	
	СведенияОПродавце = ДеревоДанных.Строки.Найти("СведенияОПродавце", "ПолныйПуть");
	
	Если СведенияОПродавце.Строки.Количество() > 1 Тогда
		ТекстИсключения = НСтр("ru = 'Ошибка загрузки электронной товарной накладной.'") + Символы.ПС + 
			НСтр("ru = 'Загрузка сводных накладных не поддерживается.'");
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	// Сведения о поставщике.
	Для Каждого СтрокаПродавца Из СведенияОПродавце.Строки Цикл
		НомерСчета	= ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(СтрокаПродавца, "СведенияОПродавце.НомерСтроки.БанковскиеРеквизиты.НомерСчета");	
		Прервать;
	КонецЦикла;
	
	Возврат НомерСчета;
КонецФункции

Процедура ПрочитатьДанныеУчастника_2019(ДеревоДанных, Шапка, ИмяВДереве, ИмяВШапке, ИмяТипаДляПоиска)
	
	СведенияУчастника = ДеревоДанных.Строки.Найти(ИмяВДереве, "ПолныйПуть");
	
	Если СведенияУчастника.Строки.Количество() > 1 Тогда
		ТекстИсключения = НСтр("ru = 'Ошибка загрузки электронной товарной накладной.'") + Символы.ПС + 
			НСтр("ru = 'Загрузка сводных накладных не поддерживается.'");
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Для Каждого СтрокаУчастника Из СведенияУчастника.Строки Цикл
		ТипУчастника = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(СтрокаУчастника, ИмяВДереве + ".НомерСтроки.ТипУчастника");
		РеквизитыПоиска = Новый Структура("ИНН, КПП, Наименование");
		
		Если ТипУчастника = "ЮЛ" Тогда
			РеквизитыПоиска.Наименование = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(СтрокаУчастника, 
				ИмяВДереве + ".НомерСтроки.ТипУчастника.ЮЛ.НаименованиеОрганизации");
			РеквизитыПоиска.ИНН = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(СтрокаУчастника, 
				ИмяВДереве + ".НомерСтроки.ТипУчастника.ЮЛ.ИНН");
			РеквизитыПоиска.КПП = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(СтрокаУчастника, 
				ИмяВДереве + ".НомерСтроки.ТипУчастника.ЮЛ.КПП");
		ИначеЕсли ТипУчастника = "ИП" ИЛИ ТипУчастника = "ФЛ" Тогда
			Фамилия = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(СтрокаУчастника, ИмяВДереве + ".НомерСтроки.ТипУчастника." + ТипУчастника + ".Фамилия");
			Имя = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(СтрокаУчастника, ИмяВДереве + ".НомерСтроки.ТипУчастника." + ТипУчастника + ".Имя");
			Отчество = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(СтрокаУчастника, ИмяВДереве + ".НомерСтроки.ТипУчастника." + ТипУчастника + ".Отчество");
			РеквизитыПоиска.Наименование = Фамилия + " " + Имя + ?(ЗначениеЗаполнено(Отчество), " " + Отчество, "");
			РеквизитыПоиска.ИНН = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(СтрокаУчастника, ИмяВДереве + ".НомерСтроки.ТипУчастника." + ТипУчастника + ".ИНН");
			РеквизитыПоиска.Удалить("КПП");
		ИначеЕсли ТипУчастника = "ИЛ" ИЛИ ТипУчастника = "ИностраннаяОрганизация" Тогда
			РеквизитыПоиска.Наименование = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(СтрокаУчастника, 
				ИмяВДереве + ".НомерСтроки.ТипУчастника.ИЛ.НаименованиеОрганизации");
			РеквизитыПоиска.Удалить("ИНН");
			РеквизитыПоиска.Удалить("КПП");
		КонецЕсли;
		СсылкаНаУчастника = НайтиСсылкуНаОбъект(ИмяТипаДляПоиска, , РеквизитыПоиска);
		Если ЗначениеЗаполнено(СсылкаНаУчастника) Тогда
			Шапка.Вставить(ИмяВШапке, СсылкаНаУчастника);
		КонецЕсли;
		
		Прервать;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПрочитатьДанныеУниверсальногоДокумента_2019(ДеревоДанных, ДанныеДокумента, ИмяТипаДокумента)
	
	Шапка = Новый Структура("");
	
	ПрочитатьДанныеУчастника_2019(ДеревоДанных, Шапка, "СведенияОПокупателе", "Организация", "Организации");
	ПрочитатьДанныеУчастника_2019(ДеревоДанных, Шапка, "СведенияОПродавце", "Контрагент", "Контрагенты");
	
	Если Шапка.Свойство("Контрагент") И ЗначениеЗаполнено(Шапка.Контрагент) Тогда
		НомерСчета = ПрочитатьНомерСчетаУчастника(ДеревоДанных);
		Если ЗначениеЗаполнено(НомерСчета) Тогда
			РеквизитыПоиска = Новый Структура("Владелец, НомерСчета", Шапка.Контрагент, НомерСчета);
			БанковскийСчетКонтрагента = НайтиСсылкуНаОбъект("БанковскиеСчетаКонтрагентов", , РеквизитыПоиска);
			Если ЗначениеЗаполнено(БанковскийСчетКонтрагента) Тогда
				Шапка.Вставить("БанковскийСчетКонтрагента", БанковскийСчетКонтрагента);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ВалютаКод = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаКод");
	ВалютаДокумента = НайтиСсылкуНаОбъект("Валюты", ВалютаКод);
	Если ЗначениеЗаполнено(ВалютаДокумента) Тогда
		Шапка.Вставить("ВалютаДокумента", ВалютаДокумента);
	КонецЕсли;
	
	НомерВходящегоДокумента = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерДокумента");
	Шапка.Вставить("НомерВходящегоДокумента", НомерВходящегоДокумента);
	ДатаВходящегоДокумента = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаДокумента");
	Шапка.Вставить("ДатаВходящегоДокумента", ДатаВходящегоДокумента);
	
	Основание = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияСчетаФактуры");
	Если ТипЗнч(Основание) = Тип("Массив") И Основание.Количество() > 0 Тогда
		ДокументОснование = Основание[0];
	Иначе
		ДокументОснование = Основание;
	КонецЕсли;
	Если ЗначениеЗаполнено(ДокументОснование) Тогда
		Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
			ДокументыОснования = ДокументОснование.ДокументыОснования;
			Если ДокументыОснования.Количество() > 0 Тогда
				ДокументПоступления = ДокументыОснования[0].ДокументОснование;
			КонецЕсли;
		Иначе
			ДокументПоступления = ДокументОснование;
		КонецЕсли;
	КонецЕсли;
	Если ДокументПоступления = Неопределено Тогда
		ДокументПоступления = Документы.ПоступлениеТоваровУслуг.ПустаяСсылка();
	КонецЕсли;
	Шапка.Вставить("ДокументПоступления", ДокументПоступления);
	
	ДанныеДокумента.Вставить("Шапка", Шапка);
	
	ПустойДокумент = Документы[ИмяТипаДокумента].ПустаяСсылка();
	
	Если Найти(ИмяТипаДокумента, "СчетФактура") = 0 Тогда
		
		Товары = ПустойДокумент.Товары.ВыгрузитьКолонки();
		ДанныеДокумента.Вставить("Товары", Товары);
		
		Если ИмяТипаДокумента<>"ВозвратТоваровОтПокупателя"
			Тогда
				Услуги = ПустойДокумент.Услуги.ВыгрузитьКолонки();
				ДанныеДокумента.Вставить("Услуги", Услуги);
		КонецЕсли;
			
		СерийныеНомера = ПустойДокумент.СерийныеНомера.ВыгрузитьКолонки();
		ДанныеДокумента.Вставить("СерийныеНомера", СерийныеНомера);
		
		ИмяТаблицы = "СведенияОТоварах";
		КлючСвязи = 1;
		
		ШтрихкодыУпаковок = ЭлектронноеВзаимодействиеИСМП.НоваяТаблицаШтрихкодыУпаковок();
		СведенияОПрослеживаемости = ПустойДокумент.СведенияПрослеживаемости.ВыгрузитьКолонки();
		
		ТаблицаСтрокНоменклатуры = ДеревоДанных.Строки.Найти(ИмяТаблицы, "ПолныйПуть").Строки;
		Для Каждого СтрокаНоменклатурыВДереве Из ТаблицаСтрокНоменклатуры Цикл
			ДанныеСтроки = Новый Структура("ПрефиксПутиКРеквизитам,КлючСвязи", ИмяТаблицы + ".НомерСтроки",КлючСвязи);
			ПрочитатьДанныеСтрокиНоменклатуры(СтрокаНоменклатурыВДереве, ДанныеСтроки, ДанныеДокумента);
			СтрокаНоменклатурыВТаблице = ДанныеДокумента[ДанныеСтроки.ТипСтроки].Добавить();
			
			Если ДанныеСтроки.ТипСтроки = "Услуги" Тогда
				Если ДанныеСтроки.Количество = 0 Тогда
					ДанныеСтроки.Количество = 1;
				КонецЕСли; 
			КонецЕСли;
			
			ЗаполнитьЗначенияСвойств(СтрокаНоменклатурыВТаблице, ДанныеСтроки);
			ПрочитатьСерийныеНомераСтрокиНоменклатуры(СтрокаНоменклатурыВДереве, СерийныеНомера, ДанныеСтроки);
			ПрочитатьСведенияОПрослеживаемостиСтрокиНоменклатуры(СтрокаНоменклатурыВДереве, СведенияОПрослеживаемости, ДанныеСтроки, ДанныеДокумента.ВидДокумента);

			КлючСвязи = КлючСвязи + 1;
			
			Если ДанныеСтроки.Свойство("НомерГТД") Тогда
				ЗаполнитьСериюНоменклатуры(СтрокаНоменклатурыВТаблице, ДанныеСтроки);
			КонецЕсли;
			
			ДобавитьШтрихкодыТаблицуШтрихкодовУпаковок(ШтрихкодыУпаковок, СтрокаНоменклатурыВДереве);
			
		КонецЦикла;
		
		ЭлектронноеВзаимодействиеИСМП.СвернутьТаблицуШтрихкодовУпаковок(ШтрихкодыУпаковок);
		ДанныеДокумента.Вставить("ШтрихкодыУпаковок", ШтрихкодыУпаковок);
		
		ДанныеДокумента.Вставить("СведенияОПрослеживаемости", СведенияОПрослеживаемости);
		
	Иначе
		ДокументыОснования = ПустойДокумент.ДокументыОснования.ВыгрузитьКолонки();
		Основания = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияСчетаФактуры");
		
		Если ТипЗнч(Основания) = Тип("Массив") Тогда
			МассивОснований = Основания;
		Иначе
			МассивОснований = Новый Массив;
			МассивОснований.Добавить(Основания);
		КонецЕсли;
		
		ДопустимыеТипы = Метаданные.Документы.СчетФактураПолученный.ТабличныеЧасти.ДокументыОснования.Реквизиты.ДокументОснование.Тип.Типы();
		
		Для Каждого Основание Из МассивОснований Цикл
			Если Основание <> Неопределено И ЗначениеЗаполнено(Основание)
				И ДопустимыеТипы.Найти(ТипЗнч(Основание)) <> Неопределено Тогда
				СтрокаОснования = ДокументыОснования.Добавить();
				СтрокаОснования.ДокументОснование = Основание;
			КонецЕсли;
		КонецЦикла;
		ДанныеДокумента.Вставить("ДокументыОснования", ДокументыОснования);
	КонецЕсли;
	
КонецПроцедуры

Процедура НайтиСоздатьУПДДокументОВозврате_2019(ДеревоДанных, СсылкаНаВладельца = Неопределено, Записывать = Истина, СпособОбработки = "") Экспорт
	
	ИмяТипаДокумента = "ВозвратТоваровОтПокупателя";
	
	ДанныеДокумента = Новый Структура("ВидДокумента", "УПДДокументОПередаче");
	ПрочитатьДанныеУниверсальногоДокумента_2019(ДеревоДанных, ДанныеДокумента, ИмяТипаДокумента);
	
	Попытка
		Если ЗначениеЗаполнено(СсылкаНаВладельца) Тогда // получены изменения по существующему документу
			Если СсылкаНаВладельца.Проведен Тогда 
				ВызватьИсключение НСтр("ru = 'Заполнение на основании ЭД возможно только для непроведенного документа'");
			КонецЕсли;
			ДокументОбъект = СсылкаНаВладельца.ПолучитьОбъект();
		Иначе  // создаем новый
			УстановитьПривилегированныйРежим(Истина);
			ДокументОбъект = Документы[ИмяТипаДокумента].СоздатьДокумент();
			ДанныеДокумента.Шапка.Свойство("Организация", ДокументОбъект.Организация);
			ДанныеДокумента.Шапка.Свойство("Контрагент", ДокументОбъект.Контрагент);
			ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ДокументОбъект, глЗначениеПеременной("глТекущийПользователь"), , "Покупка");
			ДокументОбъект.Дата = ТекущаяДатаСеанса();
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ДокументОбъект, ДанныеДокумента.Шапка);
		ДокументОбъект.Товары.Загрузить(ДанныеДокумента.Товары);
		ДокументОбъект.СерийныеНомера.Загрузить(ДанныеДокумента.СерийныеНомера);
		ПересчитатьСуммыБезУслуг(ДокументОбъект);
		ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
		СсылкаНаВладельца = ДокументОбъект.Ссылка;
	Исключение
		ОписаниеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Заполнение на основании ЭД'"),
			УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы[ИмяТипаДокумента], 
			СсылкаНаВладельца, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение ОписаниеОшибки;
	КонецПопытки;

КонецПроцедуры

Функция ПолучитьДанныеУчастникаУПД(Знач СведенияОбУчастнике, ВидАдреса = "Юр")
	
	Данные = Новый Структура("СведенияОбУчастнике", СведенияОбУчастнике);
	
	Данные.Вставить("ТипУчастника", Новый Структура);
	
	Если СведенияОбУчастнике.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
		
		Данные.ТипУчастника.Вставить("ЮЛ", Новый Структура);
		Данные.ТипУчастника.ЮЛ.Вставить("НаименованиеОрганизации", СведенияОбУчастнике.ПолноеНаименование);
		Данные.ТипУчастника.ЮЛ.Вставить("ИНН", СведенияОбУчастнике.ИНН);
		Данные.ТипУчастника.ЮЛ.Вставить("КПП", СведенияОбУчастнике.КПП);
		
		Данные.ТипУчастника.ЮЛ.Вставить("ОГРН", СведенияОбУчастнике.ОГРН);
		
	Иначе
		
		Данные.ТипУчастника.Вставить("ИП", Новый Структура);
		Данные.ТипУчастника.ИП.Вставить("ИНН", СведенияОбУчастнике.ИНН);
		Данные.ТипУчастника.ИП.Вставить("Фамилия", СведенияОбУчастнике.Фамилия);
		Данные.ТипУчастника.ИП.Вставить("Имя", СведенияОбУчастнике.Имя);
		Данные.ТипУчастника.ИП.Вставить("Отчество", СведенияОбУчастнике.Отчество);
		
		Если ТипЗнч(СведенияОбУчастнике.Ссылка) = Тип("СправочникСсылка.Организации") Тогда
			
			Данные.ТипУчастника.ИП.Вставить("СвидетельствоОГосРегистрации", СведенияОбУчастнике.Свидетельство);
			Данные.ТипУчастника.ИП.Вставить("ОГРН", СведенияОбУчастнике.ОГРН);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ТипЗнч(СведенияОбУчастнике.Ссылка) = Тип("СправочникСсылка.Организации") Тогда
		ВидКонтактнойИнформации = Справочники.ВидыКонтактнойИнформации[ВидАдреса + "АдресОрганизации"].Ссылка;
	ИначеЕсли ТипЗнч(СведенияОбУчастнике.Ссылка) = Тип("СправочникСсылка.Контрагенты") Тогда
		ВидКонтактнойИнформации = Справочники.ВидыКонтактнойИнформации[ВидАдреса + "АдресКонтрагента"].Ссылка;
	Иначе
		ВидКонтактнойИнформации = Неопределено;
	КонецЕсли;
		
	Данные.Вставить("Адрес", Новый Структура);
	АдресУчастника = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьАдресСтруктурой(СведенияОбУчастнике, "Ссылка", ВидАдреса);
	
	Если СведенияОбУчастнике.Свойство("АдресДоставки") И ЗначениеЗаполнено(СведенияОбУчастнике.АдресДоставки) Тогда
		Если Не Данные.Адрес.Свойство("АдресИнформация") Тогда
			Данные.Адрес.Вставить("АдресИнформация", Новый Структура);
			Данные.Адрес.АдресИнформация.Вставить("КодСтраны", АдресУчастника.КодСтраны);
			Данные.Адрес.АдресИнформация.Вставить("АдресТекст", АдресУчастника.АдресСтрокой);
		КонецЕсли;
		
		Данные.Адрес.АдресИнформация.АдресТекст = СведенияОбУчастнике.АдресДоставки;
	Иначе
		Если АдресУчастника.АдресРФ Тогда
			Данные.Адрес.Вставить("АдресРФ", Новый Структура);
			Данные.Адрес.АдресРФ.Вставить("Индекс", АдресУчастника.Индекс);
			Данные.Адрес.АдресРФ.Вставить("КодРегиона", АдресУчастника.КодРегион);
			Данные.Адрес.АдресРФ.Вставить("Район", АдресУчастника.Район);
			Данные.Адрес.АдресРФ.Вставить("Город", АдресУчастника.Город);
			Данные.Адрес.АдресРФ.Вставить("НаселенныйПункт", АдресУчастника.НаселПункт);
			Данные.Адрес.АдресРФ.Вставить("Улица", АдресУчастника.Улица);
			Данные.Адрес.АдресРФ.Вставить("Дом", АдресУчастника.Дом);
			Данные.Адрес.АдресРФ.Вставить("Корпус", АдресУчастника.Корпус);
			Данные.Адрес.АдресРФ.Вставить("Квартира", АдресУчастника.Кварт);
		Иначе
			Данные.Адрес.Вставить("АдресИнформация", Новый Структура);
			Данные.Адрес.АдресИнформация.Вставить("КодСтраны", АдресУчастника.КодСтраны);
			Данные.Адрес.АдресИнформация.Вставить("АдресТекст", АдресУчастника.АдресСтрокой);
		КонецЕсли;
	КонецЕсли;
		
	Данные.Вставить("КонтактныеСведения", Новый Структура);
	
	Телефон = "";
	Если СведенияОбУчастнике.Свойство("Телефоны", Телефон) И ЗначениеЗаполнено(Телефон) Тогда
		Данные.КонтактныеСведения.Вставить("Телефон", Телефон);
	КонецЕсли;
	
	Данные.Вставить("БанковскиеРеквизиты", Новый Структура);
	
	НомерСчета = "";
	Если СведенияОбУчастнике.Свойство("НомерСчета", НомерСчета) И ЗначениеЗаполнено(НомерСчета) Тогда
		
		Данные.БанковскиеРеквизиты.Вставить("НомерСчета", НомерСчета);
		
		Банк = ""; БИК = ""; КоррСчет = "";
		Если СведенияОбУчастнике.Свойство("Банк", Банк) И ЗначениеЗаполнено(Банк) Тогда
			Если ТипЗнч(Банк) = Тип("Строка") Тогда
				БанкНаименование = Банк
			Иначе
				БанкНаименование = Банк.Наименование
			КонецЕсли;
			
			Данные.БанковскиеРеквизиты.Вставить("НаименованиеБанка", БанкНаименование);
			
		КонецЕсли;
		Если СведенияОбУчастнике.Свойство("БИК", БИК) И ЗначениеЗаполнено(БИК) Тогда
			
			Данные.БанковскиеРеквизиты.Вставить("БИКБанка", БИК);
			
		КонецЕсли;
		Если СведенияОбУчастнике.Свойство("КоррСчет", КоррСчет) И ЗначениеЗаполнено(КоррСчет) Тогда
			
			Данные.БанковскиеРеквизиты.Вставить("КорреспондентскийСчетБанка", КоррСчет);
			
		КонецЕсли;
	КонецЕсли;
	
	КодПоОКПО = "";
	Если СведенияОбУчастнике.Свойство("КодПоОКПО", КодПоОКПО) И ЗначениеЗаполнено(КодПоОКПО) Тогда
	
		Данные.Вставить("КодОКПО", КодПоОКПО);
		
	КонецЕсли;
	
	Возврат Данные;
	
КонецФункции

Функция СсылкаНаДоговорПоНомеруИДате(Владелец, НомерДоговора, ДатаДоговора) Экспорт
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДоговорыКонтрагентов.Ссылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	Не ДоговорыКонтрагентов.ПометкаУдаления И
	|	ДоговорыКонтрагентов.Владелец = &Владелец И
	|	ДоговорыКонтрагентов.Номер = &НомерДоговора И
	|	ДоговорыКонтрагентов.Дата = &ДатаДоговора";
	
	Запрос.УстановитьПараметр("Владелец", Владелец);
	Запрос.УстановитьПараметр("НомерДоговора", НомерДоговора);
	Запрос.УстановитьПараметр("ДатаДоговора", ДатаДоговора);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ЗаполнитьОбстоятельстваФормированияУПД(ДеревоДанных, СтруктураЭД, ДанныеПервичногоДокумента)
	
	// Обстоятельства формирования СФ <ОбстФормСЧФ>
	//Для <Функция>=СЧФ:
	// 1 – счет-фактура, выставляемый при реализации товаров (работ, услуг), передаче имущественных прав
	// 2 – счет-фактура, выставляемый при получении оплаты, частичной оплаты в счет предстоящих поставок товаров (выполнения работ, оказания услуг), 
	// передачи имущественных прав
	// 3 – счет-фактура, применяемый в случае реализации комиссионером (агентом, экспедитором, застройщиком или заказчиком, 
	// выполняющим функции застройщика) двум и более покупателям (приобретения у двух и более продавцов) товаров (работ, услуг), 
	// имущественных прав от своего имени 
	// Для <Функция>= СЧФДОП или <Функция>= ДОП:
	// 4 – Товары переданы от Комитента (Принципала) Комиссионеру (Агенту, действующему от собственного имени) для дальнейшей реализации
	// 5 – Товары переданы от Комиссионера (Агента, действующего от собственного имени) Комитенту (Принципалу) при возврате товаров
	// 6 – Товары переданы от Комиссионера (Агента, действующего от собственного имени) Комитенту (Принципалу) при закупке товара
	// 7 – Товары переданы от Комитента (Принципала) Комиссионеру (Агенту, действующему от собственного имени) при возврате товаров
	// 8 – Возврат товара от Покупателя Продавцу
	
	ОбстоятельстваФормированияСФ = "";
	Если СтруктураЭД.Функция = "ДОП" Тогда // УПД со статусом 2
		Если ДанныеПервичногоДокумента.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером Тогда
			ОбстоятельстваФормированияСФ = "4";
		ИначеЕсли ДанныеПервичногоДокумента.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом Тогда
			ОбстоятельстваФормированияСФ = "6";
		Иначе
			ОбстоятельстваФормированияСФ = "";
		КонецЕсли;
	ИначеЕсли СтруктураЭД.Функция = "СЧФ" Тогда // счет-фактура
		Если СтруктураЭД.ЭтоСчетФактураНаАванс Тогда
			ОбстоятельстваФормированияСФ = "2";
		ИначеЕсли СтруктураЭД.ЭтоСводныйСчетФактура Тогда
			ОбстоятельстваФормированияСФ = "3";
		Иначе
			ОбстоятельстваФормированияСФ = "1";
		КонецЕсли;
	ИначеЕсли СтруктураЭД.Функция = "СЧФДОП"  Тогда
		ОбстоятельстваФормированияСФ = "";
	КонецЕсли;
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДополнительныеСведенияОбУчастниках.ОбстоятельстваФормированияСФ", ОбстоятельстваФормированияСФ);
	
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// Получение данных для формирования электронных документов

Процедура ЗаполнитьДополнительнуюИнформацию(ЗаполняемыйОбъект, ДопИнформация)
	
	// Договор контрагента из соглашения
	Если ЗаполняемыйОбъект.Метаданные().Реквизиты.Найти("ДоговорКонтрагента") <> Неопределено Тогда
		НастройкаЭДО = Неопределено;
		Если ТипЗнч(ДопИнформация) = Тип("Структура") И ДопИнформация.Свойство("НастройкаЭДО", НастройкаЭДО) Тогда
			Если ЗначениеЗаполнено(НастройкаЭДО) Тогда
				Если Не ЗначениеЗаполнено(ЗаполняемыйОбъект.ДоговорКонтрагента) И ЗначениеЗаполнено(НастройкаЭДО.ДоговорКонтрагента) Тогда
					ЗаполняемыйОбъект.ДоговорКонтрагента = НастройкаЭДО.ДоговорКонтрагента;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Возвращает строковое значение ставки НДС по значению перечисления
//
// Параметры:
//  СтавкаНДС - ПеречислениеСсылка.СтавкиНДС - значение перечисления СтавкиНДС
//
// Возвращаемое значение:
//  Строка - Значение ставки НДС строкой ( 0% | 10% | 18% | 20% | 10/110 | 18/118 | 20/120 | без НДС )
//
Функция ПолучитьСтавкуНДССтрокой(Знач СтавкаНДС) Экспорт
	
	СтавкиНДС = Перечисления.СтавкиНДС;
	
	Если 	  СтавкаНДС = СтавкиНДС.НДС10_110 Тогда
		Результат = "10/110";
	ИначеЕсли СтавкаНДС = СтавкиНДС.НДС18_118 Тогда
		Результат = "18/118";
	ИначеЕсли СтавкаНДС = СтавкиНДС.НДС20_120 Тогда
		Результат = "20/120";
	Иначе
		Результат = СокрЛП(Строка(СтавкаНДС));
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ПроверитьВозможностьФормированияЕдиногоДокумента(Документ, Отказ) Экспорт
	
	ДанныеДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Документ,
		"Организация, Контрагент, ДоговорКонтрагента");
		
	ИспользоватьУПД = ЭлектронныеДокументы.ИспользованиеУниверсальногоПередаточногоДокумента(
		ДанныеДокумента.Организация, ДанныеДокумента.Контрагент, ДанныеДокумента.ДоговорКонтрагента);
		
	ИспользоватьУКД = ЭлектронныеДокументы.ИспользованиеУниверсальногоКорректировочногоДокумента(
		ДанныеДокумента.Организация, ДанныеДокумента.Контрагент, ДанныеДокумента.ДоговорКонтрагента);
		
	ОтборСФ = Новый Структура("ПометкаУдаления", Ложь);
	Если ИспользоватьУПД И ТипЗнч(Документ) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
			
		// Если в реализации указан вид документа - УПД, то формировать его можно только в том случае,
		// если выписан счет-фактура и на основании этого счета-фактуры также будет формироваться УПД.
		СчетФактура = УчетНДС.НайтиПодчиненныйСчетФактуру(Документ, , ОтборСФ);
		
		ВидДоговора =  Документ.ДоговорКонтрагента.ВидДоговора;
		АктуальныйЭД = АктуальныйЭлектронныйДокумент(СчетФактура);
		Если ЗначениеЗаполнено(АктуальныйЭД) 
			И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(АктуальныйЭД, "ТипЭлементаВерсииЭД") <> Перечисления.ТипыЭлементовВерсииЭД.СЧФДОПУПД Тогда
			СообщениеОбОшибке = НСтр("ru='Формирование УПД на основании %1 невозможно, т.к. для счета-фактуры %2
			|уже сформирован электронный документ %3.'");
			СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							СообщениеОбОшибке, Документ, СчетФактура, АктуальныйЭД);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке, , , , Отказ);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
		
		Если ИспользоватьУПД И Документ.ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.ИсправлениеОшибки Тогда
			ИмяЭД = "УПД";
		ИначеЕсли ИспользоватьУКД И Документ.ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение Тогда
			ИмяЭД = "УКД";
		Иначе
			Возврат;
		КонецЕсли;
		
		// Если для исходного документа корректировки реализации указан вид документа - УПД/УКД, 
		// то формировать его можно только в том случае, если для него выписан счет-фактура.
		СчетФактура = УчетНДС.НайтиПодчиненныйСчетФактуру(Документ, , ОтборСФ);
		
		АктуальныйЭД = АктуальныйЭлектронныйДокумент(СчетФактура);
		Если ЗначениеЗаполнено(АктуальныйЭД)
			И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(АктуальныйЭД, "ТипЭлементаВерсииЭД") <> Перечисления.ТипыЭлементовВерсииЭД.КСЧФДИСУКД Тогда
			СообщениеОбОшибке = НСтр("ru='Формирование "+ИмяЭД+" на основании %1 невозможно, т.к. для счета-фактуры %2
			|уже сформирован электронный документ %3.'");
			СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							СообщениеОбОшибке, Документ, СчетФактура, АктуальныйЭД);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке, , , , Отказ);
		КонецЕсли;
		
	ИначеЕсли (ИспользоватьУКД ИЛИ ИспользоватьУПД) И ТипЗнч(Документ) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		
		// УПД на основании счета-фактуры можно формировать только в том случае,
		// если для документов-оснований еще не созданы электронные документы
		// УКД на основании счета-фактуры можно формировать только в том случае,
		// если для документов-оснований еще не созданы электронные документы
		
		РезультатЗапроса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "ДокументыОснования");
		ДокументыОснования = РезультатЗапроса.Выбрать();
		Пока ДокументыОснования.Следующий() Цикл
			
			Основание = ДокументыОснования.ДокументОснование;
			ТипДокумента = ТипЗнч(Основание);
			Если (ТипДокумента = Тип("ДокументСсылка.РеализацияТоваровУслуг") И ИспользоватьУПД)
				ИЛИ (ТипДокумента = Тип("ДокументСсылка.КорректировкаРеализации") И ИспользоватьУКД) Тогда
				
				АктуальныйЭД = АктуальныйЭлектронныйДокумент(Основание);
				Если ЗначениеЗаполнено(АктуальныйЭД) 
					И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(АктуальныйЭД, "ТипЭлементаВерсииЭД") <> Перечисления.ТипыЭлементовВерсииЭД.СЧФДОПУПД
					И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(АктуальныйЭД, "ТипЭлементаВерсииЭД") <> Перечисления.ТипыЭлементовВерсииЭД.КСЧФДИСУКД  Тогда
					
					СообщениеОбОшибке = НСтр("ru='Формирование УКД на основании %1 не возможно, т.к. для документа-основания %2
					|уже сформирован электронный документ %3.'");
					СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									СообщениеОбОшибке, Документ, Основание, АктуальныйЭД);
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке, , , , Отказ);
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Функция АктуальныйЭлектронныйДокумент(Ссылка)
	
	МассивСсылок = Новый Массив();
	МассивСсылок.Добавить(Ссылка);
	СоответствиеСсылок = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСоответствиеВладельцевИЭД(МассивСсылок);
	ЭД = СоответствиеСсылок.Получить(Ссылка);
	
	Возврат ЭД;
	
КонецФункции

// Статус подключения организации.
//
// Параметры:
//   Организация - ОпределяемыйТип.Организация - ссылка на определяемый справочник Организация.
//
// Возвращаемое значение:
//   Булево - признак подключения организации.
//
Функция ОрганизацияПодключена(Организация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	УчетныеЗаписиЭДО.ИдентификаторОрганизации КАК ИдентификаторЭДО
	|ИЗ
	|	Справочник.ПрофилиНастроекЭДО КАК УчетныеЗаписиЭДО
	|ГДЕ
	|	УчетныеЗаписиЭДО.Организация = &Организация";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Механизм прямого обмена с банками

// Используется для включения подсистемы Сбербанк в прикладном решении.
//
// Параметры:
//  ФлагИспользования - <Булево> - необходимо присвоить параметру Истина, если используется подсистема Сбербанк.
//
Процедура ПроверитьИспользованиеПодсистемыСбербанк(ФлагИспользования) Экспорт
	
	ФлагИспользования = Ложь;
	
КонецПроцедуры

Процедура ПересчитатьСуммыБезУслуг(ДокументОбъект)
	
	мРеквизиты = Новый Массив;
	мРеквизиты.Добавить(Новый Структура("Цена,Количество,Сумма", "Цена","Количество","Сумма"));
	Если ТипЗнч(Документобъект) = Тип("ДокументОбъект.КорректировкаПоступления") Тогда
		мРеквизиты.Добавить(Новый Структура("Цена,Количество,Сумма", "ЦенаДоИзменения","КоличествоДоИзменения","СуммаДоИзменения"));
		мРеквизиты.Добавить(Новый Структура("Цена,Количество,Сумма", "ЦенаДоКорректировки","КоличествоДоКорректировки","СуммаДоКорректировки"));
	КонецЕсли;
	
	мТЧ = Новый Массив;
	мТЧ.Добавить("Товары");
	
	СуммаВключаетНДС = ДокументОбъект.СуммаВключаетНДС;
	
	Для Каждого ТЧ Из мТЧ Цикл
		Для Каждого СтрокаТЧ Из ДокументОбъект[ТЧ] Цикл
			Для Каждого НР Из мРеквизиты Цикл
				Если СуммаВключаетНДС Тогда
					СтрокаТЧ[НР.Цена] = СтрокаТЧ[НР.Сумма] / ?(СтрокаТЧ[НР.Количество] = 0, 1, СтрокаТЧ[НР.Количество]);
				Иначе
					СтрокаТЧ[НР.Сумма] = СтрокаТЧ[НР.Цена] * СтрокаТЧ[НР.Количество];
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
		
КонецПроцедуры

// Обмен с банком

// Функция формирует прокси по настройкам прокси (передаваемому параметру)
//
// Параметры:
//  НастройкаПроксиСервера - Соответствие:
//  ИспользоватьПрокси - использовать ли прокси-сервер
//  НеИспользоватьПроксиДляЛокальныхАдресов - использовать ли прокси-сервер для локальных адресов
//  ИспользоватьСистемныеНастройки - использовать ли системные настройки прокси-сервера
//  Сервер       - адрес прокси-сервера
//  Порт         - порт прокси-сервера
//  Пользователь - имя пользователя для авторизации на прокси-сервере
//  Пароль       - пароль пользователя
//
Функция ПолучитьНастройкиПроксиСервера(НастройкаПроксиСервера) Экспорт
	
	НастройкаПроксиСервера = ПолучениеФайловИзИнтернета.ПолучитьНастройкиПроксиНаСервере1СПредприятие();
	
	Если НастройкаПроксиСервера = Неопределено Тогда
		
		НастройкаПроксиСервера = Новый Соответствие();
		НастройкаПроксиСервера.Вставить("ИспользоватьПрокси", Истина);
		НастройкаПроксиСервера.Вставить("ИспользоватьСистемныеНастройки", Истина);
		НастройкаПроксиСервера.Вставить("НеИспользоватьПроксиДляЛокальныхАдресов", Ложь);
		НастройкаПроксиСервера.Вставить("Сервер", "");
		НастройкаПроксиСервера.Вставить("Порт", "");
		НастройкаПроксиСервера.Вставить("Пользователь", "");
		НастройкаПроксиСервера.Вставить("Пароль", "");
		
	КонецЕсли;
	
КонецФункции

///////////////////////////////////////////////////////////////////////////////
// Формирование данных для электронных документов

Процедура ОкруглитьЧисловыеПоля(СтруктураПараметров, Разрядность = 2, Режим = 1) Экспорт
	
	Для Каждого Элемент Из СтруктураПараметров Цикл
		ТипЗначенияЭлемента = ТипЗнч(Элемент.Значение);
		Если ТипЗначенияЭлемента = Тип("ТаблицаЗначений") Тогда
			Для Каждого СтрокаТЗ Из Элемент.Значение Цикл
				Для Каждого Колонка Из Элемент.Значение.Колонки Цикл
					Если ТипЗнч(СтрокаТЗ[Колонка.Имя]) = Тип("Число") Тогда
						СтрокаТЗ[Колонка.Имя] = Окр(СтрокаТЗ[Колонка.Имя], Разрядность, Режим);
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
		ИначеЕсли ТипЗначенияЭлемента = Тип("Структура") Тогда
			ОкруглитьЧисловыеПоля(Элемент.Значение, Разрядность, Режим);
		ИначеЕсли ТипЗначенияЭлемента = Тип("Число") Тогда
			СтруктураПараметров[Элемент.Ключ] = Окр(Элемент.Значение, Разрядность, Режим);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа КаталогТоваров.
//
// Параметры:
//  Организация - СправочникСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  ТоварыКаталога - Массив, список товаров для заполнения каталога.
//  ДеревоДанных - дерево значений, дерево данных заполнения электронного документа.
//
Процедура ПодготовитьДанныеПоКаталогуТоваровCML(Организация, ТоварыКаталога, ДеревоДанных) Экспорт
	
	
	
КонецПроцедуры

// Заполняет данные для электронного документа типа СоглашениеОбИзмененииСтоимости.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  ДеревоДанных - дерево для заполнения.
//
Процедура ПодготовитьДанныеПоКорректировочномуДокументу(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных) Экспорт
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Прямой обмен с банком

// Разбирает переданный текстовый файл выписки.
//
// Параметры
//  СоглашениеЭД - СправочникСсылка.СоглашениеОбИспользованииЭД - соглашение, по которому производится обмен.
//  ФайлЗагрузки - Строка, содержит путь к файлу на клиенте
//
Процедура РазобратьФайлВыписки(СоглашениеЭД, ФайлЗагрузки) Экспорт
		
КонецПроцедуры

Функция ПолучитьДанныеСтрокиТЧ(ДанныеЗаполнения, ДеревоРазбора)
	
	ДанныеДляЗаполненияСтрокиТЧ = Новый Структура();
	
	Для Каждого ТекСтрока Из ДанныеЗаполнения Цикл
		
		ИмяРеквизитаВБД = ТекСтрока.Реквизит;
		
		Если ВРег(ИмяРеквизитаВБД) = ВРег("Характеристика") Тогда 
			ИмяРеквизитаВБД = "ХарактеристикаНоменклатуры";
		ИначеЕсли ВРег(ИмяРеквизитаВБД) = ВРег("Мест") Тогда
			ИмяРеквизитаВБД = "КоличествоМест";
		ИначеЕсли ВРег(ИмяРеквизитаВБД) = ВРег("Описание") Тогда
			ИмяРеквизитаВБД = "Содержание";
		ИначеЕсли Найти(ИмяРеквизитаВБД, "ДоКорректировки") <> 0 Тогда
			ИмяРеквизитаВБД = СтрЗаменить(ИмяРеквизитаВБД, "ДоКорректировки", "ДоИзменения");
		ИначеЕсли ВРег(ИмяРеквизитаВБД) = ВРег("ЕдиницаИзмерения") Тогда
			ИмяРеквизитаВБД = "ЕдиницаИзмеренияПоКлассификатору";
		ИначеЕсли ВРег(ИмяРеквизитаВБД) = ВРег("КоличествоУпаковок") Тогда
			ИмяРеквизитаВБД = "Количество";
		ИначеЕсли ВРег(ИмяРеквизитаВБД) = ВРег("Количество") Тогда
			Если ДанныеДляЗаполненияСтрокиТЧ.Свойство("Количество") Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		НайденноеЗначение = ПолучитьЗначениеРеквизитаДерева(ТекСтрока, ТекСтрока.Реквизит, Истина, ДеревоРазбора);
		
		Если ИмяРеквизитаВБД = "ЗначенияСвойств" Тогда
			ЗначенияСвойств = Новый Структура();
			Для Каждого ЗначениеСвойства Из НайденноеЗначение.ЗначенияСвойства Цикл
				ЗначенияСвойств.Вставить(ЗначениеСвойства.Наименование, ЗначениеСвойства.Значение.Получить(0));
			КонецЦикла;
			ДанныеДляЗаполненияСтрокиТЧ.Вставить(ИмяРеквизитаВБД, ЗначенияСвойств);
		ИначеЕсли ИмяРеквизитаВБД = "СтавкаНДС" Тогда
			Если Не ЗначениеЗаполнено(НайденноеЗначение) Тогда
				НайденноеЗначение = ПредопределенноеЗначение("Перечисление.СтавкиНДС.БезНДС");
			КонецЕсли;
			ДанныеДляЗаполненияСтрокиТЧ.Вставить(ИмяРеквизитаВБД, НайденноеЗначение);
		Иначе
			ДанныеДляЗаполненияСтрокиТЧ.Вставить(ИмяРеквизитаВБД, НайденноеЗначение);
		КонецЕсли;
		
		Если ТекСтрока.Реквизит = "Номенклатура" И ЗначениеЗаполнено(НайденноеЗначение) Тогда
			
			СтрокаНоменклатура = ДеревоРазбора.Строки.Найти(ТекСтрока.ЗначениеРеквизита,"ИндексСтроки",Истина);
			
			ЕдиницаИзмеренияПоКлассификатору = Неопределено;
			ДанныеДляЗаполненияСтрокиТЧ.Свойство("ЕдиницаИзмеренияПоКлассификатору", ЕдиницаИзмеренияПоКлассификатору);
			Если Не ЗначениеЗаполнено(ЕдиницаИзмеренияПоКлассификатору) Тогда
				// Получим единицу измерения по классификатору
				ЕдиницаИзмеренияПоКлассификатору = ПолучитьЗначениеРеквизитаДерева(СтрокаНоменклатура, "ЕдиницаИзмерения", Истина, ДеревоРазбора);
				Если Не ЗначениеЗаполнено(ЕдиницаИзмеренияПоКлассификатору) Тогда
					ЕдиницаИзмеренияПоКлассификатору = ПолучитьЗначениеРеквизитаДерева(СтрокаНоменклатура, "БазоваяЕдиница", Истина, ДеревоРазбора);
				КонецЕсли;
				ДанныеДляЗаполненияСтрокиТЧ.Вставить("ЕдиницаИзмеренияПоКлассификатору", ЕдиницаИзмеренияПоКлассификатору);
			КонецЕсли;
			
		ИначеЕсли ТекСтрока.Реквизит = "ЕдиницаИзмеренияКод" И ЗначениеЗаполнено(НайденноеЗначение) Тогда
			ЕдиницаИзмеренияПоКлассификатору = НайтиСсылкуНаОбъект("ЕдиницыИзмерения", НайденноеЗначение);
			Если ЗначениеЗаполнено(ЕдиницаИзмеренияПоКлассификатору) Тогда
				ДанныеДляЗаполненияСтрокиТЧ.Вставить("ЕдиницаИзмеренияПоКлассификатору", ЕдиницаИзмеренияПоКлассификатору);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	// Получим единицу измерения для данной номенклатуры в соответствии с единицей классификатора.
	Если ДанныеДляЗаполненияСтрокиТЧ.Свойство("ЕдиницаИзмеренияПоКлассификатору") Тогда
		ЕдиницаИзмерения = НайтиСсылкуНаОбъектПоРеквизиту("ЕдиницыИзмерения", "ЕдиницаПоКлассификатору",
			ДанныеДляЗаполненияСтрокиТЧ.ЕдиницаИзмеренияПоКлассификатору, НайденноеЗначение);
		ДанныеДляЗаполненияСтрокиТЧ.Вставить("ЕдиницаИзмерения", ЕдиницаИзмерения);
	Иначе
		ДанныеДляЗаполненияСтрокиТЧ.Вставить("ЕдиницаИзмерения", Справочники.ЕдиницыИзмерения.ПустаяСсылка());
	КонецЕсли;
	
	// Если коэффициент передан, используем его, иначе берем из единицы измерения
	Коэффициент = Неопределено;
	Если ДанныеДляЗаполненияСтрокиТЧ.Свойство("Коэффициент", Коэффициент) И ЗначениеЗаполнено(Коэффициент) Тогда
		ДанныеДляЗаполненияСтрокиТЧ.Вставить("Коэффициент", Число(Коэффициент));
	Иначе
		Если ЗначениеЗаполнено(ДанныеДляЗаполненияСтрокиТЧ.ЕдиницаИзмерения) Тогда
			ДанныеДляЗаполненияСтрокиТЧ.Вставить("Коэффициент", ДанныеДляЗаполненияСтрокиТЧ.ЕдиницаИзмерения.Коэффициент); 
		КонецЕсли;
	КонецЕсли;

	СодержаниеУслуги = "";
	ЕстьСодержание = ДанныеДляЗаполненияСтрокиТЧ.Свойство("Содержание", СодержаниеУслуги);
	Если Не ЕстьСодержание ИЛИ Не ЗначениеЗаполнено(СодержаниеУслуги) Тогда
		Если ЗначениеЗаполнено(ДанныеДляЗаполненияСтрокиТЧ.Номенклатура) И ДанныеДляЗаполненияСтрокиТЧ.Номенклатура.Услуга Тогда
			ДанныеДляЗаполненияСтрокиТЧ.Вставить("Содержание", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
				ДанныеДляЗаполненияСтрокиТЧ.Номенклатура, "Наименование"));
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДанныеДляЗаполненияСтрокиТЧ;
	
КонецФункции

Процедура ДобавитьСерийныеНомера(НоваяСтрока, КлючСвязи, ДанныеДляЗаполненияСтрокиТЧ, СерийныеНомера)
	
	НоваяСтрока.КлючСвязи = КлючСвязи;
	Если ЗначениеЗаполнено(ДанныеДляЗаполненияСтрокиТЧ.Номенклатура) Тогда
		
		мСерийныеНомера = Новый Массив();
		Если ДанныеДляЗаполненияСтрокиТЧ.Свойство("СерийныеНомера") Тогда
			Если ТипЗнч(ДанныеДляЗаполненияСтрокиТЧ.СерийныеНомера) = Тип("Массив") Тогда
				мСерийныеНомера = ДанныеДляЗаполненияСтрокиТЧ.СерийныеНомера;
			Иначе
				мСерийныеНомера.Добавить(ДанныеДляЗаполненияСтрокиТЧ.СерийныеНомера);
			КонецЕсли;
		ИначеЕсли ДанныеДляЗаполненияСтрокиТЧ.Свойство("СерийныйНомер") Тогда
			мСерийныеНомера = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(
			ДанныеДляЗаполненияСтрокиТЧ.СерийныйНомер, ";", Истина);
		КонецЕсли;
		
		Сч = 0;
		Для Каждого ЗначениеСН Из мСерийныеНомера Цикл
			
			Индекс = мСерийныеНомера.Найти(ЗначениеСН);
			
			Если Индекс < Сч Тогда
				Продолжить; // Был раньше
			КонецЕсли;
			
			СтрокаСН = СерийныеНомера.Добавить();
			СтрокаСН.КлючСвязи = КлючСвязи;
			СтрокаСН.СерийныйНомер = 
			НайтиСсылкуНаОбъектПоРеквизиту("СерийныеНомера", "Код", ЗначениеСН, ДанныеДляЗаполненияСтрокиТЧ.Номенклатура, Истина);
			
			Если Не ЗначениеЗаполнено(СтрокаСН.СерийныйНомер) Тогда
				НовыйСН = Справочники.СерийныеНомера.СоздатьЭлемент();
				НовыйСН.Владелец = ДанныеДляЗаполненияСтрокиТЧ.Номенклатура;
				НовыйСН.Код = ЗначениеСН;
				НовыйСН.Записать();
				СтрокаСН.СерийныйНомер = НовыйСН.Ссылка;
			ИначеЕсли СтрокаСН.СерийныйНомер.ПометкаУдаления Тогда
				СНОбъект = СтрокаСН.СерийныйНомер.ПолучитьОбъект();
				СНОбъект.ПометкаУдаления = Ложь;
				СНОбъект.Записать();
			КонецЕсли;
			
			Сч = Сч + 1;
			
		КонецЦикла;
		
		Если мСерийныеНомера.Количество() > 0 Тогда
			ВестиСерийныеНомера = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеДляЗаполненияСтрокиТЧ.Номенклатура, "ВестиСерийныеНомера");
			Если Не ВестиСерийныеНомера Тогда
				ОписаниеСобытия = "В ЭД есть информация о серийных номерах, но для элемента номенклатуры учет по серийным номерам не ведется.";
				ЭлектронныеДокументыСлужебный.ВыполнитьЗаписьСобытияПоЭДВЖурналРегистрации(ОписаниеСобытия, 2, 
					УровеньЖурналаРегистрации.Предупреждение, Метаданные.Справочники.Номенклатура, ДанныеДляЗаполненияСтрокиТЧ.Номенклатура);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	КлючСвязи = КлючСвязи + 1;

КонецПроцедуры

Процедура ЗаполнитьДокументОснование(СтрокаТЧ, ДанныеЗаполнения)
	ЗаказПоставщику = Неопределено;
	ДанныеЗаполнения.Свойство("ДокументОснование", ЗаказПоставщику);
	Если ЗначениеЗаполнено(ЗаказПоставщику) И ТипЗнч(ЗаказПоставщику) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
		СтрокаТЧ.ЗаказПоставщику = ЗаказПоставщику;
	Иначе
		ЗначенияСвойств = Неопределено;
		ДанныеЗаполнения.Свойство("ЗначенияСвойств", ЗначенияСвойств);
		Если ТипЗнч(ЗначенияСвойств) = Тип("Структура") Тогда
			ИДЭД = Неопределено;
			ЗначенияСвойств.Свойство("ИДЭД", ИДЭД);
			Если ЗначениеЗаполнено(ИДЭД) Тогда
				ЗаказПоставщику = ЭлектронныеДокументыВнутренний.ПолучитьДокументОснование(ИДЭД, Новый Структура(""));
				Если ТипЗнч(ЗаказПоставщику) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
					СтрокаТЧ.ЗаказПоставщику = ЗаказПоставщику;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Функция ПолучитьЗначениеРеквизитаДерева(СтрокаДерева, ИмяРеквизита, ВключатьПодчиненные = Ложь, ДеревоРазбора = Неопределено)
	
	Результат = Неопределено;
	
	Если СтрокаДерева.Строки.Количество() > 0 Тогда 
		НайденнаяСтрока = СтрокаДерева.Строки.Найти(ИмяРеквизита, "Реквизит", ВключатьПодчиненные);
	Иначе // передали строку с реквизитом
		НайденнаяСтрока = СтрокаДерева;
	КонецЕсли;
	Если НайденнаяСтрока <> Неопределено Тогда
		Результат = НайденнаяСтрока.ЗначениеРеквизита;
		// если реквизит ссылочного типа (передали реквизит ДеревоРазбора), тогда нашли всего лишь индекс строки
			Если ЗначениеЗаполнено(ДеревоРазбора) Тогда 
				НайденнаяСтрока = ДеревоРазбора.Строки.Найти(Результат, "ИндексСтроки", Истина);
				Если НайденнаяСтрока <> Неопределено Тогда
					Результат = НайденнаяСтрока.СсылкаНаОбъект;
				КонецЕсли;
			КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПодготовитьСтруктуруДляПоступленияТоваровУслуг(СтрокаДляЗагрузки, ДеревоРазбора)
	
	ДанныеДляОбъекта      = Новый Структура;
	ДанныеЗаполненияШапки = Новый Структура;
	Товары = Документы.ПоступлениеТоваровУслуг.ПустаяСсылка().Товары.ВыгрузитьКолонки();
	Услуги = Документы.ПоступлениеТоваровУслуг.ПустаяСсылка().Услуги.ВыгрузитьКолонки();
	СерийныеНомера = Документы.ПоступлениеТоваровУслуг.ПустаяСсылка().СерийныеНомера.ВыгрузитьКолонки();
	ДанныеЗаполненияШапки.Вставить("СуммаВключаетНДС", Ложь);
	КлючСвязи = 1;
	
	Для Каждого СтрокаРеквизита Из СтрокаДляЗагрузки.Строки Цикл
		
		Если СтрокаРеквизита.Реквизит = "СписокОписаний" Тогда
			 
			СтрокаРеквизитаОписанийРабот = ПолучитьЗначениеРеквизитаДерева(СтрокаРеквизита, СтрокаРеквизита.Реквизит, Истина, ДеревоРазбора);
			
			Для Каждого СтрокаРеквизитаОписания Из СтрокаРеквизитаОписанийРабот.Строки Цикл
				
				Если СтрокаРеквизитаОписания.Строки.Количество() = 0 Тогда
					ЗначениеРеквизита = ПолучитьЗначениеРеквизитаДерева(СтрокаРеквизитаОписания, СтрокаРеквизитаОписания.Реквизит, Истина, ДеревоРазбора);
					Если ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
						ДанныеЗаполненияШапки.Вставить(СтрокаРеквизитаОписания.Реквизит, ЗначениеРеквизита);
					КонецЕсли;
				Иначе
					ДанныеДляЗаполненияСтрокиТЧ = ПолучитьДанныеСтрокиТЧ(СтрокаРеквизитаОписания.Строки, ДеревоРазбора);
					СтрокаТЧ = Услуги.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТЧ, ДанныеДляЗаполненияСтрокиТЧ);
					ЗаполнитьДокументОснование(СтрокаТЧ, ДанныеДляЗаполненияСтрокиТЧ);
				КонецЕсли;
				
			КонецЦикла;
			
		Иначе
			
			Если СтрокаРеквизита.Строки.Количество() = 0 Тогда 
				
				// Заполним реквизит шапки
				ЗначениеРеквизита = ПолучитьЗначениеРеквизитаДерева(СтрокаРеквизита, СтрокаРеквизита.Реквизит, Истина, ДеревоРазбора);
				
				Если СтрокаРеквизита.Реквизит = "ДатаПоДаннымКлиента" Тогда
					ИмяРеквизита = "ДатаПоДаннымПокупателя";
				ИначеЕсли СтрокаРеквизита.Реквизит = "НомерПоДаннымКлиента" Тогда
					ИмяРеквизита = "НомерПоДаннымПокупателя";
				ИначеЕсли СтрокаРеквизита.Реквизит = "Валюта" Тогда
					ИмяРеквизита = "ВалютаДокумента";
				Иначе
					ИмяРеквизита = СтрокаРеквизита.Реквизит;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
					ДанныеЗаполненияШапки.Вставить(ИмяРеквизита, ЗначениеРеквизита);
				КонецЕсли;
				
			Иначе 
				// Добавим строку табличной части
				ДанныеДляЗаполненияСтрокиТЧ = ПолучитьДанныеСтрокиТЧ(СтрокаРеквизита.Строки, ДеревоРазбора);
				
				Если ТипЗнч(ДанныеДляЗаполненияСтрокиТЧ.Номенклатура) = Тип("СправочникСсылка.Номенклатура")
					И ДанныеДляЗаполненияСтрокиТЧ.Номенклатура.Услуга Тогда
					НоваяСтрока = Услуги.Добавить();
					НОваяСтрока.Содержание = ДанныеДляЗаполненияСтрокиТЧ.Номенклатура.НаименованиеПолное;
				Иначе
					НоваяСтрока = Товары.Добавить();
					 
					ДобавитьСерийныеНомера(НоваяСтрока, КлючСвязи, ДанныеДляЗаполненияСтрокиТЧ, СерийныеНомера);
					
				 КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеДляЗаполненияСтрокиТЧ);
				
				ЗаполнитьДокументОснование(НоваяСтрока, ДанныеДляЗаполненияСтрокиТЧ);
				
				НДСУчтеноВСумме = Неопределено;
				Если ДанныеДляЗаполненияСтрокиТЧ.Свойство("НДСУчтеноВСумме", НДСУчтеноВСумме) Тогда
					ДанныеЗаполненияШапки.СуммаВключаетНДС = НДСУчтеноВСумме;
				КонецЕсли;

			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// спец. значения 
	Если ЗначениеЗаполнено(ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "НомерСчетаФактуры")) Тогда // указана счет-фактура
		ДанныеЗаполненияШапки.Вставить("НомерСчетаФактуры",ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "НомерСчетаФактуры"));
		ДанныеЗаполненияШапки.Вставить("ДатаСчетаФактуры", ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "ДатаСчетаФактуры"));
	Иначе
		ДанныеЗаполненияШапки.Вставить("НомерВходящегоДокумента",ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "Номер"));
		ДанныеЗаполненияШапки.Вставить("ДатаВходящегоДокумента", ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "Дата"));
	КонецЕсли;
	
	// реквизиты для связки "заказ - поступление"
	ДанныеЗаполненияШапки.Вставить("НомерПоДаннымПоставщика", ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "НомерПоДаннымПоставщика"));
	ДанныеЗаполненияШапки.Вставить("ДатаПоДаннымПоставщика",  ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "ДатаПоДаннымПоставщика"));
	
	ДобавитьОбязательныеПоля(ДанныеЗаполненияШапки);
	
	ДанныеДляОбъекта.Вставить("Шапка", 	ДанныеЗаполненияШапки);
	ДанныеДляОбъекта.Вставить("Товары", Товары);
	ДанныеДляОбъекта.Вставить("Услуги", Услуги);
	ДанныеДляОбъекта.Вставить("СерийныеНомера", СерийныеНомера);
	
	Возврат ДанныеДляОбъекта;
	
КонецФункции

Функция ПодготовитьСтруктуруДляСчетаФактуры(СтрокаДляЗагрузки, ДеревоРазбора)
	
	ДанныеЗаполненияШапки = Новый Структура;
	ДанныеЗаполненияШапки.Вставить("НомерВходящегоДокумента", 	ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "Номер"));
	ДанныеЗаполненияШапки.Вставить("ДатаВходящегоДокумента", 	ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "Дата"));
	
	НомерСчетаФактуры = ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "НомерСчетаФактуры");
	
	Если ЗначениеЗаполнено(НомерСчетаФактуры) Тогда
		
		ДанныеЗаполненияШапки.Вставить("ВидСчетаФактуры", Перечисления.ВидСчетаФактурыПолученного.Корректировочный);
		
		НомерИсправления 	= ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "НомерИсправления");
		ДатаИсправления 	= ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "ДатаИсправления");
		ДанныеЗаполненияШапки.Вставить("НомерИсправления", 	НомерИсправления);
		ДанныеЗаполненияШапки.Вставить("ДатаИсправления", 	ДатаИсправления);
		Если ЗначениеЗаполнено(НомерИсправления) И ЗначениеЗаполнено(ДатаИсправления) Тогда
			ДанныеЗаполненияШапки.Вставить("Исправление", Истина);
		КонецЕсли;
		ДанныеЗаполненияШапки.Вставить("НомерИсходногоДокумента", 	НомерСчетаФактуры);
		ДанныеЗаполненияШапки.Вставить("ДатаИсходногоДокумента", 	ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "ДатаСчетаФактуры"));
		
		НомерИсправленияИсходногоДокумента 	= ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "НомерИсправленияСчетаФактуры");
		ДатаИсправленияИсходногоДокумента 	= ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "ДатаИсправленияСчетаФактуры");
		ДанныеЗаполненияШапки.Вставить("НомерИсправленияИсходногоДокумента",	НомерИсправленияИсходногоДокумента);
		ДанныеЗаполненияШапки.Вставить("ДатаИсправленияИсходногоДокумента", 	ДатаИсправленияИсходногоДокумента);
		Если ЗначениеЗаполнено(НомерИсправленияИсходногоДокумента) И ЗначениеЗаполнено(ДатаИсправленияИсходногоДокумента) Тогда
			ДанныеЗаполненияШапки.Вставить("УчитыватьИсправлениеИсходногоДокумента", Истина);
		КонецЕсли;
		
		
		ДанныеЗаполненияШапки.Вставить("СуммаУвеличение", 		ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "СтТовУчНалВсегоУвел"));
		ДанныеЗаполненияШапки.Вставить("СуммаУменьшение", 		ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "СтТовУчНалВсегоУм"));
		ДанныеЗаполненияШапки.Вставить("СуммаНДСУвеличение",	ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "СумНДСУвел"));
		ДанныеЗаполненияШапки.Вставить("СуммаНДСУменьшение", 	ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "СумНДСУм"));
		
	Иначе
		
		ВидСчетаФактуры = ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "ВидСчетаФактуры");
		
		Если ВидСчетаФактуры = "Авансовый" Тогда
			ДанныеЗаполненияШапки.Вставить("ВидСчетаФактуры", 	Перечисления.ВидСчетаФактурыПолученного.НаАванс);
			СтрокаТЧ = СтрокаДляЗагрузки.Строки.Найти("СтрокаТЧ", "Реквизит");
			Если СтрокаТЧ <> Неопределено Тогда
				Ставка = ПолучитьЗначениеРеквизитаДерева(СтрокаТЧ, "НалСтВел");
				Если Ставка = "10/110" Тогда
					ДанныеЗаполненияШапки.Вставить("СтавкаНДС", Перечисления.СтавкиНДС.НДС10_110);
				ИначеЕсли Ставка = "18/118" Тогда
					ДанныеЗаполненияШапки.Вставить("СтавкаНДС", Перечисления.СтавкиНДС.НДС18_118);
				ИначеЕсли Ставка = "20/120" Тогда
					ДанныеЗаполненияШапки.Вставить("СтавкаНДС", Перечисления.СтавкиНДС.НДС20_120);
				ИначеЕсли Найти(Ставка, "10") > 0 Тогда
					ДанныеЗаполненияШапки.Вставить("СтавкаНДС", Перечисления.СтавкиНДС.НДС10);
				ИначеЕсли Найти(Ставка, "18") > 0 Тогда
					ДанныеЗаполненияШапки.Вставить("СтавкаНДС", Перечисления.СтавкиНДС.НДС18);
				ИначеЕсли Найти(Ставка, "20") > 0 Тогда
					ДанныеЗаполненияШапки.Вставить("СтавкаНДС", Перечисления.СтавкиНДС.НДС20);
				ИначеЕсли Найти(Ставка, "0") > 0 Тогда
					ДанныеЗаполненияШапки.Вставить("СтавкаНДС", Перечисления.СтавкиНДС.НДС0);  
					//ТЕАЛ НДС ++  
				ИначеЕсли Найти(Ставка, "5") > 0 Тогда
					ДанныеЗаполненияШапки.Вставить("СтавкаНДС", Перечисления.СтавкиНДС.НДС5);
				ИначеЕсли Найти(Ставка, "7") > 0 Тогда
					ДанныеЗаполненияШапки.Вставить("СтавкаНДС", Перечисления.СтавкиНДС.НДС7);
                ИначеЕсли Найти(Ставка, "5/105") > 0 Тогда
					ДанныеЗаполненияШапки.Вставить("СтавкаНДС", Перечисления.СтавкиНДС.НДС5_105);
				ИначеЕсли Найти(Ставка, "7/107") > 0 Тогда
					ДанныеЗаполненияШапки.Вставить("СтавкаНДС", Перечисления.СтавкиНДС.НДС7_107);
				ИначеЕсли Найти(Ставка, "22") > 0 Тогда
					ДанныеЗаполненияШапки.Вставить("СтавкаНДС", Перечисления.СтавкиНДС.НДС22);
				ИначеЕсли Найти(Ставка, "22/122") > 0 Тогда
					ДанныеЗаполненияШапки.Вставить("СтавкаНДС", Перечисления.СтавкиНДС.НДС22_122);	
					//ТЕАЛ НДС --
				Иначе
					ДанныеЗаполненияШапки.Вставить("СтавкаНДС", Перечисления.СтавкиНДС.БезНДС);
				КонецЕсли;
			КонецЕсли;
		Иначе
			ДанныеЗаполненияШапки.Вставить("ВидСчетаФактуры", 	Перечисления.ВидСчетаФактурыПолученного.НаПоступление);
		КонецЕсли;
		ДанныеЗаполненияШапки.Вставить("СуммаДокумента", 	ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "СтТовУчНал"));
		ДанныеЗаполненияШапки.Вставить("СуммаНДСДокумента", ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "СумНДС"));
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено (ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "НомерИсправления")) Тогда
		ДанныеЗаполненияШапки.Вставить("Исправление", Истина);
		ДанныеЗаполненияШапки.Вставить("НомерИсправления", 	ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "НомерИсправления"));
		ДанныеЗаполненияШапки.Вставить("ДатаИсправления", 	ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "ДатаИсправления"));
	КонецЕсли;
	
	ДанныеЗаполненияШапки.Вставить("ВалютаДокумента", НайтиСсылкуНаОбъект("Валюты", ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "ВалКод")));
	
	// Данные по организации
	ДанныеЗаполненияШапки.Вставить("Организация", ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "Организация", , ДеревоРазбора));
	
	// Данные по контрагенту
	ДанныеЗаполненияШапки.Вставить("Контрагент", ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "Контрагент", , ДеревоРазбора));
		
	// Получим документы-основания
	СтрокаДокументыОснования = СтрокаДляЗагрузки.Строки.Найти("ДокументыОснования");
	Если СтрокаДокументыОснования <> Неопределено Тогда
		МассивДокументовОснований = Новый Массив;
		Для Каждого Строка Из СтрокаДокументыОснования.Строки Цикл
			Если ЗначениеЗаполнено(Строка.СсылкаНаОбъект) Тогда
				МассивДокументовОснований.Добавить(Строка.СсылкаНаОбъект);
			КонецЕсли;
		КонецЦикла;
		ДанныеЗаполненияШапки.Вставить("ДокументыОснования", МассивДокументовОснований);
	КонецЕсли;

	Возврат ДанныеЗаполненияШапки;
	
КонецФункции

Функция ПодготовитьСтруктуруДляЗаказаПокупателя(СтрокаДляЗагрузки, ДеревоРазбора)
	
	ДанныеДляОбъекта = Новый Структура;
	ДанныеЗаполненияШапки = Новый Структура;
	Товары = Документы.ЗаказПокупателя.ПустаяСсылка().Товары.ВыгрузитьКолонки();
	Услуги = Документы.ЗаказПокупателя.ПустаяСсылка().Услуги.ВыгрузитьКолонки();
	
	Для Каждого СтрокаРеквизита Из СтрокаДляЗагрузки.Строки Цикл
		
		Если СтрокаРеквизита.Строки.Количество()=0 Тогда // примитивный тип
			
			ЗначениеРеквизита = ПолучитьЗначениеРеквизитаДерева(СтрокаРеквизита, СтрокаРеквизита.Реквизит, Истина, ДеревоРазбора);
			
			Если СтрокаРеквизита.Реквизит = "ЦенаВключаетНДС" Тогда
				ИмяРеквизита = "СуммаВключаетНДС";
			ИначеЕсли СтрокаРеквизита.Реквизит = "Валюта" Тогда
				ИмяРеквизита = "ВалютаДокумента";
			ИначеЕсли СтрокаРеквизита.Реквизит = "Курс" Тогда
				ИмяРеквизита = "КурсВзаиморасчетов";
			ИначеЕсли СтрокаРеквизита.Реквизит = "ДатаПоДаннымКлиента" Тогда
				ИмяРеквизита = "ДатаПоДаннымПокупателя";
			ИначеЕсли СтрокаРеквизита.Реквизит = "НомерПоДаннымКлиента" Тогда
				ИмяРеквизита = "НомерПоДаннымПокупателя";
			ИначеЕсли СтрокаРеквизита.Реквизит = "БанковскийСчет" Тогда
				ИмяРеквизита = "СтруктурнаяЕдиница";
			Иначе
				ИмяРеквизита = СтрокаРеквизита.Реквизит;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ЗначениеРеквизита) ИЛИ ЗначениеРеквизита = 0 Тогда
				ДанныеЗаполненияШапки.Вставить(ИмяРеквизита, ЗначениеРеквизита);
			КонецЕсли;
			
		ИначеЕсли СтрокаРеквизита.Реквизит = "СтрокаТЧ" Тогда
			// Добавим строку табличной части
			ДанныеДляЗаполненияСтрокиТЧ = ПолучитьДанныеСтрокиТЧ(СтрокаРеквизита.Строки, ДеревоРазбора);
			
			Если ТипЗнч(ДанныеДляЗаполненияСтрокиТЧ.Номенклатура) = Тип("СправочникСсылка.Номенклатура")
				 И ДанныеДляЗаполненияСтрокиТЧ.Номенклатура.Услуга Тогда
				НоваяСтрока = Услуги.Добавить();
			Иначе
				НоваяСтрока = Товары.Добавить();
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеДляЗаполненияСтрокиТЧ);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ДобавитьОбязательныеПоля(ДанныеЗаполненияШапки);

	ДанныеДляОбъекта.Вставить("Шапка", 	ДанныеЗаполненияШапки);
	ДанныеДляОбъекта.Вставить("Товары",	Товары);
	ДанныеДляОбъекта.Вставить("Услуги",	Услуги);
	
	Возврат ДанныеДляОбъекта;
	
КонецФункции

Функция ПодготовитьСтруктуруДляЗаказаПоставщику(СтрокаДляЗагрузки, ДеревоРазбора)
	
	ДанныеДляОбъекта = Новый Структура;
	ДанныеЗаполненияШапки = Новый Структура;
	Товары = Документы.ЗаказПоставщику.ПустаяСсылка().Товары.ВыгрузитьКолонки();
	Услуги = Документы.ЗаказПоставщику.ПустаяСсылка().Услуги.ВыгрузитьКолонки();
	
	Для Каждого СтрокаРеквизита Из СтрокаДляЗагрузки.Строки Цикл
		
		Если СтрокаРеквизита.Строки.Количество()=0 Тогда // примитивный тип
			
			ЗначениеРеквизита = ПолучитьЗначениеРеквизитаДерева(СтрокаРеквизита, СтрокаРеквизита.Реквизит, Истина, ДеревоРазбора);
			
			Если СтрокаРеквизита.Реквизит = "ЦенаВключаетНДС" Тогда
				ИмяРеквизита = "СуммаВключаетНДС";
			ИначеЕсли СтрокаРеквизита.Реквизит = "Валюта" Тогда
				ИмяРеквизита = "ВалютаДокумента";
			ИначеЕсли СтрокаРеквизита.Реквизит = "Курс" Тогда
				ИмяРеквизита = "КурсВзаиморасчетов";
			ИначеЕсли СтрокаРеквизита.Реквизит = "ДатаПоДаннымКлиента" Тогда
				ИмяРеквизита = "ДатаПоДаннымПокупателя";
			ИначеЕсли СтрокаРеквизита.Реквизит = "НомерПоДаннымКлиента" Тогда
				ИмяРеквизита = "НомерПоДаннымПокупателя";
			ИначеЕсли СтрокаРеквизита.Реквизит = "БанковскийСчет" Тогда
				ИмяРеквизита = "СтруктурнаяЕдиница";
			Иначе
				ИмяРеквизита = СтрокаРеквизита.Реквизит;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ЗначениеРеквизита) ИЛИ ЗначениеРеквизита = 0 Тогда
				ДанныеЗаполненияШапки.Вставить(ИмяРеквизита, ЗначениеРеквизита);
			КонецЕсли;
			
		ИначеЕсли СтрокаРеквизита.Реквизит = "СтрокаТЧ" Тогда
			// Добавим строку табличной части
			ДанныеДляЗаполненияСтрокиТЧ = ПолучитьДанныеСтрокиТЧ(СтрокаРеквизита.Строки, ДеревоРазбора);
			
			Если ТипЗнч(ДанныеДляЗаполненияСтрокиТЧ.Номенклатура) = Тип("СправочникСсылка.Номенклатура")
				 И ДанныеДляЗаполненияСтрокиТЧ.Номенклатура.Услуга Тогда
				НоваяСтрока = Услуги.Добавить();
			Иначе
				НоваяСтрока = Товары.Добавить();
			КонецЕсли;
			
			Если ДанныеДляЗаполненияСтрокиТЧ.ЦенаВключаетНДС Тогда
				ДанныеДляЗаполненияСтрокиТЧ.Сумма = ДанныеДляЗаполненияСтрокиТЧ.Сумма + ДанныеДляЗаполненияСтрокиТЧ.СуммаНДС;
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеДляЗаполненияСтрокиТЧ);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ДобавитьОбязательныеПоля(ДанныеЗаполненияШапки);
	
	ДанныеДляОбъекта.Вставить("Шапка",  ДанныеЗаполненияШапки);
	ДанныеДляОбъекта.Вставить("Товары", Товары);
	ДанныеДляОбъекта.Вставить("Услуги", Услуги);
	
	Возврат ДанныеДляОбъекта;
	
КонецФункции

Функция ПодготовитьСтруктуруДляКорректировкиПоступления(СтрокаДляЗагрузки, ДеревоРазбора)
	
	ДанныеДляОбъекта      = Новый Структура;
	ДанныеЗаполненияШапки = Новый Структура;
	
	Товары 		   = Документы.КорректировкаПоступления.ПустаяСсылка().Товары.ВыгрузитьКолонки();
	Услуги 		   = Документы.КорректировкаПоступления.ПустаяСсылка().Услуги.ВыгрузитьКолонки();
	СерийныеНомера = Документы.КорректировкаПоступления.ПустаяСсылка().СерийныеНомера.ВыгрузитьКолонки();
	КлючСвязи = 1;
	
	Для Каждого СтрокаРеквизита Из СтрокаДляЗагрузки.Строки Цикл
		
		Если СтрокаРеквизита.Реквизит = "СписокОписаний" Тогда
			 
			СтрокаРеквизитаОписанийРабот = ПолучитьЗначениеРеквизитаДерева(СтрокаРеквизита, СтрокаРеквизита.Реквизит, Истина, ДеревоРазбора);
			
			Для Каждого СтрокаРеквизитаОписания Из СтрокаРеквизитаОписанийРабот.Строки Цикл
				
				Если СтрокаРеквизитаОписания.Строки.Количество() = 0 Тогда
					ЗначениеРеквизита = ПолучитьЗначениеРеквизитаДерева(СтрокаРеквизитаОписания, СтрокаРеквизитаОписания.Реквизит, Истина, ДеревоРазбора);
					Если ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
						ДанныеЗаполненияШапки.Вставить(СтрокаРеквизитаОписания.Реквизит, ЗначениеРеквизита);
					КонецЕсли;
				Иначе
					ДанныеДляЗаполненияСтрокиТЧ = ПолучитьДанныеСтрокиТЧ(СтрокаРеквизитаОписания.Строки, ДеревоРазбора);
					ЗаполнитьЗначенияСвойств(Услуги.Добавить(), ДанныеДляЗаполненияСтрокиТЧ);
				КонецЕсли;
				
			КонецЦикла;
			
		Иначе
			
			Если СтрокаРеквизита.Строки.Количество() = 0 Тогда 
				
				// Заполним реквизит шапки
				ЗначениеРеквизита = ПолучитьЗначениеРеквизитаДерева(СтрокаРеквизита, СтрокаРеквизита.Реквизит, Истина, ДеревоРазбора);
				
				Если СтрокаРеквизита.Реквизит = "ДатаПоДаннымКлиента" Тогда
					ИмяРеквизита = "ДатаПоДаннымПокупателя";
				ИначеЕсли СтрокаРеквизита.Реквизит = "НомерПоДаннымКлиента" Тогда
					ИмяРеквизита = "НомерПоДаннымПокупателя";
				ИначеЕсли СтрокаРеквизита.Реквизит = "Валюта" Тогда
					ИмяРеквизита = "ВалютаДокумента";
				ИначеЕсли СтрокаРеквизита.Реквизит = "ДокументОснования" ИЛИ 
					СтрокаРеквизита.Реквизит = "ДокументыОснования" Тогда
					ИмяРеквизита = "ДокументПоступления";
				Иначе
					ИмяРеквизита = СтрокаРеквизита.Реквизит;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
					ДанныеЗаполненияШапки.Вставить(ИмяРеквизита, ЗначениеРеквизита);
				КонецЕсли;
				
			Иначе 
				
				// Добавим строку табличной части
				ДанныеДляЗаполненияСтрокиТЧ = ПолучитьДанныеСтрокиТЧ(СтрокаРеквизита.Строки, ДеревоРазбора);
				
				Если ТипЗнч(ДанныеДляЗаполненияСтрокиТЧ.Номенклатура) = Тип("СправочникСсылка.Номенклатура")
					 И ДанныеДляЗаполненияСтрокиТЧ.Номенклатура.Услуга Тогда
					 НоваяСтрока = Услуги.Добавить();
					 НоваяСтрока.Содержание = ДанныеДляЗаполненияСтрокиТЧ.Номенклатура.НаименованиеПолное;
				Иначе
					 НоваяСтрока = Товары.Добавить();
					 
					 ДобавитьСерийныеНомера(НоваяСтрока, КлючСвязи, ДанныеДляЗаполненияСтрокиТЧ, СерийныеНомера);
					 
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеДляЗаполненияСтрокиТЧ);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ДанныеЗаполненияШапки.Вставить("ВидОперации", ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "ВидОперации", Истина, ДеревоРазбора));
	
	// спец. значения 
	Если ЗначениеЗаполнено(ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "НомерСчетаФактуры")) Тогда // указана счет-фактура
		ДанныеЗаполненияШапки.Вставить("НомерСчетаФактуры",ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "НомерСчетаФактуры"));
		ДанныеЗаполненияШапки.Вставить("ДатаСчетаФактуры", ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "ДатаСчетаФактуры"));
	Иначе
		ДанныеЗаполненияШапки.Вставить("НомерВходящегоДокумента",ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "Номер"));
		ДанныеЗаполненияШапки.Вставить("ДатаВходящегоДокумента", ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "Дата"));
	КонецЕсли;
	
	ДобавитьОбязательныеПоля(ДанныеЗаполненияШапки);
	
	ДанныеДляОбъекта.Вставить("Шапка",  ДанныеЗаполненияШапки);
	ДанныеДляОбъекта.Вставить("Товары", Товары);
	ДанныеДляОбъекта.Вставить("Услуги", Услуги);
	ДанныеДляОбъекта.Вставить("СерийныеНомера", СерийныеНомера);
	
	Возврат ДанныеДляОбъекта;
	
КонецФункции

Функция ПодготовитьСтруктуруДляСчетаНаОплатуПоставщика(СтрокаДляЗагрузки, ДеревоРазбора)

	ДанныеДляОбъекта      = Новый Структура;
	ДанныеЗаполненияШапки = Новый Структура;
	
	Товары = Документы.СчетНаОплатуПоставщика.ПустаяСсылка().Товары.ВыгрузитьКолонки();
	Услуги = Документы.СчетНаОплатуПоставщика.ПустаяСсылка().Услуги.ВыгрузитьКолонки();
	
	Для Каждого СтрокаРеквизита Из СтрокаДляЗагрузки.Строки Цикл
		
		Если СтрокаРеквизита.Строки.Количество()=0 Тогда // примитивный тип
			
			ЗначениеРеквизита = ПолучитьЗначениеРеквизитаДерева(СтрокаРеквизита, СтрокаРеквизита.Реквизит, Истина, ДеревоРазбора);
			
			Если СтрокаРеквизита.Реквизит = "ЦенаВключаетНДС" Тогда
				ИмяРеквизита = "СуммаВключаетНДС";
			ИначеЕсли СтрокаРеквизита.Реквизит = "Валюта" Тогда
				ИмяРеквизита = "ВалютаДокумента";
			ИначеЕсли СтрокаРеквизита.Реквизит = "Курс" Тогда
				ИмяРеквизита = "КурсВзаиморасчетов";
			Иначе
				ИмяРеквизита = СтрокаРеквизита.Реквизит;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ЗначениеРеквизита) ИЛИ ЗначениеРеквизита = 0 Тогда
				ДанныеЗаполненияШапки.Вставить(ИмяРеквизита, ЗначениеРеквизита);
			КонецЕсли;
			
		ИначеЕсли СтрокаРеквизита.Реквизит = "СтрокаТЧ" Тогда
			
			// Добавим строку табличной части
			ДанныеДляЗаполненияСтрокиТЧ = ПолучитьДанныеСтрокиТЧ(СтрокаРеквизита.Строки, ДеревоРазбора);
			
			Если ТипЗнч(ДанныеДляЗаполненияСтрокиТЧ.Номенклатура) = Тип("СправочникСсылка.Номенклатура")
				 И ДанныеДляЗаполненияСтрокиТЧ.Номенклатура.Услуга Тогда
				НоваяСтрока = Услуги.Добавить();
			Иначе
				НоваяСтрока = Товары.Добавить();
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеДляЗаполненияСтрокиТЧ);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ДобавитьОбязательныеПоля(ДанныеЗаполненияШапки);
	
	ДанныеДляОбъекта.Вставить("Шапка",  ДанныеЗаполненияШапки);
	ДанныеДляОбъекта.Вставить("Товары", Товары);
	ДанныеДляОбъекта.Вставить("Услуги", Услуги);
	
	Возврат ДанныеДляОбъекта;
	
КонецФункции

// Находит документ ИБ по параметрам.
//
// Параметры:
//  ВидЭД - Перечисления.ВидыЭД - Вид электронного документа, по которому ищется документ ИБ,
//  Контрагент - Ссылка на контрагента,
//  РеквизитыИБ - структура параметров информационной базы,
//  РеквизитыИБКонтрагента - структура параметров контрагента в информационной базе.
//
Функция НайтиДокумент(ВидЭД, Контрагент, РеквизитыИБ = Неопределено, РеквизитыИБКонтрагента = Неопределено)
	
	НайденныйДок = Неопределено;
	
	УстановитьПривилегированныйРежим(Истина);
	
	НайденныйДок = Неопределено;
	Если ВидЭД = Перечисления.ВидыЭД.ЗаказТовара Тогда
		Запрос = Новый Запрос;	
		ОсновнойТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДокументПоиска.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ЗаказПокупателя КАК ДокументПоиска
		|ГДЕ
		|	ДокументПоиска.Контрагент = &Контрагент";
		
		// будем искать по нашим реквизитам
		Если РеквизитыИБ <> Неопределено И РеквизитыИБ.Количество()>0 Тогда
			Запрос.Текст = ОсновнойТекстЗапроса;
			Запрос.УстановитьПараметр("Контрагент", Контрагент);
			Для Каждого ТекЭл Из РеквизитыИБ Цикл
				Если Найти(ВРег(ТекЭл.Ключ), ВРег("Дата"))>0 Тогда
					Запрос.Текст = Запрос.Текст+" И КОНЕЦПЕРИОДА(ДокументПоиска."+ТекЭл.Ключ+", ДЕНЬ) =  КОНЕЦПЕРИОДА(&"+ТекЭл.Ключ+", ДЕНЬ)";
				Иначе
					Запрос.Текст = Запрос.Текст+" И ДокументПоиска."+ТекЭл.Ключ+" = &"+ТекЭл.Ключ;
				КонецЕсли;
				Запрос.УстановитьПараметр(ТекЭл.Ключ, ТекЭл.Значение);
			КонецЦикла;   
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				НайденныйДок = Выборка.Ссылка;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(НайденныйДок) И РеквизитыИБКонтрагента <> Неопределено
			И РеквизитыИБКонтрагента.Количество()>0 Тогда // не нашли по нашим реквизитам, искать будем по данным контрагента
			Запрос.Текст = ОсновнойТекстЗапроса;
			Запрос.УстановитьПараметр("Контрагент", Контрагент);
			Для Каждого ТекЭл Из РеквизитыИБКонтрагента Цикл
				Запрос.Текст = Запрос.Текст+" И ДокументПоиска."+ТекЭл.Ключ+" = &"+ТекЭл.Ключ;
				Запрос.УстановитьПараметр(ТекЭл.Ключ, ТекЭл.Значение);
			КонецЦикла;   
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				НайденныйДок = Выборка.Ссылка;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ВидЭД = Перечисления.ВидыЭД.ОтветНаЗаказ Тогда
		Запрос = Новый Запрос;
		ОсновнойТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДокументПоиска.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ЗаказПоставщику КАК ДокументПоиска
		|ГДЕ
		|	ДокументПоиска.Контрагент = &Контрагент";
		
		// будем искать по нашим реквизитам
		Если РеквизитыИБ <> Неопределено И РеквизитыИБ.Количество()>0  Тогда
			Запрос.Текст = ОсновнойТекстЗапроса;
			Запрос.УстановитьПараметр("Контрагент", Контрагент);
			Для Каждого ТекЭл Из РеквизитыИБ Цикл
				Если Найти(ВРег(ТекЭл.Ключ), ВРег("Дата"))>0 Тогда
					Запрос.Текст = Запрос.Текст+" И КОНЕЦПЕРИОДА(ДокументПоиска."+ТекЭл.Ключ+", ДЕНЬ) =  КОНЕЦПЕРИОДА(&"+ТекЭл.Ключ+", ДЕНЬ)";
				Иначе
					Запрос.Текст = Запрос.Текст+" И ДокументПоиска."+ТекЭл.Ключ+" = &"+ТекЭл.Ключ;
				КонецЕсли;
				Запрос.УстановитьПараметр(ТекЭл.Ключ, ТекЭл.Значение);
			КонецЦикла;   
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				НайденныйДок = Выборка.Ссылка;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(НайденныйДок) И РеквизитыИБКонтрагента <> Неопределено 
			И РеквизитыИБКонтрагента.Количество()>0 Тогда // не нашли по нашим реквизитам, искать будем по данным контрагента
			Запрос.Текст = ОсновнойТекстЗапроса;
			Запрос.УстановитьПараметр("Контрагент", Контрагент);
			Для Каждого ТекЭл Из РеквизитыИБКонтрагента Цикл
				Запрос.Текст = Запрос.Текст+" И ДокументПоиска."+ТекЭл.Ключ+" = &"+ТекЭл.Ключ;
				Запрос.УстановитьПараметр(ТекЭл.Ключ, ТекЭл.Значение);
			КонецЦикла;   
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				НайденныйДок = Выборка.Ссылка;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат НайденныйДок;
	
КонецФункции

Функция НайтиСоздатьПоступлениеТоваровУслуг(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца = Неопределено)
	
	ВозвращаемоеЗначение = Неопределено;
	
	ДанныеДляЗагрузки = ПодготовитьСтруктуруДляПоступленияТоваровУслуг(СтрокаДляЗагрузки, ДеревоРазбора);
	
	ДанныеЗаполнения = ДанныеДляЗагрузки.Шапка;
	РежимЗаписи      = РежимЗаписиДокумента.Запись;
	
	Попытка
		
		Если ЗначениеЗаполнено(СсылкаНаВладельца) Тогда // получены изменения по существующему документу
			
			Если СсылкаНаВладельца.Проведен Тогда 
				ВызватьИсключение НСтр("ru = 'Заполнение на основании ЭД возможно только для непроведенного документа'");
			КонецЕсли;
			
			ДокументОбъект = СсылкаНаВладельца.ПолучитьОбъект();
			
		Иначе  // создаем новый
			УстановитьПривилегированныйРежим(Истина);
			ДокументОбъект = Документы.ПоступлениеТоваровУслуг.СоздатьДокумент();
			ДокументОбъект.Организация = ДанныеЗаполнения.Организация;
			ДокументОбъект.Контрагент = ДанныеЗаполнения.Контрагент;
			ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ДокументОбъект, глЗначениеПеременной("глТекущийПользователь"), , "Покупка");
			ДокументОбъект.Дата = ТекущаяДатаСеанса();
			
		КонецЕсли;
		ДокументОбъект.ОбменДанными.Загрузка = Истина; 
		
		// попробуем найти заказ поставщику
		ЗаказПоставщику = Неопределено;
		Если ДанныеЗаполнения.Свойство("Контрагент") Тогда
			РеквизитыИБКонтрагента = Новый Структура;
			РеквизитыИБ = Новый Структура;
			Если ДанныеЗаполнения.Свойство("НомерПоДаннымПокупателя") И ЗначениеЗаполнено(ДанныеЗаполнения.НомерПоДаннымПокупателя) Тогда
				РеквизитыИБ.Вставить("Номер", ДанныеЗаполнения.НомерПоДаннымПокупателя);
			КонецЕсли;
			Если ДанныеЗаполнения.Свойство("ДатаПоДаннымПокупателя") И ЗначениеЗаполнено(ДанныеЗаполнения.ДатаПоДаннымПокупателя) Тогда
				РеквизитыИБ.Вставить("Дата", ДанныеЗаполнения.ДатаПоДаннымПокупателя);
			КонецЕсли;
			Если РеквизитыИБ.Количество()>0 ИЛИ РеквизитыИБКонтрагента.Количество()>0 Тогда // есть еще реквизиты поиска, кроме Контрагента
				ЗаказПоставщику = НайтиДокумент(Перечисления.ВидыЭД.ОтветНаЗаказ, ДанныеЗаполнения.Контрагент, РеквизитыИБ, РеквизитыИБКонтрагента);
			КонецЕсли;
		КонецЕсли;
		
		// Заполним реквизиты шапки на основании структуры данных заполнения
		ПерезаполнениеЗначенийРеквизитовШапки(ДокументОбъект, ДанныеЗаполнения);
		
		// Загрузим табличные части
		ДокументОбъект.Товары.Загрузить(ДанныеДляЗагрузки.Товары);
		ДокументОбъект.Услуги.Загрузить(ДанныеДляЗагрузки.Услуги);
		ДокументОбъект.СерийныеНомера.Загрузить(ДанныеДляЗагрузки.СерийныеНомера);
		
		// Установим вручную некоторые реквизиты шапки
		ДокументОбъект.УчитыватьНДС     = Истина;
		ДокументОбъект.СуммаДокумента   = УчетНДС.ПолучитьСуммуДокументаСНДС(ДокументОбъект);
		Если НЕ ЗначениеЗаполнено(ДокументОбъект.ВалютаДокумента) Тогда
			ДокументОбъект.ВалютаДокумента = глЗначениеПеременной("ВалютаРегламентированногоУчета");
		КонецЕсли;
		
		// Заполним курс и кратность
		КурсВалюты = МодульВалютногоУчета.ПолучитьКурсВалюты(ДокументОбъект.ВалютаДокумента, ДокументОбъект.Дата);
		ДокументОбъект.КурсВзаиморасчетов      = КурсВалюты.Курс;
		ДокументОбъект.КратностьВзаиморасчетов = КурсВалюты.Кратность;
		
		Если ЗначениеЗаполнено(ЗаказПоставщику) Тогда
			ДокументОбъект.Сделка = ЗаказПоставщику;
		Иначе
			Если ДанныеЗаполнения.Свойство("Основание", ЗаказПоставщику) И ТипЗнч(ЗаказПоставщику) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
				ДокументОбъект.Сделка = ЗаказПоставщику;
			КонецЕсли;
		КонецЕсли;
		
		ДокументОбъект.ДополнительныеСвойства.Вставить("ЕстьСоглашение", Истина);
		
		Если ДокументОбъект.ЭтоНовый() Тогда
			ДокументОбъект.УстановитьНовыйНомер();
		КонецЕсли;
		
		ВозвращаемоеЗначение = ДокументОбъект;
		
	Исключение
		ОписаниеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Заполнение на основании ЭД'"),
			УровеньЖурналаРегистрации.Ошибка, 
			Метаданные.Документы.ПоступлениеТоваровУслуг, 
			СсылкаНаВладельца, 
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение ОписаниеОшибки;
		
	КонецПопытки;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция НайтиСоздатьЗаказПокупателя(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца = Неопределено)
	
	ВозвращаемоеЗначение = Неопределено;
	
	ДанныеДляЗагрузки = ПодготовитьСтруктуруДляЗаказаПокупателя(СтрокаДляЗагрузки, ДеревоРазбора);
	
	ДанныеЗаполнения = ДанныеДляЗагрузки.Шапка;
	РежимЗаписи      = РежимЗаписиДокумента.Запись;
	
	Попытка
		
		Если ЗначениеЗаполнено(СсылкаНаВладельца) Тогда // получены изменения по существующему документу
			
			Если СсылкаНаВладельца.Проведен Тогда 
				ВызватьИсключение НСтр("ru = 'Заполнение на основании ЭД возможно только для непроведенного документа'");
			КонецЕсли;
			
			ДокументОбъект = СсылкаНаВладельца.ПолучитьОбъект();
			
		Иначе // попробуем найти по РеквизитыИБ и РеквизитыИБКонтрагента
			
			УстановитьПривилегированныйРежим(Истина);
			
			НайденныйДок = Неопределено;
			Если ДанныеЗаполнения.Свойство("Контрагент") Тогда
				РеквизитыИБКонтрагента = Новый Структура;
				РеквизитыИБ = Новый Структура;
				Если ДанныеЗаполнения.Свойство("НомерПоДаннымПоставщика") И ЗначениеЗаполнено(ДанныеЗаполнения.НомерПоДаннымПоставщика) Тогда
					РеквизитыИБ.Вставить("Номер", ДанныеЗаполнения.НомерПоДаннымПоставщика);
				КонецЕсли;
				Если ДанныеЗаполнения.Свойство("ДатаПоДаннымПоставщика") И ЗначениеЗаполнено(ДанныеЗаполнения.ДатаПоДаннымПоставщика) Тогда
					РеквизитыИБ.Вставить("Дата", ДанныеЗаполнения.ДатаПоДаннымПоставщика);
				КонецЕсли;
				Если РеквизитыИБ.Количество()>0 ИЛИ РеквизитыИБКонтрагента.Количество()>0 Тогда // есть еще реквизиты поиска, кроме Контрагента
					НайденныйДок = НайтиДокумент(Перечисления.ВидыЭД.ЗаказТовара, ДанныеЗаполнения.Контрагент, РеквизитыИБ, РеквизитыИБКонтрагента);
				КонецЕсли;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(НайденныйДок) Тогда // нашли документ, вернем ссылку, чтоб просто привязать ЭД
				ДокументОбъект = НайденныйДок.ПолучитьОбъект();
				Возврат ДокументОбъект.Ссылка;
			КонецЕсли;
			
			ДокументОбъект = Документы.ЗаказПокупателя.СоздатьДокумент();
			ДокументОбъект.Организация = ДанныеЗаполнения.Организация;
			ДокументОбъект.Контрагент = ДанныеЗаполнения.Контрагент;
			ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ДокументОбъект, глЗначениеПеременной("глТекущийПользователь"), , "Продажа");
			ДокументОбъект.Дата = ТекущаяДатаСеанса();
			
		КонецЕсли;
		
		ДокументОбъект.ОбменДанными.Загрузка = Истина;
		
		// Заполним значения реквизитов шапки
		ПерезаполнениеЗначенийРеквизитовШапки(ДокументОбъект, ДанныеЗаполнения);
		
		ДанныеДляЗагрузки.Шапка.Свойство("СуммаИтог", ДокументОбъект.СуммаДокумента);
		ДокументОбъект.КратностьВзаиморасчетов 	= 1;
		
		Если ДокументОбъект.ЭтоНовый() И ЗначениеЗаполнено(ДокументОбъект.Организация) Тогда
			ДокументОбъект.УчитыватьНДС = УчетНДС.ОрганизацияУчитываетНДС(ДокументОбъект.Организация, ДокументОбъект.Дата);
		Иначе
			ДокументОбъект.УчитыватьНДС = Истина;
		КонецЕсли;
		
		// Загрузим табличные части
		ДокументОбъект.Товары.Загрузить(ДанныеДляЗагрузки.Товары);
		ДокументОбъект.Услуги.Загрузить(ДанныеДляЗагрузки.Услуги);
		
		Если ДокументОбъект.ЭтоНовый() Тогда
			ДокументОбъект.УстановитьНовыйНомер();
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДокументОбъект.Ответственный) Тогда
			ДокументОбъект.Ответственный = ПользователиКлиентСервер.АвторизованныйПользователь();
		КонецЕсли;
		
		ВозвращаемоеЗначение = ДокументОбъект;
		
	Исключение
		
		ОписаниеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначения.СообщитьОбОшибке(ОписаниеОшибки);
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Заполнение на основании ЭД'"), 
			УровеньЖурналаРегистрации.Ошибка, 
			Метаданные.Документы.ЗаказПокупателя, 
			СсылкаНаВладельца, 
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
		
	КонецПопытки;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция НайтиСоздатьЗаказПоставщику(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца = Неопределено)
	
	ВозвращаемоеЗначение = Неопределено;
	
	ДанныеДляЗагрузки = ПодготовитьСтруктуруДляЗаказаПоставщику(СтрокаДляЗагрузки, ДеревоРазбора);
	
	ДанныеЗаполнения = ДанныеДляЗагрузки.Шапка;
	РежимЗаписи      = РежимЗаписиДокумента.Запись;
	
	Попытка
		
		Если ЗначениеЗаполнено(СсылкаНаВладельца) Тогда // получены изменения по существующему документу
			
			Если СсылкаНаВладельца.Проведен Тогда 
				ВызватьИсключение НСтр("ru = 'Заполнение на основании ЭД возможно только для непроведенного документа'");
			КонецЕсли;
			
			ДокументОбъект = СсылкаНаВладельца.ПолучитьОбъект();
			
		Иначе // попробуем найти по РеквизитыИБ и РеквизитыИБКонтрагента
			
			УстановитьПривилегированныйРежим(Истина);
			
			НайденныйДок = Неопределено;
			Если ДанныеЗаполнения.Свойство("Контрагент") Тогда
				РеквизитыИБКонтрагента = Новый Структура;
				РеквизитыИБ = Новый Структура;
				Если ДанныеЗаполнения.Свойство("НомерПоДаннымПокупателя") И ЗначениеЗаполнено(ДанныеЗаполнения.НомерПоДаннымПокупателя) Тогда
					РеквизитыИБ.Вставить("Номер", ДанныеЗаполнения.НомерПоДаннымПокупателя);
				КонецЕсли;
				Если ДанныеЗаполнения.Свойство("ДатаПоДаннымПокупателя") И ЗначениеЗаполнено(ДанныеЗаполнения.ДатаПоДаннымПокупателя) Тогда
					РеквизитыИБ.Вставить("Дата", ДанныеЗаполнения.ДатаПоДаннымПокупателя);
				КонецЕсли;
				Если РеквизитыИБ.Количество()>0 ИЛИ РеквизитыИБКонтрагента.Количество()>0 Тогда // есть еще реквизиты поиска, кроме Контрагента
					НайденныйДок = НайтиДокумент(Перечисления.ВидыЭД.ОтветНаЗаказ, ДанныеЗаполнения.Контрагент, РеквизитыИБ, РеквизитыИБКонтрагента);
				КонецЕсли;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(НайденныйДок) Тогда // нашли документ, вернем ссылку, чтоб просто привязать ЭД
				ДокументОбъект = НайденныйДок.ПолучитьОбъект();
				Возврат ДокументОбъект.Ссылка;
			КонецЕсли;
			
			ДокументОбъект = Документы.ЗаказПоставщику.СоздатьДокумент();
			ДокументОбъект.Организация = ДанныеЗаполнения.Организация;
			ДокументОбъект.Контрагент = ДанныеЗаполнения.Контрагент;
			ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ДокументОбъект, глЗначениеПеременной("глТекущийПользователь"), , "Покупка");
			ДокументОбъект.Дата = ТекущаяДатаСеанса();
			
		КонецЕсли;
		
		ДокументОбъект.ОбменДанными.Загрузка = Истина;
		
		// Заполним значения реквизитов шапки
		ПерезаполнениеЗначенийРеквизитовШапки(ДокументОбъект, ДанныеЗаполнения);
		
		ДанныеДляЗагрузки.Шапка.Свойство("СуммаИтог", ДокументОбъект.СуммаДокумента);
		ДокументОбъект.КратностьВзаиморасчетов 	= 1;
		
		Если ДокументОбъект.ЭтоНовый() И ЗначениеЗаполнено(ДокументОбъект.Организация) Тогда
			ДокументОбъект.УчитыватьНДС = УчетНДС.ОрганизацияУчитываетНДС(ДокументОбъект.Организация, ДокументОбъект.Дата);
		Иначе
			ДокументОбъект.УчитыватьНДС = Истина;
		КонецЕсли;
		
		// Загрузим табличные части
		ДокументОбъект.Товары.Загрузить(ДанныеДляЗагрузки.Товары);
		ДокументОбъект.Услуги.Загрузить(ДанныеДляЗагрузки.Услуги);
		
		Если ДокументОбъект.ЭтоНовый() Тогда
			ДокументОбъект.УстановитьНовыйНомер();
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДокументОбъект.Ответственный) Тогда
			ДокументОбъект.Ответственный = ПользователиКлиентСервер.АвторизованныйПользователь();
		КонецЕсли;
		
		ВозвращаемоеЗначение = ДокументОбъект;
		
	Исключение
		
		ОписаниеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначения.СообщитьОбОшибке(ОписаниеОшибки);
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Заполнение на основании ЭД'"),
			УровеньЖурналаРегистрации.Ошибка, 
			Метаданные.Документы.ЗаказПоставщику, 
			СсылкаНаВладельца, 
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
		
	КонецПопытки;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция НайтиСоздатьСчетФактуру(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца = Неопределено)
	
	ВозвращаемоеЗначение = Неопределено;
	
	ДанныеДляЗагрузки = ПодготовитьСтруктуруДляСчетаФактуры(СтрокаДляЗагрузки, ДеревоРазбора);
	
	РежимЗаписи = РежимЗаписиДокумента.Запись;
	
	Попытка
		
		Если ЗначениеЗаполнено(СсылкаНаВладельца) Тогда
			
			Если СсылкаНаВладельца.Проведен Тогда 
				ВызватьИсключение НСтр("ru = 'Заполнение на основании ЭД возможно только для непроведенного документа'");
			КонецЕсли;
			
			ДокументОбъект = СсылкаНаВладельца.ПолучитьОбъект();
			
		Иначе
			
			УстановитьПривилегированныйРежим(Истина);
			ДокументОбъект = Документы.СчетФактураПолученный.СоздатьДокумент();
			ДокументОбъект.Заполнить(ДанныеДляЗагрузки);
			ДокументОбъект.Дата = ТекущаяДатаСеанса();
			
		КонецЕсли;
		
		ДокументОбъект.ОбменДанными.Загрузка = Истина; 
		
		ДокументОбъект.ДополнительныеСвойства.Вставить("ЕстьСоглашение", Истина);
		
		// Заполним ТЧ ДокументыОснования
		Если ДанныеДляЗагрузки.Свойство("ДокументыОснования") Тогда
			ДокументОбъект.ДокументыОснования.Очистить();
			Для Каждого ДокументОснование Из ДанныеДляЗагрузки.ДокументыОснования Цикл
				НоваяСтрока = ДокументОбъект.ДокументыОснования.Добавить();
				НоваяСтрока.ДокументОснование = ДокументОснование;
				Если ДанныеДляЗагрузки.ВидСчетаФактуры  = Перечисления.ВидСчетаФактурыПолученного.Корректировочный Тогда
					НоваяСтрока.НомерИсходногоДокумента = ДанныеДляЗагрузки.НомерИсходногоДокумента;
					НоваяСтрока.ДатаИсходногоДокумента  = ДанныеДляЗагрузки.ДатаИсходногоДокумента;
					НоваяСтрока.СуммаУвеличение         = ДанныеДляЗагрузки.СуммаУвеличение;
					НоваяСтрока.СуммаУменьшение         = ДанныеДляЗагрузки.СуммаУменьшение;
					НоваяСтрока.СуммаНДСУвеличение      = ДанныеДляЗагрузки.СуммаНДСУвеличение;
					НоваяСтрока.СуммаНДСУменьшение      = ДанныеДляЗагрузки.СуммаНДСУменьшение;
					НоваяСтрока.УчитыватьИсправлениеИсходногоДокумента = ДанныеДляЗагрузки.Свойство("УчитыватьИсправлениеИсходногоДокумента");
					Если НоваяСтрока.УчитыватьИсправлениеИсходногоДокумента Тогда
						НоваяСтрока.НомерИсправленияИсходногоДокумента = ДанныеДляЗагрузки.НомерИсправленияИсходногоДокумента;
						НоваяСтрока.ДатаИсправленияИсходногоДокумента  = ДанныеДляЗагрузки.ДатаИсправленияИсходногоДокумента;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если ДокументОбъект.ДокументыОснования.Количество() = 0 Тогда
			НоваяСтрока = ДокументОбъект.ДокументыОснования.Добавить();
			Если ДанныеДляЗагрузки.ВидСчетаФактуры  = Перечисления.ВидСчетаФактурыПолученного.Корректировочный Тогда
				НоваяСтрока.НомерИсходногоДокумента = ДанныеДляЗагрузки.НомерИсходногоДокумента;
				НоваяСтрока.ДатаИсходногоДокумента  = ДанныеДляЗагрузки.ДатаИсходногоДокумента;
				НоваяСтрока.СуммаУвеличение         = ДанныеДляЗагрузки.СуммаУвеличение;
				НоваяСтрока.СуммаУменьшение         = ДанныеДляЗагрузки.СуммаУменьшение;
				НоваяСтрока.СуммаНДСУвеличение      = ДанныеДляЗагрузки.СуммаНДСУвеличение;
				НоваяСтрока.СуммаНДСУменьшение      = ДанныеДляЗагрузки.СуммаНДСУменьшение;
				НоваяСтрока.УчитыватьИсправлениеИсходногоДокумента = ДанныеДляЗагрузки.Свойство("УчитыватьИсправлениеИсходногоДокумента");
				Если НоваяСтрока.УчитыватьИсправлениеИсходногоДокумента Тогда
					НоваяСтрока.НомерИсправленияИсходногоДокумента = ДанныеДляЗагрузки.НомерИсправленияИсходногоДокумента;
					НоваяСтрока.ДатаИсправленияИсходногоДокумента  = ДанныеДляЗагрузки.ДатаИсправленияИсходногоДокумента;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если ДанныеДляЗагрузки.ВидСчетаФактуры  = Перечисления.ВидСчетаФактурыПолученного.Корректировочный Тогда
			Если ДанныеДляЗагрузки.Свойство("НомерИсходногоДокумента") Тогда
				ДанныеДляЗагрузки.Удалить("НомерИсходногоДокумента");
			КонецЕсли;
			Если ДанныеДляЗагрузки.Свойство("ДатаИсходногоДокумента") Тогда
				ДанныеДляЗагрузки.Удалить("ДатаИсходногоДокумента");
			КонецЕсли;
			Если ДанныеДляЗагрузки.Свойство("УчитыватьИсправлениеИсходногоДокумента") Тогда
				ДанныеДляЗагрузки.Удалить("УчитыватьИсправлениеИсходногоДокумента");
			КонецЕсли;
			Если ДанныеДляЗагрузки.Свойство("НомерИсправленияИсходногоДокумента") Тогда
				ДанныеДляЗагрузки.Удалить("НомерИсправленияИсходногоДокумента");
			КонецЕсли;
			Если ДанныеДляЗагрузки.Свойство("ДатаИсправленияИсходногоДокумента") Тогда
				ДанныеДляЗагрузки.Удалить("ДатаИсправленияИсходногоДокумента");
			КонецЕсли;
		КонецЕсли;
		
		Если ДанныеДляЗагрузки.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.НаАванс Тогда
			Если ДокументОбъект.Авансы.Количество() = 0 Тогда
				НоваяСтрока = ДокументОбъект.Авансы.Добавить();
			Иначе
				НоваяСтрока = ДокументОбъект.Авансы[0];
			КонецЕсли;
			НоваяСтрока.Сумма     = ДанныеДляЗагрузки.СуммаДокумента;
			НоваяСтрока.СуммаНДС  = ДанныеДляЗагрузки.СуммаНДСДокумента;
			НоваяСтрока.СтавкаНДС = ДанныеДляЗагрузки.СтавкаНДС;
		КонецЕсли;
		// Заполним реквизиты шапки на основании структуры данных заполнения
		ПерезаполнениеЗначенийРеквизитовШапки(ДокументОбъект, ДанныеДляЗагрузки);
		Если ДокументОбъект.ЭтоНовый() Тогда
			ДокументОбъект.УстановитьНовыйНомер();
		КонецЕсли;
		
		ДокументОбъект.УстановитьКодВидаОперации();
		ДокументОбъект.КодСпособаПолучения = 2;
		
		ВозвращаемоеЗначение = ДокументОбъект;
		
	Исключение
		
		ОписаниеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначения.СообщитьОбОшибке(ОписаниеОшибки);
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Заполнение на основании ЭД'"),
			УровеньЖурналаРегистрации.Ошибка, 
			Метаданные.Документы.СчетФактураПолученный, 
			СсылкаНаВладельца, 
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ВызватьИсключение;
		
	КонецПопытки;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция НайтиСоздатьКорректировкуПоступления(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца = Неопределено)
	
	ВозвращаемоеЗначение = Неопределено;
	
	ДанныеДляЗагрузки = ПодготовитьСтруктуруДляКорректировкиПоступления(СтрокаДляЗагрузки, ДеревоРазбора);
	
	ДанныеЗаполнения = ДанныеДляЗагрузки.Шапка;
	РежимЗаписи      = РежимЗаписиДокумента.Запись;
	
	Попытка
		
		Если ЗначениеЗаполнено(СсылкаНаВладельца) Тогда // получены изменения по существующему документу
			
			Если СсылкаНаВладельца.Проведен Тогда 
				ВызватьИсключение НСтр("ru = 'Заполнение на основании ЭД возможно только для непроведенного документа'");
			КонецЕсли;
			
			ДокументОбъект = СсылкаНаВладельца.ПолучитьОбъект();
			
		Иначе  // создаем новый
			
			УстановитьПривилегированныйРежим(Истина);
			ДокументОбъект = Документы.КорректировкаПоступления.СоздатьДокумент();
			ДокументОбъект.Дата = ТекущаяДатаСеанса();
			
		КонецЕсли;
		
		ДокументОбъект.ОбменДанными.Загрузка = Истина;
		
		ДокументПоступления = ДокументОбъект.ДокументПоступления;
		
		Если Не ЗначениеЗаполнено(ДокументПоступления) И ДанныеЗаполнения.Свойство("Основание") Тогда
			
			ДокументОснование = ДанныеЗаполнения.Основание;
			
			Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
				ОснованияСФ = ДокументОснование.ДокументыОснования;
				Если ОснованияСФ.Количество() > 0 Тогда
					ДокументПоступления = ОснованияСФ[0].ДокументОснование;
				КонецЕсли;
			Иначе
				ДокументПоступления = ДокументОснование;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДокументПоступления) Тогда
			// Заполним корректировку поступления данными основания
			ДокументОбъект.Заполнить(ДокументПоступления);
		Иначе
			// Заполним шапку по данным заполнения.
			ПерезаполнениеЗначенийРеквизитовШапки(ДокументОбъект, ДанныеЗаполнения);
		КонецЕсли;
		
		// Заполним табличные части данными корректировки
		ТабличныеЧастиДляЗаполнения = Новый Структура("Товары, Услуги");
		
		Для Каждого ТабличнаяЧасть Из ТабличныеЧастиДляЗаполнения Цикл
			
			ИмяТЧ = ТабличнаяЧасть.Ключ;
			
			СтруктураПоиска = Новый Структура("Номенклатура, СтавкаНДС");
			Если ИмяТЧ = "Товары" Тогда
				СтруктураПоиска.Вставить("ХарактеристикаНоменклатуры");
				СтруктураПоиска.Вставить("ЕдиницаИзмерения");
			КонецЕсли;
			
			Для Каждого СтрокаДокумента Из ДокументОбъект[ИмяТЧ] Цикл
				
				ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаДокумента);
				РезультатПоискаДанныхЗаполнения = ДанныеДляЗагрузки[ИмяТЧ].НайтиСтроки(СтруктураПоиска);
				
				Если РезультатПоискаДанныхЗаполнения.Количество() = 0 Тогда
					
					// Такой строки в данных корректировки нет - обнуляем данные строки документа.
					СтрокаДокумента.Количество = 0;
					СтрокаДокумента.Цена       = 0;
					СтрокаДокумента.Сумма      = 0;
					СтрокаДокумента.СуммаНДС   = 0;
					
				Иначе
					
					СтрокаДанныхЗаполнения = РезультатПоискаДанныхЗаполнения[0];
					СвойстваДляЗаполнения = "Количество, Цена, Сумма, СуммаНДС";
					Если ИмяТЧ = "Товары" Тогда
						СвойстваДляЗаполнения = СвойстваДляЗаполнения + ", КлючСвязи";
					КонецЕсли;
					ЗаполнитьЗначенияСвойств(СтрокаДокумента, СтрокаДанныхЗаполнения, СвойстваДляЗаполнения);
					
					// Удалим обработанную строку из данных заполнения
					ДанныеДляЗагрузки[ИмяТЧ].Удалить(СтрокаДанныхЗаполнения);
					
				КонецЕсли;
				
			КонецЦикла;
			
			// Добавим новые строки в документ
			Если ДанныеДляЗагрузки[ИмяТЧ].Количество() > 0 Тогда
				
				Для Каждого СтрокаДанныхЗаполнения Из ДанныеДляЗагрузки[ИмяТЧ] Цикл
					
					НоваяСтрока = ДокументОбъект[ИмяТЧ].Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДанныхЗаполнения);
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
		ДокументОбъект.СерийныеНомера.Загрузить(ДанныеДляЗагрузки.СерийныеНомера);
		
		Если ДанныеЗаполнения.ВидОперации = Перечисления.ВидыОперацийЭД.Исправление Тогда
			ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.ИсправлениеОшибки;
		Иначе
			ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение;
		КонецЕсли;
		
		ДокументОбъект.СуммаДокумента = УчетНДС.ПолучитьСуммуДокументаСНДС(ДокументОбъект);
		ДокументОбъект.ДополнительныеСвойства.Вставить("ЕстьСоглашение", Истина);
		
		Если ДокументОбъект.ЭтоНовый() Тогда
			ДокументОбъект.УстановитьНовыйНомер();
			ДокументОбъект.Дата = ТекущаяДатаСеанса();
		КонецЕсли;
		
		ВозвращаемоеЗначение = ДокументОбъект;
		
	Исключение
		
		ОписаниеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначения.СообщитьОбОшибке(ОписаниеОшибки);
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Заполнение на основании ЭД'"),
			УровеньЖурналаРегистрации.Ошибка, 
			Метаданные.Документы.ПоступлениеТоваровУслуг, 
			СсылкаНаВладельца, 
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
		
	КонецПопытки;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция НайтиСоздатьСчетНаОплатуПоставщика(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца = Неопределено)

	ВозвращаемоеЗначение = Неопределено;
	
	ДанныеДляЗагрузки = ПодготовитьСтруктуруДляСчетаНаОплатуПоставщика(СтрокаДляЗагрузки, ДеревоРазбора);
	
	ДанныеЗаполнения = ДанныеДляЗагрузки.Шапка;
	РежимЗаписи      = РежимЗаписиДокумента.Запись;
	
	Попытка
		
		Если ЗначениеЗаполнено(СсылкаНаВладельца) Тогда // получены изменения по существующему документу
			
			Если СсылкаНаВладельца.Проведен Тогда 
				ВызватьИсключение НСтр("ru = 'Заполнение на основании ЭД возможно только для непроведенного документа'");
			КонецЕсли;
			
			ДокументОбъект = СсылкаНаВладельца.ПолучитьОбъект();
			
		Иначе // попробуем найти по РеквизитыИБ и РеквизитыИБКонтрагента
			
			УстановитьПривилегированныйРежим(Истина);
			
			ДокументОбъект = Документы.СчетНаОплатуПоставщика.СоздатьДокумент();
			ДокументОбъект.Дата = ТекущаяДатаСеанса();
			ДокументОбъект.Организация = ДанныеЗаполнения.Организация;
			ДокументОбъект.Контрагент = ДанныеЗаполнения.Контрагент;
			ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ДокументОбъект, глЗначениеПеременной("глТекущийПользователь"), , "Покупка");
			ДокументОбъект.Дата = ТекущаяДатаСеанса();
			ДокументОбъект.Заполнить(ДанныеЗаполнения);
			
		КонецЕсли;
		
		ДокументОбъект.ОбменДанными.Загрузка = Истина;
		
		// Заполним значения реквизитов шапки
		ПерезаполнениеЗначенийРеквизитовШапки(ДокументОбъект, ДанныеЗаполнения);
		
		ДокументОбъект.КратностьВзаиморасчетов 	= 1;
		
		Если ДокументОбъект.ЭтоНовый() И ЗначениеЗаполнено(ДокументОбъект.Организация) Тогда
			ДокументОбъект.УчитыватьНДС = УчетНДС.ОрганизацияУчитываетНДС(ДокументОбъект.Организация, ДокументОбъект.Дата);
		Иначе
			ДокументОбъект.УчитыватьНДС = Истина;
		КонецЕсли;
		
		// Загрузим табличные части
		ДокументОбъект.Товары.Загрузить(ДанныеДляЗагрузки.Товары);
		ДокументОбъект.Услуги.Загрузить(ДанныеДляЗагрузки.Услуги);
		
		ДокументОбъект.СуммаДокумента = УчетНДС.ПолучитьСуммуДокументаСНДС(ДокументОбъект);
		
		Если ДокументОбъект.ЭтоНовый() Тогда
			ДокументОбъект.УстановитьНовыйНомер();
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДокументОбъект.Ответственный) Тогда
			ДокументОбъект.Ответственный = ПользователиКлиентСервер.АвторизованныйПользователь();
		КонецЕсли;
		
		ВозвращаемоеЗначение = ДокументОбъект;
		
	Исключение
		
		ОписаниеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначения.СообщитьОбОшибке(ОписаниеОшибки);
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Заполнение на основании ЭД'"), 
			УровеньЖурналаРегистрации.Ошибка, 
			Метаданные.Документы.ЗаказПокупателя, 
			СсылкаНаВладельца, 
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
		
	КонецПопытки;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция НайтиСоздатьКонтрагента(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца = Неопределено, Записывать = Истина)
	
	Если ЗначениеЗаполнено(СсылкаНаВладельца) Тогда
		КонтрагентОбъект = СсылкаНаВладельца.ПолучитьОбъект()
	Иначе
		КонтрагентОбъект = Справочники.Контрагенты.СоздатьЭлемент();
		ОсновнойСтатусКонтрагента = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), 
			"ОсновнойСтатусКонтрагента");
		КонтрагентОбъект.Покупатель = (ОсновнойСтатусКонтрагента = Перечисления.СтатусыКонтрагентов.Покупатель);
		КонтрагентОбъект.Поставщик  = (ОсновнойСтатусКонтрагента = Перечисления.СтатусыКонтрагентов.Поставщик);
	КонецЕсли;
	
	НаименованиеПолное = ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "ПолноеНаименование", Истина, ДеревоРазбора);
	Если Не ЗначениеЗаполнено(НаименованиеПолное) Тогда
		НаименованиеПолное = ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "ОфициальноеНаименование", Истина, ДеревоРазбора);
	КонецЕсли;
	КонтрагентОбъект.НаименованиеПолное = НаименованиеПолное;		
	Наименование = ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "Наименование", Истина, ДеревоРазбора);
	Если ЗначениеЗаполнено(Наименование) Тогда
		КонтрагентОбъект.Наименование = Наименование;
	Иначе
		КонтрагентОбъект.Наименование = НаименованиеПолное;
	КонецЕсли;
	КонтрагентОбъект.ИНН = ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "ИНН", Истина, ДеревоРазбора);
	
	Если СтрДлина(КонтрагентОбъект.ИНН) = 10 Тогда
		КонтрагентОбъект.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо;
		ЮрЛицо = Истина;
		
		КПП = ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "КПП", Истина, ДеревоРазбора);
		Если ЗначениеЗаполнено(КПП) Тогда
			КонтрагентОбъект.КПП = КПП;
		КонецЕсли;
	Иначе
		КонтрагентОбъект.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо;
		ЮрЛицо = Ложь;
		
		Док = ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "УдостоверениеЛичности", Истина, ДеревоРазбора);
		Если ЗначениеЗаполнено(Док) Тогда
			КонтрагентОбъект.ДокументУдостоверяющийЛичность = Док;
		КонецЕсли;
	КонецЕсли;
	
	ОКПО = ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "ОКПО", Истина, ДеревоРазбора);
	Если ЗначениеЗаполнено(ОКПО) Тогда
		КонтрагентОбъект.КодПоОКПО = ОКПО;
	КонецЕсли;
	
	Если Записывать Тогда
		Если КонтрагентОбъект.ЭтоНовый() Тогда
			КонтрагентОбъект.Записать();
		КонецЕсли;
		КонтрагентОбъект.Записать();
		ВозвращаемоеЗначение = КонтрагентОбъект.Ссылка;
	Иначе
		ВозвращаемоеЗначение = КонтрагентОбъект;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(КонтрагентОбъект.Ссылка) Тогда
		// Объект не записан
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	ЗаписатьЕщеРаз = Ложь;
	
	// Банковский счет
	НомерСчета = ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "НомерСчета", Истина, ДеревоРазбора);
	Если ЗначениеЗаполнено(НомерСчета) Тогда
		БанкБИК = ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "БанкБИК", Истина, ДеревоРазбора);
		Если ЗначениеЗаполнено(БанкБИК) Тогда
			ДопПараметры = Новый Структура("Владелец", КонтрагентОбъект.Ссылка);
			БанковскийСчет = НайтиСсылкуНаОбъект("БанковскиеСчетаКонтрагентов", НомерСчета, ДопПараметры);
			Если Не ЗначениеЗаполнено(БанковскийСчет) Тогда
				Банк = НайтиСсылкуНаОбъект("Банки", БанкБик);
				Если ЗначениеЗаполнено(Банк) Тогда
					БанковскийСчет = Справочники.БанковскиеСчета.СоздатьЭлемент();
					БанковскийСчет.Владелец = КонтрагентОбъект.Ссылка;
					БанковскийСчет.НомерСчета = НомерСчета;
					БанковскийСчет.Банк = Банк;
					БанковскийСчет.ВалютаДенежныхСредств = глЗначениеПеременной("ВалютаРегламентированногоУчета");
					БанковскийСчет.ВидСчета = "Расчетный";
					БанковскийСчет.Наименование = Банк.Наименование + " (" + БанковскийСчет.ВидСчета + ")";
					БанковскийСчет.Записать();
					
					Если Не ЗначениеЗаполнено(КонтрагентОбъект.ОсновнойБанковскийСчет) Тогда
						КонтрагентОбъект.ОсновнойБанковскийСчет = БанковскийСчет.Ссылка;
						ЗаписатьЕщеРаз = Истина;
					КонецЕсли;
					
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ИндексКонтрагента = ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "Контрагент");
	СтрокаКонтрагентов = ДеревоРазбора.Строки.Найти("Контрагенты", "ТипОбъекта");
	Если ЗначениеЗаполнено(СтрокаКонтрагентов) Тогда
		СтрокаКонтрагента = СтрокаКонтрагентов.Строки.Найти(ИндексКонтрагента, "ИндексСтроки");
	КонецЕсли;
	Если Не ЗначениеЗаполнено(СтрокаКонтрагента) Тогда
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	// Адрес
	Адрес = ПолучитьЗначениеРеквизитаДерева(СтрокаКонтрагента, "ФактическийАдрес_Представление", Истина);
	Если ЗначениеЗаполнено(Адрес) Тогда
		
		АдресСтруктурой = ПолучитьЗначениеРеквизитаДерева(СтрокаКонтрагента, "АдресСтруктурой", Истина);
		Если ТипЗнч(АдресСтруктурой) = Тип("Структура") Тогда
			КодРегион = "";
			Если АдресСтруктурой.Свойство("КодРегион", КодРегион) Тогда
				Попытка
					КодРегионаВКоде = Число(СокрЛП(АдресСтруктурой.КодРегион));
				Исключение
					КодРегионаВКоде = 0;
				КонецПопытки;
				Запрос = Новый Запрос();
				Запрос.Текст = "ВЫБРАТЬ
				               |	АдресныйКлассификатор.Наименование,
				               |	АдресныйКлассификатор.Сокращение
				               |ИЗ
				               |	РегистрСведений.АдресныйКлассификатор КАК АдресныйКлассификатор
				               |ГДЕ
				               |	АдресныйКлассификатор.ТипАдресногоЭлемента = 1
				               |	И АдресныйКлассификатор.КодРегионаВКоде = &КодРегионаВКоде";
				Запрос.УстановитьПараметр("КодРегионаВКоде", КодРегионаВКоде);
				Результат = Запрос.Выполнить();
				Если Не Результат.Пустой() Тогда
					Выборка = Результат.Выбрать();
					Выборка.Следующий();
					ПредставлениеРегиона = СокрЛП(Выборка.Наименование + " " + Выборка.Сокращение) + ",";
					Адрес = СтрЗаменить(Адрес, " "+АдресСтруктурой.КодРегион+",", ПредставлениеРегиона);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		КИ = РегистрыСведений.КонтактнаяИнформация.СоздатьМенеджерЗаписи();
		КИ.Объект = КонтрагентОбъект.Ссылка;
		КИ.Тип = Перечисления.ТипыКонтактнойИнформации.Адрес;
		КИ.Вид = Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента;
		КИ.Поле1 = "Россия";
		КИ.Представление = Адрес;
		КИ.Записать(Истина);
		
	КонецЕсли;
	
	// Телефон
	Контакты = ПолучитьЗначениеРеквизитаДерева(СтрокаКонтрагента, "Контакты", Истина);
	Если ТипЗнч(Контакты) = Тип("ТаблицаЗначений") Тогда
		Для Каждого Контакт Из Контакты Цикл
			Если ВРег(Контакт.Вид) = "ТЕЛЕФОН" Тогда
				КИ = РегистрыСведений.КонтактнаяИнформация.СоздатьМенеджерЗаписи();
				КИ.Объект = КонтрагентОбъект.Ссылка;
				КИ.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон;
				КИ.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента;
				КИ.Представление = Контакт.Представление;
				КИ.Записать(Истина);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Руководитель
	Руководитель = ПолучитьЗначениеРеквизитаДерева(СтрокаКонтрагента, "Руководитель", Истина);
	Если ТипЗнч(Руководитель) = Тип("Структура") Тогда
		// Контактные лица
		НаименованиеКЛ = Руководитель.Фамилия + " " + Руководитель.Имя + " " + Руководитель.Отчество;
		КЛ = НайтиСсылкуНаОбъектПоРеквизиту("КонтактныеЛица", "Наименование", НаименованиеКЛ);
		Если Не ЗначениеЗаполнено(КЛ) Тогда
			КЛ = Справочники.КонтактныеЛица.СоздатьЭлемент();
			ЗаполнитьЗначенияСвойств(КЛ, Руководитель);
			КЛ.Наименование = НаименованиеКЛ;
			КЛ.Записать();
		КонецЕсли;
		// Контактные лица контрагентов
		КЛКонтрагента = НайтиСсылкуНаОбъектПоРеквизиту("КонтактныеЛицаКонтрагентов", "Наименование", 
			НаименованиеКЛ, КонтрагентОбъект.Ссылка);
		Если Не ЗначениеЗаполнено(КЛКонтрагента) Тогда
			КЛКонтрагента = Справочники.КонтактныеЛицаКонтрагентов.СоздатьЭлемент();
			КЛКонтрагента.Владелец = КонтрагентОбъект.Ссылка;
			КЛКонтрагента.Должность = Руководитель.Должность;
			КЛКонтрагента.КонтактноеЛицо = КЛ.Ссылка;
			КЛКонтрагента.Наименование = КЛ.Наименование;
			КЛКонтрагента.Записать();
		КонецЕсли;
	КонецЕсли;
	
	Если Записывать И ЗаписатьЕщеРаз Тогда
		КонтрагентОбъект.Записать();
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Документы передачи товаров/результатов работ + 

Процедура ПрочитатьДанныеДокументаПередачи(ДеревоДанных, ДанныеДокумента, ИмяТипаДокумента)
	
	Шапка = Новый Структура("");
	
	Если ДанныеДокумента.ВидДокумента = "ПередачаТоваров" Тогда
		ПрочитатьДанныеУчастника(ДеревоДанных, Шапка, "Плательщик", "Организация", "Организации");
		ПрочитатьДанныеУчастника(ДеревоДанных, Шапка, "Поставщик", "Контрагент", "Контрагенты");
		ПрочитатьДанныеУчастника(ДеревоДанных, Шапка, "Грузоотправитель", "Грузоотправитель", "Контрагенты");
		ПрочитатьДанныеУчастника(ДеревоДанных, Шапка, "Грузополучатель", "Грузополучатель", "Контрагенты");
		ИмяРеквизитаКонтрагент = "Поставщик";
		ИмяРеквизитаНомерВходящегоДокумента = "НомерТоварнойНакладной";
		ИмяРеквизитаДатаВходящегоДокумента = "ДатаТоварнойНакладной";
		ИмяТаблицы = "ТаблицаТоваров";
	ИначеЕсли ДанныеДокумента.ВидДокумента = "ПередачаРезультатовРабот" Тогда
		ПрочитатьДанныеУчастника(ДеревоДанных, Шапка, "Исполнитель", "Организация", "Организации");
		ПрочитатьДанныеУчастника(ДеревоДанных, Шапка, "Заказчик", "Контрагент", "Контрагенты");
		ИмяРеквизитаКонтрагент = "Заказчик";
		ИмяРеквизитаНомерВходящегоДокумента = "НомерАкта";
		ИмяРеквизитаДатаВходящегоДокумента = "ДатаАкта";
		ИмяТаблицы = "ТаблицаУслуг";
	КонецЕсли;

	Если Шапка.Свойство("Контрагент") И ЗначениеЗаполнено(Шапка.Контрагент) Тогда
		НомерСчета = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, ИмяРеквизитаКонтрагент+".БанковскийСчет.НомерСчета");
		Если ЗначениеЗаполнено(НомерСчета) Тогда
			РеквизитыПоиска = Новый Структура("Владелец, НомерСчета", Шапка.Контрагент, НомерСчета);
			БанковскийСчетКонтрагента = НайтиСсылкуНаОбъект("БанковскиеСчетаКонтрагентов", , РеквизитыПоиска);
			Если ЗначениеЗаполнено(БанковскийСчетКонтрагента) Тогда
				Шапка.Вставить("БанковскийСчетКонтрагента", БанковскийСчетКонтрагента);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	ВалютаКод = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаКод");
	ВалютаДокумента = НайтиСсылкуНаОбъект("Валюты", ВалютаКод);
	Если ЗначениеЗаполнено(ВалютаДокумента) Тогда
		Шапка.Вставить("ВалютаДокумента", ВалютаДокумента);
	КонецЕсли;

	НомерВходящегоДокумента = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, ИмяРеквизитаНомерВходящегоДокумента);
	Шапка.Вставить("НомерВходящегоДокумента", НомерВходящегоДокумента);
	ДатаВходящегоДокумента = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, ИмяРеквизитаДатаВходящегоДокумента);
	Шапка.Вставить("ДатаВходящегоДокумента", ДатаВходящегоДокумента);

	Основание = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснования");
	Если ТипЗнч(Основание) = Тип("Массив") И Основание.Количество() > 0 Тогда
		ДокументПоступления = Основание[0];
	Иначе
		ДокументПоступления = Основание;
	КонецЕсли;
	Если ДокументПоступления <> Неопределено И ЗначениеЗаполнено(ДокументПоступления) Тогда
		Шапка.Вставить("ДокументПоступления", ДокументПоступления);
	КонецЕсли;
	
	ДанныеДокумента.Вставить("Шапка", Шапка);

	ПустойДокумент = Документы[ИмяТипаДокумента].ПустаяСсылка();
	Товары = ПустойДокумент.Товары.ВыгрузитьКолонки();
	ДанныеДокумента.Вставить("Товары", Товары);
	Услуги = ПустойДокумент.Услуги.ВыгрузитьКолонки();
	ДанныеДокумента.Вставить("Услуги", Услуги);
	СерийныеНомера = ПустойДокумент.СерийныеНомера.ВыгрузитьКолонки();
	ДанныеДокумента.Вставить("СерийныеНомера", СерийныеНомера);
	
	КлючСвязи = 1;
	ТаблицаСтрокНоменклатуры = ДеревоДанных.Строки.Найти(ИмяТаблицы, "ПолныйПуть").Строки;
	Для Каждого СтрокаНоменклатурыВДереве Из ТаблицаСтрокНоменклатуры Цикл
		ДанныеСтроки = Новый Структура("ПрефиксПутиКРеквизитам,КлючСвязи", ИмяТаблицы + ".НомерСтроки",КлючСвязи);
		ПрочитатьДанныеСтрокиНоменклатуры(СтрокаНоменклатурыВДереве, ДанныеСтроки, ДанныеДокумента);
		СтрокаНоменклатурыВТаблице = ДанныеДокумента[ДанныеСтроки.ТипСтроки].Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаНоменклатурыВТаблице, ДанныеСтроки);
		Если ДанныеСтроки.ТипСтроки = "Товары" И ДанныеСтроки.Свойство("Номенклатура") Тогда
			ДобавитьСерийныеНомера(СтрокаНоменклатурыВТаблице, КлючСвязи, ДанныеСтроки, СерийныеНомера);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

Процедура ПересчитатьСуммы(ДокументОбъект)
	
	мРеквизиты = Новый Массив;
	мРеквизиты.Добавить(Новый Структура("Цена,Количество,Сумма", "Цена","Количество","Сумма"));
	Если ТипЗнч(Документобъект) = Тип("ДокументОбъект.КорректировкаПоступления") Тогда
		мРеквизиты.Добавить(Новый Структура("Цена,Количество,Сумма", "ЦенаДоИзменения","КоличествоДоИзменения","СуммаДоИзменения"));
		мРеквизиты.Добавить(Новый Структура("Цена,Количество,Сумма", "ЦенаДоКорректировки","КоличествоДоКорректировки","СуммаДоКорректировки"));
	КонецЕсли;
	
	мТЧ = Новый Массив;
	мТЧ.Добавить("Товары");
	мТЧ.Добавить("Услуги");
	
	СуммаВключаетНДС = ДокументОбъект.СуммаВключаетНДС;
	
	Для Каждого ТЧ Из мТЧ Цикл
		Для Каждого СтрокаТЧ Из ДокументОбъект[ТЧ] Цикл
			Для Каждого НР Из мРеквизиты Цикл
				Если СуммаВключаетНДС Тогда
					СтрокаТЧ[НР.Цена] = СтрокаТЧ[НР.Сумма] / ?(СтрокаТЧ[НР.Количество] = 0, 1, СтрокаТЧ[НР.Количество]);
				Иначе
					СтрокаТЧ[НР.Сумма] = СтрокаТЧ[НР.Цена] * СтрокаТЧ[НР.Количество];
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
		
КонецПроцедуры

// Документы передачи товаров/результатов работ - 

// Универсальные документы +

Процедура СкорректироватьСсылкиНаВладельцев(ПервичныйДокумент, СчетФактура)
	
	Если ЗначениеЗаполнено(ПервичныйДокумент) И Не ЗначениеЗаполнено(СчетФактура) Тогда
		Если ТипЗнч(ПервичныйДокумент) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") ИЛИ 
			ТипЗнч(ПервичныйДокумент) = Тип("ДокументСсылка.КорректировкаПоступления") Тогда
			СчетФактура = УчетНДС.НайтиПодчиненныйСчетФактуру(ПервичныйДокумент, "СчетФактураПолученный");
		ИначеЕсли ТипЗнч(ПервичныйДокумент) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
			СчетФактура = ПервичныйДокумент;
			Если СчетФактура.ДокументыОснования.Количество() > 0 Тогда
				ПервичныйДокумент = СчетФактура.ДокументыОснования[0].ДокументОснование;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли Не ЗначениеЗаполнено(ПервичныйДокумент) И ЗначениеЗаполнено(СчетФактура) Тогда
		Если ТипЗнч(СчетФактура) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
			Если СчетФактура.ДокументыОснования.Количество() > 0 Тогда
				ПервичныйДокумент = СчетФактура.ДокументыОснования[0].ДокументОснование;
			КонецЕсли;
		ИначеЕсли ТипЗнч(СчетФактура) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") ИЛИ
			ТипЗнч(СчетФактура) = Тип("ДокументСсылка.КорректировкаПоступления") Тогда
			ПервичныйДокумент = СчетФактура;
			СчетФактура = УчетНДС.НайтиПодчиненныйСчетФактуру(ПервичныйДокумент, "СчетФактураПолученный");
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// УПД +

// УКД -

Процедура ПрочитатьДанныеУниверсальногоДокумента(ДеревоДанных, ДанныеДокумента, ИмяТипаДокумента)
	
	Шапка = Новый Структура("");
	
	ПрочитатьДанныеУчастника(ДеревоДанных, Шапка, "СведенияОПокупателе", "Организация", "Организации");
	ПрочитатьДанныеУчастника(ДеревоДанных, Шапка, "СведенияОПродавце", "Контрагент", "Контрагенты");
	Если Найти(ДанныеДокумента.ВидДокумента, "УПД") > 0 Тогда
		ПрочитатьДанныеУчастника(ДеревоДанных, Шапка, "СведенияОГрузоотправителе.Грузоотправитель", "Грузоотправитель", "Контрагенты");
		ПрочитатьДанныеУчастника(ДеревоДанных, Шапка, "СведенияОГрузополучателе", "Грузополучатель", "Контрагенты");
	КонецЕсли;
	
	Если Шапка.Свойство("Контрагент") И ЗначениеЗаполнено(Шапка.Контрагент) Тогда
		НомерСчета = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОПродавце.БанковскиеРеквизиты.НомерСчета");
		Если ЗначениеЗаполнено(НомерСчета) Тогда
			РеквизитыПоиска = Новый Структура("Владелец, НомерСчета", Шапка.Контрагент, НомерСчета);
			БанковскийСчетКонтрагента = НайтиСсылкуНаОбъект("БанковскиеСчетаКонтрагентов", , РеквизитыПоиска);
			Если ЗначениеЗаполнено(БанковскийСчетКонтрагента) Тогда
				Шапка.Вставить("БанковскийСчетКонтрагента", БанковскийСчетКонтрагента);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ВалютаКод = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаКод");
	ВалютаДокумента = НайтиСсылкуНаОбъект("Валюты", ВалютаКод);
	Если ЗначениеЗаполнено(ВалютаДокумента) Тогда
		Шапка.Вставить("ВалютаДокумента", ВалютаДокумента);
	КонецЕсли;
	
	НомерВходящегоДокумента = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерДокумента");
	Шапка.Вставить("НомерВходящегоДокумента", НомерВходящегоДокумента);
	ДатаВходящегоДокумента = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаДокумента");
	Шапка.Вставить("ДатаВходящегоДокумента", ДатаВходящегоДокумента);
	НомерИсходногоДокумента = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсходногоДокумента");
	Шапка.Вставить("НомерИсходногоДокумента", НомерИсходногоДокумента);
	ДатаИсходногоДокумента = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсходногоДокумента");
	Шапка.Вставить("ДатаИсходногоДокумента", ДатаИсходногоДокумента);
	
	НомерИсправления = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправления");
	ДатаИсправления = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсправления");
	Если ЗначениеЗаполнено(НомерИсправления) Тогда
		Шапка.Вставить("Исправление");
		Шапка.Вставить("НомерИсправления", НомерИсправления);
		Шапка.Вставить("ДатаИсправления", ДатаИсправления);
	КонецЕсли;
	
	НомерИсправленияИсходногоДокумента = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправленияИсходногоДокумента");
	ДатаИсправленияИсходногоДокумента  = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсправленияИсходногоДокумента");
	
	Если ЗначениеЗаполнено(НомерИсправленияИсходногоДокумента) Тогда
		Шапка.Вставить("УчитыватьИсправлениеИсходногоДокумента", Истина);
		Шапка.Вставить("НомерИсправленияИсходногоДокумента", НомерИсправленияИсходногоДокумента);
		Шапка.Вставить("ДатаИсправленияИсходногоДокумента",  ДатаИсправленияИсходногоДокумента);
	Иначе
		Шапка.Вставить("УчитыватьИсправлениеИсходногоДокумента", Ложь);
	КонецЕсли;
	
	Основание = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияСчетаФактуры");
	Если ТипЗнч(Основание) = Тип("Массив") И Основание.Количество() > 0 Тогда
		ДокументОснование = Основание[0];
	Иначе
		ДокументОснование = Основание;
	КонецЕсли;
	Если ЗначениеЗаполнено(ДокументОснование) Тогда
		Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
			ДокументыОснования = ДокументОснование.ДокументыОснования;
			Если ДокументыОснования.Количество() > 0 Тогда
				ДокументПоступления = ДокументыОснования[0].ДокументОснование;
			КонецЕсли;
		Иначе
			ДокументПоступления = ДокументОснование;
		КонецЕсли;
	КонецЕсли;
	Если ДокументПоступления = Неопределено Тогда
		ДокументПоступления = Документы.ПоступлениеТоваровУслуг.ПустаяСсылка();
	КонецЕсли;
	Шапка.Вставить("ДокументПоступления", ДокументПоступления);
	
	ДанныеДокумента.Вставить("Шапка", Шапка);
	
	ПустойДокумент = Документы[ИмяТипаДокумента].ПустаяСсылка();
	
	Если Найти(ИмяТипаДокумента, "СчетФактура") = 0 Тогда
		
		Товары = ПустойДокумент.Товары.ВыгрузитьКолонки();
		ДанныеДокумента.Вставить("Товары", Товары);
		Услуги = ПустойДокумент.Услуги.ВыгрузитьКолонки();
		ДанныеДокумента.Вставить("Услуги", Услуги);
		СерийныеНомера = ПустойДокумент.СерийныеНомера.ВыгрузитьКолонки();
		ДанныеДокумента.Вставить("СерийныеНомера", СерийныеНомера);
		
		СведенияОПрослеживаемости = ПустойДокумент.СведенияПрослеживаемости.ВыгрузитьКолонки();
		ДанныеДокумента.Вставить("СведенияОПрослеживаемости", СведенияОПрослеживаемости);
		
		ИмяТаблицы = "СведенияОТоварах";
		КлючСвязи = 1;
		ТаблицаСтрокНоменклатуры = ДеревоДанных.Строки.Найти(ИмяТаблицы, "ПолныйПуть").Строки;
		Для Каждого СтрокаНоменклатурыВДереве Из ТаблицаСтрокНоменклатуры Цикл
			ДанныеСтроки = Новый Структура("ПрефиксПутиКРеквизитам,КлючСвязи", ИмяТаблицы + ".НомерСтроки",КлючСвязи);
			ПрочитатьДанныеСтрокиНоменклатуры(СтрокаНоменклатурыВДереве, ДанныеСтроки, ДанныеДокумента);
			СтрокаНоменклатурыВТаблице = ДанныеДокумента[ДанныеСтроки.ТипСтроки].Добавить();
			
			Если ДанныеСтроки.ТипСтроки = "Услуги" Тогда
				Если ДанныеСтроки.Количество = 0 Тогда
					ДанныеСтроки.Количество = 1;
				КонецЕСли; 
			КонецЕСли;
			
			ЗаполнитьЗначенияСвойств(СтрокаНоменклатурыВТаблице, ДанныеСтроки);
			СтрокаНоменклатурыВТаблице.ЕстьВДокументеПоступления = Истина;
			ПрочитатьСерийныеНомераСтрокиНоменклатуры(СтрокаНоменклатурыВДереве, СерийныеНомера, ДанныеСтроки);
			
			ПрочитатьСведенияОПрослеживаемостиСтрокиНоменклатуры(СтрокаНоменклатурыВДереве, СведенияОПрослеживаемости, ДанныеСтроки, ДанныеДокумента.ВидДокумента);
			
			КлючСвязи = КлючСвязи + 1;
			
			Если ДанныеСтроки.Свойство("НомерГТД") Тогда
				ЗаполнитьСериюНоменклатуры(СтрокаНоменклатурыВТаблице, ДанныеСтроки);
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		ДокументыОснования = ПустойДокумент.ДокументыОснования.ВыгрузитьКолонки();
		Основания = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияСчетаФактуры");
		
		Если ТипЗнч(Основания) = Тип("Массив") Тогда
			МассивОснований = Основания;
		Иначе
			МассивОснований = Новый Массив;
			МассивОснований.Добавить(Основания);
		КонецЕсли;
		
		ДопустимыеТипы = Метаданные.Документы.СчетФактураПолученный.ТабличныеЧасти.ДокументыОснования.Реквизиты.ДокументОснование.Тип.Типы();
		
		Для Каждого Основание Из МассивОснований Цикл
			Если Основание <> Неопределено И ЗначениеЗаполнено(Основание)
				И ДопустимыеТипы.Найти(ТипЗнч(Основание)) <> Неопределено Тогда
				СтрокаОснования = ДокументыОснования.Добавить();
				СтрокаОснования.ДокументОснование = Основание;
			КонецЕсли;
		КонецЦикла;
		ДанныеДокумента.Вставить("ДокументыОснования", ДокументыОснования);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПрочитатьДанныеУчастника(ДеревоДанных, Шапка, ИмяВДереве, ИмяВШапке, ИмяТипаДляПоиска)
	
	ТипУчастника = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, ИмяВДереве + ".ТипУчастника");
	РеквизитыПоиска = Новый Структура("ИНН, КПП, Наименование");
	Если ТипУчастника = "ЮЛ" Тогда
		РеквизитыПоиска.Наименование = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, 
			ИмяВДереве + ".ТипУчастника.ЮЛ.НаименованиеОрганизации");
		РеквизитыПоиска.ИНН = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, 
			ИмяВДереве + ".ТипУчастника.ЮЛ.ИНН");
		РеквизитыПоиска.КПП = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, 
			ИмяВДереве + ".ТипУчастника.ЮЛ.КПП");
	ИначеЕсли ТипУчастника = "ИП" ИЛИ ТипУчастника = "ФЛ" Тогда
		ПутьФИО = "";
		Если ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, ИмяВДереве + ".ТипУчастника."+ ТипУчастника +".ФИО") Тогда
			ПутьФИО = ".ФИО";
		КонецЕсли;
		Фамилия = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, ИмяВДереве + ".ТипУчастника." + ТипУчастника + ПутьФИО + ".Фамилия");
		Имя = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, ИмяВДереве + ".ТипУчастника." + ТипУчастника + ПутьФИО + ".Имя");
		Отчество = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, ИмяВДереве + ".ТипУчастника." + ТипУчастника + ПутьФИО + ".Отчество");
		
		РеквизитыПоиска.Наименование = Фамилия + " " + Имя + ?(ЗначениеЗаполнено(Отчество), " " + Отчество, "");
		РеквизитыПоиска.ИНН = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, ИмяВДереве + ".ТипУчастника." + ТипУчастника + ".ИНН");
		РеквизитыПоиска.Удалить("КПП");
	ИначеЕсли ТипУчастника = "ИЛ" ИЛИ ТипУчастника = "ИностраннаяОрганизация" Тогда
		РеквизитыПоиска.Наименование = ОбщегоНазначенияЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, 
			ИмяВДереве + ".ТипУчастника.ИЛ.НаименованиеОрганизации");
		РеквизитыПоиска.Удалить("ИНН");
		РеквизитыПоиска.Удалить("КПП");
	КонецЕсли;
	СсылкаНаУчастника = НайтиСсылкуНаОбъект(ИмяТипаДляПоиска, , РеквизитыПоиска);
	Если ЗначениеЗаполнено(СсылкаНаУчастника) Тогда
		Шапка.Вставить(ИмяВШапке, СсылкаНаУчастника);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПрочитатьДанныеСтрокиНоменклатуры(СтрокаНоменклатурыВДереве, ДанныеСтроки, ДанныеДокумента)
	
	ПрефиксПути = ДанныеСтроки.ПрефиксПутиКРеквизитам;
	
	Если ДанныеДокумента.ВидДокумента = "УПДДокументОПередаче" Тогда
		ПрямыеРеквизиты = Новый Структура("Количество,ЦенаЗаЕдиницуИзмерения,НалоговаяСтавка,СтоимостьТоваровСНалогом,СуммаНалога,ТоварНаименование",
			"Количество","Цена","СтавкаНДС","Сумма","СуммаНДС","Содержание");
		ИмяРеквизитаИдентификатор = ".ИдТовараУКонтрагента";
		ИмяРеквизитаКод = ".ТоварКод";
		ИмяРеквизитаЕдиницы = ".ЕдиницаИзмеренияКод";
	ИначеЕсли ДанныеДокумента.ВидДокумента = "УКДДокументОбИзмененииСтоимости" Тогда
		ПрямыеРеквизиты = Новый Структура("Количество,КоличествоДоКорректировки,ЦенаЗаЕдиницуИзмерения,ЦенаЗаЕдиницуИзмеренияДоКорректировки,
			|СуммаНалога,СуммаНалогаДоКорректировки,СтоимостьТоваровСНалогом,СтоимостьТоваровСНалогомДоКорректировки,НалоговаяСтавка",
			"Количество","КоличествоДоИзменения","Цена","ЦенаДоИзменения","СуммаНДС","СуммаНДСДоИзменения","Сумма",
			"СуммаДоИзменения","СтавкаНДС");
		ИмяРеквизитаИдентификатор = ".ИдТовараУКонтрагента";
		ИмяРеквизитаКод = ".ТоварИдентификатор";
		ИмяРеквизитаЕдиницы = ".ЕдиницаИзмеренияКод";
	ИначеЕсли ДанныеДокумента.ВидДокумента = "ПередачаТоваров" Тогда
		ПрямыеРеквизиты = Новый Структура("МассаНетто,Цена,СтавкаНДС,СуммаСНДС,СуммаНДС,НаименованиеНоменклатуры",
			"Количество","Цена","СтавкаНДС","Сумма","СуммаНДС","Содержание");
		ИмяРеквизитаИдентификатор = ".ИдТовараУКонтрагента";
		ИмяРеквизитаКод = ".КодТовара";
		ИмяРеквизитаЕдиницы = ".БазоваяЕдиницаКод";
	ИначеЕсли ДанныеДокумента.ВидДокумента = "ПередачаРезультатовРабот" Тогда
		ПрямыеРеквизиты = Новый Структура("Количество,Цена,СтавкаНДС,СуммаСНДС,СуммаНДС,Описание",
			"Количество","Цена","СтавкаНДС","Сумма","СуммаНДС","Содержание");
		ИмяРеквизитаИдентификатор = ".ИдТовараУКонтрагента";
		ИмяРеквизитаКод = ".КодТовара";
		ИмяРеквизитаЕдиницы = ".ЕдиницаИзмеренияКод";
	Иначе
		ПрямыеРеквизиты = Новый Структура("");
	КонецЕсли;
		
	Для Каждого ПрямойРеквизит Из ПрямыеРеквизиты Цикл
		СтрокаРеквизита = СтрокаНоменклатурыВДереве.Строки.Найти(ПрефиксПути + "." + ПрямойРеквизит.Ключ);
		Если СтрокаРеквизита <> Неопределено Тогда
			ДанныеСтроки.Вставить(ПрямойРеквизит.Значение, СтрокаРеквизита.Значение)
		КонецЕсли;
	КонецЦикла;
	
	// Ставка НДС
	Если ДанныеСтроки.Свойство("Сумма") И ДанныеСтроки.Свойство("СуммаНДС") И ДанныеСтроки.СуммаНДС <> 0 Тогда
		Если Не ДанныеСтроки.Свойство("СтавкаНДС") Тогда
			ДанныеСтроки.Вставить("СтавкаНДС", Перечисления.СтавкиНДС.ПустаяСсылка());
		КонецЕсли;
		ДанныеСтроки.СуммаНДС = ?(ДанныеСтроки.СуммаНДС = Неопределено, 0, ДанныеСтроки.СуммаНДС);
		Если Не ЗначениеЗаполнено(ДанныеСтроки.СтавкаНДС) Тогда
			СтавкаНДСЧислом = Окр(ДанныеСтроки.СуммаНДС/(ДанныеСтроки.Сумма - ДанныеСтроки.СуммаНДС)*100, 0);
			Если СтавкаНДСЧислом = 18 Тогда
				ДанныеСтроки.СтавкаНДС = Перечисления.СтавкиНДС.НДС18;
			ИначеЕсли СтавкаНДСЧислом = 20 Тогда
				ДанныеСтроки.СтавкаНДС = Перечисления.СтавкиНДС.НДС20;
			ИначеЕсли СтавкаНДСЧислом = 10 Тогда
				ДанныеСтроки.СтавкаНДС = Перечисления.СтавкиНДС.НДС10;
				//ТЕАЛ НДС ++
			ИначеЕсли СтавкаНДСЧислом = 5 Тогда
				ДанныеСтроки.СтавкаНДС = Перечисления.СтавкиНДС.НДС5;
			ИначеЕсли СтавкаНДСЧислом = 7 Тогда
				ДанныеСтроки.СтавкаНДС = Перечисления.СтавкиНДС.НДС7;
			ИначеЕсли СтавкаНДСЧислом = 22 Тогда
				ДанныеСтроки.СтавкаНДС = Перечисления.СтавкиНДС.НДС22;	
               	//ТЕАЛ НДС --
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Содержание
	СтрокаРеквизитаНаименование = СтрокаНоменклатурыВДереве.Строки.Найти(ПрефиксПути + ".НаименованиеНоменклатуры");
	Если СтрокаРеквизитаНаименование = Неопределено Тогда
		СтрокаРеквизитаНаименование = СтрокаНоменклатурыВДереве.Строки.Найти(ПрефиксПути + ".ТоварНаименование");
	КонецЕсли;
	Если СтрокаРеквизитаНаименование <> Неопределено Тогда
		Если Не ДанныеСтроки.Свойство("Содержание") Тогда
			ДанныеСтроки.Вставить("Содержание", "");
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ДанныеСтроки.Содержание) Тогда
			ДанныеСтроки.Содержание = СтрокаРеквизитаНаименование.Значение;
		КонецЕсли;
	КонецЕсли;
	
	// Артикул
	ЗначениеАртикула = "";
	СтрокаРеквизитаАртикул = СтрокаНоменклатурыВДереве.Строки.Найти(ПрефиксПути + ".ТоварАртикул", "ПолныйПуть");
	Если СтрокаРеквизитаАртикул <> Неопределено Тогда
		ЗначениеАртикула = СтрокаРеквизитаАртикул.Значение;
	КонецЕсли;
	
	// Номенклатура + Характеристика
	ЗначениеИдентификатора = "";
	СтрокаРеквизита = СтрокаНоменклатурыВДереве.Строки.Найти(ПрефиксПути + ИмяРеквизитаИдентификатор, "ПолныйПуть");
	Если СтрокаРеквизита = Неопределено ИЛИ Не ЗначениеЗаполнено(СтрокаРеквизита.Значение) Тогда
		// выполняется проверка сопоставления номенклатуры
		СопоставлениеНоменклатуры = СтрокаНоменклатурыВДереве.Строки.Найти(ПрефиксПути + ".Сопоставление");
		Если СопоставлениеНоменклатуры <> Неопределено Тогда
			СтрокаРеквизита = СопоставлениеНоменклатуры.Строки.Найти(ПрефиксПути + ".Сопоставление.Идентификатор", "ПолныйПуть");
			ЗначениеИдентификатора = ?(СтрокаРеквизита = Неопределено, "", СтрокаРеквизита.Значение);
		КонецЕсли;
	Иначе
		ЗначениеИдентификатора = СтрокаРеквизита.Значение;
	КонецЕсли;
	
	// Если пришел пустой ИД, используем вместо него наименование товара.
	// Актуально для входящих ЭД из учетных систем отличных от 1С.
	Если Не ЗначениеЗаполнено(ЗначениеИдентификатора) Тогда
		ЗначениеИдентификатора = ИдентификаторТовараПоСтроке(ВРег(СтрЗаменить(ДанныеСтроки.Содержание, " ", "")) + "###" + ?(ЗначениеЗаполнено(ЗначениеАртикула), ЗначениеАртикула, ""));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеИдентификатора) И ДанныеДокумента.Шапка.Свойство("Контрагент") Тогда
		РеквизитыПоиска = Новый Структура("Владелец, Идентификатор", ДанныеДокумента.Шапка.Контрагент, ЗначениеИдентификатора);
		РезультатПоиска = ПолучитьДанныеТовараПоНоменклатуреПоставщика(РеквизитыПоиска);
		Если ЗначениеЗаполнено(РезультатПоиска.Номенклатура) Тогда
			ДанныеСтроки.Вставить("Номенклатура", РезультатПоиска.Номенклатура);
		КонецЕсли;
		Если ЗначениеЗаполнено(РезультатПоиска.ХарактеристикаНоменклатуры) Тогда
			ДанныеСтроки.Вставить("ХарактеристикаНоменклатуры", РезультатПоиска.ХарактеристикаНоменклатуры);
		КонецЕсли;
	КонецЕсли;
	
	// Прослеживаемый товар
	Если ДанныеСтроки.Свойство("Номенклатура") И ЗначениеЗаполнено(ДанныеСтроки.Номенклатура) Тогда
		КодТНВЭД = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеСтроки.Номенклатура, "КодТНВЭД");
		ДанныеСтроки.Вставить("ПрослеживаемыйТовар", Ложь);
		Если ЗначениеЗаполнено(КодТНВЭД) Тогда
			ДанныеСтроки.ПрослеживаемыйТовар = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КодТНВЭД, "ПрослеживаемыйТовар");
		КонецЕсли;
	КонецЕсли;
	
	// Признак
	Если ДанныеСтроки.Свойство("Номенклатура") Тогда
		ДанныеСтроки.Вставить("ТипСтроки", ?(ДанныеСтроки.Номенклатура.Услуга, "Услуги", "Товары"));
	Иначе
		СтрокаРеквизита = СтрокаНоменклатурыВДереве.Строки.Найти(ПрефиксПути + ".Признак", "ПолныйПуть");
		Если СтрокаРеквизита <> Неопределено И СтрДлина(СтрокаРеквизита.Значение) = 1 Тогда
			Если Найти("23", СтрокаРеквизита.Значение) > 0 Тогда
				ДанныеСтроки.Вставить("ТипСтроки", "Услуги");
			ИначеЕсли Найти("145", СтрокаРеквизита.Значение) > 0 Тогда
				ДанныеСтроки.Вставить("ТипСтроки", "Товары");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Признак по умолчанию
	Если Не ДанныеСтроки.Свойство("ТипСтроки") Тогда
		
		Если Найти(ПрефиксПути, "Услуг") > 0 Тогда 
			ДанныеСтроки.Вставить("ТипСтроки", "Услуги");
		Иначе
			ДанныеСтроки.Вставить("ТипСтроки", "Товары");
		КонецЕсли;
		
	КонецЕсли;
	
	// Единица измерения
	Если ДанныеСтроки.Свойство("Номенклатура") Тогда
		СтрокаРеквизита = СтрокаНоменклатурыВДереве.Строки.Найти(ПрефиксПути + ИмяРеквизитаЕдиницы, "ПолныйПуть");
		Если СтрокаРеквизита <> Неопределено Тогда
			ЕдиницаПоКлассификатору = НайтиСсылкуНаОбъект("ЕдиницыИзмерения", СтрокаРеквизита.Значение);
			Если ЗначениеЗаполнено(ЕдиницаПоКлассификатору) Тогда
				ЕдиницаИзмерения = НайтиСсылкуНаОбъектПоРеквизиту("ЕдиницыИзмерения", "ЕдиницаПоКлассификатору", 
					ЕдиницаПоКлассификатору, ДанныеСтроки.Номенклатура);
				Если ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
					ДанныеСтроки.Вставить("ЕдиницаИзмерения", ЕдиницаИзмерения);
					Если ЕдиницаИзмерения.Коэффициент <> 0 Тогда
						ДанныеСтроки.Вставить("Коэффициент", ЕдиницаИзмерения.Коэффициент);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// СерийныеНомера
	СтрокаРеквизита = СтрокаНоменклатурыВДереве.Строки.Найти(ПрефиксПути + ".ДопДанныеПодписанные", "ПолныйПуть");
	Если СтрокаРеквизита <> Неопределено Тогда
		СтрокаСН = СтрокаРеквизита.Строки.Найти(ПрефиксПути + ".ДопДанныеПодписанные.СерийныеНомера", "ПолныйПуть");
		Если СтрокаСН <> Неопределено И ТипЗнч(СтрокаСН.Значение) = Тип("Массив") Тогда
			ДанныеСтроки.Вставить("СерийныеНомера", СтрокаСН.Значение);
		КонецЕсли;
	КонецЕсли;
	
	// Страна происхождения
	ДанныеСтроки.Вставить("СтранаПроисхождения", Справочники.КлассификаторСтранМира.ПустаяСсылка());
	СтрокаРеквизита = СтрокаНоменклатурыВДереве.Строки.Найти(ПрефиксПути + ".СтранаПроисхожденияНаименование", "ПолныйПуть");
	Если СтрокаРеквизита <> Неопределено Тогда
		НаименованиеСтраныПроисхождения = СтрокаРеквизита.Значение;
		Если ЗначениеЗаполнено(НаименованиеСтраныПроисхождения) Тогда
			ДанныеСтроки.СтранаПроисхождения = НайтиСсылкуНаОбъектПоРеквизиту("КлассификаторСтранМира","Наименование", СокрЛП(НаименованиеСтраныПроисхождения));
		КонецЕсли;
	КонецЕсли;
	
	// Сведения о ГТД
	СтрокаРеквизита = СтрокаНоменклатурыВДереве.Строки.Найти("СведенияОТаможеннойДекларации");
	Если СтрокаРеквизита <> Неопределено Тогда
		СтрокаСведенияГТД = СтрокаРеквизита.Строки.Найти(ПрефиксПути + ".СведенияОТаможеннойДекларации.НомерСтроки", "ПолныйПуть");
		Если СтрокаСведенияГТД <> Неопределено Тогда
			РеквизитНомерГТД = СтрокаСведенияГТД.Строки.Найти("ТаможеннаяДекларацияНомер");
			Если РеквизитНомерГТД <> Неопределено Тогда
				ДанныеСтроки.Вставить("НомерГТД", РеквизитНомерГТД.Значение);
			КонецЕсли;
			РеквизитСтранаПроисхождения = СтрокаСведенияГТД.Строки.Найти("СтранаПроисхожденияКод");
			Если РеквизитСтранаПроисхождения <> Неопределено Тогда
				ДанныеСтроки.Вставить("КодСтраныПроисхождения", РеквизитСтранаПроисхождения.Значение);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ДанныеСтроки.Вставить("ИдентификаторСтроки", Строка(Новый УникальныйИдентификатор));

КонецПроцедуры

Процедура ЗаполнитьСериюНоменклатуры(СтрокаТаблицы, ДанныеСтроки)
	
	Если Не ЗначениеЗаполнено(СтрокаТаблицы.Номенклатура) 
		ИЛИ Не ЗначениеЗаполнено(ДанныеСтроки.НомерГТД) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ СтрокаТаблицы.Номенклатура.ВестиУчетПоСериям Тогда
		Возврат;
	КонецЕсли;
	
	НомерГТД = НайтиСсылкуНаОбъектПоРеквизиту("НомераГТД", "Код", ДанныеСтроки.НомерГТД);
	Если Не ЗначениеЗаполнено(НомерГТД) Тогда
		
		НовыйНомерГТД = Справочники.НомераГТД.СоздатьЭлемент();
		НовыйНомерГТД.Код = ДанныеСтроки.НомерГТД;
		НовыйНомерГТД.РегистрационныйНомер = ДанныеСтроки.НомерГТД;
		НовыйНомерГТД.Записать();
		
		НомерГТД = НовыйНомерГТД.Ссылка;
		
	КонецЕсли;
	
	СерияНоменклатуры = НайтиСсылкуНаОбъектПоРеквизиту("СерииНоменклатуры", "НомерГТД", НомерГТД, СтрокаТаблицы.Номенклатура);
	Если Не ЗначениеЗаполнено(СерияНоменклатуры) Тогда
		НоваяСерия = Справочники.СерииНоменклатуры.СоздатьЭлемент();
		НоваяСерия.Владелец = СтрокаТаблицы.Номенклатура;
		НоваяСерия.НомерГТД = НомерГТД;
		Если ЗначениеЗаполнено(ДанныеСтроки.КодСтраныПроисхождения) Тогда
			НоваяСерия.СтранаПроисхождения = Справочники.КлассификаторСтранМира.НайтиПоКоду(ДанныеСтроки.КодСтраныПроисхождения);
		КонецЕсли;
		НоваяСерия.Наименование = НомерГТД;
		НоваяСерия.Записать();
		
		СерияНоменклатуры = НоваяСерия.Ссылка;
	КонецЕсли;
	
	СтрокаТаблицы.СерияНоменклатуры = СерияНоменклатуры;
	
КонецПроцедуры

Процедура ПрочитатьСерийныеНомераСтрокиНоменклатуры(СтрокаНоменклатурыВДереве, СерийныеНомера, ДанныеСтроки)
	
	Если Не ДанныеСтроки.Свойство("Номенклатура") ИЛИ Не ЗначениеЗаполнено(ДанныеСтроки.Номенклатура) Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаТекстоваяИнформация = СтрокаНоменклатурыВДереве.Строки.Найти("ТекстоваяИнформация");
	
	ЕстьСерийныеНомера = Ложь;
	Для Каждого СтрокаНомераТаблицы Из СтрокаТекстоваяИнформация.Строки Цикл
		
		СтрокиТаблицы = СтрокаНомераТаблицы.Строки;
		
		ПрефиксПути = СтрокаНомераТаблицы.ПолныйПуть;
		СтрокаИдентификатор = СтрокиТаблицы.Найти(ПрефиксПути+".Идентификатор", "ПолныйПуть");
		
		Если СтрокаИдентификатор <> Неопределено Тогда
			Если СтрокаИдентификатор.Значение = "СерийныйНомер" Тогда
				СтрокаЗначение = СтрокиТаблицы.Найти(ПрефиксПути+".Значение", "ПолныйПуть");
				Если СтрокаЗначение <> Неопределено Тогда
					ЗначениеСН = СтрокаЗначение.Значение;
					
					СтрокаСН = СерийныеНомера.Добавить();
					СтрокаСН.КлючСвязи = ДанныеСтроки.КлючСвязи;
					СтрокаСН.СерийныйНомер = 
					НайтиСсылкуНаОбъектПоРеквизиту("СерийныеНомера", "Код", ЗначениеСН, ДанныеСтроки.Номенклатура);
					
					Если Не ЗначениеЗаполнено(СтрокаСН.СерийныйНомер) Тогда
						НовыйСН = Справочники.СерийныеНомера.СоздатьЭлемент();
						НовыйСН.Владелец = ДанныеСтроки.Номенклатура;
						НовыйСН.Код = ЗначениеСН;
						НовыйСН.Записать();
						СтрокаСН.СерийныйНомер = НовыйСН.Ссылка;
					КонецЕсли;
					
					ЕстьСерийныеНомера = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЕстьСерийныеНомера Тогда
		ВестиСерийныеНомера = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеСтроки.Номенклатура, "ВестиСерийныеНомера");
		Если Не ВестиСерийныеНомера Тогда
			ОписаниеСобытия = "В ЭД есть информация о серийных номерах, но для элемента номенклатуры учет по серийным номерам не ведется.";
			ЭлектронныеДокументыСлужебный.ВыполнитьЗаписьСобытияПоЭДВЖурналРегистрации(ОписаниеСобытия, 2, 
				УровеньЖурналаРегистрации.Предупреждение, Метаданные.Справочники.Номенклатура, ДанныеСтроки.Номенклатура);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ПрочитатьСведенияОПрослеживаемостиСтрокиНоменклатуры(СтрокаНоменклатурыВДереве, СведенияОПрослеживаемости, ДанныеСтроки, ВидДокумента)
	
	Если Не ДанныеСтроки.Свойство("Номенклатура") ИЛИ Не ЗначениеЗаполнено(ДанныеСтроки.Номенклатура) Тогда
		Возврат;
	КонецЕсли;
		
	СтрокаПрослеживаемости = СтрокаНоменклатурыВДереве.Строки.Найти("СведенияОПрослеживаемости");
	Если СтрокаПрослеживаемости = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьДанныеПрослеживаемости = Ложь;
	Для Каждого СтрокаНомераТаблицы Из СтрокаПрослеживаемости.Строки Цикл
		
		СтрокиТаблицы = СтрокаНомераТаблицы.Строки;
		
		ПрефиксПути = СтрокаНомераТаблицы.ПолныйПуть;
		СтрокаНомерТовара = СтрокиТаблицы.Найти(ПрефиксПути+".НомерТовара", "ПолныйПуть");
				
		Если СтрокаНомерТовара <> Неопределено Тогда
			Если Не ЗначениеЗаполнено(СтрокаНомерТовара.Значение) Тогда
				Продолжить;
			КонецЕсли;
			НоваяСтрока = СведенияОПрослеживаемости.Добавить();
			НомерТовара = СокрЛП(СтрокаНомерТовара.Значение);
			НоваяСтрока.ИдентификаторСтроки = ДанныеСтроки.ИдентификаторСтроки;
			НоваяСтрока.РНПТ = НайтиСсылкуНаОбъектПоРеквизиту("НомераГТД", "Код", НомерТовара);
			
			Если Не ЗначениеЗаполнено(НоваяСтрока.РНПТ) Тогда
				НовыйРНПТ = Справочники.НомераГТД.СоздатьЭлемент();
				НовыйРНПТ.Код = НомерТовара;
				НовыйРНПТ.РегистрационныйНомер = НомерТовара;
				НовыйРНПТ.Записать();
				НоваяСтрока.РНПТ = НовыйРНПТ.Ссылка;
			КонецЕсли;
			
			Если ВидДокумента = "УКДДокументОбИзмененииСтоимости" Тогда
				НоваяСтрока.КоличествоПрослеживаемости = ЭлектронныеДокументыВнутренний.ЗначениеРеквизитаВДереве(СтрокаНомераТаблицы,ПрефиксПути + ".КоличествоТовара");
				НоваяСтрока.Количество = ЭлектронныеДокументыВнутренний.ЗначениеРеквизитаВДереве(СтрокаНомераТаблицы,ПрефиксПути + ".КоличествоТовараУчетноеПослеИзменения");
			Иначе
				НоваяСтрока.КоличествоПрослеживаемости = ЭлектронныеДокументыВнутренний.ЗначениеРеквизитаВДереве(СтрокаНомераТаблицы,ПрефиксПути + ".Количество");
				НоваяСтрока.Количество = ЭлектронныеДокументыВнутренний.ЗначениеРеквизитаВДереве(СтрокаНомераТаблицы,ПрефиксПути + ".КоличествоУчетное");
			КонецЕсли;
			
			ЕстьДанныеПрослеживаемости = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьДанныеПрослеживаемости Тогда
		РассчитатьПропорциональноУчетноеКоличество(СведенияОПрослеживаемости, ДанныеСтроки.Количество);
	КонецЕсли;
		
	Если ЕстьДанныеПрослеживаемости И Не ДанныеСтроки.ПрослеживаемыйТовар Тогда
		
		ОписаниеСобытия = "В ЭД есть информация о прослеживаемости, но для кода ТНВЭД номенклатуры не установлен признак Прослеживаемый товар.";
						ЭлектронныеДокументыСлужебный.ВыполнитьЗаписьСобытияПоЭДВЖурналРегистрации(ОписаниеСобытия, 2, 
						УровеньЖурналаРегистрации.Предупреждение, Метаданные.Справочники.Номенклатура, ДанныеСтроки.Номенклатура);
	КонецЕсли;

КонецПроцедуры

Процедура РассчитатьПропорциональноУчетноеКоличество(СведенияПрослеживаемости, КоличествоТовара)
	
	// Количество учетное получено из данных электронного документа, рассчитывать не нужно. 
	Если СведенияПрослеживаемости.Итог("Количество") > 0 Тогда
		Возврат;
	КонецЕсли;
	
	ВсегоКоличествоПрослеживаемости = СведенияПрослеживаемости.Итог("КоличествоПрослеживаемости");
	
	// Если количество прослеживаемости равно количеству товара в строке, то скорее всего единица измерения прослеживаемости совпадает
	// с учетной единицей измерения и количество учетное равно количеству прослеживаемости.
	Если ВсегоКоличествоПрослеживаемости = КоличествоТовара Тогда
		Для Каждого СтрокаПрослеживаемости Из СведенияПрослеживаемости Цикл
			СтрокаПрослеживаемости.Количество = СтрокаПрослеживаемости.КоличествоПрослеживаемости;
		КонецЦикла;
		
		Возврат;
		
	КонецЕсли;
	
	СведенияПрослеживаемости.Колонки.Добавить("Порядок");
	Порядок = 0;
	Для Каждого СтрокаПрослеживаемости Из СведенияПрослеживаемости Цикл
		Порядок = Порядок + 1;
		СтрокаПрослеживаемости.Порядок = Порядок;
	КонецЦикла;
	
	СведенияПрослеживаемости.Сортировать("КоличествоПрослеживаемости");
	Распределено = 0;
	Для Каждого СтрокаПрослеживаемости Из СведенияПрослеживаемости Цикл
		Если СведенияПрослеживаемости.Индекс(СтрокаПрослеживаемости) + 1 = СведенияПрослеживаемости.Количество() Тогда
			СтрокаПрослеживаемости.Количество = КоличествоТовара - Распределено;
		Иначе 
			СтрокаПрослеживаемости.Количество = ?(ВсегоКоличествоПрослеживаемости = 0, 0,
				СтрокаПрослеживаемости.КоличествоПрослеживаемости * КоличествоТовара / ВсегоКоличествоПрослеживаемости);
			Распределено = Распределено + СтрокаПрослеживаемости.Количество;
		КонецЕсли;
	КонецЦикла;
	
	СведенияПрослеживаемости.Сортировать("Порядок");
	
КонецПроцедуры

Процедура ЗаполнитьТабличныеЧастиКорректировкиПоступления(ДокументОбъект, ДанныеДокумента)
	
	ТабличныеЧастиДляЗаполнения = Новый Структура("Товары, Услуги");
	
	Для Каждого ТабличнаяЧасть Из ТабличныеЧастиДляЗаполнения Цикл
		
		ИмяТЧ = ТабличнаяЧасть.Ключ;
		
		СтруктураПоиска = Новый Структура("Номенклатура, СтавкаНДС");
		Если ИмяТЧ = "Товары" Тогда
			СтруктураПоиска.Вставить("ХарактеристикаНоменклатуры");
			СтруктураПоиска.Вставить("ЕдиницаИзмерения");
		КонецЕсли;
		
		Для Каждого СтрокаДокумента Из ДокументОбъект[ИмяТЧ] Цикл
			
			ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаДокумента);
			РезультатПоискаДанныхЗаполнения = ДанныеДокумента[ИмяТЧ].НайтиСтроки(СтруктураПоиска);
			
			Если РезультатПоискаДанныхЗаполнения.Количество() = 0 Тогда
				
				// Такой строки в данных корректировки нет - обнуляем данные строки документа.
				СтрокаДокумента.Количество = 0;
				СтрокаДокумента.Цена       = 0;
				СтрокаДокумента.Сумма      = 0;
				СтрокаДокумента.СуммаНДС   = 0;
				
			Иначе
				
				СтрокаДанныхЗаполнения = РезультатПоискаДанныхЗаполнения[0];
				СвойстваДляЗаполнения = "Количество, Цена, Сумма, СуммаНДС";
				Если ИмяТЧ = "Товары" Тогда
					СвойстваДляЗаполнения = СвойстваДляЗаполнения + ", КлючСвязи";
				КонецЕсли;
				ЗаполнитьЗначенияСвойств(СтрокаДокумента, СтрокаДанныхЗаполнения, СвойстваДляЗаполнения);
				
				// Удалим обработанную строку из данных заполнения
				ДанныеДокумента[ИмяТЧ].Удалить(СтрокаДанныхЗаполнения);
				
			КонецЕсли;
		КонецЦикла;
		
		// Добавим новые строки в документ
		Если ДанныеДокумента[ИмяТЧ].Количество() > 0 Тогда
			Для Каждого СтрокаДанныхЗаполнения Из ДанныеДокумента[ИмяТЧ] Цикл
				НоваяСтрока = ДокументОбъект[ИмяТЧ].Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДанныхЗаполнения);
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Универсальные документы -

// Возвращает данные по номенклатуре и характеристике товара на основании данных о номенклатуре поставщика
// 
// Параметры:
//  НоменклатураПоставщика - Структура, содержит сведения о номенклатуре поставщика
//
// Возвращаемое значение:
//  Структура - Структура, содержащая поля:
//               Номенклатура
//               ХарактеристикаНоменклатуры
//
Функция ПолучитьДанныеТовараПоНоменклатуреПоставщика(НоменклатураПоставщика)
	
	ДанныеПоТовару = Новый Структура(
		"Номенклатура, ХарактеристикаНоменклатуры",
		Справочники.Номенклатура.ПустаяСсылка(),
		Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
		
	Если НЕ НоменклатураПоставщика.Свойство("Идентификатор")
			И НЕ НоменклатураПоставщика.Свойство("Ид") Тогда
		Возврат ДанныеПоТовару; 
	КонецЕсли;
	
	ИдентификаторТовара = ?(НоменклатураПоставщика.Свойство("Идентификатор"), НоменклатураПоставщика.Идентификатор, НоменклатураПоставщика.Ид);
	ВладелецТовара = ?(НоменклатураПоставщика.Свойство("Владелец"), НоменклатураПоставщика.Владелец, НоменклатураПоставщика.НоменклатураПоставщика.Владелец);
	
	// Если пришел пустой ИД, используем вместо него наименование товара.
	// Актуально для входящих ЭД из учетных систем отличных от 1С.
	Если Не ЗначениеЗаполнено(ИдентификаторТовара) Тогда
		ИдентификаторТовара = ИдентификаторТовараПоСтроке(ВРег(СтрЗаменить(НоменклатураПоставщика.Наименование, " ", "")) + "###" + 
																ВРег(СтрЗаменить(НоменклатураПоставщика.Артикул, " ", "")));
	КонецЕсли;
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НоменклатураКонтрагентов.Номенклатура,
	|	НоменклатураКонтрагентов.ХарактеристикаНоменклатуры
	|ИЗ
	|	РегистрСведений.НоменклатураКонтрагентов КАК НоменклатураКонтрагентов
	|ГДЕ
	|	НоменклатураКонтрагентов.Контрагент = &Контрагент
	|	И НоменклатураКонтрагентов.Идентификатор = &Идентификатор";
	
	Запрос.УстановитьПараметр("Контрагент", ВладелецТовара);
	Запрос.УстановитьПараметр("Идентификатор", ИдентификаторТовара);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		ДанныеПоТовару.Номенклатура 				= Выборка.Номенклатура;
		ДанныеПоТовару.ХарактеристикаНоменклатуры 	= Выборка.ХарактеристикаНоменклатуры; 
	КонецЕсли;
	
	Возврат ДанныеПоТовару;
	
КонецФункции

// Процедура удаляет из исходного массива документы неподходящие для формирования ЭД
//
// Параметры
//  МассивДокументов - массив документов для формирования ЭД
//  МассивДокументовДляУдаления - массив документов неподходящих для формирования ЭД
//  ШаблонСообщения - шаблон сообщения
//
Процедура УдалитьДокументыНеподходящиеДляФормированияЭД(МассивДокументов, МассивДокументовДляУдаления, ШаблонСообщения)
	
	Для Каждого Документ Из МассивДокументовДляУдаления Цикл
		Найденный = МассивДокументов.Найти(Документ);
		Если Найденный <> Неопределено Тогда
			МассивДокументов.Удалить(Найденный);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, Строка(Документ)), 
				Документ);
		КонецЕсли;
		
	КонецЦикла
	
КонецПроцедуры

Процедура ДобавитьОбязательныеПоля(СтруктураШапки, СтрокаИменПолей = "")
	Если СтрокаИменПолей = "" Тогда
		СтрокаИменПолей = "Организация,Контрагент";
	КонецЕсли;
	ИменаПолей = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрокаИменПолей);
	Для Каждого ИмяПоля Из ИменаПолей Цикл
		Если Не СтруктураШапки.Свойство(ИмяПоля) Тогда
			СтруктураШапки.Вставить(ИмяПоля);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Вспомогательные процедуры и функции

// Перезаполняет реквизиты шапки объекта.
//
// Параметры:
//  ТекущийОбъект    - Объект ИБ, реквизиты шапки которого необходимо заполнить,
//  ДанныеЗаполнения - Структура значений, которые необходимо подставить в объект ИБ.
//
Процедура ПерезаполнениеЗначенийРеквизитовШапки(ТекущийОбъект, ДанныеЗаполнения)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого Строка Из ДанныеЗаполнения Цикл
		
		Если ЗначениеЗаполнено(Строка.Ключ) И ТекущийОбъект.Метаданные().Реквизиты.Найти(Строка.Ключ) <> Неопределено Тогда
			Если ТекущийОбъект[Строка.Ключ] <> Строка.Значение Тогда
				ТекущийОбъект[Строка.Ключ] = Строка.Значение;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработатьТаблицуТоваров(ТаблицаТоваров, ДопОбработка = Ложь, ИмяКолонкиИД = "ИД")
	
	Для Каждого Строка из ТаблицаТоваров Цикл
		
		// Cформируем идентификатор
		ИДТовара = Строка.Номенклатура.УникальныйИдентификатор();
		ИДХарактеристики = ?(ЗначениеЗаполнено(Строка.Характеристика), Строка.Характеристика.УникальныйИдентификатор(), "");
		ИДУпаковки = "";
		Строка[ИмяКолонкиИД] = Строка(ИДТовара)+"#"+Строка(ИДХарактеристики)+"#"+Строка(ИДУпаковки);
		
		// Сформируем наименование
		Строка.Наименование = Строка.Наименование + ?(ЗначениеЗаполнено(Строка.Характеристика), " ("+Строка.Характеристика+")", "");
		
		Если ДопОбработка Тогда
			Строка.ДопДанныеНеПодписанные = Новый Структура();
			Строка.ДопДанныеПодписанные = Новый Структура("СтавкаНДС, ИД", Строка(Строка.СтавкаНДС), Строка[ИмяКолонкиИД]);
			Строка.СтавкаНДС = ПолучитьСтавкуНДСЧислом(Строка.СтавкаНДС);
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Возвращает данные для формирования ЭД "ТОРГ12" на основании документов
//   - РеализацияТоваровИУслуг 
//   - КорректировкаРеализации (исправляющего реализацию)
// 
Функция ПолучитьДанныеРеализации(СсылкаНаОбъект)
	
	ДанныеДокумента = Новый Структура();
	ДокументОбъект = СсылкаНаОбъект.ПолучитьОбъект();
		
	// Подготовим данные шапки документа
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	//КорректировкаРеализации ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЭД.Исправление) КАК ВидОперации,
	|	//КорректировкаРеализации ДокументРеализации КАК ДокументОснование,
	|	//КорректировкаРеализации ИсправляемыйДокументРеализации КАК ИсправляемыйДокументРеализации,
	|	//РеализацияТоваровУслуг """" КАК ВидОперации,
	|	//РеализацияТоваровУслуг Сделка КАК ДокументОснование,
	|	//РеализацияТоваровУслуг ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка) КАК ИсправляемыйДокументРеализации,
	|	Номер КАК НомерДокумента,
	|	Дата КАК ДатаДокумента,
	|	РеализацияТоваровУслуг.Организация,
	|	РеализацияТоваровУслуг.Организация КАК ЮрФизЛицо,
	|	РеализацияТоваровУслуг.Организация КАК Поставщик,
	|	РеализацияТоваровУслуг.Контрагент КАК Контрагент,
	|	РеализацияТоваровУслуг.Организация КАК Руководители,
	|	РеализацияТоваровУслуг.АдресДоставки КАК АдресДоставки,
	|	ВЫБОР 
	|		КОГДА Грузополучатель = &ПустойКонтрагент
	|		ТОГДА Контрагент
	|		ИНАЧЕ Грузополучатель 
	|	КОНЕЦ КАК Грузополучатель,
	|	ВЫБОР 
	|		КОГДА Грузоотправитель = &ПустойКонтрагент
	|		ТОГДА РеализацияТоваровУслуг.Организация
	|		ИНАЧЕ Грузоотправитель 
	|	КОНЕЦ КАК Грузоотправитель,
	|	БанковскийСчетОрганизации КАК БанковскийСчет,
	|	Контрагент КАК Покупатель,
	|	Контрагент КАК Плательщик,
	|	Сделка,
	|	ДоговорКонтрагента.Представление  КАК ДоговорНаименование,
	|	ДоговорКонтрагента.Номер          КАК ДоговорНомер,
	|	ДоговорКонтрагента.Дата           КАК ДоговорДата,
	|	ДоговорКонтрагента.ВедениеВзаиморасчетов КАК ВедениеВзаиморасчетов,
	|	ОтветственныеЛица.ФизическоеЛицо КАК ОтветственноеЛицо,
	|	//РеализацияТоваровУслуг ОтпускРазрешил,
	|	//РеализацияТоваровУслуг ОтпускПроизвел,
	|	//КорректировкаРеализации ИсправляемыйДокументРеализации.ОтпускРазрешил КАК ОтпускРазрешил,
	|	//КорректировкаРеализации ИсправляемыйДокументРеализации.ОтпускПроизвел КАК ОтпускПроизвел,
	|	ПРЕДСТАВЛЕНИЕ(РеализацияТоваровУслуг.Подразделение) КАК Подразделение,
	|	ВалютаДокумента,
	|	КурсВзаиморасчетов      КАК Курс,
	|	КратностьВзаиморасчетов КАК Кратность,
	|	УчитыватьНДС,
	|	СуммаВключаетНДС
	|
	|ИЗ
	|	//РеализацияТоваровУслуг Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|	//КорректировкаРеализации Документ.КорректировкаРеализации КАК РеализацияТоваровУслуг
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ОтветственныеЛица.СрезПоследних(&ДатаСреза, СтруктурнаяЕдиница = &СтруктурнаяЕдиница) КАК ОтветственныеЛица
	|	ПО
	|		ОтветственныеЛица.СтруктурнаяЕдиница = РеализацияТоваровУслуг.Склад
	| 	
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка = &ТекущийДокумент
	|";
	
	Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//РеализацияТоваровУслуг", "");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//КорректировкаРеализации", "");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ДатаСреза",          СсылкаНаОбъект.Дата);
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", СсылкаНаОбъект.Склад);
	Запрос.УстановитьПараметр("ТекущийДокумент",    СсылкаНаОбъект);
	Запрос.УстановитьПараметр("ПустойКонтрагент",   Справочники.Контрагенты.ПустаяСсылка());
	Запрос.УстановитьПараметр("Организация",        СсылкаНаОбъект.Организация);
	Запрос.УстановитьПараметр("Подразделение",      СсылкаНаОбъект.Подразделение);
	
	Шапка = Запрос.Выполнить().Выбрать();
	Шапка.Следующий();
	
	ДанныеДокумента.Вставить("РеквизитыШапки", Шапка);
	
	// Подготовим данные табличных частей
	
	СтрокаВыборкиПоляСодержания    = ОбработкаТабличныхЧастей.ПолучитьЧастьЗапросаДляВыбораСодержания("РеализацияТоваровУслуг");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РеализацияТоваровУслуг.НомерСтроки                                             КАК НомерСтроки,
	|	РеализацияТоваровУслуг.Номенклатура.Код                                        КАК КодТовара,
	|	РеализацияТоваровУслуг.Номенклатура.Артикул 	                               КАК Артикул,
	|	РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное                         КАК Наименование,
	|	РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное                         КАК НаименованиеНоменклатуры,
	|	РеализацияТоваровУслуг.Номенклатура                                            КАК Номенклатура,
	|	РеализацияТоваровУслуг.Количество                                              КАК Количество,
	|	РеализацияТоваровУслуг.Сумма * &Курс / &Кратность	                           КАК Сумма,
	|	РеализацияТоваровУслуг.Цена * &Курс / &Кратность                               КАК Цена,
	|	ПРЕДСТАВЛЕНИЕ(РеализацияТоваровУслуг.ХарактеристикаНоменклатуры)               КАК НаименованиеХарактеристики,
	|	РеализацияТоваровУслуг.ХарактеристикаНоменклатуры                              КАК Характеристика,
	|	РеализацияТоваровУслуг.Номенклатура.БазоваяЕдиницаИзмерения                    КАК БазоваяЕдиница,
	|	РеализацияТоваровУслуг.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код            КАК БазоваяЕдиницаКод,
	|	РеализацияТоваровУслуг.Номенклатура.БазоваяЕдиницаИзмерения.Наименование       КАК БазоваяЕдиницаНаименование,
	|	РеализацияТоваровУслуг.Номенклатура.БазоваяЕдиницаИзмерения.НаименованиеПолное КАК БазоваяЕдиницаНаименованиеПолное,
	|	РеализацияТоваровУслуг.Номенклатура.БазоваяЕдиницаИзмерения.МеждународноеСокращение КАК БазоваяЕдиницаМеждународноеСокращение,
	|	РеализацияТоваровУслуг.ЕдиницаИзмерения.ЕдиницаПоКлассификатору                КАК ЕдиницаИзмерения,
	|	РеализацияТоваровУслуг.ЕдиницаИзмерения.ЕдиницаПоКлассификатору                КАК Упаковка,
	|	РеализацияТоваровУслуг.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код            КАК УпаковкаКод,
	|	РеализацияТоваровУслуг.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Наименование   КАК УпаковкаНаименование,
	|	РеализацияТоваровУслуг.Коэффициент                                             КАК Коэффициент,
	|	РеализацияТоваровУслуг.СтавкаНДС                                               КАК СтавкаНДС,
	|	РеализацияТоваровУслуг.СуммаНДС * &Курс / &Кратность       					   КАК СуммаНДС,
	|	0                                                                              КАК СуммаСкидки,
	|	РеализацияТоваровУслуг.ЕдиницаИзмеренияМест.Представление                      КАК ВидУпаковки,
	|	РеализацияТоваровУслуг.КоличествоМест                                          КАК КоличествоМест,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.ЕдиницаИзмеренияМест.Коэффициент, 0)
	|		/ РеализацияТоваровУслуг.Коэффициент                                       КАК КоличествоВОдномМесте,
	|	РеализацияТоваровУслуг.Количество                                              КАК МассаНетто,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслуг.КоличествоМест > 0 
	|		ТОГДА РеализацияТоваровУслуг.КоличествоМест * РеализацияТоваровУслуг.ЕдиницаИзмеренияМест.Вес
	|		ИНАЧЕ РеализацияТоваровУслуг.Количество * РеализацияТоваровУслуг.ЕдиницаИзмерения.Вес
	|	КОНЕЦ                                                                          КАК МассаБрутто,
	|   РеализацияТоваровУслуг.ЗаказПокупателя                                         КАК ДокументОснование,
	|   РеализацияТоваровУслуг.КлючСвязи                                         	   КАК КлючСвязи
	|ИЗ
	|	//РеализацияТоваровУслуг Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслуг
	|	//КорректировкаРеализации Документ.КорректировкаРеализации.Товары КАК РеализацияТоваровУслуг
	|
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка = &Ссылка
	|	
	|	//КорректировкаРеализации И (РеализацияТоваровУслуг.Сумма > 0 ИЛИ РеализацияТоваровУслуг.Количество > 0)
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.НомерСтроки,
	|	РеализацияТоваровУслуг.Номенклатура.Код,
	|	РеализацияТоваровУслуг.Номенклатура.Артикул,
	|	"+СтрокаВыборкиПоляСодержания+",
	|	"+СтрокаВыборкиПоляСодержания+",
	|	РеализацияТоваровУслуг.Номенклатура,
	|	РеализацияТоваровУслуг.Количество,
	|	РеализацияТоваровУслуг.Сумма * &Курс / &Кратность,
	|	РеализацияТоваровУслуг.Цена * &Курс / &Кратность,
	|	"""",
	|	Неопределено,
	|	РеализацияТоваровУслуг.Номенклатура.БазоваяЕдиницаИзмерения,
	|	РеализацияТоваровУслуг.Номенклатура.БазоваяЕдиницаИзмерения.Код,
	|	РеализацияТоваровУслуг.Номенклатура.БазоваяЕдиницаИзмерения.Наименование,
	|	РеализацияТоваровУслуг.Номенклатура.БазоваяЕдиницаИзмерения.НаименованиеПолное,
	|	РеализацияТоваровУслуг.Номенклатура.БазоваяЕдиницаИзмерения.МеждународноеСокращение,
	|	РеализацияТоваровУслуг.Номенклатура.БазоваяЕдиницаИзмерения,
	|	РеализацияТоваровУслуг.Номенклатура.БазоваяЕдиницаИзмерения,
	|	РеализацияТоваровУслуг.Номенклатура.БазоваяЕдиницаИзмерения.Код,
	|	РеализацияТоваровУслуг.Номенклатура.БазоваяЕдиницаИзмерения.Наименование,
	|	1,
	|	РеализацияТоваровУслуг.СтавкаНДС,
	|	РеализацияТоваровУслуг.СуммаНДС * &Курс / &Кратность,
	|	0,
	|	Неопределено,
	|	0,
	|	0,
	|	РеализацияТоваровУслуг.Количество,
	|	0,
	|	РеализацияТоваровУслуг.ЗаказПокупателя,
	|	-1
	|ИЗ
	|	//РеализацияТоваровУслуг Документ.РеализацияТоваровУслуг.Услуги КАК РеализацияТоваровУслуг
	|	//КорректировкаРеализации Документ.КорректировкаРеализации.Услуги КАК РеализацияТоваровУслуг
	|	
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка = &Ссылка
	|	
	|	//КорректировкаРеализации И (РеализацияТоваровУслуг.Сумма > 0 ИЛИ РеализацияТоваровУслуг.Количество > 0)";
	
	Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//РеализацияТоваровУслуг", "");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//КорректировкаРеализации", "");
	КонецЕсли;
	
	мВалютаРегламентированногоУчета = глЗначениеПеременной("ВалютаРегламентированногоУчета");
	
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъект);
	УниверсальныеМеханизмы.ОпределитьКурсыДокументаДляПечати(ДокументОбъект, Запрос);
	
	ТаблицаТоваров = Запрос.Выполнить().Выгрузить();
	
	ПараметрыПересчета = УчетНДС.СтруктураПараметровДляПересчетаСуммВРубли();
	ПараметрыПересчета.СуммаАвансаВалИт     = ?(Запрос.Параметры.СуммаАвансаВал=Неопределено,0,Запрос.Параметры.СуммаАвансаВал);
	ПараметрыПересчета.СуммаАвансаРубИт     = ?(Запрос.Параметры.СуммаАвансаРуб=Неопределено,0,Запрос.Параметры.СуммаАвансаРуб);
	ПараметрыПересчета.СуммаРеализацииВалИт = УчетНДС.ПолучитьСуммуДокументаСНДС(ДокументОбъект);
	ПараметрыПересчета.ВалютаВзаиморасчетов = ДокументОбъект.ВалютаДокумента;
	ПараметрыПересчета.КурсРеализации       = ЗаполнениеДокументов.КурсДокумента(ДокументОбъект, мВалютаРегламентированногоУчета);
	ПараметрыПересчета.КратностьРеализации  = ЗаполнениеДокументов.КратностьДокумента(ДокументОбъект, мВалютаРегламентированногоУчета);
	ПараметрыПересчета.СуммаВключаетНДС     = ДокументОбъект.СуммаВключаетНДС;
	ПараметрыПересчета.ДатаПересчета        = ДокументОбъект.Дата;
	УчетНДС.ПерезаполнитьСуммыПоУЕвРублях(ТаблицаТоваров, ПараметрыПересчета);
	
	ТаблицаТоваров.Колонки.Добавить("СуммаБезНДС", Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(18, 2)));
	ТаблицаТоваров.Колонки.Добавить("СуммаСНДС",   Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(18, 2)));
	
	ТаблицаТоваров.Колонки.Добавить("Сопоставление");
	
	Для каждого СтрокаТаблицы из ТаблицаТоваров Цикл
		
		СтрокаТаблицы.Наименование = Лев(СокрЛП(СтрокаТаблицы.Наименование),1000);
		СтрокаТаблицы.НаименованиеНоменклатуры = Лев(СокрЛП(СтрокаТаблицы.НаименованиеНоменклатуры),1000);
		
		СуммаСНДС   = Окр((СтрокаТаблицы.Сумма + ?(Шапка.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС)), 2);
		СуммаНДС    = Окр(СтрокаТаблицы.СуммаНДС, 2);
		СуммаБезНДС = СуммаСНДС - СуммаНДС;
		
		СтрокаТаблицы.СуммаНДС    = СуммаНДС;
		СтрокаТаблицы.СуммаБезНДС = СуммаБезНДС;
		СтрокаТаблицы.СуммаСНДС   = СуммаСНДС;
		
		Если Шапка.СуммаВключаетНДС Тогда
			СтрокаТаблицы.Цена = ?(СтрокаТаблицы.Количество = 0, 0, Окр(СуммаБезНДС / СтрокаТаблицы.Количество, 2));
		Иначе
			СтрокаТаблицы.Цена = Окр(СтрокаТаблицы.Цена, 2);
		КонецЕсли;
		
		Если СтрокаТаблицы.КоличествоВОдномМесте <> NULL
			И СтрокаТаблицы.КоличествоВОдномМесте <> Цел(СтрокаТаблицы.КоличествоВОдномМесте) Тогда
			СтрокаТаблицы.КоличествоВОдномМесте = Цел(СтрокаТаблицы.КоличествоВОдномМесте) + 1;
		КонецЕсли;
		
		ДанныеСопоставления = СопоставлениеНоменклатуры();
		ДанныеСопоставления.НоменклатураИБ   = СтрокаТаблицы.Номенклатура;
		ДанныеСопоставления.ХарактеристикаИБ = СтрокаТаблицы.Характеристика;
		ДанныеСопоставления.УпаковкаИБ       = СтрокаТаблицы.Упаковка;
		
		СтрокаТаблицы.Сопоставление = ДанныеСопоставления;
		
	КонецЦикла;
	
	ДанныеДокумента.Вставить("ТаблицаТоваров", ТаблицаТоваров);
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СерийныеНомера.КлючСвязи,
	|	СерийныеНомера.СерийныйНомер.Код КАК СерийныйНомер
	|ИЗ
	|	//РеализацияТоваровУслуг Документ.РеализацияТоваровУслуг.СерийныеНомера КАК СерийныеНомера
	|	//КорректировкаРеализации Документ.КорректировкаРеализации.СерийныеНомера КАК СерийныеНомера
	|ГДЕ
	|	СерийныеНомера.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъект);
	
	Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//РеализацияТоваровУслуг", "");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//КорректировкаРеализации", "");
	КонецЕсли;
	
	ТаблицаСН = Запрос.Выполнить().Выгрузить();
	
	ДанныеДокумента.Вставить("ТаблицаСН", ТаблицаСН);
	
	Возврат ДанныеДокумента;
	
КонецФункции

// Возвращает данные для формирования ЭД "АктВыполненныхРабот" на основании документов
//   - РеализацияТоваровИУслуг (акт выполненных работ), 
//   - КорректировкаРеализации (исправляющего реализацию)
// 
Функция ПодготовитьДанныеАктаОбОказанииУслуг(СсылкаНаОбъект)
	
	ДанныеДокумента = Новый Структура();
	ДокументОбъект = СсылкаНаОбъект.ПолучитьОбъект();
	мВалютаРегламентированногоУчета = глЗначениеПеременной("ВалютаРегламентированногоУчета");
	
	СтрокаВыборкиПоляСодержания = ОбработкаТабличныхЧастей.ПолучитьЧастьЗапросаДляВыбораСодержания("Акт");
	
	ТекстЗапросаШапка =
	"ВЫБРАТЬ
	|	//РеализацияТоваровУслуг """"   КАК ВидОперации,
	|	//РеализацияТоваровУслуг Сделка КАК ДокументОснование,
	|	//РеализацияТоваровУслуг ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка) КАК ИсправляемыйДокументРеализации,
	|	//КорректировкаРеализации ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЭД.Исправление) КАК ВидОперации,
	|	//КорректировкаРеализации ДокументРеализации КАК ДокументОснование,
	|	//КорректировкаРеализации ИсправляемыйДокументРеализации КАК ИсправляемыйДокументРеализации,
	|	Акт.Номер КАК НомерДокумента,
	|	Акт.Дата КАК ДатаДокумента,
	|	Акт.ДоговорКонтрагента,
	|	Акт.ДоговорКонтрагента.Представление  КАК ДоговорНаименование,
	|	Акт.ДоговорКонтрагента.Номер          КАК ДоговорНомер,
	|	Акт.ДоговорКонтрагента.Дата           КАК ДоговорДата,
	|	Акт.Контрагент           КАК Заказчик,
	|	Акт.Организация          КАК Исполнитель,
	|	Акт.Организация,
	|	Акт.СуммаДокумента,
	|	Акт.ВалютаДокумента,
	|	Акт.ВалютаДокумента.Код  КАК КодВалютыДокумента,
	|	Акт.УчитыватьНДС,
	|	Акт.СуммаВключаетНДС,
	|	Акт.КратностьВзаиморасчетов,
	|	Акт.КурсВзаиморасчетов
	|ИЗ
	|	//РеализацияТоваровУслуг Документ.РеализацияТоваровУслуг КАК Акт
	|	//КорректировкаРеализации Документ.КорректировкаРеализации КАК Акт
	|	
	|ГДЕ
	|	Акт.Ссылка = &ТекущийДокумент";
	
	ТекстЗапросаТЧ =
	"ВЫБРАТЬ
	|	МИНИМУМ(Акт.НомерСтроки)                     КАК НомерСтроки,
	|	Акт.Номенклатура                             КАК Номенклатура,
	|	НЕОПРЕДЕЛЕНО                                 КАК Характеристика,
	|	ВЫБОР 
	|		КОГДА НЕ (Акт.Номенклатура.НаименованиеПолное ПОДОБНО """")
	|			ТОГДА ВЫРАЗИТЬ (Акт.Номенклатура.НаименованиеПолное КАК Строка(1000))
	|		ИНАЧЕ Акт.Номенклатура.Наименование
	|	КОНЕЦ                                         КАК НаименованиеНоменклатуры,
	|	" + СтрокаВыборкиПоляСодержания + "           КАК Описание,
	|	Акт.Номенклатура.БазоваяЕдиницаИзмерения      КАК БазоваяЕдиница,
	|	Акт.Номенклатура.БазоваяЕдиницаИзмерения.Код  КАК БазоваяЕдиницаКод,
	|	
	|	//РеализацияТоваровУслуг Акт.Номенклатура.ЕдиницаХраненияОстатков.ЕдиницаПоКлассификатору КАК ЕдиницаИзмерения,
	|	//КорректировкаРеализации Акт.Номенклатура.ЕдиницаХраненияОстатков.ЕдиницаПоКлассификатору КАК ЕдиницаИзмерения,
	|	
	|	//РеализацияТоваровУслуг Акт.Номенклатура.ЕдиницаХраненияОстатков.ЕдиницаПоКлассификатору.Код КАК ЕдиницаИзмеренияКод,
	|	//КорректировкаРеализации Акт.Номенклатура.ЕдиницаХраненияОстатков.ЕдиницаПоКлассификатору.Код КАК ЕдиницаИзмеренияКод,
	|
	|	//РеализацияТоваровУслуг Акт.Номенклатура.ЕдиницаХраненияОстатков.Наименование КАК ЕдиницаИзмеренияНаименование,
	|	//КорректировкаРеализации Акт.Номенклатура.ЕдиницаХраненияОстатков.Наименование КАК ЕдиницаИзмеренияНаименование,
	|
	|	СУММА(Акт.Количество)                        КАК Количество,
	|	Акт.Цена * &Курс / &Кратность		         КАК Цена,
	|	СУММА(Акт.Сумма * &Курс / &Кратность)        КАК Сумма,
	|	Акт.СтавкаНДС                                КАК СтавкаНДС,
	|	СУММА(Акт.СуммаНДС * &Курс / &Кратность)     КАК СуммаНДС,
	|   Акт.ЗаказПокупателя                          КАК ДокументОснование
	|ИЗ
	|	
	|	//РеализацияТоваровУслуг Документ.РеализацияТоваровУслуг.Услуги КАК Акт
	|	//КорректировкаРеализации Документ.КорректировкаРеализации.Услуги КАК Акт
	|
	|ГДЕ
	|	Акт.Ссылка = &ТекущийДокумент
	|	
	|	//КорректировкаРеализации И (Акт.Сумма > 0 ИЛИ Акт.Количество > 0)
	|
	|СГРУППИРОВАТЬ ПО
	|	Акт.Номенклатура,
	|	" + СтрокаВыборкиПоляСодержания + ",
	|	
	|	//РеализацияТоваровУслуг Акт.Номенклатура.ЕдиницаХраненияОстатков.ЕдиницаПоКлассификатору,
	|	//КорректировкаРеализации Акт.Номенклатура.ЕдиницаХраненияОстатков.ЕдиницаПоКлассификатору,
	|	
	|	//РеализацияТоваровУслуг Акт.Номенклатура.ЕдиницаХраненияОстатков.Наименование,
	|	//КорректировкаРеализации Акт.Номенклатура.ЕдиницаХраненияОстатков.Наименование,
	|
	|	//РеализацияТоваровУслуг Акт.ЗаказПокупателя,
	|	//КорректировкаРеализации Акт.ЗаказПокупателя,
	|
	|	Акт.СтавкаНДС,
	|	Акт.Цена
	|	
	|УПОРЯДОЧИТЬ ПО 
	|	НомерСтроки";
	
	Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		ТекстЗапросаШапка = СтрЗаменить(ТекстЗапросаШапка, "//РеализацияТоваровУслуг", "");
		ТекстЗапросаТЧ    = СтрЗаменить(ТекстЗапросаТЧ,    "//РеализацияТоваровУслуг", "");
	Иначе
		ТекстЗапросаШапка = СтрЗаменить(ТекстЗапросаШапка, "//КорректировкаРеализации", "");
		ТекстЗапросаТЧ    = СтрЗаменить(ТекстЗапросаТЧ,    "//КорректировкаРеализации", "");
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекущийДокумент", СсылкаНаОбъект);
	Запрос.Текст = ТекстЗапросаШапка;
	
	// Получим данные шапки документа
	Шапка = Запрос.Выполнить().Выбрать();
	Шапка.Следующий();
	ДанныеДокумента.Вставить("РеквизитыШапки", Шапка);
	
	// Получим данные табличной части
	Запрос.Текст = ТекстЗапросаТЧ;
	УниверсальныеМеханизмы.ОпределитьКурсыДокументаДляПечати(ДокументОбъект, Запрос);
	ТаблицаУслуг = Запрос.Выполнить().Выгрузить();
	
	ПараметрыПересчета = УчетНДС.СтруктураПараметровДляПересчетаСуммВРубли();
	ПараметрыПересчета.СуммаАвансаВалИт     = ?(Запрос.Параметры.СуммаАвансаВал=Неопределено,0,Запрос.Параметры.СуммаАвансаВал);
	ПараметрыПересчета.СуммаАвансаРубИт     = ?(Запрос.Параметры.СуммаАвансаРуб=Неопределено,0,Запрос.Параметры.СуммаАвансаРуб);
	ПараметрыПересчета.СуммаРеализацииВалИт = УчетНДС.ПолучитьСуммуДокументаСНДС(ДокументОбъект);
	ПараметрыПересчета.ВалютаВзаиморасчетов = ДокументОбъект.ВалютаДокумента;
	ПараметрыПересчета.КурсРеализации       = ЗаполнениеДокументов.КурсДокумента(ДокументОбъект, мВалютаРегламентированногоУчета);
	ПараметрыПересчета.КратностьРеализации  = ЗаполнениеДокументов.КратностьДокумента(ДокументОбъект, мВалютаРегламентированногоУчета);
	ПараметрыПересчета.СуммаВключаетНДС     = ДокументОбъект.СуммаВключаетНДС;
	ПараметрыПересчета.ДатаПересчета        = ДокументОбъект.Дата;
	УчетНДС.ПерезаполнитьСуммыПоУЕвРублях(ТаблицаУслуг, ПараметрыПересчета);

	ТаблицаУслуг.Колонки.Добавить("СуммаБезНДС", Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(18, 2)));
	ТаблицаУслуг.Колонки.Добавить("СуммаСНДС",   Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(18, 2)));
	ТаблицаУслуг.Колонки.Добавить("ИД");
	
	ТаблицаУслуг.Колонки.Добавить("Сопоставление");
	
	Для каждого СтрокаТаблицы из ТаблицаУслуг Цикл
		
		СуммаСНДС   = Окр((СтрокаТаблицы.Сумма + ?(Шапка.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС)), 2);
		СуммаНДС    = Окр(СтрокаТаблицы.СуммаНДС, 2);
		СуммаБезНДС = СуммаСНДС - СуммаНДС;
		
		СтрокаТаблицы.СуммаНДС    = СуммаНДС;
		СтрокаТаблицы.СуммаБезНДС = СуммаБезНДС;
		СтрокаТаблицы.СуммаСНДС   = СуммаСНДС;
		
		Если Шапка.СуммаВключаетНДС Тогда
			СтрокаТаблицы.Цена = ?(СтрокаТаблицы.Количество = 0, 0, Окр(СуммаБезНДС / СтрокаТаблицы.Количество, 2));
		Иначе
			СтрокаТаблицы.Цена = Окр(СтрокаТаблицы.Цена, 2);
		КонецЕсли;
		
		// Заполним ИД номенклатуры
		СтрокаТаблицы.ИД = Строка(СтрокаТаблицы.Номенклатура.УникальныйИдентификатор()) + "##";
		
		ДанныеСопоставления = СопоставлениеНоменклатуры();
		ДанныеСопоставления.НоменклатураИБ = СтрокаТаблицы.Номенклатура;
		ДанныеСопоставления.УпаковкаИБ     = СтрокаТаблицы.ЕдиницаИзмерения;
		
		СтрокаТаблицы.Сопоставление = ДанныеСопоставления;
		
	КонецЦикла;
	
	ДанныеДокумента.Вставить("ТаблицаУслуг", ТаблицаУслуг);
	
	Возврат ДанныеДокумента;
	
КонецФункции

// Возвращает данные для формирования ЭД "СогласованноеИзменениеСтоимости" на основании документов
//   - КорректировкаРеализации (согласованное изменение)
//   - КорректировкаРеализации (исправление согласованного изменения)
// 
Функция ПолучитьДанныеСогласованногоИзменения(СсылкаНаОбъект)
	
	ДанныеДокумента = Новый Структура();
	
	// Подготовим данные шапки документа
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИсправленияПоступленияРеализации.ИсправлениеОшибки)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЭД.Исправление)
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ВидОперации,
	|	КорректировкаРеализации.ДокументРеализации КАК ДокументОснование,
	|	КорректировкаРеализации.ДокументРеализации КАК ИсправляемыйДокументРеализации,
	|	КорректировкаРеализации.Номер КАК Номер,
	|	КорректировкаРеализации.Дата КАК ДатаДокумента,
	|	КорректировкаРеализации.Организация КАК Организация,
	|	КорректировкаРеализации.Контрагент  КАК Контрагент,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.Грузополучатель = &ПустойКонтрагент
	|			ТОГДА КорректировкаРеализации.Контрагент
	|		ИНАЧЕ КорректировкаРеализации.Грузополучатель
	|	КОНЕЦ КАК Грузополучатель,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.Грузоотправитель = &ПустойКонтрагент
	|			ТОГДА КорректировкаРеализации.Организация
	|		ИНАЧЕ КорректировкаРеализации.Грузоотправитель
	|	КОНЕЦ КАК Грузоотправитель,
	|	КорректировкаРеализации.БанковскийСчетОрганизации КАК БанковскийСчет,
	|	КорректировкаРеализации.Контрагент КАК Покупатель,
	|	КорректировкаРеализации.Контрагент КАК Плательщик,
	|	КорректировкаРеализации.Сделка,
	|	КорректировкаРеализации.ДоговорКонтрагента.Представление КАК ДоговорНаименование,
	|	КорректировкаРеализации.ДоговорКонтрагента.Номер КАК ДоговорНомер,
	|	КорректировкаРеализации.ДоговорКонтрагента.Дата КАК ДоговорДата,
	|	КорректировкаРеализации.ДоговорКонтрагента.ВедениеВзаиморасчетов КАК ВедениеВзаиморасчетов,
	|	ОтветственныеЛица.ФизическоеЛицо КАК ОтветственноеЛицо,
	|	КорректировкаРеализации.Подразделение КАК Подразделение,
	|	КорректировкаРеализации.ВалютаДокумента,
	|	КорректировкаРеализации.ВалютаДокумента.Код КАК ВалютаКод,
	|	КорректировкаРеализации.КурсВзаиморасчетов КАК Курс,
	|	КорректировкаРеализации.КратностьВзаиморасчетов КАК Кратность,
	|	КорректировкаРеализации.УчитыватьНДС,
	|	КорректировкаРеализации.СуммаВключаетНДС,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИсправленияПоступленияРеализации.ИсправлениеОшибки)
	|			ТОГДА ""Корректировка""
	|		ИНАЧЕ ""Возврат""
	|	КОНЕЦ КАК ВидДокумента,
	|	ОперацияВыбытияМаркированныхТоваров КАК ОперацияВыбытияМаркированныхТоваров
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтветственныеЛица.СрезПоследних(&ДатаСреза, СтруктурнаяЕдиница = &СтруктурнаяЕдиница) КАК ОтветственныеЛица
	|		ПО (ОтветственныеЛица.СтруктурнаяЕдиница = КорректировкаРеализации.Склад)
	|ГДЕ
	|	КорректировкаРеализации.Ссылка = &ТекущийДокумент";
	
	Запрос.УстановитьПараметр("ДатаСреза",          СсылкаНаОбъект.Дата);
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", СсылкаНаОбъект.Склад);
	Запрос.УстановитьПараметр("ТекущийДокумент",    СсылкаНаОбъект);
	Запрос.УстановитьПараметр("ПустойКонтрагент",   Справочники.Контрагенты.ПустаяСсылка());
	Запрос.УстановитьПараметр("Организация",        СсылкаНаОбъект.Организация);
	Запрос.УстановитьПараметр("Подразделение",      СсылкаНаОбъект.Подразделение);
	
	Шапка = Запрос.Выполнить().Выбрать();
	Шапка.Следующий();
	
	ДанныеДокумента.Вставить("РеквизитыШапки", Шапка);
	
	// Подготовим данные табличных частей
	СтрокаВыборкиПоляСодержания    = ОбработкаТабличныхЧастей.ПолучитьЧастьЗапросаДляВыбораСодержания("КорректировкаРеализации");
	ВалютаРегламентированногоУчета = глЗначениеПеременной("ВалютаРегламентированногоУчета");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КорректировкаРеализации.НомерСтроки                                             КАК НомерСтроки,
	|	КорректировкаРеализации.Номенклатура.Код                                        КАК КодТовара,
	|	КорректировкаРеализации.Номенклатура.Артикул		                            КАК Артикул,
	|	КорректировкаРеализации.Номенклатура.НаименованиеПолное                         КАК Наименование,
	|	КорректировкаРеализации.Номенклатура.НаименованиеПолное                         КАК НаименованиеНоменклатуры,
	|	КорректировкаРеализации.Номенклатура                                            КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
	|			ТОГДА КорректировкаРеализации.КодТНВЭД.Код
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК КодВидаТовара,
	|	КорректировкаРеализации.Сумма * &Курс / &Кратность                              КАК Сумма,
	|	КорректировкаРеализации.СуммаДоИзменения * &Курс / &Кратность                   КАК СуммаДоКорректировки,
	|	КорректировкаРеализации.Цена *&Курс / &Кратность                                КАК Цена,
	|	КорректировкаРеализации.ЦенаДоИзменения *&Курс / &Кратность                     КАК ЦенаДоКорректировки,
	|	ПРЕДСТАВЛЕНИЕ(КорректировкаРеализации.ХарактеристикаНоменклатуры)               КАК НаименованиеХарактеристики,
	|	КорректировкаРеализации.ХарактеристикаНоменклатуры                              КАК Характеристика,
	|	КорректировкаРеализации.Номенклатура.БазоваяЕдиницаИзмерения                    КАК БазоваяЕдиница,
	|	КорректировкаРеализации.Номенклатура.БазоваяЕдиницаИзмерения.Код                КАК БазоваяЕдиницаКод,
	|	КорректировкаРеализации.Номенклатура.БазоваяЕдиницаИзмерения.Наименование       КАК БазоваяЕдиницаНаименование,
	|	КорректировкаРеализации.Номенклатура.БазоваяЕдиницаИзмерения.НаименованиеПолное КАК БазоваяЕдиницаНаименованиеПолное,
	|	КорректировкаРеализации.Номенклатура.БазоваяЕдиницаИзмерения.МеждународноеСокращение КАК БазоваяЕдиницаМеждународноеСокращение,
	|	КорректировкаРеализации.ЕдиницаИзмерения.ЕдиницаПоКлассификатору                КАК ЕдиницаИзмерения,
	|	КорректировкаРеализации.ЕдиницаИзмерения.ЕдиницаПоКлассификатору                КАК Упаковка,
	|	КорректировкаРеализации.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код            КАК УпаковкаКод,
	|	КорректировкаРеализации.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Наименование   КАК УпаковкаНаименование,
	|	КорректировкаРеализации.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Наименование   КАК УпаковкаНаименованиеДоКорректировки,
	|	КорректировкаРеализации.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код            КАК УпаковкаКодДоКорректировки,
	|	КорректировкаРеализации.Коэффициент                                             КАК Коэффициент,
	|	КорректировкаРеализации.СтавкаНДС                                               КАК СтавкаНДС,
	|	КорректировкаРеализации.СтавкаНДС                                               КАК СтавкаНДСДоКорректировки,
	|	КорректировкаРеализации.СуммаНДС * &Курс / &Кратность                           КАК СуммаНДС,
	|	КорректировкаРеализации.СуммаНДСДоИзменения * &Курс / &Кратность                КАК СуммаНДСДоКорректировки,
	|	0                                                                               КАК СуммаСкидки,
	|	КорректировкаРеализации.ЕдиницаИзмеренияМест.Представление                      КАК ВидУпаковки,
	|	КорректировкаРеализации.КоличествоМест                                          КАК КоличествоМест,
	|	КорректировкаРеализации.ЕдиницаИзмеренияМест.Коэффициент 
	|		/ КорректировкаРеализации.Коэффициент                                       КАК КоличествоВОдномМесте,
	|	КорректировкаРеализации.Количество                                              КАК МассаНетто,
	|	КорректировкаРеализации.КоличествоДоИзменения                                   КАК МассаНеттоДоКорректировки,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.КоличествоМест > 0 
	|		ТОГДА КорректировкаРеализации.КоличествоМест * КорректировкаРеализации.ЕдиницаИзмеренияМест.Вес
	|		ИНАЧЕ КорректировкаРеализации.Количество * КорректировкаРеализации.ЕдиницаИзмерения.Вес
	|	КОНЕЦ                                                                           КАК МассаБрутто,
	|   КорректировкаРеализации.ЗаказПокупателя                                         КАК ДокументОснование,
	|	КорректировкаРеализации.ИдентификаторСтроки                                     КАК ИдентификаторСтроки,
	|	КорректировкаРеализации.ПрослеживаемыйТовар                                     КАК ПрослеживаемыйТовар,
	|	ВЫРАЗИТЬ(КорректировкаРеализации.КодТНВЭД.ЕдиницаИзмерения.Код КАК Строка(4))   КАК ЕдиницаИзмеренияПрослеживаемостиКод,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.ПрослеживаемыйТовар
	|			ТОГДА ВЫБОР
	|					КОГДА КорректировкаРеализации.СтранаПроисхождения = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия)
	|						ТОГДА """"
	|					ИНАЧЕ КорректировкаРеализации.СтранаПроисхождения.Код
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА КорректировкаРеализации.СерияНоменклатуры.СтранаПроисхождения = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия)
	|					ТОГДА """"
	|				ИНАЧЕ КорректировкаРеализации.СерияНоменклатуры.СтранаПроисхождения.Код
	|			КОНЕЦ
	|	КОНЕЦ КАК СтранаПроисхожденияКод,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.ПрослеживаемыйТовар
	|			ТОГДА ВЫБОР
	|					КОГДА КорректировкаРеализации.СтранаПроисхождения = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия)
	|						ТОГДА """"
	|					ИНАЧЕ КорректировкаРеализации.СтранаПроисхождения.Наименование
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА КорректировкаРеализации.СерияНоменклатуры.СтранаПроисхождения = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия)
	|					ТОГДА """"
	|				ИНАЧЕ КорректировкаРеализации.СерияНоменклатуры.СтранаПроисхождения.Наименование
	|			КОНЕЦ
	|	КОНЕЦ КАК СтранаПроисхожденияНаименование,
	|	СерияНоменклатуры.НомерГТД.Код КАК ТаможеннаяДекларацияНомер
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК КорректировкаРеализации
	|
	|ГДЕ
	|	КорректировкаРеализации.Ссылка = &Ссылка
	|	И (КорректировкаРеализации.Количество <> КорректировкаРеализации.КоличествоДоИзменения
	|			ИЛИ КорректировкаРеализации.Сумма <> КорректировкаРеализации.СуммаДоИзменения
	|			ИЛИ КорректировкаРеализации.СуммаНДС <> КорректировкаРеализации.СуммаНДСДоИзменения
	|			ИЛИ КорректировкаРеализации.Цена <> КорректировкаРеализации.ЦенаДоИзменения)
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	КорректировкаРеализации.НомерСтроки,
	|	КорректировкаРеализации.Номенклатура.Код,
	|	КорректировкаРеализации.Номенклатура.Артикул,
	|	"+СтрокаВыборкиПоляСодержания+",
	|	"+СтрокаВыборкиПоляСодержания+",
	|	КорректировкаРеализации.Номенклатура,
	|	"""",
	|	КорректировкаРеализации.Сумма * &Курс / &Кратность,
	|	КорректировкаРеализации.СуммаДоИзменения * &Курс / &Кратность,
	|	КорректировкаРеализации.Цена * &Курс / &Кратность,
	|	КорректировкаРеализации.ЦенаДоИзменения * &Курс / &Кратность,
	|	"""",
	|	Неопределено,
	|	КорректировкаРеализации.Номенклатура.БазоваяЕдиницаИзмерения,
	|	КорректировкаРеализации.Номенклатура.БазоваяЕдиницаИзмерения.Код,
	|	КорректировкаРеализации.Номенклатура.БазоваяЕдиницаИзмерения.Наименование,
	|	КорректировкаРеализации.Номенклатура.БазоваяЕдиницаИзмерения.НаименованиеПолное,
	|	КорректировкаРеализации.Номенклатура.БазоваяЕдиницаИзмерения.МеждународноеСокращение,
	|	КорректировкаРеализации.Номенклатура.БазоваяЕдиницаИзмерения,
	|	КорректировкаРеализации.Номенклатура.БазоваяЕдиницаИзмерения,
	|	КорректировкаРеализации.Номенклатура.БазоваяЕдиницаИзмерения.Код,
	|	КорректировкаРеализации.Номенклатура.БазоваяЕдиницаИзмерения.Наименование,
	|	КорректировкаРеализации.Номенклатура.БазоваяЕдиницаИзмерения.Наименование,
	|	КорректировкаРеализации.Номенклатура.БазоваяЕдиницаИзмерения.Код,
	|	1,
	|	КорректировкаРеализации.СтавкаНДС,
	|	КорректировкаРеализации.СтавкаНДС,
	|	КорректировкаРеализации.СуммаНДС * &Курс / &Кратность,
	|	КорректировкаРеализации.СуммаНДСДоИзменения * &Курс / &Кратность,
	|	0,
	|	Неопределено,
	|	0,
	|	0,
	|	КорректировкаРеализации.Количество,
	|	КорректировкаРеализации.КоличествоДоИзменения,
	|	0,
	|	КорректировкаРеализации.ЗаказПокупателя,
	|	"""",
	|	Ложь,
	|	"""",
	|	"""",
	|	"""",
	|	""""
	|	
	|ИЗ
	|	Документ.КорректировкаРеализации.Услуги КАК КорректировкаРеализации
	|	
	|ГДЕ
	|	КорректировкаРеализации.Ссылка = &Ссылка
	|	И (КорректировкаРеализации.Количество <> КорректировкаРеализации.КоличествоДоИзменения
	|			ИЛИ КорректировкаРеализации.Сумма <> КорректировкаРеализации.СуммаДоИзменения
	|			ИЛИ КорректировкаРеализации.СуммаНДС <> КорректировкаРеализации.СуммаНДСДоИзменения
	|			ИЛИ КорректировкаРеализации.Цена <> КорректировкаРеализации.ЦенаДоИзменения)";
	
	Запрос.УстановитьПараметр("Курс",       ЗаполнениеДокументов.КурсДокумента(СсылкаНаОбъект, ВалютаРегламентированногоУчета));
	Запрос.УстановитьПараметр("Кратность",  ЗаполнениеДокументов.КратностьДокумента(СсылкаНаОбъект, ВалютаРегламентированногоУчета));
	Запрос.УстановитьПараметр("Ссылка",     СсылкаНаОбъект);
	
	ТаблицаТоваров = Запрос.Выполнить().Выгрузить();
	
	ТаблицаТоваров.Колонки.Добавить("СуммаБезНДС", Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(18, 2)));
	ТаблицаТоваров.Колонки.Добавить("СуммаБезНДСДоКорректировки", Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(18, 2)));
	ТаблицаТоваров.Колонки.Добавить("СуммаСНДС",   Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(18, 2)));
	ТаблицаТоваров.Колонки.Добавить("СуммаСНДСДоКорректировки",   Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(18, 2)));
	
	Для каждого СтрокаТаблицы из ТаблицаТоваров Цикл
		
		СуммаСНДС   = Окр((СтрокаТаблицы.Сумма + ?(Шапка.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС)), 2);
		СуммаНДС    = Окр(СтрокаТаблицы.СуммаНДС, 2);
		СуммаБезНДС = СуммаСНДС - СуммаНДС;
		
		СуммаСНДСДоКорректировки   = Окр((СтрокаТаблицы.СуммаДоКорректировки + ?(Шапка.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДСДоКорректировки)), 2);
		СуммаНДСДоКорректировки    = Окр(СтрокаТаблицы.СуммаНДСДоКорректировки, 2);
		СуммаБезНДСДоКорректировки = СуммаСНДСДоКорректировки - СуммаНДСДоКорректировки;
		
		СтрокаТаблицы.СуммаНДС    = СуммаНДС;
		СтрокаТаблицы.СуммаБезНДС = СуммаБезНДС;
		СтрокаТаблицы.СуммаСНДС   = СуммаСНДС;
		
		СтрокаТаблицы.СуммаНДСДоКорректировки    = СуммаНДСДоКорректировки;
		СтрокаТаблицы.СуммаБезНДСДоКорректировки = СуммаБезНДСДоКорректировки;
		СтрокаТаблицы.СуммаСНДСДоКорректировки   = СуммаСНДСДоКорректировки;
		
		Если Шапка.СуммаВключаетНДС Тогда
			СтрокаТаблицы.Цена = Окр(СтрокаТаблицы.Цена, 2);
			СтрокаТаблицы.ЦенаДоКорректировки = Окр(СтрокаТаблицы.ЦенаДоКорректировки, 2);
		Иначе
			СтрокаТаблицы.Цена = ?(СтрокаТаблицы.МассаНетто = 0, 
				0, Окр(СуммаБезНДС / СтрокаТаблицы.МассаНетто, 2));
			СтрокаТаблицы.ЦенаДоКорректировки = ?(СтрокаТаблицы.МассаНеттоДоКорректировки = 0, 
				0, Окр(СуммаБезНДСДоКорректировки / СтрокаТаблицы.МассаНеттоДоКорректировки, 2));
		КонецЕсли;
		
	КонецЦикла;
	
	ДанныеДокумента.Вставить("ТаблицаТоваров", ТаблицаТоваров);
	
	Возврат ДанныеДокумента;

КонецФункции

// Процедура добавляет новую строку в таблицы данных структуры параметров
//
Процедура ДобавитьСтрокуТаблицуДанных(ТаблицаДанных, ДанныеСтроки, СтруктураПараметров, ИмяЭлементаВладельцаДопДанных, НомерСтроки)
	
	НоваяСтрока = ТаблицаДанных.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеСтроки);
	
	НоваяСтрока.НомерСтроки = НомерСтроки;
	
	// Сформируем доп. параметры
	СтруктураДопДанных = Новый Структура;
	
	ИДТовара = ДанныеСтроки.Номенклатура.УникальныйИдентификатор();
	ИДХарактеристики = ?(ЗначениеЗаполнено(ДанныеСтроки.Характеристика), ДанныеСтроки.Характеристика.УникальныйИдентификатор(), "");
	ИДУпаковки = "";
	СтруктураДопДанных.Вставить("ИД", Строка(ИДТовара) + "#" + Строка(ИДХарактеристики) + "#" + Строка(ИДУпаковки));
	Если ЗначениеЗаполнено(ДанныеСтроки.Характеристика) Тогда
		Наименование = ДанныеСтроки.НаименованиеНоменклатуры + ?(ЗначениеЗаполнено(ДанныеСтроки.Характеристика), " ("+ДанныеСтроки.Характеристика+")", "");
		СтруктураДопДанных.Вставить("Наименование", Наименование);
		СтруктураДопДанных.Вставить("Характеристика", ДанныеСтроки.Характеристика);
	КонецЕсли;
	
	// Из-за особенностей в схеме ФНС некоторые ставки НДС необходимо передавать в доп. параметрах.
	Если ТаблицаДанных.Колонки.Найти("СтавкаНДС") <> Неопределено Тогда
		// В ТОРГ12 не предусмотрена передача 
		// - "дробных" ставок НДС
		// - ставки "Без НДС"
		
		СоответствиеСтавокНДС = Новый Соответствие;
		СоответствиеСтавокНДС.Вставить(Перечисления.СтавкиНДС.НДС18_118, Перечисления.СтавкиНДС.НДС18);
		СоответствиеСтавокНДС.Вставить(Перечисления.СтавкиНДС.НДС10_110, Перечисления.СтавкиНДС.НДС10);
		СоответствиеСтавокНДС.Вставить(Перечисления.СтавкиНДС.НДС20_120, Перечисления.СтавкиНДС.НДС20);
		//ТЕАЛ НДС ++
		СоответствиеСтавокНДС.Вставить(Перечисления.СтавкиНДС.НДС5_105, Перечисления.СтавкиНДС.НДС5);
		СоответствиеСтавокНДС.Вставить(Перечисления.СтавкиНДС.НДС7_107, Перечисления.СтавкиНДС.НДС7);
		СоответствиеСтавокНДС.Вставить(Перечисления.СтавкиНДС.НДС22_122, Перечисления.СтавкиНДС.НДС22);
		//ТЕАЛ НДС --
		СоответствиеСтавокНДС.Вставить(Перечисления.СтавкиНДС.БезНДС,    Перечисления.СтавкиНДС.ПустаяСсылка());
		
		Если СоответствиеСтавокНДС[НоваяСтрока.СтавкаНДС] <> Неопределено Тогда
			// Ставку НДС передадим в структуре доп. параметров
			СтруктураДопДанных.Вставить("СтавкаНДС", НоваяСтрока.СтавкаНДС);
			// В схему передадим соответствующую ставку НДС "числом"
			НоваяСтрока.СтавкаНДС = СоответствиеСтавокНДС[НоваяСтрока.СтавкаНДС];
			
		КонецЕсли;
		
	Иначе
		// В Акте не предусмотрена передача ставки НДС
		СтруктураДопДанных.Вставить("СтавкаНДС", ДанныеСтроки.СтавкаНДС);
	КонецЕсли;
	
	Если ИмяЭлементаВладельцаДопДанных = "Услуги" Тогда
		// Для Акта таблица услуг "вкладывается" в строку таблицы описания услуг, 
		// поэтому номер строки для доп. данных определим как составной
		НомерСтрокиДопДанных = "1." + НомерСтроки;
	Иначе
		НомерСтрокиДопДанных = НомерСтроки;
	КонецЕсли;
	
	ЭлектронныеДокументы.ДобавитьДанныеВДеревоДопДанных(СтруктураПараметров, СтруктураДопДанных, ИмяЭлементаВладельцаДопДанных, Истина, НомерСтрокиДопДанных);
	
КонецПроцедуры

Функция СтруктураИтоговыеСуммы(ТаблицаТоваров, ВидЭД)
	
	Структура = Новый Структура;
	
	Структура.Вставить("КоличествоМест", ТаблицаТоваров.Итог("КоличествоМест"));
	Структура.Вставить("МассаБрутто",    ТаблицаТоваров.Итог("МассаБрутто"));
	Структура.Вставить("МассаНетто",     ТаблицаТоваров.Итог("МассаНетто"));
	Структура.Вставить("СуммаБезНДС",    ТаблицаТоваров.Итог("СуммаБезНДС"));
	Структура.Вставить("СуммаНДС",       ТаблицаТоваров.Итог("СуммаНДС"));
	Структура.Вставить("СуммаСНДС",      ТаблицаТоваров.Итог("СуммаБезНДС") + ТаблицаТоваров.Итог("СуммаНДС"));
	Структура.Вставить("КоличествоПорядковыхНомеровЗаписей", ТаблицаТоваров.Количество());
	
	Если ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель Тогда
		Структура.Вставить("МассаНеттоДоКорректировки",  ТаблицаТоваров.Итог("МассаНеттоДоКорректировки"));
		Структура.Вставить("СуммаБезНДСДоКорректировки", ТаблицаТоваров.Итог("СуммаБезНДСДоКорректировки"));
		Структура.Вставить("СуммаНДСДоКорректировки",    ТаблицаТоваров.Итог("СуммаНДСДоКорректировки"));
		Структура.Вставить("СуммаСНДСДоКорректировки",   ТаблицаТоваров.Итог("СуммаБезНДСДоКорректировки") + ТаблицаТоваров.Итог("СуммаНДСДоКорректировки"));
	КонецЕсли;
	
	Возврат Структура;
	
КонецФункции

Процедура ЗаполнитьРеквизитыУчастниковТОРГ12(ДанныеПечати, СтруктураПараметров)
	
	СведенияОПоставщике       = ПолучитьДанныеЮрФизЛица(ДанныеПечати.Организация, ДанныеПечати.БанковскийСчет);
	СведенияОПокупателе       = ПолучитьДанныеЮрФизЛица(ДанныеПечати.Покупатель);
	СведенияОГрузополучателе  = ПолучитьДанныеЮрФизЛица(ДанныеПечати.Грузополучатель);
	СведенияОГрузоотправителе = ПолучитьДанныеЮрФизЛица(ДанныеПечати.Грузоотправитель);
	
	ЗаполнитьСведенияУчастникаОбмена(СтруктураПараметров.Поставщик, СведенияОПоставщике);
	Если ДанныеПечати.Организация <> ДанныеПечати.Грузоотправитель Тогда
		ЗаполнитьСведенияУчастникаОбмена(СтруктураПараметров.СведенияОГрузоотправителе.Грузоотправитель, СведенияОГрузоотправителе, "Факт");
		СтруктураПараметров.СведенияОГрузоотправителе.СтруктурноеПодразделение = ДанныеПечати.Подразделение;
	КонецЕсли;
	
	ЗаполнитьСведенияУчастникаОбмена(СтруктураПараметров.Плательщик,      СведенияОПокупателе);
	ЗаполнитьСведенияУчастникаОбмена(СтруктураПараметров.Грузополучатель, СведенияОГрузополучателе, "Факт");
	
КонецПроцедуры

Процедура ЗаполнитьРеквизитыУчастниковАкт501(ДанныеПечати, СтруктураПараметров)
	
	СведенияОИсполнителе = ПолучитьДанныеЮрФизЛица(ДанныеПечати.Исполнитель);
	СведенияОЗаказчике   = ПолучитьДанныеЮрФизЛица(ДанныеПечати.Заказчик);
	ЗаполнитьСведенияУчастникаОбмена(СтруктураПараметров.Исполнитель, СведенияОИсполнителе);
	ЗаполнитьСведенияУчастникаОбмена(СтруктураПараметров.Заказчик, СведенияОЗаказчике);
	
КонецПроцедуры

// Заполняет ФИО и должность в структуре или в дереве
// Для дерева параметр "ВидЛица" - обязательный
Процедура ЗаполнитьФИОиДолжность(ГдеЗаполнять, ИсточникДанных, Должность = Неопределено, ВидЛица = Неопределено)
	Если ТипЗнч(ГдеЗаполнять) = Тип("Структура") Тогда
		СтруктураПриемник = ГдеЗаполнять;
		Если ТипЗнч(ИсточникДанных) = Тип("Структура") Тогда
			ЗаполнитьЗначенияСвойств(СтруктураПриемник, ИсточникДанных);
		ИначеЕсли ТипЗнч(ИсточникДанных) = Тип("Строка") Тогда
			Фамилия = ""; Имя = ""; Отчество = "";
			ЭлектронныеДокументы.ФамилияИнициалыФизЛица(ИсточникДанных, Фамилия, Имя, Отчество);
			СтруктураПриемник.Вставить("Фамилия", Фамилия);
			СтруктураПриемник.Вставить("Имя", Имя);
			СтруктураПриемник.Вставить("Отчество", Отчество);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Должность) Тогда
			СтруктураПриемник.Вставить("Должность", Должность);
		КонецЕсли;
		ГдеЗаполнять = СтруктураПриемник;
	Иначе
		ДеревоДанных = ГдеЗаполнять;
		Если ТипЗнч(ИсточникДанных) = Тип("Структура") Тогда
			Фамилия = ИсточникДанных.Фамилия; Имя = ИсточникДанных.Имя; Отчество = ИсточникДанных.Отчество;
		ИначеЕсли ТипЗнч(ИсточникДанных) = Тип("Строка") Тогда
			Фамилия = ""; Имя = ""; Отчество = "";
			ЭлектронныеДокументы.ФамилияИнициалыФизЛица(ИсточникДанных, Фамилия, Имя, Отчество);
		КонецЕсли;
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ВидЛица + ".Фамилия", Фамилия);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ВидЛица + ".Имя", Имя);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ВидЛица + ".Отчество", Отчество);
		Если Должность <> Неопределено Тогда
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ВидЛица + ".Должность", Должность);
		КонецЕсли;
		ГдеЗаполнять = ДеревоДанных;
	КонецЕсли;
КонецПроцедуры

Функция ОтветственноеЛицоОрганизации(Период, Организация, ОтветственноеЛицо)
	
	СтруктураФИО = Новый Структура("Фамилия,Имя,Отчество", "", "", "");
	Результат = Новый Структура("ФИО, Должность, СтруктураФИО, ФизическоеЛицо", "", "", СтруктураФИО);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ОтветственныеЛицаОрганизации.ФизическоеЛицо) КАК ФИО,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ОтветственныеЛицаОрганизации.Должность) КАК Должность,
	|	ЕСТЬNULL(ФИОФизЛицСрезПоследних.Фамилия, """") КАК Фамилия,
	|	ЕСТЬNULL(ФИОФизЛицСрезПоследних.Имя, """") КАК Имя,
	|	ЕСТЬNULL(ФИОФизЛицСрезПоследних.Отчество, """") КАК Отчество,
	|	ОтветственныеЛицаОрганизации.ФизическоеЛицо
	|ИЗ
	|	РегистрСведений.ОтветственныеЛицаОрганизации.СрезПоследних(
	|			&Период,
	|			СтруктурнаяЕдиница = &Организация
	|				И ОтветственноеЛицо = &ОтветственноеЛицо) КАК ОтветственныеЛицаОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&Период, ) КАК ФИОФизЛицСрезПоследних
	|		ПО ОтветственныеЛицаОрганизации.ФизическоеЛицо = ФИОФизЛицСрезПоследних.ФизЛицо";
	Запрос.УстановитьПараметр("Период",            Период);
	Запрос.УстановитьПараметр("Организация",       Организация);
	Запрос.УстановитьПараметр("ОтветственноеЛицо", ОтветственноеЛицо);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
		ЗаполнитьЗначенияСвойств(Результат.СтруктураФИО, Выборка);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Заполняет сведения об участнике обмена в структуре или в дереве
// Для дерева параметр "ВидУчастника" - обязательный
Процедура ЗаполнитьСведенияУчастникаОбмена(ГдеЗаполнять, СведенияОбУчастнике, ВидАдреса = "Юр", ВидУчастника = Неопределено, ТипАдреса = "", УД = Ложь)
	
	Если ТипЗнч(ГдеЗаполнять) = Тип("Структура") Тогда
		СтруктураУчастника = ГдеЗаполнять;
		СтруктураУчастника.Вставить("КодОКПО",                 СведенияОбУчастнике.КодПоОКПО);
		СтруктураУчастника.Вставить("НаименованиеОрганизации", СведенияОбУчастнике.ПолноеНаименование);
		СтруктураУчастника.Вставить("ИНН",                     СведенияОбУчастнике.ИНН);
		СтруктураУчастника.Вставить("КПП",                     СведенияОбУчастнике.КПП);
		СтруктураУчастника.Вставить("ЭтоФизЛицо", СведенияОбУчастнике.ЮрФизЛицо <> Перечисления.ЮрФизЛицо.ЮрЛицо);
		Если СведенияОбУчастнике.ЮрФизЛицо <> Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
			ЗаполнитьФИОиДолжность(СтруктураУчастника, СведенияОбУчастнике.ПолноеНаименование);
		КонецЕсли;
		
		// Типы адресов представлены списком значений, в котором Представление элемента - описание типа (Структурированный,
		// Произвольный, Иностранный), значение элемента - структура описывающая поля адреса, пометка - указывает, из какого
		// элемента списка брать данные при заполнении ЭД.
		АдресУчастника = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьАдресСтруктурой(СведенияОбУчастнике, "Ссылка", ВидАдреса);
		ТипАдреса = ?(АдресУчастника.АдресРФ, "Структурированный", "Иностранный");
		ЗаполнитьАдресВСпискеТиповАдресов(СтруктураУчастника.Адрес, АдресУчастника, ТипАдреса);
		
		СтруктураУчастника.Вставить("Телефон",                 СведенияОбУчастнике.Телефоны);
		СтруктураУчастника.Вставить("Факс");
		
		СтруктураБанковскийСчет = СтруктураУчастника.БанковскийСчет;
		СтруктураБанковскийСчет.Вставить("БИК",         СведенияОбУчастнике.БИК);
		СтруктураБанковскийСчет.Вставить("НаимБанк",    СведенияОбУчастнике.Банк.Наименование);
		СтруктураБанковскийСчет.Вставить("НомерСчета ", СведенияОбУчастнике.НомерСчета);
		ГдеЗаполнять = СтруктураУчастника;
	Иначе
		ДеревоДанных = ГдеЗаполнять;
		Если СведенияОбУчастнике.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
										ДеревоДанных,
										ВидУчастника + ".ТипУчастника.ЮЛ.ИНН",
										СведенияОбУчастнике.ИНН);
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
										ДеревоДанных,
										ВидУчастника + ".ТипУчастника.ЮЛ.КПП",
										СведенияОбУчастнике.КПП);
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
										ДеревоДанных,
										ВидУчастника + ".ТипУчастника.ЮЛ.НаименованиеОрганизации",
										СведенияОбУчастнике.ПолноеНаименование);
		Иначе
			Если УД Тогда
				ПрефиксРеквизитов = ".ТипУчастника.ИП."
			Иначе
				ПрефиксРеквизитов = ".ТипУчастника.ФЛ.";
			КонецЕсли;
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
										ДеревоДанных,
										ВидУчастника + ПрефиксРеквизитов + "ИНН",
										СведенияОбУчастнике.ИНН);
			Если УД Тогда
				ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
					ДеревоДанных, ВидУчастника + ".ТипУчастника.ИП.СвидетельствоОГосРегистрации", СведенияОбУчастнике.Свидетельство);
			КонецЕсли;
			Фамилия = ""; Имя = ""; Отчество = "";
			Если СведенияОбУчастнике.Свойство("ФИО") Тогда
				ЭлектронныеДокументы.ФамилияИнициалыФизЛица(СведенияОбУчастнике.ФИО, Фамилия, Имя, Отчество);
			Иначе
				ЭлектронныеДокументы.ФамилияИнициалыФизЛица(СведенияОбУчастнике.ПолноеНаименование, Фамилия, Имя, Отчество);
			КонецЕсли;
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
										ДеревоДанных,
										ВидУчастника + ПрефиксРеквизитов + "Фамилия",
										Фамилия);
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
										ДеревоДанных,
										ВидУчастника + ПрефиксРеквизитов + "Имя",
										Имя);
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
										ДеревоДанных,
										ВидУчастника + ПрефиксРеквизитов + "Отчество",
										Отчество);
			Если Не УД Тогда
				СтрокаРеквизита = ДеревоДанных.Строки.Найти(ВидУчастника + ПрефиксРеквизитов + "ПолноеНаименование", "ПолныйПуть", Истина);
				Если СтрокаРеквизита <> Неопределено Тогда
					ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
											ДеревоДанных,
											ВидУчастника + ПрефиксРеквизитов + "ПолноеНаименование",
											СведенияОбУчастнике.ПолноеНаименование);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	
		Если СведенияОбУчастнике.Свойство("Ссылка") Тогда
			АдресУчастника = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьАдресСтруктурой(СведенияОбУчастнике, "Ссылка", ВидАдреса);
			Если ТипАдреса = "" Тогда
				ТипАдреса = ?(АдресУчастника.АдресРФ, "Структурированный", "Иностранный");
			КонецЕсли;
		Иначе
			Если ТипАдреса = "Произвольный" Тогда
				Если ВидАдреса = "Юр" Тогда
					АдресУчастника = Новый Структура("АдресСтрокой", СведенияОбУчастнике.ЮридическийАдрес);
				ИначеЕсли ВидАдреса = "Факт" Тогда
					АдресУчастника = Новый Структура("АдресСтрокой", СведенияОбУчастнике.ФактическийАдрес);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		ЗаполнитьАдресВДереве(ДеревоДанных, АдресУчастника, ТипАдреса, ВидУчастника, УД);
		
		Если ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, ВидУчастника+".Контакт") Тогда
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
				ВидУчастника + ".Контакт.Телефон", СведенияОбУчастнике.Телефоны);
		ИначеЕсли ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, ВидУчастника+".КонтактныеСведения") Тогда
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
				ВидУчастника + ".КонтактныеСведения.Телефон", СведенияОбУчастнике.Телефоны);
		КонецЕсли;
		
		Если ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, ВидУчастника+".БанковскийСчет") Тогда
			НомерСчета = "";
			Если СведенияОбУчастнике.Свойство("НомерСчета", НомерСчета) И ЗначениеЗаполнено(НомерСчета) Тогда
				Банк = "";
				БИК = "";
				ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
					ВидУчастника + ".БанковскийСчет.НомерСчета", НомерСчета);
				Если СведенияОбУчастнике.Свойство("Банк", Банк) И ЗначениеЗаполнено(Банк) Тогда
					ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
						ВидУчастника + ".БанковскийСчет.НаимБанк", Банк.Наименование);
				КонецЕсли;
				Если СведенияОбУчастнике.Свойство("БИК", БИК) И ЗначениеЗаполнено(БИК) Тогда
					ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
						ВидУчастника + ".БанковскийСчет.БИК", БИК);
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, ВидУчастника+".БанковскиеРеквизиты") Тогда
			НомерСчета = "";
			Если СведенияОбУчастнике.Свойство("НомерСчета", НомерСчета) И ЗначениеЗаполнено(НомерСчета) Тогда
				Банк = "";
				БИК = "";
				КоррСчет = "";
				ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
					ВидУчастника + ".БанковскиеРеквизиты.НомерСчета", НомерСчета);
				Если СведенияОбУчастнике.Свойство("Банк", Банк) И ЗначениеЗаполнено(Банк) Тогда
					ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
						ВидУчастника + ".БанковскиеРеквизиты.НаименованиеБанка", Банк.Наименование);
				КонецЕсли;
				Если СведенияОбУчастнике.Свойство("БИК", БИК) И ЗначениеЗаполнено(БИК) Тогда
					ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
						ВидУчастника + ".БанковскиеРеквизиты.БИКБанка", БИК);
				КонецЕсли;
				Если СведенияОбУчастнике.Свойство("КоррСчет", КоррСчет) И ЗначениеЗаполнено(КоррСчет) Тогда
					ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
						ВидУчастника + ".БанковскиеРеквизиты.КорреспондентскийСчетБанка", КоррСчет);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, ВидУчастника+".КодОКПО") Тогда
			КодПоОКПО = "";
			Если СведенияОбУчастнике.Свойство("КодПоОКПО", КодПоОКПО) Тогда
				ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ВидУчастника+".КодОКПО", КодПоОКПО);
			КонецЕсли;
		КонецЕсли;

		ГдеЗаполнять = ДеревоДанных;
	КонецЕсли;
КонецПроцедуры

Процедура ЗаполнитьРеквизитыФизЛица(ДеревоДанных, ИсточникДанных, ТипЛица, Должность = Неопределено)
	
	Фамилия = ""; Имя = ""; Отчество = "";
	ЭлектронныеДокументы.ФамилияИнициалыФизЛица(ИсточникДанных, Фамилия, Имя, Отчество);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ТипЛица + ".Фамилия", Фамилия);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ТипЛица + ".Имя", Имя);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ТипЛица + ".Отчество", Отчество);
	Если Должность <> Неопределено И ЗначениеЗаполнено(Должность) Тогда
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ТипЛица + ".Должность", Должность);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДанныеУчастника(ДеревоДанных, СведенияОбУчастнике, ВидУчастника, ВидАдреса = "Юр", ТипАдреса = "", ЕстьИП = Ложь)
	
	Нерезидент = (ТипЗнч(СведенияОбУчастнике.Ссылка) = Тип("СправочникСсылка.Контрагенты") И СведенияОбУчастнике.Ссылка.НеЯвляетсяРезидентом);
	
	Если Нерезидент Тогда
		
		АдресНерезидента = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьАдресСтруктурой(СведенияОбУчастнике, "Ссылка", ВидАдреса);
		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных, 
									ВидУчастника + ".ТипУчастника.ИностраннаяОрганизация.НаименованиеОрганизации", 
									СведенияОбУчастнике.ПолноеНаименование);
									
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных, 
									ВидУчастника + ".ТипУчастника.ИностраннаяОрганизация.Страна", АдресНерезидента.НаименованиеСтраны);
	
	ИначеЕсли СведенияОбУчастнике.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника.ЮЛ.ИНН",
									СведенияОбУчастнике.ИНН);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника.ЮЛ.КПП",
									СведенияОбУчастнике.КПП);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника.ЮЛ.НаименованиеОрганизации",
									СведенияОбУчастнике.ПолноеНаименование);

		Если ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, ВидУчастника + ".ТипУчастника.ЮЛ.ОГРН") Тогда
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
										ДеревоДанных,
										ВидУчастника + ".ТипУчастника.ЮЛ.ОГРН",
										СведенияОбУчастнике.ОГРН);
		КонецЕсли;
							
	Иначе
		ФЛ = Истина;
		
		Если ЕстьИП Тогда
			
			Если ТипЗнч(СведенияОбУчастнике.Ссылка) = Тип("СправочникСсылка.Организации") Тогда
				Если СведенияОбУчастнике.Свойство("Свидетельство") Тогда
				
					Если Не ПустаяСтрока(СведенияОбУчастнике.Свидетельство) Тогда
						ФЛ = Ложь;
					КонецЕсли;
				
				КонецЕсли;
			Иначе
				// в справочнике нет реквизита Свидетельство
				ФЛ = Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника."+?(ФЛ, "ФЛ", "ИП")+".ИНН",
									СведенияОбУчастнике.ИНН);
									
		Если ТипЗнч(СведенияОбУчастнике.Ссылка) = Тип("СправочникСсылка.Организации") И Не ФЛ Тогда
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
				ДеревоДанных, ВидУчастника + ".ТипУчастника.ИП.СвидетельствоОГосРегистрации", СведенияОбУчастнике.Свидетельство);
				
			Если ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, ВидУчастника + ".ТипУчастника.ИП.ОГРН") Тогда
				ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
					ДеревоДанных, ВидУчастника + ".ТипУчастника.ИП.ОГРН", СведенияОбУчастнике.ОГРН);
			КонецЕсли;
		КонецЕсли;
			
		Фамилия = ""; Имя = ""; Отчество = "";
		Если СведенияОбУчастнике.Свойство("ФИО") Тогда
			ЭлектронныеДокументы.ФамилияИнициалыФизЛица(СведенияОбУчастнике.ФИО, Фамилия, Имя, Отчество);
		Иначе
			ЭлектронныеДокументы.ФамилияИнициалыФизЛица(СведенияОбУчастнике.ПолноеНаименование, Фамилия, Имя, Отчество);
		КонецЕсли;
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника."+?(ФЛ, "ФЛ", "ИП")+".Фамилия",
									Фамилия);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника."+?(ФЛ, "ФЛ", "ИП")+".Имя",
									Имя);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника."+?(ФЛ, "ФЛ", "ИП")+".Отчество",
									Отчество);
									
		Если ФЛ Тогда
			СтрокаРеквизита = ДеревоДанных.Строки.Найти(ВидУчастника + ".ТипУчастника.ФЛ.ПолноеНаименование", "ПолныйПуть", Истина);
			
			Если СтрокаРеквизита <> Неопределено Тогда
				ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
										ДеревоДанных,
										ВидУчастника + ".ТипУчастника.ФЛ.ПолноеНаименование",
										СведенияОбУчастнике.ПолноеНаименование);
			КонецЕсли;
									
		КонецЕсли;
	КонецЕсли;
	
	Если СведенияОбУчастнике.Свойство("Ссылка") Тогда
		АдресУчастника = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьАдресСтруктурой(СведенияОбУчастнике, "Ссылка", ВидАдреса);
		Если ТипАдреса = "" Тогда
			Если ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, ВидУчастника+".Адрес.Структурированный") Тогда
				ТипАдреса = ?(АдресУчастника.АдресРФ, "Структурированный", "Иностранный");
			Иначе
				ТипАдреса = ?(АдресУчастника.АдресРФ, "АдресРФ", "АдресИнформация");
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если ТипАдреса = "Произвольный" Тогда
			Если ВидАдреса = "Юр" Тогда
				АдресУчастника = Новый Структура("АдрТекст", СведенияОбУчастнике.ЮридическийАдрес);
			ИначеЕсли ВидАдреса = "Факт" Тогда
				АдресУчастника = Новый Структура("АдрТекст", СведенияОбУчастнике.ФактическийАдрес);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьАдресВДереве(ДеревоДанных, АдресУчастника, ТипАдреса, ВидУчастника);
	
	Если ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, ВидУчастника+".Контакт") Тогда
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".Контакт.Телефон",
									СведенияОбУчастнике.Телефоны);
	КонецЕсли;
	
	Если ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, ВидУчастника+".БанковскийСчет") Тогда
		НомерСчета = "";
		Если СведенияОбУчастнике.Свойство("НомерСчета", НомерСчета) И ЗначениеЗаполнено(НомерСчета) Тогда
			Банк = "";
			БИК = "";
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
					ДеревоДанных,
					ВидУчастника + ".БанковскийСчет.НомерСчета",
					НомерСчета);
			Если СведенияОбУчастнике.Свойство("Банк", Банк) И ЗначениеЗаполнено(Банк) Тогда
				ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
											ДеревоДанных,
											ВидУчастника + ".БанковскийСчет.НаимБанк",
											Банк.Наименование);
			КонецЕсли;
			Если СведенияОбУчастнике.Свойство("БИК", БИК) И ЗначениеЗаполнено(БИК) Тогда
				ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
											ДеревоДанных,
											ВидУчастника + ".БанковскийСчет.БИК",
											БИК);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, ВидУчастника+".КодОКПО") Тогда
		КодПоОКПО = "";
		Если СведенияОбУчастнике.Свойство("КодПоОКПО", КодПоОКПО) Тогда
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ВидУчастника+".КодОКПО", КодПоОКПО);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Заполняет сведения о грузоотправителе или грузополучателе в дереве ЭД "Счет-фактура"
// ВидУчастника принимает одно из двух значений: "Грузоотправитель" или "Грузополучатель"
// 
Процедура ЗаполнитьСведенияУчастникаСФ(ДеревоДанных, СведенияОбУчастнике, ВидУчастника, ВидАдреса = "Юр", ТипАдреса = "")
	
	Если СведенияОбУчастнике.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ВидУчастника + ".Наименование.НаименованиеОрганизации",
			СведенияОбУчастнике.ПолноеНаименование);
	Иначе
		Фамилия = ""; Имя = ""; Отчество = "";
		Если СведенияОбУчастнике.Свойство("ФИО") Тогда
			ЭлектронныеДокументы.ФамилияИнициалыФизЛица(СведенияОбУчастнике.ФИО, Фамилия, Имя, Отчество);
		Иначе
			ЭлектронныеДокументы.ФамилияИнициалыФизЛица(СведенияОбУчастнике.ПолноеНаименование, Фамилия, Имя, Отчество);
		КонецЕсли;
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".Наименование.ФИОИП.Фамилия",
									Фамилия);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".Наименование.ФИОИП.Имя",
									Имя);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".Наименование.ФИОИП.Отчество",
									Отчество);
	КонецЕсли;
	
	Если СведенияОбУчастнике.Свойство("Ссылка") Тогда
		АдресУчастника = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьАдресСтруктурой(СведенияОбУчастнике, "Ссылка", ВидАдреса);
		Если ТипАдреса = "" Тогда
			ТипАдреса = ?(АдресУчастника.АдресРФ, "Структурированный", "Иностранный");
		КонецЕсли;
	Иначе
		Если ТипАдреса = "Произвольный" Тогда
			Если ВидАдреса = "Юр" Тогда
				АдресУчастника = Новый Структура("АдрТекст", СведенияОбУчастнике.ЮридическийАдрес);
			ИначеЕсли ВидАдреса = "Факт" Тогда
				АдресУчастника = Новый Структура("АдрТекст", СведенияОбУчастнике.ФактическийАдрес);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьАдресВДереве(ДеревоДанных, АдресУчастника, ТипАдреса, ВидУчастника);
	
КонецПроцедуры

// Заполняет соответствующий тип адреса переданными данными.
// Параметры:
//  СписокТиповАдресов - СписокЗначений - Представление элемента - описание типа (Структурированный,
//    Произвольный, Иностранный), значение элемента - структура описывающая поля адреса, пометка - указывает, из какого
//    элемента списка брать данные при заполнении ЭД.
//  АдресУчастника - Структура - содержит данные адреса участника обмена. Имена полей структуры должны совпадать с
//    именами полей структуры выбранного типа адреса:
//    Структурированный - "Индекс, КодРегион, Район, Город, НаселПункт, Улица, Дом, Корпус, Кварт";
//    Произвольный/Иностранный - "КодСтраны, АдресСтрокой" (вынесены в разные элементы списка для того,
//      чтобы правильно заполнить ЭД).
//  ТипАдреса - Строка - один из 3-х вариантов: Структурированный, Произвольный, Иностранный.
//
Процедура ЗаполнитьАдресВСпискеТиповАдресов(СписокТиповАдресов, АдресУчастника, ТипАдреса = "Структурированный")
	
	// Типы адресов представлены списком значений, в котором Представление элемента - описание типа (Структурированный,
	// Произвольный, Иностранный), значение элемента - структура описывающая поля адреса, пометка - указывает, из какого
	// элемента списка брать данные при заполнении ЭД.
	СписокТиповАдресов.ЗаполнитьПометки(Ложь);
	ВыбранныйТипАдреса = Неопределено;
	Для Каждого Элемент Из СписокТиповАдресов Цикл
		Если Элемент.Представление = ТипАдреса Тогда
			ВыбранныйТипАдреса = Элемент;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если ВыбранныйТипАдреса <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ВыбранныйТипАдреса.Значение, АдресУчастника);
		ВыбранныйТипАдреса.Пометка = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Заполняет соответствующий тип адреса переданными данными.
// Параметры:
//  СтрокаДерева - СтрокаДереваЗначений - Строка дерева, содержащая данные участника
//  АдресУчастника - Структура - содержит данные адреса участника обмена. Имена полей структуры должны совпадать с
//    именами полей структуры выбранного типа адреса:
//    Структурированный - "Индекс, КодРегион, Район, Город, НаселПункт, Улица, Дом, Корпус, Кварт";
//    Произвольный/Иностранный - "КодСтраны, АдресСтрокой" (вынесены в разные элементы списка для того,
//      чтобы правильно заполнить ЭД).
//  ТипАдреса - Строка - один из 3-х вариантов: Структурированный, Произвольный, Иностранный.
//
Процедура ЗаполнитьАдресВДереве(ДеревоДанных, АдресУчастника, ТипАдреса, ВидУчастника, УД = Ложь)
	
	Если УД = Истина Тогда
		Если ТипАдреса = "Структурированный" Тогда
			Если ВидУчастника = "Исполнитель" Или ВидУчастника = "Заказчик" Тогда
				ТипАдресаВДереве = "Структурированный";
			Иначе
				ТипАдресаВДереве = "АдресРФ";
			КонецЕсли;
		ИначеЕсли ТипАдреса = "Иностранный" Тогда
			Если ВидУчастника = "Исполнитель" Или ВидУчастника = "Заказчик" Тогда
				ТипАдресаВДереве = "Иностранный";
			Иначе
				ТипАдресаВДереве = "АдресИнформация";
			КонецЕсли;
		ИначеЕсли ТипАдреса = "Произвольный" Тогда
			Если ВидУчастника = "Исполнитель" Или ВидУчастника = "Заказчик" Тогда
				ТипАдресаВДереве = "Произвольный";
			Иначе
				ТипАдресаВДереве = "АдресИнформация";
			КонецЕсли;
		КонецЕсли;
	Иначе
		ТипАдресаВДереве = ТипАдреса;
	КонецЕсли;
	
	Если ТипАдреса = "Произвольный" Тогда
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".Адрес." + ТипАдресаВДереве,
									АдресУчастника.АдресСтрокой);
	Иначе
		Если АдресУчастника.АдресРФ Тогда
			АдресУчастника.Удалить("АдресРФ");
			АдресУчастника.Удалить("КодСтраны");
			АдресУчастника.Удалить("АдресСтрокой");
			АдресУчастника.Удалить("Регион");
			Если УД = Истина Тогда
				Значение = Неопределено;
				
				Если ВидУчастника = "Исполнитель" Или ВидУчастника = "Заказчик" Тогда
					Если АдресУчастника.Свойство("КодРегион", Значение) Тогда
						АдресУчастника.Удалить("КодРегиона");
					КонецЕсли;
					Если АдресУчастника.Свойство("НаселПункт", Значение) Тогда
						АдресУчастника.Удалить("НаселенныйПункт");
					КонецЕсли;
					Если АдресУчастника.Свойство("Кварт", Значение) Тогда
						АдресУчастника.Удалить("Квартира");
					КонецЕсли;
				Иначе
					Если АдресУчастника.Свойство("КодРегион", Значение) Тогда
						АдресУчастника.Удалить("КодРегион");
						АдресУчастника.Вставить("КодРегиона", Значение);
					КонецЕсли;
					Если АдресУчастника.Свойство("НаселПункт", Значение) Тогда
						АдресУчастника.Удалить("НаселПункт");
						АдресУчастника.Вставить("НаселенныйПункт", Значение);
					КонецЕсли;
					Если АдресУчастника.Свойство("Кварт", Значение) Тогда
						АдресУчастника.Удалить("Кварт");
						АдресУчастника.Вставить("Квартира", Значение);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		Иначе
			АдресУчастника.Удалить("АдресРФ");
			АдресУчастника.Удалить("Индекс");
			АдресУчастника.Удалить("КодРегион");
			АдресУчастника.Удалить("Регион");
			АдресУчастника.Удалить("Район");
			АдресУчастника.Удалить("Город");
			АдресУчастника.Удалить("НаселПункт");
			АдресУчастника.Удалить("Улица");
			АдресУчастника.Удалить("Дом");
			АдресУчастника.Удалить("Корпус");
			АдресУчастника.Удалить("Кварт");
		КонецЕсли;
		КодСтраныУжеЗаполнен = Ложь;
		КодСтрУжеЗаполнен = Ложь;
		АдресТекстУжеЗаполнен = Ложь;
		АдрТекстУжеЗаполнен = Ложь;
		Для Каждого Элемент Из АдресУчастника Цикл
			Если Элемент.Ключ = "КодСтраны" ИЛИ Элемент.Ключ = "КодСтр" Тогда
				
				Если ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, 
						ВидУчастника + ".Адрес." + ТипАдресаВДереве + "." + "КодСтраны") Тогда
					Если КодСтраныУжеЗаполнен Тогда
						Продолжить;
					КонецЕсли;
					ИмяРеквизита = "КодСтраны";
					Если Не ПустаяСтрока(Элемент.Значение) Тогда
						КодСтраныУжеЗаполнен = Истина;
					КонецЕсли;
				ИначеЕсли ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, 
						ВидУчастника + ".Адрес." + ТипАдресаВДереве + "." + "КодСтр") Тогда
					Если КодСтрУжеЗаполнен Тогда
						Продолжить;
					КонецЕсли;
					ИмяРеквизита = "КодСтр";
					Если Не ПустаяСтрока(Элемент.Значение) Тогда
						КодСтрУжеЗаполнен = Истина;
					КонецЕсли;
				КонецЕсли;
				
			ИначеЕсли Элемент.Ключ = "АдресСтрокой" ИЛИ Элемент.Ключ = "АдресТекст" ИЛИ Элемент.Ключ = "АдрТекст" Тогда
				
				Если ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, 
						ВидУчастника + ".Адрес." + ТипАдресаВДереве + "." + "АдресТекст") Тогда
					Если АдресТекстУжеЗаполнен Тогда
						Продолжить;
					КонецЕсли;
					ИмяРеквизита = "АдресТекст";
					Если Не ПустаяСтрока(Элемент.Значение) Тогда
						АдресТекстУжеЗаполнен = Истина;
					КонецЕсли;
				ИначеЕсли ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, 
						ВидУчастника + ".Адрес." + ТипАдресаВДереве + "." + "АдрТекст") Тогда
					Если АдрТекстУжеЗаполнен Тогда
						Продолжить;
					КонецЕсли;
					ИмяРеквизита = "АдрТекст";
					Если Не ПустаяСтрока(Элемент.Значение) Тогда
						АдрТекстУжеЗаполнен = Истина;
					КонецЕсли;
				КонецЕсли;
				
			ИначеЕсли Элемент.Ключ = "КодРегион" Тогда
				
				Если ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, 
						ВидУчастника + ".Адрес." + ТипАдресаВДереве + "." + "КодРегион") Тогда
					ИмяРеквизита = "КодРегион";
				ИначеЕсли ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, 
						ВидУчастника + ".Адрес." + ТипАдресаВДереве + "." + "КодРегиона") Тогда
					ИмяРеквизита = "КодРегиона";
				КонецЕсли;
				
			ИначеЕсли Элемент.Ключ = "НаселПункт" Тогда
				
				Если ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, 
						ВидУчастника + ".Адрес." + ТипАдресаВДереве + "." + "НаселПункт") Тогда
					ИмяРеквизита = "НаселПункт";
				ИначеЕсли ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, 
						ВидУчастника + ".Адрес." + ТипАдресаВДереве + "." + "НаселенныйПункт") Тогда
					ИмяРеквизита = "НаселенныйПункт";
				КонецЕсли;
				
			ИначеЕсли Элемент.Ключ = "Кварт" Тогда
				
				Если ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, 
						ВидУчастника + ".Адрес." + ТипАдресаВДереве + "." + "Кварт") Тогда
					ИмяРеквизита = "Кварт";
				ИначеЕсли ОбщегоНазначенияЭД.СуществуетРеквизитВДереве(ДеревоДанных, 
						ВидУчастника + ".Адрес." + ТипАдресаВДереве + "." + "Квартира") Тогда
					ИмяРеквизита = "Квартира";
				КонецЕсли;
				
			Иначе
				ИмяРеквизита = Элемент.Ключ;
			КонецЕсли;
			
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
				ВидУчастника + ".Адрес." + ТипАдресаВДереве + "." + ИмяРеквизита, Элемент.Значение);
				
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьБанковскийСчет(ДеревоДокумента, СсылкаНаБанковскийСчет)
	
	РеквизитыСчета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаБанковскийСчет, "НомерСчета,Банк,БанкДляРасчетов");
	РеквизитыБанка = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РеквизитыСчета.Банк, "Код,КоррСчет,Наименование");
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, 
		"РасчетныйСчет.НомерСчета", РеквизитыСчета.НомерСчета);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, 
		"РасчетныйСчет.Банк.БИК", РеквизитыБанка.Код);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, 
		"РасчетныйСчет.Банк.СчетКорреспондентский", РеквизитыБанка.КоррСчет);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, 
		"РасчетныйСчет.Банк.Наименование", РеквизитыБанка.Наименование);
	Если ЗначениеЗаполнено(РеквизитыСчета.БанкДляРасчетов) Тогда
		РеквизитыБанкаКорреспондента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РеквизитыСчета.БанкДляРасчетов, "Код,КоррСчет,Наименование");
		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РасчетныйСчет.БанкКорреспондент.БИК", 
			РеквизитыБанкаКорреспондента.Код);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РасчетныйСчет.БанкКорреспондент.СчетКорреспондентский", 
			РеквизитыБанкаКорреспондента.КоррСчет);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РасчетныйСчет.БанкКорреспондент.Наименование", 
			РеквизитыБанкаКорреспондента.Наименование);
	КонецЕсли;
	
КонецПроцедуры

// Заполняет в дереве документа реквизиты:
// "НомерТоварнойНакладной", "ДатаТоварнойНакладной" - ТОРГ-12 (Титул продавца)
// "НомерАкта", "ДатаАкта" - Акт (Титул исполнителя)
// "Номер", "Дата" - Акт на передачу прав
// и "НомерИсправления", "ДатаИсправления" при необходимости
//
Процедура ЗаполнитьДатуИНомер(ДеревоДанных, РеквизитыШапки, СсылкаНаОбъект, ИмяРеквизитаНомер, ИмяРеквизитаДата)
	
	Если РеквизитыШапки.ВидОперации = Перечисления.ВидыОперацийЭД.Исправление Тогда
		Если Не ЗначениеЗаполнено(РеквизитыШапки.ИсправляемыйДокументРеализации) Тогда
			РеквизитыШапки.ИсправляемыйДокументРеализации = УчетНДС.ПолучитьИсправляемыйДокументРеализации(СсылкаНаОбъект);
		КонецЕсли;
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ИмяРеквизитаНомер, 
			ПолучитьПечатныйНомерДокумента(РеквизитыШапки.ИсправляемыйДокументРеализации));
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ИмяРеквизитаДата,  
			РеквизитыШапки.ИсправляемыйДокументРеализации.Дата);
		// "НомерИсправления" и "ДатаИсправления"
		СтруктураОтбораСчетаФактуры = Новый Структура;
		СписокВидовСчетовФактур = Новый СписокЗначений;
		СписокВидовСчетовФактур.Добавить(Перечисления.ВидСчетаФактурыВыставленного.НаРеализацию);
		СтруктураОтбораСчетаФактуры.Вставить("ВидСчетаФактуры", СписокВидовСчетовФактур);
		СтруктураОтбораСчетаФактуры.Вставить("ПометкаУдаления", Ложь);
		
		СФ = УчетНДС.НайтиПодчиненныйСчетФактуру(СсылкаНаОбъект, "СчетФактураВыданный");
		Если ЗначениеЗаполнено(СФ) Тогда
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправления",
				Строка(СФ.НомерИсправления));
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсправления",
				РеквизитыШапки.ДатаДокумента);
		КонецЕсли;
	Иначе
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ИмяРеквизитаНомер,
			ПолучитьПечатныйНомерДокумента(СсылкаНаОбъект));
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ИмяРеквизитаДата,
			РеквизитыШапки.ДатаДокумента);
	КонецЕсли;

КонецПроцедуры

Функция ТипСтавкиНДС(Ставка)
	Возврат "процент";
КонецФункции

Функция ПредставлениеСтавкиНДС(Ставка)
	
	// Подготовим представление ставки в соответсвии с форматом ФНС
	ПредставлениеСтавки = Строка(Ставка);
	
	Если Ставка = Перечисления.СтавкиНДС.БезНДС Тогда
		ПредставлениеСтавки = СтрЗаменить(ПредставлениеСтавки, "Б", "б");
	Иначе
		ПредставлениеСтавки = СтрЗаменить(ПредставлениеСтавки, "%", "");
		ПредставлениеСтавки = СтрЗаменить(ПредставлениеСтавки, " ", "");
	КонецЕсли;
	
	Возврат ПредставлениеСтавки;
	
КонецФункции

// Вызывается из процедур 
// "ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево" и "ЭлектронныеДокументыВнутренний.ПроверитьЗаполнениеОбязательныхПолейТЗ".
// Здесь можно выполнять различные проверки и дополнительную обработку таблицы.
//
Процедура ПроверитьОбработатьТаблицу(ТаблицаДанных, ДеревоДанных = Неопределено, ТекстОшибки = "") Экспорт
	
	Если ТаблицаДанных.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекстОшибкиПроверки = "";
	
    ПроверитьЕдиницыИзмерения(ТаблицаДанных, ТекстОшибкиПроверки);
	Если ДеревоДанных = Неопределено Тогда
		ПроверитьНулевыеЗначения(ТаблицаДанных, ТекстОшибкиПроверки);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстОшибкиПроверки) Тогда
		Если ДеревоДанных <> Неопределено Тогда
			СтрокаСОшибкой = ДеревоДанных.Строки.Добавить();
			СтрокаСОшибкой.ПолныйПуть = "ТекстОшибки";
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ТекстОшибки", ТекстОшибкиПроверки);
		Иначе
			ТекстОшибки = ТекстОшибки + ТекстОшибкиПроверки + Символы.ПС;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Теперь 4-значные коды передавать можно, но у 3-значых нужно по-прежнему обрезать пробелы
Процедура ПроверитьЕдиницыИзмерения(ТаблицаДанных, ТекстОшибки)
	
	ТекстОшибкиПроверки = "";
	
	КолонкиЕдиницИзмеренияСтрокой = ВРег(",БазоваяЕдиницаКод,ЕдиницаИзмеренияКодДо,ЕдиницаИзмеренияКод,УпаковкаКод,ЕдиницаИзмеренияКодПоОКЕИ,");
	
	ПроверяемыеКолонки = Новый Массив;
	Модификатор = "1";
	Для Каждого Колонка Из ТаблицаДанных.Колонки Цикл
		Если Найти(КолонкиЕдиницИзмеренияСтрокой, "," + ВРег(Колонка.Имя) + ",") > 0 Тогда
			Если ТипЗнч(ТаблицаДанных[0][Колонка.Имя]) = Тип("Строка") Тогда
				ПроверяемыеКолонки.Добавить(Колонка.Имя);
				
				КС = Новый КвалификаторыСтроки(Колонка.ТипЗначения.КвалификаторыСтроки.Длина, ДопустимаяДлина.Переменная);
				ОТ = Новый ОписаниеТипов(Колонка.ТипЗначения, , , , КС);
				ТаблицаДанных.Колонки.Добавить(Колонка.Имя+Модификатор, ОТ, Колонка.Заголовок, Колонка.Ширина);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если ПроверяемыеКолонки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ИмяКолонки Из ПроверяемыеКолонки Цикл
		Для Каждого СтрокаДанных Из ТаблицаДанных Цикл
			СтрокаДанных[ИмяКолонки+Модификатор] = СокрЛП(СтрокаДанных[ИмяКолонки]);
		КонецЦикла;
		Если ЗначениеЗаполнено(ТекстОшибкиПроверки) Тогда
			Прервать;
		КонецЕсли;
		ТаблицаДанных.Колонки.Удалить(ИмяКолонки);
		ТаблицаДанных.Колонки[ИмяКолонки+Модификатор].Имя = ИмяКолонки;
	КонецЦикла;

	Если ЗначениеЗаполнено(ТекстОшибкиПроверки) Тогда
		ТекстОшибки = ТекстОшибки + ТекстОшибкиПроверки;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьНулевыеЗначения(ТаблицаДанных, ТекстОшибки)
	
	ТекстОшибкиПроверки = "";
	
	КолонкиЧисловыхПоказателей = ВРег(",Цена,Сумма,СуммаБезНДС,СуммаСНДС,");
	
	ПроверяемыеКолонки = Новый СписокЗначений;
	Для Каждого Колонка Из ТаблицаДанных.Колонки Цикл
		Если Найти(КолонкиЧисловыхПоказателей, "," + ВРег(Колонка.Имя) + ",") > 0 Тогда
			ПроверяемыеКолонки.Добавить(Колонка.Имя);
		КонецЕсли;
	КонецЦикла;
	ПроверяемыеКолонки.СортироватьПоЗначению(НаправлениеСортировки.Убыв);

	Если ПроверяемыеКолонки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ИмяКолонки Из ПроверяемыеКолонки Цикл
		Для Каждого СтрокаДанных Из ТаблицаДанных Цикл
			Если СтрокаДанных[ИмяКолонки.Значение] = 0 Тогда
				ТекстОшибкиПроверки = ТекстОшибкиПроверки + "В строке №"+Строка(ТаблицаДанных.Индекс(СтрокаДанных)+1)+
					" не заполнено значение """+ИмяКолонки.Значение+""""+Символы.ПС;
			КонецЕсли;
		КонецЦикла;
		Если ЗначениеЗаполнено(ТекстОшибкиПроверки) Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Если ЗначениеЗаполнено(ТекстОшибкиПроверки) Тогда
		ТекстОшибки = ТекстОшибки + ТекстОшибкиПроверки;
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий форм

// Обработчик события "ПриПолученииДанных" в формах списков справочника "Контрагенты"
//
Процедура УстановитьСтатусПодключенияК1СЭДО(Элемент, ОформленияСтрок) Экспорт
    нзСостоянияКонтрагентов = РегистрыСведений.СостоянияКонтрагентовБЭД.СоздатьНаборЗаписей();
	ЭлементОтбора = нзСостоянияКонтрагентов.Отбор.Контрагент;
	ЭлементОтбора.ВидСравнения = ВидСравнения.Равно;
	ЭлементОтбора.Использование = Истина;
	Для Каждого ОФ Из ОформленияСтрок Цикл
		ЭлементОтбора.Значение = ОФ.ДанныеСтроки.Ссылка;
		нзСостоянияКонтрагентов.Прочитать();
		Если нзСостоянияКонтрагентов.Количество() > 0 И 
			(нзСостоянияКонтрагентов[0].Состояние = Перечисления.СостоянияКонтрагентаБЭД.НастроенЭДО ИЛИ
			нзСостоянияКонтрагентов[0].Состояние = Перечисления.СостоянияКонтрагентаБЭД.Подключен) Тогда
			
			ОФ.Ячейки.ПодключенК1СЭДО.ЗначениеКартинки = БиблиотекаКартинок.ЭмблемаСервиса1СЭДО;
			
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ЭДО_ФормаСпискаДокумента_ПриОткрытии(ФормаСписка, мИспользоватьОбменЭД, ИмяСписка = "Список") Экспорт
	
	Если Не мИспользоватьОбменЭД Тогда
		КолонкиСписка = ФормаСписка.ЭлементыФормы[ИмяСписка].Колонки;
		КолонкиСписка.СостояниеЭД.Видимость = Ложь;
		Если КолонкиСписка.Найти("ДействияСНашейСтороны") <> Неопределено Тогда
			КолонкиСписка.ДействияСНашейСтороны.Видимость = Ложь;
		КонецЕсли;
		Если КолонкиСписка.Найти("ДействияСоСтороныДругогоУчастника") <> Неопределено Тогда
			КолонкиСписка.ДействияСНашейСтороны.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЭДО_ФормаСпискаДокумента_ПриПолученииДанных(Элемент, ОформленияСтрок, мИспользоватьОбменЭД, ЭтоСчетФактура = Ложь) Экспорт

	Если ЭтоСчетФактура Тогда
		ВидимостьКолонок = Элемент.Колонки.СостояниеЭД.Видимость
	  		ИЛИ Элемент.Колонки.ДействияСНашейСтороны.Видимость
	  		ИЛИ Элемент.Колонки.ДействияСоСтороныДругогоУчастника.Видимость;
	Иначе
		ВидимостьКолонок = Элемент.Колонки.СостояниеЭД.Видимость;
	КонецЕсли;
	
	Если Не мИспользоватьОбменЭД ИЛИ Не ВидимостьКолонок Тогда
		Возврат;
	КонецЕсли;
	
	// Сформируем массив документов для получения состояния 
	ДокументыДляПолученияСостояния = Новый Массив();
	Для Каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		ДокументыДляПолученияСостояния.Добавить(ОформлениеСтроки.ДанныеСтроки.Ссылка);
	КонецЦикла;
	
	СостоянияЭД = ЭлектронныеДокументы.ПолучитьТекстСостоянияЭДПоВладельцам(ДокументыДляПолученияСостояния, ЭтоСчетФактура);
	
	Если ЭтоСчетФактура Тогда
		Для Каждого оформлениеСтроки Из ОформленияСтрок Цикл
			ОформлениеСтроки.Ячейки.СостояниеЭД.Видимость = Ложь;
			
			СостояниеЭД = СостоянияЭД[ОформлениеСтроки.ДанныеСтроки.Ссылка];
			ДействияСНашейСтороны 			  = ?(СостояниеЭД = Неопределено, "", СостояниеЭД.ДействияСНашейСтороны);
			ДействияСоСтороныДругогоУчастника = ?(СостояниеЭД = Неопределено, "", СостояниеЭД.ДействияСоСтороныДругогоУчастника);
			
			ОформлениеСтроки.Ячейки.ДействияСНашейСтороны.УстановитьТекст(ДействияСНашейСтороны);
			ОформлениеСтроки.Ячейки.ДействияСоСтороныДругогоУчастника.УстановитьТекст(ДействияСоСтороныДругогоУчастника);
			
		КонецЦикла
	Иначе
		Для Каждого ОформлениеСтроки Из ОформленияСтрок Цикл
			ОформлениеСтроки.Ячейки.СостояниеЭД.УстановитьТекст(СостоянияЭД[ОформлениеСтроки.ДанныеСтроки.Ссылка]);
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СлужебныеПроцедурыИФункции

Процедура ДобавитьИтераторТаблице(Таблица, ЗначениеИтератора)
	
	Таблица.Колонки.Добавить("ИтераторТаблицыРеквизитовОбъекта");
	Таблица.ЗаполнитьЗначения(ЗначениеИтератора, "ИтераторТаблицыРеквизитовОбъекта");
	
КонецПроцедуры

// Проверяет переданные таблицы реквизитов на совпадения.
//
// Параметры:
//  Таблица1, Таблица2 - таблицы значений, реквизиты, которые надо проверить на совпадение,
//  РеквизитыОбъекта   - строка, содержит реквизиты, перечисленные через запятую,
//  ДопПараметры       - структура дополнительных параметров, по которым надо проводить сравнение.
//
Функция ТаблицыРеквизитовОбъектовОдинаковые(Таблица1, Таблица2, РеквизитыОбъекта) Экспорт
	
	ДобавитьИтераторТаблице(Таблица1, +1);
	ДобавитьИтераторТаблице(Таблица2, -1);
	
	ТаблицаРезультат = Таблица1.Скопировать();
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(Таблица2, ТаблицаРезультат);
	
	ТаблицаРезультат.Свернуть(РеквизитыОбъекта, "ИтераторТаблицыРеквизитовОбъекта");
	
	КоличествоОдинаковыхСтрок = ТаблицаРезультат.НайтиСтроки(Новый Структура("ИтераторТаблицыРеквизитовОбъекта", 0)).Количество();
	
	КоличествоСтрокТаблицы = ТаблицаРезультат.Количество();
	ПризнакСовпадения = КоличествоОдинаковыхСтрок = КоличествоСтрокТаблицы;
	
	Возврат ПризнакСовпадения;
	
КонецФункции

Функция ИдентификаторТовараПоСтроке(Знач СтрокаТовара, Знач МаксимальнаяДлина = Неопределено)
	
	Если Не ЗначениеЗаполнено(МаксимальнаяДлина) Тогда
		МаксимальнаяДлина = 110;
	КонецЕсли;
	
	Идентификатор = СтрокаТовара;
	Идентификатор = ВРег(Идентификатор);
	Идентификатор = СтрЗаменить(Идентификатор, " ", "");
	
	Длина = СтрДлина(Идентификатор);
	
	Если Длина > МаксимальнаяДлина Тогда
		
		ХешСтрока = СтроковыеФункцииКлиентСервер.ВычислитьХешСтрокиПоАлгоритмуMD5(Идентификатор);
		ХешСтрока = СтрЗаменить(ХешСтрока, " ", "");
		
		ДлинаХешСтроки = СтрДлина(ХешСтрока);
		
		Идентификатор = Лев(Идентификатор, МаксимальнаяДлина - ДлинаХешСтроки) + ХешСтрока;
		
	КонецЕсли;
	
	Возврат Идентификатор;
	
КонецФункции

#Область ЗапросыНастройкиДополнительныхПолей

Функция ЗапросКонструктораДополнительныхПолейШапкиУПД()
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	СчетФактураВыданныйДокументыОснования.Ссылка,
	               |	ВЫРАЗИТЬ(СчетФактураВыданныйДокументыОснования.ДокументОснование КАК Документ.РеализацияТоваровУслуг) КАК ДокументОснование
	               |ПОМЕСТИТЬ ВТДокументыОснования
	               |ИЗ
	               |	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	               |ГДЕ
	               |	СчетФактураВыданныйДокументыОснования.Ссылка = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	               |	ВТДокументыОснования.Ссылка.Номер,
	               |	ВТДокументыОснования.Ссылка.Дата,
	               |	ВТДокументыОснования.Ссылка.НомерПлатежноРасчетногоДокумента,
	               |	ВТДокументыОснования.Ссылка.ДатаПлатежноРасчетногоДокумента,
	               |	ВТДокументыОснования.Ссылка.Организация,
	               |	ВТДокументыОснования.Ссылка.Ответственный,
	               |	ВТДокументыОснования.Ссылка.СтавкаНДС,
	               |	ВТДокументыОснования.Ссылка.Сумма,
	               |	ВТДокументыОснования.Ссылка.СуммаНДС,
	               |	ВТДокументыОснования.Ссылка.Контрагент,
	               |	ВТДокументыОснования.Ссылка.КППКонтрагента,
	               |	ВТДокументыОснования.Ссылка.ВалютаДокумента,
	               |	ВТДокументыОснования.Ссылка.СуммаДокумента,
	               |	ВТДокументыОснования.Ссылка.ДоговорКонтрагента,
	               |	ВТДокументыОснования.Ссылка.ВидСчетаФактуры,
	               |	ВТДокументыОснования.Ссылка.НомерИсходногоДокумента,
	               |	ВТДокументыОснования.Ссылка.ДатаИсходногоДокумента,
	               |	ВТДокументыОснования.Ссылка.СуммаУвеличение,
	               |	ВТДокументыОснования.Ссылка.СуммаУменьшение,
	               |	ВТДокументыОснования.Ссылка.НомерИсправления,
	               |	ВТДокументыОснования.Ссылка.ДатаИсправляемогоКорректировочногоДокумента,
	               |	ВТДокументыОснования.Ссылка.НомерИсправляемогоКорректировочногоДокумента,
	               |	ВТДокументыОснования.Ссылка.СуммаНДСУвеличение,
	               |	ВТДокументыОснования.Ссылка.СуммаНДСУменьшение,
	               |	ВТДокументыОснования.Ссылка.СуммаНДСДокумента,
	               |	ВТДокументыОснования.Ссылка.Комитент,
	               |	ВТДокументыОснования.Ссылка.СуммаДокументаКомиссия,
	               |	ВТДокументыОснования.Ссылка.СуммаНДСДокументаКомиссия,
	               |	ВТДокументыОснования.Ссылка.СуммаУвеличениеКомиссия,
	               |	ВТДокументыОснования.Ссылка.СуммаУменьшениеКомиссия,
	               |	ВТДокументыОснования.Ссылка.СуммаНДСУвеличениеКомиссия,
	               |	ВТДокументыОснования.Ссылка.СуммаНДСУменьшениеКомиссия,
	               |	ВТДокументыОснования.ДокументОснование.Номер,
	               |	ВТДокументыОснования.ДокументОснование.Дата,
	               |	ВТДокументыОснования.ДокументОснование.ВидОперации,
	               |	ВТДокументыОснования.ДокументОснование.Организация,
	               |	ВТДокументыОснования.ДокументОснование.Подразделение,
	               |	ВТДокументыОснования.ДокументОснование.ДоговорКонтрагента,
	               |	ВТДокументыОснования.ДокументОснование.Контрагент,
	               |	ВТДокументыОснования.ДокументОснование.СуммаДокумента,
	               |	ВТДокументыОснования.ДокументОснование.АдресДоставки,
	               |	ВТДокументыОснования.ДокументОснование.Грузополучатель,
	               |	ВТДокументыОснования.ДокументОснование.Грузоотправитель,
	               |	ВТДокументыОснования.ДокументОснование.ОтпускРазрешил,
	               |	ВТДокументыОснования.ДокументОснование.ОтпускПроизвел,
	               |	ВТДокументыОснования.ДокументОснование.ДоверенностьНомер,
	               |	ВТДокументыОснования.ДокументОснование.ДоверенностьДата,
	               |	ВТДокументыОснования.ДокументОснование.ДоверенностьВыдана,
	               |	ВТДокументыОснования.ДокументОснование.ДоверенностьЧерезКого,
	               |	ВТДокументыОснования.ДокументОснование.ДополнениеКАдресуДоставки,
	               |	ВТДокументыОснования.ДокументОснование.НомерЧекаККМ
	               |ИЗ
	               |	ВТДокументыОснования КАК ВТДокументыОснования";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ЗапросКонструктораДополнительныхПолейШапкиСчетаФактуры()
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	СчетФактураВыданный.Номер,
	               |	СчетФактураВыданный.Дата,
	               |	СчетФактураВыданный.НомерПлатежноРасчетногоДокумента,
	               |	СчетФактураВыданный.ДатаПлатежноРасчетногоДокумента,
	               |	СчетФактураВыданный.Организация,
	               |	СчетФактураВыданный.СтавкаНДС,
	               |	СчетФактураВыданный.Сумма,
	               |	СчетФактураВыданный.СуммаНДС,
	               |	СчетФактураВыданный.Контрагент,
	               |	СчетФактураВыданный.КППКонтрагента,
	               |	СчетФактураВыданный.СуммаДокумента,
	               |	СчетФактураВыданный.ДоговорКонтрагента,
	               |	СчетФактураВыданный.ВидСчетаФактуры,
	               |	СчетФактураВыданный.ДатаВыставления,
	               |	СчетФактураВыданный.НомерИсходногоДокумента,
	               |	СчетФактураВыданный.ДатаИсходногоДокумента,
	               |	СчетФактураВыданный.СуммаУвеличение,
	               |	СчетФактураВыданный.СуммаУменьшение,
	               |	СчетФактураВыданный.НомерИсправляемогоКорректировочногоДокумента,
	               |	СчетФактураВыданный.ДатаИсправляемогоКорректировочногоДокумента,
	               |	СчетФактураВыданный.СуммаНДСУвеличение,
	               |	СчетФактураВыданный.СуммаНДСУменьшение,
	               |	СчетФактураВыданный.СуммаНДСДокумента,
	               |	СчетФактураВыданный.Комитент,
	               |	СчетФактураВыданный.СуммаДокументаКомиссия,
	               |	СчетФактураВыданный.СуммаНДСДокументаКомиссия,
	               |	СчетФактураВыданный.СуммаУвеличениеКомиссия,
	               |	СчетФактураВыданный.СуммаУменьшениеКомиссия,
	               |	СчетФактураВыданный.СуммаНДСУвеличениеКомиссия,
	               |	СчетФактураВыданный.СуммаНДСУменьшениеКомиссия,
	               |	СчетФактураВыданный.ИдентификаторГосКонтракта
	               |ИЗ
	               |	Документ.СчетФактураВыданный КАК СчетФактураВыданный
	               |ГДЕ
	               |	СчетФактураВыданный.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ЗапросКонструктораДополнительныхПолейШапкиПервичногоДокумента()
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	РеализацияТоваровУслуг.Номер,
	               |	РеализацияТоваровУслуг.Дата,
	               |	РеализацияТоваровУслуг.ВидОперации,
	               |	РеализацияТоваровУслуг.Организация,
	               |	РеализацияТоваровУслуг.Подразделение,
	               |	РеализацияТоваровУслуг.ДоговорКонтрагента,
	               |	РеализацияТоваровУслуг.Контрагент,
	               |	РеализацияТоваровУслуг.СуммаДокумента,
	               |	РеализацияТоваровУслуг.АдресДоставки,
	               |	РеализацияТоваровУслуг.Грузополучатель,
	               |	РеализацияТоваровУслуг.Грузоотправитель,
	               |	РеализацияТоваровУслуг.ОтпускРазрешил,
	               |	РеализацияТоваровУслуг.ОтпускПроизвел,
	               |	РеализацияТоваровУслуг.ДоверенностьНомер,
	               |	РеализацияТоваровУслуг.ДоверенностьДата,
	               |	РеализацияТоваровУслуг.ДоверенностьВыдана,
	               |	РеализацияТоваровУслуг.ДоверенностьЧерезКого,
	               |	РеализацияТоваровУслуг.ДополнениеКАдресуДоставки,
	               |	РеализацияТоваровУслуг.НомерЧекаККМ
	               |ИЗ
	               |	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	               |ГДЕ
	               |	РеализацияТоваровУслуг.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// МЧД

// Определяет типы для МЧД.
//
// Параметры:
//  Типы - Массив.
//
Процедура ПолучитьТипыОрганизацииПредставителяМЧД(Типы) Экспорт
	
	Типы.Добавить(Тип("СправочникСсылка.Организации"));
	Типы.Добавить(Тип("СправочникСсылка.Контрагенты"));

КонецПроцедуры

// Получить ссылку на организацию-представителя МЧД.
// 
// Параметры:
//  СведенияЮрЛица - структура:
// 		* ИНН 	- строка
// 		* КПП 	- строка
// 		* ОГРН 	- строка
// 
// Возвращаемое значение:
// 	СправочникСсылка - ссылка на элемент справочника, Неопределено
// 
Функция ПолучитьОрганизациюМЧД(СведенияЮрЛица) Экспорт
	
	ПараметрыПоиска = Новый Структура;
	ПараметрыПоиска.Вставить("ИНН", СведенияЮрЛица.ИНН);
	ПараметрыПоиска.Вставить("КПП", СведенияЮрЛица.КПП);
	
	Контрагент = НайтиСсылкуНаОбъект("Контрагенты",,ПараметрыПоиска);
	Если ЗначениеЗаполнено(Контрагент) Тогда
		Возврат Контрагент;
	КонецЕсли;
	
	Организация = НайтиСсылкуНаОбъект("Организации",,ПараметрыПоиска);
	Если ЗначениеЗаполнено(Организация) Тогда
		Возврат Организация;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Получает ссылку на физическое лицо по сведениям.
// 
// Параметры:
//  СведенияФизЛица - структура:
//  	* ИНН - строка
// 
// Возвращаемое значение:
//  - СправочникСсылка.ФизическиеЛица
//  - Неопределено - если не элемент не найден в ИБ.
//
Функция ПолучитьФизЛицоМЧД(СведенияФизЛица) Экспорт
	
	ФизическоеЛицо = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИНН", СведенияФизЛица.ИНН);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ФизическиеЛица.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ФизическиеЛица КАК ФизическиеЛица
	|ГДЕ
	|	ФизическиеЛица.ИНН = &ИНН";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ФизическоеЛицо = Выборка.Ссылка;
	КонецЕсли;
	 
	Возврат ФизическоеЛицо;
	
КонецФункции

// Возвращает признак, что ссылка является организацией.
// 
// Параметры:
//  Ссылка - ЛюбаяСсылка
// 
// Возвращаемое значение:
//  Булево
Функция ЭтоОрганизация(Ссылка) Экспорт
	
	Возврат ТипЗнч(Ссылка) = Тип("СправочникСсылка.Организации");
	
КонецФункции
 
// Вызывается при изменении реквизита формы с типом ФизЛицо, запрашивает данные физлица
//
// Параметры:
//  ФизЛицо - СправочникСсылка - ссылка на элемент справочника, по которому получаются данные.
//  Сведения - см. МашиночитаемыеДоверенности.НовыеДанныеФизЛица
//
// Пример:
// 
//  Сведения.ДатаРождения = ФизЛицо.ДатаРождения;
//	Сведения.ИНН = ФизЛицо.ИНН;
//	Сведения.МестоРождения = ФизЛицо.МестоРождения;
//	Сведения.Пол = "М";
//	Сведения.СтраховойНомерПФР = ФизЛицо.СтраховойНомерПФР;
//	Сведения.ФИО = ФизЛицо.Наименование;
//	ФИО = _ДемоОбменСКонтрагентами.ЧастиИмени(Сведения.ФИО);
//	ЗаполнитьЗначенияСвойств(Сведения, ФИО);
//	Сведения.Гражданство = Справочники.КлассификаторСтранМира.Россия;
//	Сведения.КодФНС = "";
//	Сведения.Серия = "32 01";
//	Сведения.Номер = "334455";
//	Сведения.ДатаВыдачи = Дата(2005, 10, 10));
//	Сведения.КемВыдан = "УВД г. Васюки";
//	Сведения.КодПодразделения = "502-001";
//
Процедура ПриИзмененииДанныеФизЛицаМЧД(ФизЛицо, Сведения) Экспорт
	
	ЗаполнитьЗначенияСвойств(Сведения, ФизЛицо);
	
	Отбор = Новый Структура;
	Отбор.Вставить("ФизЛицо", ФизЛицо);
	ПаспортныеДанные = РегистрыСведений.ПаспортныеДанныеФизЛиц.ПолучитьПоследнее(ТекущаяДатаСеанса(), Отбор);
	
	Сведения.Серия = ПаспортныеДанные.ДокументСерия;
	Сведения.Номер = ПаспортныеДанные.ДокументНомер;
	Сведения.ДатаВыдачи = ПаспортныеДанные.ДокументДатаВыдачи;
	Сведения.КемВыдан = ПаспортныеДанные.ДокументКемВыдан;
	Сведения.КодПодразделения = ПаспортныеДанные.ДокументКодПодразделения;
	Сведения.КодФНС = ПаспортныеДанные.ДокументВид.КодИМНС;
	
	ФИО = ПолучитьФИОФизЛица(ФизЛицо);
	ЗаполнитьЗначенияСвойств(Сведения, ФИО);
	
КонецПроцедуры

Функция ПолучитьФИОФизЛица(ФизЛицо, НаДату = Неопределено) Экспорт
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
	ДатаСрезаРегистра = ?(ЗначениеЗаполнено(НаДату), НаДату, РабочаяДата);
	#Иначе
	ДатаСрезаРегистра = ?(ЗначениеЗаполнено(НаДату), НаДату, ТекущаяДатаСеанса());
	#КонецЕсли
	
	Результат = Новый Структура("Фамилия, Имя, Отчество", "", "", "");
	Если Метаданные.Справочники.Найти("ФизическиеЛица") = Неопределено Тогда
		ЗаполнитьЗначенияСвойств(Результат, ФизЛицо);
	Иначе
		ДанныеФЛ = РегистрыСведений.ФИОФизЛиц.СрезПоследних(ДатаСрезаРегистра, Новый Структура("ФизЛицо", ФизЛицо));
		
		Если ДанныеФЛ.Количество() > 0 Тогда
			ЗаполнитьЗначенияСвойств(Результат, ДанныеФЛ[0]);
		КонецЕсли;
	КонецЕсли;
	Возврат Результат;
	
КонецФункции
  
