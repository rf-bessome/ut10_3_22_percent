&НаКлиенте
Перем ТекущаяДатаПроизводства;

// Обработчики событий формы

&НаСервере   // ***
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	РежимОтображенияСерий = "ТолькоОстатки";
	
	ПараметрыУказанияСерий = Параметры.ПараметрыУказанияСерий;
	Склад = Параметры.Склад;
	
	Количество = Параметры.Количество;
	
	АвторизованВнешнийПользователь = Ложь;
	
	МенеджерОборудованияУТ.НастроитьПодключаемоеОборудование(ЭтаФорма, "");
	
	РеквизитыНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.Номенклатура,
		"БазоваяЕдиницаИзмерения");
		
	ЗаполнитьЗначенияСвойств(ЭтаФорма, РеквизитыНоменклатуры); 
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("Номенклатура", Параметры.Номенклатура);
	ПараметрыЗапроса.Вставить("Характеристика", Параметры.Характеристика);
	ПараметрыЗапроса.Вставить("Склад", Параметры.Склад);
	ПараметрыЗапроса.Вставить("Регистратор", Параметры.Регистратор);
	
	ВариантПолучениеДанныхИзРегистров = Обработки.ПодборСерийВДокументы.ВариантПолучениеДанныхИзРегистровПоПараметрамФормы();
	
	Элементы.СтраницыСерий.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	
	Заголовки = Обработки.ПодборСерийВДокументы.ЗаголовкиПоВариантуПолучениеДанныхИзРегистров(ВариантПолучениеДанныхИзРегистров);
	
	Элементы.РежимОтображенияСерий.СписокВыбора[0].Представление = Заголовки.ЗаголовокКнопки;
	
	Элементы.ОстаткиСерийСвободныйОстаток.Видимость = Ложь;
	Элементы.РежимОтображенияСерий.Видимость = Ложь;
	
	Серия = Параметры.Серия;
		
	Элементы.ПанельНавигации.ТекущаяСтраница = Элементы.СтраницаНавигацииОкончание;
	Элементы.ГруппаНазад.Видимость = Ложь;
		
	НайденныеСтроки = ОстаткиСерий.НайтиСтроки(Новый Структура("Серия",Серия));
	Если НайденныеСтроки.Количество() > 0 Тогда
		Элементы.ОстаткиСерий.ТекущаяСтрока = НайденныеСтроки[0].ПолучитьИдентификатор();
	КонецЕсли;
	
	Элементы.ГруппаГоденДо.Видимость = Истина;
	
	Элементы.ОстаткиСерийГоденДо.Видимость = Истина;
	
	Элементы.ПроизводительЕГАИС.Видимость = Истина;
	Элементы.ОстаткиСерийПроизводительЕГАИС.Видимость = Истина;
	
	Элементы.Справка2ЕГАИС.Видимость = Истина;
	Элементы.ОстаткиСерийСправка2ЕГАИС.Видимость = Истина;
	
	ИдентификаторТекущейСтроки = Параметры.ИдентификаторТекущейСтроки;
	
	Если Параметры.ТолькоПросмотр Тогда
		Параметры.ТолькоПросмотр = Ложь; //Для исключения платформенной обработки
		
		Элементы.ОК.Доступность         = Ложь;
		
		РежимПросмотра = Истина;
	КонецЕсли;
	
	ЗакрыватьПриВыборе = Ложь;
	
	ЗаполнитьРеквизитыПоСерии(ЭтаФорма,Серия);
	
	ПравоДобавленияСерий = ПравоДоступа("Добавление", Метаданные.Справочники.СерииНоменклатуры);
	
	Если ПравоДобавленияСерий Тогда
		
		// Выведем поля для заполнения доп. реквизитов серий 
		Если ЗначениеЗаполнено(Параметры.Серия) Тогда
			СерияОбъект = Параметры.Серия.ПолучитьОбъект();
		Иначе
			СерияОбъект = Обработки.ПодборСерийВДокументы.ВладелецСвойствСерий(Параметры.Номенклатура);
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьЗаголовокДекорацииСерия();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ) // ***
	
	ПоддерживаемыеТипыОборудования = "СканерШтрихкода";
	
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(Неопределено, ЭтаФорма, ПоддерживаемыеТипыОборудования);

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()  // ***

	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(Неопределено, ЭтаФорма);

КонецПроцедуры

&НаКлиенте          // ***
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Источник = "ПодключаемоеОборудование" И ВводДоступен() И НЕ ТолькоПросмотр Тогда
		ЗавершитьПодбор = Ложь;
		
		Если ИмяСобытия = "ScanData" Тогда
			
			Если РежимПросмотра Тогда
				Возврат;
			КонецЕсли;
			
			Если ОбработатьШтрихкоды(МенеджерОборудованияКлиент.ПреобразоватьДанныеСоСканераВМассив(Параметр)) Тогда
				ЗавершитьПодбор = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗавершитьПодбор
			И ОбязательныеРеквизитыЗаполнены(Ложь) Тогда
			ПодключитьОбработчикОжидания("ПодобратьСериюЗавершитьВвод", 0.1, Истина);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_СерииНоменклатуры" Тогда
		ЗаполнитьЗначенияСвойств(ЭтаФорма,Параметр);
	КонецЕсли;
	
КонецПроцедуры

// Конец области обработчиков событий формы

// Обработчики событий

&НаКлиенте
Процедура РежимПросмотраПриИзменении(Элемент)                                           // ***
	ВыполнитьЗапросЗаполненияТаблицыОстатков();
КонецПроцедуры

&НаКлиенте
Процедура ОстаткиСерийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка) // ***
	
	ОстаткиСерийВыборКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ОстаткиСерийВыборКлиент()   // ***
	
	ОчиститьСообщения();

	ТекущиеДанные = Элементы.ОстаткиСерий.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		ТекстПредупреждения = НСтр("ru = 'Выберите серию или введите новую.'");	
		Предупреждение(ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	ПодобратьСериюЗавершитьВвод();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроизводительЕГАИСПриИзменении(Элемент)  // ***
	НайтиСерию();
	УстановитьЗаголовокДекорацииСерия();
КонецПроцедуры

&НаКлиенте
Процедура Справка2ЕГАИСПриИзменении(Элемент)   // ***
	НайтиСерию();
	УстановитьЗаголовокДекорацииСерия();
КонецПроцедуры

// Конец области обработчиков событий

// Обработчики команд формы

&НаКлиенте
Процедура ОК(Команда)       // ***
	ПодобратьСериюЗавершитьВвод();
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)    // ***
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура Далее(Команда)      // ***
	
	Если Элементы.СтраницыСерий.ТекущаяСтраница = Элементы.СтраницаОстаткиСерий Тогда
		ОстаткиСерийВыборКлиент();
	Иначе
		
		Если Не ОбязательныеРеквизитыЗаполнены() Тогда
			Возврат;
		КонецЕсли;
		ПерейтиНаСледующийШаг();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда) // ***
	
	ВернутьсяНаПредыдущийШаг();
	
КонецПроцедуры

// Конец области обработчиков команд формы

// Служебные процедуры и функции

// Печать

&НаСервере
Процедура НайтиСерию()  // ***
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СерииНоменклатуры.Ссылка КАК Серия
	|ИЗ
	|	Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|ГДЕ
	|	(СерииНоменклатуры.ПроизводительЕГАИС = &ПроизводительЕГАИС
	|			И СерииНоменклатуры.Справка2ЕГАИС = &Справка2ЕГАИС)";
	
	Запрос.УстановитьПараметр("ВидНоменклатуры", ВидНоменклатуры);
	
	Запрос.УстановитьПараметр("ПроизводительЕГАИС", ПроизводительЕГАИС);
	
	Запрос.УстановитьПараметр("Справка2ЕГАИС", Справка2ЕГАИС);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Серия = Выборка.Серия;
	Иначе
		Серия = Справочники.СерииНоменклатуры.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

// Конец области печати

// Штрихкоды и торговое оборудование

&НаКлиенте
Функция ОбработатьШтрихкоды(ДанныеШтрихкодов) // ***
	
	Если ДанныеШтрихкодов.Количество() <> 1 Тогда
		
		ТекстСообщения = НСтр("ru = 'Не поддерживается одновременное распознование нескольких штрих-кодов серий. Сканируйте серии по одной.'");
		Предупреждение(ТекстСообщения);
		Возврат Ложь;
		
	КонецЕсли;
	
	ОбработатьШтрихкодыСервер(ДанныеШтрихкодов);
	
	Возврат Истина;
	
КонецФункции

&НаСервере    // ***
Процедура ОбработатьШтрихкодыСервер(ДанныеШтрихкодов)
	
	ШтрихкодыПоТипам = Обработки.ПодборСерийВДокументы.СтруктураПоТипамШтрихкодов(ДанныеШтрихкодов);
	
	Если ШтрихкодыПоТипам.БезТипа.Количество() > 0 Тогда
		Штрихкод = ДанныеШтрихкодов[0].Штрихкод;
		
		ПоляИзШтрихкода = НоменклатураКлиентСервер.ИнформацияОСерииИзШтрихкода(Штрихкод, 
																				Ложь,
																				Ложь);
		НайтиСерию();
		УстановитьЗаголовокДекорацииСерия();
		
	КонецЕсли;
	
КонецПроцедуры

// Конец области штрихкодов и торгового оборудования

// Прочее

&НаКлиентеНаСервереБезКонтекста
Функция ДанныеСерии(ДанныеДляЗаполнения)        // ***
	
	ДанныеСерии = Новый Структура(РеквизитыВводаСерии());
	ЗаполнитьЗначенияСвойств(ДанныеСерии, ДанныеДляЗаполнения);
	
	Возврат ДанныеСерии;
	
КонецФункции

&НаКлиенте
Процедура ПодобратьСериюЗавершитьВвод()     // ***
	
	Если РежимПросмотра Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	ВозвращаемоеЗначение = НоменклатураКлиентСервер.ВыбраннаяСерия();
	
	ТекущиеДанные = Элементы.ОстаткиСерий.ТекущиеДанные;
	
	Если Элементы.СтраницыСерий.ТекущаяСтраница = Элементы.СтраницаОстаткиСерий Тогда
		Если ТекущиеДанные = Неопределено Тогда
			ТекстПредупреждения = НСтр("ru = 'Выберите или введите серию.'");	
			Предупреждение(ТекстПредупреждения);
			Возврат;
		КонецЕсли;
		
		Серия = ТекущиеДанные.Серия;
		
	ИначеЕсли НЕ ОбязательныеРеквизитыЗаполнены()
		Или НЕ НайтиСоздатьСерию() Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ВозвращаемоеЗначение.ИдентификаторТекущейСтроки = ИдентификаторТекущейСтроки;
	ВозвращаемоеЗначение.Значение                   = Серия;
	ВозвращаемоеЗначение.ИмяТЧ                      = "Товары";
	ОповеститьОВыборе(ВозвращаемоеЗначение);
	Закрыть();
	
КонецПроцедуры

&НаКлиенте // ***
Функция ОбязательныеРеквизитыЗаполнены(ВыводитьСообщения = Истина)
	
	Отказ = Ложь;
	
	Если Не ЗначениеЗаполнено(Справка2ЕГАИС) Тогда
		Отказ = Истина;
		Если ВыводитьСообщения Тогда
			ТекстСообщения = НСтр("ru = 'Поле ""Справка 2 (ЕГАИС)"" не заполнено'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"Справка2ЕГАИС");
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПроизводительЕГАИС) Тогда
		Отказ = Истина;
		Если ВыводитьСообщения Тогда
			ТекстСообщения = НСтр("ru = 'Поле ""Производитель (ЕГАИС)"" не заполнено'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"ПроизводительЕГАИС");
		КонецЕсли;
	КонецЕсли;
	
	Возврат Не Отказ;
	
КонецФункции

&НаСервере
Функция НайтиСоздатьСерию() // ***
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Серия) Тогда
		
		НайтиСерию();
		УстановитьЗаголовокДекорацииСерия();
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Серия) Тогда
		
		СоздатьСерию();
		
	ИначеЕсли ПравоДобавленияСерий Тогда
	
		СерияОбъект = Серия.ПолучитьОбъект();
		
		ЗаполнитьЗначенияСвойств(СерияОбъект, ЭтаФорма, РеквизитыВводаСерии(Истина));
		
		СерияОбъект.Записать();
		
		Серия = СерияОбъект.Ссылка;
		
	КонецЕсли;
	
	Возврат ЗначениеЗаполнено(Серия);
	
КонецФункции

&НаСервере    // ***
Функция СоздатьСерию()
		
	Если Не ПравоДобавленияСерий Тогда
		
		ТекстСообщения = НСтр("ru = 'В программе еще не зарегистрирована серия %ПредставлениеСерии%. Недостаточно прав для регистрации новой серии.'");
		
		ПредставлениеСерии = НоменклатураКлиентСервер.ПредставлениеСерии(ЭтаФорма);
		
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ПредставлениеСерии%", ПредставлениеСерии);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
		Возврат Ложь;
		
	Иначе
	
		СерияОбъект = Справочники.СерииНоменклатуры.СоздатьЭлемент();
			
		ЗаполнитьЗначенияСвойств(СерияОбъект, ЭтаФорма, РеквизитыВводаСерии(Истина));
		СерияОбъект.Владелец = ЭтаФорма.ПараметрыЗапроса.Номенклатура;
		
		СерияОбъект.Записать();
		
		Серия = СерияОбъект.Ссылка;
		
		Возврат Истина;
		
	КонецЕсли;
	
КонецФункции

&НаСервере          // ***
Процедура ВыполнитьЗапросЗаполненияТаблицыОстатков()
	
	ТекстЗапроса = Обработки.ПодборСерийВДокументы.ТекстЗапросаФормированияТаблицыДанныеРегистров(ВариантПолучениеДанныхИзРегистров, Склад);
	
	ТекстЗапроса = ТекстЗапроса + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	ТекстЗапроса = ТекстЗапроса + 
	"ВЫБРАТЬ
	|	ДанныеРегистров.Серия КАК Серия,
	|	ВЫРАЗИТЬ(ДанныеРегистров.Серия КАК Справочник.СерииНоменклатуры).ПроизводительЕГАИС КАК ПроизводительЕГАИС,
	|	ВЫРАЗИТЬ(ДанныеРегистров.Серия КАК Справочник.СерииНоменклатуры).Справка2ЕГАИС КАК Справка2ЕГАИС,
	|	ДанныеРегистров.СвободныйОстаток КАК СвободныйОстаток
	|ИЗ
	|	ДанныеРегистров КАК ДанныеРегистров
	|ГДЕ
	|	ДанныеРегистров.СвободныйОстаток > 0
	|	ИЛИ &ВсеСерии";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Для Каждого Параметр Из ПараметрыЗапроса Цикл
		Запрос.УстановитьПараметр(Параметр.Ключ, Параметр.Значение);
	КонецЦикла;
	
	Если ВариантПолучениеДанныхИзРегистров <> "ВсеСерииНоменклатуры" Тогда
		Запрос.УстановитьПараметр("ВсеСерии", РежимОтображенияСерий = "ВсеСерии");
	Иначе
		Запрос.УстановитьПараметр("ВсеСерии", Истина);
	КонецЕсли;
	
	ОстаткиСерий.Загрузить(Запрос.Выполнить().Выгрузить());
	
	КоличествоСерий = ОстаткиСерий.Количество();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция РеквизитыВводаСерии(ДляЗаполненияОбъекта = Ложь)   // ***
	РеквизитыВводаСерии = ?(ДляЗаполненияОбъекта ,"","Серия,") + "
		|СрокГодности,
		|ДатаПроизводства,
		|ПроизводительЕГАИС,
		|Справка2ЕГАИС";
	Возврат РеквизитыВводаСерии;
КонецФункции

&НаСервере
Процедура ПерейтиНаСледующийШаг()         // ***
	
	Если Элементы.СтраницыСерий.ТекущаяСтраница = Элементы.СтраницаВводСерии Тогда
		Если НЕ НайтиСоздатьСерию() Тогда
			Возврат;
		КонецЕсли;
		СерияСписываемая = Серия;
	Иначе
		ТекущиеДанные    = ОстаткиСерий.НайтиПоИдентификатору(Элементы.ОстаткиСерий.ТекущаяСтрока);
		СерияСписываемая = ТекущиеДанные.Серия;
	КонецЕсли;
	
	СерияСписываемаяКешВвода = ДанныеСерии(ЭтаФорма);
	
	НастроитьФормуПоШагу("Шаг2");
	
КонецПроцедуры

&НаСервере
Процедура ВернутьсяНаПредыдущийШаг()   // ***
	
	Если Элементы.СтраницыСерий.ТекущаяСтраница = Элементы.СтраницаВводСерии Тогда
		СерияНовая = Серия;
	Иначе
		ТекущиеДанные =ОстаткиСерий.НайтиПоИдентификатору(Элементы.ОстаткиСерий.ТекущаяСтрока);
		СерияНовая = ТекущиеДанные.Серия;
	КонецЕсли;
	
	СерияНоваяКешВвода = ДанныеСерии(ЭтаФорма);
	
	НастроитьФормуПоШагу("Шаг1");
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоШагу(Шаг) // ***
	
	Если Шаг = "Шаг1" Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтаФорма, СерияСписываемаяКешВвода);
		
		Элементы.ПанельНавигации.ТекущаяСтраница = Элементы.СтраницаНавигацииНачало;
		Элементы.Далее.КнопкаПоУмолчанию = Истина;
		
		ЭтаФорма.Заголовок = НСтр("ru = 'Укажите списываемую серию'");
		Элементы.СтраницыСерий.ТекущаяСтраница = Элементы.СтраницаОстаткиСерий;
		ИскомаяСерия = СерияСписываемая;
		
	Иначе
		
		ЗаполнитьЗначенияСвойств(ЭтаФорма, СерияНоваяКешВвода);
		
		Элементы.ПанельНавигации.ТекущаяСтраница = Элементы.СтраницаНавигацииОкончание;
		Элементы.ОК.КнопкаПоУмолчанию = Истина;
		
		ЭтаФорма.Заголовок = НСтр("ru = 'Укажите новую серию'");
		Элементы.СтраницыСерий.ТекущаяСтраница = Элементы.СтраницаВводСерии;
		ИскомаяСерия = СерияНовая;
		
	КонецЕсли;
	
	НайденныеСтроки = ОстаткиСерий.НайтиСтроки(Новый Структура("Серия",ИскомаяСерия));
	Если НайденныеСтроки.Количество() > 0 Тогда
		Элементы.ОстаткиСерий.ТекущаяСтрока = НайденныеСтроки[0].ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста  // ***
Процедура ЗаполнитьРеквизитыПоСерии(Приемник, Знач СерияДляЗаполнения)
	
	Если ЗначениеЗаполнено(СерияДляЗаполнения) Тогда
		Если ТипЗнч(СерияДляЗаполнения) = Тип("СправочникСсылка.СерииНоменклатуры") Тогда
			Приемник.Серия = СерияДляЗаполнения;
			СерияСсылка = СерияДляЗаполнения;
		Иначе
			ЗаполнитьЗначенияСвойств(Приемник, СерияДляЗаполнения);
			СерияСсылка = СерияДляЗаполнения.Серия;
		КонецЕсли;
		Если ЗначениеЗаполнено(СерияСсылка) Тогда
			РеквизитыСерии = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СерияСсылка,
				"Наименование,ПроизводительЕГАИС,Справка2ЕГАИС,ДатаПроизводства,СрокГодности
				|");
			ЗаполнитьЗначенияСвойств(Приемник, РеквизитыСерии);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовокДекорацииСерия() // *** 
	
	Элементы.ДекорацияСерия.Заголовок = ?(ЗначениеЗаполнено(Серия), НСтр("ru = 'Зарегистрированная серия'"),
		НСтр("ru = 'Новая серия'"));
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	// Вставить содержимое обработчика.
КонецПроцедуры

// Конец области прочего

// Конец области служебных процедур и функций
