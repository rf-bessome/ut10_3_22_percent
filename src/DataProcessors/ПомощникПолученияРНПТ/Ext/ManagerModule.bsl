#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Функция заполняет первые два шага в помощнике получения РНПТ
// Провести инвентаризацию и Создать уведомления
//
// Параметры:
//  Параметры - Структура: 
//						- Организация - СправочникСсылка - организация
//						- Период - Дата - дата помощника получения РНПТ
//  АдресРезультата - строка - адрес, куда будет помещен результат 
// 
Функция ДанныеЗаполненияПомощникаПолученияРНПТ(Параметры, АдресРезультата) Экспорт
	
	ДанныеЗаполнения = ПолучитьДанныеЗаполнения(
		Параметры.Период,
		Параметры.Организация);
	
	ПоместитьВоВременноеХранилище(ДанныеЗаполнения, АдресРезультата);
	
КонецФункции

// Создает документы "Уведомление об остатках прослеживаемых товаров"
// и "Уведомление о ввозе прослеживаемых товаров" в фоне.
// 
// Параметры:
//  ПараметрыCоздания - Структура:
//						Дата - Дата - текущая дата
//						Организация - СправочникСсылка.Организации - организация
//						ПериодСобытия - Дата - дата в помощнике, начало месяца
//						ТаблицаОстатков - ТаблицаЗначений - таблица данных, по которым нужно сформировать уведомления об остатках
//						ТаблицаВвоза - ТаблицаЗначений - таблица данных, по которым нужно сформировать уведомления о ввозе
//  АдресРезультата - строка - адрес, куда будет помещен результат 
//
Функция СоздатьУведомленияВФоне(ПараметрыСоздания, АдресРезультата) Экспорт
	
	ТаблицаОстатковСПервичнымиДокументами = ПараметрыСоздания.ТаблицаОстатков;
	ТаблицаВвоза = ПараметрыСоздания.ТаблицаВвоза;
	Организация  = ПараметрыСоздания.Организация;
	
	СоздатьУведомленияОбОстатках(ТаблицаОстатковСПервичнымиДокументами, Организация);
	
	СоздатьУведомленияОВвозе(ТаблицаВвоза, Организация);
	
КонецФункции

// Создает документы по инвентаризации товаров из помощника получения РНПТ
// 
// Параметры:
//  ПараметрыCоздания - Структура:
//						Дата - Дата - текущая дата
//						Организация - СправочникСсылка.Организации - организация
//						ПериодСобытия - Дата - дата в помощнике, начало месяца
//						ТаблицаОстатков - ТаблицаЗначений - таблица данных, по которым нужно сформировать уведомления
//  АдресРезультата - строка - адрес, куда будет помещен результат
//
Функция СоздатьИнвентаризациюОбОстаткахВФоне(ПараметрыСоздания, АдресРезультата) Экспорт

	ТаблицаОстатков = ПараметрыСоздания.ТаблицаОстатков;
	Организация = ПараметрыСоздания.Организация;
	Дата = ПараметрыСоздания.Дата;
	
	СоздатьИнвентаризациюОбОстатках(Дата, Организация, ТаблицаОстатков, АдресРезультата);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СоздатьИнвентаризациюОбОстатках(Дата, Организация, ТаблицаОстатков, АдресРезультата)
	
	МассивСкладов = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ТаблицаОстатков.ВыгрузитьКолонку("Склад"));
	ФильтрПоСкладу = Новый Структура("Склад");
	СписокНоменклатуры = Новый СписокЗначений();
	СозданныеДокументы = Новый Массив();
	
	Для каждого ТекущийСклад Из МассивСкладов Цикл
		
		ФильтрПоСкладу.Склад = ТекущийСклад;
		НайденныеСтроки = ТаблицаОстатков.НайтиСтроки(ФильтрПоСкладу);
		
		Для каждого ТекущаяСтрока Из НайденныеСтроки Цикл
			СписокНоменклатуры.Добавить(ТекущаяСтрока.Номенклатура);
		КонецЦикла;
		
		СсылкаНаДокумент = СоздатьИнвентаризациюТовара(Дата, Организация, ТекущийСклад, СписокНоменклатуры);
		
		СозданныеДокументы.Добавить(СсылкаНаДокумент);
		
		СписокНоменклатуры.Очистить();
		
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(СозданныеДокументы, АдресРезультата);
	
КонецПроцедуры

Функция СоздатьИнвентаризациюТовара(Дата, Организация, ТекущийСклад, СписокНоменклатуры)
	
	НовыйДокумент = Документы.ИнвентаризацияТоваровНаСкладе.СоздатьДокумент();
	НовыйДокумент.Дата = Дата;
	
	ДанныеЗаполнения = ДанныеЗаполненияИнвентаризации(Дата, Организация, ТекущийСклад, СписокНоменклатуры);
	
	НовыйДокумент.Заполнить(ДанныеЗаполнения);
	
	НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение);
	
	Возврат НовыйДокумент.Ссылка;
	
КонецФункции

Функция ДанныеЗаполненияИнвентаризации(Дата, Организация, Склад, СписокНоменклатуры)
	
	ДанныеЗаполнения = Новый Структура();
	
	ДанныеЗаполнения.Вставить("Дата",                 Дата);
	ДанныеЗаполнения.Вставить("Организация",          Организация);
	ДанныеЗаполнения.Вставить("Склад",                Склад);
	ДанныеЗаполнения.Вставить("СписокНоменклатуры",   СписокНоменклатуры);
	
	Возврат ДанныеЗаполнения;
КонецФункции

Процедура СоздатьУведомленияОбОстатках(ТаблицаОстатковСПервичнымиДокументами, Организация)
	
	ТаблицаТНВЭД = ТаблицаОстатковСПервичнымиДокументами.Скопировать(,"КодТНВЭД,Основание,ЕдиницаПоКлассификатору,ЕдиницаПрослеживаемостиПоКлассификатору");
	ТаблицаТНВЭД.Свернуть("КодТНВЭД,Основание,ЕдиницаПоКлассификатору,ЕдиницаПрослеживаемостиПоКлассификатору");
	Фильтр = Новый Структура("КодТНВЭД,Основание,ЕдиницаПоКлассификатору,ЕдиницаПрослеживаемостиПоКлассификатору");
	
	Если НЕ ЗначениеЗаполнено(ТаблицаОстатковСПервичнымиДокументами) Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого ТекущаяСтрокТНВЭД Из ТаблицаТНВЭД Цикл
		
		ДатаОснования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущаяСтрокТНВЭД.Основание, "Дата");
		
		НовыйДокумент = Документы.УведомлениеОбОстаткахПрослеживаемыхТоваров.СоздатьДокумент();
		
		ЗаполнитьЗначенияСвойств(НовыйДокумент, ТекущаяСтрокТНВЭД);
		
		НовыйДокумент.Дата = ДатаОснования + 1;
		НовыйДокумент.ПервичныйДокумент = ТекущаяСтрокТНВЭД.Основание;
		НовыйДокумент.Организация = Организация;
		НовыйДокумент.ЕдиницаИзмерения = ТекущаяСтрокТНВЭД.ЕдиницаПоКлассификатору;
		НовыйДокумент.ЕдиницаПрослеживаемости = ТекущаяСтрокТНВЭД.ЕдиницаПрослеживаемостиПоКлассификатору;
		
		ТаблицаТовары = НовыйДокумент.Товары;
		
		ЗаполнитьЗначенияСвойств(Фильтр, ТекущаяСтрокТНВЭД);
		НайденныеСтроки = ТаблицаОстатковСПервичнымиДокументами.НайтиСтроки(Фильтр);
		
		Для каждого ТекущаяСтрока Из НайденныеСтроки Цикл
			
			НоваяСтрока = ТаблицаТовары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
			
		КонецЦикла;
		
		НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СоздатьУведомленияОВвозе(ТаблицаВвоза, Организация)
	
	ТаблицаТНВЭД = ТаблицаВвоза.Скопировать(,"КодТНВЭД,Основание,ЕдиницаПоКлассификатору,ЕдиницаПрослеживаемостиПоКлассификатору");
	ТаблицаТНВЭД.Свернуть("КодТНВЭД,Основание,ЕдиницаПоКлассификатору,ЕдиницаПрослеживаемостиПоКлассификатору");
	Фильтр = Новый Структура("КодТНВЭД, Основание, ЕдиницаПоКлассификатору,ЕдиницаПрослеживаемостиПоКлассификатору");
	
	Если НЕ ЗначениеЗаполнено(ТаблицаВвоза) Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого ТекущаяСтрокТНВЭД Из ТаблицаТНВЭД Цикл
		
		РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТекущаяСтрокТНВЭД.Основание, "Дата, Контрагент");
		
		НовыйДокумент = Документы.УведомлениеОВвозеПрослеживаемыхТоваров.СоздатьДокумент();
		
		ЗаполнитьЗначенияСвойств(НовыйДокумент, ТекущаяСтрокТНВЭД);
		
		НовыйДокумент.Дата = РеквизитыОснования.Дата + 1;
		НовыйДокумент.Контрагент = РеквизитыОснования.Контрагент;
		НовыйДокумент.ПервичныйДокумент = ТекущаяСтрокТНВЭД.Основание;
		НовыйДокумент.Организация = Организация;
		НовыйДокумент.ЕдиницаИзмерения = ТекущаяСтрокТНВЭД.ЕдиницаПоКлассификатору;
		НовыйДокумент.ЕдиницаПрослеживаемости = ТекущаяСтрокТНВЭД.ЕдиницаПрослеживаемостиПоКлассификатору;
		
		ТаблицаТовары = НовыйДокумент.Товары;
		
		ЗаполнитьЗначенияСвойств(Фильтр, ТекущаяСтрокТНВЭД);
		НайденныеСтроки = ТаблицаВвоза.НайтиСтроки(Фильтр);
		
		Для каждого ТекущаяСтрока Из НайденныеСтроки Цикл
			НоваяСтрока = ТаблицаТовары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		КонецЦикла;
		
		НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьДанныеЗаполнения(Период, Организация)
	
	Результат = Новый Структура("НачальныеДанные");
	
	Результат.НачальныеДанные = ПолучитьНачальныеДанныеДляПомощникаПолученияРНПТ(Период, Организация);

	Возврат Результат;
	
КонецФункции

Функция ПолучитьНачальныеДанныеДляПомощникаПолученияРНПТ(Период, Организация)
	
	Если НЕ ПрослеживаемостьБРУ.ВедетсяУчетПрослеживаемыхТоваров(Период) Тогда
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	Возврат ПолучитьНачальныеДанные(Период, Организация);
	
КонецФункции

Функция ПолучитьНачальныеДанные(Период, Организация) Экспорт
	
//УПП	ПорядокСубконтоКоличество = Новый Массив();
//УПП	ПорядокСубконтоКоличество.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
//УПП	ПорядокСубконтоКоличество.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);
	
//УПП	МассивИсклСчетов = Новый Массив();
//УПП	МассивИсклСчетов.Добавить(ПланыСчетов.Хозрасчетный.ТорговаяНаценка);                            // 42
//УПП	МассивИсклСчетов = БухгалтерскийУчет.СформироватьМассивСубсчетов(МассивИсклСчетов);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("НачалоПериода", ПрослеживаемостьБРУ.ДатаНачалаУчетаПрослеживаемыхТоваров());
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Период));
//УПП	Запрос.УстановитьПараметр("ИсклСчета",                 МассивИсклСчетов);
//УПП	Запрос.УстановитьПараметр("ПорядокСубконтоКоличество", ПорядокСубконтоКоличество);
//УПП	Запрос.УстановитьПараметр("ВестиСкладскойУчетБУ", Истина);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТоварыОрганизацийОстатки.Номенклатура КАК Номенклатура,
	|	0 КАК Сумма,
	|	СУММА(ТоварыОрганизацийОстатки.КоличествоОстаток) КАК Количество,
	|	ТоварыОрганизацийОстатки.Склад КАК Склад
	|ПОМЕСТИТЬ ТаблицаОстатков
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций.Остатки(&Период, Организация = &Организация) КАК ТоварыОрганизацийОстатки
    |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторТНВЭД КАК КлассификаторТНВЭД
	|			ПО СправочникНоменклатура.КодТНВЭД = КлассификаторТНВЭД.Ссылка
    |		ПО ТоварыОрганизацийОстатки.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТоварыОрганизацийОстатки.КоличествоОстаток > 0
	|	И КлассификаторТНВЭД.ПрослеживаемыйТовар
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыОрганизацийОстатки.Номенклатура,
	|	ТоварыОрганизацийОстатки.Склад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегистрацияПрослеживаемыхТоваров.Номенклатура КАК Номенклатура,
	|	СУММА(РегистрацияПрослеживаемыхТоваров.Количество) КАК Количество,
	|	СУММА(РегистрацияПрослеживаемыхТоваров.Сумма) КАК Сумма,
	|	РегистрацияПрослеживаемыхТоваров.Основание КАК ДокументРегистрации,
	|	РегистрацияПрослеживаемыхТоваров.Основание.Склад КАК Склад,
	|	РегистрацияПрослеживаемыхТоваров.ТНВЭД КАК ТНВЭД
	|ПОМЕСТИТЬ ЗарегистрированныеОстаткиПоОснованию
	|ИЗ
	|	РегистрСведений.РегистрацияПрослеживаемыхТоваров КАК РегистрацияПрослеживаемыхТоваров
	|ГДЕ
	|	РегистрацияПрослеживаемыхТоваров.Организация = &Организация
	|	И РегистрацияПрослеживаемыхТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацииПрослеживаемыхТоваров.ИнвентаризацияТоваров)
	|	И РегистрацияПрослеживаемыхТоваров.ПериодСобытия <= КОНЕЦПЕРИОДА(&Период, МЕСЯЦ)
	|
	|СГРУППИРОВАТЬ ПО
	|	РегистрацияПрослеживаемыхТоваров.Номенклатура,
	|	РегистрацияПрослеживаемыхТоваров.Основание,
	|	РегистрацияПрослеживаемыхТоваров.ТНВЭД,
	|	РегистрацияПрослеживаемыхТоваров.Основание.Склад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрослеживаемыеТоварыОбороты.Номенклатура КАК Номенклатура,
	|	СУММА(ПрослеживаемыеТоварыОбороты.КоличествоПриход) КАК КоличествоПриход,
	|	СУММА(ПрослеживаемыеТоварыОбороты.КоличествоРасход) КАК КоличествоРасход,
	|	ПрослеживаемыеТоварыОбороты.Регистратор.Склад КАК Склад,
	|	ПрослеживаемыеТоварыОбороты.РНПТ КАК РНПТ,
	|	ВЫБОР
	|		КОГДА НЕ ПрослеживаемыеТоварыОбороты.Регистратор ССЫЛКА Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоРНПТОтПоставщика
	|ПОМЕСТИТЬ ПрослеживаемыеТоварыСРНПТ
	|ИЗ
	|	РегистрНакопления.ПрослеживаемыеТовары.Обороты(&НачалоПериода, &Период, Регистратор, Организация = &Организация) КАК ПрослеживаемыеТоварыОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ПрослеживаемыеТоварыОбороты.Номенклатура,
	|	ПрослеживаемыеТоварыОбороты.РНПТ,
	|	ВЫБОР
	|		КОГДА НЕ ПрослеживаемыеТоварыОбороты.Регистратор ССЫЛКА Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ПрослеживаемыеТоварыОбороты.Регистратор.Склад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрослеживаемыеТоварыСРНПТ.РНПТ КАК РНПТ
	|ПОМЕСТИТЬ СписокРНПТОтПоставщика
	|ИЗ
	|	ПрослеживаемыеТоварыСРНПТ КАК ПрослеживаемыеТоварыСРНПТ
	|ГДЕ
	|	ПрослеживаемыеТоварыСРНПТ.ЭтоРНПТОтПоставщика = ИСТИНА
	|
	|СГРУППИРОВАТЬ ПО
	|	ПрослеживаемыеТоварыСРНПТ.РНПТ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрослеживаемыеТоварыСРНПТ.Номенклатура КАК Номенклатура,
	|	СУММА(ПрослеживаемыеТоварыСРНПТ.КоличествоПриход - ПрослеживаемыеТоварыСРНПТ.КоличествоРасход) КАК Остаток,
	|	ПрослеживаемыеТоварыСРНПТ.Склад КАК Склад
	|ПОМЕСТИТЬ ТаблицаОстатковПрослеживаемогоТовараСРНПТОтПоставщика
	|ИЗ
	|	ПрослеживаемыеТоварыСРНПТ КАК ПрослеживаемыеТоварыСРНПТ
	|ГДЕ
	|	ПрослеживаемыеТоварыСРНПТ.РНПТ В
	|			(ВЫБРАТЬ
	|				СписокРНПТОтПоставщика.РНПТ
	|			ИЗ
	|				СписокРНПТОтПоставщика КАК СписокРНПТОтПоставщика)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПрослеживаемыеТоварыСРНПТ.Номенклатура,
	|	ПрослеживаемыеТоварыСРНПТ.Склад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОстатковБезОснования.Номенклатура КАК Номенклатура,
	|	СУММА(ЕСТЬNULL(ТаблицаОстатковБезОснования.Количество, 0) - ЕСТЬNULL(ТаблицаОстатковПоОснованию.Количество, 0) - ЕСТЬNULL(ТаблицаОстатковПрослеживаемогоТовараСРНПТОтПоставщика.Остаток, 0)) КАК Количество,
	|	СУММА(ЕСТЬNULL(ТаблицаОстатковБезОснования.Сумма, 0) - ЕСТЬNULL(ТаблицаОстатковПоОснованию.Сумма, 0)) КАК Сумма,
	|	ТаблицаОстатковБезОснования.Склад КАК Склад
	|ПОМЕСТИТЬ ТаблицаОстатковБезОснования
	|ИЗ
	|	ТаблицаОстатков КАК ТаблицаОстатковБезОснования
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЗарегистрированныеОстаткиПоОснованию КАК ТаблицаОстатковПоОснованию
	|		ПО ТаблицаОстатковБезОснования.Номенклатура = ТаблицаОстатковПоОснованию.Номенклатура
	|			И ТаблицаОстатковБезОснования.Склад = ТаблицаОстатковПоОснованию.Склад
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОстатковПрослеживаемогоТовараСРНПТОтПоставщика КАК ТаблицаОстатковПрослеживаемогоТовараСРНПТОтПоставщика
	|		ПО ТаблицаОстатковБезОснования.Номенклатура = ТаблицаОстатковПрослеживаемогоТовараСРНПТОтПоставщика.Номенклатура
	|			И ТаблицаОстатковБезОснования.Склад = ТаблицаОстатковПрослеживаемогоТовараСРНПТОтПоставщика.Склад
	|ГДЕ
	|	ЕСТЬNULL(ТаблицаОстатковБезОснования.Количество, 0) - ЕСТЬNULL(ТаблицаОстатковПоОснованию.Количество, 0) > 0
	|	И ТаблицаОстатковПоОснованию.Количество ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаОстатковБезОснования.Номенклатура,
	|	ТаблицаОстатковБезОснования.Склад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗарегистрированныеОстатки.Номенклатура КАК Номенклатура,
	|	СУММА(ЗарегистрированныеОстатки.Количество) КАК Количество,
	|	СУММА(ЗарегистрированныеОстатки.Сумма) КАК Сумма,
	|	ЗарегистрированныеОстатки.ДокументРегистрации КАК ДокументРегистрации,
	|	НоменклатураСправочник.ЕдиницаХраненияОстатков КАК ЕдиницаИзмерения,
	|	ЗарегистрированныеОстатки.ТНВЭД КАК КодТНВЭД,
	|	НоменклатураСправочник.ЕдиницаИзмеренияПрослеживаемости КАК ЕдиницаПрослеживаемости,
	|	ЗарегистрированныеОстатки.Количество КАК КоличествоПрослеживаеомсти,
	|	ЗарегистрированныеОстатки.Склад КАК Склад,
	|	НоменклатураСправочник.СтранаПроисхождения
	|ПОМЕСТИТЬ ТаблицаОстатковПоОснованию
	|ИЗ
	|	ЗарегистрированныеОстаткиПоОснованию КАК ЗарегистрированныеОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСправочник
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторТНВЭД КАК КлассификаторТНВЭД
	|			ПО НоменклатураСправочник.КодТНВЭД = КлассификаторТНВЭД.Ссылка
	|		ПО ЗарегистрированныеОстатки.Номенклатура = НоменклатураСправочник.Ссылка
	|ГДЕ
	|	ЗарегистрированныеОстатки.Количество > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗарегистрированныеОстатки.Номенклатура,
	|	ЗарегистрированныеОстатки.ДокументРегистрации,
	|	НоменклатураСправочник.ЕдиницаХраненияОстатков,
	|	НоменклатураСправочник.ЕдиницаИзмеренияПрослеживаемости,
	|	ЗарегистрированныеОстатки.Количество,
	|	ЗарегистрированныеОстатки.Склад,
	|	ЗарегистрированныеОстатки.ТНВЭД,
	|	НоменклатураСправочник.СтранаПроисхождения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОстатковБезОснования.Номенклатура КАК Номенклатура,
	|	ТаблицаОстатковБезОснования.Количество КАК Количество,
	|	ТаблицаОстатковБезОснования.Сумма КАК Сумма,
	|	ТаблицаОстатковБезОснования.Склад КАК Склад
	|ПОМЕСТИТЬ ТаблицаОстатковБезОснованияБезИнвентаризации
	|ИЗ
	|	ТаблицаОстатковБезОснования КАК ТаблицаОстатковБезОснования
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЗарегистрированныеОстаткиПоОснованию КАК ЗарегистрированныеОстаткиПоОснованию
	|		ПО ТаблицаОстатковБезОснования.Номенклатура = ЗарегистрированныеОстаткиПоОснованию.Номенклатура
	|			И ТаблицаОстатковБезОснования.Склад = ЗарегистрированныеОстаткиПоОснованию.Склад
	|ГДЕ
	|	ЗарегистрированныеОстаткиПоОснованию.Номенклатура ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОстатковБезОснованияБезИнвентаризации.Номенклатура КАК Номенклатура,
	|	ТаблицаОстатковБезОснованияБезИнвентаризации.Количество КАК Количество,
	|	ТаблицаОстатковБезОснованияБезИнвентаризации.Сумма КАК Сумма,
	|	NULL КАК Основание,
	|	ЛОЖЬ КАК ЕстьПервичныйДокумент,
	|	НоменклатураСправочник.ЕдиницаХраненияОстатков КАК ЕдиницаИзмерения,
	|	НЕОПРЕДЕЛЕНО КАК КодТНВЭД,
	|	НЕОПРЕДЕЛЕНО КАК ЕдиницаПрослеживаемости,
	|	НЕОПРЕДЕЛЕНО КАК КоличествоПрослеживаемости,
	|	ТаблицаОстатковБезОснованияБезИнвентаризации.Склад КАК Склад,
	|	ЛОЖЬ КАК ИмпортИзЕАЭС,
	|	NULL КАК ЕдиницаПоКлассификатору,
	|	NULL КАК ЕдиницаПрослеживаемостиПоКлассификатору,
	|	"""" КАК СтранаПроисхождения,
	|	НоменклатураСправочник.ЕдиницаХраненияОстатков.Коэффициент КАК Коэффициент
	|ИЗ
	|	ТаблицаОстатковБезОснованияБезИнвентаризации КАК ТаблицаОстатковБезОснованияБезИнвентаризации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСправочник
	|		ПО ТаблицаОстатковБезОснованияБезИнвентаризации.Номенклатура = НоменклатураСправочник.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаОстатковПоРегистрации.Номенклатура,
	|	ТаблицаОстатковПоРегистрации.Количество,
	|	ТаблицаОстатковПоРегистрации.Сумма,
	|	ТаблицаОстатковПоРегистрации.ДокументРегистрации,
	|	ИСТИНА,
	|	ТаблицаОстатковПоРегистрации.ЕдиницаИзмерения,
	|	ТаблицаОстатковПоРегистрации.КодТНВЭД,
	|	ТаблицаОстатковПоРегистрации.ЕдиницаПрослеживаемости,
	|	ТаблицаОстатковПоРегистрации.КоличествоПрослеживаеомсти * ТаблицаОстатковПоРегистрации.ЕдиницаИзмерения.Коэффициент,
	|	ТаблицаОстатковПоРегистрации.Склад,
	|	ЛОЖЬ,
	|	ТаблицаОстатковПоРегистрации.ЕдиницаИзмерения.ЕдиницаПоКлассификатору,
	|	ТаблицаОстатковПоРегистрации.ЕдиницаПрослеживаемости.ЕдиницаПоКлассификатору,
	|	ТаблицаОстатковПоРегистрации.СтранаПроисхождения,
	|	ТаблицаОстатковПоРегистрации.ЕдиницаИзмерения.Коэффициент
	|ИЗ
	|	ТаблицаОстатковПоОснованию КАК ТаблицаОстатковПоРегистрации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РегистрацияПрослеживаемыхТоваров.Номенклатура,
	|	РегистрацияПрослеживаемыхТоваров.Количество,
	|	РегистрацияПрослеживаемыхТоваров.Сумма,
	|	РегистрацияПрослеживаемыхТоваров.Основание,
	|	ИСТИНА,
	|	РегистрацияПрослеживаемыхТоваров.ЕдиницаИзмерения,
	|	РегистрацияПрослеживаемыхТоваров.ТНВЭД,
	|	НоменклатураСправочник.ЕдиницаИзмеренияПрослеживаемости,
	|	РегистрацияПрослеживаемыхТоваров.КоличествоПрослеживаемости,
	|	НЕОПРЕДЕЛЕНО,
	|	ИСТИНА,
	|	РегистрацияПрослеживаемыхТоваров.ЕдиницаИзмерения.ЕдиницаПоКлассификатору,
	|	НоменклатураСправочник.ЕдиницаИзмеренияПрослеживаемости.ЕдиницаПоКлассификатору,
	|	РегистрацияПрослеживаемыхТоваров.СтранаПроисхождения,
	|	РегистрацияПрослеживаемыхТоваров.ЕдиницаИзмерения.Коэффициент
	|ИЗ
	|	РегистрСведений.РегистрацияПрослеживаемыхТоваров КАК РегистрацияПрослеживаемыхТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСправочник
	|		ПО РегистрацияПрослеживаемыхТоваров.Номенклатура = НоменклатураСправочник.Ссылка
	|ГДЕ
	|	РегистрацияПрослеживаемыхТоваров.Количество > 0
	|	И РегистрацияПрослеживаемыхТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацииПрослеживаемыхТоваров.ВвозТоваровИзЕАЭС)
	|	И РегистрацияПрослеживаемыхТоваров.Организация = &Организация";
	
	Запрос.Текст = ТекстЗапроса;
	
	Результат = Запрос.Выполнить();
	
	Возврат Результат.Выгрузить();
	
КонецФункции

#КонецОбласти

#КонецЕсли
