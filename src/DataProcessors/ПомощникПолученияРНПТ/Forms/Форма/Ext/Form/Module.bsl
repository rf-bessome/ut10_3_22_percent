
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Организация",      Объект.Организация);
	Параметры.Свойство("ПериодСобытия",    Объект.Период);
	
	ДатаНачалаУчетаПрослеживаемыхТоваров = ПрослеживаемостьБРУ.ДатаНачалаУчетаПрослеживаемыхТоваров();
	
	Если Параметры.КонтекстныйВызов Тогда
		ПредставлениеПериода = ПредставлениеПериода(
			НачалоМесяца(Объект.Период), КонецМесяца(Объект.Период), Истина);
		Заголовок = СтрШаблон(НСтр("ru = 'Помощник получения РНПТ за %1'"), ПредставлениеПериода);
		Элементы.ПредставлениеПериода.Видимость = Ложь;
		// Скрываем поле ввода, оставляем гиперссылку
		Элементы.Организация.Видимость = Ложь;
		Элементы.ОрганизацияПредставление.Видимость = Истина;
		
	Иначе
		Заголовок = НСтр("ru = 'Помощник получения РНПТ'");
		Элементы.ПредставлениеПериода.Видимость = Истина;
		Если Не ЗначениеЗаполнено(Объект.Период) Тогда
			
			Объект.Период = ?(ДатаНачалаУчетаПрослеживаемыхТоваров < НачалоМесяца(ТекущаяДатаСеанса()), 
			НачалоМесяца(ТекущаяДатаСеанса()), ДатаНачалаУчетаПрослеживаемыхТоваров);
			
			ПредставлениеПериода = ПредставлениеПериода(
				НачалоМесяца(Объект.Период), КонецМесяца(Объект.Период), Истина);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
			Объект.Организация = 
				УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(
					глЗначениеПеременной("глТекущийПользователь"), "ОсновнаяОрганизация"
				);
		КонецЕсли;
		// Скрываем гиперссылку в зависимости от ФО
		Элементы.ОрганизацияПредставление.Видимость = Ложь;
		Элементы.Организация.Видимость = Истина;
	КонецЕсли;
	
	Объект.Период = НачалоМесяца(Объект.Период);
	
	// Инициализируем цвета
	Желтый = Новый Цвет(251, 225, 81);
	Серый  = Новый Цвет(242, 242, 242);
	
	УстановитьПараметрыРазделов();
	
	ПодготовитьФормуНаСервере();
	
	УстановитьТекущуюСтраницу(ЭтаФорма);

	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.СтраницыПомощник.ТекущаяСтраница = Элементы.СтраницаПустая;
	ПолучитьДанныеДляЗаполнения();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_УведомлениеОбОстаткахПрослеживаемыхТоваров"
		ИЛИ ИмяСобытия = "Запись_УведомлениеОВвозеПрослеживаемыхТоваров" Тогда
		
		Элементы.Уведомления.Обновить();
		ОбновитьНавигациюПоРазделамНаСервере(Истина);
		УправлениеФормой(ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОбновитьДанныеПомощника(Истина);
	ОбновитьСпискиНаСервере();
	ПерейтиНаСледующийАктуальныйШаг();
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыВыбора = Новый Структура;
	ПараметрыВыбора.Вставить("НачалоПериода", НачалоМесяца(Объект.Период));
	ПараметрыВыбора.Вставить("КонецПериода",  КонецМесяца(Объект.Период));
	ПараметрыВыбора.Вставить("ВидПериода",    ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Месяц"));
	ПараметрыВыбора.Вставить("ОграничениеСнизу", ДатаНачалаУчетаПрослеживаемыхТоваров);
		
	Оповещение = Новый ОписаниеОповещения("ПериодЗавершениеВыбора", ЭтаФорма);
	ПараметрыФормыВыбораПериода = Новый Структура("Значение", Объект.Период);
	
	ОткрытьФорму("ОбщаяФорма.ВыборПериодаИС",
		ПараметрыФормыВыбораПериода,
		ЭтаФорма,
		ЭтаФорма.УникальныйИдентификатор,
		,
		,
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаКлиенте
Процедура ПериодЗавершениеВыбора(ВыбранныйПериод, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйПериод <> Неопределено Тогда
		
		Объект.Период = НачалоМесяца(ВыбранныйПериод);
		ПредставлениеПериода = ПолучитьПредставлениеПериода(ВыбранныйПериод);
		
		ОбновитьСпискиНаСервере();
		ОбновитьДанныеПомощника(Истина);
		ПерейтиНаСледующийАктуальныйШаг();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		НачалоПериода = ВыбранноеЗначение.НачалоПериода;
		КонецПериода = ВыбранноеЗначение.КонецПериода;
	Иначе
		НачалоПериода = ВыбранноеЗначение;
		КонецПериода = ВыбранноеЗначение;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПредставлениеПериодаНачалоВыбораЗавершение", ЭтаФорма);
	ПараметрыФормыВыбораПериода = Новый Структура("Значение", Объект.Период);
	
	ОткрытьФорму("ОбщаяФорма.ВыборПериодаИС",
		ПараметрыФормыВыбораПериода,
		ЭтаФорма,
		ЭтаФорма.УникальныйИдентификатор,
		,
		,
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	ОбновитьСпискиНаСервере();
	
	ОбновитьДанныеПомощника(Истина);
	ПерейтиНаСледующийАктуальныйШаг();
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодаНачалоВыбораЗавершение(ВыбранныйПериод, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйПериод <> Неопределено Тогда
		
		Объект.Период = ВыбранныйПериод;
		ПредставлениеПериода = ПолучитьПредставлениеПериода(ВыбранныйПериод);
		
		ОбновитьСпискиНаСервере();
		ОбновитьДанныеПомощника(Истина);
		ПерейтиНаСледующийАктуальныйШаг();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьПредставлениеПериода(Период)
	
	Возврат Формат(Период, "ДФ='ММММ гггг'");
	
КонецФункции

&НаКлиенте
Процедура СоздатьИнвентаризации(Команда)
	
	НачатьСозданиеИнвентаризаций();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьУведомления(Команда)
	
	НачатьСозданиеУведомлений();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЖурналУведомлений(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КонтекстныйВызов", Истина);
	ПараметрыФормы.Вставить("ДатаБольшеИлиРавно", НачалоМесяца(Объект.Период));
	ПараметрыФормы.Вставить("ДатаменьшеИлиРавно", КонецМесяца(Объект.Период));
	ПараметрыФормы.Вставить("Организация", Объект.Организация);
	
	ПараметрыФормы.Вставить("ОтобратьУведомленияСРНПТ", Ложь);
	
	ОткрытьФорму("ЖурналДокументов.ПрослеживаемостьУведомления.Форма.ФормаСпискаРНПТ", ПараметрыФормы, ЭтаФорма, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаНажатие(Элемент)
	
	НовыйШаг = Элемент.Имя;
	Если НовыйШаг <> ТекущийШаг Тогда
		
		ПредыдущаяГиперссылка = ТекущийРазделФормы(ЭтаФорма);
		ТекущаяГиперссылка    = Элемент;
	
		ПриПереключенииРазделаСервер(ПредыдущаяГиперссылка.Имя, ТекущаяГиперссылка.Имя);
		
		ПриИзмененииРаздела(НовыйШаг);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНачальныеДанные

&НаКлиенте
Процедура НачальныеДанныеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ДанныеСтроки = Элемент.ДанныеСтроки(ВыбраннаяСтрока);
	ПараметрыФормы = Новый Структура();
	
	Если Поле.Имя = "НачальныеДанныеПредставлениеОснование" Тогда
		
		ИмяДокументаОснования = ПрослеживаемостьФормыВызовСервера.ИмяДокумента(ДанныеСтроки.Основание);

		ПараметрыФормы.Вставить("Ключ", ДанныеСтроки.Основание);
		ОткрытьФорму("Документ."+ ИмяДокументаОснования +".ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУведомления

&НаКлиенте
Процедура УведомленияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ДанныеСтроки = Элемент.ДанныеСтроки(ВыбраннаяСтрока);
	ПараметрыФормы = Новый Структура();
	
	Если Поле.Имя = "УведомленияПредставлениеОснование" Тогда
		
		ИмяДокументаОснования = ПрослеживаемостьФормыВызовСервера.ИмяДокумента(ДанныеСтроки.Основание);
		
		ПараметрыФормы.Вставить("Ключ", ДанныеСтроки.Основание);
		ОткрытьФорму("Документ."+ ИмяДокументаОснования +".ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
		
	ИначеЕсли Поле.Имя = "УведомленияПредставлениеУведомление" Тогда
		
		ПараметрыФормы.Вставить("Ключ", ДанныеСтроки.Уведомление);
		Если ТипЗнч(ДанныеСтроки.Уведомление) = Тип("ДокументСсылка.УведомлениеОбОстаткахПрослеживаемыхТоваров") Тогда
			ОткрытьФорму("Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
		Иначе
			 ОткрытьФорму("Документ.УведомлениеОВвозеПрослеживаемыхТоваров.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТоварыПоУведомлениям

&НаКлиенте
Процедура УведомленияПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.Уведомления.ТекущиеДанные;
	
	ОбновитьТаблицуТоварыПоУведомлениям();
	
	Если ТекущиеДанные = Неопределено Тогда
		Элементы.ДекорацияТНВЭДНаименование.Заголовок = "";
		Возврат;
	КонецЕсли;
	
	Элементы.ДекорацияТНВЭДНаименование.Заголовок = ОписаниеТНВЭД(ТекущиеДанные.ТНВЭД);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОписаниеТНВЭД(ТНВЭД)
	
	ОписаниеТНВЭД = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТНВЭД, "Наименование");

	ТекстТНВЭД = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='ТН ВЭД: %1 %2'"), ТНВЭД, ОписаниеТНВЭД);
	
	Возврат ТекстТНВЭД;
КонецФункции

&НаСервереБезКонтекста
Процедура ТоварыПоУведомлениямПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	ЗаполненоОКПД2 = Ложь;
	Для каждого ТекущаяСтрока Из Строки Цикл
		
		Данные = ТекущаяСтрока.Значение.Данные;
		
		Если ЗначениеЗаполнено(Данные.КодОКПД2) Тогда
			
			ЗаполненоОКПД2 = Истина;
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для каждого ТекущаяСтрока Из Строки Цикл
		
		Данные = ТекущаяСтрока.Значение.Данные;
		
		Данные.ЗаполненоОКПД2 = ЗаполненоОКПД2;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьДанныеПомощника(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция СтруктураОписанияШага()
	
	Возврат Новый Структура("Подсказка, ВысотаПодсказки",,1);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьОписаниеШагаСтруктурой(МассивСтрок, Высота)
	
	ОписаниеШага = СтруктураОписанияШага();
	
	ОписаниеШага.Подсказка = Новый ФорматированнаяСтрока(МассивСтрок);
	ОписаниеШага.ВысотаПодсказки = 2;
	
	Возврат ОписаниеШага;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьОписаниеШага(Раздел)
	
	РазделОписаниеРаздела = Новый Соответствие();
	
	МассивСтрок = Новый Массив;
	МассивСтрок.Добавить(НСтр("ru='Остатки прослеживаемого товара, по которым необходимо провести инвентаризацию товаров'"));
	МассивСтрок.Добавить(Символы.ПС);
	МассивСтрок.Добавить(НСтр("ru='Чтобы автоматически создать инвентаризацию товаров, нажмите на кнопку Провести инвентаризацию'"));
	РазделОписаниеРаздела.Вставить("ПровестиИнвентаризацию", ПолучитьОписаниеШагаСтруктурой(МассивСтрок, 2));
	
	МассивСтрок = Новый Массив;
	МассивСтрок.Добавить(НСтр("ru='Остатки прослеживаемого товара, по которым необходимо создать уведомления. Для создания уведомлений, нажмите на кнопку Создать уведомления'") +
	     Символы.ПС +  НСтр("ru='Если остаток товара был в собственности физического лица, плательщика НПД или государственных органов, то необходимо заполнить дополнительные сведения в уведомлении'"));
	РазделОписаниеРаздела.Вставить("СоздатьУведомления", ПолучитьОписаниеШагаСтруктурой(МассивСтрок, 1));
	
	Возврат РазделОписаниеРаздела[Раздел];
	
КонецФункции

&НаКлиенте
Процедура ПоказатьФормуСтатусовОтправкиИзСписка(УведомлениеСсылка)
	
	Если НЕ ЗначениеЗаполнено(УведомлениеСсылка) Тогда
		Возврат;
	КонецЕсли;
	
	ЦиклОбмена = КонтекстЭДО.ПолучитьПоследнийЦиклОбмена(УведомлениеСсылка);
	Если НЕ ЗначениеЗаполнено(ЦиклОбмена) Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьЗначение(ЦиклОбмена);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСпискиНаСервере()
	
	УстановитьПараметрыРазделов();
	
КонецПроцедуры

&НаСервере 
Процедура УстановитьПараметрыРазделов()

	Список = ЭтаФорма["Уведомления"];
	
	ТекстЗапроса = "";
	ПрослеживаемостьБРУ.ТекстЗапросаДинамическогоСпискаУведомления(ТекстЗапроса);
	Если ЗначениеЗаполнено(ТекстЗапроса) Тогда
		СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
		СвойстваСписка.ТекстЗапроса = ТекстЗапроса;
		ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.Уведомления, СвойстваСписка);
	КонецЕсли;
	
	// todo
	Список.Параметры.УстановитьЗначениеПараметра("НачалоПериода", НачалоМесяца(Объект.Период));
	Список.Параметры.УстановитьЗначениеПараметра("КонецПериода", КонецМесяца(Объект.Период));
	Список.Параметры.УстановитьЗначениеПараметра("Организация", Объект.Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьСозданиеИнвентаризаций()
	
	ЗапущеноСозданиеИнвентаризаций = Истина;
	ОбновитьТекстОжиданияВыполненияДействия();
	ПоказатьКомандыРаздела(ТекущийШаг);
	
	ДлительнаяОперация = СоздатьИнвентаризацииНаСервереВФоне();
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус <> "Ошибка" Тогда
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтаФорма);
		НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
		
		Обработчик = Новый ОписаниеОповещения("ПослеСозданияИнвентаризацийОбОстатках", ЭтаФорма);
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
	Иначе
		ПоказатьОшибкуСозданияИнвентаризацийОбОстатках(ДлительнаяОперация);
		УправлениеФормой(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьСозданиеУведомлений()
	
	ЗапущеноСозданиеУведомлений = Истина;
	ОбновитьТекстОжиданияВыполненияДействия();
	ПоказатьКомандыРаздела(ТекущийШаг);
	
	ДлительнаяОперация = СоздатьУведомленияНаСервереВФоне();
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус <> "Ошибка" Тогда
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтаФорма);
		НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
		
		Обработчик = Новый ОписаниеОповещения("ПослеСозданияУведомлений", ЭтаФорма);
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
	Иначе
		ПоказатьОшибкуСозданияУведомленийОбОстатках(ДлительнаяОперация);
		УправлениеФормой(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьСписокУведомленийДляОтправкиВФНС()
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	Если СравнитьВерсии(СистемнаяИнформация.ВерсияПриложения, "8.3.6.0") < 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Для выполнения операции требуется версия платформы не ниже 8.3.6'"));
		Возврат Ложь;
	КонецЕсли;
	
	Схема = Элементы.Уведомления.ПолучитьИсполняемуюСхемуКомпоновкиДанных();
	Настройки = Элементы.Уведомления.ПолучитьИсполняемыеНастройкиКомпоновкиДанных();
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных();
	ИмяТипаГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений = "ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений";
	МакетКомпоновки =
		КомпоновщикМакета.Выполнить(Схема, Настройки, , ,Тип(ИмяТипаГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений));
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ТаблицаРезультат = ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	// выгружаем ссылки в массив для передачи клиенту
	МассивСсылок = ТаблицаРезультат.ВыгрузитьКолонку("Уведомление"); 
	
	УведомленияСОшибками = ПроверитьУведомленияНаОшибки(МассивСсылок);
	
	УведомленияДляОтправкиВФНС.Очистить();
	
	Для каждого ТекущeeЗначение Из МассивСсылок Цикл
		
		Если УведомленияСОшибками.Найти(ТекущeeЗначение) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
			
			
		УведомленияДляОтправкиВФНС.Добавить(ТекущeeЗначение);
		
	КонецЦикла;
	
	Возврат УведомленияСОшибками;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПроверитьУведомленияНаОшибки(МассивСсылок)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.Товары КАК УведомлениеОбОстаткахПрослеживаемыхТоваровТовары
	|ГДЕ
	|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Ссылка В(&МассивДокументов)
	|	И УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.СтранаПроисхождения = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УведомлениеОВвозеПрослеживаемыхТоваровТовары.Ссылка
	|ИЗ
	|	Документ.УведомлениеОВвозеПрослеживаемыхТоваров.Товары КАК УведомлениеОВвозеПрослеживаемыхТоваровТовары
	|ГДЕ
	|	УведомлениеОВвозеПрослеживаемыхТоваровТовары.Ссылка В(&МассивДокументов)
	|	И УведомлениеОВвозеПрослеживаемыхТоваровТовары.СтранаПроисхождения = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.ПустаяСсылка)";
	
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивСсылок);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Если Результат.Количество() > 0 Тогда
		
		Возврат Результат.ВыгрузитьКолонку("Ссылка");
		
	КонецЕсли;
	
	Возврат Новый Массив;
	
КонецФункции

&НаСервереБезКонтекста
Функция СравнитьВерсии(Знач СтрокаВерсии1, Знач СтрокаВерсии2) Экспорт
	
	Версия1 = ОбщегоНазначения.РазложитьСтрокуВМассивПодстрок(СтрокаВерсии1, ".");
	Если Версия1.Количество() <> 4 Тогда
		ВызватьИсключение НСтр("ru = 'Неправильный формат строки версии'") + ": " + СтрокаВерсии1;
	КонецЕсли;
	Версия2 = ОбщегоНазначения.РазложитьСтрокуВМассивПодстрок(СтрокаВерсии2, ".");
	Если Версия2.Количество() <> 4 Тогда
		ВызватьИсключение НСтр("ru = 'Неправильный формат строки версии'") + ": " + СтрокаВерсии2;
	КонецЕсли;
	
	Результат = 0;
	Для Разряд = 0 По 3 Цикл
		Результат = Число(Версия1[Разряд]) - Число(Версия2[Разряд]);
		Если Результат <> 0 Тогда
			Возврат Результат;
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция НомерВыделеннойСтроки(ЭлементТаблицыФормы)
	
	Если ЭлементТаблицыФормы.ТекущаяСтрока = Неопределено Тогда
		Возврат 0;
	КонецЕсли;
	
	СписокСтрок = Новый СписокЗначений;
	СписокСтрок.ЗагрузитьЗначения(ЭлементТаблицыФормы.ВыделенныеСтроки);
	СписокСтрок.СортироватьПоЗначению(НаправлениеСортировки.Возр);
	ЭлементСписка = СписокСтрок.НайтиПоЗначению(ЭлементТаблицыФормы.ТекущаяСтрока);
	Если ЭлементСписка = Неопределено Тогда
		Возврат 0;
	КонецЕсли;
	
	Возврат СписокСтрок.Индекс(ЭлементСписка) + 1;
	
Конецфункции

&НаКлиенте
Процедура ПослеСозданияИнвентаризацийОбОстатках(ДлительнаяОперация, ДополнительныеПараметры) Экспорт

	Если ДлительнаяОперация = Неопределено Тогда
		Возврат; // Отменено
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		
		СозданныеПервичныеДокументы.ЗагрузитьЗначения(ПолучитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата));
		
		ЗапущеноСозданиеИнвентаризаций = Ложь;
		
		ОбновитьДанныеПомощника(Ложь, Истина);
		
		ПерейтиНаСледующийАктуальныйШаг();
		
		УправлениеФормой(ЭтаФорма);
		
	Иначе
		ПоказатьОшибкуПолученияДанных(ДлительнаяОперация);
		УправлениеФормой(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СоздатьИнвентаризацииНаСервереВФоне()
	
	ПараметрыОбработчика = ПараметрыСозданияИнвентаризацийОбОстатках();
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Создание инвентаризаций об остатках'");
	НастройкиЗапуска.ОжидатьЗавершение = 0;
	НастройкиЗапуска.ЗапуститьВФоне = Истина;
	
	НастроитьДлительнуюОперацию(НастройкиЗапуска);
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.ПомощникПолученияРНПТ.СоздатьИнвентаризациюОбОстаткахВФоне",
		ПараметрыОбработчика,
		НастройкиЗапуска);
	
	Возврат ДлительнаяОперация;
	
КонецФункции

&НаСервере
Функция СоздатьУведомленияНаСервереВФоне()
	
	ПараметрыОбработчика = ПараметрыСозданияУведомлений();
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Создание уведомлений'");
	НастройкиЗапуска.ОжидатьЗавершение = 0;
	НастройкиЗапуска.ЗапуститьВФоне = Истина;
	
	НастроитьДлительнуюОперацию(НастройкиЗапуска);
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.ПомощникПолученияРНПТ.СоздатьУведомленияВФоне",
		ПараметрыОбработчика,
		НастройкиЗапуска);
	
	Возврат ДлительнаяОперация;
	
КонецФункции

&НаСервере
Функция ЗаписьРНПТНаСервереВФоне()
	
	ПараметрыОбработчика = ПараметрыЗаписиРНПТ();
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Запись РНПТ'");
	НастройкиЗапуска.ОжидатьЗавершение = 0;
	НастройкиЗапуска.ЗапуститьВФоне = Истина;
	
	НастроитьДлительнуюОперацию(НастройкиЗапуска);
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.ПомощникПолученияРНПТ.ЗаписьПолученныхРНПТВФоне",
		ПараметрыОбработчика,
		НастройкиЗапуска);
	
	Возврат ДлительнаяОперация;
	
КонецФункции

&НаСервере
Функция ПараметрыЗаписиРНПТ()
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ПериодСобытия",   Объект.Период);
	СтруктураПараметров.Вставить("Организация",     Объект.Организация);

	Возврат СтруктураПараметров;
	
КонецФункции

&НаСервере
Функция ПараметрыСозданияИнвентаризацийОбОстатках()
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ПериодСобытия",   Объект.Период);
	СтруктураПараметров.Вставить("Дата",             Объект.Период);
	СтруктураПараметров.Вставить("Организация",     Объект.Организация);
	СтруктураПараметров.Вставить("ТаблицаОстатков",  
		Объект.НачальныеДанные.Выгрузить(Новый Структура("ЕстьПервичныйДокумент", Ложь)));

	Возврат СтруктураПараметров;
	
КонецФункции

&НаСервере
Функция ПараметрыСозданияУведомлений()
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ПериодСобытия",   Объект.Период);
	СтруктураПараметров.Вставить("Дата",            ТекущаяДатаСеанса());
	СтруктураПараметров.Вставить("Организация",     Объект.Организация);
	СтруктураПараметров.Вставить("ТаблицаОстатков",  
		Объект.НачальныеДанные.Выгрузить(Новый Структура("ЕстьПервичныйДокумент, ИмпортИзЕАЭС", Истина, Ложь)));
	СтруктураПараметров.Вставить("ТаблицаВвоза",  
		Объект.НачальныеДанные.Выгрузить(Новый Структура("ЕстьПервичныйДокумент, ИмпортИзЕАЭС", Истина, Истина)));
		
	Возврат СтруктураПараметров;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьОшибкуСозданияИнвентаризацийОбОстатках(ДлительнаяОперация)
	
	МассивИзДвухСтрок = Новый Массив;
	МассивИзДвухСтрок.Добавить(НСтр("ru = 'Ошибка при создании инвентаризаций..'"));
	МассивИзДвухСтрок.Добавить(ДлительнаяОперация.КраткоеПредставлениеОшибки);
	
	ВызватьИсключение СтрСоединить(МассивИзДвухСтрок, Символы.ПС);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОшибкуСозданияУведомленийОбОстатках(ДлительнаяОперация)
	
	МассивИзДвухСтрок = Новый Массив;
	МассивИзДвухСтрок.Добавить(НСтр("ru = 'Ошибка при создании уведомлений..'"));
	МассивИзДвухСтрок.Добавить(ДлительнаяОперация.КраткоеПредставлениеОшибки);
	
	ВызватьИсключение СтрСоединить(МассивИзДвухСтрок, Символы.ПС);
	
КонецПроцедуры

&НаКлиенте
Функция ОбновитьСтатусФоновогоЗадания()

	ЧастиТекста = Новый Массив;
	
	ЧастиТекста.Добавить(НСтр("ru = 'Выполняется получение данных...'"));
	
	СтатусФоновогоЗадания =  Новый ФорматированнаяСтрока(ЧастиТекста);
	
КонецФункции

&НаКлиенте
Процедура ПолучитьДанныеДляЗаполнения()
	
	ОбновитьДанныеПомощника();
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	УстановитьУсловноеОформление();
	
	Элементы.ПанельСписков.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	
	ИнициализироватьРазделы();
	
	НастроитьСпискиРазделов();
	
	ВестиСкладскойУчетБУ = Истина;
	
	ОбновитьНавигациюПоРазделамНаСервере(Истина);

КонецПроцедуры

&НаСервере
Процедура НастроитьСпискиРазделов()
	
	СоздатьОтборПоРазделамВСпискахРазделов();

КонецПроцедуры

&НаСервере 
Процедура СоздатьОтборПоРазделамВСпискахРазделов()
	
	Список = Уведомления;
	
	ГруппаОтборПоРазделам = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
		Список.Отбор.Элементы, "ОтборПоРазделам", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
		
	Разделы = Новый Массив;
		
	Для каждого Раздел Из Разделы Цикл
		
		ГруппаРаздел = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
			ГруппаОтборПоРазделам, "Раздел" + Раздел, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
			
		СоздатьОтборПоРазделу(Раздел, ГруппаРаздел);
		
	КонецЦикла;

КонецПроцедуры

&НаСервереБезКонтекста 
Процедура СоздатьОтборПоРазделу(Знач Раздел, ГруппаОтбора)

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьТекущуюСтраницу(Форма)
	
	Элементы = Форма.Элементы;

	Если Форма.ЗапущенноОбновлениеДанных Тогда
		
		Элементы.СтраницыПомощник.ТекущаяСтраница = Элементы.СтраницаПустая;
		
	ИначеЕсли Форма.ДанныеПолучены Тогда
		
		Элементы.СтраницыПомощник.ТекущаяСтраница = Элементы.СтраницаРабочая;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	// НачальныеДанныеСклад
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	ДобавитьОформляемоеПоле(ЭлементУО.Поля, "НачальныеДанныеСклад");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ВестиСкладскойУчетБУ", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// НачальныеДанныеНоменклатура
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	ДобавитьОформляемоеПоле(ЭлементУО.Поля, "НачальныеДанныеНоменклатура");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"НачальныеДанные.ЭтоНовыйДокумент", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ГиперссылкаЦвет);

	// ТоварыПоУведомлениямЕдиницаИзмерения, ТоварыПоУведомлениямЕдиницаПрослеживаемости
	// , ТоварыПоУведомлениямКоличествоПрослеживаемости
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыПоУведомлениямЕдиницаИзмерения");
	ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыПоУведомлениямЕдиницаПрослеживаемости");
	ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыПоУведомлениямКоличествоПрослеживаемости");
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ТоварыПоУведомлениям.ЕдиницыИзмеренияОдинаковы", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);

	// ТоварыПоУведомлениямКодОКПД2
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыПоУведомлениямКодОКПД2");
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ТоварыПоУведомлениям.ЗаполненоОКПД2", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// УведомленияСтатус
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УведомленияСтатус");
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Уведомления.Статус", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СтатусыОтправки.НеПринят);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийОшибкуТекст);
	
КонецПроцедуры

&НаСервере
// Добавляет в коллекцию оформляемых полей компоновки данных новое поле
//
// Параметры:
//	КоллекцияОформляемыхПолей 	- коллекция оформляемых полей КД
//	ИмяПоля						- Строка - имя поля
//
// Возвращаемое значение:
//	ОформляемоеПолеКомпоновкиДанных - созданное поле
//
// Пример:
// 	Форма.УсловноеОформление.Элементы[0].Поля
//
Функция ДобавитьОформляемоеПоле(КоллекцияОформляемыхПолей, ИмяПоля)
	
	ПолеЭлемента 		= КоллекцияОформляемыхПолей.Элементы.Добавить();
	ПолеЭлемента.Поле 	= Новый ПолеКомпоновкиДанных(ИмяПоля);

	Возврат ПолеЭлемента;
	
КонецФункции

&НаСервере 
Процедура ОбновитьНавигациюПоРазделамНаСервере(Знач ПересчитатьКоличество = Истина, Знач ПроверитьШагОжиданияОтвета = Ложь)
	
	РассчитатьКоличествоЭлементовВРазделах(ПересчитатьКоличество);
	
	ОбновитьМенюНавигации();
	ОбновитьДоступностьКоманд();
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьКоличествоЭлементовВРазделах(Знач ПересчитатьКоличество = Истина)
	
	Если Не (ПересчитатьКоличество) Тогда
		Возврат;
	КонецЕсли;
	
	Форма = ЭтаФорма;
	
	ДанныеДляРасчета = Новый Массив;
	
	Для каждого Раздел Из ОписаниеРазделов Цикл
		
		Если Не (Раздел.РассчитыватьКоличество И Раздел.Видимость) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ПересчитатьКоличество Тогда
			ЭлементРасчета = Новый Структура("Раздел, УчитыватьОтбор, Количество", Раздел.Имя, Ложь, 0);
			ДанныеДляРасчета.Добавить(ЭлементРасчета);
		КонецЕсли;
		
	КонецЦикла;
	
	ВыполнитьРасчетКоличестваЭлементовВРазделах(ДанныеДляРасчета);
	
	Для каждого ЭлементРасчета Из ДанныеДляРасчета Цикл
		
		ОтборРаздела = Новый Структура("Имя", ЭлементРасчета.Раздел);
		ВсеСтрокиРаздела = ОписаниеРазделов.НайтиСтроки(ОтборРаздела);
		
		Для каждого СтрокаРаздела Из ВсеСтрокиРаздела Цикл
			
			Если ЭлементРасчета.УчитыватьОтбор Тогда
				СтрокаРаздела.КоличествоОтбор = ЭлементРасчета.Количество;
			Иначе
				СтрокаРаздела.Количество = ЭлементРасчета.Количество;
				СтрокаРаздела.КоличествоОтбор = ЭлементРасчета.Количество;
			КонецЕсли;
			
			СтрокаРаздела.КоличествоРассчитано = Истина;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ОбновитьМенюНавигации() 
	
	КОбработке = 0;
	
	Для каждого Раздел Из ОписаниеРазделов Цикл
		
		Элементы[Раздел.Имя].Заголовок = Раздел.Представление;
		Элементы[Раздел.Имя].Шрифт = Новый Шрифт(); 
		
		Если Раздел.Видимость Тогда
			КОбработке = КОбработке + Раздел.Количество;
			
			ВыделятьПолужирнымШрифтом = (Раздел.Количество > 0);
			
			Если ВыделятьПолужирнымШрифтом  Тогда
				
				Если (Раздел.Имя = "ОжиданиеОтветаОтФНС" И Не ПросмотренШагОжиданиеОтветаОтФНС)
					ИЛИ Раздел.Имя <> "ОжиданиеОтветаОтФНС" Тогда
				
					Элементы[Раздел.Имя].Шрифт = Новый Шрифт(ШрифтыСтиля.ОбычныйШрифтТекста, , , Истина, Ложь, Ложь, Ложь, );
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если Раздел.РассчитыватьКоличество И Раздел.КоличествоРассчитано И Раздел.Количество > 0 Тогда
			
			Представление= Раздел.Представление;
			КоличествоОтборСтрокой = ?(Раздел.КоличествоОтбор >= 1000, "999+", Формат(Раздел.КоличествоОтбор, "ЧН=0; ЧГ=0"));
			Представление = Представление + " (" + КоличествоОтборСтрокой;
			
			Если Раздел.Количество <> Раздел.КоличествоОтбор Тогда
				
				КоличествоСтрокой = ?(Раздел.Количество >= 1000, "999+", Формат(Раздел.Количество, "ЧН=0; ЧГ=0"));
				Представление = Представление + "/" + КоличествоСтрокой;
				
			КонецЕсли;
			
			Представление = Представление + ")";
			
			Элементы[Раздел.Имя].Заголовок = Представление;
			
		КонецЕсли;
			
	КонецЦикла;
	
	Возврат КОбработке;
	
КонецФункции

&НаСервере
Процедура ОбновитьДоступностьКоманд()
	
	КомандыРазделов = ПолучитьКомандыРазделов();
	
	НедоступныеКоманды.Очистить();
	
	Для каждого ТекущаяСтрока Из ОписаниеРазделов Цикл
		
		СтрКоманды = КомандыРазделов.Получить(ТекущаяСтрока.Имя);
		МассивКоманд = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрКоманды, ",");
		КоличествоСтрокВТекущемШаге = ТекущаяСтрока.Количество;
		
		Если КоличествоСтрокВТекущемШаге <> 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Для каждого ТекущаяКоманда Из МассивКоманд Цикл
			Если ТекущаяКоманда = "Обновить"
				ИЛИ ТекущаяКоманда = "ОткрытьЖурналУведомлений"
				ИЛИ ТекущаяКоманда = "ПоискУведомления"
				ИЛИ ТекущаяКоманда = "ОтменитьПоискУведомления"
				ИЛИ ТекущаяКоманда = "ПоискНачальныеДанные"
				ИЛИ ТекущаяКоманда = "ОтменитьПоискНачальныеДанные" Тогда 
				Продолжить;
			КонецЕсли;
			
			НедоступныеКоманды.Добавить(ТекущаяКоманда);
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьРасчетКоличестваЭлементовВРазделах(ДанныеДляРасчета)
	
	Если Не ЗначениеЗаполнено(ДанныеДляРасчета) Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого ЭлементРасчета Из ДанныеДляРасчета Цикл
		
		НазваниеТаблицы = СписокРаздела(ЭлементРасчета.Раздел);
		
		Если НазваниеТаблицы = "НачальныеДанные" Тогда
			
			ТаблицаДанных = Объект[НазваниеТаблицы];
			
			Фильтр = Новый Структура("ЕстьПервичныйДокумент", ЭлементРасчета.Раздел = "СоздатьУведомления");
			
			НайденныеСтроки = ТаблицаДанных.НайтиСтроки(Фильтр);
			ЭлементРасчета.Количество = НайденныеСтроки.Количество();
		
		ИначеЕсли НазваниеТаблицы = "Уведомления" Тогда
			
			ТекстЗапросаУведомлениеОбОСтатках = РегламентированнаяОтчетность.ТекстыДляЗапросаДокументовРеализацииПолномочийНО(
				"УведомлениеОбОстаткахПрослеживаемыхТоваров", Истина);
			ТекстЗапросаУведомлениеОВвозе = РегламентированнаяОтчетность.ТекстыДляЗапросаДокументовРеализацииПолномочийНО(
				"УведомлениеОВвозеПрослеживаемыхТоваров", Истина);
			
			Запрос = Новый Запрос;
			Запрос.Текст =
			"ВЫБРАТЬ
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка) КАК Количество,
			|	ЕСТЬNULL(СтатусыОтправки.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыОтправки.ПустаяСсылка)) КАК Статус,
			|	" + ТекстЗапросаУведомлениеОбОСтатках.ПеречислениеКолонок + "
			|ПОМЕСТИТЬ ВТ_КоличествоУведомлений
			|ИЗ
			|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров КАК УведомлениеОбОстаткахПрослеживаемыхТоваров
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыОтправки КАК СтатусыОтправки
			|		ПО УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка = СтатусыОтправки.Объект
			|	" + ТекстЗапросаУведомлениеОбОСтатках.СоединениеСДокументом + "
			|ГДЕ
			|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Организация = &Организация
			|	И НАЧАЛОПЕРИОДА(УведомлениеОбОстаткахПрослеживаемыхТоваров.Дата, МЕСЯЦ) = &Период
			|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.Проведен
			|	И ЕСТЬNULL(СтатусыОтправки.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыОтправки.ПустаяСсылка)) В (&Статусы)
			|	И ВЫБОР
			|			КОГДА &ОпределитьНаличиеРНПТ
			|				ТОГДА НЕ УведомлениеОбОстаткахПрослеживаемыхТоваров.РНПТ = ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
			|			ИНАЧЕ УведомлениеОбОстаткахПрослеживаемыхТоваров.РНПТ = ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
			|		КОНЕЦ
			|
			|СГРУППИРОВАТЬ ПО
			|	ЕСТЬNULL(СтатусыОтправки.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыОтправки.ПустаяСсылка)),
			|	ЛОЖЬ
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка),
			|	ЕСТЬNULL(СтатусыОтправки.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыОтправки.ПустаяСсылка)),
			|	ЛОЖЬ КАК ДокументыРеализацииПолномочийНО_РНПТЗаполнен
			|ИЗ
			|	Документ.УведомлениеОВвозеПрослеживаемыхТоваров КАК УведомлениеОВвозеПрослеживаемыхТоваров
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыОтправки КАК СтатусыОтправки
			|		ПО УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка = СтатусыОтправки.Объект
			|	" + ТекстЗапросаУведомлениеОВвозе.СоединениеСДокументом + "
			|ГДЕ
			|	УведомлениеОВвозеПрослеживаемыхТоваров.Организация = &Организация
			|	И НАЧАЛОПЕРИОДА(УведомлениеОВвозеПрослеживаемыхТоваров.Дата, МЕСЯЦ) = &Период
			|	И УведомлениеОВвозеПрослеживаемыхТоваров.Проведен
			|	И ЕСТЬNULL(СтатусыОтправки.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыОтправки.ПустаяСсылка)) В (&Статусы)
			|	И УведомлениеОВвозеПрослеживаемыхТоваров.РНПТ = ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
			|
			|СГРУППИРОВАТЬ ПО
			|	ЕСТЬNULL(СтатусыОтправки.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыОтправки.ПустаяСсылка)),
			|	ЛОЖЬ
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СУММА(ВТ_КоличествоУведомлений.Количество) КАК Количество,
			|	ВТ_КоличествоУведомлений.Статус КАК Статус
			|ИЗ
			|	ВТ_КоличествоУведомлений КАК ВТ_КоличествоУведомлений
			|ГДЕ
			|	ВТ_КоличествоУведомлений.ДокументыРеализацииПолномочийНО_РНПТЗаполнен = &ОпределитьНаличиеРНПТ
			|
			|СГРУППИРОВАТЬ ПО
			|	ВТ_КоличествоУведомлений.Статус";
			
			Запрос.УстановитьПараметр("Организация", Объект.Организация);
			Запрос.УстановитьПараметр("Период", Объект.Период);
			Запрос.УстановитьПараметр("Статусы", ОпределитьСтатус(ЭлементРасчета.Раздел));
			Запрос.УстановитьПараметр("ОпределитьНаличиеРНПТ", ОпределитьНаличиеРНПТ(ЭлементРасчета.Раздел));
			ТаблицаДанных = Запрос.Выполнить().Выгрузить();
			
			ЭлементРасчета.Количество = 0;
			Если ТаблицаДанных.Количество() > 0 Тогда
				ЭлементРасчета.Количество = ТаблицаДанных[0].Количество;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ОпределитьНаличиеРНПТ(Раздел)
	
	Если Раздел = "ЗаписатьПолученныеРНПТ" Тогда
		
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ОпределитьСтатус(Раздел)
	
	МассивСтатусов = Новый Массив;
	МассивСтатусов.Добавить(Перечисления.СтатусыОтправки.ПустаяСсылка());
	
	Возврат МассивСтатусов;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьДанныеПомощника(ВыводитьОкноОжидания = Ложь, ОбновитьНачальныеДанные = Истина)
	
	Элементы.Уведомления.Обновить();
	Элементы.ТоварыПоУведомлениям.Обновить();
	
	Если Не ОбновитьНачальныеДанные Тогда
		Возврат;
	КонецЕсли;
	
	ЗапущенноОбновлениеДанных = Истина;
	Если Не ВыводитьОкноОжидания Тогда
		
		ОбновитьТекстОжиданияВыполненияДействия();
		
	Иначе
		
		ОбновитьСтатусФоновогоЗадания();
		
	КонецЕсли;
	
	ДлительнаяОперация = ВыполнитьПолучениеДанныхНаСервере();
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус <> "Ошибка" Тогда
		
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтаФорма);
		НастройкиОжидания.ВыводитьОкноОжидания = ВыводитьОкноОжидания;
		НастройкиОжидания.ТекстСообщения = НСтр("ru='Выполняется получение данных'");
		
		Обработчик = Новый ОписаниеОповещения("ПослеПолученияДанных", ЭтаФорма);
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
		
	Иначе
		
		ПоказатьОшибкуПолученияДанных(ДлительнаяОперация);
		УправлениеФормой(ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияДанных(ДлительнаяОперация, ДополнительныеПараметры) Экспорт

	Если ДлительнаяОперация = Неопределено Тогда
		Возврат; // Отменено
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		
		ЗапущенноОбновлениеДанных = Ложь;
		ДанныеПолучены = Истина;
		
		ЗагрузитьДанныеВПомощник(ДлительнаяОперация.АдресРезультата);
		
		УстановитьТекущуюСтраницу(ЭтаФорма);
		
		ОбновитьНавигациюПоРазделамНаСервере(Истина);
		
		ПоказатьКомандыРаздела(ТекущийШаг);
		
		ПерейтиНаСледующийАктуальныйШаг();
		
	Иначе
		ПоказатьОшибкуПолученияДанных(ДлительнаяОперация);
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДанныеВПомощник(АдресРезультата)
	
	СтруктураДанных = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	Объект.НачальныеДанные.Очистить();
	
	Если НЕ ЗначениеЗаполнено(СтруктураДанных.НачальныеДанные) Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого ТекущаяСтрока Из СтруктураДанных.НачальныеДанные Цикл
		НоваяСтрока = Объект.НачальныеДанные.Добавить();
		ЗаполнитьЗначениЯСвойств(НоваяСтрока, ТекущаяСтрока);
		Если НЕ ЗначениеЗаполнено(НоваяСтрока.Основание) Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока.ПредставлениеОснование = ТекстДокументаДляПреставления(НоваяСтрока.Основание);
		НоваяСтрока.ЭтоНовыйДокумент = СозданныеПервичныеДокументы.НайтиПоЗначению(НоваяСтрока.Основание) <> Неопределено;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТекстДокументаДляПреставления(Документ)
	
	Возврат ПредставлениеДокумента(Документ);
	
КонецФункции

// Возвращает представление документа на форме
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ
//  
// Возвращаемое значение:
//  Строка - Текст, составленный по ссылке документа
// 
&НаСервереБезКонтекста
Функция ПредставлениеДокумента(Документ) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Документ)Тогда
		Возврат "";
	КонецЕсли;
	
	ПараметрыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Документ, "Номер,Дата");
	ТекстОснование = "";
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.ИнвентаризацияТоваровНаСкладе") Тогда
		
		ТекстОснование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Инвентаризация №%1 от %2'"),
			ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ПараметрыДокумента.Номер, Истина, Ложь), 
			Формат(ПараметрыДокумента.Дата, "ДФ=dd.MM.yyyy"));
			
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.УведомлениеОбОстаткахПрослеживаемыхТоваров") Тогда
		
		ТекстОснование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Уведомление об остатках №%1 от %2'"),
			ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ПараметрыДокумента.Номер, Истина, Ложь), 
			Формат(ПараметрыДокумента.Дата, "ДФ=dd.MM.yyyy"));
			
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.УведомлениеОВвозеПрослеживаемыхТоваров") Тогда
		
		ТекстОснование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Уведомление о ввозе №%1 от %2'"),
			ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ПараметрыДокумента.Номер, Истина, Ложь), 
			Формат(ПараметрыДокумента.Дата, "ДФ=dd.MM.yyyy"));
			
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.УведомлениеОПеремещенииПрослеживаемыхТоваров") Тогда
		
		ТекстОснование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Уведомление о перемещении №%1 от %2'"),
			ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ПараметрыДокумента.Номер, Истина, Ложь), 
			Формат(ПараметрыДокумента.Дата, "ДФ=dd.MM.yyyy"));
			
	ИначеЕсли  ТипЗнч(Документ) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
		
		ТекстОснование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Поступление (акт, накладная) №%1 от %2'"),
			ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ПараметрыДокумента.Номер, Истина, Ложь), 
			Формат(ПараметрыДокумента.Дата, "ДФ=dd.MM.yyyy"));
			
	КонецЕсли;
	
	Возврат ТекстОснование;
	
КонецФункции

&НаСервере
Функция ВыполнитьПолучениеДанныхНаСервере()
	
	ПараметрыОбработчика = ПараметрыПолученияДанных();
	НастройкаЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкаЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Заполнение начальных данных из помощника получения РНПТ'");
	НастройкаЗапуска.ОжидатьЗавершение = 0;
	НастройкаЗапуска.ЗапуститьВФоне = Истина;
	
	НастроитьДлительнуюОперацию(НастройкаЗапуска);
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне("Обработки.ПомощникПолученияРНПТ.ДанныеЗаполненияПомощникаПолученияРНПТ",
		ПараметрыОбработчика,
		НастройкаЗапуска);

	Возврат ДлительнаяОперация;
		
КонецФункции

//Особенности работы с длительными операциями в конкретном окружении
//
//Параметры:
//   Параметры - Структура - параметры фонового задания.
//   ПараметрыВыполнения - см. ДлительныеОперации.ПараметрыВыполненияВФоне - параметры выполнения фонового задания.
&НаСервере
Процедура НастроитьДлительнуюОперацию(ПараметрыВыполнения)
	
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		ПараметрыВыполнения.ЗапуститьВФоне = Ложь;
		ПараметрыВыполнения.ЗапуститьНеВФоне = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПараметрыПолученияДанных()
	
	ПараметрыПолучения = Новый Структура();
	ПараметрыПолучения.Вставить("Период", Объект.Период);
	ПараметрыПолучения.Вставить("Организация", Объект.Организация);
	
	Возврат ПараметрыПолучения;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	Элементы = Форма.Элементы;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиНаСледующийАктуальныйШаг()
	
	ОбновлятьШагВсегда = Ложь;
	
	Если ТекущийШаг = "" Тогда
		
		ТекущийШаг = "ПровестиИнвентаризацию";
		ОбновлятьШагВсегда = Истина;
		
	КонецЕсли;
	
	НайденныеСтроки = ОписаниеРазделов.НайтиСтроки(Новый Структура("Имя", ТекущийШаг));
	НаТекущемШагеЕстьДанные = Ложь;
	
	Если НайденныеСтроки.Количество() > 0 Тогда
		
		НаТекущемШагеЕстьДанные = НайденныеСтроки[0].Количество > 0 И НайденныеСтроки[0].Видимость;
		ТекущаяСтрока = НайденныеСтроки[0].НомерСтроки;
		
		// На последнем шаге не переключаем на другие шаги
		Если ЭтоПоследнийШаг(ТекущаяСтрока, ОписаниеРазделов) Тогда
			НаТекущемШагеЕстьДанные = Истина;
		КонецЕсли;
	Иначе
		ТекущаяСтрока = 0;
	КонецЕсли;
	
	Если Не НаТекущемШагеЕстьДанные Тогда
		
		НомерТекущейСтроки = НомерШагаСЗаполненнымиДанными(ОписаниеРазделов, ТекущаяСтрока);
		
		// Если с текущего номера шага не нашли шаг с данными, то попробуем поискать с начала самого
		Если НомерТекущейСтроки = Неопределено Тогда
			НомерТекущейСтроки = НомерШагаСЗаполненнымиДанными(ОписаниеРазделов, 0);
			Если НомерТекущейСтроки = Неопределено Тогда
				НомерТекущейСтроки = 0;
			КонецЕсли;
		КонецЕсли;
		
		Если НомерТекущейСтроки <> Неопределено Тогда
			НайденныеСтроки = ОписаниеРазделов.НайтиСтроки(Новый Структура("НомерСтроки", НомерТекущейСтроки));
			
			НовыйШаг = НайденныеСтроки[0].Имя;
			Если НовыйШаг <> ТекущийШаг 
				ИЛИ ОбновлятьШагВсегда Тогда
				
				ПредыдущаяГиперссылка = ТекущийРазделФормы(ЭтаФорма);
				ТекущаяГиперссылка    = Элементы[НовыйШаг];
				
				ПриПереключенииРазделаСервер(ПредыдущаяГиперссылка.Имя, ТекущаяГиперссылка.Имя);
				
				ПриИзмененииРаздела(НовыйШаг);
				
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		
		ПредыдущаяГиперссылка = ТекущийРазделФормы(ЭтаФорма);
		ТекущаяГиперссылка    = Элементы[ТекущийШаг];
		ПриПереключенииРазделаСервер(ПредыдущаяГиперссылка.Имя, ТекущаяГиперссылка.Имя);
		ПриИзмененииРаздела(ТекущийШаг);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоПоследнийШаг(ТекущаяСтрока, ОписаниеРазделов)
	
	Количество = ОписаниеРазделов.Количество();
	
	НомерПоследнегоШага = ОписаниеРазделов[Количество-1].НомерСтроки;
	
	Возврат ТекущаяСтрока = НомерПоследнегоШага;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НомерШагаСЗаполненнымиДанными(ОписаниеРазделов, ТекущаяСтрока)

	ТекущиеДанные = ОписаниеРазделов[ТекущаяСтрока];
	Если ТекущиеДанные.Количество = 0 ИЛИ Не ТекущиеДанные.Видимость Тогда
		
		КоличествоРазделов = ОписаниеРазделов.Количество()-1;
		Если КоличествоРазделов = ТекущаяСтрока Тогда 
			Возврат Неопределено;
		Иначе
			ТекущаяСтрока = ТекущаяСтрока + 1;
			ТекущаяСтрока = НомерШагаСЗаполненнымиДанными(ОписаниеРазделов, ТекущаяСтрока);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ТекущаяСтрока;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьОшибкуПолученияДанных(ДлительнаяОперация)
	
	МассивИзДвухСтрок = Новый Массив;
	МассивИзДвухСтрок.Добавить(НСтр("ru = 'Ошибка при получении данных:'"));
	МассивИзДвухСтрок.Добавить(ДлительнаяОперация.КраткоеПредставлениеОшибки);
	
	ВызватьИсключение СтрСоединить(МассивИзДвухСтрок, Символы.ПС);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииРаздела(Знач НовыйРаздел)
	
	ПоказатьРаздел(НовыйРаздел);
	ОбновитьТаблицуТоварыПоУведомлениям();
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьРаздел(Знач Раздел)
	
	ТекущийШаг = Раздел;
	
	ПоказатьКомандыРаздела(Раздел);
	ПоказатьСписокРаздела(Раздел);
	ПоказатьОписаниеРаздела(Раздел);
	
КонецПроцедуры

&НаСервере 
Процедура ПоказатьОписаниеРаздела(Раздел)
	
	ОписаниеШага = ПолучитьОписаниеШага(Раздел);
	
	ОписаниеПодсказкиШага = ОписаниеШага.Подсказка;
	Элементы.ОписаниеПодсказкиШага.МаксимальнаяВысота = ОписаниеШага.ВысотаПодсказки;
	
КонецПроцедуры 

&НаСервере
Процедура ПоказатьСписокРаздела(Знач Раздел)
	
	ПоказатьСтраницуРаздела(Раздел);
	
	ПоказатьПоляРаздела(Раздел);
	
	ПрименитьОтборыКРазделу(Раздел);
	
КонецПроцедуры

&НаСервере
Процедура ПрименитьОтборыКРазделу(Знач Раздел)
	
	ИмяСписка = СписокРаздела(Раздел);
	СписокЭлемент = ЭтаФорма.Элементы[ИмяСписка];
	
	Если Не Раздел = "ПровестиИнвентаризацию"
			И НЕ Раздел = "СоздатьУведомления" Тогда
		СписокОбъект = ЭтаФорма[ИмяСписка];
	Иначе
		СписокОбъект = Неопределено;
	КонецЕсли;

	ПрименитьОтборыКСпискуРаздела(Раздел, СписокЭлемент, СписокОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПрименитьОтборыКСпискуРаздела(Знач Раздел, Список, СписокОбъект = Неопределено)
	
	УстановитьОтборПоРазделу(Раздел, Список, СписокОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьОтборПоРазделу(Знач Раздел, Список, СписокОбъект = Неопределено)
	
	Если Раздел = "ПровестиИнвентаризацию" Тогда
		Список.ОтборСтрок = Новый ФиксированнаяСтруктура("ЕстьПервичныйДокумент", Ложь);
	ИначеЕсли Раздел = "СоздатьУведомления" Тогда
		Список.ОтборСтрок = Новый ФиксированнаяСтруктура("ЕстьПервичныйДокумент", Истина);
		
	Иначе
		
		ГруппаОтборПоРазделам = ОбщегоНазначенияКлиентСервер.НайтиЭлементОтбораПоПредставлению(СписокОбъект.Отбор.Элементы, "ОтборПоРазделам");
		Если ГруппаОтборПоРазделам = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		// Включаем использование группы отбора, соответствующей разделу.
		РазделНайден = Ложь;
		Для каждого ГруппаРаздела Из ГруппаОтборПоРазделам.Элементы Цикл
			
			Если ГруппаРаздела.Представление = "Раздел" + Раздел Тогда
				ГруппаРаздела.Использование = Истина;
				РазделНайден = Истина;
			Иначе
				ГруппаРаздела.Использование = Ложь;
			КонецЕсли;
			
		КонецЦикла;
		// Если не удалось найти группу раздела, то включаем все разделы.
		Если Не РазделНайден Тогда
			Для каждого ГруппаРаздела Из ГруппаОтборПоРазделам.Элементы Цикл
				ГруппаРаздела.Использование = Истина;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ЭлементыОтбора(Знач Отбор)
	
	НайденныеЭлементы = Новый Массив;
	
	Если Отбор = Неопределено Тогда
		Возврат НайденныеЭлементы;
	КонецЕсли;
	
	Для каждого Элемент Из Отбор.Элементы Цикл
		
		Если ТипЗнч(Элемент) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			
			НайденныеЭлементыГруппы = ЭлементыОтбора(Элемент);
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(НайденныеЭлементы, НайденныеЭлементыГруппы);
			
		Иначе
			
			НайденныеЭлементы.Добавить(Элемент);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат НайденныеЭлементы;
	
КонецФункции

&НаСервере 
Процедура ПоказатьПоляРаздела(Знач Раздел)
	
	ИмяТаблицы = ТаблицаРаздела(Раздел);
	Если Не ЗначениеЗаполнено(ИмяТаблицы) Тогда
		Возврат;
	КонецЕсли;
	
	ПоляТаблицы = ПоляРаздела(Раздел);
	
	Для каждого Поле Из ПоляТаблицы Цикл
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы, ИмяТаблицы + Поле.Ключ, "Видимость", Поле.Значение);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПоляРаздела(Знач Раздел)
	
	ПоляТаблицы = "";
	ПоляРаздела = "";
	
	Если Раздел = "ПровестиИнвентаризацию" Тогда
		
		ПоляТаблицы = "ПредставлениеОснование,Склад,Номенклатура,ТНВЭД,ЕдиницаИзмерения,Количество,Сумма";
		ПоляРаздела = "Склад,Номенклатура,ТНВЭД,ЕдиницаИзмерения,Количество";;

	ИначеЕсли Раздел = "СоздатьУведомления" Тогда
		
		ПоляТаблицы = "ПредставлениеОснование,Склад,Номенклатура,ТНВЭД,ЕдиницаИзмерения,Количество,Сумма";
		ПоляРаздела = "ПредставлениеОснование,Номенклатура,ТНВЭД,ЕдиницаИзмерения,Количество,Сумма";
		
	КонецЕсли;
	
	ПоляТаблицы = СтрЗаменить(ПоляТаблицы, " ", "");
	ПоляРаздела = СтрЗаменить(ПоляРаздела, " ", "");
	
	МассивПолеТаблицы = СтрРазделить(ПоляТаблицы, ",", Ложь);
	МассивПолейРаздела = СтрРазделить(ПоляРаздела, ",", Ложь);
	
	ВидимостьПолей = Новый Соответствие;
	Для каждого Поле Из МассивПолеТаблицы Цикл
		
		Видимость = (МассивПолейРаздела.Найти(Поле) <> Неопределено);
		ВидимостьПолей.Вставить(Поле, Видимость);
		
	КонецЦикла;
	
	Возврат ВидимостьПолей;
	
КонецФункции

&НаСервере 
Процедура ПоказатьСтраницуРаздела(Знач Раздел) 
	
	ИмяСтраницы = СтраницаРаздела(Раздел);
	
	Если ЗначениеЗаполнено(ИмяСтраницы) Тогда
		Элементы.ПанельСписков.ТекущаяСтраница = Элементы[ИмяСтраницы];
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СтраницаРаздела(Знач Раздел)
	
	Страница = Неопределено;
	
	СтраницыРазделов = Новый Соответствие;
	СтраницыРазделов.Вставить("ПровестиИнвентаризацию", "НачальныеДанные");
	СтраницыРазделов.Вставить("СоздатьУведомления", "НачальныеДанные");

	Страница = СтраницыРазделов.Получить(Раздел);

	Если ЗначениеЗаполнено(Страница) Тогда
		Страница = "Страница" + СтраницыРазделов.Получить(Раздел);
	КонецЕсли;
	
	Возврат Страница;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТаблицаРаздела(Знач Раздел)
	
	ТаблицыРазделов = Новый Соответствие;
	ТаблицыРазделов.Вставить("ПровестиИнвентаризацию", "НачальныеДанные");
	ТаблицыРазделов.Вставить("СоздатьУведомления", "НачальныеДанные");
	
	Таблица = ТаблицыРазделов.Получить(Раздел);
	
	Возврат Таблица;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СписокРаздела(Знач Раздел)
	
	СпискиРазделов = Новый Соответствие;
	СпискиРазделов.Вставить("ПровестиИнвентаризацию", "НачальныеДанные");
	СпискиРазделов.Вставить("СоздатьУведомления", "НачальныеДанные");
	
	Список = СпискиРазделов.Получить(Раздел);
	
	Возврат Список;
	
КонецФункции

Функция ПолучитьКомандыРазделов()
	
	КомандыРазделов = Новый Соответствие;
	КомандыРазделов.Вставить("ПровестиИнвентаризацию", "Обновить,СоздатьИнвентаризации,ПоискНачальныеДанные,ОтменитьПоискНачальныеДанные");
	КомандыРазделов.Вставить("СоздатьУведомления", "Обновить,СоздатьУведомления,ПоискНачальныеДанные,ОтменитьПоискНачальныеДанные");
	
	Возврат КомандыРазделов;
	
КонецФункции

&НаСервере
Процедура ПоказатьКомандыРаздела(Знач Раздел)
	
	КомандыРазделов = ПолучитьКомандыРазделов();
		
	ЭлементыКоманд = Новый Соответствие;
	ЭлементыКоманд.Вставить("Обновить", Элементы.КомандаОбновить);
	ЭлементыКоманд.Вставить("СоздатьУведомления", Элементы.КомандаСоздатьУведомления);
	ЭлементыКоманд.Вставить("ОткрытьЖурналУведомлений", Элементы.КомандаОткрытьЖурналУведомлений);
	ЭлементыКоманд.Вставить("СоздатьИнвентаризации", Элементы.КомандаСоздатьИнвентаризации);
	ЭлементыКоманд.Вставить("ПоискУведомления", Элементы.ПоискУведомления);
	ЭлементыКоманд.Вставить("ОтменитьПоискУведомления", Элементы.ОтменитьПоискУведомления);
	ЭлементыКоманд.Вставить("ПоискНачальныеДанные", Элементы.ПоискНачальныеДанные);
	ЭлементыКоманд.Вставить("ОтменитьПоискНачальныеДанные", Элементы.ОтменитьПоискНачальныеДанные);
	
	КомандыСОжиданиемДействия = Новый Соответствие;
	КомандыСОжиданиемДействия.Вставить("СоздатьУведомления",    "ЗапущеноСозданиеУведомлений");
	КомандыСОжиданиемДействия.Вставить("СоздатьИнвентаризации", "ЗапущеноСозданиеИнвентаризаций");
	
	КомандыПоУмолчанию = Новый Соответствие;
	КомандыПоУмолчанию.Вставить("СоздатьУведомления",      "СоздатьУведомления");
	КомандыПоУмолчанию.Вставить("СоздатьИнвентаризации", "СоздатьИнвентаризации");
	
	СвязанныеКоманды = Новый Соответствие();
	
	КомандыРаздела = КомандыРазделов.Получить(Раздел);
	
	Элементы.ГруппаОжиданиеВыполненияДействия.Видимость = Ложь;
	
	Для каждого КлючЗначение Из ЭлементыКоманд Цикл
		
		ИмяКоманды = КлючЗначение.Ключ;
		ЭлементФормы = КлючЗначение.Значение;
		
		ЕстьКоманда = (СтрНайти(КомандыРаздела, ИмяКоманды) > 0);
		КомандаДоступна = (НедоступныеКоманды.НайтиПоЗначению(ИмяКоманды) = Неопределено);
		
		КомандаПоУмолчанию = КомандыПоУмолчанию.Получить(ИмяКоманды) <> Неопределено;
		
		ЗапущеноОжиданиеДействия = Ложь;
		
		Если ЕстьКоманда И КомандаДоступна Тогда
			
			ИмяПризнакаОжиданияДействия = КомандыСОжиданиемДействия.Получить(ИмяКоманды);
			
			Если ИмяПризнакаОжиданияДействия <> Неопределено Тогда
				
				ЗапущеноОжиданиеДействия = ЭтаФорма[ИмяПризнакаОжиданияДействия];
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗапущеноОжиданиеДействия Тогда
			
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
				Элементы, "ГруппаОжиданиеВыполненияДействия", "Видимость", ЕстьКоманда);
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
				Элементы, ЭлементФормы.Имя, "Видимость", Ложь);
			ОбновитьТекстОжиданияВыполненияДействия();
			УстановитьРежимВидимостиСвязанныхКоманд(ЭлементФормы.Имя, СвязанныеКоманды, Ложь);
			
		Иначе
			
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
				Элементы, ЭлементФормы.Имя, "Видимость", ЕстьКоманда);
				
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
				Элементы, ЭлементФормы.Имя, "Доступность", КомандаДоступна);
				
			Если ТипЗНЧ(Элементы[ЭлементФормы.Имя]) = Тип("КнопкаФормы")
					И КомандаПоУмолчанию Тогда
				ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
					Элементы, ЭлементФормы.Имя, "КнопкаПоУмолчанию", ЕстьКоманда И КомандаДоступна);
			КонецЕсли;
				
		КонецЕсли;
			
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьРежимВидимостиСвязанныхКоманд(ИмяЭлемента, СвязанныеКоманды, ВидимостьКоманды);
		
	СвязанныеКомандыСтрока = СвязанныеКоманды.Получить(ИмяЭлемента);
	МассивСвязанныхКоманд = СтрРазделить(СвязанныеКомандыСтрока, ",", Ложь);
			
	Для каждого ИмяСвязаннойКоманды Из МассивСвязанныхКоманд Цикл
				
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы, ИмяСвязаннойКоманды, "Видимость", ВидимостьКоманды);
				
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ОбновитьТекстОжиданияВыполненияДействия()
	
	ЧастиТекста = Новый Массив;
	
	ЧастиТекста.Добавить(НСтр("ru = 'Выполняется'"));
	
	Если ЗапущеноСозданиеИнвентаризаций Тогда
		
		ЧастиТекста.Добавить(НСтр("ru = ' создание инвентаризации товаров'"));
		
	ИначеЕсли ЗапущеноСозданиеУведомлений Тогда
		
		ЧастиТекста.Добавить(НСтр("ru = ' создание уведомлений'"));
		
	ИначеЕсли ЗапущеноОтправлениеУведомленийВФНС Тогда
		
		ЧастиТекста.Добавить(НСтр("ru = ' отправление в ФНС'"));
		
	ИначеЕсли ЗапущенноОбновлениеДанных Тогда
		
		ЧастиТекста.Добавить(НСтр("ru = ' обновление данных'"));
		
	КонецЕсли;
	
	ТекстОжиданияВыполненияДействия =  Новый ФорматированнаяСтрока(ЧастиТекста);
	
КонецПроцедуры

&НаСервере 
Процедура ИнициализироватьРазделы()
	
	ДобавитьОписаниеРаздела(ОписаниеРазделов, "ПровестиИнвентаризацию", НСтр("ru = 'Инвентаризация'"));
	ДобавитьОписаниеРаздела(ОписаниеРазделов, "СоздатьУведомления", НСтр("ru = 'Уведомления'"));

КонецПроцедуры

&НаСервереБезКонтекста
Функция ДобавитьОписаниеРаздела(ОписаниеРазделов, Знач Имя, Знач Представление) 
	
	Количество = ОписаниеРазделов.Количество();
	
	НовыйРаздел = ОписаниеРазделов.Добавить();
	НовыйРаздел.Имя = Имя;
	НовыйРаздел.Представление = Представление;
	НовыйРаздел.Видимость = Истина;
	НовыйРаздел.РассчитыватьКоличество = Истина;
	НовыйРаздел.КоличествоРассчитано = Ложь;
	НовыйРаздел.НомерСтроки = Количество;
	
	Возврат НовыйРаздел;
	
КонецФункции

&НаСервереБезКонтекста
Процедура УстановитьЗначениеРеквизитаДерева(Дерево, ИмяДействия, ИмяРеквизита, Значение, Установлено = Ложь)

	Для Каждого ПодчиненнаяСтрока Из Дерево.ПолучитьЭлементы() Цикл
		Если ПодчиненнаяСтрока.Значение = ИмяДействия Тогда
			ПодчиненнаяСтрока[ИмяРеквизита] = Значение;
			Установлено = Ложь;
		Иначе
			УстановитьЗначениеРеквизитаДерева(ПодчиненнаяСтрока, ИмяДействия, ИмяРеквизита, Значение, Установлено);
		КонецЕсли;
		
		Если Установлено Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТаблицуТоварыПоУведомлениям()

	ТекущиеДанные = Элементы.Уведомления.ТекущиеДанные;
	
	Уведомление = ?(ТекущиеДанные <> Неопределено, ТекущиеДанные.Уведомление,
			ПредопределенноеЗначение("Перечисление.СтатусыОтправки.ПустаяСсылка"));
		
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ТоварыПоУведомлениям, "Уведомление", 
		Уведомление, ВидСравненияКомпоновкиДанных.Равно,, Истина);
	
КонецПроцедуры 

&НаСервере
Процедура ПриПереключенииРазделаСервер(ПредыдущаяГиперссылкаИмя, ТекущаяГиперссылкаИмя)
	
	ПредыдущаяГиперссылка 	= Элементы[ПредыдущаяГиперссылкаИмя];
	ТекущаяГиперссылка 		= Элементы[ТекущаяГиперссылкаИмя];
	
	// Делаем серой предыдущую гиперссылку.
	ПредыдущаяГиперссылка.ЦветФона		= Серый;
	ПредыдущаяГиперссылка.Гиперссылка	= Истина;

	// Делаем желтой текущую гиперссылку.
	ТекущаяГиперссылка.ЦветФона		= Желтый;
	ТекущаяГиперссылка.Гиперссылка	= Ложь;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ТекущийРазделФормы(Форма)
	
	Результат = Неопределено;
	
	Для каждого Раздел Из Форма.ОписаниеРазделов Цикл
		
		Гиперссылка = Форма.Элементы[Раздел.Имя];
		
		Если Гиперссылка.ЦветФона = Форма.Желтый Тогда
			Результат = Гиперссылка;
		КонецЕсли;
	
	КонецЦикла; 
	
	Если Результат = Неопределено Тогда 
		Возврат Форма.Элементы["ПровестиИнвентаризацию"];
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПослеСозданияУведомлений(ДлительнаяОперация, ДополнительныеПараметры) Экспорт

	Если ДлительнаяОперация = Неопределено Тогда
		Возврат; // Отменено
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		
		ЗапущеноСозданиеУведомлений = Ложь;
		
		ОбновитьДанныеПомощника(Ложь, Истина);
		
		Оповестить("СозданыУведомленияВПомощникеПолученияРНПТ");
		
		ПерейтиНаСледующийАктуальныйШаг();
		
		УправлениеФормой(ЭтаФорма);
		
	Иначе
		ПоказатьОшибкуПолученияДанных(ДлительнаяОперация);
		УправлениеФормой(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
