
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Заголовок = Параметры.ЗаголовокФормы;
	Элементы.Строка.МногострочныйРежим = Параметры.Многострочность;
	Обязательность = Параметры.Обязательность;
	КомментарийОбязательностиВвода = Параметры.КомментарийОбязательностиВвода;
	Элементы.ОК.Заголовок = Параметры.НазваниеКнопкиПоУмолчанию;
	
	ПредставлениеДанных = Параметры.ПредставлениеДанных;
	Если Не ЗначениеЗаполнено(ПредставлениеДанных) Тогда
		ПредставлениеДанных = НСтр("ru = 'Обрабатываемые данные'");
	КонецЕсли;
	Если Параметры.Данные <> Неопределено Тогда
		Для каждого ЭлементДанных Из Параметры.Данные Цикл
			Данные.Добавить(ЭлементДанных, ЭлементДанных);
		КонецЦикла;
		Элементы.ПредставлениеДанных.Видимость = Истина;
		Если Параметры.ОбработчикПолученияПредставлений <> "" Тогда
			Попытка
				ПредставленияДанных = Вычислить(Параметры.ОбработчикПолученияПредставлений + "(Параметры.Данные)");
				Для каждого ЭлементСписка Из Данные Цикл
					ЭлементСписка.Представление = ПредставленияДанных[ЭлементСписка.Значение];
				КонецЦикла;
			Исключение
				ЭлектронныеДокументыСлужебныйВызовСервера.ОбработатьОшибку(НСтр("ru = 'Обрабатываемые данные'"),
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()), НСтр("ru = 'Не удалось получить представление данных по причине:'") +
					КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			КонецПопытки;
		КонецЕсли;
		Если Данные.Количество() = 1 И ПредставленияДанных <> Неопределено Тогда
			ПредставлениеДанных = ПредставленияДанных[Параметры.Данные[0]];
		Иначе 
			ПредставлениеДанных = ПредставлениеДанных + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(" (%1)", Данные.Количество());
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеДанныхНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Данные.Количество() = 1 Тогда
		ОткрытьЗначение(Данные[0].Значение);
	Иначе 
		СписокПредставленийДанных = Новый Массив;
		Для Каждого ЭлементСписка Из Данные Цикл
			СписокПредставленийДанных.Добавить(ЭлементСписка.Представление);
		КонецЦикла;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("СписокПредставленийДанных", СписокПредставленийДанных);
		ПараметрыФормы.Вставить("ПредставлениеДанных", ПредставлениеДанных);
		НоваяФорма = ОткрытьФорму("Обработка.ЭлектронныеДокументы.Форма.ПросмотрДанных", ПараметрыФормы, ЭтаФорма);
		НоваяФорма.УстановитьСписокПредставлений(Данные, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	Если Обязательность И Не ЗначениеЗаполнено(Строка) Тогда
		ТекстСообщения = ?(ЗначениеЗаполнено(КомментарийОбязательностиВвода), КомментарийОбязательностиВвода, НСтр("ru = 'Необходимо ввести текст'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат; 
	КонецЕсли;
	
	Закрыть(Строка);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

