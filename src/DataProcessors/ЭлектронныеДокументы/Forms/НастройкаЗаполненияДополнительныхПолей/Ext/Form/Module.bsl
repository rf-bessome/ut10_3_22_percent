////////////////////////////////////////////////////////////////////////////////
//  ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	НастройкаЭДО = Параметры.НастройкаЭДО;
	ВидЭлектронногоДокумента = Параметры.ВидЭлектронногоДокумента;
	Формат = Параметры.Формат;
	
	ЗаполнитьРазделыДополнительныхПолей();
	
	ПодготовитьФормуНаСервере();
	
	ПрочитатьНастройкиЗаполненияПолей();
	
	ИзменитьВидимостьИнформацииОЗаполненииПолей(ЭтаФорма);
	
КонецПроцедуры

//////////////////////////////////////////////////////////
// ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФорматПредставлениеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ЗначениеЗаполнено(СсылкаНаПриказОВведенииФормата) Тогда
		ПерейтиПоНавигационнойСсылке(СсылкаНаПриказОВведенииФормата);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
//  ОбработчикиСобытийЭлементовТаблицыФормыОбластиДопПолей

&НаКлиенте
Процедура Подключаемый_ТаблицаНастроекПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		ОбновитьКоличествоСтрокТаблицыНастроек(Элемент);
		Элемент.ТекущиеДанные.Идентификатор = НовыйИдентификаторНастройкиПоля();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ТаблицаНастроекПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если НоваяСтрока И ОтменаРедактирования Тогда
		ОбновитьКоличествоСтрокТаблицыНастроек(Элемент);
		ИзменитьВидимостьИнформацииОЗаполненииПолей(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ТаблицаНастроекПослеУдаления(Элемент)
	
	ОбновитьКоличествоСтрокТаблицыНастроек(Элемент);
	ИзменитьВидимостьИнформацииОЗаполненииПолей(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПравилоОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИмяТаблицы = Элемент.Родитель.Имя;
	ТекущиеДанные = Элемент.Родитель.ТекущиеДанные;
	ОбработатьВыборПравилЗаполнения(ВыбранноеЗначение, ТекущиеДанные, ИмяТаблицы);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПравилоОчистка(Элемент, СтандартнаяОбработка)
	
	Элемент.Родитель.ТекущиеДанные.Значение = Неопределено;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
//  ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьИЗакрыть(Команда)
	
	Если Не Модифицированность Тогда
		Закрыть();
		Возврат;
	КонецЕсли;
	
	НастройкаПустая = Истина;
	Для Каждого Раздел Из РазделыДополнительныхПолей Цикл
		Если ЭтаФорма[Раздел.ИмяТаблицыНастроек].Количество() Тогда
			НастройкаПустая = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если НастройкаПустая Тогда
		УдалитьНастройкуЗаполненияПолей();
		ОповеститьОВыборе(Ложь);
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	
	ПроверитьЗаполнениеОбязательныхПолей(Отказ);
	
	СохранитьНастройкуЗаполненияПолей(Отказ);
	
	Если Не Отказ Тогда
		ОповеститьОВыборе(Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьВФайл(Команда)
	
	АдресНастройки = АдресВыгрузкиНастройкиЗаполненияДопПолей();
	ИмяФайла = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Доп поля %1 %2.json'"), ВидЭлектронногоДокумента, ФорматПредставление));
	ПолучитьФайл(АдресНастройки, ИмяФайла, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда)
	
	ЕстьНастройкиПолей = Ложь;
	Для Каждого Раздел Из РазделыДополнительныхПолей Цикл
		Если ЭтаФорма[Раздел.ИмяТаблицыНастроек].Количество() Тогда
			ЕстьНастройкиПолей = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьНастройкиПолей Тогда
		ТекстВопроса = НСтр("ru = 'Текущие настройки будут замещены данными из файла. Продолжить?'");
		Если Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
			ЗагрузитьДанныеИзФайла();
		КонецЕсли;
	Иначе
		ЗагрузитьДанныеИзФайла();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьНастройку(Команда)
	
	КодВозврата = Вопрос(НСтр("ru = 'Удалить настройку заполнения дополнительных полей?'"),
		РежимДиалогаВопрос.ДаНет);
		
	Если КодВозврата = КодВозвратаДиалога.Да Тогда
		УдалитьНастройкуЗаполненияПолей();
		ОповеститьОВыборе(Ложь);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
//  СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьРазделыДополнительныхПолей()
	
	РазделыДополнительныхПолейФормата = 
		ЭлектронныеДокументыВнутренний.РазделыДополнительныхПолейФорматаЭлектронногоДокумента(
			ВидЭлектронногоДокумента, Формат);
	Для Каждого РазделФормата Из РазделыДополнительныхПолейФормата Цикл
		Раздел = РазделыДополнительныхПолей.Добавить();
		ЗаполнитьЗначенияСвойств(Раздел, РазделФормата);
		Раздел.ИмяТаблицыНастроек = "ТаблицаНастроек" + Раздел.Имя;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	УсловноеОформление.Элементы.Очистить();
	
	ЗакрыватьПриВыборе = Истина;
	
	ВариантыПравилЗаполнения.Добавить("ВручнуюСтрокой", НСтр("ru = 'Вручную (строкой)'"));
	ВариантыПравилЗаполнения.Добавить("ВручнуюДатой",   НСтр("ru = 'Вручную (датой)'"));
	ВариантыПравилЗаполнения.Добавить("ВручнуюЧислом",  НСтр("ru = 'Вручную (числом)'"));
	ВариантыПравилЗаполнения.Добавить("ИзСписка",       НСтр("ru = 'Из списка'"));
	ВариантыПравилЗаполнения.Добавить("ПоФормуле",      НСтр("ru = 'По формуле'"));
	
	Для Каждого Раздел Из РазделыДополнительныхПолей Цикл
		
		СоздатьРеквизитыИЭлементыРазделаДополнительныхПолей(Раздел);
		
		УстановитьУсловноеОформлениеРазделаДополнительныхПолей(Раздел);
		
		ЗаполнитьСписокВыбораПравилЗаполнения(Раздел);
		
	КонецЦикла;
	
	СведенияОФорматах = ЭлектронныеДокументыПовтИсп.СведенияОФорматахЭлектронныхДокументов();
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ВидЭлектронногоДокумента", ВидЭлектронногоДокумента);
	ПараметрыОтбора.Вставить("ИдентификаторФормата", Формат);
	НайденныеСтроки = СведенияОФорматах.НайтиСтроки(ПараметрыОтбора);
	Если НайденныеСтроки.Количество() Тогда
		ФорматПредставление = НайденныеСтроки[0].ПредставлениеФормата;
		СсылкаНаПриказОВведенииФормата = НайденныеСтроки[0].СсылкаНаПриказОВведенииФормата;
	Иначе
		ФорматПредставление = Формат;
		Элементы.ФорматПредставление.Гиперссылка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьРеквизитыИЭлементыРазделаДополнительныхПолей(Раздел)
	
	ИмяТаблицыНастроек = Раздел.ИмяТаблицыНастроек;
	ОписаниеТипаСтрока = Новый ОписаниеТипов("Строка");
	
	ДобавляемыеРеквизиты = Новый Массив;
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(
		"КоличествоСтрок" + ИмяТаблицыНастроек, Новый ОписаниеТипов("Число")));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(
		ИмяТаблицыНастроек, Новый ОписаниеТипов("ТаблицаЗначений"),,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Таблица дополнительных полей области ""%1""'"), Раздел.Представление), Истина));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(
		"Идентификатор", ОписаниеТипаСтрока, ИмяТаблицыНастроек));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(
		"Имя", ОписаниеТипаСтрока, ИмяТаблицыНастроек, НСтр("ru = 'Имя поля'")));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(
		"Правило", ОписаниеТипаСтрока, ИмяТаблицыНастроек, НСтр("ru = 'Правила заполнения'")));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(
		"Представление", ОписаниеТипаСтрока, ИмяТаблицыНастроек, НСтр("ru = 'Представление'")));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(
		"Описание", ОписаниеТипаСтрока, ИмяТаблицыНастроек, НСтр("ru = 'Описание'")));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(
		"Заполнение", ОписаниеТипаСтрока, ИмяТаблицыНастроек));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(
		"Значение", Новый ОписаниеТипов(), ИмяТаблицыНастроек));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(
		"ОшибкаВФормуле", Новый ОписаниеТипов("Булево"), ИмяТаблицыНастроек));
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	Страница = Элементы.Добавить("Страница" + Раздел.Имя, Тип("ГруппаФормы"), Элементы.ГруппаСтраницы);
	Страница.Вид = ВидГруппыФормы.Страница;
	Страница.Заголовок = Раздел.Представление;
	Страница.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Страница.ПутьКДаннымЗаголовка = "КоличествоСтрок" + ИмяТаблицыНастроек;
	
	Пояснение = Элементы.Добавить("Пояснение" + Раздел.Имя, Тип("ДекорацияФормы"), Страница);
	Пояснение.Вид = ВидДекорацииФормы.Надпись;
	Пояснение.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Указанные поля будут добавлены в поле %1'"), Раздел.ПутьКЭлементуXML);
	Пояснение.ЦветТекста = ЦветаСтиля.ПоясняющийТекст;
	
	ТаблицаНастроек = Элементы.Добавить(ИмяТаблицыНастроек, Тип("ТаблицаФормы"), Страница);
	ТаблицаНастроек.ПутьКДанным = ИмяТаблицыНастроек;
	ТаблицаНастроек.АвтоВводНовойСтроки = Ложь;
	ТаблицаНастроек.УстановитьДействие("ПриНачалеРедактирования", "Подключаемый_ТаблицаНастроекПриНачалеРедактирования");
	ТаблицаНастроек.УстановитьДействие("ПриОкончанииРедактирования", "Подключаемый_ТаблицаНастроекПриОкончанииРедактирования");
	ТаблицаНастроек.УстановитьДействие("ПослеУдаления", "Подключаемый_ТаблицаНастроекПослеУдаления");
	
	КолонкаТаблицы = Элементы.Добавить(ИмяТаблицыНастроек + "Имя", Тип("ПолеФормы"), ТаблицаНастроек);
	КолонкаТаблицы.Вид = ВидПоляФормы.ПолеВвода;
	КолонкаТаблицы.ПутьКДанным = ИмяТаблицыНастроек + ".Имя";
	КолонкаТаблицы.АвтоОтметкаНезаполненного = Истина;
	КолонкаТаблицы.Подсказка = НСтр("ru = 'Под этим именем дополнительное поле попадет в электронный документ'");
	
	КолонкаТаблицы = Элементы.Добавить(ИмяТаблицыНастроек + "Правило", Тип("ПолеФормы"), ТаблицаНастроек);
	КолонкаТаблицы.Вид = ВидПоляФормы.ПолеВвода;
	КолонкаТаблицы.ПутьКДанным = ИмяТаблицыНастроек + ".Правило";
	КолонкаТаблицы.АвтоОтметкаНезаполненного = Истина;
	Если ОбщегоНазначенияКлиентСервер.ЭтоПлатформа83() Тогда
		КолонкаТаблицы.КнопкаВыпадающегоСписка = Истина;
	КонецЕсли;
	КолонкаТаблицы.КнопкаСпискаВыбора = Истина;
	КолонкаТаблицы.РедактированиеТекста = Ложь;
	КолонкаТаблицы.УстановитьДействие("ОбработкаВыбора", "Подключаемый_ПравилоОбработкаВыбора");
	КолонкаТаблицы.УстановитьДействие("Очистка", "Подключаемый_ПравилоОчистка");
	
	ГруппаКолонок = Элементы.Добавить(ИмяТаблицыНастроек + "НастройкаОтображения", Тип("ГруппаФормы"), ТаблицаНастроек);
	ГруппаКолонок.Вид = ВидГруппыФормы.ГруппаКолонок;
	ГруппаКолонок.Группировка = ГруппировкаКолонок.Вертикальная;
	ГруппаКолонок.Заголовок = НСтр("ru = 'Настройка отображения'");
	ГруппаКолонок.ОтображатьЗаголовок = Истина;
	ГруппаКолонок.ОтображатьВШапке = Истина;
	
	КолонкаТаблицы = Элементы.Добавить(ИмяТаблицыНастроек + "Представление", Тип("ПолеФормы"), ГруппаКолонок);
	КолонкаТаблицы.Вид = ВидПоляФормы.ПолеВвода;
	КолонкаТаблицы.ПутьКДанным = ИмяТаблицыНастроек + ".Представление";
	КолонкаТаблицы.ОтображатьВШапке = Ложь;
	КолонкаТаблицы.Подсказка = НСтр("ru = 'Заголовок поля в форме заполнения'");
	
	КолонкаТаблицы = Элементы.Добавить(ИмяТаблицыНастроек + "Описание", Тип("ПолеФормы"), ГруппаКолонок);
	КолонкаТаблицы.Вид = ВидПоляФормы.ПолеВвода;
	КолонкаТаблицы.ПутьКДанным = ИмяТаблицыНастроек + ".Описание";
	КолонкаТаблицы.ОтображатьВШапке = Ложь;
	КолонкаТаблицы.Подсказка = НСтр("ru = 'Введите описание поля для вывода подсказки'");
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеРазделаДополнительныхПолей(Раздел)
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Раздел.ИмяТаблицыНастроек + "Представление");
	
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(Раздел.ИмяТаблицыНастроек + ".Представление");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Заголовок поля в форме заполнения'"));
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.СветлоСерый);
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Раздел.ИмяТаблицыНастроек + "Описание");
	
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(Раздел.ИмяТаблицыНастроек + ".Описание");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Введите описание поля для вывода подсказки'"));
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.СветлоСерый);
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Раздел.ИмяТаблицыНастроек + "Правило");
	
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(Раздел.ИмяТаблицыНастроек + ".ОшибкаВФормуле");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.Красный);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораПравилЗаполнения(Раздел)
	
	СписокВыбора = Элементы[Раздел.ИмяТаблицыНастроек + "Правило"].СписокВыбора;
	
	Для Каждого Вариант Из ВариантыПравилЗаполнения Цикл
		ЗаполнитьЗначенияСвойств(СписокВыбора.Добавить(), Вариант);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНастройкиЗаполненияПолей()
	
	ТекстОшибки = Неопределено;
	
	НастройкаЗаполнения = ЭлектронныеДокументыВнутренний.НастройкаЗаполненияДополнительныхПолей(
		НастройкаЭДО, ВидЭлектронногоДокумента, формат, ТекстОшибки);
	
	Если НастройкаЗаполнения = Неопределено Тогда
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ЗаполнитьТаблицыНастроекПоРазделам(НастройкаЗаполнения);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицыНастроекПоРазделам(НастройкаЗаполнения)
	
	Для Каждого Раздел Из РазделыДополнительныхПолей Цикл
		
		ТаблицаНастроек = ЭтаФорма[Раздел.ИмяТаблицыНастроек];
		ТаблицаНастроек.Очистить();
		
		МассивНастроек = НастройкаЗаполнения.НайтиСтроки(Новый Структура("Раздел", Раздел.Имя));
		Для Каждого НастройкаПоля Из МассивНастроек Цикл
			Если НастройкаПоля.Заполнение = "ИзСписка"
				И ТипЗнч(НастройкаПоля.Значение) = Тип("Массив") Тогда
				НастройкаПоля.Значение = Новый ФиксированныйМассив(НастройкаПоля.Значение);
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(ТаблицаНастроек.Добавить(), НастройкаПоля);
		КонецЦикла;
		
		ЭтаФорма["КоличествоСтрок" + Раздел.ИмяТаблицыНастроек] = ТаблицаНастроек.Количество();
		
		ПроверитьКорректностьЗаполненияФормул(Раздел);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьКорректностьЗаполненияФормул(Раздел)
	
	ТаблицаНастроек = ЭтаФорма[Раздел.ИмяТаблицыНастроек];
	НастройкиПоФормуле = ТаблицаНастроек.НайтиСтроки(Новый Структура("Заполнение", "ПоФормуле"));
	Если НастройкиПоФормуле.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(Раздел.АдресЗапросаКонструктораФормул) Тогда
		Раздел.АдресЗапросаКонструктораФормул = ЗапросКонструктораФормул(
			ВидЭлектронногоДокумента, Формат, Раздел.Тип, УникальныйИдентификатор);
	КонецЕсли;
	
	ТекстЗапроса = ПолучитьИзВременногоХранилища(Раздел.АдресЗапросаКонструктораФормул);
	Если Не ЗначениеЗаполнено(ТекстЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;
	
	СхемаКомпоновкиДанных = Новый СхемаКомпоновкиДанных;
	
	Источник = СхемаКомпоновкиДанных.ИсточникиДанных.Добавить();
	Источник.Имя = "Источник";
	Источник.ТипИсточникаДанных = "Local";
	
	Набор = СхемаКомпоновкиДанных.НаборыДанных.Добавить(Тип("НаборДанныхЗапросСхемыКомпоновкиДанных"));
	Набор.Имя = Источник.Имя;
	Набор.ИсточникДанных = Источник.Имя;
	Набор.АвтоЗаполнениеДоступныхПолей = Истина;
	Набор.Запрос = ТекстЗапроса;
	
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
	
	Для Каждого НастройкаПоля Из НастройкиПоФормуле Цикл
		
		Результат = ЭлектронныеДокументыВнутренний.ПроверитьФормулу(
			СокрЛП(НастройкаПоля.Значение), КомпоновщикНастроек.Настройки.ДоступныеПоляОтбора);
		
		НастройкаПоля.ОшибкаВФормуле = Не Результат;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКоличествоСтрокТаблицыНастроек(ТаблицаФормы)
	
	ЭтаФорма["КоличествоСтрок" + ТаблицаФормы.Имя] = ЭтаФорма[ТаблицаФормы.Имя].Количество();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборПравилЗаполнения(ВыбранноеЗначение, ТекущиеДанные, ИмяТаблицы)
	
	Если ВыбранноеЗначение = "ВручнуюСтрокой" Тогда
		УстановитьВариантВручнуюСтрокой(ВыбранноеЗначение, ТекущиеДанные);
		
	ИначеЕсли ВыбранноеЗначение = "ВручнуюДатой" Тогда
		УстановитьВариантВручнуюДатой(ВыбранноеЗначение, ТекущиеДанные);
		
	ИначеЕсли ВыбранноеЗначение = "ВручнуюЧислом" Тогда
		УстановитьВариантВручнуюЧислом(ВыбранноеЗначение, ТекущиеДанные);
		
	ИначеЕсли ВыбранноеЗначение = "ИзСписка" Тогда
		УстановитьВариантИзСписка(ВыбранноеЗначение, ТекущиеДанные);
		
	ИначеЕсли ВыбранноеЗначение = "ПоФормуле" Тогда
		УстановитьВариантПоФормуле(ВыбранноеЗначение, ТекущиеДанные, ИмяТаблицы);
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВариантВручнуюСтрокой(ВыбранноеЗначение, ТекущиеДанные)
	
	Если ТекущиеДанные.Заполнение <> ВыбранноеЗначение Тогда
		Модифицированность = Истина;
		ТекущиеДанные.Идентификатор = НовыйИдентификаторНастройкиПоля();
	КонецЕсли;
	
	ТекущиеДанные.Правило = ВариантыПравилЗаполнения.НайтиПоЗначению(ВыбранноеЗначение);
	ТекущиеДанные.Заполнение = ВыбранноеЗначение;
	ТекущиеДанные.Значение = "";
	
	ИзменитьВидимостьИнформацииОЗаполненииПолей(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВариантВручнуюДатой(ВыбранноеЗначение, ТекущиеДанные)
	
	Контекст = Новый Структура;
	Контекст.Вставить("ВыбранноеЗначение", ВыбранноеЗначение);
	Контекст.Вставить("ТекущиеДанные", ТекущиеДанные);
	
	Конструктор = Новый КонструкторФорматнойСтроки;
	Конструктор.ДоступныеТипы = Новый ОписаниеТипов("Дата");
	Если ТекущиеДанные.Заполнение = "ВручнуюДатой" Тогда
		Конструктор.Текст = ТекущиеДанные.Значение;
	КонецЕсли;
	
	Если Конструктор.ОткрытьМодально() Тогда
        ПослеРедактированияФорматаЗначения(Конструктор.Текст, Контекст);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВариантВручнуюЧислом(ВыбранноеЗначение, ТекущиеДанные)
	
	Контекст = Новый Структура;
	Контекст.Вставить("ВыбранноеЗначение", ВыбранноеЗначение);
	Контекст.Вставить("ТекущиеДанные", ТекущиеДанные);
	
	Конструктор = Новый КонструкторФорматнойСтроки;
	Конструктор.ДоступныеТипы = Новый ОписаниеТипов("Число");
	Если ТекущиеДанные.Заполнение = "ВручнуюЧислом" Тогда
		Конструктор.Текст = ТекущиеДанные.Значение;
	КонецЕсли;
	
	Если Конструктор.ОткрытьМодально() Тогда
        ПослеРедактированияФорматаЗначения(Конструктор.Текст, Контекст);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВариантИзСписка(ВыбранноеЗначение, ТекущиеДанные)
	
	Контекст = Новый Структура;
	Контекст.Вставить("ВыбранноеЗначение", ВыбранноеЗначение);
	Контекст.Вставить("ТекущиеДанные", ТекущиеДанные);
	
	ПараметрыФормы = Неопределено;
	Если ТекущиеДанные.Заполнение = "ИзСписка"
		И ЗначениеЗаполнено(ТекущиеДанные.Значение) Тогда
		ПараметрыФормы = Новый Структура("ВариантыЗаполнения", ТекущиеДанные.Значение);
	КонецЕсли;
	
	Результат = ОткрытьФормуМодально("Обработка.ЭлектронныеДокументы.Форма.СписокВариантовЗаполненияДополнительногоПоля",
		ПараметрыФормы, ЭтаФорма);
	
	ПослеРедактированияСпискаЗначений(Результат, Контекст);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВариантПоФормуле(ВыбранноеЗначение, ТекущиеДанные, ИмяТаблицы)
	
	Контекст = Новый Структура;
	Контекст.Вставить("ВыбранноеЗначение", ВыбранноеЗначение);
	Контекст.Вставить("ТекущиеДанные", ТекущиеДанные);
	
	ПараметрыОтбора = Новый Структура("ИмяТаблицыНастроек", ИмяТаблицы);
	Разделы = РазделыДополнительныхПолей.НайтиСтроки(ПараметрыОтбора);
	Если Разделы.Количество() Тогда
		Раздел = Разделы[0];
	Иначе
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(Раздел.АдресЗапросаКонструктораФормул) Тогда
		Раздел.АдресЗапросаКонструктораФормул = ЗапросКонструктораФормул(
			ВидЭлектронногоДокумента, Формат, Раздел.Тип, УникальныйИдентификатор);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("АдресНабораОперандов", Раздел.АдресЗапросаКонструктораФормул);
	Если ТекущиеДанные.Заполнение = "ПоФормуле"
		И ЗначениеЗаполнено(ТекущиеДанные.Значение) Тогда
		ПараметрыФормы.Вставить("Формула", ТекущиеДанные.Значение);
	КонецЕсли;
	
	Результат = ОткрытьФормуМодально("Обработка.ЭлектронныеДокументы.Форма.КонструкторФормул", ПараметрыФормы, ЭтаФорма);
	
	ПослеРедактированияФормулы(Результат, Контекст);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗапросКонструктораФормул(Знач ВидЭлектронногоДокумента, Знач Формат, Знач ТипРаздела, Знач УникальныйИдентификатор)
	
	Результат = Неопределено;
	
	ТекстЗапроса = ЭлектронныеДокументыВнутренний.ЗапросКонструктораДополнительныхПолей(ВидЭлектронногоДокумента, Формат, ТипРаздела);
	Если Не ЗначениеЗаполнено(ТекстЗапроса) Тогда
		Возврат Результат;
	КонецЕсли;
	
	ОчиститьПараметрыВТекстеЗапроса(ТекстЗапроса);
	
	Результат = ПоместитьВоВременноеХранилище(ТекстЗапроса, УникальныйИдентификатор);
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ОчиститьПараметрыВТекстеЗапроса(ТекстЗапроса)
	
	Запрос = Новый Запрос(ТекстЗапроса);
	ПараметрыЗапроса = Запрос.НайтиПараметры();
	Для Каждого Параметр Из ПараметрыЗапроса Цикл
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&" + Параметр.Имя, """""");
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеРедактированияФорматаЗначения(Текст, Контекст)
	
	Если Текст = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Контекст.ТекущиеДанные;
	
	Если ТекущиеДанные.Заполнение <> Контекст.ВыбранноеЗначение
		ИЛИ ТекущиеДанные.Значение <> Текст Тогда
		Модифицированность = Истина;
		ТекущиеДанные.Идентификатор = НовыйИдентификаторНастройкиПоля();
	КонецЕсли;
	
	ПредставлениеВарианта = ВариантыПравилЗаполнения.НайтиПоЗначению(Контекст.ВыбранноеЗначение);
	Если ПустаяСтрока(Текст) Тогда
		Правило = ПредставлениеВарианта;
	Иначе
		Правило = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 в формате: %2'"), ПредставлениеВарианта, Текст);
	КонецЕсли;
	
	ТекущиеДанные.Правило = Правило;
	ТекущиеДанные.Заполнение = Контекст.ВыбранноеЗначение;
	ТекущиеДанные.Значение = Текст;
	
	ИзменитьВидимостьИнформацииОЗаполненииПолей(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеРедактированияСпискаЗначений(Результат, Контекст)
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	ТекущиеДанные = Контекст.ТекущиеДанные;
	ТекущиеДанные.Идентификатор = НовыйИдентификаторНастройкиПоля();
	
	ПредставлениеВарианта = ВариантыПравилЗаполнения.НайтиПоЗначению(Контекст.ВыбранноеЗначение);
	ТекущиеДанные.Правило = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1: %2'"), ПредставлениеВарианта, Результат.Представление);
	ТекущиеДанные.Заполнение = Контекст.ВыбранноеЗначение;
	ТекущиеДанные.Значение = Результат.Значение;
	
	ИзменитьВидимостьИнформацииОЗаполненииПолей(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеРедактированияФормулы(Результат, Контекст)
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Контекст.ТекущиеДанные;
	
	Если ТекущиеДанные.Заполнение <> Контекст.ВыбранноеЗначение
		ИЛИ ТекущиеДанные.Значение <> Результат Тогда
		Модифицированность = Истина;
		ТекущиеДанные.Идентификатор = НовыйИдентификаторНастройкиПоля();
	КонецЕсли;
	
	ТекущиеДанные.Правило = ВариантыПравилЗаполнения.НайтиПоЗначению(Контекст.ВыбранноеЗначение);
	ТекущиеДанные.Заполнение = Контекст.ВыбранноеЗначение;
	ТекущиеДанные.Значение = Результат;
	ТекущиеДанные.ОшибкаВФормуле = Ложь;
	
	ИзменитьВидимостьИнформацииОЗаполненииПолей(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ИзменитьВидимостьИнформацииОЗаполненииПолей(Форма)
	
	Элементы = Форма.Элементы;
	
	ОтображатьИнформацию = Ложь;
	
	Для Каждого Раздел Из Форма.РазделыДополнительныхПолей Цикл
		
		Если ТребуетсяЗаполнениеПолейВручную(Форма, Раздел.ИмяТаблицыНастроек) Тогда
			ОтображатьИнформацию = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Элементы.КартинкаИнформация.Видимость = ОтображатьИнформацию;
	Элементы.НадписьИнформация.Видимость = ОтображатьИнформацию;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ТребуетсяЗаполнениеПолейВручную(Форма, ИмяТаблицыНастроек)
	
	Результат = Ложь;
	
	ТаблицаНастроек = Форма[ИмяТаблицыНастроек];
	КоличествоСтрок = ТаблицаНастроек.Количество();
	Если КоличествоСтрок > 0
		И ТаблицаНастроек.НайтиСтроки(Новый Структура("Заполнение", "ПоФормуле")).Количество() <> КоличествоСтрок Тогда
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПроверитьЗаполнениеОбязательныхПолей(Отказ)
	
	ШаблонСообщения = НСтр("ru = 'Не заполнена колонка ""%1"" в строке %2 списка ""%3""'");
	Для Каждого Раздел Из РазделыДополнительныхПолей Цикл
		
		ПредставлениеСписка = Раздел.Представление;
		ТаблицаНастроек = ЭтаФорма[Раздел.ИмяТаблицыНастроек];
		НомерСтроки = 1;
		
		Для Каждого СтрокаТаблицы Из ТаблицаНастроек Цикл
			
			Если Не ЗначениеЗаполнено(СтрокаТаблицы.Имя) Тогда
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ШаблонСообщения, НСтр("ru = 'Имя поля'"), НомерСтроки, ПредставлениеСписка);
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(Раздел.ИмяТаблицыНастроек, НомерСтроки, "Имя");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,Поле,,Отказ);
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(СтрокаТаблицы.Правило) Тогда
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ШаблонСообщения, НСтр("ru = 'Правила заполнения'"), НомерСтроки, ПредставлениеСписка);
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(Раздел.ИмяТаблицыНастроек, НомерСтроки, "Правило");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,Поле,,Отказ);
			КонецЕсли;
			
			Если СтрокаТаблицы.Заполнение = "ПоФормуле"
				И СтрокаТаблицы.ОшибкаВФормуле Тогда
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Некорректно заполнена формула в строке %1 списка ""%2""'"),
					НомерСтроки, ПредставлениеСписка);
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(Раздел.ИмяТаблицыНастроек, НомерСтроки, "Правило");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,Поле,,Отказ);
			КонецЕсли;
			
			НомерСтроки = НомерСтроки + 1;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьКорректностьИменПолей(Отказ)
	
	Для Каждого Раздел Из РазделыДополнительныхПолей Цикл
		
		ПредставлениеСписка = Раздел.Представление;
		ТаблицаНастроек = ЭтаФорма[Раздел.ИмяТаблицыНастроек];
		НомерСтроки = 1;
		
		ШаблонСообщения = НСтр("ru = 'Имя поля совпадает со служебным в строке %1 списка ""%2""'");
		Для Каждого СтрокаТаблицы Из ТаблицаНастроек Цикл
			
			Если ЭлектронныеДокументыВнутренний.ЭтоСлужебноеИмяДополнительногоПоля(СтрокаТаблицы.Имя, Формат) Тогда
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ШаблонСообщения, НомерСтроки, ПредставлениеСписка);
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(Раздел.ИмяТаблицыНастроек, НомерСтроки, "Имя");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,Поле,,Отказ);
			КонецЕсли;
			
			НомерСтроки = НомерСтроки + 1;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция НастройкаЗаполненияДополнительныхПолейДокумента()
	
	НастройкиПолей = Новый Массив;
	
	Для Каждого Раздел Из РазделыДополнительныхПолей Цикл
		
		ТаблицаНастроек = ЭтаФорма[Раздел.ИмяТаблицыНастроек];
		Если ТаблицаНастроек.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого СтрокаТаблицы Из ТаблицаНастроек Цикл
			
			НастройкаПоля = Новый Структура;
			НастройкаПоля.Вставить("Идентификатор", СтрокаТаблицы.Идентификатор);
			НастройкаПоля.Вставить("Имя",           СтрокаТаблицы.Имя);
			НастройкаПоля.Вставить("Представление", СтрокаТаблицы.Представление);
			НастройкаПоля.Вставить("Описание",      СтрокаТаблицы.Описание);
			НастройкаПоля.Вставить("Правило",       СтрокаТаблицы.Правило);
			НастройкаПоля.Вставить("Заполнение",    СтрокаТаблицы.Заполнение);
			НастройкаПоля.Вставить("Значение",      СтрокаТаблицы.Значение);
			НастройкаПоля.Вставить("Раздел",        Раздел.Имя);
			
			НастройкиПолей.Добавить(НастройкаПоля);
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат НастройкиПолей;
	
КонецФункции

&НаСервере
Процедура СохранитьНастройкуЗаполненияПолей(Отказ)
	
	ПроверитьКорректностьИменПолей(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	НастройкаЗаполнения = НастройкаЗаполненияДополнительныхПолейДокумента();
	
	Если Не ЗначениеЗаполнено(НастройкаЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	НастройкаЗаполненияJSON = ЭлектронныеДокументыСлужебный.ЗначениеВСтрокуJSON(НастройкаЗаполнения);
	
	МенеджерЗаписи = РегистрыСведений.НастройкиЗаполненияДополнительныхПолей.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.НастройкаЭДО = НастройкаЭДО;
	МенеджерЗаписи.ВидЭлектронногоДокумента = ВидЭлектронногоДокумента;
	МенеджерЗаписи.Формат = Формат;
	МенеджерЗаписи.Настройка = Новый ХранилищеЗначения(НастройкаЗаполненияJSON, Новый СжатиеДанных(9));
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

&НаСервере
Процедура УдалитьНастройкуЗаполненияПолей()
	
	РегистрыСведений.НастройкиЗаполненияДополнительныхПолей.УдалитьНастройкуЗаполненияПолей(
		НастройкаЭДО, ВидЭлектронногоДокумента, Формат);
	
КонецПроцедуры

&НаСервере
Функция АдресВыгрузкиНастройкиЗаполненияДопПолей()
	
	НастройкаЗаполнения = НастройкаЗаполненияДополнительныхПолейДокумента();
	
	ИндексПеречисления = Перечисления.ВидыЭД.Индекс(ВидЭлектронногоДокумента);
	ИмяПеречисления = ВидЭлектронногоДокумента.Метаданные().ЗначенияПеречисления[ИндексПеречисления].Имя;
	
	ПараметрыНастройки = Новый Структура;
	ПараметрыНастройки.Вставить("ВидЭД",  ИмяПеречисления);
	ПараметрыНастройки.Вставить("Формат", Формат);
	ПараметрыНастройки.Вставить("Поля", НастройкаЗаполнения);
	
	СтрокаJSON = ЭлектронныеДокументыСлужебный.ЗначениеВСтрокуJSON(ПараметрыНастройки);
	
	Возврат ПоместитьВоВременноеХранилище(СтрокаJSON, УникальныйИдентификатор);
	
КонецФункции

&НаКлиенте
Процедура ЗагрузитьДанныеИзФайла()
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.Заголовок          = НСтр("ru = 'Выберите файл для загрузки'"); 
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
	ДиалогВыбораФайла.Фильтр             = НСтр("ru = 'Файл json|*.json'");
	ДиалогВыбораФайла.Расширение         = "json";
	
	ПомещенныеФайлы = Новый Массив;
	Если ПоместитьФайлы(,ПомещенныеФайлы, ДиалогВыбораФайла,,УникальныйИдентификатор) Тогда
		ЗагрузитьДанныеПослеВыбораВДиалоге(ПомещенныеФайлы);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанныеПослеВыбораВДиалоге(ПомещенныеФайлы)
	
	Если ЗначениеЗаполнено(ПомещенныеФайлы) Тогда
		ЗагрузитьДанныеНаСервере(ПомещенныеФайлы[0].Хранение);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДанныеНаСервере(ДанныеФайла)
	
	ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(ДанныеФайла);
	ИмяФайла = ПолучитьИмяВременногоФайла();
	ДвоичныеДанныеФайла.Записать(ИмяФайла);
	
	СтруктураДанных = Неопределено;
	
	Попытка
		ЧтениеТекста = Новый ЧтениеТекста(ИмяФайла, КодировкаТекста.UTF8);
		СтрокаJSON = ЧтениеТекста.Прочитать();
		СтруктураДанных = ЭлектронныеДокументыСлужебный.ЗначениеИзСтрокиJSON(СтрокаJSON);
 	Исключение
		ЭлектронныеДокументыСлужебный.ВыполнитьЗаписьСобытияПоЭДВЖурналРегистрации(
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()), 2);
	КонецПопытки;

	ЧтениеТекста.Закрыть();
	УдалитьФайлы(ИмяФайла);
	
	Если СтруктураДанныхФайлаНекорректна(СтруктураДанных) Тогда
		Возврат;
	КонецЕсли;
	
	НастройкаЗаполнения = ЭлектронныеДокументыВнутренний.НоваяНастройкаЗаполненияДополнительныхПолей(СтруктураДанных.Поля);
	
	ЗаполнитьТаблицыНастроекПоРазделам(НастройкаЗаполнения);
	
	ИзменитьВидимостьИнформацииОЗаполненииПолей(ЭтаФорма);
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Функция НовыйИдентификаторНастройкиПоля()
	
	Возврат "f" + СтрЗаменить(Новый УникальныйИдентификатор, "-", "");
	
КонецФункции

&НаСервере
Функция СтруктураДанныхФайлаНекорректна(СтруктураДанных)
	
	Если Не ЗначениеЗаполнено(СтруктураДанных)
		ИЛИ Не СтруктураДанных.Свойство("Поля") Тогда
		СообщитьОбОшибкеЧтенияНастройкиИзФайла();
		Возврат Истина;
	КонецЕсли;
	
	СопоставлениеРазделов = Новый Соответствие;
	
	Для Каждого Раздел Из РазделыДополнительныхПолей Цикл
		СопоставлениеРазделов.Вставить(Раздел.Имя, Истина);
	КонецЦикла;
	
	ЕстьСопоставленныеРазделы = Ложь;
	НезагруженныеНастройкиПолей = Новый Массив;
	
	Для Каждого НастройкаПоля Из СтруктураДанных.Поля Цикл
		ИмяРаздела = Неопределено;
		Если Не НастройкаПоля.Свойство("Раздел", ИмяРаздела)
			ИЛИ Не ЗначениеЗаполнено(ИмяРаздела) Тогда
			Продолжить;
		КонецЕсли;
		
		Если СопоставлениеРазделов[ИмяРаздела] <> Неопределено Тогда
			ЕстьСопоставленныеРазделы = Истина;
		ИначеЕсли НастройкаПоля.Свойство("Имя")
			И ЗначениеЗаполнено(НастройкаПоля.Имя) Тогда
			НезагруженныеНастройкиПолей.Добавить(НастройкаПоля.Имя);
		КонецЕсли;
	КонецЦикла;
	
	Если Не ЕстьСопоставленныеРазделы Тогда
		СообщитьОбОшибкеЧтенияНастройкиИзФайла();
		Возврат Истина;
	КонецЕсли;
	
	Если НезагруженныеНастройкиПолей.Количество() Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось загрузить настройки по следующим полям: %1.'"),
			СтроковыеФункцииКлиентСервер.СтрокаИзМассиваПодстрок(НезагруженныеНастройкиПолей, ", "));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Процедура СообщитьОбОшибкеЧтенияНастройкиИзФайла()
	ТекстСообщения = НСтр("ru = 'Ошибка при чтении файла настроек заполнения дополнительных полей.
		|Неизвестный формат файла.'");
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
КонецПроцедуры
