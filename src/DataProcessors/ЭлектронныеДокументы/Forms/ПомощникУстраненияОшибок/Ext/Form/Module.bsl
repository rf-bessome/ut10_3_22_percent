////////////////////////////////////////////////////////////////////////////////
// ОписаниеПеременных

&НаКлиенте
Перем ИзмененыНастройки;

////////////////////////////////////////////////////////////////////////////////
// ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УсловноеОформление.Элементы.Очистить();
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("АдресСведенийОбОшибках") Тогда
		ОшибкиПриФормированииДокументов = ПолучитьИзВременногоХранилища(Параметры.АдресСведенийОбОшибках);
		УдалитьИзВременногоХранилища(Параметры.АдресСведенийОбОшибках);
		
		ОшибкиПриФормированииДокумента = ОшибкиПриФормированииДокументов[0];
		ДанныеДляОбработкиОшибок = ОшибкиПриФормированииДокумента.ДанныеДляОбработкиОшибок;
		
		Документ                 = ДанныеДляОбработкиОшибок.Документ;
		Контрагент               = ДанныеДляОбработкиОшибок.Контрагент;
		НастройкаЭДО             = ДанныеДляОбработкиОшибок.НастройкаЭДО;
		ВидЭлектронногоДокумента = ДанныеДляОбработкиОшибок.ВидЭлектронногоДокумента;
		Формат                   = ДанныеДляОбработкиОшибок.Формат;
		ОшибкиЗаполнения         = ОшибкиПриФормированииДокумента.ОшибкиЗаполнения;
		
		ДополнительныеДанные = Неопределено;
		Если ОшибкиПриФормированииДокумента.Свойство("ДополнительныеДанные", ДополнительныеДанные) Тогда
			КэшДополнительныхДанных = Новый ФиксированнаяСтруктура(ДополнительныеДанные);
		КонецЕсли;
	Иначе
		Документ                 = Параметры.Документ;
		Контрагент               = Параметры.Контрагент;
		НастройкаЭДО             = Параметры.НастройкаЭДО;
		ВидЭлектронногоДокумента = Параметры.ВидЭлектронногоДокумента;
		Формат                   = Параметры.Формат;
		ОшибкиЗаполнения         = Параметры.ОшибкиЗаполнения;
	КонецЕсли;
	
	Параметры.Свойство("Действие", Действие);
	ПодписатьОтправить = Найти(Действие, "Подписать") > 0;
	
	ПодготовитьДополнительныеПоляДляЗаполнения(ОшибкиЗаполнения.ДополнительныеПоля);
	
	Заголовок = НСтр("ru = 'Помощник заполнения данных'");
	
	Если ПодписатьОтправить Тогда
		Элементы.Готово.Заголовок = НСтр("ru = 'Сформировать и отправить документ'");
	КонецЕсли;
	
	Элементы.КартинкаИнформация.Видимость = Ложь;
	Элементы.НадписьИзмененыНастройки.Видимость = Ложь;
	
	СброситьРазмерыИПоложениеОкна();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПоказатьОшибкиВычисленияДополнительныхПолей();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ИсточникВыбора.ИмяФормы = "Обработка.ЭлектронныеДокументы.Форма.НастройкаЗаполненияДополнительныхПолей" Тогда
		ОбработатьЗавершениеНастройкиЗаполненияДополнительныхПолей(ВыбранноеЗначение);
	КонецЕсли;	
		
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОбработчикиСобытийЭлементовТаблицыФормыРазделДополнительныхПолей

&НаКлиенте
Процедура Подключаемый_ЭлементРазделаДополнительныхПолейОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ПредставлениеЗначения = Элемент.СписокВыбора.НайтиПоЗначению(ВыбранноеЗначение);
	Элемент.Родитель.ТекущиеДанные[Элемент.Имя + "Представление"] = ПредставлениеЗначения;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	Результат = Новый Структура;
	Результат.Вставить("Документ", Документ);
	Результат.Вставить("Действие", Действие);
	
	Если ИзмененыНастройки <> Неопределено Тогда
		Результат.Вставить("ДополнительныеДанные");
		
	ИначеЕсли ЗначениеЗаполнено(ИменаРеквизитовПоРазделам) Тогда
		Отказ = Ложь;
		ПроверитьЗаполнениеДополнительныхПолей(Отказ);
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
		ДополнительныеДанные = ДанныеДополнительныхПолей();
		Если ЗначениеЗаполнено(ДополнительныеДанные) Тогда
			Результат.Вставить("ДополнительныеДанные", ДополнительныеДанные);
		КонецЕсли;
		
	ИначеЕсли КэшДополнительныхДанных <> Неопределено Тогда
		Результат.Вставить("ДополнительныеДанные", Новый Структура(КэшДополнительныхДанных));
		
	КонецЕсли;
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаДополнительныхПолей(Команда)

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаЭДО", НастройкаЭДО);
	ПараметрыФормы.Вставить("ВидЭлектронногоДокумента", ВидЭлектронногоДокумента);
	ПараметрыФормы.Вставить("Формат", Формат);
	
	ОткрытьФорму("Обработка.ЭлектронныеДокументы.Форма.НастройкаЗаполненияДополнительныхПолей", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьДополнительныеПоляДляЗаполнения(ДополнительныеПоля)
	
	МассивРеквизитовПоРазделам = Новый Массив;
	
	ТекстОшибки = Неопределено;
	
	НастройкаЗаполнения = ЭлектронныеДокументыВнутренний.НастройкаЗаполненияДополнительныхПолей(
		НастройкаЭДО, ВидЭлектронногоДокумента, Формат, ТекстОшибки);
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	РазделыДополнительныхПолей = ЭлектронныеДокументыВнутренний.РазделыДополнительныхПолейФорматаЭлектронногоДокумента(
		ВидЭлектронногоДокумента, Формат);
	
	НастройкиПолейРазделовШапки  = Новый Соответствие;
	НастройкиПолейРазделовТаблиц = Новый Соответствие;
	
	ДобавляемыеРеквизиты = Новый Массив;
	
	Для Каждого Раздел Из РазделыДополнительныхПолей Цикл
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Раздел", Раздел.Имя);
		НастройкиПолей = НастройкаЗаполнения.НайтиСтроки(ПараметрыОтбора);
		Если НастройкиПолей.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если Раздел.Тип = ЭлектронныеДокументыКлиентСервер.ТипРазделаДополнительныхПолейШапка() Тогда
			
			НастройкиПолейРазделовШапки.Вставить(Раздел, НастройкиПолей);
			
			СоздатьРеквизитыДополнительныхПолейШапки(НастройкиПолей, ДобавляемыеРеквизиты);
			
		ИначеЕсли Раздел.Тип = ЭлектронныеДокументыКлиентСервер.ТипРазделаДополнительныхПолейТаблица() Тогда
			
			НастройкиПолейРазделовТаблиц.Вставить(Раздел, НастройкиПолей);
			
			СоздатьРеквизитыДополнительныхПолейТаблицы(НастройкиПолей, ДобавляемыеРеквизиты);
			
		КонецЕсли;
		
		ЗаполнитьИменаРеквизитовПоРазделу(МассивРеквизитовПоРазделам, НастройкиПолей, Раздел);
		
	КонецЦикла;
	
	Если ДобавляемыеРеквизиты.Количество() Тогда
		ИзменитьРеквизиты(ДобавляемыеРеквизиты);
		ИменаРеквизитовПоРазделам = Новый ФиксированныйМассив(МассивРеквизитовПоРазделам);
	Иначе
		Возврат;
	КонецЕсли;
	
	НесколькоРазделовШапки  = НастройкиПолейРазделовШапки.Количество() > 1;
	НесколькоРазделовТаблиц = НастройкиПолейРазделовТаблиц.Количество()> 1;
	
	Если НесколькоРазделовШапки И НесколькоРазделовТаблиц Тогда
		ГруппаРодительПолейШапки = Элементы.ГруппаРазделовОбщая;
		ГруппаРодительПолейТаблицы = Элементы.ГруппаРазделовОбщая;
	Иначе
		ГруппаРодительПолейШапки   = Элементы.ГруппаРазделовШапка;
		ГруппаРодительПолейТаблицы = Элементы.ГруппаРазделовТаблица;
		ГруппаРодительПолейШапки.ОтображениеСтраниц = ?(НесколькоРазделовШапки,
			ОтображениеСтраницФормы.ЗакладкиСверху, ОтображениеСтраницФормы.Нет);
		ГруппаРодительПолейТаблицы.ОтображениеСтраниц = ?(НесколькоРазделовТаблиц,
			ОтображениеСтраницФормы.ЗакладкиСверху, ОтображениеСтраницФормы.Нет);
	КонецЕсли;
	
	Для Каждого НастройкиПолейРаздела Из НастройкиПолейРазделовШапки Цикл
		ИмяРаздела = НастройкиПолейРаздела.Ключ.Имя;
		
		ДанныеРаздела = Неопределено;
		ДополнительныеПоля.Данные.Свойство(ИмяРаздела, ДанныеРаздела);
		
		ОшибкиРаздела = Неопределено;
		ДополнительныеПоля.Ошибки.Свойство(ИмяРаздела, ОшибкиРаздела);
		
		СоздатьЭлементыДополнительныхПолейШапки(ГруппаРодительПолейШапки, НастройкиПолейРаздела);
		ЗаполнитьДанныеДополнительныхПолейШапки(ДанныеРаздела);
		ЗаполнитьОшибкиВычисленияДополнительныхПолейШапки(ОшибкиРаздела);
	КонецЦикла;
	
	Для Каждого НастройкиПолейРаздела Из НастройкиПолейРазделовТаблиц Цикл
		ИмяРаздела = НастройкиПолейРаздела.Ключ.Имя;
		
		ДанныеРаздела = Неопределено;
		ДополнительныеПоля.Данные.Свойство(ИмяРаздела, ДанныеРаздела);
		
		ОшибкиРаздела = Неопределено;
		ДополнительныеПоля.Ошибки.Свойство(ИмяРаздела, ОшибкиРаздела);
	
		СоздатьЭлементыДополнительныхПолейТаблицы(ГруппаРодительПолейТаблицы, НастройкиПолейРаздела);
		ЗаполнитьДанныеДополнительныхПолейТаблицы(ДанныеРаздела, НастройкиПолейРаздела);
		ЗаполнитьОшибкиВычисленияДополнительныхПолейТаблицы(ОшибкиРаздела, НастройкиПолейРаздела);
	КонецЦикла;
	
	ЗаголовокНадписи = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1 требует заполнения дополнительных данных в электронных документах.'"), Контрагент);
	
	Если ЭлектронныеДокументыСлужебныйВызовСервера.ЕстьПравоНастройкиЭДО(Ложь) Тогда
		ЗаголовокНадписи = ЗаголовокНадписи + Символы.ПС
			+ НСтр("ru = 'Заполните их вручную или настройте правила автоматического заполнения'");
	КонецЕсли;
	
	Элементы.НадписьТребование.Заголовок = ЗаголовокНадписи;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьРеквизитыДополнительныхПолейШапки(НастройкиПолей, ДобавляемыеРеквизиты)
	
	Для Каждого НастройкаПоля Из НастройкиПолей Цикл
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(
			НастройкаПоля.Идентификатор, ТипРеквизитаПоВариантуЗаполнения(НастройкаПоля)));
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьРеквизитыДополнительныхПолейТаблицы(НастройкиПолей, ДобавляемыеРеквизиты)
	
	ИмяТаблицыРаздела = "Таблица" + НастройкиПолей[0].Раздел;
	
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(
		ИмяТаблицыРаздела, Новый ОписаниеТипов("ТаблицаЗначений")));
	
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("НомерСтроки", Новый ОписаниеТипов("Число",
		Новый КвалификаторыЧисла(3, 0, ДопустимыйЗнак.Неотрицательный)), ИмяТаблицыРаздела));
		
	Если НастройкиПолей[0].Раздел = "ТоварыУслуги" Тогда
		ТипРеквизита = Новый ОписаниеТипов("СправочникСсылка."
			+ ЭлектронныеДокументыПовтИсп.ПолучитьИмяПрикладногоСправочника("Номенклатура"));	
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("Номенклатура", ТипРеквизита,	ИмяТаблицыРаздела));
		
		ТипРеквизита = Новый ОписаниеТипов("СправочникСсылка."
			+ ЭлектронныеДокументыПовтИсп.ПолучитьИмяПрикладногоСправочника("ХарактеристикиНоменклатуры"));	
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("Характеристика", ТипРеквизита,	ИмяТаблицыРаздела));
		
		ТипРеквизита = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Любой));	
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("Сумма", ТипРеквизита, ИмяТаблицыРаздела));
	КонецЕсли;
	
	Для Каждого НастройкаПоля Из НастройкиПолей Цикл
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(НастройкаПоля.Идентификатор,
			ТипРеквизитаПоВариантуЗаполнения(НастройкаПоля), ИмяТаблицыРаздела));
		Если НастройкаПоля.Заполнение = "ИзСписка" Тогда
			ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(НастройкаПоля.Идентификатор + "Представление",
				Новый ОписаниеТипов("Строка"), ИмяТаблицыРаздела));
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ТипРеквизитаПоВариантуЗаполнения(НастройкаПоля)
	
	Если НастройкаПоля.Заполнение = "ВручнуюДатой" Тогда
		ТипРеквизита = Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя));
	ИначеЕсли НастройкаПоля.Заполнение = "ВручнуюЧислом" Тогда
		Если ЗначениеЗаполнено(НастройкаПоля.Значение) Тогда
			ПараметрыЧисла = СтроковыеФункцииКлиентСервер.ПолучитьПараметрыИзСтроки(
				СтрЗаменить(НастройкаПоля.Значение,"'",""));
				
			Длина = Неопределено;
			Если Не ПараметрыЧисла.Свойство("ЧЦ", Длина) Тогда
				Длина = 15;
			КонецЕсли;
			
			Точность = Неопределено;
			Если Не ПараметрыЧисла.Свойство("ЧДЦ", Точность) Тогда
				Точность = 0;
			КонецЕсли;
			
			ТипРеквизита = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(Длина, Точность));
		Иначе
			ТипРеквизита = Новый ОписаниеТипов("Число");
		КонецЕсли;
	Иначе
		ТипРеквизита = Новый ОписаниеТипов("Строка");
	КонецЕсли;
	
	Возврат ТипРеквизита;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьИменаРеквизитовПоРазделу(МассивРеквизитовПоРазделам, НастройкиПолей, Раздел)
	
	ИменаРеквизитов = Новый Массив;
	Для Каждого НастройкаПоля Из НастройкиПолей Цикл
		ИменаРеквизитов.Добавить(НастройкаПоля.Идентификатор);
	КонецЦикла;
	
	РеквизитыРаздела = Новый Структура;
	РеквизитыРаздела.Вставить("ИмяРаздела", Раздел.Имя);
	РеквизитыРаздела.Вставить("ТипРаздела", Раздел.Тип);
	РеквизитыРаздела.Вставить("ИменаРеквизитов", ИменаРеквизитов);
	
	МассивРеквизитовПоРазделам.Добавить(РеквизитыРаздела);
	
КонецПроцедуры

&НаСервере
Процедура СоздатьЭлементыДополнительныхПолейШапки(ГруппаРодительПолейШапки, НастройкиПолейРаздела)
	
	Раздел = НастройкиПолейРаздела.Ключ;
	НастройкиПолей = НастройкиПолейРаздела.Значение;
	
	СтраницаРаздела = Элементы.Добавить("Страница" + Раздел.Имя, Тип("ГруппаФормы"), ГруппаРодительПолейШапки);
	СтраницаРаздела.Вид = ВидГруппыФормы.Страница;
	СтраницаРаздела.Заголовок = Раздел.Представление;
	СтраницаРаздела.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	
	ГруппаРазделаЛево = Элементы.Добавить("ГруппаЛево" + Раздел.Имя, Тип("ГруппаФормы"), СтраницаРаздела);
	ГруппаРазделаЛево.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаРазделаЛево.ОтображатьЗаголовок = Ложь;
	ГруппаРазделаЛево.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаРазделаЛево.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	ГруппаРазделаПраво = Элементы.Добавить("ГруппаПраво" + Раздел.Имя, Тип("ГруппаФормы"), СтраницаРаздела);
	ГруппаРазделаПраво.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаРазделаПраво.ОтображатьЗаголовок = Ложь;
	ГруппаРазделаПраво.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаРазделаПраво.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	КоличествоЭлементовВГруппе = Окр(НастройкиПолей.Количество() / 2);
	
	НомерЭлемента = 1;
	
	Для Каждого НастройкаПоля Из НастройкиПолей Цикл
		ГруппаЭлементов = ?(НомерЭлемента <= КоличествоЭлементовВГруппе, ГруппаРазделаЛево, ГруппаРазделаПраво);
		ИмяЭлемента = НастройкаПоля.Идентификатор;
		ЭлементГруппы = Элементы.Добавить(ИмяЭлемента, Тип("ПолеФормы"), ГруппаЭлементов);
		ЗаполнитьСвойстваПоляФормы(ЭлементГруппы, НастройкаПоля, ИмяЭлемента);
		
		НомерЭлемента = НомерЭлемента + 1;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьЭлементыДополнительныхПолейТаблицы(ГруппаРодительПолейТаблицы, НастройкиПолейРаздела)
	
	Раздел = НастройкиПолейРаздела.Ключ;
	НастройкиПолей = НастройкиПолейРаздела.Значение;
	
	СтраницаРаздела = Элементы.Добавить("Страница" + Раздел.Имя, Тип("ГруппаФормы"), ГруппаРодительПолейТаблицы);
	СтраницаРаздела.Вид = ВидГруппыФормы.Страница;
	СтраницаРаздела.Заголовок = Раздел.Представление;
	СтраницаРаздела.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	ИмяТаблицыРаздела = "Таблица" + Раздел.Имя;
	ТаблицаРаздела = Элементы.Добавить(ИмяТаблицыРаздела, Тип("ТаблицаФормы"), СтраницаРаздела);
	ТаблицаРаздела.ПутьКДанным = ИмяТаблицыРаздела;
	ТаблицаРаздела.ИзменятьСоставСтрок  = Ложь;
	ТаблицаРаздела.ИзменятьПорядокСтрок = Ложь;
	ТаблицаРаздела.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиЭлементаФормы.Нет;
	
	КолонкаТаблицы = Элементы.Добавить(ИмяТаблицыРаздела + "НомерСтроки", Тип("ПолеФормы"), ТаблицаРаздела);
	КолонкаТаблицы.ПутьКДанным = ИмяТаблицыРаздела + ".НомерСтроки";
	КолонкаТаблицы.Заголовок = НСтр("ru = 'N'");
	
	Если Раздел.Имя = "ТоварыУслуги" Тогда
		ГруппаКолонок = Элементы.Добавить(ИмяТаблицыРаздела + "ГруппаНоменклатура", Тип("ГруппаФормы"), ТаблицаРаздела);
		ГруппаКолонок.Группировка = ГруппировкаКолонок.ВЯчейке;
		
		КолонкаТаблицы = Элементы.Добавить(ИмяТаблицыРаздела + "Номенклатура", Тип("ПолеФормы"), ГруппаКолонок);
		КолонкаТаблицы.ПутьКДанным = ИмяТаблицыРаздела + ".Номенклатура";
		
		КолонкаТаблицы = Элементы.Добавить(ИмяТаблицыРаздела + "Характеристика", Тип("ПолеФормы"), ГруппаКолонок);
		КолонкаТаблицы.ПутьКДанным = ИмяТаблицыРаздела + ".Характеристика";
		КолонкаТаблицы.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		
		КолонкаТаблицы = Элементы.Добавить(ИмяТаблицыРаздела + "Сумма", Тип("ПолеФормы"), ТаблицаРаздела);
		КолонкаТаблицы.ПутьКДанным = ИмяТаблицыРаздела + ".Сумма";
		КолонкаТаблицы.Ширина = 7;
	КонецЕсли;
	
	Для Каждого НастройкаПоля Из НастройкиПолей Цикл
		КолонкаТаблицы = Элементы.Добавить(НастройкаПоля.Идентификатор, Тип("ПолеФормы"), ТаблицаРаздела);
		ЗаполнитьСвойстваПоляФормы(КолонкаТаблицы, НастройкаПоля, ИмяТаблицыРаздела + "." + НастройкаПоля.Идентификатор);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСвойстваПоляФормы(Элемент, НастройкаПоля, ПутьКДанным)
	
	Элемент.Вид = ВидПоляФормы.ПолеВвода;
	Элемент.ПутьКДанным = ПутьКДанным;
	Если ЗначениеЗаполнено(НастройкаПоля.Представление) Тогда
		Элемент.Заголовок = НастройкаПоля.Представление;
	Иначе
		Элемент.Заголовок = НастройкаПоля.Имя;
	КонецЕсли;
	Элемент.Подсказка = НастройкаПоля.Описание;
	Элемент.АвтоОтметкаНезаполненного = Истина;
	Если НастройкаПоля.Заполнение = "ВручнуюДатой"
		ИЛИ НастройкаПоля.Заполнение = "ВручнуюЧислом" Тогда
		Элемент.Формат = НастройкаПоля.Значение;
		Элемент.ФорматРедактирования = НастройкаПоля.Значение;
	ИначеЕсли НастройкаПоля.Заполнение = "ИзСписка" Тогда
		Элемент.РежимВыбораИзСписка = Истина;
		Если ЗначениеЗаполнено(НастройкаПоля.Значение) Тогда
			Для Каждого Вариант Из НастройкаПоля.Значение Цикл
				ЗаполнитьЗначенияСвойств(Элемент.СписокВыбора.Добавить(), Вариант);
			КонецЦикла;
		КонецЕсли;
		Если Найти(ПутьКДанным, ".") Тогда
			Элемент.УстановитьДействие("ОбработкаВыбора", "Подключаемый_ЭлементРазделаДополнительныхПолейОбработкаВыбора");
			УстановитьУсловноеОформлениеДляЭлементаСВыборомИзСписка(Элемент);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеДляЭлементаСВыборомИзСписка(Элемент)
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элемент.Имя);
	
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(Элемент.ПутьКДанным + "Представление");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст", ОтборЭлемента.ЛевоеЗначение);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеДополнительныхПолейШапки(ДанныеРаздела)
	
	Если Не ЗначениеЗаполнено(ДанныеРаздела) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, ДанныеРаздела);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеДополнительныхПолейТаблицы(ДанныеРаздела, НастройкиПолейРаздела)
	
	Если Не ЗначениеЗаполнено(ДанныеРаздела) Тогда
		Возврат;
	КонецЕсли;
	
	Раздел = НастройкиПолейРаздела.Ключ;
	ИмяТаблицыРаздела = "Таблица" + Раздел.Имя;
	
	НомерСтроки = 1;
	
	Для Каждого ДанныеСтроки Из ДанныеРаздела Цикл
		
		НоваяСтрока = ЭтаФорма[ИмяТаблицыРаздела].Добавить();
		
		ЗначениеСвойства = Неопределено;
		Если ДанныеСтроки.Свойство("КлючСтроки", ЗначениеСвойства) Тогда
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ЗначениеСвойства);
		КонецЕсли;
		
		Если ДанныеСтроки.Свойство("ЗначенияПолей", ЗначениеСвойства) Тогда
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ЗначениеСвойства);
		КонецЕсли;
		
		НоваяСтрока.НомерСтроки = НомерСтроки;
		
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОшибкиВычисленияДополнительныхПолейШапки(ОшибкиРаздела)
	
	Если Не ЗначениеЗаполнено(ОшибкиРаздела) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Ошибка Из ОшибкиРаздела Цикл
		ОшибкиВычисленияДополнительныхПолей.Добавить(Ошибка.Ключ, Ошибка.Значение);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОшибкиВычисленияДополнительныхПолейТаблицы(ОшибкиРаздела, НастройкиПолейРаздела)
	
	Если Не ЗначениеЗаполнено(ОшибкиРаздела) Тогда
		Возврат;
	КонецЕсли;
	
	Раздел = НастройкиПолейРаздела.Ключ;
	ИмяТаблицыРаздела = "Таблица" + Раздел.Имя;
	
	Для Каждого ОшибкиПоСтроке Из ОшибкиРаздела Цикл
		Для Каждого Ошибка Из ОшибкиПоСтроке.Значение Цикл
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТаблицыРаздела, ОшибкиПоСтроке.Ключ, Ошибка.Ключ);
			ОшибкиВычисленияДополнительныхПолей.Добавить(Поле, Ошибка.Значение);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОшибкиВычисленияДополнительныхПолей()
	
	Если Элементы.ГруппаОшибкиЗаполнения.ТекущаяСтраница <> Элементы.СтраницаДополнительныеПоля
		ИЛИ ОшибкиВычисленияДополнительныхПолей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Ошибка Из ОшибкиВычисленияДополнительныхПолей Цикл
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Ошибка.Представление,,Ошибка.Значение);
	КонецЦикла;
	
	ОшибкиВычисленияДополнительныхПолей.Очистить();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьЗавершениеНастройкиЗаполненияДополнительныхПолей(Результат)
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИзмененыНастройки = Истина;
	
	Если ПодписатьОтправить Тогда
		МассивСтрок = Новый Массив;
		МассивСтрок.Добавить(НСтр("ru = 'Изменены настройки заполнения дополнительных полей.'"));
		МассивСтрок.Добавить(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Для того чтобы сформировать документ с новыми настройками, нажмите ""%1"".'")
				,Элементы.Готово.Заголовок));
		
		Элементы.НадписьИзмененыНастройки.Заголовок = СтроковыеФункцииКлиентСервер.СтрокаИзМассиваПодстрок(
			МассивСтрок, Символы.ПС);
	КонецЕсли;
	
	Элементы.КартинкаИнформация.Видимость = Истина;
	Элементы.НадписьИзмененыНастройки.Видимость = Истина;
	Элементы.ГруппаРазделовШапка.Доступность = Ложь;
	Элементы.ГруппаРазделовТаблица.Доступность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполнениеДополнительныхПолей(Отказ)
	
	Для Каждого РеквизитыРаздела Из ИменаРеквизитовПоРазделам Цикл
		
		Если РеквизитыРаздела.ТипРаздела = 
			ЭлектронныеДокументыКлиентСервер.ТипРазделаДополнительныхПолейШапка() Тогда
			
			ПроверитьЗаполнениеДополнительныхПолейШапки(РеквизитыРаздела.ИменаРеквизитов, Отказ);
			
		ИначеЕсли РеквизитыРаздела.ТипРаздела = 
			ЭлектронныеДокументыКлиентСервер.ТипРазделаДополнительныхПолейТаблица() Тогда
			
			ПроверитьЗаполнениеДополнительныхПолейТаблицы(РеквизитыРаздела.ИменаРеквизитов, РеквизитыРаздела.ИмяРаздела, Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполнениеДополнительныхПолейШапки(ИменаРеквизитов, Отказ)
	
	Для Каждого ИмяРеквизита Из ИменаРеквизитов Цикл
		
		Если Не ЗначениеЗаполнено(ЭтаФорма[ИмяРеквизита]) Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Поле ""%1"" не заполнено.'"),
				Элементы[ИмяРеквизита].Заголовок);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,ИмяРеквизита,,Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполнениеДополнительныхПолейТаблицы(ИменаРеквизитов, ИмяРаздела, Отказ)
	
	ИмяТаблицыРаздела = "Таблица" + ИмяРаздела;
	
	НомерСтроки = 1;
	Для Каждого СтрокаТаблицы Из ЭтаФорма[ИмяТаблицыРаздела] Цикл
		
		Для Каждого ИмяРеквизита Из ИменаРеквизитов Цикл
			
			Если Не ЗначениеЗаполнено(СтрокаТаблицы[ИмяРеквизита]) Тогда
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТаблицыРаздела, НомерСтроки, ИмяРеквизита);
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не заполнена колонка ""%1"" в строке %2'"),
					Элементы[ИмяРеквизита].Заголовок, НомерСтроки);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,Поле,,Отказ);
			КонецЕсли;
			
		КонецЦикла;
		
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция ДанныеДополнительныхПолей()
	
	ДополнительныеДанные = Новый Структура;
	
	Для Каждого РеквизитыРаздела Из ИменаРеквизитовПоРазделам Цикл
		
		Если РеквизитыРаздела.ТипРаздела =
			ЭлектронныеДокументыКлиентСервер.ТипРазделаДополнительныхПолейШапка() Тогда
			
			ДанныеРаздела = ДанныеДополнительныхПолейШапки(РеквизитыРаздела.ИменаРеквизитов);
			
		ИначеЕсли РеквизитыРаздела.ТипРаздела =
			ЭлектронныеДокументыКлиентСервер.ТипРазделаДополнительныхПолейТаблица() Тогда
			
			ДанныеРаздела = ДанныеДополнительныхПолейТаблицы(РеквизитыРаздела.ИменаРеквизитов, РеквизитыРаздела.ИмяРаздела);
			
		КонецЕсли;
		
		ДополнительныеДанные.Вставить(РеквизитыРаздела.ИмяРаздела, ДанныеРаздела);
		
	КонецЦикла;
	
	Возврат ДополнительныеДанные;
	
КонецФункции

&НаКлиенте
Функция ДанныеДополнительныхПолейШапки(ИменаРеквизитов)
	
	ДанныеРаздела = Новый Структура;
	Для Каждого ИмяРеквизита Из ИменаРеквизитов Цикл
		ДанныеРаздела.Вставить(ИмяРеквизита, ЭтаФорма[ИмяРеквизита]);
	КонецЦикла;
	
	Возврат ДанныеРаздела;
	
КонецФункции

&НаКлиенте
Функция ДанныеДополнительныхПолейТаблицы(ИменаРеквизитов, ИмяРаздела)
	
	ДанныеРаздела = Новый Массив;
	
	Для Каждого СтрокаТаблицы Из ЭтаФорма["Таблица" + ИмяРаздела] Цикл
		
		ДанныеСтроки = Новый Структура;
		
		Если ИмяРаздела = "ТоварыУслуги" Тогда
			КлючСтроки = Новый Структура("Номенклатура, Характеристика, Упаковка, Сумма");
			ЗаполнитьЗначенияСвойств(КлючСтроки, СтрокаТаблицы);
			ДанныеСтроки.Вставить("КлючСтроки", КлючСтроки);
		КонецЕсли;
		
		ЗначенияПолей = Новый Структура;
		Для Каждого ИмяРеквизита Из ИменаРеквизитов Цикл
			ЗначенияПолей.Вставить(ИмяРеквизита, СтрокаТаблицы[ИмяРеквизита]);
		КонецЦикла;
		ДанныеСтроки.Вставить("ЗначенияПолей", ЗначенияПолей);
		
		ДанныеРаздела.Добавить(ДанныеСтроки);
		
	КонецЦикла;
	
	Возврат ДанныеРаздела;
	
КонецФункции

&НаСервере
Процедура СброситьРазмерыИПоложениеОкна()
	
	ИмяПользователя = ПользователиИнформационнойБазы.ТекущийПользователь().Имя;
	
	Если ПравоДоступа("СохранениеДанныхПользователя", Метаданные) Тогда
		ХранилищеСистемныхНастроек.Удалить("Обработка.ОбменСКонтрагентами.Форма.ПомощникУстраненияОшибок", "", ИмяПользователя);
	КонецЕсли;
	
	КлючСохраненияПоложенияОкна = Строка(Новый УникальныйИдентификатор);
	
КонецПроцедуры
