////////////////////////////////////////////////////////////////////////////////
// ОписаниеПеременных

&НаКлиенте
Перем РезультатУстраненияОшибок;

&НаКлиенте
Перем КэшДополнительныхДанных;

////////////////////////////////////////////////////////////////////////////////
// ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ПодготовитьФормуНаСервере(Параметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ПоказатьПроблемыПриФормированииДокументов" Тогда
		Параметр.ФормаОткрыта = Истина;
		ПодготовитьФормуНаСервере(Параметр);
		Активизировать();
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОбработчикиСобытийЭлементовТаблицыФормыДокументыСОшибками

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "СписокРезультат" Тогда
		
		ТекущиеДанные = Элемент.ТекущиеДанные;
		Если ТекущиеДанные.Статус = 1 Тогда
			ЭлектронныеДокументыКлиент.ОткрытьАктуальныйЭД(ТекущиеДанные.Документ);
		ИначеЕсли ТекущиеДанные.Статус = 2 Тогда
			ОткрытьФормуПомощникаУстраненияОшибок(ТекущиеДанные);
		КонецЕсли;
		
	ИначеЕсли Поле.Имя = "СписокКонтрагент"
		ИЛИ Поле.Имя = "СписокДокумент"
		ИЛИ Поле.Имя = "СписокОрганизация"
		ИЛИ Поле.Имя = "СписокДоговорКонтрагента" Тогда
		
		СсылкаНаОбъект = Элемент.ТекущиеДанные[СтрЗаменить(Поле.Имя, Элемент.Имя, "")];
		Если ЗначениеЗаполнено(СсылкаНаОбъект) Тогда
			ОткрытьЗначение(СсылкаНаОбъект);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодписатьИОтправить(Команда)
	
	ПараметрыОтбора = Новый Структура("Статус", 1);
	НайденныеСтроки = Список.НайтиСтроки(ПараметрыОтбора);
	
	Если ПараметрыОтбора.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МассивСсылок = Новый Массив;
	Для Каждого СтрокаТаблицы Из НайденныеСтроки Цикл
		МассивСсылок.Добавить(СтрокаТаблицы.Документ);
		Список.Удалить(СтрокаТаблицы);
		Если КэшДополнительныхДанных[СтрокаТаблицы.Документ] <> Неопределено Тогда
			КэшДополнительныхДанных.Удалить(СтрокаТаблицы.Документ)
		КонецЕсли;
	КонецЦикла;
	
	ЭлектронныеДокументыСлужебныйКлиент.ОбработатьЭД(МассивСсылок, "ПодписатьОтправить");
	
	Если Список.Количество() = 0 Тогда
		Закрыть();
	Иначе
		ОбновитьСведенияСформированоНеСформировано(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеОформления = ЭлементОформления.Поля.Элементы.Добавить();
	ПолеОформления.Поле = Новый ПолеКомпоновкиДанных("СписокРезультат");
	
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Список.Статус");
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = 1;
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветГиперссылкиБЭД);
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере(ПараметрыФормы)
	
	АдресСведенийОбОшибках = ПараметрыФормы.АдресСведенийОбОшибках;
	ОшибкиПриФормированииДокументов = ПолучитьИзВременногоХранилища(АдресСведенийОбОшибках);
	
	УдалитьИзВременногоХранилища(АдресСведенийОбОшибках);
	
	ДополнитьСписокДокументовСОшибками(ОшибкиПриФормированииДокументов);
	
	ПодписатьОтправить = Найти(ПараметрыФормы.Действие, "Подписать") > 0;
	Элементы.ПодписатьИОтправить.Видимость = ПодписатьОтправить;
	
	ОбновитьСведенияСформированоНеСформировано(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ДополнитьСписокДокументовСОшибками(ОшибкиПриФормированииДокументов)
	
	ПараметрыОтбора = Новый Структура;
	Для Каждого ОшибкиПриФормированииДокумента Из ОшибкиПриФормированииДокументов Цикл
		ПараметрыОтбора.Вставить("Документ", ОшибкиПриФормированииДокумента.ДанныеДляОбработкиОшибок.Документ);
		НайденныеСтроки = Список.НайтиСтроки(ПараметрыОтбора);
		Если НайденныеСтроки.Количество() Тогда
			ЗаполнитьИнформациюПоОшибкам(НайденныеСтроки[0], ОшибкиПриФормированииДокумента.ОшибкиЗаполнения);
		Иначе
			НоваяСтрока = Список.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ОшибкиПриФормированииДокумента.ДанныеДляОбработкиОшибок);
			ЗаполнитьИнформациюПоОшибкам(НоваяСтрока, ОшибкиПриФормированииДокумента.ОшибкиЗаполнения);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИнформациюПоОшибкам(СтрокаТаблицы, СтруктураОшибок)
	
	Если Не ЗначениеЗаполнено(СтруктураОшибок) Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОшибок.Свойство("ОсновныеПоля") Тогда
		КоличествоОшибок = СтруктураОшибок.ОсновныеПоля.Количество();
	Иначе
		КоличествоОшибок = 0;
	КонецЕсли;
	
	КоличествоОшибок = КоличествоОшибок + СтруктураОшибок.Свойство("ДополнительныеПоля");
	
	ПредставлениеОшибок = Неопределено;
	Если КоличествоОшибок = 0 Тогда
		ПредставлениеОшибок = НСтр("ru = 'Отсутствуют'");
	ИначеЕсли КоличествоОшибок > 1 Тогда
		ПредставлениеОшибок = СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(
			КоличествоОшибок, НСтр("ru = 'ошибка,ошибки,ошибок'"));
	ИначеЕсли СтруктураОшибок.Свойство("ОсновныеПоля") Тогда
		ПредставлениеОшибок = НСтр("ru = 'Некоторые поля заполнены некорректно'");
	ИначеЕсли СтруктураОшибок.Свойство("ДополнительныеПоля") Тогда
		ПредставлениеОшибок = НСтр("ru = 'Не заполнены дополнительные поля'");
	КонецЕсли;
	
	СтрокаТаблицы.Статус = 1 + (КоличествоОшибок > 0);
	СтрокаТаблицы.Результат = ПредставлениеОшибок;
	СтрокаТаблицы.СтруктураОшибок = СтруктураОшибок;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьСведенияСформированоНеСформировано(Форма)
	
	Форма.Сформировано = Форма.Список.НайтиСтроки(Новый Структура("Статус", 1)).Количество();
	Форма.НеСформировано = Форма.Список.НайтиСтроки(Новый Структура("Статус", 2)).Количество();
	
	Если Форма.ПодписатьОтправить Тогда
		Форма.Элементы.ПодписатьИОтправить.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Подписать и отправить документы (%1 из %2)'"),
			Форма.Сформировано, Форма.НеСформировано + Форма.Сформировано);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуПомощникаУстраненияОшибок(ТекущиеДанные);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Документ", ТекущиеДанные.Документ);
	ПараметрыФормы.Вставить("Контрагент", ТекущиеДанные.Контрагент);
	ПараметрыФормы.Вставить("НастройкаЭДО", ТекущиеДанные.НастройкаЭДО);
	ПараметрыФормы.Вставить("ВидЭлектронногоДокумента", ТекущиеДанные.ВидЭлектронногоДокумента);
	ПараметрыФормы.Вставить("Формат", ТекущиеДанные.Формат);
	ПараметрыФормы.Вставить("ОшибкиЗаполнения", ТекущиеДанные.СтруктураОшибок);
	
	Результат = ОткрытьФормуМодально("Обработка.ЭлектронныеДокументы.Форма.ПомощникУстраненияОшибок",
		ПараметрыФормы, ЭтаФорма);
		
	ОбработатьДокументПослеУстраненияОшибок(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьДокументПослеУстраненияОшибок(Результат)
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РезультатУстраненияОшибок = Результат;
	
	ПараметрыОтбора = Новый Структура("Документ", Результат.Документ);
	НайденныеСтроки = Список.НайтиСтроки(ПараметрыОтбора);
	Если НайденныеСтроки.Количество() Тогда
		НайденныеСтроки[0].Статус = 0;
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("ОбработатьДокументПослеИзмененияСтатуса", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьДокументПослеИзмененияСтатуса()
	
	МассивСсылокНаОбъекты = Новый Массив;
	МассивСсылокНаОбъекты.Добавить(РезультатУстраненияОшибок.Документ);
	
	ДополнительныеПараметры = Неопределено;
	Если РезультатУстраненияОшибок.Свойство("ДополнительныеДанные") Тогда
		ДополнительныеПараметры = Новый Структура("ДополнительныеДанные", РезультатУстраненияОшибок.ДополнительныеДанные);
		КэшДополнительныхДанных.Вставить(РезультатУстраненияОшибок.Документ, РезультатУстраненияОшибок.ДополнительныеДанные);
	ИначеЕсли КэшДополнительныхДанных[РезультатУстраненияОшибок.Документ] <> Неопределено Тогда
		ДополнительныеПараметры = Новый Структура("ДополнительныеДанные", КэшДополнительныхДанных[РезультатУстраненияОшибок.Документ]);
	КонецЕсли;
	
	ЭлектронныеДокументыСлужебныйКлиент.ОбработатьЭД(МассивСсылокНаОбъекты,
		РезультатУстраненияОшибок.Действие, ДополнительныеПараметры);
	
	ПараметрыОтбора = Новый Структура("Документ", РезультатУстраненияОшибок.Документ);
	НайденныеСтроки = Список.НайтиСтроки(ПараметрыОтбора);
	Если НайденныеСтроки.Количество()
		И НайденныеСтроки[0].Статус = 0 Тогда
		
		Если РезультатУстраненияОшибок.Действие = "Сформировать" Тогда
			НайденныеСтроки[0].Статус = 1;
			НайденныеСтроки[0].Результат = НСтр("ru = 'Сформирован'");
			НайденныеСтроки[0].СтруктураОшибок = Неопределено;
		Иначе
			Список.Удалить(НайденныеСтроки[0]);
			Если КэшДополнительныхДанных[РезультатУстраненияОшибок.Документ] <> Неопределено Тогда
				КэшДополнительныхДанных.Удалить(РезультатУстраненияОшибок.Документ)
			КонецЕсли;
		КонецЕсли;
		
		ОбновитьСведенияСформированоНеСформировано(ЭтаФорма);
		
	КонецЕсли;
	
	Если Список.НайтиСтроки(Новый Структура("Статус", 1)).Количество() = Список.Количество() Тогда
		
		Элементы.НадписьПояснение.Заголовок = НСтр("ru = 'Все электронные документы сформированы'");
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Инициализация

КэшДополнительныхДанных = Новый Соответствие;