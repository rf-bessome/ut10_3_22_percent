////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервереБезКонтекста
Функция СоздатьОбъектыИБ(АдресВременногоХранилища, ОшибкаЗаписи)
	
	Перем ДеревоРазбора;
	
	СтруктураРазбора = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	
	Если СтруктураРазбора <> Неопределено И СтруктураРазбора.Свойство("ДеревоРазбора", ДеревоРазбора) Тогда
		// Заполним ссылки на объекты из дерева соответствий, если ссылок нет,
		// тогда будем создавать объекты
		ЭлектронныеДокументыВнутренний.ЗаполнитьСсылкиНаОбъектыВДереве(ДеревоРазбора, ОшибкаЗаписи);
	КонецЕсли;
	
КонецФункции   

&НаСервере
Функция ИмяОбъектаМетаданных(Ссылка)
	Возврат Метаданные.НайтиПоТипу(ТипЗнч(Ссылка)).ПолноеИмя();
КонецФункции

&НаСервере
Функция МножественнаяЗагрузка()
	
	Возврат Найти(ТипОбъекта, "; ") > 0;
	
КонецФункции

&НаСервереБезКонтекста
Функция СохранитьДанныеОбъектаВБД(АдресВременногоХранилища)
	
	СтруктураРазбора = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	
	Если СтруктураРазбора <> Неопределено И СтруктураРазбора.Свойство("ДеревоРазбора") Тогда
		ЭлектронныеДокументыПереопределяемый.СохранитьДанныеОбъектаВБД(
										СтруктураРазбора.СтрокаОбъекта,
										СтруктураРазбора.ДеревоРазбора);
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция НайтиКонтрагентаВДеревеЭД(ДеревоЭД)
	
	Результат = Неопределено;

	ФункцияЭД = ЭлектронныеДокументыВнутренний.ЗначениеРеквизитаВДереве(ДеревоЭД, "Функция");
	СведенияОПродавце = "СведенияОПродавце";
	Если ФункцияЭД = "СЧФДОП" тогда
		СведенияОПродавце = СведенияОПродавце + ".НомерСтроки";
	КонецЕсли;
	
	ТипУчастника = ЭлектронныеДокументыВнутренний.ЗначениеРеквизитаВДереве(ДеревоЭД, СведенияОПродавце + ".ТипУчастника");
	ИННПоставщика = "";
	КПППоставщика = "";
	
	Если ТипУчастника = "ЮЛ" Тогда 
		ИННПоставщика = ЭлектронныеДокументыВнутренний.ЗначениеРеквизитаВДереве(ДеревоЭД, СведенияОПродавце + ".ТипУчастника.ЮЛ.ИНН");
		КПППоставщика = ЭлектронныеДокументыВнутренний.ЗначениеРеквизитаВДереве(ДеревоЭД, СведенияОПродавце + ".ТипУчастника.ЮЛ.КПП");
	ИначеЕсли ТипУчастника = "ФЛ" Тогда 
		ИННПоставщика = ЭлектронныеДокументыВнутренний.ЗначениеРеквизитаВДереве(ДеревоЭД, СведенияОПродавце + ".ТипУчастника.ФЛ.ИНН");
	ИначеЕсли ТипУчастника = "ИП" Тогда 
		ИННПоставщика = ЭлектронныеДокументыВнутренний.ЗначениеРеквизитаВДереве(ДеревоЭД, СведенияОПродавце + ".ТипУчастника.ИП.ИНН");
	КонецЕсли;
	
	Результат = ЭлектронныеДокументыПереопределяемый.СсылкаНаОбъектПоИННКПП("Контрагенты", ИННПоставщика, КПППоставщика);
		
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция СопоставитьНоменклатуру(ДанныеФормы = Неопределено)
	
	ЗначениеВозврата = Неопределено;
	СтруктураЭД = Новый Структура;
	СтруктураЭД.Вставить("ВидЭД", ВидЭД);
	СтруктураЭД.Вставить("СпособОбменаЭД", ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.БыстрыйОбмен"));
	СтруктураЭД.Вставить("ДанныеФайлаРазбора", ДанныеФайлаРазбора);
	СтруктураЭД.Вставить("Контрагент", Контрагент);
	СтруктураЭД.Вставить("НаправлениеЭД", ПредопределенноеЗначение("Перечисление.НаправленияЭД.Входящий"));
	Если МножественнаяЗагрузка() тогда
		СтруктураЭД.Вставить("ВладелецФайла", ?(СпособЗагрузкиДокумента = 0, Неопределено, ДокументИБ2));
		СтруктураЭД.Вставить("ТипЭлементаВерсииЭД", ТипЭлементаВерсииЭД);
		
	Иначе
		СтруктураЭД.Вставить("ВладелецФайла", ?(СпособЗагрузкиДокумента = 0, Неопределено, ДокументИБ));
	КонецЕсли;

	СтруктураПараметров = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьПараметрыФормыСопоставленияНоменклатуры(СтруктураЭД);
	Если ЗначениеЗаполнено(СтруктураПараметров) Тогда
		ЗначениеВозврата = ОткрытьФормуМодально(СтруктураПараметров.ИмяФормы, СтруктураПараметров.ПараметрыОткрытияФормы);
	КонецЕсли;
	
	Возврат ЗначениеВозврата;
	
КонецФункции

&НаКлиенте
Процедура ИзменитьВидимостьДоступность()
	
	Если Врег(ВидЭД) = ВРег("РеквизитыОрганизации") Тогда
		
		Элементы.ЭлементСправочникаИБ.Доступность = (СпособЗагрузкиДокумента = 1);
		
	Иначе	
		Если ЗагрузкаЭД Тогда
			Элементы.ДокументИБ.Доступность = (СпособЗагрузкиДокумента = 1);
			Элементы.ДокументИБ2.Доступность = (СпособЗагрузкиДокумента = 1) И МножественнаяЗагрузка();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьВидимостьДоступностьПриСозданииНаСервере()
	
	Элементы.ГруппаСодержимоеДокумента.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	
	Если ЗагрузкаЭД Тогда
		
		Элементы.ГруппаКнопок.Видимость = Истина;
		Элементы.ГруппаГиперссылка.Видимость = Ложь;

		Если ВРег(ВидЭД) = ВРег("РеквизитыОрганизации") Тогда
			
			Заголовок = Нстр("ru = 'Загрузка данных из файла'");
			
			Элементы.ГруппаНастроекСправочники.Видимость = Истина;
			Элементы.ГруппаНастроекДокументы.Видимость = Ложь;
		Иначе
			
			Заголовок = Нстр("ru = 'Загрузка документа из файла'");
			
			Элементы.ГруппаНастроекСправочники.Видимость = Ложь;
			Элементы.ГруппаНастроекДокументы.Видимость = Истина;
		КонецЕсли;
		
	Иначе
		Текст = Нстр("ru = 'Электронный документ'");
		Заголовок = Текст;
		Элементы.ГруппаНастроекСправочники.Видимость = Ложь;
		Элементы.ГруппаНастроекДокументы.Видимость = Ложь;
		Элементы.ГруппаКнопок.Видимость = Ложь;
		Элементы.ГруппаГиперссылка.Видимость = Истина;
	КонецЕсли;
	
	Если ВидЭД = Перечисления.ВидыЭД.КаталогТоваров Тогда
		Элементы.Загрузка.Видимость = Ложь;
		Элементы.ТипОбъекта.Заголовок = Нстр("ru = 'Загрузить'");
		ТипОбъекта = "Каталог товаров";
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьПросмотрЭДСервер(СтруктураЭД, Отказ)
	
	Перем ПерезаполняемыйДокумент, ДеревоРазбора, СтрокаОбъекта;
	
	ФайлПросмотра = Неопределено;
	ИмяФайлаКартинок = Неопределено;
	ФайлДопДанных = Неопределено;
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(СтруктураЭД.АдресХранилища);
	
	Если СтруктураЭД.ФайлАрхива Тогда
		
		ПапкаДляРаспаковки = ЭлектронныеДокументыСлужебный.РабочийКаталог("ext", СтруктураЭД.УникальныйИдентификатор);
		ИмяФайлаАрхива = ЭлектронныеДокументыСлужебный.ТекущееИмяВременногоФайла("zip");
		ДвоичныеДанные.Записать(ИмяФайлаАрхива);
		
		УдалитьФайлы(ПапкаДляРаспаковки, "*");
		
		Попытка
			ЧтениеЗИП = Новый ЧтениеZIPФайла(ИмяФайлаАрхива);
			ЧтениеЗИП.ИзвлечьВсе(ПапкаДляРаспаковки);
		Исключение
			
			ТекстОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			
			Если ЧтениеЗИП = Неопределено Тогда
				ТекстСообщения = НСтр("ru = 'Невозможно прочитать архив электронного документа'")
			Иначе
				Если НЕ ЭлектронныеДокументыСлужебный.ВозможноИзвлечьФайлы(ЧтениеЗИП, ПапкаДляРаспаковки) Тогда
					ТекстСообщения = ЭлектронныеДокументыПовтИсп.ПолучитьСообщениеОбОшибке("006");
				КонецЕсли;
			КонецЕсли;
			
			ЭлектронныеДокументыСлужебныйВызовСервера.ОбработатьИсключениеПоЭДНаСервере(,
				НСтр("ru = 'Распаковка архива ЭД'"), ТекстОшибки, ТекстСообщения);
			
			УдалитьФайлы(ИмяФайлаАрхива);
			УдалитьФайлы(ПапкаДляРаспаковки);
			
			Возврат;
			
		КонецПопытки;
		
		УдалитьФайлы(ИмяФайлаАрхива);
		// скопируем файл просмотра
		МассивФайловПросмотра = НайтиФайлы(ПапкаДляРаспаковки, "*.pdf", Истина);
		Если МассивФайловПросмотра.Количество() > 0 Тогда
			ФайлПросмотра = МассивФайловПросмотра[0];
		КонецЕсли;
		
		// Расшифровать файл с данными
		МассивФайлИнформации = НайтиФайлы(ПапкаДляРаспаковки, "meta*.xml", Истина);
		Если МассивФайлИнформации.Количество() > 0 Тогда
			ФайлИнформации = МассивФайлИнформации[0];
		КонецЕсли;
		
		МассивФайлКарточки = НайтиФайлы(ПапкаДляРаспаковки, "card*.xml", Истина);
		Если МассивФайлКарточки.Количество() > 0 Тогда
			ФайлКарточки = МассивФайлКарточки[0];
		КонецЕсли;
		
		// скопируем файл просмотра
		МассивФайловКартинок = НайтиФайлы(ПапкаДляРаспаковки, "*.zip", Истина);
		Если МассивФайловКартинок.Количество() > 0 Тогда
			ФайлКартинок = МассивФайловКартинок[0];
			ИмяФайлаКартинок = ФайлКартинок.ПолноеИмя;
		КонецЕсли;
		
		Если ФайлКарточки = Неопределено Или ФайлИнформации = Неопределено Тогда
			
			ШаблонСообщения = НСтр("ru = 'Возникла ошибка при чтении данных из файла ""%1"" (подробности см. в Журнале регистрации).'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, СтруктураЭД.ИмяФайла);
			
			ШаблонСообщения = НСтр("ru = 'Файл ""%1"" не содержит электронных документов.'");
			ПредставлениеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, СтруктураЭД.ИмяФайла);
			ЭлектронныеДокументыСлужебныйВызовСервера.ОбработатьИсключениеПоЭДНаСервере(НСтр("ru = 'Чтение ЭД.'"),
			ПредставлениеОшибки,
			ТекстСообщения);
			УдалитьФайлы(ПапкаДляРаспаковки);
			Отказ = Истина;
			Возврат;
			
		КонецЕсли;
		
		СоответствиеФайлПараметры = ЭлектронныеДокументыВнутренний.ПолучитьСоответствиеФайлПараметры(ФайлИнформации, ФайлКарточки);
		
		Для Каждого ЭлементСоответствия Из СоответствиеФайлПараметры Цикл
			
			МассивФайловИсточник = НайтиФайлы(ПапкаДляРаспаковки, ЭлементСоответствия.Ключ, Истина);
			Если МассивФайловИсточник.Количество() = 0 Тогда
				ШаблонСообщения = НСтр("ru = 'В архиве не найден файл электронного документа: ""%1""'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ШаблонСообщения, ЭлементСоответствия.Ключ);
				ЭлектронныеДокументыСлужебныйВызовСервера.ОбработатьИсключениеПоЭДНаСервере(
					НСтр("ru = 'Чтение ЭД.'"), ТекстСообщения, ТекстСообщения);
				УдалитьФайлы(ПапкаДляРаспаковки);
				Отказ = Истина;
				Возврат;
			Иначе				
				Если МассивФайловИсточник[0].Расширение = ".zip" Тогда
					
					ПапкаДляРаспаковкиФайлаЭД = ЭлектронныеДокументыСлужебный.РабочийКаталог("ext", УникальныйИдентификатор);
					ИмяФайлаАрхива = МассивФайловИсточник[0].ПолноеИмя;
					УдалитьФайлы(ПапкаДляРаспаковкиФайлаЭД, "*");
					
					Попытка
						ЧтениеЗИП = Новый ЧтениеZIPФайла(ИмяФайлаАрхива);
						ЧтениеЗИП.ИзвлечьВсе(ПапкаДляРаспаковкиФайлаЭД);
					Исключение
						
						ТекстОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
						
						Если ЧтениеЗИП = Неопределено Тогда
							ТекстСообщения = НСтр("ru = 'Невозможно прочитать транспортный архив'")
						Иначе
							Если НЕ ЭлектронныеДокументыСлужебный.ВозможноИзвлечьФайлы(ЧтениеЗИП, ПапкаДляРаспаковкиФайлаЭД) Тогда
								ТекстСообщения = ЭлектронныеДокументыПовтИсп.ПолучитьСообщениеОбОшибке("006");
							КонецЕсли;
							ЧтениеЗИП.Закрыть();
						КонецЕсли;
						
						ЭлектронныеДокументыСлужебныйВызовСервера.ОбработатьИсключениеПоЭДНаСервере(
							НСтр("ru = 'Распаковка архива ЭД'"), ТекстОшибки, ТекстСообщения);
						
						УдалитьФайлы(ПапкаДляРаспаковкиФайлаЭД);
						Возврат;
						
					КонецПопытки;
					
					// скопируем файл просмотра
					МассивФайловИсточник = НайтиФайлы(ПапкаДляРаспаковкиФайлаЭД, "*.xml", Истина);
				КонецЕсли;
				ИмяФайла = ЭлектронныеДокументыСлужебный.ТекущееИмяВременногоФайла("xml");
				КопироватьФайл(МассивФайловИсточник[0].ПолноеИмя, ИмяФайла);
			КонецЕсли;
			
			ДопДанные = Неопределено;
			Если ЭлементСоответствия.Значение.Свойство("ДопДанные", ДопДанные) И ТипЗнч(ДопДанные) = Тип("Структура") Тогда
				
				ИмяФайлаДопДанных = Неопределено;
				Если ДопДанные.Свойство("ФайлДопДанных",ИмяФайлаДопДанных) И ЗначениеЗаполнено(ИмяФайлаДопДанных) Тогда
					
					МассивФайловДопДанных = НайтиФайлы(ПапкаДляРаспаковки, ИмяФайлаДопДанных, Истина);
					Если МассивФайловДопДанных.Количество() > 0 Тогда
						
						ФайлДопДанных = ЭлектронныеДокументыСлужебный.ТекущееИмяВременногоФайла("xml");
						КопироватьФайл(МассивФайловДопДанных[0].ПолноеИмя, ФайлДопДанных);
						
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		ИмяФайла = ЭлектронныеДокументыСлужебный.ТекущееИмяВременногоФайла("xml");
		ДвоичныеДанные.Записать(ИмяФайла);
	КонецЕсли;
	
	СтруктураЭД.Свойство("СсылкаНаДокумент", ПерезаполняемыйДокумент);
	
	СтруктураРазбора = ЭлектронныеДокументыВнутренний.СформироватьДеревоРазбора(ИмяФайла,
		Перечисления.НаправленияЭД.Входящий,
		ФайлДопДанных,
		ИмяФайлаКартинок);
	ДвоичныеДанныеФайлаРазбора = Новый ДвоичныеДанные(ИмяФайла);
	ДанныеФайлаРазбора = ПоместитьВоВременноеХранилище(ДвоичныеДанныеФайлаРазбора, УникальныйИдентификатор);
	
	ДанныеЭД = Неопределено;
	
	Если ТипЗнч(СтруктураРазбора) = Тип("Структура") Тогда
		
		АдресСтруктурыРазбораЭД = ПоместитьВоВременноеХранилище(СтруктураРазбора, УникальныйИдентификатор);
		
		ПараметрыПечати = Новый Структура;
		ПараметрыПечати.Вставить("ИД", СтруктураЭД.УникальныйИдентификатор);

		ДанныеЭД = ЭлектронныеДокументыВнутренний.ПечатнаяФормаЭД(
			СтруктураРазбора, СтруктураЭД.НаправлениеЭД, ПараметрыПечати);
		
		ВидЭД = СтруктураРазбора.СтрокаОбъекта.ВидЭД;
		ТипЭлементаВерсииЭД = СтруктураРазбора.СтрокаОбъекта.ТипЭлементаВерсииЭД;
		
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЭД) = Тип("ТабличныйДокумент") Тогда
		
		Если ЗагрузкаЭД Тогда
			Если (НЕ ЗначениеЗаполнено(ДокументИБ) ИЛИ СпособЗагрузкиДокумента = 0) И СтруктураРазбора <> Неопределено
				И СтруктураРазбора.Свойство("ДеревоРазбора", ДеревоРазбора)
				И СтруктураРазбора.Свойство("СтрокаОбъекта", СтрокаОбъекта) Тогда
				ОшибкаЗаписи = Ложь;
				СтрокаДерева = НайтиСтрокуВДереве(ДеревоРазбора, СтрокаОбъекта, "Контрагент");
				Если СтрокаДерева <> Неопределено Тогда
					Контрагент = СтрокаДерева.СсылкаНаОбъект;
				ИначеЕсли ЗначениеЗаполнено(СтруктураРазбора.СтрокаОбъекта.ЗначениеРеквизита) Тогда
					Контрагент = НайтиКонтрагентаВДеревеЭД(СтруктураРазбора.СтрокаОбъекта.ЗначениеРеквизита);					
				КонецЕсли;
			КонецЕсли;
						
			ДопДокумент = Неопределено;
			Если ТипЭлементаВерсииЭД = Перечисления.ТипыЭлементовВерсииЭД.СЧФДОПУПД
				Или ТипЭлементаВерсииЭД = Перечисления.ТипыЭлементовВерсииЭД.КСЧФДИСУКД Тогда
		
				ДопДокумент = ЭлектронныеДокументыПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении(
								"ПоступлениеТоваровУслугВМетаданных");
				ДокументИБ2 = Документы[ДопДокумент].ПустаяСсылка(); 
				ДопДокументПредставление = Метаданные.Документы[ДопДокумент].Представление(); 
				
				СписокТипов2 = Новый СписокЗначений;
				СписокТипов2.Добавить(ДокументИБ2, ДопДокументПредставление); 
			КонецЕсли;			
			
			ЭлектронныеДокументыПереопределяемый.СписокТиповДокументовПоВидуЭД(ВидЭД, СписокТипов);
			Для Каждого ТекЗначение Из СписокТипов Цикл
				ТекПредставление = ТекЗначение.Представление + ?(ДопДокумент=Неопределено, "", "; "+ДопДокументПредставление);
				ТекЭлемент = Элементы.ТипОбъекта.СписокВыбора.Добавить();
				ТекЭлемент.Значение = ТекПредставление; 
				
				// Если реквизит ДокументИБ еще не заполнен и зачитано первое по списку значение, то заполним имеющимися данными:
				Если НЕ ЗначениеЗаполнено(ДокументИБ) И СписокТипов.Индекс(ТекЗначение) = 0 Тогда
					ТипОбъекта = ТекПредставление;
					ДокументИБ = ТекЗначение.Значение; 
				КонецЕсли;
				
				Если ВРег(ВидЭД) = ВРег("РеквизитыОрганизации") Тогда
					Если ЗначениеЗаполнено(Контрагент) Тогда
						ДокументИБ = Контрагент;
						СпособЗагрузкиДокумента = 1;
					КонецЕсли;
				КонецЕсли;
				
				// Если в структуре параметров есть ссылка на (перезаполняемый) документ ИБ и его тип совпал с типом одного из значений
				// списка типов, то заполним этими данными соответствующие реквизиты формы.
				// Данное условие необходимо для корректной обработки ситуации, когда в качестве перезаполняемого документа, выбран
				// документ с типом не совпадающим ни с одним из доступных в списке или не совпадает с типом первого элемента списка.
				Если ЗначениеЗаполнено(ПерезаполняемыйДокумент) И ТипЗнч(ПерезаполняемыйДокумент) = ТипЗнч(ТекЗначение.Значение) Тогда
					ТипОбъекта = ТекПредставление;
					ДокументИБ = ПерезаполняемыйДокумент;  
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ПерезаполняемыйДокумент) И ТипЗнч(ПерезаполняемыйДокумент) = ТипЗнч(ДокументИБ2) Тогда
					ТипОбъекта  = ТекПредставление;
					ДокументИБ2 = ПерезаполняемыйДокумент;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Контрагент) И ЗначениеЗаполнено(ПерезаполняемыйДокумент) Тогда
			
			ИмяСправочникаКонтрагенты = ИмяСправочника("Контрагенты");
			
			Если Не ЗначениеЗаполнено(ИмяСправочникаКонтрагенты) Тогда
				ИмяСправочникаКонтрагенты = "Контрагенты";
			КонецЕсли;
			
			Если ТипЗнч(ПерезаполняемыйДокумент) = Тип("СправочникСсылка."+ ИмяСправочникаКонтрагенты) Тогда
				Контрагент = ПерезаполняемыйДокумент;
			Иначе
				Если ПерезаполняемыйДокумент.Метаданные().Реквизиты.Найти("Контрагент") = Истина Тогда
					Контрагент = ПерезаполняемыйДокумент.Контрагент;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
		ТабличныйДокументФормы = ДанныеЭД;
		Элементы.ГруппаСодержимоеДокумента.ТекущаяСтраница = Элементы.СтраницаТабличныйДокумент;
		
	Иначе
		
		Если Не ФайлПросмотра = Неопределено Тогда
			
			
			ПутьКФайлу = ФайлПросмотра.ПолноеИмя;
			РасширениеФайла = СтрЗаменить(ФайлПросмотра.Расширение, ".", "");
			
			ДДФайла = Новый ДвоичныеДанные(ПутьКФайлу);
			
			// Передадим на клиента двоичные данные файла для просмотра:
			АдресСтруктурыРазбораЭД = ПоместитьВоВременноеХранилище(ДДФайла, УникальныйИдентификатор);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПапкаДляРаспаковки) Тогда
		УдалитьФайлы(ПапкаДляРаспаковки);
	КонецЕсли;
	
	Если СтруктураРазбора = Неопределено
		И ДанныеЭД = Неопределено
		И ПустаяСтрока(АдресСтруктурыРазбораЭД) Тогда 
		Отказ = Истина; // Если нечего открывать, то и форму открывать незачем.
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ИмяСправочника(ИмяСправочника)
	
	ИмяСправочника = ЭлектронныеДокументыПовтИсп.ПолучитьИмяПрикладногоСправочника(ИмяСправочника);
	
	Возврат ИмяСправочника;
	
КонецФункции

&НаСервере
Функция НайтиСтрокуВДереве(ДеревоРазбора, СтрокаОбъекта, ИмяОбъектаПоиска)
	
	ВозвращаемоеЗначение = Неопределено;
	
	СтруктураПоиска = Новый Структура("Реквизит", ИмяОбъектаПоиска);
	МассивСтрок = СтрокаОбъекта.Строки.НайтиСтроки(СтруктураПоиска);
	Если МассивСтрок.Количество() > 0 Тогда
		ИндексСтрокиКонтрагента = МассивСтрок[0].ЗначениеРеквизита;
		СтруктураПоиска = Новый Структура("ИндексСтроки", ИндексСтрокиКонтрагента);
		МассивСтрок = ДеревоРазбора.Строки.НайтиСтроки(СтруктураПоиска, Истина);
		Если МассивСтрок.Количество() > 0 Тогда
			СтрокаДерева = МассивСтрок[0];
			ВозвращаемоеЗначение = СтрокаДерева;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

&НаСервере
Функция СформироватьДокументИБ(ДанныеФормы, ТекстСообщения, Записывать = Ложь, ОбновитьСтруктуруРазбора = Ложь)
	
	Перем СтрокаОбъекта, ДеревоРазбора;
	
	ДокументИБСформирован = Истина;
	
	Если Не ОбновитьСтруктуруРазбора
		И ЗначениеЗаполнено(АдресСтруктурыРазбораЭД)
		И ЭтоАдресВременногоХранилища(АдресСтруктурыРазбораЭД) Тогда
		
		СтруктураРазбора = ПолучитьИзВременногоХранилища(АдресСтруктурыРазбораЭД);
		
	Иначе
		
		ИмяФайла = ПолучитьИмяВременногоФайла("xml");
		ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(ДанныеФайлаРазбора);
		ДвоичныеДанныеФайла.Записать(ИмяФайла);
		
		СтруктураРазбора = ЭлектронныеДокументыВнутренний.СформироватьДеревоРазбора(ИмяФайла,
																					Перечисления.НаправленияЭД.Входящий);
		УдалитьФайлы(ИмяФайла);

	КонецЕсли;
	
	Если СтруктураРазбора <> Неопределено И СтруктураРазбора.Свойство("ДеревоРазбора", ДеревоРазбора)
		И СтруктураРазбора.Свойство("СтрокаОбъекта", СтрокаОбъекта) Тогда
		
		Если МножественнаяЗагрузка() Тогда 
			МассивДокументов = Новый Массив;
			
			Если СпособЗагрузкиДокумента = 1 Тогда 
				МассивДокументов.Вставить(ДокументИБ);
				МассивДокументов.Вставить(ДокументИБ2);			
			КонецЕсли;
			ДокументСсылка = МассивДокументов;
		Иначе
			ДокументСсылка = ?(СпособЗагрузкиДокумента = 1, ДокументИБ, Неопределено);
		КонецЕсли;
		Отказ = Ложь;
		ЭлектронныеДокументыСлужебный.СформироватьДокумент(
			СтруктураРазбора.ФорматЭлектронногоДокумента, ДеревоРазбора, СтрокаОбъекта, ДокументСсылка, Контрагент, Записывать, ТекстСообщения, Отказ);
		
		Если Не Отказ Тогда
				
			Если МножественнаяЗагрузка() Тогда
				ДанныеФормы = ДокументСсылка; 
				Возврат Истина;
			КонецЕсли;
				
			Если Записывать Тогда
				ЭлементОбъект = ДокументСсылка.ПолучитьОбъект();
			Иначе
				ЭлементОбъект = ДокументСсылка;
			КонецЕсли;
			Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(ЭлементОбъект)) Тогда
				ЭлементОбъект = ЭлементОбъект.ПолучитьОбъект();
			КонецЕсли;
			
			Если ДанныеФормы <> Неопределено Тогда
				
				ЗначениеВДанныеФормы(ЭлементОбъект, ДанныеФормы);
			Иначе
				
				Если ВРег(ВидЭД) = ВРег("РеквизитыОрганизации") Тогда
					
					ДанныеФормы = ДокументСсылка;
					
				Иначе
					
					ДанныеФормы = ДокументСсылка;
					
				КонецЕсли;
			КонецЕсли;
		Иначе
			ДокументИБСформирован = Ложь;
		КонецЕсли;		
	Иначе
		ДокументИБСформирован = Ложь;
	КонецЕсли;
	
	Возврат ДокументИБСформирован;
	
КонецФункции

&НаСервереБезКонтекста
Функция МожноЗагрузитьЭДВида(Знач ВидЭД)
	
	МожноЗагрузить = Истина;
	МассивАктуальныхВидовЭД = ЭлектронныеДокументыПовтИсп.ПолучитьАктуальныеВидыЭД();
	Если МассивАктуальныхВидовЭД.Найти(ВидЭД) = Неопределено Тогда
		МожноЗагрузить = Ложь;
	КонецЕсли;
	
	Возврат МожноЗагрузить;
	
КонецФункции

&НаСервере
Функция ФайлДанныхЭД(СсылкаНаЭД, Знач ИмяФайлаПодчиненногоЭД = Неопределено)
	
	ПараметрыПросмотра = Новый Структура;
	ПараметрыПросмотра.Вставить("ИмяФайлаПодчиненногоЭД", ИмяФайлаПодчиненногоЭД);
	
	ТабличныйДокумент = ЭлектронныеДокументыВнутренний.ФайлДанныхЭД(СсылкаНаЭД, ПараметрыПросмотра);
	
	Возврат ТабличныйДокумент;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьВыборТипаОбъекта()
	
	Элементы.ДокументИБ2.Доступность = Ложь;

	Если ЗначениеЗаполнено(ТипОбъекта) Тогда
		Для Каждого Элемент ИЗ СписокТипов Цикл
			Если Найти(Элемент.Представление, ТипОбъекта) Тогда
				ВыбраннаяСсылка = Элемент.Значение;
				Если НЕ ЗначениеЗаполнено(ДокументИБ) ИЛИ ТипЗнч(ДокументИБ) <> ТипЗнч(ВыбраннаяСсылка) Тогда
					ДокументИБ = ВыбраннаяСсылка;
				КонецЕсли;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого Элемент ИЗ СписокТипов2 Цикл
			Если Найти(Элемент.Представление, ТипОбъекта) Тогда
				ВыбраннаяСсылка = Элемент.Значение;
				Если НЕ ЗначениеЗаполнено(ДокументИБ2) ИЛИ ТипЗнч(ДокументИБ2) <> ТипЗнч(ВыбраннаяСсылка) Тогда
					ДокументИБ2 = ВыбраннаяСсылка;
					Элементы.ДокументИБ2.Доступность = Истина;
				КонецЕсли;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьТекстСлужебногоСообщения()
	
	Если ЗначениеЗаполнено(ИмяФайлаХМЛ) Тогда
		
		ЗаголовокЭлемента = НСтр("ru = 'Не удалось прочитать файл """+ ИмяФайлаХМЛ+".""'");
		
	Иначе
		
		ЗаголовокЭлемента = НСтр("ru = 'Не найден файл электронного документа ""* .xml.""'");
		
	КонецЕсли;
	
	Элементы.КомментарийСлужебноеСообщение.Заголовок = ЗаголовокЭлемента;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ВыполнитьДействие(Команда)
	
	Если ЗагрузкаЭД Тогда
		ОчиститьСообщения();
		
		Отказ = Ложь;
		ТекстСообщения = "";
		
		Если ВРег(ВидЭД) = ВРег("РеквизитыОрганизации") Тогда
			
			Отказ = Ложь;
			ТекстСообщения = "";
			
			Если СпособЗагрузкиДокумента = 1 И Не ЗначениеЗаполнено(ДокументИБ) Тогда
				ТекстСообщения = НСтр("ru = 'Не указан элемент справочника для перезаполнения.'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				Отказ = Истина;
			КонецЕсли;
			
			СоздатьОбъектыИБ(АдресСтруктурыРазбораЭД, Отказ);
			
			Если Отказ Тогда
				Возврат;
			КонецЕсли;
			
			ДанныеФормы = Неопределено;
			Если Не СформироватьДокументИБ(ДанныеФормы, ТекстСообщения, Истина) Тогда
				Отказ = Истина;
			КонецЕсли;
			
			
			Если Отказ Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				Возврат;
			КонецЕсли;
			
			Закрыть();
			
			ОткрытьЗначение(ДанныеФормы);
			
		Иначе
			
			Если НЕ МожноЗагрузитьЭДВида(ВидЭД) Тогда
				ТекстСообщения = НСтр("ru = 'Не поддерживается загрузка электронных документов вида ""%1"".'");
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", ВидЭД);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				Отказ = Истина;
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(Контрагент) Тогда
				ТекстСообщения = НСтр("ru = 'Не указан контрагент.'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				Отказ = Истина;
			КонецЕсли;
			
			Если НЕ МножественнаяЗагрузка() Тогда 
			
				Если СпособЗагрузкиДокумента = 1 И НЕ ЗначениеЗаполнено(ДокументИБ) Тогда
					ТекстСообщения = НСтр("ru = 'Не указан документ для перезаполнения.'");
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
					Отказ = Истина;
				КонецЕсли;
				
				СоздатьОбъектыИБ(АдресСтруктурыРазбораЭД, Отказ);
				
				Если Отказ Тогда
					Возврат;
				КонецЕсли;
				
				СопоставлятьНоменклатуруПередЗаполнениемДокумента = Ложь;
				ЭлектронныеДокументыКлиентПереопределяемый.СопоставлятьНоменклатуруПередЗаполнениемДокумента(СопоставлятьНоменклатуруПередЗаполнениемДокумента);
				
				Если СопоставлятьНоменклатуруПередЗаполнениемДокумента Тогда
					ОбновитьСтруктуруРазбора = СопоставитьНоменклатуру();
				КонецЕсли;

				Попытка
					ИмяОбъектаМетаданных = ИмяОбъектаМетаданных(ДокументИБ);
					Если СпособЗагрузкиДокумента = 1 И ЗначениеЗаполнено(ДокументИБ) Тогда
						ФормаДокумента = ПолучитьФорму(ИмяОбъектаМетаданных + ".ФормаОбъекта", Новый Структура("Ключ", ДокументИБ));
					Иначе
						ФормаДокумента = ПолучитьФорму(ИмяОбъектаМетаданных + ".ФормаОбъекта");
					КонецЕсли;
				Исключение
					ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , , Отказ);
				КонецПопытки;
				
				Если НЕ Отказ Тогда
					Если ТипЗнч(ФормаДокумента) = Тип("УправляемаяФорма") Тогда
						ДанныеФормы = ФормаДокумента.Объект;
					Иначе
						ДанныеФормы = Неопределено;
					КонецЕсли;
					
					Если ОбновитьСтруктуруРазбора = Неопределено Тогда
						ОбновитьСтруктуруРазбора = Ложь;
					КонецЕсли;
					
					Если Не СформироватьДокументИБ(ДанныеФормы, ТекстСообщения, , ОбновитьСтруктуруРазбора) Тогда
						Отказ = Истина;
					КонецЕсли;
					
					Если Отказ Тогда
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
					Иначе
						
						Если Не СопоставлятьНоменклатуруПередЗаполнениемДокумента Тогда
							ЗначениеВозврата = СопоставитьНоменклатуру(ДанныеФормы);
							Если ЗначениеЗаполнено(ЗначениеВозврата) Тогда
								ЭлектронныеДокументыСлужебныйВызовСервера.ЗаполнитьИсточник(ДанныеФормы, ЗначениеВозврата);
							КонецЕсли;
						КонецЕсли;
						Если ТипЗнч(ФормаДокумента) = Тип("УправляемаяФорма") Тогда
							КопироватьДанныеФормы(ДанныеФормы, ФормаДокумента.Объект);
						Иначе
							ФормаДокумента.ДокументОбъект = ДанныеФормы;
						КонецЕсли;
						
						МассивОповещения = Новый Массив;
						МассивОповещения.Добавить(ДокументИБ);
						Оповестить("ОбновитьДокументИБПослеЗаполненияИзФайла", МассивОповещения);
						
						ФормаДокумента.Открыть();
						ФормаДокумента.Модифицированность = Истина;
						Закрыть();
					КонецЕсли;
				КонецЕсли; 
				
			Иначе
				
				Если СпособЗагрузкиДокумента = 1 И (НЕ ЗначениеЗаполнено(ДокументИБ) ИЛИ НЕ ЗначениеЗаполнено(ДокументИБ2)) Тогда
					ТекстСообщения = НСтр("ru = 'Не указан один из документов для перезаполнения.'");
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
					Отказ = Истина;
				КонецЕсли;
				
				СоздатьОбъектыИБ(АдресСтруктурыРазбораЭД, Отказ);
				
				Если Отказ Тогда
					Возврат;
				КонецЕсли;
				
				ЭлектронныеДокументыКлиентПереопределяемый.СопоставлятьНоменклатуруПередЗаполнениемДокумента(Истина);
				ОбновитьСтруктуруРазбора = СопоставитьНоменклатуру();
				Если ОбновитьСтруктуруРазбора = Неопределено Тогда
					ОбновитьСтруктуруРазбора = Ложь;
				КонецЕсли;
				ДанныеФормы = Неопределено;
			
				Если Не СформироватьДокументИБ(ДанныеФормы, ТекстСообщения, Ложь, ОбновитьСтруктуруРазбора) Тогда
					Отказ = Истина;
				КонецЕсли;
				
				Если Отказ Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				Иначе
					ОткрытьЗначение(ДанныеФормы[0]);   
					ОткрытьЗначение(ДанныеФормы[1]);   						
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ВидЭД = ПредопределенноеЗначение("Перечисление.ВидыЭД.КаталогТоваров") Тогда
		
		СоздатьОбъектыИБ(АдресСтруктурыРазбораЭД, Ложь);
		СопоставитьНоменклатуру();
		СохранитьДанныеОбъектаВБД(АдресСтруктурыРазбораЭД);
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ПОЛЕЙ ФОРМЫ

&НаКлиенте
Процедура СпособЗагрузкиДокументаПриИзменении(Элемент)
	
	ИзменитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура СпособЗагрузкиСправочникаПриИзменении(Элемент)
	
	ИзменитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ТипОбъектаПриИзменении(Элемент)
	
	ОбработатьВыборТипаОбъекта();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокТиповСправочниковПриИзменении(Элемент)
	
	ОбработатьВыборТипаОбъекта();
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументИБНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ДокументИБ = Неопределено Тогда
		Если ЗначениеЗаполнено(ТипОбъекта) Тогда
			Для Каждого ЭлементСписка Из СписокТипов Цикл
				Если Найти(ЭлементСписка.Представление, ТипОбъекта) Тогда
					ДокументИБ = ЭлементСписка.Значение;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли; 
	
	Если ДокументИБ2 = Неопределено Тогда
		Если ЗначениеЗаполнено(ТипОбъекта) Тогда
			Для Каждого ЭлементСписка Из СписокТипов2 Цикл
				Если Найти(ЭлементСписка.Представление, ТипОбъекта) Тогда
					ДокументИБ2 = ЭлементСписка.Значение;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураЭД = "";
	НаправлениеЭД = "";
	Если Параметры.Свойство("СтруктураЭД", СтруктураЭД) И ТипЗнч(СтруктураЭД) = Тип("Структура")
		И СтруктураЭД.Свойство("НаправлениеЭД", НаправлениеЭД) Тогда
		
		ЗагрузкаЭД = (НаправлениеЭД = Перечисления.НаправленияЭД.Входящий);
		
		СтруктураЭД.Свойство("ВладелецЭД", ДокументИБ);
		Если ЗагрузкаЭД Тогда
			
			СсылкаНаЗаполняемыйДокумент = "";
			Если СтруктураЭД.Свойство("СсылкаНаДокумент", СсылкаНаЗаполняемыйДокумент)
				И ЗначениеЗаполнено(СсылкаНаЗаполняемыйДокумент) Тогда
				
				СпособЗагрузкиДокумента = 1;
			КонецЕсли;
		КонецЕсли;
		ВыполнитьПросмотрЭДСервер(СтруктураЭД, Отказ);
	КонецЕсли;
	
	ЭлектронныйДокумент = Неопределено;
	Если Параметры.Свойство("ЭлектронныйДокумент", ЭлектронныйДокумент) Тогда
		ЗагрузкаЭД = Ложь;
		Параметры.Свойство("ВладелецЭД", ДокументИБ);
		ДанныеЭД = ФайлДанныхЭД(ЭлектронныйДокумент);
		Если ДанныеЭД = Неопределено Тогда
			Возврат;
		КонецЕсли;
		Если ТипЗнч(ДанныеЭД) = Тип("ТабличныйДокумент") Тогда
			ТабличныйДокументФормы = ДанныеЭД;
		КонецЕсли;
	КонецЕсли;
	
	ИзменитьВидимостьДоступностьПриСозданииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ИзменитьВидимостьДоступность();
	
	Если ЭтоАдресВременногоХранилища(АдресСтруктурыРазбораЭД) И ЗначениеЗаполнено(РасширениеФайла) Тогда
		#Если ВебКлиент Тогда
			ПутьКФайлуПросмотра = АдресСтруктурыРазбораЭД;
		#Иначе
			ПутьКФайлуПросмотра = ПолучитьИмяВременногоФайла(РасширениеФайла);
			ДДФайла = ПолучитьИзВременногоХранилища(АдресСтруктурыРазбораЭД);
			ДДФайла.Записать(ПутьКФайлуПросмотра);
		#КонецЕсли
		Если Найти("HTML PDF DOCX XLSX", ВРег(РасширениеФайла)) > 0 Тогда
			
			СформироватьТекстСлужебногоСообщения();
			
			Элементы.Панель.ТекущаяСтраница = Элементы.ПросмотрЭД;
			
		Иначе
			#Если НЕ ВебКлиент Тогда
				ЗапуститьПриложение(ПутьКФайлуПросмотра);
			#КонецЕсли
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	
КонецПроцедуры

