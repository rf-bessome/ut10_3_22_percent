
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Владелец") И ЗначениеЗаполнено(Параметры.Владелец) Тогда
		КонтактнаяИнформацияОбъект = РеквизитФормыВЗначение("КонтактнаяИнформация");
		УправлениеКонтактнойИнформацией.ПрочитатьКонтактнуюИнформацию(КонтактнаяИнформацияОбъект, Параметры.Владелец);
		КонтактнаяИнформацияТаблица = КонтактнаяИнформацияОбъект.Выгрузить();
		КонтактнаяИнформацияОбъект.Очистить();
		Если Параметры.Свойство("ТипКонтактнойИнформации")
			И ЗначениеЗаполнено(Параметры.ТипКонтактнойИнформации) Тогда
			
			Для каждого Строка из КонтактнаяИнформацияТаблица Цикл
				Если Строка.Тип = Параметры.ТипКонтактнойИнформации Тогда
					НоваяЗапись = КонтактнаяИнформацияОбъект.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяЗапись, Строка);
				КонецЕсли;
			КонецЦикла;
		Иначе
			Для каждого Строка из КонтактнаяИнформацияТаблица Цикл
				НоваяЗапись = КонтактнаяИнформацияОбъект.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяЗапись, Строка);
			КонецЦикла;
		КонецЕсли;
		
		ЗначениеВРеквизитФормы(КонтактнаяИнформацияОбъект, "КонтактнаяИнформация");
	КонецЕсли;
	
	Элементы.КонтактнаяИнформация.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактнаяИнформацияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.КонтактнаяИнформация.ТекущиеДанные;
	ВыбратьАдрес(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	ТекущиеДанные = Элементы.КонтактнаяИнформация.ТекущиеДанные;
	ВыбратьАдрес(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьАдрес(ВыбраннаяСтрока)
	
	ДанныеВыбраннойСтроки = ВыбраннаяСтрока;
	СтруктураДанныхАдреса = ПолучитьСтруктуруАдреса();
	Для каждого Строка из СтруктураДанныхАдреса Цикл
		СтруктураДанныхАдреса[Строка.Ключ] = ?(ДанныеВыбраннойСтроки.Свойство(Строка.Ключ), ДанныеВыбраннойСтроки[Строка.Ключ], "");
	КонецЦикла;
	Закрыть(СтруктураДанныхАдреса);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСтруктуруАдреса()
	
	СтруктураАдреса = Новый Структура;
	Для каждого Измерение Из Метаданные.РегистрыСведений.КонтактнаяИнформация.Измерения Цикл
		СтруктураАдреса.Вставить(Измерение.Имя, "");
	КонецЦикла;
	Для каждого Ресурс Из Метаданные.РегистрыСведений.КонтактнаяИнформация.Ресурсы Цикл
		СтруктураАдреса.Вставить(Ресурс.Имя, "");
	КонецЦикла;
	Для каждого Реквизит Из Метаданные.РегистрыСведений.КонтактнаяИнформация.Реквизиты Цикл
		СтруктураАдреса.Вставить(Реквизит.Имя, "");
	КонецЦикла;
	
	Возврат СтруктураАдреса;
	
КонецФункции
