
Функция ТекстЗапросаТоварныеПозицииЧека() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВозвратТоваровОтПокупателяТовары.ЕдиницаИзмерения,
	|	ВозвратТоваровОтПокупателяТовары.Номенклатура,
	|	ВозвратТоваровОтПокупателяТовары.ХарактеристикаНоменклатуры КАК Характеристика,
	|	ВозвратТоваровОтПокупателяТовары.СерияНоменклатуры КАК Серия,
	|	ВозвратТоваровОтПокупателяТовары.СерияНоменклатуры.НомерГТД КАК НомерГТД,
	|	ВозвратТоваровОтПокупателяТовары.СерияНоменклатуры.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ВозвратТоваровОтПокупателяТовары.Количество,
	|	ВозвратТоваровОтПокупателяТовары.Коэффициент,
	|	ВозвратТоваровОтПокупателяТовары.СтавкаНДС,
	|	ВЫБОР
	|		КОГДА ВозвратТоваровОтПокупателяТовары.Ссылка.СуммаВключаетНДС
	|			ТОГДА ВозвратТоваровОтПокупателяТовары.Сумма
	|		ИНАЧЕ ВозвратТоваровОтПокупателяТовары.Сумма + ВозвратТоваровОтПокупателяТовары.СуммаНДС
	|	КОНЕЦ КАК Сумма,
	|	ВозвратТоваровОтПокупателяТовары.СуммаНДС,
	|	ВЫБОР
	|		КОГДА ВозвратТоваровОтПокупателяТовары.Ссылка.СуммаВключаетНДС
	|			ТОГДА ВозвратТоваровОтПокупателяТовары.Цена
	|		ИНАЧЕ ВозвратТоваровОтПокупателяТовары.Цена + ВозвратТоваровОтПокупателяТовары.СуммаНДС / ВозвратТоваровОтПокупателяТовары.Количество
	|	КОНЕЦ КАК Цена,
	|	ВозвратТоваровОтПокупателяТовары.Номенклатура.Наименование КАК Наименование,
	|	ВЫБОР
	|		КОГДА ВозвратТоваровОтПокупателяТовары.Склад <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|			ТОГДА ВозвратТоваровОтПокупателяТовары.Склад.НомерСекции
	|		КОГДА ТИПЗНАЧЕНИЯ(ВозвратТоваровОтПокупателяТовары.Ссылка.СкладОрдер) = ТИП(Справочник.Склады)
	|				И ВозвратТоваровОтПокупателяТовары.Ссылка.СкладОрдер <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|			ТОГДА ВозвратТоваровОтПокупателяТовары.Ссылка.СкладОрдер.НомерСекции
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НомерСекции
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
	|ГДЕ
	|	ВозвратТоваровОтПокупателяТовары.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ИменаРеквизитовДляЗаполненияПараметровУказанияСерий() Экспорт
	Возврат "";
КонецФункции

Функция ПараметрыУказанияСерий(ИменаРеквизитов) Экспорт 
	Возврат Неопределено;
КонецФункции

// Возвращает предварительные параметры фискализации чека для документа.
//
// Параметры:
//   ДокументСсылка - ДокументСсылка.ВозвратТоваровОтПокупателя - Документ.
//   ВерсияФФД - Строка - Версия ФФД.
//
// ВозвращаемоеЗначение:
//  Неопределено, Структура - см. функцию МенеджерОборудованияКлиентСервер.ПараметрыОперацииФискализацииЧека().
//
Функция ПараметрыОперацииФискализацииЧека(ДокументСсылка, СуммаПредоплатыКорректировка = Неопределено, ВерсияФФД = Неопределено) Экспорт
	
	Если СуммаПредоплатыКорректировка = Неопределено Тогда
		СуммаПредоплатыКорректировка = 0;
		
		Если ДокументСсылка.ДокументыРасчетовСКонтрагентом.Количество() <> 0 Тогда
			СуммаПредоплатыКорректировка = 0;
			Для Каждого СтрокаРасчетныйДокумент Из ДокументСсылка.ДокументыРасчетовСКонтрагентом Цикл
			
				Если ДокументСсылка.ВалютаДокумента = глЗначениеПеременной("ВалютаРегламентированногоУчета") Тогда
					КурсОплаты = 1;
				Иначе
					КурсОплаты = ДокументСсылка.КурсВзаиморасчетов / ДокументСсылка.КратностьВзаиморасчетов;
				КонецЕсли;

				СуммаПредоплатыКорректировка = СуммаПредоплатыКорректировка + СтрокаРасчетныйДокумент.СуммаВзаиморасчетов * КурсОплаты;
			
			КонецЦикла;
		
			Если СуммаПредоплатыКорректировка >= ДокументСсылка.СуммаДокумента * КурсОплаты Тогда
			    СуммаПредоплатыКорректировка = ДокументСсылка.СуммаДокумента * КурсОплаты;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
		
	Возврат ТорговоеОборудованиеУТВызовСервера.ПараметрыОперацииФискализацииЧекаВозвратТоваровОтПокупателя(ДокументСсылка, СуммаПредоплатыКорректировка, ВерсияФФД);
	
КонецФункции

Функция ТекстЗапросаПрослеживаемыеТовары(НомераТаблиц, ПараметрыПроведения, Реквизиты) Экспорт
	
	Если Не Реквизиты.ВедетсяУчетПрослеживаемыхТоваров Тогда
		ПараметрыПроведения.Вставить("ВТ_Прослеживаемость",    Неопределено);
		ПараметрыПроведения.Вставить("ВТ_СуммыБезНДС",         Неопределено);
		ПараметрыПроведения.Вставить("ПрослеживаемыеОперации", Неопределено);
		ПараметрыПроведения.Вставить("ПрослеживаемыеТовары",   Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ВТ_Прослеживаемость",    НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВТ_СуммыБезНДС",         НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПрослеживаемыеОперации", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПрослеживаемыеТовары",   НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВозвратТоваровОтПокупателяТовары.Номенклатура КАК Номенклатура,
	|	СУММА(ВозвратТоваровОтПокупателяСведенияПрослеживаемости.Количество) КАК Количество,
	|	ВозвратТоваровОтПокупателяТовары.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ВозвратТоваровОтПокупателяСведенияПрослеживаемости.РНПТ КАК РНПТ,
	|	СУММА(ВозвратТоваровОтПокупателяСведенияПрослеживаемости.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости,
	|	ВозвратТоваровОтПокупателяТовары.Ссылка КАК ДокументОперации,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ВозвратТоваровОтПокупателяТовары.ДокументПартии) = ТИП(Документ.ОтчетОРозничныхПродажах)
	|			ТОГДА ВозвратТоваровОтПокупателяТовары.ДокументПартии.Дата
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|	КОНЕЦ КАК ДатаРеализации,
	|	ВЫБОР
	|		КОГДА НЕ &ЭтоДоговорСКомиссионером
	|			ТОГДА ВозвратТоваровОтПокупателяТовары.ДокументПартии
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ОснованиеДляВозврата,
	|	ЕСТЬNULL(ВозвратТоваровОтПокупателя.Номер, """") КАК НомерДокумента,
	|	ЕСТЬNULL(ВозвратТоваровОтПокупателя.Дата, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаДокумента,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ВозвратТоваровОтПокупателяТовары.ДокументПартии) = ТИП(Документ.ОтчетОРозничныхПродажах)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ОтчетОбОперациях)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.КнигаПокупок)
	|	КОНЕЦ КАК ОтражениеВОтчетности,
	|	ЗНАЧЕНИЕ(Справочник.ТипыДокументов.Акт) КАК ТипДокументаВПрослеживаемости,
	|	ВозвратТоваровОтПокупателя.Контрагент КАК Контрагент,
	|	НАЧАЛОПЕРИОДА(ВозвратТоваровОтПокупателя.Дата, КВАРТАЛ) КАК ПериодОперации,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ВозвратТоваровОтПокупателяТовары.ДокументПартии) = ТИП(Документ.ОтчетОРозничныхПродажах)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ВозвратОтРозничногоПокупателя)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ПустаяСсылка)
	|	КОНЕЦ КАК КодОперации,
	|	ВозвратТоваровОтПокупателяСведенияПрослеживаемости.Сумма КАК СуммаБезНДС
	|ПОМЕСТИТЬ ВТ_Прослеживаемость
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтПокупателя.СведенияПрослеживаемости КАК ВозвратТоваровОтПокупателяСведенияПрослеживаемости
	|		ПО ВозвратТоваровОтПокупателяТовары.Ссылка = ВозвратТоваровОтПокупателяСведенияПрослеживаемости.Ссылка
	|			И ВозвратТоваровОтПокупателяТовары.ИдентификаторСтроки = ВозвратТоваровОтПокупателяСведенияПрослеживаемости.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|		ПО ВозвратТоваровОтПокупателяТовары.Ссылка = ВозвратТоваровОтПокупателя.Ссылка
	|ГДЕ
	|	ВозвратТоваровОтПокупателяТовары.ПрослеживаемыйТовар
	|	И ВозвратТоваровОтПокупателяТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВозвратТоваровОтПокупателяТовары.СтранаПроисхождения,
	|	ВозвратТоваровОтПокупателяСведенияПрослеживаемости.РНПТ,
	|	ВозвратТоваровОтПокупателяТовары.Номенклатура,
	|	ВозвратТоваровОтПокупателяТовары.Ссылка,
	|	ВозвратТоваровОтПокупателя.Контрагент,
	|	ЕСТЬNULL(ВозвратТоваровОтПокупателя.Номер, """"),
	|	ЕСТЬNULL(ВозвратТоваровОтПокупателя.Дата, ДАТАВРЕМЯ(1, 1, 1)),
	|	НАЧАЛОПЕРИОДА(ВозвратТоваровОтПокупателя.Дата, КВАРТАЛ),
	|	ВЫБОР
	|		КОГДА НЕ &ЭтоДоговорСКомиссионером
	|			ТОГДА ВозвратТоваровОтПокупателяТовары.ДокументПартии
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ВозвратТоваровОтПокупателяТовары.ДокументПартии) = ТИП(Документ.ОтчетОРозничныхПродажах)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ОтчетОбОперациях)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.КнигаПокупок)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ВозвратТоваровОтПокупателяТовары.ДокументПартии) = ТИП(Документ.ОтчетОРозничныхПродажах)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ВозвратОтРозничногоПокупателя)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ВозвратТоваровОтПокупателяТовары.ДокументПартии) = ТИП(Документ.ОтчетОРозничныхПродажах)
	|			ТОГДА ВозвратТоваровОтПокупателяТовары.ДокументПартии.Дата
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|	КОНЕЦ,
	|	ВозвратТоваровОтПокупателяСведенияПрослеживаемости.Сумма
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВозвратТоваровОтПокупателяТовары.Номенклатура КАК Номенклатура,
	|	СУММА(ВЫБОР
	|			КОГДА ВозвратТоваровОтПокупателяТовары.Ссылка.СуммаВключаетНДС
	|				ТОГДА ВозвратТоваровОтПокупателяТовары.Сумма - ВозвратТоваровОтПокупателяТовары.СуммаНДС
	|			ИНАЧЕ ВозвратТоваровОтПокупателяТовары.Сумма
	|		КОНЕЦ) КАК СуммаБезНДС
	|ПОМЕСТИТЬ ВТ_СуммыБезНДС
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
	|ГДЕ
	|	ВозвратТоваровОтПокупателяТовары.Ссылка = &Ссылка
	|	И ВозвратТоваровОтПокупателяТовары.ПрослеживаемыйТовар
	|
	|СГРУППИРОВАТЬ ПО
	|	ВозвратТоваровОтПокупателяТовары.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Прослеживаемость.Номенклатура КАК Номенклатура,
	|	ВТ_Прослеживаемость.Количество КАК Количество,
	|	ВТ_Прослеживаемость.РНПТ КАК РНПТ,
	|	ВТ_Прослеживаемость.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|	ВТ_Прослеживаемость.ДокументОперации КАК ДокументОперации,
	|	ЕСТЬNULL(ВТ_СуммыБезНДС.СуммаБезНДС, 0) КАК СуммаБезНДС,
	|	ВТ_Прослеживаемость.НомерДокумента КАК НомерДокумента,
	|	ВТ_Прослеживаемость.ДатаДокумента КАК ДатаДокумента,
	|	ВТ_Прослеживаемость.ОтражениеВОтчетности КАК ОтражениеВОтчетности,
	|	ВТ_Прослеживаемость.ТипДокументаВПрослеживаемости КАК ТипДокументаВПрослеживаемости,
	|	ВТ_Прослеживаемость.Контрагент КАК Контрагент,
	|	ВТ_Прослеживаемость.ПериодОперации КАК ПериодОперации,
	|	ВТ_Прослеживаемость.КодОперации КАК КодОперации
	|ИЗ
	|	ВТ_Прослеживаемость КАК ВТ_Прослеживаемость
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СуммыБезНДС КАК ВТ_СуммыБезНДС
	|		ПО ВТ_Прослеживаемость.Номенклатура = ВТ_СуммыБезНДС.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Прослеживаемость.Номенклатура КАК Номенклатура,
	|	СУММА(ВТ_Прослеживаемость.Количество) КАК Количество,
	|	ВТ_Прослеживаемость.СтранаПроисхождения КАК СтранаПроисхождения,
	|	СУММА(ВТ_Прослеживаемость.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Комитент,
	|	ВТ_Прослеживаемость.РНПТ КАК РНПТ,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыЗапасовПрослеживаемыхТоваров.ПустаяСсылка) КАК ВидЗапасов,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Комиссионер,
	|	ВТ_Прослеживаемость.ДатаРеализации КАК ДатаРеализации,
	|	ВТ_Прослеживаемость.ОснованиеДляВозврата КАК ОснованиеДляВозврата
	|ИЗ
	|	ВТ_Прослеживаемость КАК ВТ_Прослеживаемость
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Прослеживаемость.Номенклатура,
	|	ВТ_Прослеживаемость.СтранаПроисхождения,
	|	ВТ_Прослеживаемость.РНПТ,
	|	ВТ_Прослеживаемость.ДатаРеализации,
	|	ВТ_Прослеживаемость.ОснованиеДляВозврата";
	
	Возврат ТекстЗапроса + ПрослеживаемостьБП.ТекстРазделителяЗапросовПакета();
		
КонецФункции