
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПолучитьСерверТО().ПодключитьКлиента(ЭтаФорма);
	КомпьютерШапка = ПолучитьСерверТО().ПолучитьИмяКомпьютераТО();
	УстановитьОтборСписка();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	ПолучитьСерверТО().ОтключитьКлиента(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура РеквизитШапкиПриИзменении(Элемент)
	
	УстановитьОтборСписка();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьКассовуюСмену(Команда)
	
	ОчиститьСообщения();
	
	МассивККТ = ПолучитьСерверТО().ПолучитьСписокУстройств(
		ПредопределенноеЗначение("Перечисление.ВидыТорговогоОборудования.ККТ"), КассаККМШапка);
		
	КоличествоККТ = МассивККТ.Количество();
	Если КоличествоККТ = 0 Тогда
		ТекстСообщения = НСтр("ru='Отсутствуют доступные фискальные устройства'");
		ОбщегоНазначения.СообщитьИнформациюПользователю(ТекстСообщения);
	ИначеЕсли КоличествоККТ = 1 Тогда
		ККТ = МассивККТ[0];
	Иначе
		ПредставлениеУстройства = "";
		ВидУстройства = "";
		СписокККТ = Новый СписокЗначений;

		Для Каждого Устройство Из МассивККТ Цикл
			ПолучитьСерверТО().ПолучитьПредставлениеУстройства(Устройство, ВидУстройства, ПредставлениеУстройства);
			СписокККТ.Добавить(Устройство, ПредставлениеУстройства);
		КонецЦикла;

		ККТ = СписокККТ.ВыбратьЭлемент("Необходимо выбрать фискальное устройство");
		Если ККТ <> Неопределено Тогда
			ККТ = ККТ.Значение;
		КонецЕсли;
	КонецЕсли;
	
	Если ККТ = NULL ИЛИ ККТ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КассовыеСменыКлиент.ОткрытьКассовуюСмену(ККТ);
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьКассовуюСмену(Команда)
	
	Если Не КассовыеСменыВызовСервера.ЕстьПравоНаЗакрытиеСмены() Тогда 
		Предупреждение("Нарушение прав доступа");
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	ТекущиеДанныеСписка = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанныеСписка <> Неопределено Тогда
		СтатусСмены = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущиеДанныеСписка.Ссылка, "Статус");
		Если СтатусСмены = ПредопределенноеЗначение("Перечисление.СтатусыКассовойСмены.Закрыта") Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Смена уже закрыта.");
			Возврат;
		КонецЕсли;
	Иначе
		ОбщегоНазначения.СообщитьИнформациюПользователю("Выберите смену, которую нужно закрыть.");
		Возврат;
	КонецЕсли;
	ККТ = ТекущиеДанныеСписка.ИдентификаторУстройства;
	
	Если ККТ = NULL ИЛИ ККТ = Неопределено ИЛИ ПустаяСтрока(ККТ) Тогда
		Возврат;
	КонецЕсли;

	КассовыеСменыКлиент.ЗакрытьКассовуюСмену(ККТ, ТекущиеДанныеСписка.Ссылка);
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьОтборСписка()
	
	Список.Отбор.Элементы.Очистить();
	
	Если ЗначениеЗаполнено(КомпьютерШапка) Тогда
		
		ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение    = Новый ПолеКомпоновкиДанных("Компьютер");
		ЭлементОтбора.ВидСравнения     = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.Использование    = Истина;
		ЭлементОтбора.ПравоеЗначение   = КомпьютерШапка;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КассаККМШапка) Тогда
		
		ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение    = Новый ПолеКомпоновкиДанных("КассаККМ");
		ЭлементОтбора.ВидСравнения     = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.Использование    = Истина;
		ЭлементОтбора.ПравоеЗначение   = КассаККМШапка;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(МодельШапка) Тогда
		
		ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение    = Новый ПолеКомпоновкиДанных("Модель");
		ЭлементОтбора.ВидСравнения     = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.Использование    = Истина;
		ЭлементОтбора.ПравоеЗначение   = МодельШапка;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Функция ПоддерживаетсяВидТО(Вид) Экспорт

	Результат = Ложь;

	Если Вид = ПредопределенноеЗначение("Перечисление.ВидыТорговогоОборудования.ККТ") Тогда
		Результат = Истина;
	КонецЕсли;

	Возврат Результат;

КонецФункции // ПоддерживаетсяВидТО()

#КонецОбласти
