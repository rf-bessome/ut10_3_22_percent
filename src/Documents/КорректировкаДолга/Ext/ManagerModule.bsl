#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура СформироватьОчередьФискальныхЧеков(ФУ, Ссылка) Экспорт
		
	МенеджерОборудованияВызовСервера.УдалитьНефискализированныеЧекиИзОчереди(Ссылка);
	
	Для Каждого ВыбраннаяСтрока Из Ссылка.СуммыДолга Цикл
		ДанныеЧека = ПолучитьДанныеЧека(Ссылка, ВыбраннаяСтрока, ФУ);
		
		Если ДанныеЧека <> Неопределено Тогда
			МенеджерОборудованияВызовСервера.ДобавитьЧекВОчередьЧековККТ(ДанныеЧека)
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьДанныеЧека(Ссылка, СтрокаТЧ, ИдентификаторУстройства = Неопределено) Экспорт;
	
	Объект = Ссылка.ПолучитьОбъект();
	
	СерверТО = Обработки.ТОСервер.Создать();
	
	// Общие параметры чека
		Если СтрокаТЧ.ВидЗадолженности = Перечисления.ВидыЗадолженности.Дебиторская Тогда	// Обработка дебиторской задолженности
			ОбщиеПараметры = МенеджерОборудованияКлиентСервер.ПараметрыОперацииФискализацииЧека();
			
			Если ЗначениеЗаполнено(СтрокаТЧ.ИдентификаторФискальнойЗаписи) Тогда
				ОбщиеПараметры.ИдентификаторФискальнойЗаписи = СтрокаТЧ.ИдентификаторФискальнойЗаписи;
			Иначе
				ОбщиеПараметры.ИдентификаторФискальнойЗаписи = ИдентификаторФискальнойЗаписи(Ссылка, СтрокаТЧ);
			КонецЕсли;
	
			ОбщиеПараметры.ДокументОснование = Ссылка;  
	
			СтрокиПозицииЧекаЗаполнены = Ложь;
			ИтогПоПозициям = 0;
			
			Если ЗначениеЗаполнено(СтрокаТЧ.ДокументРасчетовСКонтрагентом) Тогда // Обработка документа расчета с контрагентом
				Услуги = Ложь;
				Товары = Ложь;
				
				Если ТипЗнч(СтрокаТЧ.ДокументРасчетовСКонтрагентом) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда 
					
					Если СтрокаТЧ.ДокументРасчетовСКонтрагентом.Услуги.Количество() >0 Тогда
						Услуги = Истина;
					Иначе
						Товары = Истина;
					КонецЕсли;
					
				ИначеЕсли (ТипЗнч(СтрокаТЧ.ДокументРасчетовСКонтрагентом) = Тип("ДокументСсылка.ВозвратТоваровПоставщику")) Или 
					(ТипЗнч(СтрокаТЧ.ДокументРасчетовСКонтрагентом) = Тип("ДокументСсылка.ВозвратТоваровПоставщикуИзНТТ")) Тогда 
						Товары = Истина;
				КонецЕсли;
					
				Если Услуги Тогда
					
					Для Каждого СтрокаТЧОснования Из СтрокаТЧ.ДокументРасчетовСКонтрагентом.Услуги Цикл
						НомерСекции = 0;
						СтрокаПозицииЧека = МенеджерОборудованияКлиентСервер.ПараметрыФискальнойСтрокиЧека();
						СтрокаПозицииЧека.ПризнакСпособаРасчета  	= Перечисления.ПризнакиСпособаРасчета.ОплатаКредита;
						СтрокаПозицииЧека.ПризнакПредметаРасчета 	= Перечисления.ПризнакиПредметаРасчета.Услуга;
						СтрокаПозицииЧека.Наименование 				= ?(ПустаяСтрока(СтрокаТЧОснования.Содержание), Строка(СтрокаТЧОснования.Номенклатура), СокрЛП(СтрокаТЧОснования.Содержание));
						СтрокаПозицииЧека.Количество   				= СтрокаТЧОснования.Количество;
						СтрокаПозицииЧека.Цена 						= СтрокаТЧОснования.Цена + 
							?(СтрокаТЧ.ДокументРасчетовСКонтрагентом.СуммаВключаетНДС, 0, ?(СтрокаТЧОснования.Количество = 0, СтрокаТЧОснования.СуммаНДС, Окр(СтрокаТЧОснования.СуммаНДС / СтрокаТЧОснования.Количество, 2)));
						СтрокаПозицииЧека.Сумма 					= СтрокаТЧОснования.Сумма + ?(СтрокаТЧ.ДокументРасчетовСКонтрагентом.СуммаВключаетНДС, 0, СтрокаТЧОснования.СуммаНДС);
						СтрокаПозицииЧека.НомерСекции 				= НомерСекции;
						СтрокаПозицииЧека.СуммаСкидок 				= 0;
						СтрокаПозицииЧека.СтавкаНДС 				= УчетНДС.ПолучитьСтавкуНДС(СтрокаТЧОснования.СтавкаНДС);
						СтрокаПозицииЧека.НомерСтрокиТовара 		= СтрокаТЧОснования.НомерСтроки;
						СтрокаПозицииЧека.СуммаНДС 					= СтрокаТЧОснования.СуммаНДС;      
						СтрокаПозицииЧека.ЕдиницаИзмерения 			= "Штука";
		
						ОбщиеПараметры.ПозицииЧека.Добавить(СтрокаПозицииЧека);
						
						ИтогПоПозициям = ИтогПоПозициям + СтрокаПозицииЧека.Сумма;
						
						Если Не СтрокиПозицииЧекаЗаполнены Тогда
							СтрокиПозицииЧекаЗаполнены = Истина;
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЕсли;
				
				Если Товары Тогда
					
					Для Каждого СтрокаТЧОснования Из СтрокаТЧ.ДокументРасчетовСКонтрагентом.Товары Цикл
						НомерСекции = 0;
						СтрокаПозицииЧека = МенеджерОборудованияКлиентСервер.ПараметрыФискальнойСтрокиЧека();
						СтрокаПозицииЧека.ПризнакСпособаРасчета		= Перечисления.ПризнакиСпособаРасчета.ОплатаКредита;
						СтрокаПозицииЧека.ПризнакПредметаРасчета	= Перечисления.ПризнакиПредметаРасчета.Товар;
						СтрокаПозицииЧека.Наименование 				= Строка(СтрокаТЧОснования.Номенклатура);
						СтрокаПозицииЧека.Количество   				= СтрокаТЧОснования.Количество;
						СтрокаПозицииЧека.Цена 						= СтрокаТЧОснования.Цена + 
							?(СтрокаТЧ.ДокументРасчетовСКонтрагентом.СуммаВключаетНДС, 0, ?(СтрокаТЧОснования.Количество = 0, СтрокаТЧОснования.СуммаНДС, Окр(СтрокаТЧОснования.СуммаНДС / СтрокаТЧОснования.Количество, 2)));
						СтрокаПозицииЧека.Сумма 					= СтрокаТЧОснования.Сумма + ?(СтрокаТЧ.ДокументРасчетовСКонтрагентом.СуммаВключаетНДС, 0, СтрокаТЧОснования.СуммаНДС);
						СтрокаПозицииЧека.НомерСекции 				= НомерСекции;
						СтрокаПозицииЧека.СуммаСкидок 				= 0;
						СтрокаПозицииЧека.СтавкаНДС 				= УчетНДС.ПолучитьСтавкуНДС(СтрокаТЧОснования.СтавкаНДС);
						СтрокаПозицииЧека.НомерСтрокиТовара 		= СтрокаТЧОснования.НомерСтроки;
						СтрокаПозицииЧека.СуммаНДС 					= СтрокаТЧОснования.СуммаНДС;      
						СтрокаПозицииЧека.ЕдиницаИзмерения 			= СтрокаТЧОснования.ЕдиницаИзмерения.Наименование;
						
						Если ЗначениеЗаполнено(СтрокаТЧОснования.СерияНоменклатуры) Тогда
							СтрокаПозицииЧека.КодСтраныПроисхожденияТовара = ?(ЗначениеЗаполнено(СтрокаТЧОснования.СерияНоменклатуры.СтранаПроисхождения),
							СтрокаТЧОснования.СерияНоменклатуры.СтранаПроисхождения.Код, "");
							СтрокаПозицииЧека.НомерТаможеннойДекларации    = ?(ЗначениеЗаполнено(СтрокаТЧОснования.СерияНоменклатуры.НомерГТД), 
							СтрокаТЧОснования.СерияНоменклатуры.НомерГТД.РегистрационныйНомер, "");
						КонецЕсли;
																			
						
						ОбщиеПараметры.ПозицииЧека.Добавить(СтрокаПозицииЧека);
						
						ИтогПоПозициям = ИтогПоПозициям + СтрокаПозицииЧека.Сумма;
						
						Если Не СтрокиПозицииЧекаЗаполнены Тогда
							СтрокиПозицииЧекаЗаполнены = Истина;
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЕсли;
				
			КонецЕсли; // Конец обработки документа расчета с контрагентом
			
			Если Не СтрокиПозицииЧекаЗаполнены Тогда
				
				Если ЗначениеЗаполнено(СтрокаТЧ.Сделка) Тогда	// Обработка заказа
					
					Для Каждого СтрокаТЧОснования Из СтрокаТЧ.Сделка.Услуги Цикл
						НомерСекции = 0;
						СтрокаПозицииЧека = МенеджерОборудованияКлиентСервер.ПараметрыФискальнойСтрокиЧека();
						СтрокаПозицииЧека.ПризнакСпособаРасчета  	= Перечисления.ПризнакиСпособаРасчета.ОплатаКредита;
						СтрокаПозицииЧека.ПризнакПредметаРасчета 	= Перечисления.ПризнакиПредметаРасчета.Услуга;
						СтрокаПозицииЧека.Наименование 				= ?(ПустаяСтрока(СтрокаТЧОснования.Содержание), Строка(СтрокаТЧОснования.Номенклатура), СокрЛП(СтрокаТЧОснования.Содержание));
						СтрокаПозицииЧека.Количество   				= СтрокаТЧОснования.Количество;
						СтрокаПозицииЧека.Цена 						= СтрокаТЧОснования.Цена + 
							?(СтрокаТЧ.Сделка.СуммаВключаетНДС, 0, ?(СтрокаТЧОснования.Количество = 0, СтрокаТЧОснования.СуммаНДС, Окр(СтрокаТЧОснования.СуммаНДС / СтрокаТЧОснования.Количество, 2)));
						СтрокаПозицииЧека.Сумма 					= СтрокаТЧОснования.Сумма + ?(СтрокаТЧ.Сделка.СуммаВключаетНДС, 0, СтрокаТЧОснования.СуммаНДС);
						СтрокаПозицииЧека.НомерСекции 				= НомерСекции;
						СтрокаПозицииЧека.СуммаСкидок 				= 0;
						СтрокаПозицииЧека.СтавкаНДС 				= УчетНДС.ПолучитьСтавкуНДС(СтрокаТЧОснования.СтавкаНДС);
						СтрокаПозицииЧека.НомерСтрокиТовара 		= СтрокаТЧОснования.НомерСтроки;
						СтрокаПозицииЧека.СуммаНДС 					= СтрокаТЧОснования.СуммаНДС;      
						СтрокаПозицииЧека.ЕдиницаИзмерения 			= "Штука";
		
						ОбщиеПараметры.ПозицииЧека.Добавить(СтрокаПозицииЧека);
					
						ИтогПоПозициям = ИтогПоПозициям + СтрокаПозицииЧека.Сумма;
					КонецЦикла;
					
					Для Каждого СтрокаТЧОснования Из СтрокаТЧ.Сделка.Товары Цикл
						НомерСекции = 0;
						СтрокаПозицииЧека = МенеджерОборудованияКлиентСервер.ПараметрыФискальнойСтрокиЧека();
						СтрокаПозицииЧека.ПризнакСпособаРасчета  	= Перечисления.ПризнакиСпособаРасчета.ОплатаКредита;
						СтрокаПозицииЧека.ПризнакПредметаРасчета	= Перечисления.ПризнакиПредметаРасчета.Товар;
						СтрокаПозицииЧека.Наименование 				= Строка(СтрокаТЧОснования.Номенклатура);
						СтрокаПозицииЧека.Количество   				= СтрокаТЧОснования.Количество;
						СтрокаПозицииЧека.Цена 						= СтрокаТЧОснования.Цена + 
							?(СтрокаТЧ.Сделка.СуммаВключаетНДС, 0, ?(СтрокаТЧОснования.Количество = 0, СтрокаТЧОснования.СуммаНДС, Окр(СтрокаТЧОснования.СуммаНДС / СтрокаТЧОснования.Количество, 2)));
						СтрокаПозицииЧека.Сумма 					= СтрокаТЧОснования.Сумма + ?(СтрокаТЧ.Сделка.СуммаВключаетНДС, 0, СтрокаТЧОснования.СуммаНДС);
						СтрокаПозицииЧека.НомерСекции 				= НомерСекции;
						СтрокаПозицииЧека.СуммаСкидок 				= 0;
						СтрокаПозицииЧека.СтавкаНДС 				= УчетНДС.ПолучитьСтавкуНДС(СтрокаТЧОснования.СтавкаНДС);
						СтрокаПозицииЧека.НомерСтрокиТовара 		= СтрокаТЧОснования.НомерСтроки;
						СтрокаПозицииЧека.СуммаНДС 					= СтрокаТЧОснования.СуммаНДС;      
						СтрокаПозицииЧека.ЕдиницаИзмерения 			= СтрокаТЧОснования.ЕдиницаИзмерения.Наименование;
						
						Если ТипЗнч(СтрокаТЧ.Сделка) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
							Если ЗначениеЗаполнено(СтрокаТЧОснования.СерияНоменклатуры) Тогда
								СтрокаПозицииЧека.КодСтраныПроисхожденияТовара = ?(ЗначениеЗаполнено(СтрокаТЧОснования.СерияНоменклатуры.СтранаПроисхождения),
																					СтрокаТЧОснования.СерияНоменклатуры.СтранаПроисхождения.Код, "");
								СтрокаПозицииЧека.НомерТаможеннойДекларации    = ?(ЗначениеЗаполнено(СтрокаТЧОснования.СерияНоменклатуры.НомерГТД), 
																					СтрокаТЧОснования.СерияНоменклатуры.НомерГТД.РегистрационныйНомер, "");

							КонецЕсли;
						КонецЕсли;
						
						ОбщиеПараметры.ПозицииЧека.Добавить(СтрокаПозицииЧека);
						
						ИтогПоПозициям = ИтогПоПозициям + СтрокаПозицииЧека.Сумма;
					КонецЦикла;
				
				КонецЕсли; // Конец обработки заказа
				
			КонецЕсли; 
			
				Если (ИтогПоПозициям <> СтрокаТЧ.Сумма) И (ИтогПоПозициям <> 0) Тогда
					ОсталосьРаспределить = СтрокаТЧ.Сумма;
					Коэффициент = СтрокаТЧ.Сумма / ИтогПоПозициям;
					
					Для Каждого СтрокаПозиции Из ОбщиеПараметры.ПозицииЧека Цикл
						СтрокаПозиции.СуммаНДС = Окр(СтрокаПозиции.СуммаНДС * Коэффициент, 2);
						СтрокаПозиции.Сумма = Окр(СтрокаПозиции.Сумма * Коэффициент, 2);
						СтрокаПозиции.Цена = Окр(СтрокаПозиции.Цена * Коэффициент, 2);
						ОсталосьРаспределить = ОсталосьРаспределить - СтрокаПозиции.Сумма;
					КонецЦикла;
					
					Если ОсталосьРаспределить <> 0 Тогда
						СтрокаПозиции.Сумма = СтрокаПозиции.Сумма + ОсталосьРаспределить;
					КонецЕсли;
					
				КонецЕсли;
					
			// Подготовка таблицы оплат
				СтрокаОплаты  = МенеджерОборудованияКлиентСервер.ПараметрыСтрокиОплаты();
				СтрокаОплаты.ТипОплаты = Перечисления.ТипыОплатыККТ.ВстречноеПредоставление;
				СтрокаОплаты.Сумма = СтрокаТЧ.Сумма;
				ОбщиеПараметры.ТаблицаОплат.Добавить(СтрокаОплаты);
	
			// Общие реквизиты для всех типов оборудования.
				ОбщиеПараметры.ТипРасчета          = Перечисления.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств;
				ОбщиеПараметры.Кассир              = СокрЛП(?(ЗначениеЗаполнено(Ссылка.Ответственный), Ссылка.Ответственный.ФизЛицо.Наименование, ""));
				ОбщиеПараметры.КассирИНН           = "";
	
				ОбщиеПараметры.Организация			= Ссылка.Организация;
				ОбщиеПараметры.ОрганизацияНазвание 	= Ссылка.Организация.НаименованиеПолное;
				ОбщиеПараметры.ОрганизацияИНН      	= Ссылка.Организация.ИНН;
				ОбщиеПараметры.ОрганизацияКПП      	= Ссылка.Организация.КПП;
	
				ОбщиеПараметры.Получатель    = Строка(Ссылка.КонтрагентДебитор.Наименование);
				ОбщиеПараметры.ПолучательИНН = Ссылка.КонтрагентДебитор.ИНН;
	
			// Параметры необходимые для чека ЕНВД на принтере чеков
				Если ИдентификаторУстройства <> Неопределено Тогда
					ОписаниеПКС = КассовыеСменыВызовСервера.ОписаниеПоследнейКассовойСмены(ИдентификаторУстройства);
					
					Если ОписаниеПКС = Неопределено ИЛИ ОписаниеПКС.Статус = Перечисления.СтатусыКассовойСмены.Закрыта Тогда
						ОбщегоНазначения.СообщитьОбОшибке("Кассовая смена не открыта");
						Возврат Неопределено;
					КонецЕсли;

					ОбщиеПараметры.НомерЧека  = КассовыеСменыВызовСервера.ТекущийНомерЧека(ОписаниеПКС)+1;
					ОбщиеПараметры.НомерСмены = КассовыеСменыВызовСервера.ТекущийНомерСмены(ОписаниеПКС);
					ТО = СерверТО.ПолучитьМодель(ИдентификаторУстройства);
					ОбщиеПараметры.СпособФорматоЛогическогоКонтроля = ТО.СпособФЛК;
					ОбщиеПараметры.ДопустимоеРасхождениеФорматоЛогическогоКонтроля = ТО.ДопустимоеРасхождениеФЛК;
					ОбщиеПараметры.ТорговыйОбъект = ОписаниеПКС.КассаККМ;
				КонецЕсли;
				
				ОбщиеПараметры.ПокупательEmail = СокрЛП(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "АдресЭП"));
				АдресЭПЗадан = Не ПустаяСтрока(ОбщиеПараметры.ПокупательEmail);
				ОбщиеПараметры.Электронно = глЗначениеПеременной("НеПечататьФискальныйЧекПриОтправкеЭлектронного") И АдресЭПЗадан;
				ОбщиеПараметры.Отправляет1СSMS = Ложь;
				ОбщиеПараметры.Отправляет1СEmail = Не глЗначениеПеременной("ОтправлятьЭлектронныеЧекиПоEmailЧерезОФД") И АдресЭПЗадан;
				ОбщиеПараметры.СистемаНалогообложения = МенеджерОборудованияКлиентСервер.СистемаНалогообложения
					(Ссылка.Организация, Ссылка.Дата);
				МенеджерОборудованияКлиентСервер.ПровестиФорматоЛогическийКонтроль(ОбщиеПараметры);
				// Новые параметры для ККТ по ФЗ-54
					//ОбщиеПараметры.ОтправительEmail  = "info@1c.ru";
	
		КонецЕсли; // Конец обработки дебиторской задолженности
		
	Возврат ОбщиеПараметры;
	
КонецФункции

Функция ИдентификаторФискальнойЗаписи(ДокументСсылка,СтрокаДанных) Экспорт
	
	ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.CRC32);
	ХешированиеДанных.Добавить(XMLСтрока(ДокументСсылка));
	ХешированиеДанных.Добавить(XMLСтрока(СтрокаДанных.ДоговорКонтрагента.Владелец));
	ХешированиеДанных.Добавить(XMLСтрока(СтрокаДанных.ДоговорКонтрагента));
	ХешированиеДанных.Добавить(XMLСтрока(СтрокаДанных.Сделка));
	ХешированиеДанных.Добавить(XMLСтрока(СтрокаДанных.ДокументРасчетовСКонтрагентом));
	ХешированиеДанных.Добавить(XMLСтрока("ВстречноеПредоставление"));
	ХешированиеДанных.Добавить(Формат(СтрокаДанных.Сумма, "ЧДЦ=2; ЧРД=.; ЧН=-; ЧГ=; ЧО=1"));
	
	Возврат ОбщегоНазначения.СократитьСтрокуКонтрольнойСуммой(Формат(ХешированиеДанных.ХешСумма, "ЧГ="), 36);
	
КонецФункции

#КонецЕсли
