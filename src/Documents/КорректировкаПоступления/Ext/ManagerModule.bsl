Функция ТекстЗапросаПрослеживаемыеТовары(НомераТаблиц, ПараметрыПроведения, Реквизиты) Экспорт
	
	Если НЕ Реквизиты.КорректироватьБУиНУ
		И НЕ Реквизиты.КорректироватьНДС
		ИЛИ НЕ Реквизиты.ЕстьТовары Тогда
		ПараметрыПроведения.Вставить("ВТ_РНПТКорректировки",           Неопределено);
		ПараметрыПроведения.Вставить("ВТ_РНПТДокументПоступления",     Неопределено);
		ПараметрыПроведения.Вставить("ВТ_СуммыБезНДС",                 Неопределено);
		ПараметрыПроведения.Вставить("ВТ_ПрослеживаемыеТоварыПредварительная", Неопределено);
		ПараметрыПроведения.Вставить("ПрослеживаемыеТоварыУвеличение", Неопределено);
		ПараметрыПроведения.Вставить("ПрослеживаемыеТоварыУменьшение", Неопределено);
		ПараметрыПроведения.Вставить("ПрослеживаемыеОперации",         Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ВТ_РНПТКорректировки",       НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВТ_РНПТДокументПоступления", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВТ_СуммыБезНДС",             НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВТ_ПрослеживаемыеТоварыПредварительная", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПрослеживаемыеТоварыУвеличение", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПрослеживаемыеТоварыУменьшение", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПрослеживаемыеОперации",         НомераТаблиц.Количество());
	
	ТекстЗапроса =  
	"ВЫБРАТЬ
	|	КорректировкаПоступленияСведенияПрослеживаемости.Ссылка КАК Ссылка,
	|	КорректировкаПоступленияТовары.Ссылка.Контрагент КАК Контрагент,
	|	КорректировкаПоступленияТовары.Номенклатура КАК Номенклатура,
	|	КорректировкаПоступленияТовары.СтранаПроисхождения КАК СтранаПроисхождения,
	|	КорректировкаПоступленияСведенияПрослеживаемости.РНПТ КАК РНПТ,
	|	СУММА(КорректировкаПоступленияСведенияПрослеживаемости.Количество) КАК Количество,
	|	СУММА(КорректировкаПоступленияСведенияПрослеживаемости.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости,
	|	КорректировкаПоступленияСведенияПрослеживаемости.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	КорректировкаПоступленияТовары.СуммаНДС КАК СуммаНДС,
	|	КорректировкаПоступленияТовары.СуммаНДСДоИзменения КАК СуммаНДСДоИзменения,
	|	КорректировкаПоступленияТовары.Цена КАК Цена,
	|	КорректировкаПоступленияТовары.ЦенаДоИзменения КАК ЦенаДоИзменения,
	|	КорректировкаПоступленияТовары.Сумма КАК Сумма,
	|	КорректировкаПоступленияТовары.СуммаДоИзменения КАК СуммаДоИзменения,
	|	КорректировкаПоступленияСведенияПрослеживаемости.Сумма КАК СуммаБезНДС,
	|	КорректировкаПоступленияТовары.Ссылка.Дата КАК Дата,
	|	КорректировкаПоступленияТовары.Ссылка.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
	|	КорректировкаПоступленияТовары.Ссылка.НомерВходящегоДокумента КАК НомерВходящегоДокумента
	|ПОМЕСТИТЬ ВТ_РНПТКорректировки
	|ИЗ
	|	Документ.КорректировкаПоступления.Товары КАК КорректировкаПоступленияТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаПоступления.СведенияПрослеживаемости КАК КорректировкаПоступленияСведенияПрослеживаемости
	|		ПО (КорректировкаПоступленияСведенияПрослеживаемости.Ссылка = КорректировкаПоступленияТовары.Ссылка)
	|			И (КорректировкаПоступленияСведенияПрослеживаемости.ИдентификаторСтроки = КорректировкаПоступленияТовары.ИдентификаторСтроки)
	|ГДЕ
	|	КорректировкаПоступленияСведенияПрослеживаемости.Ссылка = &Ссылка
	|	И КорректировкаПоступленияТовары.ПрослеживаемыйТовар
	|	И КорректировкаПоступленияТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаПоступленияСведенияПрослеживаемости.ИдентификаторСтроки,
	|	КорректировкаПоступленияТовары.Номенклатура,
	|	КорректировкаПоступленияСведенияПрослеживаемости.РНПТ,
	|	КорректировкаПоступленияТовары.Ссылка.Контрагент,
	|	КорректировкаПоступленияСведенияПрослеживаемости.Ссылка,
	|	КорректировкаПоступленияТовары.СуммаНДС,
	|	КорректировкаПоступленияТовары.СуммаНДСДоИзменения,
	|	КорректировкаПоступленияТовары.Цена,
	|	КорректировкаПоступленияТовары.ЦенаДоИзменения,
	|	КорректировкаПоступленияТовары.Сумма,
	|	КорректировкаПоступленияТовары.СуммаДоИзменения,
	|	КорректировкаПоступленияТовары.СтранаПроисхождения,
	|	КорректировкаПоступленияСведенияПрослеживаемости.Сумма,
	|	КорректировкаПоступленияТовары.Ссылка.Дата,
	|	КорректировкаПоступленияТовары.Ссылка.ДатаВходящегоДокумента,
	|	КорректировкаПоступленияТовары.Ссылка.НомерВходящегоДокумента
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ИдентификаторСтроки,
	|	РНПТ,
	|	Номенклатура,
	|	СтранаПроисхождения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.РНПТ КАК РНПТ,
	|	СУММА(ПоступлениеТоваровУслугСведенияПрослеживаемости.Количество) КАК Количество,
	|	СУММА(ПоступлениеТоваровУслугСведенияПрослеживаемости.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости,
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ПоступлениеТоваровУслугТовары.Номенклатура КАК Номенклатура,
	|	ПоступлениеТоваровУслугТовары.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ПоступлениеТоваровУслугТовары.Ссылка.Контрагент КАК Контрагент,
	|	ПоступлениеТоваровУслугТовары.СуммаНДС КАК СуммаНДС,
	|	ПоступлениеТоваровУслугТовары.Цена КАК Цена,
	|	ПоступлениеТоваровУслугТовары.Сумма КАК Сумма,
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.Сумма КАК СуммаБезНДС
	|ПОМЕСТИТЬ ВТ_РНПТДокументПоступления
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.СведенияПрослеживаемости КАК ПоступлениеТоваровУслугСведенияПрослеживаемости
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
	|		ПО ПоступлениеТоваровУслугСведенияПрослеживаемости.Ссылка = ПоступлениеТоваровУслугТовары.Ссылка
	|			И ПоступлениеТоваровУслугСведенияПрослеживаемости.ИдентификаторСтроки = ПоступлениеТоваровУслугТовары.ИдентификаторСтроки
	|ГДЕ
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.Ссылка = &ДокументПоступления
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.ИдентификаторСтроки,
	|	ПоступлениеТоваровУслугТовары.СтранаПроисхождения,
	|	ПоступлениеТоваровУслугТовары.Номенклатура,
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.РНПТ,
	|	ПоступлениеТоваровУслугТовары.Ссылка.Контрагент,
	|	ПоступлениеТоваровУслугТовары.СуммаНДС,
	|	ПоступлениеТоваровУслугТовары.Цена,
	|	ПоступлениеТоваровУслугТовары.Сумма,
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.Сумма
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ИдентификаторСтроки,
	|	РНПТ,
	|	Номенклатура,
	|	СтранаПроисхождения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КорректировкаПоступленияТовары.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА КорректировкаПоступленияТовары.Ссылка.СуммаВключаетНДС
	|			ТОГДА СУММА(КорректировкаПоступленияТовары.Сумма - КорректировкаПоступленияТовары.СуммаНДС)
	|		ИНАЧЕ СУММА(КорректировкаПоступленияТовары.Сумма)
	|	КОНЕЦ КАК СуммаБезНДС,
	|	ВЫБОР
	|		КОГДА КорректировкаПоступленияТовары.Ссылка.СуммаВключаетНДС
	|			ТОГДА СУММА(КорректировкаПоступленияТовары.СуммаДоИзменения - КорректировкаПоступленияТовары.СуммаНДСДоИзменения)
	|		ИНАЧЕ СУММА(КорректировкаПоступленияТовары.СуммаДоИзменения)
	|	КОНЕЦ КАК СуммаБезНДСДоИзменения,
	|	ВЫБОР
	|		КОГДА КорректировкаПоступленияТовары.Ссылка.СуммаВключаетНДС
	|			ТОГДА СУММА(КорректировкаПоступленияТовары.СуммаДоКорректировки - КорректировкаПоступленияТовары.СуммаНДСДоКорректировки)
	|		ИНАЧЕ СУММА(КорректировкаПоступленияТовары.СуммаДоКорректировки)
	|	КОНЕЦ КАК СуммаБезНДСДоКорректироки,
	|	КорректировкаПоступленияТовары.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВТ_СуммыБезНДС
	|ИЗ
	|	Документ.КорректировкаПоступления.Товары КАК КорректировкаПоступленияТовары
	|ГДЕ
	|	КорректировкаПоступленияТовары.Ссылка = &Ссылка
	|	И КорректировкаПоступленияТовары.ПрослеживаемыйТовар
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаПоступленияТовары.Номенклатура,
	|	КорректировкаПоступленияТовары.ИдентификаторСтроки,
	|	КорректировкаПоступленияТовары.Ссылка.СуммаВключаетНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ВТ_РНПТКорректировки.РНПТ, ВТ_РНПТДокументПоступления.РНПТ) КАК РНПТ,
	|	СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(ВТ_РНПТКорректировки.Количество, 0) - ЕСТЬNULL(ВТ_РНПТДокументПоступления.Количество, 0) > 0
	|				ТОГДА ЕСТЬNULL(ВТ_РНПТКорректировки.Количество, 0) - ЕСТЬNULL(ВТ_РНПТДокументПоступления.Количество, 0)
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоУвеличение,
	|	СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(ВТ_РНПТДокументПоступления.Количество, 0) - ЕСТЬNULL(ВТ_РНПТКорректировки.Количество, 0) > 0
	|				ТОГДА ЕСТЬNULL(ВТ_РНПТДокументПоступления.Количество, 0) - ЕСТЬNULL(ВТ_РНПТКорректировки.Количество, 0)
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоУменьшение,
	|	СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(ВТ_РНПТКорректировки.КоличествоПрослеживаемости, 0) - ЕСТЬNULL(ВТ_РНПТДокументПоступления.КоличествоПрослеживаемости, 0) > 0
	|				ТОГДА ЕСТЬNULL(ВТ_РНПТКорректировки.КоличествоПрослеживаемости, 0) - ЕСТЬNULL(ВТ_РНПТДокументПоступления.КоличествоПрослеживаемости, 0)
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоПрослеживаемостиУвеличение,
	|	СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(ВТ_РНПТДокументПоступления.КоличествоПрослеживаемости, 0) - ЕСТЬNULL(ВТ_РНПТКорректировки.КоличествоПрослеживаемости, 0) > 0
	|				ТОГДА ЕСТЬNULL(ВТ_РНПТДокументПоступления.КоличествоПрослеживаемости, 0) - ЕСТЬNULL(ВТ_РНПТКорректировки.КоличествоПрослеживаемости, 0)
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоПрослеживаемостиУменьшение,
	|	ЕСТЬNULL(ВТ_РНПТКорректировки.Номенклатура, ВТ_РНПТДокументПоступления.Номенклатура) КАК Номенклатура,
	|	ЕСТЬNULL(ВТ_РНПТКорректировки.СтранаПроисхождения, ВТ_РНПТДокументПоступления.СтранаПроисхождения) КАК СтранаПроисхождения
	|ПОМЕСТИТЬ ВТ_ПрослеживаемыеТоварыПредварительная
	|ИЗ
	|	ВТ_РНПТКорректировки КАК ВТ_РНПТКорректировки
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВТ_РНПТДокументПоступления КАК ВТ_РНПТДокументПоступления
	|		ПО ВТ_РНПТКорректировки.ИдентификаторСтроки = ВТ_РНПТДокументПоступления.ИдентификаторСтроки
	|			И ВТ_РНПТКорректировки.РНПТ = ВТ_РНПТДокументПоступления.РНПТ
	|			И ВТ_РНПТКорректировки.Номенклатура = ВТ_РНПТДокументПоступления.Номенклатура
	|			И ВТ_РНПТКорректировки.СтранаПроисхождения = ВТ_РНПТДокументПоступления.СтранаПроисхождения
	|
	|СГРУППИРОВАТЬ ПО
	|	ЕСТЬNULL(ВТ_РНПТКорректировки.РНПТ, ВТ_РНПТДокументПоступления.РНПТ),
	|	ЕСТЬNULL(ВТ_РНПТКорректировки.Номенклатура, ВТ_РНПТДокументПоступления.Номенклатура),
	|	ЕСТЬNULL(ВТ_РНПТКорректировки.СтранаПроисхождения, ВТ_РНПТДокументПоступления.СтранаПроисхождения)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПрослеживаемыеТоварыПредварительная.РНПТ КАК РНПТ,
	|	ВТ_ПрослеживаемыеТоварыПредварительная.КоличествоУвеличение КАК Количество,
	|	ВТ_ПрослеживаемыеТоварыПредварительная.КоличествоПрослеживаемостиУвеличение КАК КоличествоПрослеживаемости,
	|	ВТ_ПрослеживаемыеТоварыПредварительная.Номенклатура КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Комитент,
	|	НЕОПРЕДЕЛЕНО КАК ВидЗапасов,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Комиссионер,
	|	ВТ_ПрослеживаемыеТоварыПредварительная.СтранаПроисхождения КАК СтранаПроисхождения,
	|	&ПервичныйДокументПоступления КАК ОснованиеДляВозврата,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаРеализации
	|ИЗ
	|	ВТ_ПрослеживаемыеТоварыПредварительная КАК ВТ_ПрослеживаемыеТоварыПредварительная
	|ГДЕ
	|	(ВТ_ПрослеживаемыеТоварыПредварительная.КоличествоПрослеживаемостиУвеличение > 0
	|			ИЛИ ВТ_ПрослеживаемыеТоварыПредварительная.КоличествоУвеличение > 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПрослеживаемыеТоварыПредварительная.РНПТ КАК РНПТ,
	|	ВТ_ПрослеживаемыеТоварыПредварительная.КоличествоУменьшение КАК Количество,
	|	ВТ_ПрослеживаемыеТоварыПредварительная.КоличествоПрослеживаемостиУменьшение КАК КоличествоПрослеживаемости,
	|	ВТ_ПрослеживаемыеТоварыПредварительная.Номенклатура КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Комитент,
	|	НЕОПРЕДЕЛЕНО КАК ВидЗапасов,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Комиссионер,
	|	ВТ_ПрослеживаемыеТоварыПредварительная.СтранаПроисхождения КАК СтранаПроисхождения,
	|	&ПервичныйДокументПоступления КАК ОснованиеДляВозврата,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаРеализации
	|ИЗ
	|	ВТ_ПрослеживаемыеТоварыПредварительная КАК ВТ_ПрослеживаемыеТоварыПредварительная
	|ГДЕ
	|	(ВТ_ПрослеживаемыеТоварыПредварительная.КоличествоУменьшение > 0
	|			ИЛИ ВТ_ПрослеживаемыеТоварыПредварительная.КоличествоПрослеживаемостиУменьшение > 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ВТ_РНПТКорректировки.Номенклатура, ВТ_РНПТДокументПоступления.Номенклатура) КАК Номенклатура,
	|	ЕСТЬNULL(ВТ_РНПТКорректировки.Количество, 0) КАК Количество,
	|	ЕСТЬNULL(ВТ_РНПТКорректировки.РНПТ, ВТ_РНПТДокументПоступления.РНПТ) КАК РНПТ,
	|	ЕСТЬNULL(ВТ_РНПТКорректировки.КоличествоПрослеживаемости, 0) КАК КоличествоПрослеживаемости,
	|	ЕСТЬNULL(ВТ_РНПТКорректировки.Контрагент, ВТ_РНПТДокументПоступления.Контрагент) КАК Контрагент,
	|	ЕСТЬNULL(ВТ_СуммыБезНДС.СуммаБезНДС, 0) КАК СуммаБезНДС,
	|	ВЫБОР
	|		КОГДА НЕ &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ОтчетОбОперациях)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.КнигаПокупок)
	|	КОНЕЦ КАК ОтражениеВОтчетности,
	|	ВЫБОР
	|		КОГДА &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ПустаяСсылка)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.Приобретение)
	|	КОНЕЦ КАК КодОперации,
	|	ВЫБОР
	|		КОГДА &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|			ТОГДА ВТ_РНПТКорректировки.Ссылка
	|		ИНАЧЕ &ДокументПоступленияСсылка
	|	КОНЕЦ КАК ДокументОперации,
	|	ВЫБОР
	|		КОГДА &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|			ТОГДА НАЧАЛОПЕРИОДА(ВТ_РНПТКорректировки.Ссылка.Дата, КВАРТАЛ)
	|		ИНАЧЕ &ПериодИсходнойОперации
	|	КОНЕЦ КАК ПериодОперации,
	|	ВЫБОР
	|		КОГДА &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|				ИЛИ ВТ_РНПТКорректировки.Номенклатура ЕСТЬ NULL
	|			ТОГДА ДАТАВРЕМЯ(1, 1, 1)
	|		ИНАЧЕ &ДатаИсходногоДокумента
	|	КОНЕЦ КАК ДатаДокумента,
	|	ВЫБОР
	|		КОГДА &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|				ИЛИ ВТ_РНПТКорректировки.Номенклатура ЕСТЬ NULL
	|			ТОГДА """"
	|		ИНАЧЕ &НомерИсходногоДокумента
	|	КОНЕЦ КАК НомерДокумента,
	|	ВЫБОР
	|		КОГДА &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|				ИЛИ ВТ_РНПТКорректировки.Номенклатура ЕСТЬ NULL
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ТипыДокументов.УПД)
	|	КОНЕЦ КАК ТипДокументаВПрослеживаемости,
	|	0 КАК КоличествоДляРаспределения,
	|	ВТ_РНПТДокументПоступления.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ИЗ
	|	ВТ_РНПТКорректировки КАК ВТ_РНПТКорректировки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СуммыБезНДС КАК ВТ_СуммыБезНДС
	|		ПО ВТ_РНПТКорректировки.ИдентификаторСтроки = ВТ_СуммыБезНДС.ИдентификаторСтроки
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВТ_РНПТДокументПоступления КАК ВТ_РНПТДокументПоступления
	|		ПО ВТ_РНПТКорректировки.ИдентификаторСтроки = ВТ_РНПТДокументПоступления.ИдентификаторСтроки
	|			И ВТ_РНПТКорректировки.РНПТ = ВТ_РНПТДокументПоступления.РНПТ
	|			И ВТ_РНПТКорректировки.Номенклатура = ВТ_РНПТДокументПоступления.Номенклатура
	|			И ВТ_РНПТКорректировки.СтранаПроисхождения = ВТ_РНПТДокументПоступления.СтранаПроисхождения
	|ГДЕ
	|	ЕСТЬNULL(ВТ_РНПТКорректировки.Ссылка.ВидОперации, ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение)) <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение)
	|	И НЕ &ЭтоИсправлениеКорректировки
	|	И ВЫБОР
	|			КОГДА &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|				ТОГДА НЕ ВТ_РНПТКорректировки.Номенклатура ЕСТЬ NULL
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Количество,
	|	ВложенныйЗапрос.РНПТ,
	|	ВложенныйЗапрос.КоличествоПрослеживаемости,
	|	ВложенныйЗапрос.Контрагент,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТ_СуммыБезНДС.СуммаБезНДС, 0) - ЕСТЬNULL(ВТ_СуммыБезНДС.СуммаБезНДСДоИзменения, 0) > 0
	|			ТОГДА ЕСТЬNULL(ВТ_СуммыБезНДС.СуммаБезНДС, 0) - ЕСТЬNULL(ВТ_СуммыБезНДС.СуммаБезНДСДоИзменения, 0)
	|			ИНАЧЕ ЕСТЬNULL(ВТ_СуммыБезНДС.СуммаБезНДСДоИзменения, 0) - ЕСТЬNULL(ВТ_СуммыБезНДС.СуммаБезНДС, 0)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ОтчетОбОперациях)
	|		КОГДА ВложенныйЗапрос.СуммаНДСДоИзменения < ВложенныйЗапрос.СуммаНДС
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.КнигаПокупок)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.КнигаПродаж)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ПустаяСсылка)
	|		КОГДА ВложенныйЗапрос.СуммаДоИзменения < ВложенныйЗапрос.Сумма
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.УКДНаУвеличениеПолученный)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.УКДНаУменьшениеПолученный)
	|	КОНЕЦ,
	|	ВложенныйЗапрос.Ссылка,
	|	НАЧАЛОПЕРИОДА(ВложенныйЗапрос.Дата, КВАРТАЛ),
	|	ВЫБОР
	|		КОГДА НЕ &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|			ТОГДА ВложенныйЗапрос.ДатаВходящегоДокумента
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|			ТОГДА ВложенныйЗапрос.НомерВходящегоДокумента
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ТипыДокументов.УКД)
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВложенныйЗапрос.КоличествоДляРаспределения,
	|	ВложенныйЗапрос.ИдентификаторСтроки
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЕСТЬNULL(ВТ_РНПТКорректировки.Номенклатура, ВТ_РНПТДокументПоступления.Номенклатура) КАК Номенклатура,
	|		ВЫБОР
	|			КОГДА ЕСТЬNULL(ВТ_РНПТДокументПоступления.Количество, 0) - ЕСТЬNULL(ВТ_РНПТКорректировки.Количество, 0) > 0
	|				ТОГДА ЕСТЬNULL(ВТ_РНПТДокументПоступления.Количество, 0) - ЕСТЬNULL(ВТ_РНПТКорректировки.Количество, 0)
	|			ИНАЧЕ ЕСТЬNULL(ВТ_РНПТКорректировки.Количество, 0) - ЕСТЬNULL(ВТ_РНПТДокументПоступления.Количество, 0)
	|		КОНЕЦ КАК Количество,
	|		ЕСТЬNULL(ВТ_РНПТКорректировки.РНПТ, ВТ_РНПТДокументПоступления.РНПТ) КАК РНПТ,
	|		ВЫБОР
	|			КОГДА ЕСТЬNULL(ВТ_РНПТДокументПоступления.КоличествоПрослеживаемости, 0) - ЕСТЬNULL(ВТ_РНПТКорректировки.КоличествоПрослеживаемости, 0) > 0
	|				ТОГДА ЕСТЬNULL(ВТ_РНПТДокументПоступления.КоличествоПрослеживаемости, 0) - ЕСТЬNULL(ВТ_РНПТКорректировки.КоличествоПрослеживаемости, 0)
	|			ИНАЧЕ ЕСТЬNULL(ВТ_РНПТКорректировки.КоличествоПрослеживаемости, 0) - ЕСТЬNULL(ВТ_РНПТДокументПоступления.КоличествоПрослеживаемости, 0)
	|		КОНЕЦ КАК КоличествоПрослеживаемости,
	|		ЕСТЬNULL(ВТ_РНПТКорректировки.Контрагент, ВТ_РНПТДокументПоступления.Контрагент) КАК Контрагент,
	|		&Ссылка КАК Ссылка,
	|		ЕСТЬNULL(ВТ_РНПТКорректировки.СуммаНДС, ВТ_РНПТДокументПоступления.СуммаНДС) КАК СуммаНДС,
	|		ЕСТЬNULL(ВТ_РНПТКорректировки.СуммаНДСДоИзменения, ВТ_РНПТДокументПоступления.СуммаНДС) КАК СуммаНДСДоИзменения,
	|		ЕСТЬNULL(ВТ_РНПТКорректировки.Цена, ВТ_РНПТДокументПоступления.Цена) КАК Цена,
	|		ЕСТЬNULL(ВТ_РНПТДокументПоступления.Цена, ВТ_РНПТКорректировки.ЦенаДоИзменения) КАК ЦенаДоИзменения,
	|		ЕСТЬNULL(ВТ_РНПТКорректировки.Сумма, ВТ_РНПТДокументПоступления.Сумма) КАК Сумма,
	|		ЕСТЬNULL(ВТ_РНПТДокументПоступления.Сумма, ВТ_РНПТКорректировки.СуммаДоИзменения) КАК СуммаДоИзменения,
	|		ЕСТЬNULL(ВТ_РНПТКорректировки.Количество, 0) КАК КоличествоДляРаспределения,
	|		ЕСТЬNULL(ВТ_РНПТКорректировки.Ссылка.ВидОперации, ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИсправленияПоступленияРеализации.ПустаяСсылка)) КАК ВидОперации,
	|		ЕСТЬNULL(ВТ_РНПТКорректировки.Дата, ДАТАВРЕМЯ(1, 1, 1)) КАК Дата,
	|		ЕСТЬNULL(ВТ_РНПТКорректировки.ДатаВходящегоДокумента, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаВходящегоДокумента,
	|		ЕСТЬNULL(ВТ_РНПТКорректировки.НомерВходящегоДокумента, """") КАК НомерВходящегоДокумента,
	|		ЕСТЬNULL(ВТ_РНПТДокументПоступления.ИдентификаторСтроки, ВТ_РНПТКорректировки.ИдентификаторСтроки) КАК ИдентификаторСтроки,
	|		ЕСТЬNULL(ВТ_РНПТКорректировки.СуммаБезНДС, 0) - ЕСТЬNULL(ВТ_РНПТДокументПоступления.СуммаБезНДС, 0) КАК СуммаБезНДС
	|	ИЗ
	|		ВТ_РНПТКорректировки КАК ВТ_РНПТКорректировки
	|			ПОЛНОЕ СОЕДИНЕНИЕ ВТ_РНПТДокументПоступления КАК ВТ_РНПТДокументПоступления
	|			ПО ВТ_РНПТКорректировки.ИдентификаторСтроки = ВТ_РНПТДокументПоступления.ИдентификаторСтроки
	|				И ВТ_РНПТКорректировки.РНПТ = ВТ_РНПТДокументПоступления.РНПТ
	|				И ВТ_РНПТКорректировки.Номенклатура = ВТ_РНПТДокументПоступления.Номенклатура
	|				И ВТ_РНПТКорректировки.СтранаПроисхождения = ВТ_РНПТДокументПоступления.СтранаПроисхождения
	|	ГДЕ
	|		ВТ_РНПТКорректировки.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение)) КАК ВложенныйЗапрос
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СуммыБезНДС КАК ВТ_СуммыБезНДС
	|		ПО ВложенныйЗапрос.ИдентификаторСтроки = ВТ_СуммыБезНДС.ИдентификаторСтроки
	|ГДЕ
	|	ВложенныйЗапрос.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение)
	|	И ВЫБОР
	|			КОГДА &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|				ТОГДА ВложенныйЗапрос.СуммаНДС <> ВложенныйЗапрос.СуммаНДСДоИзменения
	|			ИНАЧЕ ВложенныйЗапрос.Сумма <> ВложенныйЗапрос.СуммаДоИзменения
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА ВложенныйЗапрос.Цена = ВложенныйЗапрос.ЦенаДоИзменения
	|				ТОГДА ВложенныйЗапрос.Количество > 0
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ";
	
	Возврат ТекстЗапроса + ПрослеживаемостьБП.ТекстРазделителяЗапросовПакета();
	
КонецФункции