#Область ПрограммныйИнтерфейс

#Область Серии

Функция ИменаРеквизитовДляЗаполненияПараметровУказанияСерий() Экспорт
	Возврат "";
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Печать

// Типизируем колонки таблицы МаркированныеТовары для передачи в запрос
// и добавляем колонку НомерСтроки
Процедура ТипизироватьКолонкиМаркированныхТоваров(ТаблицаМаркированныхТоваров)
	
	ТаблицаМаркированныхТоваров.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"));
	ТаблицаМаркированныхТоваров.Колонки.Добавить("НоменклатураСсылка", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаМаркированныхТоваров.Колонки.Добавить("ХарактеристикаСсылка", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ТаблицаМаркированныхТоваров.Колонки.Добавить("СерияСсылка", Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	ТаблицаМаркированныхТоваров.Колонки.Добавить("ШтрихкодСтрока", Новый ОписаниеТипов("Строка"));
	
	Для Каждого СтрокаШтрихкодыУпаковок Из ТаблицаМаркированныхТоваров Цикл
		СтрокаШтрихкодыУпаковок.НомерСтроки = ТаблицаМаркированныхТоваров.Индекс(СтрокаШтрихкодыУпаковок);
		СтрокаШтрихкодыУпаковок.НоменклатураСсылка = СтрокаШтрихкодыУпаковок.Номенклатура;
		СтрокаШтрихкодыУпаковок.ХарактеристикаСсылка = СтрокаШтрихкодыУпаковок.Характеристика;
		СтрокаШтрихкодыУпаковок.СерияСсылка = СтрокаШтрихкодыУпаковок.Серия;
		СтрокаШтрихкодыУпаковок.ШтрихкодСтрока = СтрокаШтрихкодыУпаковок.ШтрихкодУпаковки;
	КонецЦикла;	
	
	ТаблицаМаркированныхТоваров.Колонки.Удалить("Номенклатура");
	ТаблицаМаркированныхТоваров.Колонки.Удалить("Характеристика");
	ТаблицаМаркированныхТоваров.Колонки.Удалить("ШтрихкодУпаковки");
	
КонецПроцедуры
	
Функция ДанныеДляМОТП(ДокументСсылка, МенеджерВременныхТаблиц)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ШтрихкодыУпаковок.ШтрихкодУпаковки КАК ШтрихкодУпаковки
	               |ИЗ
	               |	Документ.КорректировкаРеализации.ШтрихкодыУпаковок КАК ШтрихкодыУпаковок
	               |ГДЕ
	               |	ШтрихкодыУпаковок.Ссылка = &Ссылка
	               |";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	РезультатЗапроса = Запрос.Выполнить();
	МассивУпаковок = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("ШтрихкодУпаковки");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МаркированныеТовары.НомерСтроки КАК НомерСтроки,
	|	МаркированныеТовары.НоменклатураСсылка КАК Номенклатура,
	|	МаркированныеТовары.ХарактеристикаСсылка КАК Характеристика,
	|	МаркированныеТовары.СерияСсылка КАК Серия,
	|	МаркированныеТовары.ШтрихкодСтрока КАК Штрихкод
	|ПОМЕСТИТЬ ТаблицаМаркированныеТовары
	|ИЗ
	|	&МаркированныеТовары КАК МаркированныеТовары
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Серия
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Упаковка КАК Упаковка,
	|	Товары.СуммаСкидки КАК СуммаСкидки,
	|	Товары.СуммаСНДС КАК СуммаСНДС,
	|	Товары.Количество,
	|	Товары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ЕСТЬNULL(МаркированныеТовары.Штрихкод, """") КАК Штрихкод,
	|	1 КАК МаркТоварыКоличество,
	|	ВЫБОР КОГДА НЕ МаркированныеТовары.Штрихкод ЕСТь NULL И Товары.Количество <> 1 ТОГДА
	|		ВЫРАЗИТЬ(1 / 1 КАК ЧИСЛО(12,3))
	|	ИНАЧЕ
	|		Товары.КоличествоУпаковок
	|	КОНЕЦ КАК МаркТоварыКоличествоУпаковок,
	|	ВЫБОР
	|		КОГДА НЕ МаркированныеТовары.Штрихкод ЕСТЬ NULL
	|				И Товары.Количество <> 1
	|			ТОГДА ВЫРАЗИТЬ(Товары.НомерСтроки * &КоличествоМаркированныхТоваров + МаркированныеТовары.НомерСтроки КАК ЧИСЛО(10, 0))
	|		ИНАЧЕ ВЫРАЗИТЬ(Товары.НомерСтроки * ВЫБОР
	|					КОГДА &КоличествоМаркированныхТоваров = 0
	|						ТОГДА 1
	|					ИНАЧЕ &КоличествоМаркированныхТоваров
	|				КОНЕЦ КАК ЧИСЛО(10, 0))
	|	КОНЕЦ КАК ИндексМаркированногоТовара,
	|	ВЫБОР
	|		КОГДА НЕ МаркированныеТовары.Штрихкод ЕСТЬ NULL
	|				И Товары.КоличествоУпаковок <> 1
	|			ТОГДА ВЫРАЗИТЬ(Товары.СуммаСкидки / Товары.Количество КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ Товары.СуммаСкидки
	|	КОНЕЦ КАК МаркТоварыСуммаСкидки,
	|	ВЫБОР
	|		КОГДА НЕ МаркированныеТовары.Штрихкод ЕСТЬ NULL
	|				И Товары.КоличествоУпаковок <> 1
	|			ТОГДА ВЫРАЗИТЬ(Товары.СуммаСНДС / Товары.Количество КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ Товары.СуммаСНДС
	|	КОНЕЦ КАК МаркТоварыСуммаСНДС
	|ПОМЕСТИТЬ ТоварыИСерииПоМаркированнымТоварам
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаМаркированныеТовары КАК МаркированныеТовары
	|		ПО (МаркированныеТовары.Номенклатура = Товары.Номенклатура)
	|			И (МаркированныеТовары.Характеристика = Товары.Характеристика)
	|			И (МаркированныеТовары.Серия = Товары.Серия)
	|ГДЕ Товары.Номенклатура.ВидПродукцииИС <> ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПустаяСсылка)
	|	И Товары.Номенклатура.ВидПродукцииИС <> ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Алкогольная)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаМаркированныеТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ТоварыИСерииПоМаркированнымТоварам.ИндексМаркированногоТовара) КАК ИндексМаркированногоТовара,
	|	СУММА(ТоварыИСерииПоМаркированнымТоварам.МаркТоварыСуммаСкидки) КАК СуммаСкидки,
	|	СУММА(ТоварыИСерииПоМаркированнымТоварам.МаркТоварыСуммаСНДС) КАК СуммаСНДС
	|ПОМЕСТИТЬ СуммовыеРазницыМаркированныхТоваров
	|ИЗ
	|	ТоварыИСерииПоМаркированнымТоварам КАК ТоварыИСерииПоМаркированнымТоварам
	|ГДЕ
	|	&КоличествоМаркированныхТоваров > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыИСерииПоМаркированнымТоварам.НомерСтроки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ИндексМаркированногоТовара
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Штрихкод КАК Штрихкод,
	|	Товары.МаркТоварыКоличество КАК Количество,
	|	Товары.МаркТоварыКоличествоУпаковок КАК КоличествоУпаковок,
	|	ВЫБОР
	|		КОГДА СуммовыеРазницыМаркированныхТоваров.СуммаСкидки ЕСТЬ NULL
	|			ТОГДА Товары.МаркТоварыСуммаСкидки
	|		ИНАЧЕ ВЫРАЗИТЬ(Товары.МаркТоварыСуммаСкидки + Товары.СуммаСкидки - СуммовыеРазницыМаркированныхТоваров.СуммаСкидки КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК СуммаСкидки,
	|	ВЫБОР
	|		КОГДА СуммовыеРазницыМаркированныхТоваров.СуммаСНДС ЕСТЬ NULL
	|			ТОГДА Товары.МаркТоварыСуммаСНДС
	|		ИНАЧЕ ВЫРАЗИТЬ(Товары.МаркТоварыСуммаСНДС + Товары.СуммаСНДС - СуммовыеРазницыМаркированныхТоваров.СуммаСНДС КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК СуммаСНДС
	|ИЗ
	|	ТоварыИСерииПоМаркированнымТоварам КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ СуммовыеРазницыМаркированныхТоваров КАК СуммовыеРазницыМаркированныхТоваров
	|		ПО (СуммовыеРазницыМаркированныхТоваров.ИндексМаркированногоТовара = Товары.ИндексМаркированногоТовара)";
	
	ПараметрыСканирования = ШтрихкодированиеИС.ПараметрыСканирования(ДокументСсылка);
	ШтрихкодыУпаковок = ШтрихкодированиеИС.ВложенныеШтрихкодыУпаковок(МассивУпаковок, ПараметрыСканирования);
	// нужно типизировать колонки и добавить НомерСтроки
	ТипизироватьКолонкиМаркированныхТоваров(ШтрихкодыУпаковок.МаркированныеТовары);
	
	Запрос.УстановитьПараметр("МаркированныеТовары", ШтрихкодыУпаковок.МаркированныеТовары);
	Запрос.УстановитьПараметр("КоличествоМаркированныхТоваров", ШтрихкодыУпаковок.МаркированныеТовары.Количество());
	
	Результат = Запрос.Выполнить().Выгрузить();
	Результат.Индексы.Добавить("НомерСтроки");
	Возврат Результат;
	
КонецФункции

// Возвращает предварительные параметры фискализации чека для документа.
//
// Параметры:
//   ДокументСсылка - ДокументСсылка.РеализацияТоваровУслуг - Документ.
//   СуммаПредоплатыКорректировка - Число - Откорректированная сумма предоплаты.
//   ВерсияФФД - Строка - Версия ФФД.
//
// ВозвращаемоеЗначение:
//  Неопределено, Структура - см. функцию МенеджерОборудованияКлиентСервер.ПараметрыОперацииФискализацииЧека().
//
Функция ПараметрыОперацииФискализацииЧека(ДокументСсылка, СуммаПредоплатыКорректировка = Неопределено, ВерсияФФД = Неопределено) Экспорт
	
	Возврат ТорговоеОборудованиеУТВызовСервера.ПараметрыОперацииФискализацииЧекаКорректировкаРеализации(ДокументСсылка, СуммаПредоплатыКорректировка, ВерсияФФД);
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВалютаРегл = Константы.ВалютаРегламентированногоУчета.Получить();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК ДокументСсылка,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА Контрагенты.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
	|			ТОГДА ДанныеДокумента.Контрагент
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|	КОНЕЦ КАК Контрагент,
	|	ДанныеДокумента.Склад КАК ТорговыйОбъект,
	|	ЕСТЬNULL(ДанныеДокумента.Ответственный.ФизЛицо.Наименование, """") КАК Кассир,
	|	"""" КАК КассирИНН,
	|	ЛОЖЬ КАК ПоЗаказам,
	|	ДанныеДокумента.СуммаВключаетНДС КАК ЦенаВключаетНДС,
	|	ДанныеДокумента.УчитыватьНДС КАК НалогообложениеНДС,
	|	ДанныеДокумента.ВалютаДокумента КАК Валюта,
	|	0 КАК СуммаПредоплаты,
	|	ДанныеДокумента.СуммаДокумента КАК СуммаДокумента
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО ДанныеДокумента.Контрагент = Контрагенты.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.Ссылка,
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.СуммаВключаетНДС,
	|	ДанныеДокумента.УчитыватьНДС,
	|	ДанныеДокумента.ВалютаДокумента,
	|	ДанныеДокумента.СуммаДокумента,
	|	ЕСТЬNULL(ДанныеДокумента.Ответственный.ФизЛицо.Наименование, """"),
	|	"""",
	|	ВЫБОР
	|		КОГДА Контрагенты.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
	|			ТОГДА ДанныеДокумента.Контрагент
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|	КОНЕЦ");
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Шапка = Запрос.Выполнить().Выбрать();
	Шапка.Следующий();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТабличнаяЧасть.НомерСтроки КАК НомерСтроки,
	|	ТабличнаяЧасть.КлючСтроки = 0 КАК СверхЗаказа,
	|	ТабличнаяЧасть.ЗаказПокупателя КАК Заказ,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.ХарактеристикаНоменклатуры КАК Характеристика,
	|	ТабличнаяЧасть.СерияНоменклатуры КАК Серия,
	|	ТабличнаяЧасть.ЕдиницаИзмерения КАК Упаковка,
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ВидНоменклатуры.ТипНоменклатуры, ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПустаяСсылка)) КАК ТипНоменклатуры,
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ТабачнаяПродукция
	|			ИЛИ ТабличнаяЧасть.Номенклатура.АлкогольнаяПродукция
	|			ИЛИ ТабличнаяЧасть.Номенклатура.ПодакцизныйТовар, ЛОЖЬ) КАК ПодакцизныйТовар,
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.НаименованиеПолное, """") КАК НоменклатураНаименование,
	|	ЕСТЬNULL(ТабличнаяЧасть.ХарактеристикаНоменклатуры.Наименование, """") КАК ХарактеристикаНаименование,
	|	ТабличнаяЧасть.ЕдиницаИзмерения КАК УпаковкаНаименование,
	|	ТабличнаяЧасть.Количество КАК Количество,
	|	ТабличнаяЧасть.Количество КАК КоличествоУпаковок,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА &ЦенаВключаетНДС
	|				ТОГДА ТабличнаяЧасть.Цена
	|			КОГДА ТабличнаяЧасть.Количество = 0
	|				ТОГДА ТабличнаяЧасть.Сумма + ТабличнаяЧасть.СуммаНДС
	|			ИНАЧЕ (ТабличнаяЧасть.Сумма + ТабличнаяЧасть.СуммаНДС) / ТабличнаяЧасть.Количество
	|		КОНЕЦ КАК ЧИСЛО(31, 2)) КАК Цена,
	|	0 КАК СуммаСкидки,
	|	ТабличнаяЧасть.СтавкаНДС КАК СтавкаНДС,
	|	ВЫБОР
	|		КОГДА &ЦенаВключаетНДС
	|			ТОГДА ТабличнаяЧасть.Сумма
	|		ИНАЧЕ ТабличнаяЧасть.Сумма + ТабличнаяЧасть.СуммаНДС
	|	КОНЕЦ КАК СуммаСНДС,
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.АлкогольнаяПродукция, ЛОЖЬ) КАК АлкогольнаяПродукция,
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ТабачнаяПродукция, ЛОЖЬ) КАК ТабачнаяПродукция
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.НомерСтроки,
	|	ЛОЖЬ,
	|	ТабличнаяЧасть.ЗаказПокупателя,
	|	ТабличнаяЧасть.Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка),
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ВидНоменклатуры.ТипНоменклатуры, ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПустаяСсылка)),
	|	ЛОЖЬ,
	|	ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.НаименованиеПолное, ТабличнаяЧасть.Содержание),
	|	"""",
	|	"""",
	|	ТабличнаяЧасть.Количество,
	|	ТабличнаяЧасть.Количество,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА &ЦенаВключаетНДС
	|				ТОГДА ТабличнаяЧасть.Цена
	|			КОГДА ТабличнаяЧасть.Количество = 0
	|				ТОГДА ТабличнаяЧасть.Сумма + ТабличнаяЧасть.СуммаНДС
	|			ИНАЧЕ (ТабличнаяЧасть.Сумма + ТабличнаяЧасть.СуммаНДС) / ТабличнаяЧасть.Количество
	|		КОНЕЦ КАК ЧИСЛО(31, 2)),
	|	0,
	|	ТабличнаяЧасть.СтавкаНДС,
	|	ВЫБОР
	|		КОГДА &ЦенаВключаетНДС
	|			ТОГДА ТабличнаяЧасть.Сумма
	|		ИНАЧЕ ТабличнаяЧасть.Сумма + ТабличнаяЧасть.СуммаНДС
	|	КОНЕЦ,
	|	ЛОЖЬ,
	|	ЛОЖЬ
	|ИЗ
	|	Документ.КорректировкаРеализации.Услуги КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки,
	|	Товары.СверхЗаказа,
	|	Товары.Заказ,
	|	Товары.ТипНоменклатуры,
	|	Товары.ПодакцизныйТовар,
	|	Товары.НоменклатураНаименование,
	|	Товары.ХарактеристикаНаименование,
	|	Товары.УпаковкаНаименование,
	|	Товары.КоличествоУпаковок,
	|	Товары.Цена,
	|	Товары.СуммаСкидки,
	|	Товары.СтавкаНДС,
	|	Товары.СуммаСНДС,
	|	Товары.АлкогольнаяПродукция,
	|	Товары.ТабачнаяПродукция
	|ИЗ
	|	Товары КАК Товары");
	
	Запрос.УстановитьПараметр("Ссылка",          ДокументСсылка);
	Запрос.УстановитьПараметр("ЦенаВключаетНДС",      Шапка.ЦенаВключаетНДС);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ДанныеДокументаДляЧека = РозничныеПродажиКлиентСервер.СтруктураДанныхДокументаДляПараметровФискализацииЧека();
	ДанныеДокументаДляЧека.Шапка = Шапка;
	ДанныеДокументаДляЧека.Товары = РезультатЗапроса[РезультатЗапроса.Количество() - 1].Выгрузить();
	ДанныеДокументаДляЧека.ДанныеДляМОТП = ДанныеДляМОТП(ДокументСсылка, МенеджерВременныхТаблиц);
	
	ПараметрыФискализацииЧека = ПодключаемоеОборудованиеУТВызовСервера.ПараметрыФискализацииЧека(ДанныеДокументаДляЧека, , );
		
	Возврат ПараметрыФискализацииЧека;
КонецФункции

Функция ПараметрыОперацииФискализацииЧекаКоррекции(ДокументСсылка, ДанныеЧекаКоррекции, СуммаПредоплатыКорректировка = Неопределено, ВерсияФФД = Неопределено) Экспорт
	
	ПараметрыФискализацииЧека = ПараметрыОперацииФискализацииЧека(ДокументСсылка, СуммаПредоплатыКорректировка, ВерсияФФД);
	
	Если ПараметрыФискализацииЧека <> Неопределено Тогда
		ПараметрыФискализацииЧека.Вставить("ДанныеКоррекции"		, ДанныеЧекаКоррекции.ДанныеЧекаКоррекции);
		ПараметрыФискализацииЧека.Вставить("НеприменениеККТ"		, ДанныеЧекаКоррекции.НеприменениеККТ);
		ПараметрыФискализацииЧека.Вставить("КорректируемыйДокумент"	, ДанныеЧекаКоррекции.КорректируемыйДокумент);
		
		ПараметрыФискализацииЧека.ДокументОснование = ДанныеЧекаКоррекции.ДокументОснование;
	КонецЕсли;
	
	Возврат ПараметрыФискализацииЧека;
	
КонецФункции

#КонецОбласти

Функция ТекстЗапросаПрослеживаемыеТовары(НомераТаблиц, ПараметрыПроведения, Реквизиты) Экспорт
	
	Если НЕ Реквизиты.КорректироватьБУиНУ
		И НЕ Реквизиты.КорректироватьНДС
		ИЛИ НЕ Реквизиты.ЕстьТовары Тогда
		ПараметрыПроведения.Вставить("ВТ_РНПТКорректировки",           Неопределено);
		ПараметрыПроведения.Вставить("ВТ_РНПТДокументРеализации",      Неопределено);
		ПараметрыПроведения.Вставить("ВТ_СуммыБезНДС",                 Неопределено);
		ПараметрыПроведения.Вставить("ВТ_ПрослеживаемыеТоварыПредварительная", Неопределено);
		ПараметрыПроведения.Вставить("ПрослеживаемыеТоварыУвеличение", Неопределено);
		ПараметрыПроведения.Вставить("ПрослеживаемыеТоварыУменьшение", Неопределено);
		ПараметрыПроведения.Вставить("ПрослеживаемыеОперации",         Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ВТ_РНПТКорректировки",           НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВТ_РНПТДокументРеализации",      НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВТ_СуммыБезНДС",                 НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВТ_ПрослеживаемыеТоварыПредварительная", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПрослеживаемыеТоварыУвеличение", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПрослеживаемыеТоварыУменьшение", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПрослеживаемыеОперации",         НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	КорректировкаРеализацииСведенияПрослеживаемости.Ссылка КАК Ссылка,
	|	КорректировкаРеализацииТовары.Ссылка.Контрагент КАК Контрагент,
	|	КорректировкаРеализацииТовары.Номенклатура КАК Номенклатура,
	|	КорректировкаРеализацииТовары.СтранаПроисхождения КАК СтранаПроисхождения,
	|	КорректировкаРеализацииСведенияПрослеживаемости.РНПТ КАК РНПТ,
	|	СУММА(КорректировкаРеализацииСведенияПрослеживаемости.Количество) КАК Количество,
	|	СУММА(КорректировкаРеализацииСведенияПрослеживаемости.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости,
	|	КорректировкаРеализацииСведенияПрослеживаемости.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	КорректировкаРеализацииТовары.СуммаНДС КАК СуммаНДС,
	|	КорректировкаРеализацииТовары.СуммаНДСДоИзменения КАК СуммаНДСДоИзменения,
	|	КорректировкаРеализацииТовары.Цена КАК Цена,
	|	КорректировкаРеализацииТовары.ЦенаДоИзменения КАК ЦенаДоИзменения,
	|	КорректировкаРеализацииТовары.Сумма КАК Сумма,
	|	КорректировкаРеализацииТовары.СуммаДоИзменения КАК СуммаДоИзменения,
	|	КорректировкаРеализацииТовары.Ссылка.Дата КАК Дата,
	|	КорректировкаРеализацииТовары.Ссылка.Номер КАК Номер,
	|	КорректировкаРеализацииСведенияПрослеживаемости.Сумма КАК СуммаБезНДС,
	|	КорректировкаРеализацииТовары.Количество КАК КоличествоПоНоменклатуре,
	|	КорректировкаРеализацииТовары.КоличествоДоИзменения КАК КоличествоДоИзмененияПоНоменклатуре
	|ПОМЕСТИТЬ ВТ_РНПТКорректировки
	|ИЗ
	|	Документ.КорректировкаРеализации.СведенияПрослеживаемости КАК КорректировкаРеализацииСведенияПрослеживаемости
	|		ПОЛНОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации.Товары КАК КорректировкаРеализацииТовары
	|		ПО КорректировкаРеализацииСведенияПрослеживаемости.Ссылка = КорректировкаРеализацииТовары.Ссылка
	|			И КорректировкаРеализацииСведенияПрослеживаемости.ИдентификаторСтроки = КорректировкаРеализацииТовары.ИдентификаторСтроки
	|ГДЕ
	|	КорректировкаРеализацииСведенияПрослеживаемости.Ссылка = &Ссылка
	|	И КорректировкаРеализацииТовары.ПрослеживаемыйТовар
	|	И КорректировкаРеализацииТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаРеализацииСведенияПрослеживаемости.ИдентификаторСтроки,
	|	КорректировкаРеализацииТовары.Номенклатура,
	|	КорректировкаРеализацииСведенияПрослеживаемости.РНПТ,
	|	КорректировкаРеализацииТовары.Ссылка.Контрагент,
	|	КорректировкаРеализацииСведенияПрослеживаемости.Ссылка,
	|	КорректировкаРеализацииТовары.СуммаНДС,
	|	КорректировкаРеализацииТовары.СуммаНДСДоИзменения,
	|	КорректировкаРеализацииТовары.Цена,
	|	КорректировкаРеализацииТовары.ЦенаДоИзменения,
	|	КорректировкаРеализацииТовары.Сумма,
	|	КорректировкаРеализацииТовары.СуммаДоИзменения,
	|	КорректировкаРеализацииТовары.Ссылка.Дата,
	|	КорректировкаРеализацииТовары.Ссылка.Номер,
	|	КорректировкаРеализацииСведенияПрослеживаемости.Сумма,
	|	КорректировкаРеализацииТовары.Количество,
	|	КорректировкаРеализацииТовары.КоличествоДоИзменения,
	|	КорректировкаРеализацииТовары.СтранаПроисхождения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ИдентификаторСтроки,
	|	РНПТ,
	|	Номенклатура,
	|	СтранаПроисхождения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслугСведенияПрослеживаемости.РНПТ КАК РНПТ,
	|	СУММА(РеализацияТоваровУслугСведенияПрослеживаемости.Количество) КАК Количество,
	|	СУММА(РеализацияТоваровУслугСведенияПрослеживаемости.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	РеализацияТоваровУслугТовары.Номенклатура КАК Номенклатура,
	|	РеализацияТоваровУслугТовары.СтранаПроисхождения КАК СтранаПроисхождения,
	|	РеализацияТоваровУслугТовары.Ссылка.Контрагент КАК Контрагент,
	|	РеализацияТоваровУслугТовары.СуммаНДС КАК СуммаНДС,
	|	РеализацияТоваровУслугТовары.Цена КАК Цена,
	|	РеализацияТоваровУслугТовары.Сумма КАК Сумма,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.Сумма КАК СуммаБезНДС
	|ПОМЕСТИТЬ ВТ_РНПТДокументРеализации
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.СведенияПрослеживаемости КАК РеализацияТоваровУслугСведенияПрослеживаемости
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|		ПО РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка = РеализацияТоваровУслугТовары.Ссылка
	|			И РеализацияТоваровУслугСведенияПрослеживаемости.ИдентификаторСтроки = РеализацияТоваровУслугТовары.ИдентификаторСтроки
	|ГДЕ
	|	РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка = &ДокументРеализации
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияТоваровУслугСведенияПрослеживаемости.ИдентификаторСтроки,
	|	РеализацияТоваровУслугТовары.СтранаПроисхождения,
	|	РеализацияТоваровУслугТовары.Номенклатура,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.РНПТ,
	|	РеализацияТоваровУслугТовары.Ссылка.Контрагент,
	|	РеализацияТоваровУслугТовары.СуммаНДС,
	|	РеализацияТоваровУслугТовары.Цена,
	|	РеализацияТоваровУслугТовары.Сумма,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.Сумма
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаРеализацииСведенияПрослеживаемости.РНПТ,
	|	КорректировкаРеализацииСведенияПрослеживаемости.Количество,
	|	КорректировкаРеализацииСведенияПрослеживаемости.КоличествоПрослеживаемости,
	|	КорректировкаРеализацииСведенияПрослеживаемости.ИдентификаторСтроки,
	|	КорректировкаРеализацииТовары.Номенклатура,
	|	КорректировкаРеализацииТовары.СтранаПроисхождения,
	|	КорректировкаРеализацииТовары.Ссылка.Контрагент,
	|	КорректировкаРеализацииТовары.СуммаНДС,
	|	КорректировкаРеализацииТовары.Цена,
	|	КорректировкаРеализацииТовары.Сумма,
	|	КорректировкаРеализацииСведенияПрослеживаемости.Сумма
	|ИЗ
	|	Документ.КорректировкаРеализации.СведенияПрослеживаемости КАК КорректировкаРеализацииСведенияПрослеживаемости
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации.Товары КАК КорректировкаРеализацииТовары
	|		ПО КорректировкаРеализацииСведенияПрослеживаемости.Ссылка = КорректировкаРеализацииТовары.Ссылка
	|			И КорректировкаРеализацииСведенияПрослеживаемости.ИдентификаторСтроки = КорректировкаРеализацииТовары.ИдентификаторСтроки
	|ГДЕ
	|	КорректировкаРеализацииСведенияПрослеживаемости.Ссылка = &ДокументРеализации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КорректировкаРеализацииТовары.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализацииТовары.Ссылка.СуммаВключаетНДС
	|			ТОГДА СУММА(КорректировкаРеализацииТовары.Сумма - КорректировкаРеализацииТовары.СуммаНДС)
	|		ИНАЧЕ СУММА(КорректировкаРеализацииТовары.Сумма)
	|	КОНЕЦ КАК СуммаБезНДС,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализацииТовары.Ссылка.СуммаВключаетНДС
	|			ТОГДА СУММА(КорректировкаРеализацииТовары.СуммаДоИзменения - КорректировкаРеализацииТовары.СуммаНДСДоИзменения)
	|		ИНАЧЕ СУММА(КорректировкаРеализацииТовары.СуммаДоИзменения)
	|	КОНЕЦ КАК СуммаБезНДСДоИзменения,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализацииТовары.Ссылка.СуммаВключаетНДС
	|			ТОГДА СУММА(КорректировкаРеализацииТовары.СуммаДоКорректировки - КорректировкаРеализацииТовары.СуммаНДСДоКорректировки)
	|		ИНАЧЕ СУММА(КорректировкаРеализацииТовары.СуммаДоКорректировки)
	|	КОНЕЦ КАК СуммаБезНДСДоКорректироки,
	|	КорректировкаРеализацииТовары.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВТ_СуммыБезНДС
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК КорректировкаРеализацииТовары
	|ГДЕ
	|	КорректировкаРеализацииТовары.Ссылка = &Ссылка
	|	И КорректировкаРеализацииТовары.ПрослеживаемыйТовар
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаРеализацииТовары.Номенклатура,
	|	КорректировкаРеализацииТовары.ИдентификаторСтроки,
	|	КорректировкаРеализацииТовары.Ссылка.СуммаВключаетНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ВТ_РНПТКорректировки.РНПТ, ВТ_РНПТДокументРеализации.РНПТ) КАК РНПТ,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_РНПТКорректировки.Количество - ЕСТЬNULL(ВТ_РНПТДокументРеализации.Количество, 0) > 0
	|				ТОГДА ВТ_РНПТКорректировки.Количество - ЕСТЬNULL(ВТ_РНПТДокументРеализации.Количество, 0)
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоУвеличение,
	|	СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(ВТ_РНПТДокументРеализации.Количество, 0) - ВТ_РНПТКорректировки.Количество > 0
	|				ТОГДА ЕСТЬNULL(ВТ_РНПТДокументРеализации.Количество, 0) - ВТ_РНПТКорректировки.Количество
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоУменьшение,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_РНПТКорректировки.КоличествоПрослеживаемости - ЕСТЬNULL(ВТ_РНПТДокументРеализации.КоличествоПрослеживаемости, 0) > 0
	|				ТОГДА ВТ_РНПТКорректировки.КоличествоПрослеживаемости - ЕСТЬNULL(ВТ_РНПТДокументРеализации.КоличествоПрослеживаемости, 0)
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоПрослеживаемостиУвеличение,
	|	СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(ВТ_РНПТДокументРеализации.КоличествоПрослеживаемости, 0) - ВТ_РНПТКорректировки.КоличествоПрослеживаемости > 0
	|				ТОГДА ЕСТЬNULL(ВТ_РНПТДокументРеализации.КоличествоПрослеживаемости, 0) - ВТ_РНПТКорректировки.КоличествоПрослеживаемости
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоПрослеживаемостиУменьшение,
	|	ЕСТЬNULL(ВТ_РНПТКорректировки.Номенклатура, ВТ_РНПТДокументРеализации.Номенклатура) КАК Номенклатура,
	|	ЕСТЬNULL(ВТ_РНПТКорректировки.СтранаПроисхождения, ВТ_РНПТДокументРеализации.СтранаПроисхождения) КАК СтранаПроисхождения
	|ПОМЕСТИТЬ ВТ_ПрослеживаемыеТоварыПредварительная
	|ИЗ
	|	ВТ_РНПТКорректировки КАК ВТ_РНПТКорректировки
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВТ_РНПТДокументРеализации КАК ВТ_РНПТДокументРеализации
	|		ПО ВТ_РНПТКорректировки.ИдентификаторСтроки = ВТ_РНПТДокументРеализации.ИдентификаторСтроки
	|			И ВТ_РНПТКорректировки.РНПТ = ВТ_РНПТДокументРеализации.РНПТ
	|			И ВТ_РНПТКорректировки.Номенклатура = ВТ_РНПТДокументРеализации.Номенклатура
	|			И ВТ_РНПТКорректировки.СтранаПроисхождения = ВТ_РНПТДокументРеализации.СтранаПроисхождения
	|
	|СГРУППИРОВАТЬ ПО
	|	ЕСТЬNULL(ВТ_РНПТКорректировки.РНПТ, ВТ_РНПТДокументРеализации.РНПТ),
	|	ЕСТЬNULL(ВТ_РНПТКорректировки.Номенклатура, ВТ_РНПТДокументРеализации.Номенклатура),
	|	ЕСТЬNULL(ВТ_РНПТКорректировки.СтранаПроисхождения, ВТ_РНПТДокументРеализации.СтранаПроисхождения)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПрослеживаемыеТоварыПредварительная.РНПТ КАК РНПТ,
	|	ВТ_ПрослеживаемыеТоварыПредварительная.КоличествоУвеличение КАК Количество,
	|	ВТ_ПрослеживаемыеТоварыПредварительная.КоличествоПрослеживаемостиУвеличение КАК КоличествоПрослеживаемости,
	|	ВТ_ПрослеживаемыеТоварыПредварительная.Номенклатура КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Комитент,
	|	НЕОПРЕДЕЛЕНО КАК ВидЗапасов,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Комиссионер,
	|	ВТ_ПрослеживаемыеТоварыПредварительная.СтранаПроисхождения КАК СтранаПроисхождения,
	|	&ДокументРеализацииСсылка КАК ОснованиеДляВозврата,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаРеализации
	|ИЗ
	|	ВТ_ПрослеживаемыеТоварыПредварительная КАК ВТ_ПрослеживаемыеТоварыПредварительная
	|ГДЕ
	|	(ВТ_ПрослеживаемыеТоварыПредварительная.КоличествоПрослеживаемостиУвеличение > 0
	|			ИЛИ ВТ_ПрослеживаемыеТоварыПредварительная.КоличествоУвеличение > 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПрослеживаемыеТоварыПредварительная.РНПТ КАК РНПТ,
	|	ВТ_ПрослеживаемыеТоварыПредварительная.КоличествоУменьшение КАК Количество,
	|	ВТ_ПрослеживаемыеТоварыПредварительная.КоличествоПрослеживаемостиУменьшение КАК КоличествоПрослеживаемости,
	|	ВТ_ПрослеживаемыеТоварыПредварительная.Номенклатура КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Комитент,
	|	НЕОПРЕДЕЛЕНО КАК ВидЗапасов,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Комиссионер,
	|	ВТ_ПрослеживаемыеТоварыПредварительная.СтранаПроисхождения КАК СтранаПроисхождения,
	|	&ДокументРеализацииСсылка КАК ОснованиеДляВозврата,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаРеализации
	|ИЗ
	|	ВТ_ПрослеживаемыеТоварыПредварительная КАК ВТ_ПрослеживаемыеТоварыПредварительная
	|ГДЕ
	|	(ВТ_ПрослеживаемыеТоварыПредварительная.КоличествоУменьшение > 0
	|			ИЛИ ВТ_ПрослеживаемыеТоварыПредварительная.КоличествоПрослеживаемостиУменьшение > 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_РНПТКорректировки.Номенклатура КАК Номенклатура,
	|	ЕСТЬNULL(ВТ_РНПТКорректировки.Количество, 0) КАК Количество,
	|	ЕСТЬNULL(ВТ_РНПТКорректировки.РНПТ, ВТ_РНПТДокументРеализации.РНПТ) КАК РНПТ,
	|	ЕСТЬNULL(ВТ_РНПТКорректировки.КоличествоПрослеживаемости, 0) КАК КоличествоПрослеживаемости,
	|	ЕСТЬNULL(ВТ_РНПТКорректировки.Контрагент, ВТ_РНПТДокументРеализации.Контрагент) КАК Контрагент,
	|	ЕСТЬNULL(ВТ_СуммыБезНДС.СуммаБезНДС, 0) КАК СуммаБезНДС,
	|	ВЫБОР
	|		КОГДА НЕ &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ОтчетОбОперациях)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.КнигаПродаж)
	|	КОНЕЦ КАК ОтражениеВОтчетности,
	|	ВЫБОР
	|		КОГДА &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ПустаяСсылка)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.Реализация)
	|	КОНЕЦ КАК КодОперации,
	|	ВЫБОР
	|		КОГДА &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|			ТОГДА ВТ_РНПТКорректировки.Ссылка
	|		ИНАЧЕ &ДокументРеализацииСсылка
	|	КОНЕЦ КАК ДокументОперации,
	|	ВЫБОР
	|		КОГДА &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|			ТОГДА НАЧАЛОПЕРИОДА(ВТ_РНПТКорректировки.Ссылка.Дата, КВАРТАЛ)
	|		ИНАЧЕ &ПериодИсходнойОперации
	|	КОНЕЦ КАК ПериодОперации,
	|	ВЫБОР
	|		КОГДА &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|				ИЛИ ВТ_РНПТКорректировки.Номенклатура ЕСТЬ NULL
	|			ТОГДА ДАТАВРЕМЯ(1, 1, 1)
	|		ИНАЧЕ &ДатаДокументаРеализации
	|	КОНЕЦ КАК ДатаДокумента,
	|	ВЫБОР
	|		КОГДА &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|				ИЛИ ВТ_РНПТКорректировки.Номенклатура ЕСТЬ NULL
	|			ТОГДА """"
	|		ИНАЧЕ &НомерИсходногоДокумента
	|	КОНЕЦ КАК НомерДокумента,
	|	ВЫБОР
	|		КОГДА &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|				ИЛИ ВТ_РНПТКорректировки.Номенклатура ЕСТЬ NULL
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ТипыДокументов.УПД)
	|	КОНЕЦ КАК ТипДокументаВПрослеживаемости,
	|	ЕСТЬNULL(ВТ_РНПТКорректировки.ИдентификаторСтроки, ВТ_РНПТДокументРеализации.ИдентификаторСтроки) КАК ИдентификаторСтроки
	|ИЗ
	|	ВТ_РНПТКорректировки КАК ВТ_РНПТКорректировки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СуммыБезНДС КАК ВТ_СуммыБезНДС
	|		ПО ВТ_РНПТКорректировки.ИдентификаторСтроки = ВТ_СуммыБезНДС.ИдентификаторСтроки
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВТ_РНПТДокументРеализации КАК ВТ_РНПТДокументРеализации
	|		ПО ВТ_РНПТКорректировки.ИдентификаторСтроки = ВТ_РНПТДокументРеализации.ИдентификаторСтроки
	|			И ВТ_РНПТКорректировки.РНПТ = ВТ_РНПТДокументРеализации.РНПТ
	|			И ВТ_РНПТКорректировки.Номенклатура = ВТ_РНПТДокументРеализации.Номенклатура
	|			И ВТ_РНПТКорректировки.СтранаПроисхождения = ВТ_РНПТДокументРеализации.СтранаПроисхождения
	|ГДЕ
	|	ЕСТЬNULL(ВТ_РНПТКорректировки.Ссылка.ВидОперации, ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение)) <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение)
	|	И НЕ &ЭтоИсправлениеКорректировки
	|	И ВЫБОР
	|			КОГДА &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|				ТОГДА НЕ ВТ_РНПТКорректировки.Номенклатура ЕСТЬ NULL
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Количество,
	|	ВложенныйЗапрос.РНПТ,
	|	ВложенныйЗапрос.КоличествоПрослеживаемости,
	|	ВложенныйЗапрос.Контрагент,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТ_СуммыБезНДС.СуммаБезНДС, 0) - ЕСТЬNULL(ВТ_СуммыБезНДС.СуммаБезНДСДоИзменения, 0) > 0
	|			ТОГДА ЕСТЬNULL(ВТ_СуммыБезНДС.СуммаБезНДС, 0) - ЕСТЬNULL(ВТ_СуммыБезНДС.СуммаБезНДСДоИзменения, 0)
	|			ИНАЧЕ ЕСТЬNULL(ВТ_СуммыБезНДС.СуммаБезНДСДоИзменения, 0) - ЕСТЬNULL(ВТ_СуммыБезНДС.СуммаБезНДС, 0)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ОтчетОбОперациях)
	|		КОГДА ВложенныйЗапрос.СуммаНДСДоИзменения < ВложенныйЗапрос.СуммаНДС
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.КнигаПродаж)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.КнигаПокупок)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ПустаяСсылка)
	|		КОГДА ВложенныйЗапрос.СуммаДоИзменения < ВложенныйЗапрос.Сумма
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.УКДНаУвеличениеВыданный)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.УКДНаУменьшениеВыданный)
	|	КОНЕЦ,
	|	ВложенныйЗапрос.Ссылка,
	|	НАЧАЛОПЕРИОДА(ВложенныйЗапрос.Дата, КВАРТАЛ),
	|	ВЫБОР
	|		КОГДА НЕ &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|			ТОГДА ВложенныйЗапрос.Дата
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|			ТОГДА ВложенныйЗапрос.Номер
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ТипыДокументов.УКД)
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВложенныйЗапрос.ИдентификаторСтроки
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЕСТЬNULL(ВТ_РНПТКорректировки.Номенклатура, ВТ_РНПТДокументРеализации.Номенклатура) КАК Номенклатура,
	|		ВЫБОР
	|			КОГДА ЕСТЬNULL(ВТ_РНПТДокументРеализации.Количество, 0) - ЕСТЬNULL(ВТ_РНПТКорректировки.Количество, 0) > 0
	|				ТОГДА ЕСТЬNULL(ВТ_РНПТДокументРеализации.Количество, 0) - ЕСТЬNULL(ВТ_РНПТКорректировки.Количество, 0)
	|			ИНАЧЕ ЕСТЬNULL(ВТ_РНПТКорректировки.Количество, 0) - ЕСТЬNULL(ВТ_РНПТДокументРеализации.Количество, 0)
	|		КОНЕЦ КАК Количество,
	|		ЕСТЬNULL(ВТ_РНПТКорректировки.РНПТ, ВТ_РНПТДокументРеализации.РНПТ) КАК РНПТ,
	|		ВЫБОР
	|			КОГДА ЕСТЬNULL(ВТ_РНПТДокументРеализации.КоличествоПрослеживаемости, 0) - ЕСТЬNULL(ВТ_РНПТКорректировки.КоличествоПрослеживаемости, 0) > 0
	|				ТОГДА ЕСТЬNULL(ВТ_РНПТДокументРеализации.КоличествоПрослеживаемости, 0) - ЕСТЬNULL(ВТ_РНПТКорректировки.КоличествоПрослеживаемости, 0)
	|			ИНАЧЕ ЕСТЬNULL(ВТ_РНПТКорректировки.КоличествоПрослеживаемости, 0) - ЕСТЬNULL(ВТ_РНПТДокументРеализации.КоличествоПрослеживаемости, 0)
	|		КОНЕЦ КАК КоличествоПрослеживаемости,
	|		ЕСТЬNULL(ВТ_РНПТКорректировки.Контрагент, ВТ_РНПТДокументРеализации.Контрагент) КАК Контрагент,
	|		&Ссылка КАК Ссылка,
	|		ЕСТЬNULL(ВТ_РНПТКорректировки.СуммаНДС, ВТ_РНПТДокументРеализации.СуммаНДС) КАК СуммаНДС,
	|		ЕСТЬNULL(ВТ_РНПТКорректировки.СуммаНДСДоИзменения, ВТ_РНПТДокументРеализации.СуммаНДС) КАК СуммаНДСДоИзменения,
	|		ЕСТЬNULL(ВТ_РНПТКорректировки.Цена, ВТ_РНПТДокументРеализации.Цена) КАК Цена,
	|		ЕСТЬNULL(ВТ_РНПТДокументРеализации.Цена, ВТ_РНПТКорректировки.ЦенаДоИзменения) КАК ЦенаДоИзменения,
	|		ЕСТЬNULL(ВТ_РНПТКорректировки.Сумма, ВТ_РНПТДокументРеализации.Сумма) КАК Сумма,
	|		ЕСТЬNULL(ВТ_РНПТДокументРеализации.Сумма, ВТ_РНПТКорректировки.СуммаДоИзменения) КАК СуммаДоИзменения,
	|		ЕСТЬNULL(ВТ_РНПТКорректировки.Количество, 0) КАК КоличествоДляРаспределения,
	|		ЕСТЬNULL(ВТ_РНПТКорректировки.Ссылка.ВидОперации, ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИсправленияПоступленияРеализации.ПустаяСсылка)) КАК ВидОперации,
	|		ЕСТЬNULL(ВТ_РНПТКорректировки.Дата, ДАТАВРЕМЯ(1, 1, 1)) КАК Дата,
	|		ЕСТЬNULL(ВТ_РНПТКорректировки.Номер, """") КАК Номер,
	|		ЕСТЬNULL(ВТ_РНПТДокументРеализации.ИдентификаторСтроки, ВТ_РНПТКорректировки.ИдентификаторСтроки) КАК ИдентификаторСтроки,
	|		ЕСТЬNULL(ВТ_РНПТДокументРеализации.СуммаБезНДС, 0) - ЕСТЬNULL(ВТ_РНПТКорректировки.СуммаБезНДС, 0) КАК СуммаБезНДС
	|	ИЗ
	|		ВТ_РНПТКорректировки КАК ВТ_РНПТКорректировки
	|			ПОЛНОЕ СОЕДИНЕНИЕ ВТ_РНПТДокументРеализации КАК ВТ_РНПТДокументРеализации
	|			ПО ВТ_РНПТКорректировки.ИдентификаторСтроки = ВТ_РНПТДокументРеализации.ИдентификаторСтроки
	|				И ВТ_РНПТКорректировки.РНПТ = ВТ_РНПТДокументРеализации.РНПТ
	|				И ВТ_РНПТКорректировки.Номенклатура = ВТ_РНПТДокументРеализации.Номенклатура
	|				И ВТ_РНПТКорректировки.СтранаПроисхождения = ВТ_РНПТДокументРеализации.СтранаПроисхождения
	|	ГДЕ
	|		ВТ_РНПТКорректировки.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение)) КАК ВложенныйЗапрос
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СуммыБезНДС КАК ВТ_СуммыБезНДС
	|		ПО ВложенныйЗапрос.ИдентификаторСтроки = ВТ_СуммыБезНДС.ИдентификаторСтроки
	|ГДЕ
	|	ВложенныйЗапрос.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение)
	|	И ВЫБОР
	|			КОГДА &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|				ТОГДА ВложенныйЗапрос.СуммаНДС <> ВложенныйЗапрос.СуммаНДСДоИзменения
	|			ИНАЧЕ ВложенныйЗапрос.Сумма <> ВложенныйЗапрос.СуммаДоИзменения
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА ВложенныйЗапрос.Цена = ВложенныйЗапрос.ЦенаДоИзменения
	|				ТОГДА ВложенныйЗапрос.Количество > 0
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ";
	
	Возврат ТекстЗапроса + ПрослеживаемостьБП.ТекстРазделителяЗапросовПакета();
	
КонецФункции

#КонецОбласти
