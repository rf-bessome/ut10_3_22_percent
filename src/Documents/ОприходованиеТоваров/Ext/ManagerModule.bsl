Функция ТекстЗапросаПрослеживаемыеТовары(НомераТаблиц, ПараметрыПроведения, ВедетсяУчетПрослеживаемыхТоваров) Экспорт
	
	Если Не ВедетсяУчетПрослеживаемыхТоваров Тогда
		ПараметрыПроведения.Вставить("ВТ_Прослеживаемость",                Неопределено);
		ПараметрыПроведения.Вставить("ВТ_СуммыБезНДС",                     Неопределено);
		ПараметрыПроведения.Вставить("ПрослеживаемыеОперации",             Неопределено);
		ПараметрыПроведения.Вставить("ПрослеживаемыеТовары",               Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ВТ_Прослеживаемость",                НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВТ_СуммыБезНДС",                     НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПрослеживаемыеОперации",             НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПрослеживаемыеТовары",               НомераТаблиц.Количество());
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ОприходованиеТоваровТовары.Номенклатура КАК Номенклатура,
		|	СУММА(ОприходованиеТоваровСведенияПрослеживаемости.Количество) КАК Количество,
		|	ОприходованиеТоваровТовары.СтранаПроисхождения КАК СтранаПроисхождения,
		|	ОприходованиеТоваровСведенияПрослеживаемости.РНПТ КАК РНПТ,
		|	СУММА(ОприходованиеТоваровСведенияПрослеживаемости.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости,
		|	ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ОтчетОбОперациях) КАК ОтражениеВОтчетности,
		|	ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ВозвратРанееУтраченногоТовара) КАК КодОперации,
		|	ОприходованиеТоваровТовары.Ссылка КАК ДокументОперации,
		|	НАЧАЛОПЕРИОДА(ОприходованиеТоваровТовары.Ссылка.Дата, КВАРТАЛ) КАК ПериодОперации,
		|	ЕСТЬNULL(ОприходованиеТоваровТовары.Ссылка.Номер, """") КАК НомерДокумента,
		|	ЕСТЬNULL(ОприходованиеТоваровТовары.Ссылка.Дата, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаДокумента,
		|	ЗНАЧЕНИЕ(Справочник.ТипыДокументов.Акт) КАК ТипДокументаВПрослеживаемости,
		|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Контрагент,
		|	ОприходованиеТоваровТовары.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ОприходованиеТоваровСведенияПрослеживаемости.Сумма КАК СуммаБезНДС
		|ПОМЕСТИТЬ ВТ_Прослеживаемость
		|ИЗ
		|	Документ.ОприходованиеТоваров.Товары КАК ОприходованиеТоваровТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОприходованиеТоваров.СведенияПрослеживаемости КАК ОприходованиеТоваровСведенияПрослеживаемости
		|		ПО ОприходованиеТоваровТовары.Ссылка = ОприходованиеТоваровСведенияПрослеживаемости.Ссылка
		|			И ОприходованиеТоваровТовары.ИдентификаторСтроки = ОприходованиеТоваровСведенияПрослеживаемости.ИдентификаторСтроки
		|ГДЕ
		|	ОприходованиеТоваровТовары.ПрослеживаемыйТовар
		|	И ОприходованиеТоваровТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ОприходованиеТоваровТовары.Номенклатура,
		|	ЕСТЬNULL(ОприходованиеТоваровТовары.Ссылка.Номер, """"),
		|	ОприходованиеТоваровТовары.Ссылка,
		|	ОприходованиеТоваровТовары.СтранаПроисхождения,
		|	ОприходованиеТоваровСведенияПрослеживаемости.РНПТ,
		|	ЕСТЬNULL(ОприходованиеТоваровТовары.Ссылка.Дата, ДАТАВРЕМЯ(1, 1, 1)),
		|	НАЧАЛОПЕРИОДА(ОприходованиеТоваровТовары.Ссылка.Дата, КВАРТАЛ),
		|	ОприходованиеТоваровТовары.ИдентификаторСтроки,
		|	ОприходованиеТоваровСведенияПрослеживаемости.Сумма
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОприходованиеТоваровТовары.Номенклатура КАК Номенклатура,
		|	СУММА(ОприходованиеТоваровТовары.Сумма) КАК СуммаБезНДС,
		|	ОприходованиеТоваровТовары.ИдентификаторСтроки КАК ИдентификаторСтроки
		|ПОМЕСТИТЬ ВТ_СуммыБезНДС
		|ИЗ
		|	Документ.ОприходованиеТоваров.Товары КАК ОприходованиеТоваровТовары
		|ГДЕ
		|	ОприходованиеТоваровТовары.Ссылка = &Ссылка
		|	И ОприходованиеТоваровТовары.ПрослеживаемыйТовар
		|
		|СГРУППИРОВАТЬ ПО
		|	ОприходованиеТоваровТовары.Номенклатура,
		|	ОприходованиеТоваровТовары.ИдентификаторСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Прослеживаемость.Номенклатура КАК Номенклатура,
		|	СУММА(ВТ_Прослеживаемость.Количество) КАК Количество,
		|	ВТ_Прослеживаемость.РНПТ КАК РНПТ,
		|	СУММА(ВТ_Прослеживаемость.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости,
		|	ВТ_Прослеживаемость.ОтражениеВОтчетности КАК ОтражениеВОтчетности,
		|	ВТ_Прослеживаемость.КодОперации КАК КодОперации,
		|	ВТ_Прослеживаемость.ДокументОперации КАК ДокументОперации,
		|	ВТ_Прослеживаемость.Контрагент КАК Контрагент,
		|	ВТ_Прослеживаемость.ПериодОперации КАК ПериодОперации,
		|	ВТ_Прослеживаемость.НомерДокумента КАК НомерДокумента,
		|	ВТ_Прослеживаемость.ДатаДокумента КАК ДатаДокумента,
		|	ВТ_Прослеживаемость.ТипДокументаВПрослеживаемости КАК ТипДокументаВПрослеживаемости,
		|	СУММА(ЕСТЬNULL(ВТ_СуммыБезНДС.СуммаБезНДС, 0)) КАК СуммаБезНДС,
		|	ВТ_Прослеживаемость.ИдентификаторСтроки КАК ИдентификаторСтроки
		|ИЗ
		|	ВТ_Прослеживаемость КАК ВТ_Прослеживаемость
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СуммыБезНДС КАК ВТ_СуммыБезНДС
		|		ПО ВТ_Прослеживаемость.ИдентификаторСтроки = ВТ_СуммыБезНДС.ИдентификаторСтроки
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Прослеживаемость.Номенклатура,
		|	ВТ_Прослеживаемость.ТипДокументаВПрослеживаемости,
		|	ВТ_Прослеживаемость.НомерДокумента,
		|	ВТ_Прослеживаемость.ДатаДокумента,
		|	ВТ_Прослеживаемость.Контрагент,
		|	ВТ_Прослеживаемость.ПериодОперации,
		|	ВТ_Прослеживаемость.ДокументОперации,
		|	ВТ_Прослеживаемость.ОтражениеВОтчетности,
		|	ВТ_Прослеживаемость.КодОперации,
		|	ВТ_Прослеживаемость.РНПТ,
		|	ВТ_Прослеживаемость.ИдентификаторСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Прослеживаемость.Номенклатура КАК Номенклатура,
		|	СУММА(ВТ_Прослеживаемость.Количество) КАК Количество,
		|	ВТ_Прослеживаемость.СтранаПроисхождения КАК СтранаПроисхождения,
		|	СУММА(ВТ_Прослеживаемость.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости,
		|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Комитент,
		|	НЕОПРЕДЕЛЕНО КАК ВидЗапасов,
		|	ВТ_Прослеживаемость.РНПТ КАК РНПТ,
		|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Комиссионер,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаРеализации,
		|	НЕОПРЕДЕЛЕНО КАК ОснованиеДляВозврата
		|ИЗ
		|	ВТ_Прослеживаемость КАК ВТ_Прослеживаемость
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Прослеживаемость.Номенклатура,
		|	ВТ_Прослеживаемость.СтранаПроисхождения,
		|	ВТ_Прослеживаемость.РНПТ";
	
	Возврат ТекстЗапроса + ПрослеживаемостьБП.ТекстРазделителяЗапросовПакета();
	
КонецФункции