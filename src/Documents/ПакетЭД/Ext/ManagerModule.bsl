
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Обработчик обновления БЭД 1.1.25.0
// Удаляет распакованные пакеты ЭД и переводит на новую архитектуру нераспакованные.
//
// Параметры:
//  Параметры - Структура - параметры обработчика обновления.
//
Процедура ПеревестиНаНовуюАрхитектуруПакетыЭД() Экспорт
	
	УдаляемыеСтатусы = Новый Массив;
	УдаляемыеСтатусы.Добавить(Перечисления.СтатусыПакетовЭД.Отправлен);
	УдаляемыеСтатусы.Добавить(Перечисления.СтатусыПакетовЭД.Распакован);
	УдаляемыеСтатусы.Добавить(Перечисления.СтатусыПакетовЭД.Отменен);
	УдаляемыеСтатусы.Добавить(Перечисления.СтатусыПакетовЭД.Неизвестный);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("УдаляемыеСтатусы", УдаляемыеСтатусы);
	
	// Удаление пакетов.
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1000
	|	ПакетЭД.Ссылка КАК ПакетСсылка,
	|	ЕСТЬNULL(ЭДПрисоединенныеФайлы.Ссылка, ЗНАЧЕНИЕ(Справочник.ЭДПрисоединенныеФайлы.ПустаяСсылка)) КАК ПрисоединенныйФайлСсылка
	|ИЗ
	|	Документ.ПакетЭД КАК ПакетЭД
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЭДПрисоединенныеФайлы КАК ЭДПрисоединенныеФайлы
	|		ПО ПакетЭД.Ссылка = ЭДПрисоединенныеФайлы.УдалитьВладелецФайла
	|ГДЕ
	|	ПакетЭД.СтатусПакета В(&УдаляемыеСтатусы)
	|	И НЕ ПакетЭД.ПометкаУдаления";
	
	Результат = Запрос.Выполнить();
	Пока Не Результат.Пустой() Цикл
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			НачатьТранзакцию();
			
			Попытка
				
				ПакетОбъект = Выборка.ПакетСсылка.ПолучитьОбъект();
				ПакетОбъект.ПометкаУдаления = Истина;
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(ПакетОбъект);
				
				Если ЗначениеЗаполнено(Выборка.ПрисоединенныйФайлСсылка) Тогда
					ПрисоединенныйФайлОбъект = Выборка.ПрисоединенныйФайлСсылка.ПолучитьОбъект();
					ПрисоединенныйФайлОбъект.ПометкаУдаления = Истина;
					// В связи с тем, что поле "ВладелецФайла" в версии 1.1.25 было заменено (в новом нет типа "Документ.ПакетЭД"),
					// необходимо записать в него пустую ссылку, чтобы обойти ошибку при удалении элемента справочника из базы.
					ПрисоединенныйФайлОбъект.ВладелецФайла = Справочники.СоглашенияОбИспользованииЭД.ПустаяСсылка();
					ОбновлениеИнформационнойБазы.ЗаписатьДанные(ПрисоединенныйФайлОбъект);
				КонецЕсли;
				
				ЗафиксироватьТранзакцию();
				
			Исключение
				
				ОтменитьТранзакцию();
				Операция = НСтр("ru = 'Обновление подсистемы обмена с контрагентами'");
				ПодробныйТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ЭлектронныеДокументыСлужебныйВызовСервера.ОбработатьОшибку(Операция, ПодробныйТекстОшибки);
				ВызватьИсключение;
				
			КонецПопытки;
			
		КонецЦикла;
		
		Результат = Запрос.Выполнить();
		
	КонецЦикла;
	
	// Перевод на новую архитектуру.
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1000
	|	ПакетЭД.Ссылка КАК ПакетСсылка,
	|	ЭДПрисоединенныеФайлы.Ссылка КАК ПрисоединенныйФайлСсылка
	|ИЗ
	|	Документ.ПакетЭД КАК ПакетЭД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭДПрисоединенныеФайлы КАК ЭДПрисоединенныеФайлы
	|		ПО ПакетЭД.Ссылка = ЭДПрисоединенныеФайлы.УдалитьВладелецФайла
	|			И (НЕ ЭДПрисоединенныеФайлы.ПометкаУдаления)
	|ГДЕ
	|	НЕ ПакетЭД.СтатусПакета В (&УдаляемыеСтатусы)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПакетЭД.Дата УБЫВ";
	
	Результат = Запрос.Выполнить();
	Пока Не Результат.Пустой() Цикл
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			НачатьТранзакцию();
			
			Попытка
				
				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить("Справочник.ЭДПрисоединенныеФайлы");
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.ПрисоединенныйФайлСсылка);
				Блокировка.Заблокировать();
				
				ДанныеФайла = ПрисоединенныеФайлы.ПолучитьДанныеФайла(Выборка.ПрисоединенныйФайлСсылка,, Истина);
				ПрисоединенныеФайлы.ДобавитьФайл(
					Выборка.ПакетСсылка, ДанныеФайла.ИмяФайла, ДанныеФайла.Расширение,,, ДанныеФайла.СсылкаНаДвоичныеДанныеФайла);
				
				ФайлОбъект = Выборка.ПрисоединенныйФайлСсылка.ПолучитьОбъект();
				ФайлОбъект.ПометкаУдаления = Истина;
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(ФайлОбъект);
				
				ПакетОбъект = Выборка.ПакетСсылка.ПолучитьОбъект();
				Для Каждого СтрокаВладельца Из ПакетОбъект.ЭлектронныеДокументы Цикл
					СтрокаВладельца.ОбъектВладелец = ОбщегоНазначения.ПолучитьЗначениеРеквизита(
						СтрокаВладельца.ЭлектронныйДокумент, "ВладелецФайла");
				КонецЦикла;
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(ПакетОбъект);
				
				ЗафиксироватьТранзакцию();
				
			Исключение
				
				ОтменитьТранзакцию();
				Операция = НСтр("ru = 'Обновление подсистемы обмена с контрагентами'");
				ПодробныйТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ЭлектронныеДокументыСлужебныйВызовСервера.ОбработатьОшибку(Операция, ПодробныйТекстОшибки);
				ВызватьИсключение;
				
			КонецПопытки;
			
		КонецЦикла;
		
		Результат = Запрос.Выполнить();
	КонецЦикла;
	
КонецПроцедуры

// Обработчик обновления БЭД 1.1.25.17
//
// Параметры:
//  Параметры - Структура - параметры обработчика обновления.
//
Процедура ПометитьНаУдалениеПрисоединенныеФайлыПакетовЭД() Экспорт
	
	// Помечаем на удаление присоединенные файлы, которые не пометили при переходе на новую архитектуру.
	
	УдаляемыеСтатусы = Новый Массив;
	УдаляемыеСтатусы.Добавить(Перечисления.СтатусыПакетовЭД.Отправлен);
	УдаляемыеСтатусы.Добавить(Перечисления.СтатусыПакетовЭД.Распакован);
	УдаляемыеСтатусы.Добавить(Перечисления.СтатусыПакетовЭД.Отменен);
	УдаляемыеСтатусы.Добавить(Перечисления.СтатусыПакетовЭД.Неизвестный);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("УдаляемыеСтатусы", УдаляемыеСтатусы);
	
	// Удаление пакетов.
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1000
	|	ЭДПрисоединенныеФайлы.Ссылка
	|ИЗ
	|	Документ.ПакетЭД КАК ПакетЭД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭДПрисоединенныеФайлы КАК ЭДПрисоединенныеФайлы
	|		ПО ПакетЭД.Ссылка = ЭДПрисоединенныеФайлы.УдалитьВладелецФайла
	|ГДЕ
	|	ПакетЭД.СтатусПакета В(&УдаляемыеСтатусы)
	|	И ПакетЭД.ПометкаУдаления
	|	И НЕ ЭДПрисоединенныеФайлы.ПометкаУдаления";
	
	Результат = Запрос.Выполнить();
	Пока Не Результат.Пустой() Цикл
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ПрисоединенныйФайлОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ПрисоединенныйФайлОбъект.ПометкаУдаления = Истина;
			// В связи с тем, что поле "ВладелецФайла" в версии 1.1.25 было заменено (в новом нет типа "Документ.ПакетЭД"),
			// необходимо записать в него пустую ссылку, чтобы обойти ошибку при удалении элемента справочника из базы.
			ПрисоединенныйФайлОбъект.ВладелецФайла = Справочники.СоглашенияОбИспользованииЭД.ПустаяСсылка();
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ПрисоединенныйФайлОбъект);
			
		КонецЦикла;
		
		Результат = Запрос.Выполнить();
		
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли
