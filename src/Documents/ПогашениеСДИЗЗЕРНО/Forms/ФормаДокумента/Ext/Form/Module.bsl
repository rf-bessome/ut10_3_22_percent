#Область ОписаниеПеременных

&НаКлиенте
Перем КэшированныеЗначения; //используется механизмом обработки изменения реквизитов ТЧ

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ИнтеграцияИС.НастроитьВидимостьДокументаОснования(ЭтаФорма);
	Элементы.ДокументОснование.ДоступныеТипы = ОпределяемыеТипы().ОснованиеПогашениеСДИЗЗЕРНО.Тип;
	
	ЗаполнитьДоступныеВидыПродукции();
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ВерсионированиеОбъектов") Тогда
		МодульВерсионированиеОбъектов = ОбщегоНазначения.ОбщийМодуль("ВерсионированиеОбъектов");
		МодульВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтаФорма);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыИС.ПриСозданииНаСервере(ЭтаФорма);
	
	СобытияФормИСПереопределяемый.УстановитьСвязиПараметровВыбораСНоменклатурой(ЭтаФорма,   "ТоварыХарактеристика");
	СобытияФормИСПереопределяемый.УстановитьСвязиПараметровВыбораСНоменклатурой(ЭтаФорма,   "ТоварыСерия");
	СобытияФормИСПереопределяемый.УстановитьСвязиПараметровВыбораСХарактеристикой(ЭтаФорма, "ТоварыСерия");
	
	Элементы.Подразделение.Видимость = ИнтеграцияИС.ИспользоватьОбособленныеПодразделенияВыделенныеНаБаланс();
	
	Если Объект.Ссылка.Пустая() Тогда
		ПриСозданииЧтенииНаСервере();
		ИнтеграцияИСПереопределяемый.ЗаполнитьСтатусыУказанияСерий(Объект, ПараметрыУказанияСерий);
	КонецЕсли;
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма,, "ДокументОснование");
	
	СобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПриСозданииЧтенииНаСервере();
	
	СобытияФормИСПереопределяемый.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтаФорма, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЭтаФорма, "ВидимостьПодключаемыхКоманд") Тогда
		НастроитьЗависимыеЭлементыФормы(ЭтаФорма,, "ДокументОснование");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ИнтеграцияИСКлиент.ПослеЗаписиВФормеОбъектаДокументаИС(
		ЭтаФорма,
		Объект,
		ИнтеграцияЗЕРНОКлиентСервер.ИмяПодсистемы(),
		ПараметрыЗаписи);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды") Тогда
		МодульПодключаемыеКомандыКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиент");
		МодульПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтаФорма, Объект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтаФорма, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПриСозданииЧтенииНаСервере();
	
	РазблокироватьДанныеФормыДляРедактирования();
	
	ИнтеграцияИС.ПослеЗаписиНаСервереВФормеОбъектаДокументаИС(
		ЭтаФорма,
		ТекущийОбъект,
		ИнтеграцияЗЕРНОКлиентСервер.ИмяПодсистемы(),
		ПараметрыЗаписи);
	
	СобытияФормИСПереопределяемый.ПослеЗаписиНаСервере(ЭтаФорма, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	СобытияФормЗЕРНОКлиентПереопределяемый.ОбработкаВыбораСерии(
		ЭтаФорма, ВыбранноеЗначение, ИсточникВыбора, ПараметрыУказанияСерий);
	
	СобытияФормЗЕРНОКлиентПереопределяемый.ОбработкаВыбора(ЭтаФорма, ВыбранноеЗначение, ИсточникВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	
	Если НовыйОбъект = Элементы.ДокументОснование.ДоступныеТипы.ПривестиЗначение(НовыйОбъект) Тогда
		Объект.ДокументОснование = НовыйОбъект;
		Модифицированность = Истина;
		Записать();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = ИнтеграцияИСКлиентСервер.ИмяСобытияИзмененоСостояние(ИнтеграцияЗЕРНОКлиентСервер.ИмяПодсистемы())
		И Параметр.Ссылка = Объект.Ссылка Тогда
		
		Если Параметр.Свойство("ОбъектИзменен")
			И Параметр.ОбъектИзменен Тогда
			ОбновитьПредставленияНаФорме(Истина);
		Иначе
			ОбновитьПредставленияНаФорме(Ложь);
		КонецЕсли;
		
		ОтключитьОтметкуНезаполненного();
		
	КонецЕсли;
	
	Если ИмяСобытия = ИнтеграцияИСКлиентСервер.ИмяСобытияВыполненОбмен(ИнтеграцияЗЕРНОКлиентСервер.ИмяПодсистемы())
	 И (Параметр = Неопределено
		Или (ТипЗнч(Параметр) = Тип("Структура") И Параметр.ОбновлятьСтатусВФормахДокументов)) Тогда
		
		ОбновитьПредставленияНаФорме(Истина);
		ОтключитьОтметкуНезаполненного();
		
	КонецЕсли;
	
	Если (ИмяСобытия = "Запись_КлючиРеквизитовОрганизацийЗЕРНО"
		Или ИмяСобытия = "Запись_КлючиАдресовЗЕРНО") Тогда
		
		ЗаполнитьГиперссылкиРеквизитов();
		
	КонецЕсли;
	
	СобытияФормИСКлиентПереопределяемый.ОбработкаОповещения(ЭтаФорма, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СобытияФормИСКлиент.ОбработкаНавигационнойСсылки(ЭтаФорма, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	СобытияФормИСКлиентПереопределяемый.ПередЗаписью(ЭтаФорма, Отказ, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ОбновитьЗаписатьПараметрыОбновленияСтатуса(Отказ, ТекущийОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтатусЗЕРНОПредставлениеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОчиститьСообщения();
	
	Если (Не ЗначениеЗаполнено(Объект.Ссылка)) Или (Не Объект.Проведен) Тогда
		
		ОписаниеОповещенияВопрос = Новый ОписаниеОповещения(
			"СтатусПредставлениеОбработкаНавигационнойСсылкиЗавершение",
			ЭтаФорма,
			Новый Структура("НавигационнаяСсылкаФорматированнойСтроки", НавигационнаяСсылкаФорматированнойСтроки));
		ТекстВопроса = НСтр("ru = 'Документ ""Погашение СДИЗ ЗЕРНО"" не проведен. Провести?'");
		ПоказатьВопрос(ОписаниеОповещенияВопрос, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	ИначеЕсли Модифицированность Тогда
		
		ОписаниеОповещенияВопрос = Новый ОписаниеОповещения(
			"СтатусПредставлениеОбработкаНавигационнойСсылкиЗавершение",
			ЭтаФорма,
			Новый Структура("НавигационнаяСсылкаФорматированнойСтроки", НавигационнаяСсылкаФорматированнойСтроки));
		ТекстВопроса = НСтр("ru = 'Документ ""Погашение СДИЗ ЗЕРНО"" был изменен. Провести?'");
		ПоказатьВопрос(ОписаниеОповещенияВопрос, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		
		ОбработатьНажатиеНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма,, "Организация");
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Объект.Товары.Количество() > 0 И ВыбранноеЗначение <> Объект.Организация Тогда
		
		СтандартнаяОбработка = Ложь;
		ТекстВопроса = НСтр("ru = 'При изменении организации табличная часть Товары и связанные с ней
								  |партии будут очищены. Продолжить?'");
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросПриИзмененииОрганизацииПриЗавершении", ЭтаФорма, ВыбранноеЗначение);

		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидПродукцииПриИзменении(Элемент)
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма,, "ВидПродукции");
	
	ИнтеграцияЗЕРНОКлиентСервер.УстановитьПараметрыВыбораНоменклатуры(ЭтаФорма, Объект.ВидПродукции);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидПродукцииОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Объект.Товары.Количество() > 0 И ВыбранноеЗначение <> Объект.ВидПродукции Тогда
		
		СтандартнаяОбработка = Ложь;
		ТекстВопроса = НСтр("ru = 'При изменении вида продукции табличная часть Товары и связанные с ней
								  |транспортные средства будут очищены. Продолжить?'");
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросПриИзмененииВидПродукцииПриЗавершении", ЭтаФорма, ВыбранноеЗначение);
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСпискомПолноеПогашениеПриИзменении(Элемент)
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	Если Не ТекущиеДанные.ПолноеПогашение Тогда
		ТекущиеДанные.ПричинаРасхождения         = Неопределено;
		ТекущиеДанные.ОписаниеПричиныРасхождения = Неопределено;
	КонецЕсли;
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма, Элементы.Товары.ТекущаяСтрока, "ПолноеПогашение");
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСпискомСДИЗПриИзменении(Элемент)
	
	СДИЗПриИзмененииНаСервере();
	УстановитьОтборТранспортныхСредствПоСтроке();
	ТранспортныеСредстваУстновитьСнятьОтметку(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСписокмКоличествоЗЕРНОПриИзменении(Элемент)
	
	КоличествоЗЕРНОПриИзменении(Элементы.Товары.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСпискомСДИЗНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СДИЗНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПунктНазначенияПриИзменении(Элемент)
	ПриИзмененииРеквизитаШапкиСДИЗ(Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура ПокупательПриИзменении(Элемент)
	ПриИзмененииРеквизитаШапкиСДИЗ(Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура ГрузополучательПриИзменении(Элемент)
	ПриИзмененииРеквизитаШапкиСДИЗ(Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура ПунктОтправленияПриИзменении(Элемент)
	ПриИзмененииРеквизитаШапкиСДИЗ(Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура ПродавецПриИзменении(Элемент)
	ПриИзмененииРеквизитаШапкиСДИЗ(Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура ГрузоотправительПриИзменении(Элемент)
	ПриИзмененииРеквизитаШапкиСДИЗ(Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура ГрузоотправительОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ОбработкаВыбораРеквизитаШапкиСДИЗ(Элемент, СтандартнаяОбработка, ВыбранноеЗначение);
КонецПроцедуры

&НаКлиенте
Процедура ГрузополучательОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ОбработкаВыбораРеквизитаШапкиСДИЗ(Элемент, СтандартнаяОбработка, ВыбранноеЗначение);
КонецПроцедуры

&НаКлиенте
Процедура ПокупательОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ОбработкаВыбораРеквизитаШапкиСДИЗ(Элемент, СтандартнаяОбработка, ВыбранноеЗначение);
КонецПроцедуры

&НаКлиенте
Процедура ПродавецОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ОбработкаВыбораРеквизитаШапкиСДИЗ(Элемент, СтандартнаяОбработка, ВыбранноеЗначение);
КонецПроцедуры

&НаКлиенте
Процедура ПунктОтправленияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ОбработкаВыбораРеквизитаШапкиСДИЗ(Элемент, СтандартнаяОбработка, ВыбранноеЗначение);
КонецПроцедуры

&НаКлиенте
Процедура ПунктНазначенияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ОбработкаВыбораРеквизитаШапкиСДИЗ(Элемент, СтандартнаяОбработка, ВыбранноеЗначение);
КонецПроцедуры

&НаКлиенте
Процедура ГрузоотправительОчистка(Элемент, СтандартнаяОбработка)
	ОбработкаВыбораРеквизитаШапкиСДИЗ(Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ПродавецОчистка(Элемент, СтандартнаяОбработка)
	ОбработкаВыбораРеквизитаШапкиСДИЗ(Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ПунктОтправленияОчистка(Элемент, СтандартнаяОбработка)
	ОбработкаВыбораРеквизитаШапкиСДИЗ(Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ГрузополучательОчистка(Элемент, СтандартнаяОбработка)
	ОбработкаВыбораРеквизитаШапкиСДИЗ(Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ПокупательОчистка(Элемент, СтандартнаяОбработка)
	ОбработкаВыбораРеквизитаШапкиСДИЗ(Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ПунктНазначенияОчистка(Элемент, СтандартнаяОбработка)
	ОбработкаВыбораРеквизитаШапкиСДИЗ(Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура СвязанныеДокументыПредставлениеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = ИнтеграцияЗЕРНОКлиентСервер.ПараметрыФормыРедактированияСвязанныхДокументов(ТипЗнч(Объект.Ссылка));
	ПараметрыФормы.ТолькоПросмотр = Истина;
	Для Каждого СтрокаТаблицы Из СвязанныеДокументы Цикл
		ДанныеДокумента = Новый Структура();
		ДанныеДокумента.Вставить("ПервичныйДокумент", СтрокаТаблицы.ПервичныйДокумент);
		ПараметрыФормы.ДанныеДокументов.Добавить(ДанныеДокумента);
	КонецЦикла;
	
	ОткрытьФорму(
		"ОбщаяФорма.СвязанныеДокументыЗЕРНО",
		ПараметрыФормы, 
		ЭтаФорма,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьОтборТранспортныхСредствПоСтроке();
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма, Элементы.Товары.ТекущаяСтрока, "Товары");
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		
		ТекущиеДанные              = Элемент.ТекущиеДанные;
		ИдентификаторСтрокиТоваров = ТекущиеДанные.Идентификатор;
		
		Если Не ЗначениеЗаполнено(ИдентификаторСтрокиТоваров) Или Копирование Тогда
			ТекущиеДанные.Идентификатор = Строка(Новый УникальныйИдентификатор);
		КонецЕсли;
		
		Если Копирование Тогда
			СкопироватьТранспорныеСредстваНаФорма(ИдентификаторСтрокиТоваров, ТекущиеДанные.Идентификатор);
			УстановитьОтборТранспортныхСредствПоСтроке();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если НоваяСтрока И ОтменаРедактирования Тогда
		
		ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
		
		ОчиститьТранспортныеСредстваНаФормеПоИдентификаторуСтроки(ТекущиеДанные.Идентификатор);
		ОчиститьТранспортныеСредстваПоИдентификаторуСтроки(ТекущиеДанные.Идентификатор);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПередУдалением(Элемент, Отказ)
	
	Если РедактированиеФормыНедоступно Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	ОчиститьТранспортныеСредстваПоИдентификаторуСтроки(ТекущиеДанные.Идентификатор);
	
	ТекущиеДанные.КоличествоЗЕРНО = 0;
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма, Элементы.Товары.ТекущаяСтрока, "ПолноеПогашение");
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	ПараметрыОтбораНоменклатурыПоСДИЗ = Новый Структура;
	Если ЗначениеЗаполнено(ТекущиеДанные.СДИЗ) Тогда
		Если ЗначениеЗаполнено(ТекущиеДанные.КодТНВЭД) Тогда
			ПараметрыОтбораНоменклатурыПоСДИЗ.Вставить("КодТНВЭД", ТекущиеДанные.КодТНВЭД);
		КонецЕсли;
		Если ЗначениеЗаполнено(ТекущиеДанные.ОКПД2) Тогда
			ПараметрыОтбораНоменклатурыПоСДИЗ.Вставить("ОКПД2", ТекущиеДанные.ОКПД2);
		КонецЕсли;
	КонецЕсли;
	
	СобытияФормЗЕРНОКлиент.ПриНачалеВыбораНоменклатуры(
		Элемент, Объект.ВидПродукции, СтандартнаяОбработка,,ПараметрыОтбораНоменклатурыПоСДИЗ);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	НоменклатураПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыХарактеристикаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	СобытияФормЗЕРНОКлиентПереопределяемый.ПриНачалеВыбораХарактеристики(
		Элемент, ТекущиеДанные, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыХарактеристикаПриИзменении(Элемент)
	
	ХарактеристикаПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСерияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ИнтеграцияИСКлиент.ОткрытьПодборСерий(ЭтаФорма,, Элемент.ТекстРедактирования, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСерияПриИзменении(Элемент)
	
	СерияПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	КоличествоПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоЗЕРНОПриИзменении(Элемент)
	
	КоличествоЗЕРНОПриИзменении(Элементы.Товары.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ВыбраннаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСтроки = Объект.Товары.НайтиПоИдентификатору(ВыбраннаяСтрока);
	
	Если Поле = Элементы.ТоварыПредставлениеПартия Тогда
		
		Если ЗначениеЗаполнено(ДанныеСтроки.Партия) Тогда
			ПоказатьЗначение(, ДанныеСтроки.Партия);
		КонецЕсли;
	
	ИначеЕсли Поле = Элементы.ТоварыТранспортПредставление Тогда
		
		Если ДанныеСтроки.СтатусУказанияТранспорта > 0 Тогда
			
			ПараметрыОткрытияФормы = ИнтеграцияЗЕРНОСлужебныйКлиент.ПараметрыОткрытияФормыУказанияТранспортныхСредств();
			ПараметрыОткрытияФормы.РежимОтметки     = Истина;
			ПараметрыОткрытияФормы.РежимОткрытия    = "ТранспортныеСредства";
			ПараметрыОткрытияФормы.ПеревозчикВШапке = Ложь;
			ПараметрыОткрытияФормы.СДИЗ             = ДанныеСтроки.СДИЗ;
			ПараметрыОткрытияФормы.ТолькоПросмотр   = РедактированиеФормыНеДоступно;
			
			СтруктураОтбора = Новый Структура("ИдентификаторСтрокиТоваров", ДанныеСтроки.Идентификатор);
			СтрокиДанных = Объект.ТранспортныеСредства.НайтиСтроки(СтруктураОтбора);
			Для Каждого СтрокаТаблицы Из СтрокиДанных Цикл
				СтрокаДанных = Новый Структура();
				СтрокаДанных.Вставить("Отметка", Истина);
				СтрокаДанных.Вставить("ТипТранспорта");
				СтрокаДанных.Вставить("ТранспортноеСредство");
				СтрокаДанных.Вставить("НомерТранспортногоСредства");
				СтрокаДанных.Вставить("НомерАвтомобильногоКонтейнера");
				ЗаполнитьЗначенияСвойств(СтрокаДанных, СтрокаТаблицы);
				ПараметрыОткрытияФормы.ТранспортныеСредства.Добавить(СтрокаДанных);
			КонецЦикла;
			
			ПараметрыОповещения = Новый Структура();
			ПараметрыОповещения.Вставить("ИдентификаторСтрокиТоваров", ДанныеСтроки.Идентификатор);
			ПараметрыОповещения.Вставить("ВыбраннаяСтрока",            ВыбраннаяСтрока);
			
			ИнтеграцияЗЕРНОСлужебныйКлиент.ОткрытьФормуУказанияТранспортныхСредств(
				ПараметрыОткрытияФормы,
				ЭтаФорма,
				Новый ОписаниеОповещения("ВыборТранспортныхСредствЗавершение", ЭтаФорма, ПараметрыОповещения));
		
		КонецЕсли;
		
	ИначеЕсли Поле = Элементы.ТоварыСДИЗ Тогда
		
		Если РедактированиеФормыНеДоступно Тогда
			ПоказатьЗначение(, ДанныеСтроки.СДИЗ);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСДИЗПриИзменении(Элемент)
	
	СДИЗПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСДИЗНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СДИЗНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПолноеПогашениеПриИзменении(Элемент)
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма, Элементы.Товары.ТекущаяСтрока, "ПолноеПогашение");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТранспортныеСредства

&НаКлиенте
Процедура ТранспортныеСредстваОтметкаПриИзменении(Элемент)
	
	ТекущиеДанные = ТранспортныеСредства.НайтиПоИдентификатору(Элементы.ТранспортныеСредства.ТекущаяСтрока);
	
	ОбработкаСнятияУстановкиОтметкиТранспортногоСредства(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПровестиДокумент(Команда)
	
	ОчиститьСообщения();
	ПараметрыЗаписи = Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение);
	Записать(ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьДокумент(Команда)
	
	ОчиститьСообщения();
	Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиИЗакрыть(Команда)
	
	ОчиститьСообщения();
	ПараметрыЗаписи = Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение);
	
	Если Записать(ПараметрыЗаписи) Тогда
		Закрыть();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ТранспортныеСредстваОтметитьВсе(Команда)
	
	ТранспортныеСредстваУстновитьСнятьОтметку(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ТранспортныеСредстваСнятьОтметку(Команда)
	
	ТранспортныеСредстваУстновитьСнятьОтметку(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура СвернутьРазвернутьПанель(Команда)
	
	ПанельДополнительныеСвойстваСвернута = Не ПанельДополнительныеСвойстваСвернута;
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма, Элементы.Товары.ТекущаяСтрока, "ПанельДополнительныеСвойстваСвернута");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПодбор(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("Подключаемый_ОбработкаРезультатаПодбораНоменклатуры", ЭтаФорма);
	СобытияФормЗЕРНОКлиентПереопределяемый.ОткрытьФормуПодбораНоменклатуры(ЭтаФорма, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура АрхивироватьДокумент(Команда)
	
	ИнтеграцияИСКлиент.АрхивироватьДокументы(ЭтаФорма, Объект.Ссылка, ИнтеграцияЗЕРНОКлиент);
	
КонецПроцедуры

&НаКлиенте
Процедура АннулироватьПогашениеСДИЗ(Команда)
	
	ОчиститьСообщения();
	
	СтрокиДляСписания = Новый Массив();
	
	Для Каждого ВыделеннаяСтрока Из Элементы.Товары.ВыделенныеСтроки Цикл
		ДанныеСтроки = Объект.Товары.НайтиПоИдентификатору(ВыделеннаяСтрока);
		Если ДанныеСтроки.СтатусОбработки = ПредопределенноеЗначение("Перечисление.СтатусыОбработкиПогашенияСДИЗЗЕРНО.СДИЗПогашен")
			Или ДанныеСтроки.СтатусОбработки = ПредопределенноеЗначение("Перечисление.СтатусыОбработкиПогашенияСДИЗЗЕРНО.АннулированиеПогашенияОшибкаПередачи") Тогда
			СтрокиДляСписания.Добавить(ДанныеСтроки);
		КонецЕсли;
	КонецЦикла;
	
	Если СтрокиДляСписания.Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Аннулирование списания по выделенным строкам невозможно'"));
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		
		ОписаниеОповещенияВопрос = Новый ОписаниеОповещения(
			"ВопросАннулированиеПогашенияСДИЗЗавершение",
			ЭтаФорма,
			СтрокиДляСписания);
		ТекстВопроса = НСтр("ru = 'Документ ""Погашение СДИЗ ЗЕРНО"" был изменен. Провести?'");
		ПоказатьВопрос(ОписаниеОповещенияВопрос, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		
		СтрокиТекстаВопроса = Новый Массив();
		
		Если СтрокиДляСписания.Количество() = 1 Тогда
			СтрокиТекстаВопроса.Добавить(
				СтрШаблон(НСтр("ru = 'Аннулировать погашение СДИЗ %1 ?'"), СтрокиДляСписания[0].Партия));
		Иначе
			СтрокиТекстаВопроса.Добавить(НСтр("ru = 'Аннулировать погашения СДИЗ ?'"));
			Для Каждого СтрокаТаблицы Из СтрокиДляСписания Цикл
				СтрокиТекстаВопроса.Добавить(Строка(СтрокаТаблицы.Партия));
			КонецЦикла;
		КонецЕсли;
		
		ОписаниеОповещенияВопрос = Новый ОписаниеОповещения(
			"ПодтверждениеАннулированияПогашенияСДИЗЗавершение",
			ЭтаФорма,
			СтрокиДляСписания);
		
		ТекстВопроса = СтрСоединить(СтрокиТекстаВопроса, Символы.ПС);
		ПоказатьВопрос(ОписаниеОповещенияВопрос, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДоступноеКоличество(Команда)
	
	Если Элементы.Товары.ТекущаяСтрока <> Неопределено Тогда

		ТекущиеДанные                 = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
		ВсегоКПогашениюПоСДИЗ         = ВсегоКПогашениюПоСДИЗ(ЭтаФорма, ТекущиеДанные, Истина);
		УстановитьКоличество          = ТекущиеДанные.КоличествоДоступно - ВсегоКПогашениюПоСДИЗ;
		
		Если ТекущиеДанные.КоличествоЗЕРНО = УстановитьКоличество Тогда
			Возврат;
		КонецЕсли;
		
		ТекущиеДанные.КоличествоЗЕРНО = УстановитьКоличество;
		КоличествоЗЕРНОПриИзменении(Элементы.Товары.ТекущиеДанные);
		
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РазбитьСтроку(Команда)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КоличествоРазбить = Элементы.Товары.ТекущиеДанные.КоличествоЗЕРНО;
	Оповещение = Новый ОписаниеОповещения("РазбитьСтрокуЗавершение", ЭтаФорма);
	
	ПараметрыРазбиенияСтроки = СобытияФормИСКлиент.ПараметрыРазбиенияСтроки(КоличествоРазбить);
	ПараметрыРазбиенияСтроки.ИмяПоляКоличество = "КоличествоЗЕРНО";
	
	СобытияФормИСКлиент.РазбитьСтрокуТабличнойЧасти(Объект.Товары, Элементы.Товары, ПараметрыРазбиенияСтроки, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура СгенерироватьСерии(Команда)
	
	ОчиститьСообщения();
	
	Результат = СгенерироватьСерииНаСервере();
	
	ИнтеграцияИСКлиент.ОповеститьОбОкончанииЗаполненияСерийВДокументе(Результат.ЗаполнениеЗавершено,
		Результат.СписокОшибок);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерезаполнитьПоОснованию(Команда)
	
	ОбработчикПерезаполненияПоОснованию();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ТранспортныеСредства

&НаКлиентеНаСервереБезКонтекста
Процедура СинхронизироватьОтметкиТранспортныхСредствНаФорме(Форма)
	
	Объект = Форма.Объект;
	
	Для Каждого СтрокаТаблицы Из Форма.ТранспортныеСредства Цикл
		
		СтруктураПоиска = СтруктураПоискаСтрокиТранспортнногоСредства();
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаТаблицы);
		
		СтрокаТаблицы.Отметка = (Объект.ТранспортныеСредства.НайтиСтроки(СтруктураПоиска).Количество() <> 0);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СтруктураПоискаСтрокиТранспортнногоСредства()
	
	СтруктураПоиска = Новый Структура();
	СтруктураПоиска.Вставить("ИдентификаторСтрокиТоваров");
	СтруктураПоиска.Вставить("ТипТранспорта");
	СтруктураПоиска.Вставить("НомерТранспортногоСредства");
	СтруктураПоиска.Вставить("НомерАвтомобильногоКонтейнера");
	
	Возврат СтруктураПоиска;
	
КонецФункции

&НаСервере
Процедура ОчиститьТранспортныеСредстваПоИдентификаторуСтроки(ИдентификаторСтроки)
	
	Отбор = Новый Структура("ИдентификаторСтрокиТоваров", ИдентификаторСтроки);
	СтрокиУдаления = Объект.ТранспортныеСредства.НайтиСтроки(Отбор);
	
	Для Каждого СтрокаТаблицы Из СтрокиУдаления Цикл
		Объект.ТранспортныеСредства.Удалить(СтрокаТаблицы);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьТранспортныеСредстваНаФормеПоИдентификаторуСтроки(ИдентификаторСтроки)
	
	Отбор = Новый Структура("ИдентификаторСтрокиТоваров", ИдентификаторСтроки);
	СтрокиУдаления = ТранспортныеСредства.НайтиСтроки(Отбор);
	
	Для Каждого СтрокаТаблицы Из СтрокиУдаления Цикл
		ТранспортныеСредства.Удалить(СтрокаТаблицы);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ТранспортныеСредстваУстновитьСнятьОтметку(Отметка)
	
	Если Элементы.ТранспортныеСредства.ВыделенныеСтроки.Количество() > 1 Тогда
	
		Для Каждого ИдентификаторСтроки Из Элементы.ТранспортныеСредства.ВыделенныеСтроки Цикл
			ТекущиеДанные = ТранспортныеСредства.НайтиПоИдентификатору(ИдентификаторСтроки);
			ТекущиеДанные.Отметка = Отметка;
			ОбработкаСнятияУстановкиОтметкиТранспортногоСредства(ТекущиеДанные);
		КонецЦикла;
		
	ИначеЕсли Элементы.Товары.ТекущаяСтрока <> Неопределено Тогда
		
		ТекущиеДанные   = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
		СтруктураОтбора = Новый Структура("ИдентификаторСтрокиТоваров", ТекущиеДанные.Идентификатор);
		СтрокиДанных    = ТранспортныеСредства.НайтиСтроки(СтруктураОтбора);
	
		Для Каждого СтрокаТаблицы Из СтрокиДанных Цикл
			СтрокаТаблицы.Отметка = Отметка;
			ОбработкаСнятияУстановкиОтметкиТранспортногоСредства(СтрокаТаблицы);
		КонецЦикла;
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаСнятияУстановкиОтметкиТранспортногоСредства(ТекущиеДанные)
	
	СтруктураПоиска = СтруктураПоискаСтрокиТранспортнногоСредства();
	ЗаполнитьЗначенияСвойств(СтруктураПоиска, ТекущиеДанные);
	
	ПоискСтрок = Объект.ТранспортныеСредства.НайтиСтроки(СтруктураПоиска);
	
	Если ТекущиеДанные.Отметка И ПоискСтрок.Количество() = 0 Тогда
		НоваяСтрока = Объект.ТранспортныеСредства.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущиеДанные);
	ИначеЕсли Не ТекущиеДанные.Отметка И ПоискСтрок.Количество() Тогда
		Для Каждого СтрокаТаблицы Из ПоискСтрок Цикл
			Объект.ТранспортныеСредства.Удалить(СтрокаТаблицы);
		КонецЦикла;
	КонецЕсли;
	
	СтруктураПоиска = Новый Структура("Идентификатор", ТекущиеДанные.ИдентификаторСтрокиТоваров);
	СтрокиТоваров = Объект.Товары.НайтиСтроки(СтруктураПоиска);
	СтрокаТовары  = СтрокиТоваров[0];
	Если СтрокиТоваров.Количество() Тогда
		УстановитьПредставлениеТранспортныхСредствПоСтроке(ЭтаФорма, СтрокаТовары);
	КонецЕсли;
	
	СтруктураПоиска = Новый Структура();
	СтруктураПоиска.Вставить("ИдентификаторСтрокиТоваров", ТекущиеДанные.ИдентификаторСтрокиТоваров);
	
	ОтмеченныеСтроки = Объект.ТранспортныеСредства.НайтиСтроки(СтруктураПоиска);
	ВсеСтроки        = ТранспортныеСредства.НайтиСтроки(СтруктураПоиска);
	
	Если ОтмеченныеСтроки.Количество() Тогда
		СтрокаТовары.СтатусУказанияТранспорта = 2;
	ИначеЕсли ВсеСтроки.Количество() Тогда
		СтрокаТовары.СтатусУказанияТранспорта = 1;
	Иначе
		СтрокаТовары.СтатусУказанияТранспорта = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборТранспортныхСредствЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторСтроки = ДополнительныеПараметры.ИдентификаторСтрокиТоваров;
	ВыбраннаяСтрока     = ДополнительныеПараметры.ВыбраннаяСтрока;
	
	СтруктураОтбора = Новый Структура("ИдентификаторСтрокиТоваров", ИдентификаторСтроки);
	СтрокиДанных = Объект.ТранспортныеСредства.НайтиСтроки(СтруктураОтбора);
	Для Каждого СтрокаТаблицы Из СтрокиДанных Цикл
		Объект.ТранспортныеСредства.Удалить(СтрокаТаблицы);
	КонецЦикла;
	
	СтрокиРасчетаПредставления = Новый Массив;
	
	Для Каждого СтрокаДанных Из Результат.ТранспортныеСредства Цикл
		
		Если Не СтрокаДанных.Отметка Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = Объект.ТранспортныеСредства.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДанных);
		НоваяСтрока.ИдентификаторСтрокиТоваров = ИдентификаторСтроки;
		
		СтрокиРасчетаПредставления.Добавить(НоваяСтрока);
		
	КонецЦикла;
	
	СтрокаТовары = Объект.Товары.НайтиПоИдентификатору(ВыбраннаяСтрока);
	УстановитьПредставлениеТранспортныхСредствПоСтроке(ЭтаФорма, СтрокаТовары);
	СинхронизироватьОтметкиТранспортныхСредствНаФорме(ЭтаФорма);
	РассчитатьСтатусУказанияТранспортныхСредствПоСтроке(СтрокаТовары.Идентификатор);
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьСтатусУказанияТранспортныхСредствПоСтроке(ИдентификаторСтроки = Неопределено)
	
	ИнтеграцияЗЕРНО.РассчитатьСтатусУказанияТранспортныхСредств(Объект, ИдентификаторСтроки);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПредставлениеТранспортныхСредствПоСтроке(Форма, СтрокаТовары)
	
	СтруктураПоиска = Новый Структура("ИдентификаторСтрокиТоваров", СтрокаТовары.Идентификатор);
	СтрокиТранспортныхСредств = Форма.Объект.ТранспортныеСредства.НайтиСтроки(СтруктураПоиска);
	
	ПараметрыФормированияНадписи = ИнтеграцияЗЕРНОКлиентСервер.ПараметрыПредставленияТабличнойЧастиТранспортныеСредства();
	
	ПредставлениеТранспортныхСредств = ИнтеграцияЗЕРНОКлиентСервер.СформироватьНадписьПоДаннымТабличнойЧасти(
		СтрокиТранспортныхСредств,
		ПараметрыФормированияНадписи);
	
	СтрокаТовары.ТранспортПредставление = ПредставлениеТранспортныхСредств;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборТранспортныхСредствПоСтроке()
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	ПараметрыОтбора = Новый Структура;
	Если ТекущиеДанные = Неопределено
		Или Не ЗначениеЗаполнено(ТекущиеДанные.Идентификатор) Тогда
		ПараметрыОтбора.Вставить("ИдентификаторСтрокиТоваров", Строка(Новый УникальныйИдентификатор));
	Иначе
		ПараметрыОтбора.Вставить("ИдентификаторСтрокиТоваров", ТекущиеДанные.Идентификатор);
	КонецЕсли;
	
	Элементы.ТранспортныеСредства.ОтборСтрок = Новый ФиксированнаяСтруктура(ПараметрыОтбора);
	
КонецПроцедуры

&НаКлиенте
Процедура СкопироватьТранспорныеСредстваНаФорма(ИсходныйИдентификаторСтроки, НовыйИдентификаторСтроки)
	
	СтруктураОтбора = Новый Структура("ИдентификаторСтрокиТоваров", ИсходныйИдентификаторСтроки);
	
	Для Каждого СтрокаТаблицы Из ТранспортныеСредства.НайтиСтроки(СтруктураОтбора) Цикл
		
		НоваяСтрока = ТранспортныеСредства.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		НоваяСтрока.ИдентификаторСтрокиТоваров = НовыйИдентификаторСтроки;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура СДИЗНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("МножественныйВыбор", Ложь);
	ПараметрыОткрытия.Вставить("ЗакрыватьПриВыборе", Истина);
	ПараметрыОткрытия.Вставить("Отбор",              СтруктураОтбораСДИЗ(ТекущиеДанные));
	ПараметрыОткрытия.Вставить("Получатель",         Объект.Организация);
	
	ОткрытьФорму("Справочник.СДИЗЗЕРНО.ФормаВыбора",
		ПараметрыОткрытия, Элемент,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Функция СтруктураОтбораСДИЗ(ТекущиеДанные)
	
	СтруктураОтбора = Новый Структура;
	
	СтруктураОтбора.Вставить("Статус", Новый Массив);
	СтруктураОтбора.Статус.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыСДИЗЗЕРНО.Оформлен"));
	СтруктураОтбора.Статус.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыСДИЗЗЕРНО.ОформленИПодтвержден"));
	
	РеквизитыОтбора = ПоляОтбораШапкиСДИЗ();
	
	Для Каждого КлючИЗначение Из РеквизитыОтбора Цикл
		СтруктураОтбора.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Номенклатура) Тогда
		
		Если ЗначениеЗаполнено(ТекущиеДанные.ОКПД2) Тогда
			ЭлементОКПД2 = ТекущиеДанные.ОКПД2;
			ДоступныеОКПД2 = Новый Массив;
			Пока ЭлементОКПД2 <> "" Цикл
				ДоступныеОКПД2.Добавить(ЭлементОКПД2);
				ПозицияТочки = СтрНайти(ЭлементОКПД2, ".", НаправлениеПоиска.СКонца);
				ЭлементОКПД2 = ?(ПозицияТочки > 0, Лев(ЭлементОКПД2, ПозицияТочки - 1), "");
			КонецЦикла;
			СтруктураОтбора.Вставить("ОКПД2", ДоступныеОКПД2);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекущиеДанные.КодТНВЭД) Тогда
			ДоступныеТНВЭД = Новый Массив;
			ДоступныеТНВЭД.Добавить("");
			ДоступныеТНВЭД.Добавить(ТекущиеДанные.КодТНВЭД);
			СтруктураОтбора.Вставить("КодТНВЭД", ДоступныеТНВЭД);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СтруктураОтбора;
	
КонецФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
//	СобытияФормИСПереопределяемый.УстановитьУсловноеОформлениеЕдиницИзмерения(ЭтаФорма);
	СобытияФормИСПереопределяемый.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(ЭтаФорма);
	СобытияФормИСПереопределяемый.УстановитьУсловноеОформлениеСерийНоменклатуры(ЭтаФорма);
	СобытияФормЗЕРНО.УстановитьУсловноеОформлениеКоличестваДляПустойНоменклатуры(ЭтаФорма);
	СобытияФормЗЕРНО.УстановитьУсловноеОформлениеОКПД2(ЭтаФорма);
	
	СтатусыВозможногоАннулирования = Новый СписокЗначений();
	СтатусыВозможногоАннулирования.Добавить(Перечисления.СтатусыОбработкиПогашенияСДИЗЗЕРНО.СДИЗПогашен);
	СтатусыВозможногоАннулирования.Добавить(Перечисления.СтатусыОбработкиПогашенияСДИЗЗЕРНО.СДИЗПогашенЧастично);
	
	//
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыСтатусОбработки.Имя);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных(Элементы.ТоварыСтатусОбработки.ПутьКДанным);
	ОтборЭлемента.ПравоеЗначение = Перечисления.СтатусыОбработкиПогашенияСДИЗЗЕРНО.СДИЗПогашен;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Погашен'"));
	
	//
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыСтатусОбработки.Имя);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных(Элементы.ТоварыСтатусОбработки.ПутьКДанным);
	ОтборЭлемента.ПравоеЗначение = Перечисления.СтатусыОбработкиПогашенияСДИЗЗЕРНО.СДИЗПогашенЧастично;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Погашен частично'"));
	
	//
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыСтатусОбработки.Имя);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных(Элементы.ТоварыСтатусОбработки.ПутьКДанным);
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаНеТребуетВниманияГосИС);
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<Отсутствует>'"));
	
	//
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыТранспортПредставление.Имя);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Объект.Товары.СтатусУказанияТранспорта");
	ОтборЭлемента.ПравоеЗначение = 0;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаНеТребуетВниманияГосИС);
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<Не требуется>'"));
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
	//
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыПредставлениеПартия.Имя);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.Партия");
	ОтборЭлемента.ВидСравнения  = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаНеТребуетВниманияГосИС);
	
	//
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТранспортныеСредства.ПутьКДанным);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных(Элементы.ТранспортныеСредстваОтметка.ПутьКДанным);
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаНеТребуетВниманияГосИС);
	
	СтатусыМожноРедактировать = Новый СписокЗначений();
	СтатусыМожноРедактировать.Добавить(Перечисления.СтатусыОбработкиПогашенияСДИЗЗЕРНО.Черновик);
	СтатусыМожноРедактировать.Добавить(Перечисления.СтатусыОбработкиПогашенияСДИЗЗЕРНО.ПогашениеСДИЗОшибкаПередачи);
	
	//
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыПричинаРасхождения.Имя);
	
	ГруппаУсловий = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаУсловий.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	
	ОтборЭлемента = ГруппаУсловий.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных(Элементы.ТоварыПолноеПогашение.ПутьКДанным);
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	ОтборЭлемента = ГруппаУсловий.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("СтатусЗЕРНО");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.НеВСписке;
	ОтборЭлемента.ПравоеЗначение = СтатусыМожноРедактировать;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииЧтенииНаСервере()
	
	ИнтеграцияЗЕРНО.УстановитьДоступностьПоляСтатус(ЭтаФорма);
	
	ЦветГиперссылки = ЦветаСтиля.ЦветГиперссылкиГосИС;
	
	ПравоИзменения = ПравоДоступа("Изменение", Метаданные.Документы.ПогашениеСДИЗЗЕРНО);
	
	ПараметрыУказанияСерий = ИнтеграцияИС.ПараметрыУказанияСерийФормыОбъекта(Объект, Документы.ПогашениеСДИЗЗЕРНО);
	
	ЕдиницаИзмеренияКилограмм = ИнтеграцияИСКлиентСерверПовтИсп.ЕдиницаИзмеренияКилограмм();
	
	Если Объект.Товары.Количество() Тогда
		ИнициализироватьСлужебныеРеквизитыТоваров();
	КонецЕсли;
	
	ИнтеграцияЗЕРНОКлиентСервер.УстановитьПараметрыВыбораНоменклатуры(ЭтаФорма, Объект.ВидПродукции);
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма);
	
	СерииИспользуются = ИнтеграцияИС.СерииИспользуются();
	Элементы.ТоварыСерия.Видимость = СерииИспользуются;
	Элементы.СгенерироватьСерии.Видимость = СерииИспользуются;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЗависимыеЭлементыФормы(Форма, ИдентификаторСтроки = Неопределено, СписокРеквизитов = "")
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	Инициализация = ПустаяСтрока(СписокРеквизитов);
	СтруктураРеквизитов = Новый Структура(СписокРеквизитов);
	
	Если ИдентификаторСтроки <> Неопределено Тогда
		ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(ИдентификаторСтроки);
	ИначеЕсли Элементы.Товары.ТекущаяСтрока <> Неопределено Тогда
		ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	КонецЕсли;
	
	Если СтруктураРеквизитов.Свойство("ДокументОснование")
		Или СтруктураРеквизитов.Свойство("Операция") Тогда
		ПодключаемыеКомандыИСКлиентСервер.УправлениеВидимостьюКомандПодключенныхКОбъекту(Форма);
	КонецЕсли;
	
	Если СтруктураРеквизитов.Свойство("СтатусЗЕРНО") Тогда
		
		РедактированиеФормыНеДоступно = Форма.СтатусЗЕРНО <> ПредопределенноеЗначение("Перечисление.СтатусыОбработкиПогашенияСДИЗЗЕРНО.Черновик")
		                          И Форма.СтатусЗЕРНО <> ПредопределенноеЗначение("Перечисление.СтатусыОбработкиПогашенияСДИЗЗЕРНО.ПогашениеСДИЗОшибкаПередачи");
		
		Форма.РедактированиеФормыНеДоступно = РедактированиеФормыНеДоступно;
		
		ЗависимыеОтСтатусаЗЕРНО = Новый Массив;
		ЗависимыеОтСтатусаЗЕРНО.Добавить("ГруппаНередактируемыеПослеОтправкиРеквизитыОсновное");
		ЗависимыеОтСтатусаЗЕРНО.Добавить("ГруппаНередактируемыеПослеОтправкиКомандыДанныеОтчета");
		
		ВидимостьСтатусаСтроки = Форма.СтатусЗЕРНО <> ПредопределенноеЗначение("Перечисление.СтатусыОбработкиПогашенияСДИЗЗЕРНО.Черновик")
		                          И Форма.СтатусЗЕРНО <> ПредопределенноеЗначение("Перечисление.СтатусыОбработкиПогашенияСДИЗЗЕРНО.ПогашениеСДИЗАннулировано");
		
		ДоступностьКнопкиАннулирования = Форма.СтатусЗЕРНО = ПредопределенноеЗначение("Перечисление.СтатусыОбработкиПогашенияСДИЗЗЕРНО.СДИЗПогашен")
		                             Или Форма.СтатусЗЕРНО = ПредопределенноеЗначение("Перечисление.СтатусыОбработкиПогашенияСДИЗЗЕРНО.СДИЗПогашенЧастично");
		
		Элементы.ТоварыСтатусОбработки.Видимость             = ВидимостьСтатусаСтроки И Объект.Товары.Количество() > 1;
		Элементы.ТоварыАннулироватьПогашениеСДИЗ.Доступность = ДоступностьКнопкиАннулирования;
		
		Если РедактированиеФормыНеДоступно Тогда
			Элементы.ТоварыСпискомПричинаРасхождения.АвтоОтметкаНезаполненного = Ложь;
			Элементы.ТоварыПричинаРасхождения.АвтоОтметкаНезаполненного        = Ложь;
		Иначе
			Элементы.ТоварыСпискомПричинаРасхождения.АвтоОтметкаНезаполненного = Истина;
			Элементы.ТоварыПричинаРасхождения.АвтоОтметкаНезаполненного        = Истина;
		КонецЕсли;
		
		// Товары.
		Исключения = "ТоварыНайти, ТоварыОтменитьПоиск, ТоварыКонтекстноеМенюКопированиеИВыделение, "
				+ "ТоварыАннулироватьСписаниеПартии, СгенерироватьСерии, ТоварыПерезаполнитьПоОснованию, "
				+ "ТоварыНоменклатура, ТоварыХарактеристика, ТоварыСерия, ТоварыКоличество, СвернутьПанель, РазвернутьПанель, "
				+ "ТранспортныеСредстваТипТранспорта, ТранспортныеСредстваНомерТранспортногоСредства, "
				+ "ТранспортныеСредстваНомерАвтомобильногоКонтейнера, ТоварыТранспортПредставление, ТоварыПредставлениеПартия, "
				+ "ТоварыЕдиницаИзмерения, ТоварыОКПД2, ТоварыКодТНВЭД, ТоварыКоличествоДоступно, ТоварыКоличествоПогашено, ТоварыКоличествоОтказПогашения, ТоварыАннулироватьПогашениеСДИЗ, ";
			
		ИнтеграцияИСКлиентСервер.УстановитьДоступностьЭлементовГруппыФормыРекурсивно(
			Элементы.СтраницаТовары, Не Форма.РедактированиеФормыНеДоступно, Исключения);
		ИнтеграцияИСКлиентСервер.УстановитьДоступностьЭлементовФормы(
			Форма, ЗависимыеОтСтатусаЗЕРНО, Не Форма.РедактированиеФормыНеДоступно);
		
	КонецЕсли;
	
	Если СтруктураРеквизитов.Свойство("ПанельДополнительныеСвойстваСвернута")
		Или Инициализация Тогда
		
		Элементы.ГруппаТоварыСпискомПанель.Видимость = Не Форма.ПанельДополнительныеСвойстваСвернута;
		Элементы.ГруппаТоварыСпискомПанельСвернута.Видимость = Форма.ПанельДополнительныеСвойстваСвернута;
	
	КонецЕсли;
	
	Если Инициализация Или СтруктураРеквизитов.Свойство("ОбновитьСтатусЗЕРНО") Тогда
		
		УстановитьПараметрыОбновленияСтатуса = Форма.Модифицированность И НЕ Инициализация;
		ОбновитьСтатусЗЕРНО(Форма, УстановитьПараметрыОбновленияСтатуса);
		
	КонецЕсли;
	
	Если Инициализация
		Или СтруктураРеквизитов.Свойство("Товары")
		Или СтруктураРеквизитов.Свойство("СтатусЗЕРНО")
		Или СтруктураРеквизитов.Свойство("ОбновитьСтатусЗЕРНО") Тогда
		
		ВидимостьОстаткаКПогашению = (Форма.СтатусЗЕРНО = ПредопределенноеЗначение("Перечисление.СтатусыОбработкиПогашенияСДИЗЗЕРНО.Черновик")
			Или Форма.СтатусЗЕРНО = ПредопределенноеЗначение("Перечисление.СтатусыОбработкиПогашенияСДИЗЗЕРНО.ПогашениеСДИЗКПередаче")
			Или Форма.СтатусЗЕРНО = ПредопределенноеЗначение("Перечисление.СтатусыОбработкиПогашенияСДИЗЗЕРНО.СДИЗПогашен")
			Или Форма.СтатусЗЕРНО = ПредопределенноеЗначение("Перечисление.СтатусыОбработкиПогашенияСДИЗЗЕРНО.СДИЗПогашенЧастично"));
		
		Если ТекущиеДанные = Неопределено Или Не ВидимостьОстаткаКПогашению Тогда
			
			Элементы.ЗаполнитьДоступноеКоличество.РасширеннаяПодсказка.Заголовок = "";
			
		Иначе
			
			Если ТекущиеДанные.СтатусОбработки = ПредопределенноеЗначение("Перечисление.СтатусыОбработкиПогашенияСДИЗЗЕРНО.СДИЗПогашен") Тогда
				ОтображаемоеКоличество = ТекущиеДанные.КоличествоИсходное;
			Иначе
				ОтображаемоеКоличество = ТекущиеДанные.КоличествоДоступно;
			КонецЕсли;
			Элементы.ЗаполнитьДоступноеКоличество.РасширеннаяПодсказка.Заголовок = СтрШаблон(
				НСтр("ru = 'из %1 кг'"), ОтображаемоеКоличество);
			
		КонецЕсли;
		
		Элементы.СгенерироватьСерии.Доступность = Не Форма.РедактированиеФормыНеДоступно;
		Элементы.РазбитьСтроку.Доступность      = Не Форма.РедактированиеФормыНеДоступно;
		
	КонецЕсли;
		
	Если ТекущиеДанные <> Неопределено
		И (Инициализация
			Или СтруктураРеквизитов.Свойство("СтатусЗЕРНО")
			Или СтруктураРеквизитов.Свойство("ОбновитьСтатусЗЕРНО")
			Или СтруктураРеквизитов.Свойство("ПолноеПогашение")
			Или СтруктураРеквизитов.Свойство("Товары")) Тогда
		
		ДоступностьПричины = Ложь;
		
		Если ТекущиеДанные.ПолноеПогашение Тогда
			
			ВсегоКПогашениюПоСДИЗ = ВсегоКПогашениюПоСДИЗ(Форма, ТекущиеДанные);
			Если (ТекущиеДанные.КоличествоДоступно - ВсегоКПогашениюПоСДИЗ) <> 0 Тогда
				ДоступностьПричины = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
		Элементы.ТоварыСпискомПричинаРасхождения.Доступность         = ДоступностьПричины;
		Элементы.ТоварыСпискомОписаниеПричиныРасхождения.Доступность = ДоступностьПричины;
		Если Не Форма.РедактированиеФормыНеДоступно Тогда
			Элементы.ТоварыПричинаРасхождения.ТолькоПросмотр             = Не ДоступностьПричины;
			Элементы.ТоварыОписаниеПричиныРасхождения.ТолькоПросмотр     = Не ДоступностьПричины;
		КонецЕсли;
		
		// Действия с погашением СДИЗ на разную номенклатуру
		Если ЗначениеЗаполнено(ТекущиеДанные.СДИЗ) Тогда
			Отбор = Новый Структура("СДИЗ", ТекущиеДанные.СДИЗ);
			СтрокиСДИЗ = Объект.Товары.НайтиСтроки(Отбор);
			Если СтрокиСДИЗ.Количество() > 1 Тогда
				ВсегоПоСДИЗ = 0;
				Для Каждого СтрокаСДИЗ Из СтрокиСДИЗ Цикл
					ВсегоПоСДИЗ = ВсегоПоСДИЗ + СтрокаСДИЗ.КоличествоЗЕРНО;
					Если ТекущиеДанные.ПолноеПогашение И ТекущиеДанные <> СтрокаСДИЗ Тогда
						СтрокаСДИЗ.ПолноеПогашение = Ложь;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Инициализация
		Или СтруктураРеквизитов.Свойство("Товары") Тогда
		
		ПараметрыФормированияНадписи = ИнтеграцияЗЕРНОКлиентСервер.ПараметрыПредставленияТабличнойЧастиСвязанныхДокументов();
		ПараметрыФормированияНадписи.Уникальные = Истина;
		
		ПредставлениеСвязанныхДокументов = ИнтеграцияЗЕРНОКлиентСервер.СформироватьНадписьПоДаннымТабличнойЧасти(
			Форма.СвязанныеДокументы,
			ПараметрыФормированияНадписи);
		
		Если Форма.СвязанныеДокументы.Количество() Тогда
			Форма.ПредставлениеСвязанныхДокументов = Новый ФорматированнаяСтрока(
				ПредставлениеСвязанныхДокументов,,,, "ОткрытьОбщуюФормуСвязанныеДокументы");
		Иначе
			Форма.ПредставлениеСвязанныхДокументов = Новый ФорматированнаяСтрока(ПредставлениеСвязанныхДокументов);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ВсегоКПогашениюПоСДИЗ(Форма, ТекущиеДанные, БезУчетаТекущейСтроки = Ложь)
	
	ВсегоПоСДИЗ = 0;
	Отбор = Новый Структура("СДИЗ", ТекущиеДанные.СДИЗ);
	СтрокиСДИЗ = Форма.Объект.Товары.НайтиСтроки(Отбор);
	Для Каждого СтрокаТаблицы Из СтрокиСДИЗ Цикл
		Если БезУчетаТекущейСтроки И СтрокаТаблицы = ТекущиеДанные Тогда
			Продолжить;
		КонецЕсли;
		ВсегоПоСДИЗ = ВсегоПоСДИЗ + СтрокаТаблицы.КоличествоЗЕРНО;
	КонецЦикла;
	
	Возврат ВсегоПоСДИЗ;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПослеВыбораОснования(ДанныеВыбора, ДополнительныеПараметры) Экспорт
	
	Если ДанныеВыбора = Элементы.ДокументОснование.ДоступныеТипы.ПривестиЗначение(ДанныеВыбора) Тогда
		Объект.ДокументОснование = ДанныеВыбора;
		Модифицированность       = Истина;
	КонецЕсли;
	
	ЗаполнитьТовары = (ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
		И ДополнительныеПараметры.Свойство("ОбработатьПерезаполнение"));
	Если ЗаполнитьТовары Тогда
		ОбработчикПерезаполненияПоОснованию();
	КонецЕсли;
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма,, "ДокументОснование");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикПерезаполненияПоОснованию()
	
	ОчиститьСообщения();
	
	ПерезаполнитьПоОснованиюСервер();
	
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьПоОснованиюСервер()
	
	ТекущийОбъект = РеквизитФормыВЗначение("Объект");
	ТекущийОбъект.Заполнить(Объект.ДокументОснование);
	
	ЗначениеВРеквизитФормы(ТекущийОбъект, "Объект");
	
	ПриСозданииЧтенииНаСервере();
	ИнтеграцияИСПереопределяемый.ЗаполнитьСтатусыУказанияСерий(Объект, ПараметрыУказанияСерий);
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриЗавершенииОперации(Результат, ДополнительныеПараметры) Экспорт
	
	Прочитать();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораРеквизитаШапкиСДИЗ(Элемент, СтандартнаяОбработка, ВыбранноеЗначение = Неопределено)
	
	ПервыйЗаполненныйСДИЗ = ПервыйЗаполненныйСДИЗ(ЭтаФорма);
	ТекущееЗначение = Объект[Элемент.Имя];
	Если ЗначениеЗаполнено(ПервыйЗаполненныйСДИЗ)
		И (ВыбранноеЗначение <> ТекущееЗначение
			Или ВыбранноеЗначение = Неопределено И ЗначениеЗаполнено(ТекущееЗначение)) Тогда
		
		СтандартнаяОбработка = Ложь;
		ТекстВопроса = СтрШаблон(
			НСтр("ru = 'При изменении поля %1 в табличной части Товары будут очищены СДИЗ. Продолжить?'"),
			Элемент.Заголовок);
		ПараметрыОповещения = Новый Структура();
		ПараметрыОповещения.Вставить("ВыбранноеЗначение", ВыбранноеЗначение);
		ПараметрыОповещения.Вставить("ИмяПоля",           Элемент.Имя);
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ВопросПриИзмененииПоляШапкиСДИЗЗавершении",
			ЭтаФорма,
			ПараметрыОповещения);

		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		
		НастроитьСвязиПараметровВыбораСДИЗ();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПервыйЗаполненныйСДИЗ(Форма)
	
	СДИЗ = Неопределено;
	Для Каждого СтрокаТовары Из Форма.Объект.Товары Цикл
		Если ЗначениеЗаполнено(СтрокаТовары.СДИЗ) Тогда
			СДИЗ = СтрокаТовары.СДИЗ;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СДИЗ;
	
КонецФункции

#Область ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда) Экспорт
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтаФорма, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт 
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтаФорма, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды() Экспорт
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтаФорма, Объект);
	ПодключаемыеКомандыИСКлиентСервер.УправлениеВидимостьюКомандПодключенныхКОбъекту(ЭтаФорма);
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура НоменклатураПриИзменении()
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СохраняемыеДанные = Новый Структура;
	Если ЗначениеЗаполнено(ТекущиеДанные.СДИЗ) Тогда
		СохраняемыеДанные.Вставить("ОКПД2", ТекущиеДанные.ОКПД2);
		СохраняемыеДанные.Вставить("КодТНВЭД", ТекущиеДанные.КодТНВЭД);
	КонецЕсли;
	
	СобытияФормЗЕРНОКлиентПереопределяемый.ПриИзмененииНоменклатуры(
		ЭтаФорма, ТекущиеДанные, КэшированныеЗначения, ПараметрыУказанияСерий);
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, СохраняемыеДанные);
	
	Если ЗначениеЗаполнено(ТекущиеДанные.КоличествоЗЕРНО) Тогда
		КоличествоЗЕРНОПриИзменении(ТекущиеДанные);
	ИначеЕсли ЗначениеЗаполнено(ТекущиеДанные.Количество) Тогда
		КоличествоПриИзменении();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ХарактеристикаПриИзменении()
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	СобытияФормЗЕРНОКлиентПереопределяемый.ПриИзмененииХарактеристики(ЭтаФорма, ТекущиеДанные, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура СерияПриИзменении()
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СобытияФормЗЕРНОКлиентПереопределяемый.ПриИзмененииСерии(
		ЭтаФорма, Элементы.Товары.ТекущиеДанные, КэшированныеЗначения, ПараметрыУказанияСерий);
	
КонецПроцедуры

&НаСервере
Функция СгенерироватьСерииНаСервере()
	
	Результат = ИнтеграцияИС.СгенерироватьСерии(
		Объект, Объект.Товары, Элементы.Товары.ВыделенныеСтроки, ПараметрыУказанияСерий);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура КоличествоПриИзменении()
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СобытияФормЗЕРНОКлиентПереопределяемый.ПриИзмененииКоличества(
		ЭтаФорма, ТекущиеДанные, ЕдиницаИзмеренияКилограмм, КэшированныеЗначения);
	
	Если ТекущиеДанные.КоличествоЗЕРНО >= ТекущиеДанные.КоличествоДоступно Тогда
		ТекущиеДанные.ПолноеПогашение = Истина;
	КонецЕсли;
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма, Элементы.Товары.ТекущаяСтрока, "КоличествоЗЕРНО,ПолноеПогашение");
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоЗЕРНОПриИзменении(ТекущиеДанные)
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СобытияФормЗЕРНОКлиентПереопределяемый.ПриИзмененииКоличестваВКилограммах(
		ЭтаФорма, ТекущиеДанные, ЕдиницаИзмеренияКилограмм, КэшированныеЗначения);
	
	ВсегоКПогашениюПоСДИЗ = ВсегоКПогашениюПоСДИЗ(ЭтаФорма, ТекущиеДанные);
	Если ВсегоКПогашениюПоСДИЗ >= ТекущиеДанные.КоличествоДоступно Тогда
		ТекущиеДанные.ПолноеПогашение = Истина;
	КонецЕсли;
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма, Элементы.Товары.ТекущаяСтрока, "КоличествоЗЕРНО,ПолноеПогашение");
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииРеквизитаШапкиСДИЗ(ИмяРеквизита)
	ЗаполнитьГиперссылкиРеквизитов(ИмяРеквизита);
	НастроитьСвязиПараметровВыбораСДИЗ();
КонецПроцедуры

&НаСервере
Процедура СДИЗПриИзмененииНаСервере()
	
	Если Элементы.Товары.ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	
	ОчиститьТранспортныеСредстваПоИдентификаторуСтроки(ТекущиеДанные.Идентификатор);
	
	ДанныеПоСДИЗам = Справочники.СДИЗЗЕРНО.ДанныеПоСДИЗ(Объект.Товары.Выгрузить().ВыгрузитьКолонку("СДИЗ"));
	
	СДИЗЗаполненияШапки = Неопределено;
	Если ЗначениеЗаполнено(ТекущиеДанные.СДИЗ) Тогда
		СДИЗЗаполненияШапки = ТекущиеДанные.СДИЗ;
	Иначе
		Для Каждого СтрокаТовары Из Объект.Товары Цикл
			Если ЗначениеЗаполнено(СтрокаТовары.СДИЗ) Тогда
				СДИЗЗаполненияШапки = СтрокаТовары.СДИЗ;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ЗаполнитьДанныеШапкиПоСДИЗ(СДИЗЗаполненияШапки, ДанныеПоСДИЗам);
	ЗаполнитьДанныеСтрокиПоСДИЗ(ТекущиеДанные, ДанныеПоСДИЗам);
	ЗаполнитьСвязанныеДокументы(ДанныеПоСДИЗам);
	ЗаполнитьГиперссылкиРеквизитов();
	
	ТаблицаОКПД2 = ИнтеграцияЗЕРНО.НаименованияКодовОКПД2ПоТабличнойЧасти(Объект.Товары);
	
	ДанныеОКПД2 = ТаблицаОКПД2.Найти(ТекущиеДанные.ОКПД2);
	Если ДанныеОКПД2 = Неопределено Тогда
		ТекущиеДанные.ОКПД2Представление = Неопределено;
	Иначе
		ТекущиеДанные.ОКПД2Представление = ИнтеграцияЗЕРНОКлиентСервер.ПредставлениеОКПД2(
			ДанныеОКПД2.Наименование, ДанныеОКПД2.Идентификатор);
	КонецЕсли;
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма, Элементы.Товары.ТекущаяСтрока, "Товары");
	НастроитьСвязиПараметровВыбораСДИЗ();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеШапкиПоСДИЗ(СДИЗ, ДанныеПоСДИЗам)
	
	Если ЗначениеЗаполнено(СДИЗ) Тогда
	
		ДанныеПоСДИЗ = ДанныеПоСДИЗам[СДИЗ];
		
		Если ДанныеПоСДИЗ = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Объект.Грузоотправитель = ДанныеПоСДИЗ.Грузоотправитель;
		Объект.Грузополучатель  = ДанныеПоСДИЗ.Грузополучатель;
		Объект.Продавец         = ДанныеПоСДИЗ.Продавец;
		Объект.Покупатель       = ДанныеПоСДИЗ.Покупатель;
		Объект.ПунктОтправления = ДанныеПоСДИЗ.ПунктОтправления;
		Объект.ПунктНазначения  = ДанныеПоСДИЗ.ПунктНазначения;
	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСвязанныеДокументы(ДанныеПоСДИЗам)
	
	СвязанныеДокументы.Очистить();
	
	СтруктураПоиска = Новый Структура();
	СтруктураПоиска.Вставить("ПервичныйДокумент");
	
	Для Каждого КлючИЗначение Из ДанныеПоСДИЗам Цикл
		
		ДанныеСДИЗ = КлючИЗначение.Значение;
		
		Для Каждого СтрокаСвязанныйДокумент Из ДанныеСДИЗ.СвязанныеДокументы Цикл
			
			ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаСвязанныйДокумент);
			
			ПоискСтрок = СвязанныеДокументы.НайтиСтроки(СтруктураПоиска);
			
			Если ПоискСтрок.Количество() = 0 Тогда
				НоваяСтрока = СвязанныеДокументы.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаСвязанныйДокумент);
			КонецЕсли;
			
		КонецЦикла;
		
		Для Каждого СтрокаСвязанныйДокумент Из ДанныеСДИЗ.СвязанныеДокументыПрочие Цикл
			
			ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаСвязанныйДокумент);
			
			ПоискСтрок = СвязанныеДокументы.НайтиСтроки(СтруктураПоиска);
			
			Если ПоискСтрок.Количество() = 0 Тогда
				НоваяСтрока = СвязанныеДокументы.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаСвязанныйДокумент);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеСтрокиПоСДИЗ(ТекущиеДанные, ДанныеПоСДИЗам)
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.КоличествоДоступно       = 0;
	ТекущиеДанные.КоличествоПогашено       = 0;
	ТекущиеДанные.КоличествоОтказПогашения = 0;
	ТекущиеДанные.КоличествоИсходное       = 0;
	ТекущиеДанные.КодТНВЭД                 = "";
	ТекущиеДанные.ОКПД2                    = "";
	
	ОчиститьТранспортныеСредстваНаФормеПоИдентификаторуСтроки(ТекущиеДанные.Идентификатор);
	
	Если ЗначениеЗаполнено(ТекущиеДанные.СДИЗ) Тогда
		
		ДанныеСДИЗ = ДанныеПоСДИЗам[ТекущиеДанные.СДИЗ];
		
		ТекущиеДанные.ОКПД2                    = ДанныеСДИЗ.ОКПД2;
		ТекущиеДанные.КодТНВЭД                 = ДанныеСДИЗ.КодТНВЭД;
		ТекущиеДанные.КоличествоДоступно       = ДанныеСДИЗ.КоличествоДоступно;
		ТекущиеДанные.КоличествоИсходное       = ДанныеСДИЗ.Количество;
		ТекущиеДанные.КоличествоПогашено       = ДанныеСДИЗ.КоличествоПогашено;
		ТекущиеДанные.КоличествоОтказПогашения = ДанныеСДИЗ.КоличествоОтказПогашения;
		
		Для Каждого СтрокаТаблицы Из ДанныеСДИЗ.ТранспортныеСредства Цикл
			
			НоваяСтрока = ТранспортныеСредства.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.ИдентификаторСтрокиТоваров = ТекущиеДанные.Идентификатор;
			
		КонецЦикла;
		
		СинхронизироватьОтметкиТранспортныхСредствНаФорме(ЭтаФорма);
		
		Если Не ЗначениеЗаполнено(ТекущиеДанные.Номенклатура) Тогда
			ТекущиеДанные.Номенклатура = ДанныеСДИЗ.Номенклатура;
			ТекущиеДанные.Характеристика = ДанныеСДИЗ.Характеристика;
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьПредставлениеТранспортныхСредствПоСтроке(ЭтаФорма, ТекущиеДанные);
	
	ИнтеграцияЗЕРНО.РассчитатьСтатусУказанияТранспортныхСредств(Объект, ТекущиеДанные.Идентификатор);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПредставленияНаФорме(Прочитать = Ложь)
	
	Если Прочитать Тогда
		Прочитать();
	Иначе
		ОбновитьСтатусЗЕРНО(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусПредставлениеОбработкаНавигационнойСсылкиЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если Не РезультатВопроса = КодВозвратаДиалога.Да Тогда
		 Возврат;
	КонецЕсли;
	
	Если ПроверитьЗаполнение() Тогда
		Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
		РазблокироватьДанныеФормыДляРедактирования();
	КонецЕсли;
	
	Если Не Модифицированность И Объект.Проведен Тогда
		ОбработатьНажатиеНавигационнойСсылки(ДополнительныеПараметры.НавигационнаяСсылкаФорматированнойСтроки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросАннулированиеПогашенияСДИЗЗавершение(РезультатВопроса, СтрокиДляСписания) Экспорт
	
	Если Не РезультатВопроса = КодВозвратаДиалога.Да Тогда
		 Возврат;
	КонецЕсли;
	
	Если ПроверитьЗаполнение() Тогда
		Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
		РазблокироватьДанныеФормыДляРедактирования();
	КонецЕсли;
	
	Если Не Модифицированность И Объект.Проведен Тогда
		ПодтверждениеАннулированияПогашенияСДИЗЗавершение(РезультатВопроса, СтрокиДляСписания);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодтверждениеАннулированияПогашенияСДИЗЗавершение(РезультатВопроса, СтрокиДляСписания) Экспорт
	
	Если Не РезультатВопроса = КодВозвратаДиалога.Да Тогда
		 Возврат;
	КонецЕсли;
	
	ПараметрыПередачи = ИнтеграцияЗЕРНОСлужебныйКлиентСервер.ПараметрыОбработкиСообщений();
	ПараметрыПередачи.Ссылка             = Объект.Ссылка;
	ПараметрыПередачи.Организация        = Объект.Организация;
	ПараметрыПередачи.Подразделение      = Объект.Подразделение;
	ПараметрыПередачи.ДальнейшееДействие = ПредопределенноеЗначение("Перечисление.ДальнейшиеДействияПоВзаимодействиюЗЕРНО.АннулируйтеОперацию");
	
	ПараметрыПередачи.ПараметрыЗапроса = Новый Массив;
	Для Каждого СтрокаТаблицы Из СтрокиДляСписания Цикл
		ПараметрыПередачи.ПараметрыЗапроса.Добавить(СтрокаТаблицы.Идентификатор);
	КонецЦикла;
	
	ОписаниеПриЗавершении = Новый ОписаниеОповещения(
		"Подключаемый_ПриЗавершенииОперации", ЭтаФорма, ПараметрыПередачи);
	
	ИнтеграцияЗЕРНОКлиент.ПодготовитьКПередаче(ЭтаФорма, ПараметрыПередачи, ОписаниеПриЗавершении);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьНажатиеНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ПередатьДанные" Тогда
		
		ПараметрыПередачи = ИнтеграцияЗЕРНОСлужебныйКлиентСервер.ПараметрыОбработкиСообщений();
		ПараметрыПередачи.Ссылка        = Объект.Ссылка;
		ПараметрыПередачи.Организация   = Объект.Организация;
		ПараметрыПередачи.Подразделение = Объект.Подразделение;
		
		ОписаниеПриЗавершении = Новый ОписаниеОповещения(
			"Подключаемый_ПриЗавершенииОперации", ЭтаФорма, ПараметрыПередачи);
		
		ИнтеграцияЗЕРНОКлиент.ПодготовитьКПередаче(ЭтаФорма, ПараметрыПередачи, ОписаниеПриЗавершении);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОтменитьОперацию" Тогда
		
		ИнтеграцияЗЕРНОКлиент.ОтменитьПоследнююОперацию(Объект.Ссылка);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОтменитьПередачуДанных" Тогда
		
		ИнтеграцияЗЕРНОКлиент.ОтменитьПередачу(Объект.Ссылка);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ПоказатьПричинуОшибки" Тогда
		
		ПараметрыОткрытияФормы = Новый Структура;
		ПараметрыОткрытияФормы.Вставить("Документ", Объект.Ссылка);
		
		ОткрытьФорму(
			"Справочник.ЗЕРНОПрисоединенныеФайлы.Форма.ФормаОшибки",
			ПараметрыОткрытияФормы,
			ЭтаФорма);
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПриИзмененииОрганизацииПриЗавершении(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		Объект.Организация = ДополнительныеПараметры;
		Объект.Товары.Очистить();
		Объект.ТранспортныеСредства.Очистить();
		Модифицированность = Истина;
		НастроитьЗависимыеЭлементыФормы(ЭтаФорма,, "Организация");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПриИзмененииВидПродукцииПриЗавершении(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		Объект.ВидПродукции = ДополнительныеПараметры;
		Объект.Товары.Очистить();
		Объект.ТранспортныеСредства.Очистить();
		Модифицированность = Истина;
		НастроитьЗависимыеЭлементыФормы(ЭтаФорма,, "ВидПродукции");
		
		ИнтеграцияЗЕРНОКлиентСервер.УстановитьПараметрыВыбораНоменклатуры(ЭтаФорма, Объект.ВидПродукции);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПриИзмененииПоляШапкиСДИЗЗавершении(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		Объект[ДополнительныеПараметры.ИмяПоля] = ДополнительныеПараметры.ВыбранноеЗначение;
		
		Для Каждого СтрокаТовары Из Объект.Товары Цикл
			СтрокаТовары.СДИЗ = Неопределено;
		КонецЦикла;
		Модифицированность = Истина;
		НастроитьЗависимыеЭлементыФормы(ЭтаФорма, Элементы.Товары.ТекущаяСтрока, "Товары");
		
	КонецЕсли;
	
КонецПроцедуры

#Область Статус

&НаСервере
Процедура ОбновитьЗаписатьПараметрыОбновленияСтатуса(Отказ, ТекущийОбъект)
	
	Если ПараметрыОбновленияСтатуса = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РегистрыСведений.СтатусыОбъектовСинхронизацииЗЕРНО.ОбновитьСтатус(
		ТекущийОбъект.Ссылка,
		ПараметрыОбновленияСтатуса);
	
	ПараметрыОбновленияСтатуса = Неопределено;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьСтатусЗЕРНО(Форма, УстановитьПараметрыОбновленияСтатуса = Ложь)
	
	Объект = Форма.Объект;
	
	ПараметрыСтатуса = ПараметрыСтатусаДокумента(Объект);
	
	Форма.СтатусЗЕРНО                   = ПараметрыСтатуса.СтатусЗЕРНО;
	Форма.СтатусЗЕРНОПредставление      = ПараметрыСтатуса.СтатусЗЕРНОПредставление;
	Форма.РедактированиеФормыНеДоступно = ПараметрыСтатуса.РедактированиеФормыНеДоступно;
	
	Для Каждого СтрокаТовары Из Объект.Товары Цикл
		
		СтрокаТовары.ДальнейшееДействие = Неопределено;
		ДанныеПоСтроке = ПараметрыСтатуса.СтатусыПоСтрокам[СтрокаТовары.Идентификатор];
		Если ДанныеПоСтроке = Неопределено Тогда
			СтрокаТовары.СтатусОбработки = ПредопределенноеЗначение("Перечисление.СтатусыОбработкиПогашенияСДИЗЗЕРНО.Черновик");
		Иначе
			СтрокаТовары.СтатусОбработки = ДанныеПоСтроке.СтатусЗЕРНО;
		КонецЕсли;
		
	КонецЦикла;
	
	НастроитьЗависимыеЭлементыФормы(Форма,, "СтатусЗЕРНО");
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПараметрыСтатусаДокумента(ДокументОбъект)
	
	Результат = Новый Структура;
	Результат.Вставить("СтатусЗЕРНО");
	Результат.Вставить("СтатусЗЕРНОПредставление");
	Результат.Вставить("РедактированиеФормыНеДоступно", Ложь);
	Результат.Вставить("СтатусыПоСтрокам", Новый Соответствие());
	
	Ссылка             = ДокументОбъект.Ссылка;
	МенеджерОбъекта    = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Ссылка);
	СтатусЗЕРНО        = МенеджерОбъекта.СтатусПоУмолчанию();
	ДальнейшееДействие = МенеджерОбъекта.ДальнейшееДействиеПоУмолчанию();
	
	ДопустимыеДействия = Новый Массив;
	ДопустимыеДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЗЕРНО.ПередайтеДанные);
	ДопустимыеДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЗЕРНО.ОтменитеОперацию);
	ДопустимыеДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЗЕРНО.ОтменитеПередачуДанных);
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Статусы.Статус              КАК Статус,
		|	Статусы.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ВЫБОР
		|		КОГДА Статусы.ДальнейшееДействие1 В (&МассивДальнейшиеДействия)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюЗЕРНО.НеТребуется)
		|		ИНАЧЕ Статусы.ДальнейшееДействие1
		|	КОНЕЦ КАК ДальнейшееДействие1,
		|	ВЫБОР
		|		КОГДА Статусы.ДальнейшееДействие2 В (&МассивДальнейшиеДействия)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюЗЕРНО.НеТребуется)
		|		ИНАЧЕ Статусы.ДальнейшееДействие2
		|	КОНЕЦ КАК ДальнейшееДействие2,
		|	ВЫБОР
		|		КОГДА Статусы.ДальнейшееДействие3 В (&МассивДальнейшиеДействия)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюЗЕРНО.НеТребуется)
		|		ИНАЧЕ Статусы.ДальнейшееДействие3
		|	КОНЕЦ КАК ДальнейшееДействие3
		|ИЗ
		|	РегистрСведений.СтатусыОбъектовСинхронизацииЗЕРНО КАК Статусы
		|ГДЕ
		|	Статусы.ОбъектСинхронизации = &Документ
		|УПОРЯДОЧИТЬ ПО
		|	Статусы.ИдентификаторСтроки");
		
		Запрос.УстановитьПараметр("Документ",                 Ссылка);
		Запрос.УстановитьПараметр("МассивДальнейшиеДействия", ИнтеграцияЗЕРНО.НеотображаемыеВДокументахДальнейшиеДействия());
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Если ЗначениеЗаполнено(Выборка.ИдентификаторСтроки) Тогда
				
				ДанныеПоСтроке = Новый Структура;
				ДанныеПоСтроке.Вставить("СтатусЗЕРНО", Выборка.Статус);
				Результат.СтатусыПоСтрокам.Вставить(Выборка.ИдентификаторСтроки, ДанныеПоСтроке);
				
			Иначе
				
				СтатусЗЕРНО = Выборка.Статус;
				
				ДальнейшееДействие = Новый Массив;
				ДальнейшееДействие.Добавить(Выборка.ДальнейшееДействие1);
				ДальнейшееДействие.Добавить(Выборка.ДальнейшееДействие2);
				ДальнейшееДействие.Добавить(Выборка.ДальнейшееДействие3);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	СтатусЗЕРНОПредставление = ИнтеграцияЗЕРНО.ПредставлениеСтатуса(СтатусЗЕРНО, ДальнейшееДействие, ДопустимыеДействия);
	
	РедактированиеФормыНеДоступно = СтатусЗЕРНО <> Перечисления.СтатусыОбработкиПогашенияСДИЗЗЕРНО.Черновик
	                              И СтатусЗЕРНО <> Перечисления.СтатусыОбработкиПогашенияСДИЗЗЕРНО.ПогашениеСДИЗОшибкаПередачи
	                              И СтатусЗЕРНО <> Перечисления.СтатусыОбработкиПогашенияСДИЗЗЕРНО.АннулированиеПогашенияОшибкаПередачи;
	
	Результат.СтатусЗЕРНО                   = СтатусЗЕРНО;
	Результат.СтатусЗЕРНОПредставление      = СтатусЗЕРНОПредставление;
	Результат.РедактированиеФормыНеДоступно = РедактированиеФормыНеДоступно;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

&НаСервере
Процедура ИнициализироватьСлужебныеРеквизитыТоваров()
	
	ИнтеграцияИСПереопределяемый.ЗаполнитьСлужебныеРеквизитыВКоллекции(ЭтаФорма, Объект.Товары);
	
	ДанныеПоСДИЗ = Справочники.СДИЗЗЕРНО.ДанныеПоСДИЗ(Объект.Товары.Выгрузить().ВыгрузитьКолонку("СДИЗ"));
	
	Для Каждого ТекущаяСтрока Из Объект.Товары Цикл
		ЗаполнитьДанныеСтрокиПоСДИЗ(ТекущаяСтрока, ДанныеПоСДИЗ);
	КонецЦикла;
	
	ЗаполнитьСвязанныеДокументы(ДанныеПоСДИЗ);
	
	ТаблицаОКПД2 = ИнтеграцияЗЕРНО.НаименованияКодовОКПД2ПоТабличнойЧасти(Объект.Товары);
	
	Для Каждого ТекущаяСтрока Из Объект.Товары Цикл
		
		ДанныеОКПД2 = ТаблицаОКПД2.Найти(ТекущаяСтрока.ОКПД2);
		Если ДанныеОКПД2 <> Неопределено Тогда
			ТекущаяСтрока.ОКПД2Представление = ИнтеграцияЗЕРНОКлиентСервер.ПредставлениеОКПД2(
				ДанныеОКПД2.Наименование, ДанныеОКПД2.Идентификатор);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекущаяСтрока.Партия) Тогда
			ТекущаяСтрока.ПредставлениеПартия = Новый ФорматированнаяСтрока(
				Строка(ТекущаяСтрока.Партия),,,, ПолучитьНавигационнуюСсылку(ТекущаяСтрока.Партия));
		Иначе
			ТекущаяСтрока.ПредставлениеПартия = Новый ФорматированнаяСтрока(
				НСтр("ru = '<будет создана автоматически>'"),, ЦветТекстаПоля("ТекстЗапрещеннойЯчейкиЦвет"));
		КонецЕсли;
		
		УстановитьПредставлениеТранспортныхСредствПоСтроке(ЭтаФорма, ТекущаяСтрока);
		
	КонецЦикла;
	
	ЗаполнитьГиперссылкиРеквизитов();
	НастроитьСвязиПараметровВыбораСДИЗ();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьСвязиПараметровВыбораСДИЗ()
	
	ВсеСвязи = Новый Массив();
	
	РеквизитыОтбора = ПоляОтбораШапкиСДИЗ();
	
	Для Каждого КлючИЗначение Из РеквизитыОтбора Цикл
		НоваяСвязь = Новый СвязьПараметраВыбора(
			СтрШаблон("Отбор.%1", КлючИЗначение.Ключ),
			СтрШаблон("Объект.%1", КлючИЗначение.Ключ));
		ВсеСвязи.Добавить(НоваяСвязь);
	КонецЦикла;
	
	Элементы.ТоварыСДИЗ.СвязиПараметровВыбора        = Новый ФиксированныйМассив(ВсеСвязи);
	Элементы.ТоварыСпискомСДИЗ.СвязиПараметровВыбора = Новый ФиксированныйМассив(ВсеСвязи);
	
КонецПроцедуры

&НаСервере
Функция ПоляОтбораШапкиСДИЗ()
	
	ВозвращаемоеЗначение  = Новый Структура();
	ПервыйЗаполненныйСДИЗ = ПервыйЗаполненныйСДИЗ(ЭтаФорма);
	
	РеквизитыСвязей = Новый Массив();
	РеквизитыСвязей.Добавить("Грузоотправитель");
	РеквизитыСвязей.Добавить("Продавец");
	РеквизитыСвязей.Добавить("ПунктОтправления");
	РеквизитыСвязей.Добавить("Грузополучатель");
	РеквизитыСвязей.Добавить("Покупатель");
	РеквизитыСвязей.Добавить("ПунктНазначения");
	
	Для Каждого РеквизитСвязи Из РеквизитыСвязей Цикл
		Если Не ЗначениеЗаполнено(ПервыйЗаполненныйСДИЗ)
			И Не ЗначениеЗаполнено(Объект[РеквизитСвязи]) Тогда
			Продолжить;
		КонецЕсли;
		ВозвращаемоеЗначение.Вставить(РеквизитСвязи, Объект[РеквизитСвязи]);
	КонецЦикла;
	
	ВозвращаемоеЗначение.Вставить("ВидПродукции", Объект.ВидПродукции);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьГиперссылкиРеквизитов(ИмяРеквизита = Неопределено)
	
	ПараметрыОбработкиПредставлений = Новый Массив();
	
	Если ИмяРеквизита = Неопределено Тогда
		ПараметрыОбработкиПредставлений.Добавить("Грузоотправитель");
		ПараметрыОбработкиПредставлений.Добавить("ПунктОтправления");
		ПараметрыОбработкиПредставлений.Добавить("Продавец");
		ПараметрыОбработкиПредставлений.Добавить("Грузополучатель");
		ПараметрыОбработкиПредставлений.Добавить("Покупатель");
		ПараметрыОбработкиПредставлений.Добавить("ПунктНазначения");
	Иначе
		ПараметрыОбработкиПредставлений.Добавить(ИмяРеквизита);
	КонецЕсли;
	
	КлючиОрганизаций = Новый Соответствие();
	КлючиАдресов     = Новый Соответствие();
	Для Каждого ИмяРеквизита Из ПараметрыОбработкиПредставлений Цикл
		ЗначениеРеквизита = Объект[ИмяРеквизита];
		Если Не ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
			Продолжить;
		КонецЕсли;
		Если ТипЗнч(ЗначениеРеквизита) = Тип("СправочникСсылка.КлючиРеквизитовОрганизацийЗЕРНО") Тогда
			КлючиОрганизаций.Вставить(
				ЗначениеРеквизита,
				ИнтеграцияЗЕРНОКлиентСервер.ПараметрыФормированияПредставленияСопоставления());
		ИначеЕсли ТипЗнч(ЗначениеРеквизита) = Тип("СправочникСсылка.КлючиАдресовЗЕРНО") Тогда
			КлючиАдресов.Вставить(
				ЗначениеРеквизита,
				ИнтеграцияЗЕРНОКлиентСервер.ПараметрыФормированияПредставленияСопоставления(
					ИмяРеквизита = "ПунктНазначения"));
		КонецЕсли;
	КонецЦикла;
	
	ПредставленияСопоставлений = ИнтеграцияЗЕРНОВызовСервера.ПредставленияСопоставлений(КлючиОрганизаций, КлючиАдресов);
	
	Для Каждого ИмяРеквизита Из ПараметрыОбработкиПредставлений Цикл
		
		ЗначениеРеквизита = Объект[ИмяРеквизита];
		
		Если ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
			ДанныеПредставления = ПредставленияСопоставлений[ЗначениеРеквизита];
			Если ДанныеПредставления <> Неопределено Тогда
				Представление = ДанныеПредставления.Представление
			КонецЕсли;
		Иначе
			Представление = Неопределено;
		КонецЕсли;
		
		ЭтаФорма["Надпись" + ИмяРеквизита] = Представление
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЦветТекстаПоля(ИмяЦвета = "ЦветОсобогоТекста")
	
	#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
		Возврат ЦветаСтиля[ИмяЦвета];
	#Иначе
		Возврат ОбщегоНазначенияКлиент.ЦветСтиля(ИмяЦвета);
	#КонецЕсли
	
КонецФункции

&НаСервере
Процедура ЗаполнитьДоступныеВидыПродукции()
	
	ВидыПродукции = ИнтеграцияЗЕРНОКлиентСерверПовтИсп.УчитываемыеВидыПродукции();
	
	Элементы.ВидПродукции.СписокВыбора.Очистить();
	Для Каждого ВидПродукции Из ВидыПродукции Цикл
		Элементы.ВидПродукции.СписокВыбора.Добавить(ВидПродукции, Строка(ВидПродукции));
	КонецЦикла;
	Элементы.ВидПродукции.СписокВыбора.СортироватьПоПредставлению();
	
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ВыполнитьОбменОбработкаОжидания()
	
	ИнтеграцияЗЕРНОСлужебныйКлиент.ПродолжитьВыполнениеОбмена(ЭтаФорма, Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработкаРезультатаПодбораНоменклатуры(Результат, ДополнительныеПараметры) Экспорт
	
	ОбработкаРезультатаПодбораНоменклатуры(Результат, КэшированныеЗначения);
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаРезультатаПодбораНоменклатуры(ВыбранноеЗначение, КэшированныеЗначения)
	
	ПараметрыЗаполнения = ИнтеграцияЗЕРНО.ПараметрыЗаполненияТабличнойЧастиТовары();
	ПараметрыЗаполнения.Вставить("ПараметрыУказанияСерий", ПараметрыУказанияСерий);
	
	ДобавленныеСтроки = Новый Массив;
	
	СобытияФормЗЕРНОПереопределяемый.ОбработкаРезультатаПодбораНоменклатуры(
		ЭтаФорма, ВыбранноеЗначение, ПараметрыЗаполнения, КэшированныеЗначения, ДобавленныеСтроки);
	
	ИнтеграцияИСПереопределяемый.ЗаполнитьСлужебныеРеквизитыВКоллекции(ЭтаФорма, Объект.Товары);
	ИнтеграцияИСПереопределяемый.ЗаполнитьСтатусыУказанияСерий(Объект, ПараметрыУказанияСерий);
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма, Элементы.Товары.ТекущаяСтрока, "ТоварыНоменклатура");
	
КонецПроцедуры

&НаКлиенте
Процедура РазбитьСтрокуЗавершение(НоваяСтрока, ДополнительныеПараметры) Экспорт
	
	Если НоваяСтрока <> Неопределено Тогда
		
		ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
		НоваяСтрока.Идентификатор = Строка(Новый УникальныйИдентификатор);
		
		КоличествоЗЕРНОПриИзменении(НоваяСтрока);
		КоличествоЗЕРНОПриИзменении(ТекущиеДанные);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

