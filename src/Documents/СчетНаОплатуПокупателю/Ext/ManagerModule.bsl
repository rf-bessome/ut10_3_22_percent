#Область СлужебныйПрограммныйИнтерфейс

#Область СчетНаОплату_1_01

// Возвращает данные для формирования электронного Счета на оплату.
//
// Параметры:
//	МассивОбъектов - Массив из ДокументСсылка.СчетНаОплатуКлиенту - ссылки на документы, по которым необходимо
//																	получить данные.
//
// Возвращаемое значение:
//	Структура - коллекция данных, используемая для формирования электронного Счета на оплату, содержащая следующие свойства:
//		* РезультатПоШапке - РезультатЗапроса - общие данные документа.
//		* РезультатПоЭтапамОплаты - РезультатЗапроса - данные графика оплаты по документу.
//		* РезультатПоТабличнойЧасти - РезультатЗапроса - данные табличной части документа.
//
Функция ДанныеДокументовДляСчетаНаОплату_1_01(МассивОбъектов) Экспорт
	
	ДопКолонка = Константы.ДополнительнаяКолонкаПечатныхФормДокументов.Получить();
	Если ДопКолонка = Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул Тогда
		КолонкаКодов = "Артикул";
	Иначе
		КолонкаКодов = "Код";
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаДанныхДокументовДляСчетаНаОплату();
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("КолонкаКодов",   КолонкаКодов);
	
	ПакетРезультатаЗапроса    = Запрос.ВыполнитьПакет();
	МаксимальныйИндексПакета  = ПакетРезультатаЗапроса.ВГраница();
	РезультатПоШапке          = ПакетРезультатаЗапроса[МаксимальныйИндексПакета - 1];
	РезультатПоТабличнойЧасти = ПакетРезультатаЗапроса[МаксимальныйИндексПакета];
	
	ДанныеДокументов = Новый Структура;
	ДанныеДокументов.Вставить("РезультатПоШапке",          РезультатПоШапке);
	ДанныеДокументов.Вставить("РезультатПоТабличнойЧасти", РезультатПоТабличнойЧасти);
	
	Возврат ДанныеДокументов;
	
КонецФункции

Функция ТекстЗапросаДанныхДокументовДляСчетаНаОплату()
	
	ТекстыЗапроса = Новый Массив;
	ТекстыЗапроса.Добавить(ТекстЗапросаДанныеШапкиСчетаНаОплату_1_01());
	ТекстыЗапроса.Добавить(ТекстЗапросаДанныеТоваровСчетаНаОплату_1_01());
	
	ТекстЗапроса = СтрСоединить(ТекстыЗапроса, УчетНДС.РазделительПакетаЗапросов());
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДанныеШапкиСчетаНаОплату_1_01()

	ТекстЗапроса = "ВЫБРАТЬ
	               |	СчетНаОплатуПокупателю.Ссылка,
	               |	СчетНаОплатуПокупателю.Номер КАК НомерДокумента,
	               |	СчетНаОплатуПокупателю.Дата КАК ДатаДокумента,
	               |	СчетНаОплатуПокупателю.Организация КАК Организация,
	               |	ВЫРАЗИТЬ(СчетНаОплатуПокупателю.СтруктурнаяЕдиница КАК Справочник.БанковскиеСчета) КАК БанковскийСчет,
	               |	СчетНаОплатуПокупателю.Контрагент,
	               |	СчетНаОплатуПокупателю.ВалютаДокумента КАК Валюта,
	               |	СчетНаОплатуПокупателю.ДатаОтгрузки КАК ДатаНачалаПоставки,
	               |	СчетНаОплатуПокупателю.Грузоотправитель,
	               |	СчетНаОплатуПокупателю.Грузополучатель,
	               |	СчетНаОплатуПокупателю.Организация КАК ОрганизацияПолучатель,
	               |	"""" КАК НазначениеПлатежа,
	               |	"""" КАК СпособОплаты,
	               |	СчетНаОплатуПокупателю.УчитыватьНДС,
	               |	Валюты.НаименованиеПолное КАК НаименованиеВалюты,
	               |	Валюты.Код КАК КодВалюты,
	               |	ВЫБОР
	               |		КОГДА СчетНаОплатуПокупателю.ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	               |			ТОГДА СчетНаОплатуПокупателю.ДоговорКонтрагента
	               |		ИНАЧЕ СчетНаОплатуПокупателю.ЗаказПокупателя
	               |	КОНЕЦ КАК ДокументОснование,
	               |	ПРЕДСТАВЛЕНИЕ(ВЫБОР
	               |			КОГДА СчетНаОплатуПокупателю.ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	               |				ТОГДА СчетНаОплатуПокупателю.ДоговорКонтрагента
	               |			ИНАЧЕ СчетНаОплатуПокупателю.ЗаказПокупателя
	               |		КОНЕЦ) КАК ДокументОснованиеНаименование,
	               |	СчетНаОплатуПокупателю.ДатаОплаты КАК ОграничениеПоДатеОплаты,
	               |	СчетНаОплатуПокупателю.ДатаОплаты КАК ОграничениеПоДатеВремениОплаты
	               |ИЗ
	               |	Документ.СчетНаОплатуПокупателю КАК СчетНаОплатуПокупателю
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Валюты КАК Валюты
	               |		ПО СчетНаОплатуПокупателю.ВалютаДокумента = Валюты.Ссылка
	               |ГДЕ
	               |	СчетНаОплатуПокупателю.Ссылка В(&МассивОбъектов)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДанныеТоваровСчетаНаОплату_1_01()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеНоменклатуры.Код КАК КодТовара,
	|	ДанныеНоменклатуры.Артикул КАК Артикул,
	|	ВЫРАЗИТЬ(СчетНаОплатуПокупателю.Номенклатура.НаименованиеПолное КАК СТРОКА(255)) КАК Наименование,
	|	СчетНаОплатуПокупателю.Номенклатура КАК Номенклатура,
	|	СчетНаОплатуПокупателю.ХарактеристикаНоменклатуры КАК Характеристика,
	|	ДанныеНоменклатуры.БазоваяЕдиницаИзмерения КАК БазоваяЕдиница,
	|	ДанныеНоменклатуры.БазоваяЕдиницаИзмерения.Код КАК БазоваяЕдиницаКод,
	|	ДанныеНоменклатуры.БазоваяЕдиницаИзмерения.Наименование КАК БазоваяЕдиницаНаименование,
	|	ДанныеНоменклатуры.БазоваяЕдиницаИзмерения.НаименованиеПолное КАК БазоваяЕдиницаНаименованиеПолное,
	|	ДанныеНоменклатуры.БазоваяЕдиницаИзмерения.МеждународноеСокращение КАК БазоваяЕдиницаМеждународноеСокращение,
	|	ДанныеЕдиницыИзмерения.ЕдиницаПоКлассификатору КАК ЕдиницаИзмерения,
	|	ДанныеЕдиницыИзмерения.ЕдиницаПоКлассификатору КАК Упаковка,
	|	ДанныеЕдиницыИзмерения.ЕдиницаПоКлассификатору.Код КАК ЕдиницаИзмеренияКодПоОКЕИ,
	|	ДанныеЕдиницыИзмерения.ЕдиницаПоКлассификатору.Наименование КАК ЕдиницаИзмеренияНаименование,
	|	ДанныеЕдиницыИзмерения.Коэффициент КАК ЕдиницаИзмеренияКоэффициент,
	|	СчетНаОплатуПокупателю.Цена,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.СуммаВключаетНДС
	|			ТОГДА СчетНаОплатуПокупателю.Сумма
	|		ИНАЧЕ СчетНаОплатуПокупателю.Сумма + СчетНаОплатуПокупателю.СуммаНДС
	|	КОНЕЦ КАК СтоимостьСНДС,
	|	СчетНаОплатуПокупателю.СтавкаНДС,
	|	СчетНаОплатуПокупателю.СуммаНДС,
	|	СчетНаОплатуПокупателю.Количество,
	|	ДанныеДокумента.СуммаВключаетНДС,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.СуммаВключаетНДС
	|			ТОГДА СчетНаОплатуПокупателю.Сумма - СчетНаОплатуПокупателю.СуммаНДС
	|		ИНАЧЕ СчетНаОплатуПокупателю.Сумма
	|	КОНЕЦ КАК СтоимостьБезНДС,
	|	СчетНаОплатуПокупателю.ПроцентСкидкиНаценки + СчетНаОплатуПокупателю.ПроцентАвтоматическихСкидок КАК Скидка,
	|	ДанныеНоменклатуры.КодТНВЭД.Код КАК КодВидаТовара,
	|	СчетНаОплатуПокупателю.ХарактеристикаНоменклатуры.Наименование КАК ХарактеристикаНаименование,
	|	ДанныеДокумента.УчитыватьНДС
	|ИЗ
	|	Документ.СчетНаОплатуПокупателю.Товары КАК СчетНаОплатуПокупателю
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ДанныеНоменклатуры
	|		ПО СчетНаОплатуПокупателю.Номенклатура = ДанныеНоменклатуры.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЕдиницыИзмерения КАК ДанныеЕдиницыИзмерения
	|		ПО СчетНаОплатуПокупателю.ЕдиницаИзмерения = ДанныеЕдиницыИзмерения.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетНаОплатуПокупателю КАК ДанныеДокумента
	|		ПО СчетНаОплатуПокупателю.Ссылка = ДанныеДокумента.Ссылка
	|ГДЕ
	|	СчетНаОплатуПокупателю.Ссылка В(&МассивОбъектов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеНоменклатуры.Код,
	|	ДанныеНоменклатуры.Артикул,
	|	СчетНаОплатуПокупателю.Содержание,
	|	СчетНаОплатуПокупателю.Номенклатура,
	|	"""",
	|	ДанныеНоменклатуры.БазоваяЕдиницаИзмерения,
	|	ДанныеНоменклатуры.БазоваяЕдиницаИзмерения.Код,
	|	ДанныеНоменклатуры.БазоваяЕдиницаИзмерения.Наименование,
	|	ДанныеНоменклатуры.БазоваяЕдиницаИзмерения.НаименованиеПолное,
	|	ДанныеНоменклатуры.БазоваяЕдиницаИзмерения.МеждународноеСокращение,
	|	ДанныеНоменклатуры.БазоваяЕдиницаИзмерения,
	|	ДанныеНоменклатуры.БазоваяЕдиницаИзмерения,
	|	ДанныеНоменклатуры.БазоваяЕдиницаИзмерения.Код,
	|	ДанныеНоменклатуры.БазоваяЕдиницаИзмерения.Наименование,
	|	1,
	|	СчетНаОплатуПокупателю.Цена,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.СуммаВключаетНДС
	|			ТОГДА СчетНаОплатуПокупателю.Сумма
	|		ИНАЧЕ СчетНаОплатуПокупателю.Сумма + СчетНаОплатуПокупателю.СуммаНДС
	|	КОНЕЦ,
	|	СчетНаОплатуПокупателю.СтавкаНДС,
	|	СчетНаОплатуПокупателю.СуммаНДС,
	|	СчетНаОплатуПокупателю.Количество,
	|	ДанныеДокумента.СуммаВключаетНДС,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.СуммаВключаетНДС
	|			ТОГДА СчетНаОплатуПокупателю.Сумма - СчетНаОплатуПокупателю.СуммаНДС
	|		ИНАЧЕ СчетНаОплатуПокупателю.Сумма
	|	КОНЕЦ,
	|	СчетНаОплатуПокупателю.ПроцентСкидкиНаценки + СчетНаОплатуПокупателю.ПроцентАвтоматическихСкидок,
	|	"""",
	|	"""",
	|	ДанныеДокумента.УчитыватьНДС
	|ИЗ
	|	Документ.СчетНаОплатуПокупателю.Услуги КАК СчетНаОплатуПокупателю
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ДанныеНоменклатуры
	|		ПО СчетНаОплатуПокупателю.Номенклатура = ДанныеНоменклатуры.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетНаОплатуПокупателю КАК ДанныеДокумента
	|		ПО СчетНаОплатуПокупателю.Ссылка = ДанныеДокумента.Ссылка
	|ГДЕ
	|	СчетНаОплатуПокупателю.Ссылка В(&МассивОбъектов)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти
