
///////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ТТН_ЕГАИС = Параметры.ТоварноТранспортнаяНакладнаяЕГАИС;
	ЗаполнитьТаблицуРасхожденийТТН_ЕГАИС();
	
	СобытияФормЕГАИСПереопределяемый.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	СобытияФормЕГАИСКлиентПереопределяемый.ОбработкаОповещения(ЭтаФорма, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ПринятьНакладную(Команда)
	Закрыть(Новый Структура("ПринятьСРасхождениями, ТаблицаРасхождений", Истина, Расхождения));
КонецПроцедуры

&НаКлиенте
Процедура ПринятьНакладнуюБезРасхождений(Команда)
	Закрыть(Новый Структура("ПринятьСРасхождениями", Ложь));
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

///////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	ЦветПоложительнойРазницы = WebЦвета.Зеленый;
	ЦветОтрицательнойРазницы = WebЦвета.Кирпичный;
	
	УсловноеОформление.Элементы.Очистить();
	
	// Положительная общая разница
	Элемент = УсловноеОформление.Элементы.Добавить();
	Элемент.Представление = НСтр("ru = 'Положительная общая разница'");
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Расхождение.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Расхождения.Расхождение");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветПоложительнойРазницы);
	
	// Отрицательная общая разница
	Элемент = УсловноеОформление.Элементы.Добавить();
	Элемент.Представление = НСтр("ru = 'Отрицательная общая разница'");
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Расхождение.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Расхождения.Расхождение");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Меньше;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветОтрицательнойРазницы);
	
	// Положительная разница
	Элемент = УсловноеОформление.Элементы.Добавить();
	Элемент.Представление = НСтр("ru = 'Положительная разница'");
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.УвеличитьУменьшить.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Расхождения.УвеличитьУменьшить");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветПоложительнойРазницы);
	
	// Отрицательная разница
	Элемент = УсловноеОформление.Элементы.Добавить();
	Элемент.Представление = НСтр("ru = 'Отрицательная разница'");
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.УвеличитьУменьшить.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Расхождения.УвеличитьУменьшить");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Меньше;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветОтрицательнойРазницы);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуРасхожденийТТН_ЕГАИС()
	
	Расхождения.Загрузить(ТаблицаРасхожденийТТН_ЕГАИС(ТТН_ЕГАИС));
	
КонецПроцедуры

&НаСервере
Функция ТаблицаРасхожденийТТН_ЕГАИС(ДокументСсылка) Экспорт
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("АлкогольнаяПродукция");
	Результат.Колонки.Добавить("Справка2");
	Результат.Колонки.Добавить("Количество");
	Результат.Колонки.Добавить("КоличествоФакт");
	Результат.Колонки.Добавить("Расхождение");
	Результат.Колонки.Добавить("ИдентификаторСтроки");
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура РасхожденияКоличествоНакладнаяПриИзменении(Элемент)
	
	КоличествоФактИтог = 0;
	КоличествоИтог     = 0;
	
	ТекущиеДанные = Элементы.Расхождения.ТекущиеДанные;
	НайденныеСтроки = Расхождения.НайтиСтроки(Новый Структура("АлкогольнаяПродукция", ТекущиеДанные.АлкогольнаяПродукция));
	
	Для каждого СтрокаРасхождения Из НайденныеСтроки Цикл
		
		КоличествоФактИтог = КоличествоФактИтог + СтрокаРасхождения.КоличествоФакт;
		КоличествоИтог     = КоличествоИтог + СтрокаРасхождения.Количество;
		
	КонецЦикла;
	
	Для каждого СтрокаРасхождения Из НайденныеСтроки Цикл
		СтрокаРасхождения.УвеличитьУменьшить = (КоличествоИтог - СтрокаРасхождения.Расхождение)-КоличествоФактИтог;
	КонецЦикла;
	
КонецПроцедуры
