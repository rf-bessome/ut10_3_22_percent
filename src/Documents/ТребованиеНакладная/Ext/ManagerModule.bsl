
Функция ТекстЗапросаПрослеживаемыеТовары(НомераТаблиц, ПараметрыПроведения, Реквизиты) Экспорт
	
	Если НЕ Реквизиты.ВедетсяУчетПрослеживаемыхТоваров Тогда
		ПараметрыПроведения.Вставить("ВТ_Прослеживаемость", Неопределено);
		ПараметрыПроведения.Вставить("ПрослеживаемыеОперации", Неопределено);
		ПараметрыПроведения.Вставить("ПрослеживаемыеТовары", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ВТ_Прослеживаемость",    НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПрослеживаемыеОперации", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПрослеживаемыеТовары",   НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТребованиеНакладнаяМатериалы.Номенклатура КАК Номенклатура,
	|	СУММА(ТребованиеНакладнаяСведенияПрослеживаемости.Количество) КАК Количество,
	|	ТребованиеНакладнаяМатериалы.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ТребованиеНакладнаяСведенияПрослеживаемости.РНПТ КАК РНПТ,
	|	СУММА(ТребованиеНакладнаяСведенияПрослеживаемости.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости,
	|	ТребованиеНакладнаяМатериалы.Ссылка КАК ДокументОперации,
	|	НАЧАЛОПЕРИОДА(ТребованиеНакладнаяМатериалы.Ссылка.Дата, КВАРТАЛ) КАК ПериодОперации,
	|	ЕСТЬNULL(ТребованиеНакладнаяМатериалы.Ссылка.Номер, """") КАК НомерДокумента,
	|	ЕСТЬNULL(ТребованиеНакладнаяМатериалы.Ссылка.Дата, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ОтчетОбОперациях) КАК ОтражениеВОтчетности,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Контрагент,
	|	ЗНАЧЕНИЕ(Справочник.ТипыДокументов.Акт) КАК ТипДокументаВПрослеживаемости,
	|	ТребованиеНакладнаяМатериалы.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ПередачаВПроизводство) КАК КодОперации,
	|	ТребованиеНакладнаяСведенияПрослеживаемости.Сумма КАК СуммаБезНДС
	|ПОМЕСТИТЬ ВТ_Прослеживаемость
	|ИЗ
	|	Документ.ТребованиеНакладная.Материалы КАК ТребованиеНакладнаяМатериалы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТребованиеНакладная.СведенияПрослеживаемости КАК ТребованиеНакладнаяСведенияПрослеживаемости
	|		ПО ТребованиеНакладнаяМатериалы.Ссылка = ТребованиеНакладнаяСведенияПрослеживаемости.Ссылка
	|			И ТребованиеНакладнаяМатериалы.ИдентификаторСтроки = ТребованиеНакладнаяСведенияПрослеживаемости.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ТребованиеНакладная КАК ТребованиеНакладная
	|		ПО ТребованиеНакладнаяМатериалы.Ссылка = ТребованиеНакладная.Ссылка
	|ГДЕ
	|	ТребованиеНакладнаяМатериалы.ПрослеживаемыйТовар
	|	И ТребованиеНакладнаяМатериалы.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТребованиеНакладнаяСведенияПрослеживаемости.РНПТ,
	|	ТребованиеНакладнаяМатериалы.Номенклатура,
	|	ТребованиеНакладнаяМатериалы.Ссылка,
	|	НАЧАЛОПЕРИОДА(ТребованиеНакладнаяМатериалы.Ссылка.Дата, КВАРТАЛ),
	|	ЕСТЬNULL(ТребованиеНакладнаяМатериалы.Ссылка.Номер, """"),
	|	ЕСТЬNULL(ТребованиеНакладнаяМатериалы.Ссылка.Дата, ДАТАВРЕМЯ(1, 1, 1)),
	|	ТребованиеНакладнаяМатериалы.ИдентификаторСтроки,
	|	ТребованиеНакладнаяСведенияПрослеживаемости.Сумма,
//	|	ТребованиеНакладная.КодОперацииПрослеживаемости
	|	ТребованиеНакладнаяМатериалы.СтранаПроисхождения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Прослеживаемость.Номенклатура КАК Номенклатура,
	|	ВТ_Прослеживаемость.Количество КАК Количество,
	|	ВТ_Прослеживаемость.РНПТ КАК РНПТ,
	|	ВТ_Прослеживаемость.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|	ВТ_Прослеживаемость.ДокументОперации КАК ДокументОперации,
	|	ВТ_Прослеживаемость.ПериодОперации КАК ПериодОперации,
	|	ВТ_Прослеживаемость.НомерДокумента КАК НомерДокумента,
	|	ВТ_Прослеживаемость.ДатаДокумента КАК ДатаДокумента,
	|	ВТ_Прослеживаемость.ОтражениеВОтчетности КАК ОтражениеВОтчетности,
	|	ВТ_Прослеживаемость.Контрагент КАК Контрагент,
	|	ВТ_Прослеживаемость.ТипДокументаВПрослеживаемости КАК ТипДокументаВПрослеживаемости,
	|	0 КАК СуммаБезНДС,
	|	ВТ_Прослеживаемость.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ВТ_Прослеживаемость.КодОперации КАК КодОперации
	|ИЗ
	|	ВТ_Прослеживаемость КАК ВТ_Прослеживаемость
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Прослеживаемость.Номенклатура КАК Номенклатура,
	|	СУММА(ВТ_Прослеживаемость.Количество) КАК Количество,
	|	ВТ_Прослеживаемость.СтранаПроисхождения КАК СтранаПроисхождения,
	|	СУММА(ВТ_Прослеживаемость.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Комитент,
	|	НЕОПРЕДЕЛЕНО КАК ВидЗапасов,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Комиссионер,
	|	ВТ_Прослеживаемость.РНПТ КАК РНПТ,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаРеализации,
	|	НЕОПРЕДЕЛЕНО КАК ОснованиеДляВозврата
	|ИЗ
	|	ВТ_Прослеживаемость КАК ВТ_Прослеживаемость
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Прослеживаемость.Номенклатура,
	|	ВТ_Прослеживаемость.СтранаПроисхождения,
	|	ВТ_Прослеживаемость.РНПТ";
	
	Возврат ТекстЗапроса + ПрослеживаемостьБП.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Процедура ДвиженияПоРегистрамПрослеживаемыхТоваров(СтруктураШапкиДокумента, ТаблицаСумм = Неопределено) Экспорт
	
	Отказ = Ложь;
	УчетнаяПолитикаРегл = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитики(СтруктураШапкиДокумента.Дата, Отказ, СтруктураШапкиДокумента.Организация, "Нал");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", СтруктураШапкиДокумента.Ссылка);
	
	НомераТаблиц = Новый Структура;
	ПараметрыПроведения = Новый Структура;
	
	УсловияПрослеживаемости = Новый Структура;
	УсловияПрослеживаемости.Вставить("ВедетсяУчетПрослеживаемыхТоваров", ПолучитьФункциональнуюОпцию("ВестиУчетПрослеживаемыхТоваров")
		И ПрослеживаемостьБРУ.ВедетсяУчетПрослеживаемыхТоваров(СтруктураШапкиДокумента.Дата));
	
	Запрос.Текст = Документы.ТребованиеНакладная.ТекстЗапросаПрослеживаемыеТовары(НомераТаблиц, ПараметрыПроведения, УсловияПрослеживаемости);
	
	Если НЕ ПустаяСтрока(Запрос.Текст) Тогда
		Результат = Запрос.ВыполнитьПакет();
		
		Для Каждого НомерТаблицы Из НомераТаблиц Цикл
			ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
		КонецЦикла;
	КонецЕсли;
	
	Если УсловияПрослеживаемости.ВедетсяУчетПрослеживаемыхТоваров Тогда
		ТаблицаРеквизиты = ПрослеживаемостьБП.СтруктуруВТаблицуЗначений(СтруктураШапкиДокумента);
		
		Если НЕ ТаблицаСумм = Неопределено Тогда
			Для каждого Строка Из ПараметрыПроведения.ПрослеживаемыеОперации Цикл
				СтрокаССуммой = ТаблицаСумм.Найти(Строка.ИдентификаторСтроки, "ИдСтрокиПрослеживаемогоТовара");
				
				Если ЗначениеЗаполнено(СтрокаССуммой) Тогда
					Строка.СуммаБезНДС = СтрокаССуммой.Стоимость;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	
		ПрослеживаемостьБП.СформироватьДвиженияРеализацияТоваров(
			ПараметрыПроведения.ПрослеживаемыеТовары,
			ПараметрыПроведения.ПрослеживаемыеОперации,
			ТаблицаРеквизиты,
			СтруктураШапкиДокумента.Ссылка.ПолучитьОбъект().Движения);
	КонецЕсли;
	
КонецПроцедуры