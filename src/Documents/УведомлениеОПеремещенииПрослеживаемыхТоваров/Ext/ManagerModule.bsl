#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ВыгрузитьУведомлениеОПеремещенииПрослеживаемыхТоваров(Документ) Экспорт
	
	ПараметрыВыгрузки = ПараметрыУведомления(Документ);
	
	Контейнер = РегламентированнаяОтчетность.СтруктураКонтейнераДанных(ПараметрыВыгрузки);
	
	Если Контейнер.Свойство("Ошибки") 
		И Контейнер.Ошибки.Количество() > 0 Тогда
		
		Возврат Новый Структура("АдресФайлаВыгрузки,ИмяФайлаВыгрузки,Ошибки",
					Контейнер.АдресФайлаВыгрузки, Контейнер.ИмяФайлаВыгрузки, Контейнер.Ошибки);
	
	КонецЕсли;
	
	ЗаполнитьДополнительныеРеквизиты(Контейнер, Документ);
	
	Если ЗначениеЗаполнено(Контейнер.Раздел1) Тогда
		ЗаполнитьРаздел1(Контейнер.Раздел1, Документ);
	КонецЕсли;
	
	Результат = РегламентированнаяОтчетность.ВыгрузитьДанныеАлгоритмамиРегламентированногоОтчета(Контейнер);
	
	Возврат Новый Структура("АдресФайлаВыгрузки,ИмяФайлаВыгрузки,Ошибки",
					Результат.АдресФайлаВыгрузки, Результат.ИмяФайлаВыгрузки, Результат.Ошибки);
	
КонецФункции

// Формирует заполненные таблицы документа
//
// Параметры:
//  Параметры - Структура: 
//						- Организация - СправочникСсылка - организация
//						- Период - Дата - дата помощника получения РНПТ
//  АдресРезультата - строка - адрес, куда будет помещен результат 
// 
Процедура ЗаполнитьДокумент(Параметры, АдресРезультата) Экспорт
	
	ДанныеТаблиц = ЗаполнитьДокументДанными(
		Параметры.Дата,
		Параметры.Организация);
	
	ПоместитьВоВременноеХранилище(ДанныеТаблиц, АдресРезультата);
	
КонецПроцедуры

#Область ПодготовкаПараметровПроведенияДокумента

// Формирует и возвращает текст запроса для выборки данных
// необходимых для формирования движений документа Уведомление о перемещении
//
// Параметры:
//  НомераТаблиц - Число - Структура с таблицами параметров проведения
// 
// Возвращаемое значение:
//  Строка - текст запроса
//
Функция ТекстЗапросаТаблицаТоваровУведомлениеОПеремещении(НомераТаблиц, ДокументСсылка) Экспорт
	
	НомераТаблиц.Вставить("ТаблицаТоваров", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Ссылка КАК Ссылка,
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Номенклатура КАК Номенклатура,
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Количество КАК Количество,
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Сумма КАК Сумма,
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.РНПТ КАК РНПТ,
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.ПорядковыйНомер КАК ПорядковыйНомер,
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровКонтрагенты.Контрагент КАК Контрагент,
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.СопроводительныйДокумент КАК СопроводительныйДокумент,
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.НомерСтроки КАК НомерСтроки,
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.ЕдиницаИзмерения
	|ИЗ
	|	Документ.УведомлениеОПеремещенииПрослеживаемыхТоваров.Товары КАК УведомлениеОПеремещенииПрослеживаемыхТоваровТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОПеремещенииПрослеживаемыхТоваров.Контрагенты КАК УведомлениеОПеремещенииПрослеживаемыхТоваровКонтрагенты
	|		ПО УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Ссылка = УведомлениеОПеремещенииПрослеживаемыхТоваровКонтрагенты.Ссылка
	|			И УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.КлючСтроки = УведомлениеОПеремещенииПрослеживаемыхТоваровКонтрагенты.КлючСтроки
	|ГДЕ
	|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ПрослеживаемостьБП.ТекстРазделителяЗапросовПакета();
	
КонецФункции

// Функция подготавливает параметры проведения документа
Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт

	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаРеквизитыДокумента(НомераТаблиц);
	Результат    = Запрос.ВыполнитьПакет();
	ТаблицаРеквизиты = Результат[НомераТаблиц["Реквизиты"]].Выгрузить();
	ПараметрыПроведения.Вставить("Реквизиты", ТаблицаРеквизиты);
	
	Реквизиты = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ТаблицаРеквизиты[0]);
	Если ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитики(Реквизиты.Период, Отказ, Реквизиты.Организация, "Нал", Истина) = Неопределено Тогда
		Отказ = Истина;
		Возврат ПараметрыПроведения;
	КонецЕсли;
	
	НомераТаблиц = Новый Структура;
	
	Запрос.Текст = 
		ТекстЗапросаТаблицаТоваровУведомлениеОПеремещении(НомераТаблиц, ДокументСсылка);
		
	Если НЕ ПустаяСтрока(Запрос.Текст) Тогда
		Результат = Запрос.ВыполнитьПакет();
		Для Каждого НомерТаблицы Из НомераТаблиц Цикл
			ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
		КонецЦикла;
	КонецЕсли;
	
	Возврат ПараметрыПроведения;
	
КонецФункции

#КонецОбласти

// Процедура проведения документа Уведомления о перемещении прослеживаемых товаров
// 
// Параметры:
//  ПараметрыПроведения - Структура таблиц см. Документы.УведомлениеОПеремещенииПрослеживаемыхТоваров.ПодготовитьПараметрыПроведения
//  Движения - ссылка на движения документа
//  Отказ - Булево - признак отказа от записи
Процедура ОбработкаПроведенияУведомлениеОПеремещении(ПараметрыПроведения, Движения, Отказ) Экспорт

	ТаблицаРеализацииТоваров = ПодготовитьТаблицуПрослеживаемыеТоварыОтгрузкаВЕАЭС(ПараметрыПроведения.ТаблицаТоваров,
		ПараметрыПроведения.Реквизиты, Отказ);
	
	СформироватьДвиженияПрослеживаемыеТоварыОтгруженныевЕАЭС(
		ТаблицаРеализацииТоваров,
		Движения,
		Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьОстаткиТовара(Реквизиты, ТаблицаТоваров, Отказ)
	
	Запрос = Новый Запрос;
	
	// Блокируем регистр ПрослеживаемыеТоварыОтгруженныеВЕАЭС для получения остатков
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ПрослеживаемыеТоварыОтгруженныеВЕАЭС");
	ЭлементБлокировки.УстановитьЗначение("Организация", Реквизиты.Организация);
	ЭлементБлокировки.УстановитьЗначение("Период", Новый Диапазон(, Реквизиты.Период));
	ЭлементБлокировки.ИсточникДанных = ТаблицаТоваров;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Контрагент", "Контрагент");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("СопроводительныйДокумент", "СопроводительныйДокумент");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("РНПТ", "РНПТ");
	Блокировка.Заблокировать();
	
	Запрос.УстановитьПараметр("ТаблицаТоваров", ТаблицаТоваров);
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	
	МоментВремени = Новый Граница(Новый МоментВремени(Реквизиты.Период, Реквизиты.Регистратор), ВидГраницы.Исключая);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.Количество КАК Количество,
	|	ТаблицаТоваров.РНПТ КАК РНПТ,
	|	ТаблицаТоваров.ПорядковыйНомер КАК ПорядковыйНомер,
	|	ТаблицаТоваров.Контрагент КАК Контрагент,
	|	ТаблицаТоваров.СопроводительныйДокумент КАК СопроводительныйДокумент
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ
	|	&ТаблицаТоваров КАК ТаблицаТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрослеживаемыеТоварыОтгруженныеВЕАЭСОстатки.Организация КАК Организация,
	|	ПрослеживаемыеТоварыОтгруженныеВЕАЭСОстатки.Контрагент КАК Контрагент,
	|	ПрослеживаемыеТоварыОтгруженныеВЕАЭСОстатки.СопроводительныйДокумент КАК СопроводительныйДокумент,
	|	ПрослеживаемыеТоварыОтгруженныеВЕАЭСОстатки.Номенклатура КАК Номенклатура,
	|	ПрослеживаемыеТоварыОтгруженныеВЕАЭСОстатки.РНПТ КАК РНПТ,
	|	ПрослеживаемыеТоварыОтгруженныеВЕАЭСОстатки.ПорядковыйНомер КАК ПорядковыйНомер,
	|	ПрослеживаемыеТоварыОтгруженныеВЕАЭСОстатки.КоличествоОстаток КАК КоличествоОстаток,
	|	ПрослеживаемыеТоварыОтгруженныеВЕАЭСОстатки.КоличествоПрослеживаемостиОстаток КАК КоличествоПрослеживаемостиОстаток,
	|	ПрослеживаемыеТоварыОтгруженныеВЕАЭСОстатки.СуммаОстаток КАК СуммаОстаток,
	|	ТаблицаТоваров.Количество КАК Количество,
	|	ТаблицаТоваров.Количество - ПрослеживаемыеТоварыОтгруженныеВЕАЭСОстатки.КоличествоОстаток КАК КоличествоОсталосьПогасить,
	|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПрослеживаемыеТоварыОтгруженныеВЕАЭС.Остатки(&МоментВремени, Организация = &Организация) КАК ПрослеживаемыеТоварыОтгруженныеВЕАЭСОстатки
	|		ПО ТаблицаТоваров.Номенклатура = ПрослеживаемыеТоварыОтгруженныеВЕАЭСОстатки.Номенклатура
	|			И ТаблицаТоваров.РНПТ = ПрослеживаемыеТоварыОтгруженныеВЕАЭСОстатки.РНПТ
	|			И ТаблицаТоваров.ПорядковыйНомер = ПрослеживаемыеТоварыОтгруженныеВЕАЭСОстатки.ПорядковыйНомер
	|			И ТаблицаТоваров.СопроводительныйДокумент = ПрослеживаемыеТоварыОтгруженныеВЕАЭСОстатки.СопроводительныйДокумент
	|ГДЕ
	|	ПрослеживаемыеТоварыОтгруженныеВЕАЭСОстатки.КоличествоОстаток - ТаблицаТоваров.Количество < 0";
	
	Результат = Запрос.Выполнить().Выбрать();
	
	ТаблицаОшибок = ТаблицаТоваров.СкопироватьКолонки("Контрагент, НомерСтроки, Количество");
	ТаблицаОшибок.Колонки.Добавить("КоличествоОсталосьПогасить", Новый ОписаниеТипов("Число"));
	
	Пока Результат.Следующий() Цикл
		
		СтрокаТаблицыОшибок = ТаблицаОшибок.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицыОшибок, Результат);
		СтрокаТаблицыОшибок.КоличествоОсталосьПогасить = Результат.КоличествоОсталосьПогасить;
		
	КонецЦикла;
	
	ВывестиСообщениеОбОшибках(ТаблицаОшибок, Реквизиты.Регистратор, Отказ)
	
КонецПроцедуры

Процедура ВывестиСообщениеОбОшибках(ТаблицаОшибок, Регистратор, Отказ)
	
	Для каждого Ошибка Из ТаблицаОшибок Цикл
		
		ТекстОшибки = НСтр("ru='По контрагенту %1, указанное количество превышает количество по сопроводительному документу. Количество: %2; Не хватает: %3'");
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки,
			Ошибка.Контрагент,
			Ошибка.Количество,
			Ошибка.КоличествоОсталосьПогасить);
		
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
			"Колонка", 
			"Корректность",
			НСтр("ru = 'Количество'"),
			Ошибка.НомерСтроки,
			"Товары",
			ТекстОшибки);
			
		ПолеКоличество = "Товары" + "[" + Формат(Ошибка.НомерСтроки - 1, "ЧН=0; ЧГ=") + "].Количество";
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Регистратор, ПолеКоличество, "Объект", Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПодготовитьТаблицуПрослеживаемыеТоварыОтгрузкаВЕАЭС(ТаблицаТоваров, ТаблицаРеквизитов, Отказ)
	
	Если Не ЗначениеЗаполнено(ТаблицаТоваров)
	 Или Не ЗначениеЗаполнено(ТаблицаРеквизитов) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыТаблицыРеализацияПрослеживаемыхТоваров(ТаблицаТоваров, ТаблицаРеквизитов);
	Реквизиты = Параметры.Реквизиты[0];
	
	ТаблицаПрослеживаемыхТоваров = ПодготовитьТаблицуПрослеживаемыеТоварыОтгруженныевЕАЭС(Реквизиты, Параметры.ТаблицаТоваров, Отказ);
	
	Возврат ТаблицаПрослеживаемыхТоваров;
	
КонецФункции

Функция ПодготовитьПараметрыТаблицыРеализацияПрослеживаемыхТоваров(ТаблицаТоваров, ТаблицаРеквизитов)

	Параметры = Новый Структура;
	
	// Подготовка таблицы Параметры.ТаблицаТоваров
	
	СписокОбязательныхКолонок = ""
		+ "РНПТ,"                       // <СправочникСсылка.*> - РНПТ
		+ "Номенклатура,"               // <СправочникСсылка.*> - списываемая номенклатура
		+ "НомерСтроки,"                // <Число> - номер строки в списке 
		+ "ЕдиницаИзмерения,"           // <СправочникСсылка.ЕдиницыИзмерения> - Единица измерения
		+ "Количество,"                 // <Число> - количество по первичным документам
		+ "КоличествоПрослеживаемости," // <Число> - количество прослеживаемости
		+ "Сумма,"						// <Число> - сумма
		+ "ПорядковыйНомер,"            // <Число> - номер строки номеклатуры в сопроводительном документе
		+ "Контрагент,"                 // <СправочникСсылка.Контрагенты> - количество по первичным документам
		+ "СопроводительныйДокумент,";  // <ОпределяемыйТип.СопроводительныйДокументУведомлениеОПеремещении> - документ отгрузки
		
	Параметры.Вставить("ТаблицаТоваров", ПрослеживаемостьБП.ПолучитьТаблицуПараметровПроведения(
		ТаблицаТоваров, СписокОбязательныхКолонок));
		
	// Подготовка таблицы Параметры.Реквизиты
	
	СписокОбязательныхКолонок = ""
		+ "Период,"				// <Дата> - период движений - дата документа
		+ "Регистратор,"		// <ДокументСсылка.*> - документ-регистратор движений
		+ "Организация";		// <СправочникСсылка.Организации>
		
	Параметры.Вставить("Реквизиты", ПрослеживаемостьБП.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизитов, СписокОбязательныхКолонок));
		
	Возврат Параметры;

КонецФункции

Функция ПодготовитьТаблицуПрослеживаемыеТоварыОтгруженныевЕАЭС(Реквизиты, ТаблицаТоваров, Отказ)

	ТаблицаПрослеживаемогоТовара = ТаблицаТоваров.Скопировать();
	ТаблицаПрослеживаемогоТовара.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	ТаблицаПрослеживаемогоТовара.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	
	ТаблицаПрослеживаемогоТовара.ЗаполнитьЗначения(Реквизиты.Период, "Период");
	ТаблицаПрослеживаемогоТовара.ЗаполнитьЗначения(Реквизиты.Организация, "Организация");
	
	ПроверитьОстаткиТовара(Реквизиты, ТаблицаТоваров, Отказ);
	
	Возврат ТаблицаПрослеживаемогоТовара;
	
КонецФункции

Процедура СформироватьДвиженияПрослеживаемыеТоварыОтгруженныевЕАЭС(ТаблицаТоваров, Движения, Отказ)
	
	Если НЕ ЗначениеЗаполнено(ТаблицаТоваров) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из ТаблицаТоваров Цикл
		Запись = Движения.ПрослеживаемыеТоварыОтгруженныеВЕАЭС.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, СтрокаТаблицы);
		Запись.ВидДвижения = ВидДвиженияНакопления.Расход;
	КонецЦикла;
	
	Движения.ПрослеживаемыеТоварыОтгруженныеВЕАЭС.Записывать = Истина;
	
КонецПроцедуры

// Вызывается при вводе документа (Уведомление об остатках, Уведомление о ввозе, 
// Уведомление о перемещении) на основании,
// при выполнении метода Заполнить или при интерактивном вводе нового.
//
// Параметры:
//  ДокументОбъект - ДокументОбъект - заполняемый документ,
//  ДанныеЗаполнения - Произвольный - значение, которое используется как основание для заполнения,
//  ТекстЗаполнения - Строка, Неопределено - текст, используемый для заполнения документа,
//  СтандартнаяОбработка - Булево - признак выполнения стандартной (системной) обработки события.
//
Процедура ОбработкаЗаполненияДокумента(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	
	
КонецПроцедуры

// Предоставляет возможность переопределить обработку заполнения документа "Уведомление о перемещении"
//
// Параметры:
//  УведомлениеОбъект    - ДокументОбъект.УведомлениеОперемещенииПрослеживаемыхТоваров - документ, для которого
//                                                                                        выполняется заполнение.
//  ДанныеЗаполнения - Произвольный - см. описание параметра в синтаксис-помощнике к обработчику ОбработкаЗаполнения.
//  ТекстЗаполнения - Строка - см. описание параметра в синтаксис-помощнике к обработчику ОбработкаЗаполнения.
//  СтандартнаяОбработка - Булево - см. описание параметра в синтаксис-помощнике к обработчику ОбработкаЗаполнения.
//
Процедура ОбработкаЗаполненияУведомленияОПеремещении(УведомлениеОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(УведомлениеОбъект, глЗначениеПеременной("глТекущийПользователь"));
	
	Если НЕ ЗначениеЗаполнено(ДанныеЗаполнения) 
		ИЛИ ТипЗнч(ДанныеЗаполнения) = Тип("Структура")  Тогда
		Возврат;
	КонецЕсли;
	
	ПрослеживаемостьБРУ.ЗаполнитьУведомлениеОПеремещенииПрослеживаемыхТоваров(
		УведомлениеОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		
	ОбработкаЗаполненияДокумента(УведомлениеОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		
КонецПроцедуры

Функция ПолучитьСписокПечатныхФормУведомлениеОПеремещенииПрослеживаемыхТоваров(Документ) Экспорт
	
	ПараметрыПечати = ПараметрыУведомления(Документ);
	
	Контейнер = РегламентированнаяОтчетность.СтруктураКонтейнераДанных(ПараметрыПечати);
	
	Если Контейнер.Свойство("Ошибки") 
		И Контейнер.Ошибки.Количество() > 0 Тогда
		
		Возврат Новый СписокЗначений;
	
	КонецЕсли;
	
	ЗаполнитьДополнительныеРеквизиты(Контейнер, Документ);
	
	Если ЗначениеЗаполнено(Контейнер.Раздел1) Тогда
		ЗаполнитьРаздел1(Контейнер.Раздел1, Документ);
	КонецЕсли;
	
	Результат = РегламентированнаяОтчетность.ПечатныйБланкСформированныйАлгоритмамиРегламентированныхОтчетов(Контейнер);
	
	Если Результат.Количество() = 0 Тогда
		Возврат Новый СписокЗначений;
	КонецЕсли;
	
	СписокПечатныхФорм = Новый СписокЗначений;
	
	НомерЛиста = 0;
	Для каждого ТекущаяСтрокаРезультат Из Результат Цикл
		
		НомерЛиста = НомерЛиста + 1;
		
		МассивПараметров = Новый Массив;
		МассивПараметров.Добавить(ТекущаяСтрокаРезультат);
		МассивПараметров.Добавить(Новый УникальныйИдентификатор());
		МассивПараметров.Добавить("");
		
		ТекстПечатнойформы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Уведомление о перемещении. Лист %1'"), НомерЛиста);
		СписокПечатныхФорм.Добавить(МассивПараметров, ТекстПечатнойформы);
		
	КонецЦикла;
	
	Возврат СписокПечатныхФорм;
	
КонецФункции

// Формирует данные по контрагенту
// документа УведомлениеОПеремещенииПрослеживаемыхТоваров
//
// Возвращаемое значение:
//  Строка - текст запроса
//
// Формирует структуру данных контрагента, необходимых для печати
// Уведомления о перемещении прослеживаемых товаров
//
// Параметры:
// Контрагент - СправочникСсылка.Контрагенты - Контрагент 
// 
// Возвращаемое значение:
//    Структура:
//		Признак - Строка - Признак физического лица, возможны значения: "V" - физ. лицо; "" - организация.
//		КодГосударства - Строка - Код государства-члена ЕАЭС по ОКСМ (3 цифры).
//		Наименование - Строка - Наименование организации (1 - 1000 символов).
//		Фамилия - Строка - Фамилия физического лица (1 - 60 символов).
//		Имя - Строка - Имя физического лица (1 - 60 символов).
//		Отчетсво - -  Отчество физического лица (1 - 60 символов).
//		ИдентификационныйКод - Строка -  Идентификационный код (номер) в государстве-члене ЕАЭС (8 - 14 символов).
//		Адрес - Строка - Адрес (Строка: 1 - 1000 символов).
//
Функция РеквизитыКонтрагента(Контрагент, ДатаСопроводительногоДокумента) Экспорт
	
	Реквизиты = Новый Структура("Признак,КодГосударства,Наименование,Фамилия,Имя,Отчетсво,ИдентификационныйКод,Адрес",
	 "","","","","","","","");
	
	ЭтоФизическоеЛицо = Контрагент.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо;
	
	ДанныеКонтрагента = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Контрагент, ДатаСопроводительногоДокумента);
	
	Если ЭтоФизическоеЛицо Тогда
		
		МассивФИО = ОбщегоНазначения.ПолучитьМассивФИО(ДанныеКонтрагента.ПолноеНаименование);
		
		Реквизиты["Признак"] = "V";
		Реквизиты["Фамилия"] = МассивФИО[0];
		Реквизиты["Имя"] = МассивФИО[1];
		Реквизиты["Отчетсво"] = МассивФИО[2];
		
	Иначе
		
		Реквизиты["Наименование"] = ДанныеКонтрагента.ПолноеНаименование;
		
	КонецЕсли;
	
	// todo across point
	Реквизиты["КодГосударства"] = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
		Контрагент.СтранаРегистрации, "Код");
		
	// todo too across point
	Реквизиты["ИдентификационныйКод"] = Контрагент.РегистрационныйНомер;
	
	Реквизиты["Адрес"] = ДанныеКонтрагента.ЮридическийАдрес;
	
	Возврат Реквизиты;
		
КонецФункции

// Формирует текст запроса по таблице товаров
// документа УведомлениеОПеремещенииПрослеживаемыхТоваров
//
// Возвращаемое значение:
//  Строка - текст запроса
//
Функция ТекстЗапросаПоТаблицамУведомленияОПеремещенииПрослеживаемыхТоваров() Экспорт
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	УведомлениеОПеремещенииПрослеживаемыхТоваровКонтрагенты.Контрагент КАК Контрагент,
		|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.СопроводительныйДокумент КАК СопроводительныйДокумент,
		|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.НомерСтроки КАК ПорядковыйНомер,
		|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Номенклатура КАК Номенклатура,
		|	МАКСИМУМ(УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Номенклатура.Наименование) КАК НоменклатураНаименование,
		|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.КодТНВЭД КАК КодТНВЭД,
		|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.ЕдиницаИзмерения.ЕдиницаПоКлассификатору КАК ЕдиницаИзмерения,
		|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.ЕдиницаПрослеживаемости.ЕдиницаПоКлассификатору КАК ЕдиницаПрослеживаемости,
		|	МАКСИМУМ(УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Количество) КАК Количество,
		|	МАКСИМУМ(УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости,
		|	МАКСИМУМ(УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Сумма) КАК Сумма,
		|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.РНПТ КАК РНПТ,
		|	МАКСИМУМ(УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Ссылка.Номер) КАК НомерСопроводительногоДокумента,
		|	МАКСИМУМ(УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Ссылка.Дата) КАК ДатаСопроводительногоДокумента,
		|	МАКСИМУМ(УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.КодТНВЭД.Код) КАК КодТНВЭДКод,
		|	МАКСИМУМ(УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код) КАК ЕдиницаИзмеренияКод,
		|	МАКСИМУМ(УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.ЕдиницаПрослеживаемости.ЕдиницаПоКлассификатору.Код) КАК ЕдиницаПрослеживаемостиКод,
		|	МАКСИМУМ(УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.РНПТ.Код) КАК РНПТКод
		|ИЗ
		|	Документ.УведомлениеОПеремещенииПрослеживаемыхТоваров.Контрагенты КАК УведомлениеОПеремещенииПрослеживаемыхТоваровКонтрагенты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОПеремещенииПрослеживаемыхТоваров.Товары КАК УведомлениеОПеремещенииПрослеживаемыхТоваровТовары
		|		ПО УведомлениеОПеремещенииПрослеживаемыхТоваровКонтрагенты.Ссылка = УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Ссылка
		|			И УведомлениеОПеремещенииПрослеживаемыхТоваровКонтрагенты.КлючСтроки = УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.КлючСтроки
		|ГДЕ
		|	УведомлениеОПеремещенииПрослеживаемыхТоваровКонтрагенты.Ссылка = &Документ
		|
		|СГРУППИРОВАТЬ ПО
		|	УведомлениеОПеремещенииПрослеживаемыхТоваровКонтрагенты.Контрагент,
		|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.СопроводительныйДокумент,
		|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.НомерСтроки,
		|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.Номенклатура,
		|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.КодТНВЭД,
		|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.РНПТ,
		|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.ЕдиницаИзмерения.ЕдиницаПоКлассификатору,
		|	УведомлениеОПеремещенииПрослеживаемыхТоваровТовары.ЕдиницаПрослеживаемости.ЕдиницаПоКлассификатору
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСопроводительногоДокумента,
		|	ПорядковыйНомер
		|ИТОГИ ПО
		|	Контрагент,
		|	СопроводительныйДокумент,
		|	ПорядковыйНомер";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ЗаполнитьРаздел1(ТаблицаРаздел1, Документ)

	// Схема таблицы ТаблицаРаздел1
	// Данные - Структура данные контрагента
	// ДанныеМногострочныхЧастей - Дерево значений -  Содержит две таблицы  П00001М1, П00001М1
	//
	//	П00001М1 - дерево значений - содержит "сведения о сопроводительном документе"
	//		Данные - Структура - 
	//                 П00001М100001 - Строка - Возможны значения: 1-счет-фактура, 2-УПД, 3-Иное.
	//                 П00001М100002 - Строка - номер докуента
	//                 П00001М100003 - Дата   - дата документа
	//		ДанныеМногострочныхЧастей - Структура
	//
	//	П00001М2 - дерево значений - содержатся "Сведения о пунктах назначения товара, подлежащего прослеживаемости,
	//                        на территории государств-членов ЕАЭС в соответствии с сопроводительным документом"
	//		Данные - Структура - показатели:
	//					П00001М200001 - Строка -  пункт назначения
	//      ДанныеМногострочныхЧастей - Структура
	//
	//	П00001М3 - дерево значений - содержатся строки по товарам
	//		Данные - Структура - показатели:
	//                 П00001М300001 - Число  - порядковый номер номенклатуры в сопроводительном документе
	//                 П00001М300002 - Строка - наименование товара
	//                 П00001М300003 - Число  - количество
	//                 П00001М300004 - Строка - единица измерения
	//                 П00001М300005 - Строка - РНПТ.
	//                 П00001М300006 - Строка - единица измерения прослеживаемости
	//                 П00001М300007 - Число  - количество прослеживаемости.
	//                 П00001М300008 - Число  - сумма без НДС
	//
	//      ДанныеМногострочныхЧастей - Структура
	
	// Формируем запрос по данным
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаПоТаблицамУведомленияОПеремещенииПрослеживаемыхТоваров();
	Запрос.УстановитьПараметр("Документ", Документ);
	Выборка = Запрос.Выполнить();
	
	Если Выборка.Пустой() Тогда
		Возврат ;
	КонецЕсли;
		
	Результат = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	// Формируем шаблоны таблицы заполнения
	Раздел1 = ТаблицаРаздел1.Строки[0];
	ШаблонДанныеРазделДанныеКонтрагента = Раздел1.Данные;
	ШаблонДанныеМногострочныхЧастейРаздел1 = Раздел1.ДанныеМногострочныхЧастей;
	
	ШаблонТаблицаП00001М1СопроводительныйДокумент = ШаблонДанныеМногострочныхЧастейРаздел1.П00001М1.Скопировать();
		ШаблонДанныеТаблицаП00001М1СопроводительныйДокумент = ШаблонТаблицаП00001М1СопроводительныйДокумент.Строки[0].Данные;
		
	ШаблонТаблицаП00001М2ПунктыНазначенияТоваров = ШаблонДанныеМногострочныхЧастейРаздел1.П00001М2.Скопировать();
		ШаблонДанныеТаблицаП00001М2ПунктыНазначенияТовара = ШаблонТаблицаП00001М2ПунктыНазначенияТоваров.Строки[0].Данные;
		
	ШаблонТаблицаП00001М3Товары = ШаблонДанныеМногострочныхЧастейРаздел1.П00001М3.Скопировать();
		ШаблонДанныеТаблицаП00001М3Товары = ШаблонТаблицаП00001М3Товары.Строки[0].Данные;
	
	ПрослеживаемостьФормированиеОтчетности.ОчиститьЗначенияПоказателей(ШаблонДанныеРазделДанныеКонтрагента);
	ПрослеживаемостьФормированиеОтчетности.ОчиститьЗначенияПоказателей(
		ШаблонДанныеТаблицаП00001М1СопроводительныйДокумент);
	ПрослеживаемостьФормированиеОтчетности.ОчиститьЗначенияПоказателей(ШаблонДанныеТаблицаП00001М2ПунктыНазначенияТовара);
	ПрослеживаемостьФормированиеОтчетности.ОчиститьЗначенияПоказателей(ШаблонДанныеТаблицаП00001М3Товары);
	
	ШаблонТаблицаП00001М1СопроводительныйДокумент.Строки.Очистить();
	ШаблонТаблицаП00001М2ПунктыНазначенияТоваров.Строки.Очистить();
	ШаблонТаблицаП00001М3Товары.Строки.Очистить();
	ТаблицаРаздел1.Строки.Очистить();
	
	// Заполняем таблицу Раздел1 данными
	
	// Выборка по контрагенту
	Пока Результат.Следующий() Цикл
		
		Раздел1НоваяСтрока = ТаблицаРаздел1.Строки.Добавить();
		Раздел1НоваяСтрока.Данные = 
			ПрослеживаемостьФормированиеОтчетности.НоваяСтруктура(ШаблонДанныеРазделДанныеКонтрагента);
		Раздел1НоваяСтрока.ДанныеМногострочныхЧастей = 
			ПрослеживаемостьФормированиеОтчетности.НоваяСтруктура(ШаблонДанныеМногострочныхЧастейРаздел1);
		
		ДанныеРаздел1 = Раздел1НоваяСтрока.Данные;
		ДанныеМногострочныхЧастейРаздел1 = Раздел1НоваяСтрока.ДанныеМногострочныхЧастей;
		
		ДанныеКонтрагента = РеквизитыКонтрагента(Результат.Контрагент, Результат.ДатаСопроводительногоДокумента);
		ДанныеРаздел1.П000010000100 = ДанныеКонтрагента["Признак"]; // Признак физического лица, возможны значения: "V" - физ. лицо; "" - организация.
		ДанныеРаздел1.П000010000200 = ДанныеКонтрагента["Наименование"]; // Наименование организации (1 - 1000 символов).
		ДанныеРаздел1.П000010000300 = ДанныеКонтрагента["Фамилия"]; // Фамилия физического лица (1 - 60 символов).
		ДанныеРаздел1.П000010000400 = ДанныеКонтрагента["Имя"]; // Имя физического лица (1 - 60 символов).
		ДанныеРаздел1.П000010000500 = ДанныеКонтрагента["Отчетсво"]; // Отчество физического лица (1 - 60 символов).
		ДанныеРаздел1.П000010000600 = ДанныеКонтрагента["КодГосударства"]; // Код государства-члена ЕАЭС по ОКСМ (3 цифры).
		ДанныеРаздел1.П000010000700 = ДанныеКонтрагента["ИдентификационныйКод"]; // Идентификационный код (номер) в государстве-члене ЕАЭС (8 - 14 символов).
		ДанныеРаздел1.П000010000800 = ДанныеКонтрагента["Адрес"]; // Адрес (Строка: 1 - 1000 символов).
		
		// Добавляем в таблицу данные по сопроводительным документам
		ВыборкаПоСопроводительнымДокументам = Результат.Выбрать(
			ОбходРезультатаЗапроса.ПоГруппировкам, "СопроводительныйДокумент"
			);
		
		СписокПунктовНазначения = ДобавитьДанныеПоСопроводительномуДокументуПоКонтрагенту(
																	ДанныеМногострочныхЧастейРаздел1,
																	ШаблонТаблицаП00001М1СопроводительныйДокумент,
																	ШаблонДанныеТаблицаП00001М1СопроводительныйДокумент,
																	ВыборкаПоСопроводительнымДокументам);
		
		ДобавитьДанныеПоПунктуНазначенияПоКонтрагенту(
			ДанныеМногострочныхЧастейРаздел1,
			ШаблонТаблицаП00001М2ПунктыНазначенияТоваров,
			ШаблонДанныеТаблицаП00001М2ПунктыНазначенияТовара,
			СписокПунктовНазначения);
		
		// Добавляем в таблицу данные по товарам
		ВыборкаНомерСтроки = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ПорядковыйНомер");
			
		ДобавитьДанныеПоТоварамПоКонтрагенту(
			ДанныеМногострочныхЧастейРаздел1,
			ШаблонТаблицаП00001М3Товары,
			ШаблонДанныеТаблицаП00001М3Товары,
			ВыборкаНомерСтроки);
	
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьДанныеПоПунктуНазначенияПоКонтрагенту(ДанныеМногострочныхЧастейРаздел1,
			ШаблонТаблицаП00001М2ПунктыНазначенияТоваров, ШаблонДанныеТаблицаП00001М2ПунктыНазначенияТовара,
			ВыборкаПоПунктамНазначения)

		ДанныеМногострочныхЧастейРаздел1.П00001М2 = ШаблонТаблицаП00001М2ПунктыНазначенияТоваров.Скопировать();
		ПунктыНазначения = ДанныеМногострочныхЧастейРаздел1.П00001М2;
		
		Для каждого ТекущаяСтрока Из ВыборкаПоПунктамНазначения Цикл

			ПунктыНазначенияНоваяСтрока = ПунктыНазначения.Строки.Добавить();
			ПунктыНазначенияНоваяСтрока.Данные = 
				ПрослеживаемостьФормированиеОтчетности.НоваяСтруктура(ШаблонДанныеТаблицаП00001М2ПунктыНазначенияТовара);
			ПунктыНазначенияНоваяСтрока.Данные.П00001М200001 = ТекущаяСтрока.Значение; 
			
		КонецЦикла;
			
КонецПроцедуры

Функция ДобавитьДанныеПоСопроводительномуДокументуПоКонтрагенту(ДанныеМногострочныхЧастейРаздел1,
			ШаблонТаблицаП00001М1СопроводительныйДокумент, ШаблонДанныеТаблицаП00001М1СопроводительныйДокумент,
			ВыборкаПоСопроводительнымДокументам)

		ДанныеМногострочныхЧастейРаздел1.П00001М1 = ШаблонТаблицаП00001М1СопроводительныйДокумент.Скопировать();
		СопроводительныеДокументы = ДанныеМногострочныхЧастейРаздел1.П00001М1;
		
		СписокПунктовНазначения = Новый СписокЗначений;
		
		Пока ВыборкаПоСопроводительнымДокументам.Следующий() Цикл

			ДанныеСтроки = ПолучитьДанныеСтрокиВыборки(ВыборкаПоСопроводительнымДокументам);
			
			СопроводительныйДокументНоваяСтрока = СопроводительныеДокументы.Строки.Добавить();
			СопроводительныйДокументНоваяСтрока.Данные = 
				ПрослеживаемостьФормированиеОтчетности.НоваяСтруктура(ШаблонДанныеТаблицаП00001М1СопроводительныйДокумент);
			СопроводительныйДокументНоваяСтрока.Данные.П00001М100001 = 2;  // Возможны значения: 1 - СФ, 2 - УПД, 3 - Иное.
			СопроводительныйДокументНоваяСтрока.Данные.П00001М100002 = ДанныеСтроки["НомерСопроводительногоДокумента"]; // Строка - графа 2 (1 - 255 символов).
			СопроводительныйДокументНоваяСтрока.Данные.П00001М100003 = ДанныеСтроки["ДатаСопроводительногоДокумента"]; // графа 3, дата
			
			Если ЗначениеЗаполнено(ДанныеСтроки.АдресДоставки)
				И СписокПунктовНазначения.НайтиПоЗначению(ДанныеСтроки.АдресДоставки) = Неопределено Тогда
				
				СписокПунктовНазначения.Добавить(ДанныеСтроки.АдресДоставки);
			
			КонецЕсли;
			
		КонецЦикла;
		
		
		Возврат СписокПунктовНазначения;
		
КонецФункции

// Формирует данные многострочной строки по таблице товаров
// документа УведомлениеОПеремещенииПрослеживаемыхТоваров
//
// Возвращаемое значение:
//  Структура:
//			- НомерПоПорядкуТоваров - Число  - графа 1 (0 - 999)
//			- НаименованиеСопроводительногоДокумента - Строка - графа 2 (1 - 255 символов)
//			- НомерСопроводительногоДокумента - Строка - графа 3 (1 - 255 символов)
//			- ДатаСопроводительногоДокумента - Дата   - графа 4.
//			- КодТНВЭД - Строка - графа 6 (6 - 10 символов)
//			- ПорядковыйНомерСопроводительногоДокумента - Число - графа 7 (0 - 9999)
//			- НаименованиеТовара - Строка - графа 8 (1 - 255 символов)
//			- Количество - Число  - графа 9 (0 - 9999999999999.999999)
//			- КодЕдиницаИзмерения - Строка - графа 10 (3 - 4 цифры)
//			- КоличествоПрослеживаемости - Число  - графа 11 (0 - 999999999999999.99999999999)
//			- ЕдиницаПрослеживаемости - Строка - графа 12 (3 - 4 цифры)
//			- РНПТ - Строка - графа 13 (27 - 29 смволов)
//			- АдресДоставки - Строка - графа 14 (1 - 255 символов)
//			- СуммаБезНДС - Число  - графа 15 (0 - 99999999999999999.99)
//
Функция ПолучитьДанныеСтрокиВыборки(ВыборкаНомерСтроки) Экспорт
	
	ДанныеСтроки = Новый Структура();
	
	НаименованиеСопроводительногоДокумента = "";
	АдресДоставки = "";
	
	Если ТипЗНЧ(ВыборкаНомерСтроки.СопроводительныйДокумент) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		
		НаименованиеСопроводительногоДокумента = "Накладная";
		
		АдресДоставки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			ВыборкаНомерСтроки.СопроводительныйДокумент, "АдресДоставки");
			
	КонецЕсли;
		
	ДанныеСтроки.Вставить("НаименованиеСопроводительногоДокумента", НаименованиеСопроводительногоДокумента);
	ДанныеСтроки.Вставить("НомерСопроводительногоДокумента", ВыборкаНомерСтроки.НомерСопроводительногоДокумента);
	ДанныеСтроки.Вставить("ДатаСопроводительногоДокумента", ВыборкаНомерСтроки.ДатаСопроводительногоДокумента);
	
	ДанныеСтроки.Вставить("КодТНВЭД", ВыборкаНомерСтроки.КодТНВЭДКод);
	ДанныеСтроки.Вставить("ПорядковыйНомерВСопроводительномДокументе", ВыборкаНомерСтроки.ПорядковыйНомер);
	ДанныеСтроки.Вставить("НаименованиеТовара", ВыборкаНомерСтроки.НоменклатураНаименование);
	ДанныеСтроки.Вставить("Количество", ВыборкаНомерСтроки.Количество);
	ДанныеСтроки.Вставить("КодЕдиницаИзмерения", ВыборкаНомерСтроки.ЕдиницаИзмеренияКод);
	ДанныеСтроки.Вставить("КоличествоПрослеживаемости", ВыборкаНомерСтроки.КоличествоПрослеживаемости);
	ДанныеСтроки.Вставить("ЕдиницаПрослеживаемости", ВыборкаНомерСтроки.ЕдиницаПрослеживаемостиКод);
	ДанныеСтроки.Вставить("РНПТ", ВыборкаНомерСтроки.РНПТКод);
	ДанныеСтроки.Вставить("СуммаБезНДС", ВыборкаНомерСтроки.Сумма);
	
	ДанныеСтроки.Вставить("АдресДоставки", АдресДоставки);
	
	Возврат ДанныеСтроки;
	
КонецФункции

Процедура ДобавитьДанныеПоТоварамПоКонтрагенту(ДанныеМногострочныхЧастейРаздел1, ШаблонТаблицаП00001М3Товары,
			ШаблонДанныеТаблицаП00001М3Товары, ВыборкаНомерСтроки)
			
		ДанныеМногострочныхЧастейРаздел1.П00001М3 = ШаблонТаблицаП00001М3Товары.Скопировать();
		Товары = ДанныеМногострочныхЧастейРаздел1.П00001М3;
		
		НомерСтроки = 0;
		Пока ВыборкаНомерСтроки.Следующий() Цикл

			ДанныеСтроки = ПолучитьДанныеСтрокиВыборки(ВыборкаНомерСтроки);
			
			НомерСтроки = НомерСтроки + 1;
			
			ТоварыНоваяСтрока = Товары.Строки.Добавить();
			ТоварыНоваяСтрока.Данные = ПрослеживаемостьФормированиеОтчетности.НоваяСтруктура(ШаблонДанныеТаблицаП00001М3Товары);
			ТоварыНоваяСтрока.Данные.П00001М300001 = ДанныеСтроки["ПорядковыйНомерВСопроводительномДокументе"]; // - Число  - графа 1 (0 - 999)
			ТоварыНоваяСтрока.Данные.П00001М300002 = ДанныеСтроки["НаименованиеТовара"]; //- Строка - графа 2 (1 - 255 символов).
			ТоварыНоваяСтрока.Данные.П00001М300003 = ДанныеСтроки["Количество"]; // Число  - графа 3 (0 - 9999999999999.999999).
			ТоварыНоваяСтрока.Данные.П00001М300004 = ДанныеСтроки["КодЕдиницаИзмерения"]; // - Строка - графа 4 (3 - 4 цифры).
			ТоварыНоваяСтрока.Данные.П00001М300005 = ДанныеСтроки["РНПТ"]; // - Строка - графа 5 (1 - 29 смволов).
			ТоварыНоваяСтрока.Данные.П00001М300006 = ДанныеСтроки["ЕдиницаПрослеживаемости"]; // - Строка - графа 6 (3 - 4 цифры).
			ТоварыНоваяСтрока.Данные.П00001М300007 = ДанныеСтроки["КоличествоПрослеживаемости"]; //Число  - графа 7 (0 - 999999999999999.99999999999).
			ТоварыНоваяСтрока.Данные.П00001М300008 = ДанныеСтроки["СуммаБезНДС"]; //- Число  - графа 8 (0 - 99999999999999999.99).
			
		КонецЦикла;
			
КонецПроцедуры

Функция ЗаполнитьДополнительныеРеквизиты(Контейнер, Документ)
	
	ПараметрыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Документ, "Организация,Дата,Номер");
	
	Контейнер.Организация = Документ.Организация;
	Контейнер.НалоговыйОрган = ПрослеживаемостьБРУ.КодГосударственногоОрганаОрганизации(
			ПараметрыДокумента.Организация);
			
	Контейнер.ДатаУведомления = ПараметрыДокумента.Дата;
	Контейнер.ДатаПодписи = ПараметрыДокумента.Дата;
	
	Контейнер.КПП = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыДокумента.Организация, "КПП");
	Контейнер.НомерУведомления = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ПараметрыДокумента.Номер, Истина, Ложь);
	Контейнер.ДатаУведомления = ПараметрыДокумента.Дата;
	
КонецФункции

Функция ЗаполнитьДокументДанными(Период, Организация, ДокументОснование = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РеализацияПрослеживаемыхТоваровОстатки.Контрагент КАК Контрагент,
	|	РеализацияПрослеживаемыхТоваровОстатки.СопроводительныйДокумент КАК СопроводительныйДокумент,
	|	РеализацияПрослеживаемыхТоваровОстатки.Номенклатура КАК Номенклатура,
	|	РеализацияПрослеживаемыхТоваровОстатки.РНПТ КАК РНПТ,
	|	РеализацияПрослеживаемыхТоваровОстатки.ПорядковыйНомер КАК ПорядковыйНомер,
	|	РеализацияПрослеживаемыхТоваровОстатки.КоличествоОстаток КАК КоличествоОстаток,
	|	РеализацияПрослеживаемыхТоваровОстатки.КоличествоПрослеживаемостиОстаток КАК КоличествоПрослеживаемостиОстаток,
	|	РеализацияПрослеживаемыхТоваровОстатки.СуммаОстаток КАК СуммаОстаток,
	|	ВЫРАЗИТЬ(РеализацияПрослеживаемыхТоваровОстатки.Номенклатура КАК Справочник.Номенклатура).КодТНВЭД КАК КодТНВЭД,
	|	РеализацияПрослеживаемыхТоваровОстатки.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВЫРАЗИТЬ(РеализацияПрослеживаемыхТоваровОстатки.Номенклатура КАК Справочник.Номенклатура).ЕдиницаИзмеренияПрослеживаемости КАК ЕдиницаИзмеренияПрослеживаемости
	|ПОМЕСТИТЬ ВТ_ВыборкаДанных
	|ИЗ
	|	РегистрНакопления.ПрослеживаемыеТоварыОтгруженныеВЕАЭС.Остатки(&Период, Организация = &Организация) КАК РеализацияПрослеживаемыхТоваровОстатки
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ДокументОснование <> НЕОПРЕДЕЛЕНО
	|				ТОГДА РеализацияПрослеживаемыхТоваровОстатки.СопроводительныйДокумент = &ДокументОснование
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ВыборкаДанных.Контрагент КАК Контрагент
	|ИЗ
	|	ВТ_ВыборкаДанных КАК ВТ_ВыборкаДанных
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ВыборкаДанных.Контрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ВыборкаДанных.Номенклатура КАК Номенклатура,
	|	ВТ_ВыборкаДанных.РНПТ КАК РНПТ,
	|	ВТ_ВыборкаДанных.Контрагент КАК Контрагент,
	|	ВТ_ВыборкаДанных.ПорядковыйНомер КАК ПорядковыйНомер,
	|	ВТ_ВыборкаДанных.КоличествоОстаток КАК Количество,
	|	ВТ_ВыборкаДанных.КоличествоПрослеживаемостиОстаток КАК КоличествоПрослеживаемости,
	|	ВТ_ВыборкаДанных.СуммаОстаток КАК Сумма,
	|	ВТ_ВыборкаДанных.СопроводительныйДокумент КАК СопроводительныйДокумент,
	|	ВТ_ВыборкаДанных.КодТНВЭД КАК КодТНВЭД,
	|	ВТ_ВыборкаДанных.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТ_ВыборкаДанных.ЕдиницаИзмеренияПрослеживаемости КАК ЕдиницаПрослеживаемости
	|ИЗ
	|	ВТ_ВыборкаДанных КАК ВТ_ВыборкаДанных";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	// В таблице товары СопроводительныйДокумент показывает сигнал к новому ключу
	ТаблицаКонтрагенты = РезультатЗапроса[1].Выгрузить();
	ТаблицаКонтрагенты.Колонки.Добавить("КлючСтроки", Новый ОписаниеТипов("УникальныйИдентификатор"));
	ТаблицаКонтрагенты.Колонки.Добавить("ЕдиницыПрослеживаемостиСовпадают", Новый ОписаниеТипов("Булево"));
	
	ТаблицаТовары = РезультатЗапроса[2].Выгрузить();
	ТаблицаТовары.Колонки.Добавить("КлючСтроки", Новый ОписаниеТипов("УникальныйИдентификатор"));
	
	ФильтрОтбора = Новый Структура("Контрагент");
	
	Для каждого ТекущаСтрокаКонтрагенты Из ТаблицаКонтрагенты Цикл
		
		ЗаполнитьЗначенияСвойств(ФильтрОтбора, ТекущаСтрокаКонтрагенты);
		ТекущаСтрокаКонтрагенты.КлючСтроки = Новый УникальныйИдентификатор();
		
		ТекущаСтрокаКонтрагенты.ЕдиницыПрослеживаемостиСовпадают = Истина;
		
		НаденныйСтрокиТовары = ТаблицаТовары.НайтиСтроки(ФильтрОтбора);
		
		Для каждого ТекущаяСтрокаТовары Из НаденныйСтрокиТовары Цикл
			
			ТекущаяСтрокаТовары.КлючСтроки = ТекущаСтрокаКонтрагенты.КлючСтроки;
			
			Если ТекущаяСтрокаТовары.ЕдиницаИзмерения <> ТекущаяСтрокаТовары.ЕдиницаПрослеживаемости Тогда
				
				ТекущаСтрокаКонтрагенты.ЕдиницыПрослеживаемостиСовпадают = Ложь;
				
			КонецЕсли;
		
		КонецЦикла;
	КонецЦикла;
	
	Возврат Новый Структура("Контрагенты,Товары", ТаблицаКонтрагенты, ТаблицаТовары)
	
КонецФункции

Функция ТекстЗапросаРеквизитыДокумента(НомераТаблиц)
	
	НомераТаблиц.Вставить("ВременнаяТаблицаРеквизиты", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Реквизиты", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация
	|ПОМЕСТИТЬ Реквизиты
	|ИЗ
	|	Документ.УведомлениеОПеремещенииПрослеживаемыхТоваров КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Период КАК Период,
	|	Реквизиты.Организация КАК Организация
	|ИЗ
	|	Реквизиты КАК Реквизиты";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПараметрыУведомления(Документ)
	
	ПараметрыВыгрузки = Новый Структура();
	ПараметрыВыгрузки.Вставить("ИсточникОтчета", "РегламентированныйОтчетПрослеживаемыеТоварыПеремещение");
	ПараметрыВыгрузки.Вставить("ВыбраннаяФорма", "ФормаОтчета2021Кв3");
	// todo temp
	//	ПараметрыВыгрузки.Вставить("ВерсияФормата",  "5.02");
	ПараметрыВыгрузки.Вставить("ВерсияФормата",  "5.01");
	ПараметрыВыгрузки.Вставить("СсылкаНаВнешнийОбъект", Документ);
	
	Возврат ПараметрыВыгрузки;
	
КонецФункции

#КонецОбласти

#КонецЕсли
