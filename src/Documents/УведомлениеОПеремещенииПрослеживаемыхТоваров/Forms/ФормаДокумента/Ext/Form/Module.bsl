
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если Параметры.Ключ.Пустая() Тогда
		ПодготовитьФормуНаСервере();
	КонецЕсли;
	
	ТекущаяОрганизация = Объект.Организация;
	
	УстановитьУсловноеОформление();
	
	ЗаполнитьВременныеРеквизиты();
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		
		ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
		ТекущиеДанные.СопроводительныйДокумент = ВыбранноеЗначение;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
		
	ПодготовитьФормуНаСервере();
		
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	//ПрослеживаемостьСобытияФормКлиентПереопределяемый.ПередЗаписью(ЭтотОбъект, Отказ, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если Не ПараметрыЗаписи.Свойство("СопроводительныеДокументы") Тогда
		ПараметрыЗаписи.Вставить("СопроводительныеДокументы", ТекущийОбъект.Товары.ВыгрузитьКолонку("СопроводительныйДокумент"));
	КонецЕсли;
		
	ЗаполнитьВременныеРеквизиты();
		
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_УведомлениеОПеремещенииПрослеживаемыхТоваров", ПараметрыЗаписи, Объект.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Если Объект.Контрагенты.Количество() <> 0
		ИЛИ Объект.Товары.Количество() <> 0 Тогда

		ТекстВопроса = НСтр("ru = 'Необходимо очистить табличные части. Продолжить?'");
		Оповещение = Новый ОписаниеОповещения("ВопросПриИзмененииОрганизацииЗавершение", ЭтаФорма);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
	Иначе
		ТекущаяОрганизация = Объект.Организация;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКонтрагенты

&НаКлиенте
Процедура КонтрагентыПриАктивизацииСтроки(Элемент)
	
	ДанныеКонтрагентыУстановитьОтборСтрок();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентыКонтрагентПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Контрагенты.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено
		ИЛИ ТекущиеДанные.Контрагент = ТекущийКонтрагент Тогда
		
		Возврат;
		
	КонецЕсли;
	
	НайденныеСтроки = Объект.Товары.НайтиСтроки(Новый Структура("КлючСтроки", ТекущиеДанные.КлючСтроки));
	
	Если НайденныеСтроки.Количество() <> 0 Тогда

		Для каждого ТекущаяСтрока Из НайденныеСтроки Цикл
			
			Объект.Товары.Удалить(ТекущаяСтрока);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентыПередУдалением(Элемент, Отказ)
	
	ВыделенныеСтроки = Элементы.Контрагенты.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Для каждого ИдСтроки Из ВыделенныеСтроки Цикл
		ТекДанные = Объект.Контрагенты.НайтиПоИдентификатору(ИдСтроки);
		Если ТекДанные <> Неопределено Тогда
			// Необходимо очистить дополнительные сведения
			НоваяСтрока = ТаблицаУдаленныхКлючей.Добавить();
			НоваяСтрока.КлючСтроки = ТекДанные.КлючСтроки;
		КонецЕсли;
	КонецЦикла;
	
	ПодключитьОбработчикОжидания("Подключаемый_УдалитьСвязанныеЗаписи", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		ТекДанныеКонтрагенты = Элементы.Контрагенты.ТекущиеДанные;
		ТекДанныеКонтрагенты.КлючСтроки = Новый УникальныйИдентификатор();
		
		ДанныеКонтрагентыУстановитьОтборСтрок();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ДанныеСтрокиКонтрагента = Элементы.Контрагенты.ТекущиеДанные;
	
	Если ДанныеСтрокиКонтрагента = Неопределено Тогда
		Отказ = Истина;
		ТекстСообщения = НСтр("ru = 'Не выбрана строка в списке Контрагенты!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Контрагенты", "Объект", Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Номенклатура) Тогда
		
		ПараметрыНомеклатуры = ПрослеживаемостьФормыВызовСервера.ЕдиницыИзмеренийНоменклатуры(ТекущиеДанные.Номенклатура);
		ЗаполнитьЗначенияСвойств(ТекущиеДанные, ПараметрыНомеклатуры);
		
	КонецЕсли;
	
	ДанныеСтрокиКонтрагента = Элементы.Контрагенты.ТекущиеДанные;
	
	Если ТекущиеДанные.ЕдиницаИзмерения <> ТекущиеДанные.ЕдиницаПрослеживаемости Тогда
		
		ДанныеСтрокиКонтрагента.ЕдиницыПрослеживаемостиСовпадают = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьПоНоменклатуреЗначенияПоУмолчанию(Номенклатура)
	
	ИзменяемыеПоля = Новый Структура("КодТНВЭД,ЕдиницаИзмерения,ЕдиницаПрослеживаемости,РНПТ");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Номенклатура.КодТНВЭД КАК КодТНВЭД,
	|	Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВЫРАЗИТЬ(Номенклатура.КодТНВЭД КАК Справочник.КлассификаторТНВЭД).ЕдиницаИзмерения КАК ЕдиницаПрослеживаемости
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Номенклатура);
	Результат = Запрос.Выполнить().Выбрать();
	
	Если Результат.Следующий() Тогда
	
		ЗаполнитьЗначенияСвойств(ИзменяемыеПоля, Результат);
		
	Иначе
	
		ИзменяемыеПоля.КодТНВЭД = Справочники.КлассификаторТНВЭД.ПустаяСсылка();
		ИзменяемыеПоля.ЕдиницаИзмерения = Справочники.КлассификаторЕдиницИзмерения.ПустаяСсылка();
		ИзменяемыеПоля.ЕдиницаПрослеживаемости = Справочники.КлассификаторЕдиницИзмерения.ПустаяСсылка();
		
	КонецЕсли;
	
	ИзменяемыеПоля.РНПТ = "";
	
	Возврат ИзменяемыеПоля;
КонецФункции

&НаКлиенте
Процедура ТоварыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)

	ПриНачалеРедактированияСтрокиТоваров(
		Элементы.Товары.ТекущиеДанные, Элементы.Контрагенты.ТекущиеДанные, НоваяСтрока, Копирование);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриНачалеРедактированияСтрокиТоваров(ДанныеСтроки, ДанныеСтрокиКонтрагента, НоваяСтрока, Копирование)

	Если НоваяСтрока И ДанныеСтрокиКонтрагента <> Неопределено Тогда
		ДанныеСтроки.КлючСтроки = ДанныеСтрокиКонтрагента.КлючСтроки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСопроводительныйДокументНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.Контрагенты.ТекущиеДанные;
	
	ОткрытьФормуПодбораСопроводительногоДокумента(ЭтаФорма, ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если ТекущиеДанные.ЕдиницаИзмерения = ТекущиеДанные.ЕдиницаПрослеживаемости Тогда
		
		ТекущиеДанные.КоличествоПрослеживаемости = ТекущиеДанные.Количество;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаЗаполнить(Команда)
	
	ЕстьДанные = Объект.Контрагенты.Количество() > 0;
	
	Если ЕстьДанные 
		И Объект.Проведен Тогда
		
			ТекстВопроса = НСтр("ru = 'Перед заполнением проведение документа будет отменено, а табличные части будут очищены. Заполнить?'");
		Оповещение = Новый ОписаниеОповещения("ВопросЗаполнитьДокументЗавершение", ЭтаФорма, Новый Структура);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		ВыполнитьЗаполнениеДокумента();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыгрузитьУведомлениеВXML(Команда)
	
	Если Модифицированность Тогда
		Записать();
	КонецЕсли;
		
	ПрослеживаемостьКлиент.ВыгрузитьВФайлУведомлениеПоПрослеживаемости(Объект.Ссылка);

КонецПроцедуры

&НаКлиенте
Процедура ОтправитьВКонтролирующийОрган(Команда)
	
	Если Модифицированность Тогда
		Записать();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьЦиклыОбмена(Команда)
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПечать(Команда)
	
	Если Модифицированность 
		ИЛИ НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Записать();
	КонецЕсли;
	
	СписокПечатаемыхЛистов = 
		Документы.УведомлениеОПеремещенииПрослеживаемыхТоваров.ПолучитьСписокПечатныхФормУведомлениеОПеремещенииПрослеживаемыхТоваров(Объект.Ссылка);
	
	ОткрытьФормуПредварительногоПросмотра(СписокПечатаемыхЛистов);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуПредварительногоПросмотра(СписокПечатаемыхЛистов)

	ПредПросмотр = РегламентированнаяОтчетность.роПолучитьОбщуюФорму("ПечатьРегламентированныхОтчетов", ЭтаФорма);
	Если  ПредПросмотр.Открыта() Тогда
		ПредПросмотр.Закрыть();
		ПредПросмотр = РегламентированнаяОтчетность.роПолучитьОбщуюФорму("ПечатьРегламентированныхОтчетов", ЭтаФорма);
	КонецЕсли;
	ПредПросмотр.ЗакрыватьПриЗакрытииВладельца = Ложь;
	
	ЭтаФорма.Заголовок = ПрослеживаемостьБРУ.ПредставлениеДокумента(Объект.Ссылка);
	НомерЛиста = 1;
	ПредПросмотр.СписокПечатаемыхЛистов.Очистить();
	Для Каждого Эл Из СписокПечатаемыхЛистов Цикл
		НовСтр = ПредПросмотр.СписокПечатаемыхЛистов.Добавить();
		НовСтр.Наименование = Эл.Представление;
		НовСтр.ТабличныйДокумент = ПолучитьИзВременногоХранилища(Эл.Значение[0]).Значение;
		НовСтр.Идентификатор = ЭтаФорма; 
		НомерЛиста = НомерЛиста + 1;
	КонецЦикла;
	ПредПросмотр.ВидПечати = "Показать бланк";
	ПредПросмотр.Открыть();
	
	ЭтаФорма.Заголовок = "";
	
КонецПроцедуры 

&НаКлиенте
Процедура ПроверитьВИнтернете(Команда)
	
	Если Модифицированность Тогда
		Записать();
	КонецЕсли;
		
КонецПроцедуры

#Область ПанельОтправкиВКонтролирующиеОрганы

&НаКлиенте
Процедура ОбновитьСостояниеОтправки(Команда)
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПротоколОтправки(Команда)
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		КонтекстЭДО.ОткрытьПротоколОтправкиУведомленияОКонтролируемыхСделках(ЭтаФорма);
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОткрытьФормуПодбораСопроводительногоДокумента(Форма, ТекущиеДанные) Экспорт
	
	ПараметрыФормы = Новый Структура("Организация, Контрагент", Объект.Организация, ТекущиеДанные.Контрагент);
	
	ОткрытьФорму("Документ.РеализацияТоваровУслуг.Форма.ФормаВыбора", ПараметрыФормы, Форма);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПриИзмененииОрганизацииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Объект.Товары.Очистить();
		Объект.Контрагенты.Очистить();
	Иначе
		Объект.Организация = ТекущаяОрганизация;
	КонецЕсли;
	
	ТекущаяОрганизация = Объект.Организация;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросЗаполнитьДокументЗавершение(Результат, ДополнительныйПараметр) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ВыполнитьЗаполнениеДокумента();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗаполнениеДокумента()

	ДлительнаяОперация = ЗаполнитьДокументНаСервере();
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус <> "Ошибка" Тогда
		
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтаФорма);
		НастройкиОжидания.ВыводитьОкноОжидания = Истина;
		НастройкиОжидания.ТекстСообщения = НСтр("ru='Выполняется заполнение документа'");
		
		Обработчик = Новый ОписаниеОповещения("ЗаполнитьДокументЗавершение", ЭтаФорма);
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
		
	Иначе
		
		ПоказатьОшибкуЗаполненияДокумента(ДлительнаяОперация);
		
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДокументЗавершение(ДлительнаяОперация, ДополнительныеПараметры) Экспорт

	Если ДлительнаяОперация = Неопределено Тогда
		Возврат; // Отменено
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		
		ЗагрузитьДанныеВДокумент(ДлительнаяОперация.АдресРезультата);
		
	Иначе
		ПоказатьОшибкуЗаполненияДокумента(ДлительнаяОперация);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДанныеВДокумент(АдресРезультата)
	
	РезультатЗаполненияТаблиц = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	Объект.Контрагенты.Загрузить(РезультатЗаполненияТаблиц.Контрагенты);
	Объект.Товары.Загрузить(РезультатЗаполненияТаблиц.Товары);
	
	Если Объект.Контрагенты.Количество() > 0 Тогда
		Элементы.Контрагенты.ТекущаяСтрока = Объект.Контрагенты[0].ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОшибкуЗаполненияДокумента(ДлительнаяОперация)
	
	МассивИзДвухСтрок = Новый Массив;
	МассивИзДвухСтрок.Добавить(НСтр("ru = 'Ошибка заполнения документа:'"));
	МассивИзДвухСтрок.Добавить(ДлительнаяОперация.КраткоеПредставлениеОшибки);
	
	ВызватьИсключение СтрСоединить(МассивИзДвухСтрок, Символы.ПС);
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьДокументНаСервере()
	
	Если Объект.Проведен Тогда
		Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.ОтменаПроведения));
	КонецЕсли;
	
	Объект.Контрагенты.Очистить();
	Объект.Товары.Очистить();
	
	ПараметрыОбработчика = Новый Структура("Организация, Дата",Объект.Организация, КонецДня(Объект.Дата));
	НастройкаЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкаЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Заполнение документа....'");
	НастройкаЗапуска.ОжидатьЗавершение = 0;
	НастройкаЗапуска.ЗапуститьНеВФоне = Истина;
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне("Документы.УведомлениеОПеремещенииПрослеживаемыхТоваров.ЗаполнитьДокумент",
		ПараметрыОбработчика,
		НастройкаЗапуска);

	Возврат ДлительнаяОперация;

КонецФункции

&НаКлиенте
Процедура Подключаемый_УдалитьСвязанныеЗаписи()
	
	УдалитьСвязанныеЗаписиНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура УдалитьСвязанныеЗаписиНаСервере()
	
	ВидыУдаляемыхСтрок = Новый Структура; 
	
	ВидыУдаляемыхСтрок.Вставить("Товары", ТаблицаУдаленныхКлючей);
	
	Для каждого ВидУдаляемыхСтрок Из ВидыУдаляемыхСтрок Цикл
		ИмяТабличнойЧасти 		= ВидУдаляемыхСтрок.Ключ;
		СписокУдаленныхКлючей 	= ВидУдаляемыхСтрок.Значение;
		Для каждого СтрокаКлюча Из СписокУдаленныхКлючей Цикл
			КлючПоиска = Новый Структура("КлючСтроки", СтрокаКлюча.КлючСтроки);
			МассивСтрокТовары = Объект[ИмяТабличнойЧасти].НайтиСтроки(КлючПоиска);
			
			Для каждого СтрокаТЧ Из МассивСтрокТовары Цикл
				Объект[ИмяТабличнойЧасти].Удалить(СтрокаТЧ);
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ДанныеКонтрагентыУстановитьОтборСтрок()
	
	ТекДанныеКонтрагенты = Элементы.Контрагенты.ТекущиеДанные;
	
	Если ТекДанныеКонтрагенты <> Неопределено Тогда
		КлючСтроки = ТекДанныеКонтрагенты.КлючСтроки;
		Элементы.Товары.ОтборСтрок = Новый ФиксированнаяСтруктура("КлючСтроки", КлючСтроки);
		
		ТекущийКонтрагент = ТекДанныеКонтрагенты.Контрагент;
		
	Иначе
		
		Элементы.Товары.ОтборСтрок = Новый ФиксированнаяСтруктура("КлючСтроки", 0);
		
		ТекущийКонтрагент = "";
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	// КонтрагентыСтраныПеревозкиПредставление

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КонтрагентыСтраныПеревозкиПредставление");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Контрагенты.СтраныПеревозкиПредставление", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не указана>'"));
	
	
	// ТоварыЕдиницаПрослеживаемости, ТоварыКоличествоПрослеживаемости
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыЕдиницаПрослеживаемости");
	ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыКоличествоПрослеживаемости");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Контрагенты.ЕдиницыПрослеживаемостиСовпадают", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
КонецПроцедуры

&НаСервере
// Добавляет в коллекцию оформляемых полей компоновки данных новое поле
//
// Параметры:
//	КоллекцияОформляемыхПолей 	- коллекция оформляемых полей КД
//	ИмяПоля						- Строка - имя поля
//
// Возвращаемое значение:
//	ОформляемоеПолеКомпоновкиДанных - созданное поле
//
// Пример:
// 	Форма.УсловноеОформление.Элементы[0].Поля
//
Функция ДобавитьОформляемоеПоле(КоллекцияОформляемыхПолей, ИмяПоля)
	
	ПолеЭлемента 		= КоллекцияОформляемыхПолей.Элементы.Добавить();
	ПолеЭлемента.Поле 	= Новый ПолеКомпоновкиДанных(ИмяПоля);

	Возврат ПолеЭлемента;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьВременныеРеквизиты()
	
	ФильтрПоТаблице = Новый Структура("КлючСтроки");
	
	Для каждого ТекущаяСтрокаКонтрагенты Из Объект.Контрагенты Цикл
		
		ФильтрПоТаблице.КлючСтроки = ТекущаяСтрокаКонтрагенты.КлючСтроки;
		
		// Если по этой реализации в табличной части товары Единица прослеживаемости = Единицы в документе, то Истина
		ТекущаяСтрокаКонтрагенты.ЕдиницыПрослеживаемостиСовпадают = 
			ЕдиницыПрослеживаемостиИЕдиницаВДокументеСовпадают(ФильтрПоТаблице);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ЕдиницыПрослеживаемостиИЕдиницаВДокументеСовпадают(ФильтрПоТаблице)
	
	НайденныеСтроки = Объект.Товары.НайтиСтроки(ФильтрПоТаблице);
	ЕдиницыПрослеживаемостиСовпадают = Истина;
	
	Если НайденныеСтроки.Количество() > 0 Тогда
		
		Для каждого ТекущаяСтрока Из НайденныеСтроки Цикл
		
			Если ТекущаяСтрока.ЕдиницаИзмерения <> ТекущаяСтрока.ЕдиницаПрослеживаемости Тогда
			
				ЕдиницыПрослеживаемостиСовпадают = Ложь;
			
			КонецЕсли;
		
		КонецЦикла;
	Иначе
		
		ЕдиницыПрослеживаемостиСовпадают = Ложь;
		
	КонецЕсли;
		
	Возврат ЕдиницыПрослеживаемостиСовпадают;
	
КонецФункции

#КонецОбласти
