#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "УведомлениеОбОстаткахПрослеживаемыхТоваров") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм, 
			"УведомлениеОбОстаткахПрослеживаемыхТоваров", 
			"Уведомление об остатках прослеживаемого товара",
			ПечатьУведомленияОбОстаткахПрослеживаемогоТовара(МассивОбъектов, ОбъектыПечати));
			
	КонецЕсли;
	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор  = "УведомлениеОбОстаткахПрослеживаемыхТоваров";
	КомандаПечати.Представление  = НСтр("ru = 'Печать'");
	КомандаПечати.ЗаголовокФормы = НСтр("ru = 'Уведомление об остатках прослеживаемых товаров'");
	КомандаПечати.Обработчик     = "ПрослеживаемостьФормыКлиент.ВыполнитьКомандуПечатиУведомленияОбОСтаткахПрослеживаемыхТоваров";
	КомандаПечати.СписокФорм     = "ФормаДокументаОсновная,ФормаДокументаКорректировочная,ФормаСписка";
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы <> "ФормаДокумента"
		И ВидФормы <> "ФормаОбъекта" Тогда
		Возврат;
	КонецЕсли;
	
	Если (Параметры.Свойство("Ключ") И ЗначениеЗаполнено(Параметры.Ключ)) Тогда
		ВидУведомления= ОпределитьВидУведомления(
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Ключ, "НомерКорректировки"));
	ИначеЕсли (Параметры.Свойство("Основание") И ЗначениеЗаполнено(Параметры.Основание)) Тогда
		ВидУведомления= ОпределитьВидУведомления(
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Основание, "НомерКорректировки"));
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	ФормыУведомлений = ПолучитьСоответствиеВидовУведомленийФормам();
	ВыбраннаяФорма = ФормыУведомлений[ВидУведомления];
	Если ВыбраннаяФорма = Неопределено Тогда
		ВыбраннаяФорма = "ФормаДокументаОсновная";
		Параметры.Вставить("ИзменитьВидОперации");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ПодготовкаПараметровПроведенияДокумента

// Функция подготавливает параметры проведения документа
Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт

	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаРеквизитыДокумента(НомераТаблиц);
	Результат    = Запрос.ВыполнитьПакет();
	ТаблицаРеквизиты = Результат[НомераТаблиц["Реквизиты"]].Выгрузить();
	ПараметрыПроведения.Вставить("Реквизиты", ТаблицаРеквизиты);
	
	Реквизиты = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ТаблицаРеквизиты[0]);
	Если ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитики(Реквизиты.Период, Отказ, Реквизиты.Организация, "Нал", Истина) = Неопределено Тогда
		Отказ = Истина;
		Возврат ПараметрыПроведения;
	КонецЕсли;
	
	НомераТаблиц = Новый Структура;
	
	Запрос.Текст = ТекстЗапросаТаблицаТоваровУведомлениеОбОстатках(НомераТаблиц, ДокументСсылка);
	
	Если НЕ ПустаяСтрока(Запрос.Текст) Тогда
		Результат = Запрос.ВыполнитьПакет();
		Для Каждого НомерТаблицы Из НомераТаблиц Цикл
			ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
		КонецЦикла;
	КонецЕсли;
	
	Возврат ПараметрыПроведения;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Формирует и возвращает текст запроса для выборки данных
// необходимых для формирования движений документа
//
// Параметры:
//  НомераТаблиц - Число - Структура с таблицами параметров проведения
// 
// Возвращаемое значение:
//  Строка - текст запроса
//
Функция ТекстЗапросаТаблицаТоваровУведомлениеОбОстатках(НомераТаблиц, ДокументСсылка) Экспорт
	
	МетаданныеДокумента = ДокументСсылка.Метаданные();
	ИмяВидаДокумента = МетаданныеДокумента.Имя;
	
	НомераТаблиц.Вставить("ТаблицаТоваров", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Уведомление.Ссылка КАК Ссылка,
	|	Уведомление.НомерСтроки КАК НомерСтроки,
	|	Уведомление.Номенклатура КАК Номенклатура,
	|	Уведомление.Количество КАК Количество,
	|	Уведомление.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|	Уведомление.Сумма КАК Сумма,
	|	Уведомление.СтранаПроисхождения КАК СтранаПроисхождения
	|ИЗ
	|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.Товары КАК Уведомление
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО Уведомление.Ссылка = Реквизиты.Ссылка
	|ГДЕ
	|	Уведомление.Ссылка = &Ссылка
	|	И НЕ Реквизиты.ЭтоКорректировочноеУведомление
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировочноеУведомление.Ссылка,
	|	КорректировочноеУведомление.НомерСтроки,
	|	КорректировочноеУведомление.Номенклатура,
	|	-КорректировочноеУведомление.Количество,
	|	-КорректировочноеУведомление.КоличествоПрослеживаемости,
	|	-КорректировочноеУведомление.Сумма,
	|	КорректировочноеУведомление.СтранаПроисхождения
	|ИЗ
	|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.Товары КАК КорректировочноеУведомление
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСправочник
	|		ПО КорректировочноеУведомление.Номенклатура = НоменклатураСправочник.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО КорректировочноеУведомление.Ссылка = Реквизиты.Ссылка
	|ГДЕ
	|	КорректировочноеУведомление.Ссылка = &Ссылка
	|	И ВЫБОР
	|			КОГДА Реквизиты.ЭтоКорректировочноеУведомление
	|				ТОГДА Реквизиты.ПолученоПодтверждениеИзФНС
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ
	|	И (КорректировочноеУведомление.Количество <> КорректировочноеУведомление.КоличествоПослеИзменения
	|			ИЛИ КорректировочноеУведомление.КоличествоПрослеживаемости <> КорректировочноеУведомление.КоличествоПрослеживаемостиПослеИзменения
	|			ИЛИ КорректировочноеУведомление.Сумма <> КорректировочноеУведомление.СуммаПослеИзменения
	|			ИЛИ КорректировочноеУведомление.СтранаПроисхождения <> КорректировочноеУведомление.СтранаПроисхожденияПослеИзменения)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировочноеУведомление.Ссылка,
	|	КорректировочноеУведомление.НомерСтроки,
	|	КорректировочноеУведомление.Номенклатура,
	|	КорректировочноеУведомление.КоличествоПослеИзменения,
	|	КорректировочноеУведомление.КоличествоПрослеживаемостиПослеИзменения,
	|	КорректировочноеУведомление.СуммаПослеИзменения,
	|	КорректировочноеУведомление.СтранаПроисхожденияПослеИзменения
	|ИЗ
	|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.Товары КАК КорректировочноеУведомление
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСправочник
	|		ПО КорректировочноеУведомление.Номенклатура = НоменклатураСправочник.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО КорректировочноеУведомление.Ссылка = Реквизиты.Ссылка
	|ГДЕ
	|	КорректировочноеУведомление.Ссылка = &Ссылка
	|	И ВЫБОР
	|			КОГДА Реквизиты.ЭтоКорректировочноеУведомление
	|				ТОГДА Реквизиты.ПолученоПодтверждениеИзФНС
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ
	|	И (КорректировочноеУведомление.Количество <> КорректировочноеУведомление.КоличествоПослеИзменения
	|			ИЛИ КорректировочноеУведомление.КоличествоПрослеживаемости <> КорректировочноеУведомление.КоличествоПрослеживаемостиПослеИзменения
	|			ИЛИ КорректировочноеУведомление.Сумма <> КорректировочноеУведомление.СуммаПослеИзменения
	|			ИЛИ КорректировочноеУведомление.СтранаПроисхождения <> КорректировочноеУведомление.СтранаПроисхожденияПослеИзменения)";
	
	Возврат ТекстЗапроса + ПрослеживаемостьБП.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ПечатьУведомленияОбОстаткахПрослеживаемогоТовара(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабДокумент = ПодготовитьТаблицыУведомленияОбОстаткахПрослеживаемогоТовара(МассивОбъектов);
	
	Возврат ТабДокумент;

КонецФункции

Функция ПодготовитьТаблицыУведомленияОбОстаткахПрослеживаемогоТовара(ОбъектыПечати)
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабДокумент.ПолеСверху = 10;
	ТабДокумент.ПолеСнизу  = 10;
	ТабДокумент.АвтоМасштаб = Истина;
	ТабДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_УведомлениеОбОстаткахПрослеживаемыхТоваров";
	ТабДокумент.ЭкземпляровНаСтранице = 1;
	
	Результат = ПолучитьПечатнуюФормуУведомлениеОбОстаткахПрослеживаемыхТоваров(ОбъектыПечати);
		
	ТабДокумент.Вывести(Результат.Значение);
		
	Возврат ТабДокумент;
	
КонецФункции

Функция ПолучитьПечатнуюФормуУведомлениеОбОстаткахПрослеживаемыхТоваров(Документ) Экспорт
	
	ПараметрыПечати = ПараметрыУведомления(Документ);
	
	Контейнер = РегламентированнаяОтчетность.СтруктураКонтейнераДанных(ПараметрыПечати);
	
	Если Контейнер.Свойство("Ошибки") 
		И Контейнер.Ошибки.Количество() > 0 Тогда
		
		Возврат Новый ТабличныйДокумент;
	
	КонецЕсли;
	
	ЗаполнитьДополнительныеРеквизиты(Контейнер, Документ);
	ЗаполнитьСодержательнуюЧасть(Контейнер.СодержательнаяЧасть, Документ);
	
	Результат = РегламентированнаяОтчетность.ПечатныйБланкСформированныйАлгоритмамиРегламентированныхОтчетов(Контейнер);
	
	Если Результат.Количество() = 0 Тогда
		Возврат Новый ТабличныйДокумент;
	КонецЕсли;
	
	Возврат ПолучитьИзВременногоХранилища(Результат[0]);
	
КонецФункции

Функция ВыгрузитьУведомлениеОбОстаткахПрослеживаемыхТоваров(Документ) Экспорт
	
	ПараметрыВыгрузки = ПараметрыУведомления(Документ);
	
	Контейнер = РегламентированнаяОтчетность.СтруктураКонтейнераДанных(ПараметрыВыгрузки);
	
	Если Контейнер.Свойство("Ошибки") 
		И Контейнер.Ошибки.Количество() > 0 Тогда
		
		Возврат Новый Структура("АдресФайлаВыгрузки,ИмяФайлаВыгрузки,Ошибки",
					Контейнер.АдресФайлаВыгрузки, Контейнер.ИмяФайлаВыгрузки, Контейнер.Ошибки);
	
	КонецЕсли;
	
	ЗаполнитьДополнительныеРеквизиты(Контейнер, Документ);
	
	ЗаполнитьСодержательнуюЧасть(Контейнер.СодержательнаяЧасть, Документ);
	
	Результат = РегламентированнаяОтчетность.ВыгрузитьДанныеАлгоритмамиРегламентированногоОтчета(Контейнер);
	
	Возврат Новый Структура("АдресФайлаВыгрузки,ИмяФайлаВыгрузки,Ошибки",
					Результат.АдресФайлаВыгрузки, Результат.ИмяФайлаВыгрузки, Результат.Ошибки);
	
КонецФункции

Функция ПараметрыУведомления(Документ)
	
	ПараметрыВыгрузки = Новый Структура();
	ПараметрыВыгрузки.Вставить("ИсточникОтчета", "РегламентированныйОтчетПрослеживаемыеТоварыОстатки");
	ПараметрыВыгрузки.Вставить("ВыбраннаяФорма", "ФормаОтчета2021Кв3");
	//todo temp
//	ПараметрыВыгрузки.Вставить("ВерсияФормата",  "5.02");
	ПараметрыВыгрузки.Вставить("ВерсияФормата",  "5.01");
	ПараметрыВыгрузки.Вставить("СсылкаНаВнешнийОбъект", Документ);
	
	Возврат ПараметрыВыгрузки;
	
КонецФункции

Функция ЗаполнитьДополнительныеРеквизиты(Контейнер, Документ)
	
	ПараметрыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Документ, 
		"Организация,Дата,Номер,НомерКорректировки,ДокументУведомлениеОбОстатках,
		|КодФормыРеорганизации,ПризнакУведомления,ИННДоРеорганизации,КППДоРеорганизации");
	
	НомерУведомления = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ПараметрыДокумента.Номер, Истина, Ложь);
	Если ПараметрыДокумента.НомерКорректировки > 0 Тогда
		НомерУведомления = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыДокумента.ДокументУведомлениеОбОстатках, "Номер"),
				Истина, Ложь);
	КонецЕсли;
	
	Контейнер.Организация = Документ.Организация;
	Контейнер.НалоговыйОрган = ПрослеживаемостьБРУ.КодГосударственногоОрганаОрганизации(
			ПараметрыДокумента.Организация);
			
	Контейнер.ДатаУведомления = ПараметрыДокумента.Дата;
	Контейнер.ДатаПодписи = ПараметрыДокумента.Дата;
	
	Контейнер.КПП = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыДокумента.Организация, "КПП");
	Контейнер.НомерУведомления = НомерУведомления;
	Контейнер.ДатаУведомления = ПараметрыДокумента.Дата;
	Контейнер.НомерКорректировки = ПараметрыДокумента.НомерКорректировки;
	
	Если ПараметрыДокумента.ПризнакУведомления > 0 Тогда
		Контейнер.ПризнакУведомления = ПрослеживаемостьБРУ.ПолучитьПризнакУведомления(ПараметрыДокумента.ПризнакУведомления);
		
		Контейнер.ИННПродавца = ИННПродавцовСтрокой(Документ);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыДокумента.КодФормыРеорганизации) Тогда
		
		Контейнер.КодФормыРеорганизации = ПараметрыДокумента.КодФормыРеорганизации;
		Контейнер.ИННРеорганизованнойОрганизации = ПараметрыДокумента.ИННДоРеорганизации;
		Контейнер.КППРеорганизованнойОрганизации = ПараметрыДокумента.КППДоРеорганизации;
		
	КонецЕсли;
		
КонецФункции

Функция ИННПродавцовСтрокой(Документ)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Документ);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Контрагенты.ИНН КАК ИНН
	|ИЗ
	|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.Продавцы КАК УведомлениеОбОстаткахПрослеживаемыхТоваровПродавцы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО УведомлениеОбОстаткахПрослеживаемыхТоваровПродавцы.Продавец = Контрагенты.Ссылка
	|ГДЕ
	|	УведомлениеОбОстаткахПрослеживаемыхТоваровПродавцы.Ссылка = &Ссылка";
	
	ТаблицаРезультат = Запрос.Выполнить().Выгрузить();
	
	СтрокаИННПродавцов = "";
	НеобходимостьЗапятой = Ложь;
	НомерСтроки = 0;
	КоличествоСтрок = ТаблицаРезультат.Количество();
	
	Для каждого ТекущаяСтрока Из ТаблицаРезультат Цикл
		
		НомерСтроки = НомерСтроки + 1;
		
		Если КоличествоСтрок > 1 
			И КоличествоСтрок <> НомерСтроки Тогда
			НеобходимостьЗапятой = Истина;
		Иначе
			НеобходимостьЗапятой = Ложь;
		КонецЕсли;
		
		СтрокаИННПродавцов = СтрокаИННПродавцов + ТекущаяСтрока.ИНН + ?(НеобходимостьЗапятой, ",","");
		
	КонецЦикла;
	
	Возврат СтрокаИННПродавцов;
	
КонецФункции

// Формирует текст запроса по таблице товаров
// документа УведомлениеОбОстаткахПрослеживаемыхТоваров
//
// Возвращаемое значение:
//  Строка - текст запроса
//
Функция ТекстЗапросаПоТаблицеТоваровУведомленияОбОстатках()
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Номенклатура КАК Справочник.Номенклатура).Наименование КАК НаименованиеТовара,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.КодОКПД2 КАК КодОКПД2,
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.ЕдиницаИзмерения КАК Справочник.КлассификаторЕдиницИзмерения).Код КАК ЕдиницаИзмерения,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Количество КАК Количество,
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.ЕдиницаПрослеживаемости КАК Справочник.КлассификаторЕдиницИзмерения).Код КАК ЕдиницаПрослеживаемости,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Сумма КАК Сумма,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ПервичныйДокумент КАК ПервичныйДокумент,
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.КодТНВЭД КАК Справочник.КлассификаторТНВЭД).Код КАК КодТНВЭД,
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.КодТНВЭД КАК Справочник.КлассификаторТНВЭД).Наименование КАК ТНВЭДНаименование,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка.Номер КАК НомерПервичногоДокумента,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка.Дата КАК ДатаПервичногоДокумента,
		|	"""" КАК РНПТ
		|ПОМЕСТИТЬ ВТ_ВыборкаДанных
		|ИЗ
		|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.Товары КАК УведомлениеОбОстаткахПрослеживаемыхТоваровТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров КАК УведомлениеОбОстаткахПрослеживаемыхТоваров
		|		ПО УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Ссылка = УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка
		|ГДЕ
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка = &Документ
		|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Номенклатура КАК Справочник.Номенклатура).Наименование,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.КодОКПД2,
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.ЕдиницаИзмеренияПослеИзменения КАК Справочник.КлассификаторЕдиницИзмерения).Код,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.КоличествоПослеИзменения,
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.ЕдиницаПрослеживаемости КАК Справочник.КлассификаторЕдиницИзмерения).Код,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.КоличествоПрослеживаемостиПослеИзменения,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.СуммаПослеИзменения,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ПервичныйДокумент,
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.КодТНВЭДПослеИзменения КАК Справочник.КлассификаторТНВЭД).Код,
		|	ВЫРАЗИТЬ(УведомлениеОбОстаткахПрослеживаемыхТоваров.КодТНВЭДПослеИзменения КАК Справочник.КлассификаторТНВЭД).Наименование,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Ссылка.Номер,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Ссылка.Дата,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.РНПТ
		|ИЗ
		|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.Товары КАК УведомлениеОбОстаткахПрослеживаемыхТоваровТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров КАК УведомлениеОбОстаткахПрослеживаемыхТоваров
		|		ПО УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Ссылка = УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка
		|ГДЕ
		|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка = &Документ
		|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(1) КАК Количество,
		|	ВТ_ВыборкаДанных.КодТНВЭД КАК КодТНВЭД
		|ПОМЕСТИТЬ ВТ_КоличествоСтрок
		|ИЗ
		|	ВТ_ВыборкаДанных КАК ВТ_ВыборкаДанных
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ВыборкаДанных.КодТНВЭД
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ВТ_КоличествоСтрок.Количество > 1
		|			ТОГДА ВТ_ВыборкаДанных.ТНВЭДНаименование
		|		ИНАЧЕ ВТ_ВыборкаДанных.НаименованиеТовара
		|	КОНЕЦ КАК НаимТовДок,
		|	ВТ_ВыборкаДанных.КодОКПД2 КАК КодОКПД2,
		|	ВТ_ВыборкаДанных.ЕдиницаИзмерения КАК ЕдинИзмерДок,
		|	СУММА(ВТ_ВыборкаДанных.Количество) КАК КолТоварДок,
		|	ВТ_ВыборкаДанных.ЕдиницаПрослеживаемости КАК ЕдинИзмерПер,
		|	СУММА(ВТ_ВыборкаДанных.КоличествоПрослеживаемости) КАК КоличТоварПер,
		|	СУММА(ВТ_ВыборкаДанных.Сумма) КАК СтТоварБезНДС,
		|	ВТ_ВыборкаДанных.КодТНВЭД КАК КодТНВЭД,
		|	ВТ_ВыборкаДанных.НомерПервичногоДокумента КАК НомПервичДок,
		|	ВТ_ВыборкаДанных.ДатаПервичногоДокумента КАК ДатаПервичДок,
		|	ВТ_ВыборкаДанных.РНПТ КАК РегНомерТов
		|ИЗ
		|	ВТ_ВыборкаДанных КАК ВТ_ВыборкаДанных
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КоличествоСтрок КАК ВТ_КоличествоСтрок
		|		ПО ВТ_ВыборкаДанных.КодТНВЭД = ВТ_КоличествоСтрок.КодТНВЭД
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ВыборкаДанных.КодОКПД2,
		|	ВТ_ВыборкаДанных.ЕдиницаИзмерения,
		|	ВТ_ВыборкаДанных.ЕдиницаПрослеживаемости,
		|	ВЫБОР
		|		КОГДА ВТ_КоличествоСтрок.Количество > 1
		|			ТОГДА ВТ_ВыборкаДанных.ТНВЭДНаименование
		|		ИНАЧЕ ВТ_ВыборкаДанных.НаименованиеТовара
		|	КОНЕЦ,
		|	ВТ_ВыборкаДанных.КодТНВЭД,
		|	ВТ_ВыборкаДанных.ДатаПервичногоДокумента,
		|	ВТ_ВыборкаДанных.НомерПервичногоДокумента,
		|	ВТ_ВыборкаДанных.РНПТ";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ЗаполнитьСодержательнуюЧасть(СодержательнаяЧасть, Документ)

	Запрос = Новый Запрос;
	
	Запрос.Текст = ТекстЗапросаПоТаблицеТоваровУведомленияОбОстатках();
	
	Запрос.УстановитьПараметр("Документ", Документ);
	ТаблицаРезультатов = Запрос.Выполнить().Выгрузить();
	
	ТаблицаРезультатов.Свернуть(
		"НомПервичДок,ДатаПервичДок,НаимТовДок,ЕдинИзмерДок,КодТНВЭД,КодОКПД2,ЕдинИзмерПер,РегНомерТов",
		"КолТоварДок,КоличТоварПер,СтТоварБезНДС"
		);
	
	Если ТаблицаРезультатов.Количество() > 1 Тогда
		
		
		Для каждого ТекущаяСтрока Из ТаблицаРезультатов Цикл
			
			ТекущаяСтрока.КодОКПД2 = "";
			
		КонецЦикла;
		
		ТаблицаРезультатов.Свернуть(
			"НомПервичДок,ДатаПервичДок,НаимТовДок,ЕдинИзмерДок,КодТНВЭД,КодОКПД2,ЕдинИзмерПер,РегНомерТов",
			"КолТоварДок,КоличТоварПер,СтТоварБезНДС"
			);
		
	КонецЕсли;
	
	Результат = ТаблицаРезультатов[0];
	
	СодержательнаяЧасть.П000010000101 = НСтр("ru='Инвентаризация товаров'"); // НаимПервичДок
	СодержательнаяЧасть.П000010000201 = Результат["НомПервичДок"];     // НомПервичДок
	СодержательнаяЧасть.П000010000301 = Результат["ДатаПервичДок"];    // ДатаПервичДок
	СодержательнаяЧасть.П000010000401 = Результат["НаимТовДок"];       // НаимТовДок
	СодержательнаяЧасть.П000010000501 = Результат["КолТоварДок"];      // КолТоварДок
	СодержательнаяЧасть.П000010000601 = Результат["ЕдинИзмерДок"];     // ЕдинИзмерДок
	СодержательнаяЧасть.П000010000701 = Результат["КодТНВЭД"];         // КодТНВЭД
	СодержательнаяЧасть.П000010000801 = Результат["КодОКПД2"];         // КодОКПД2
	СодержательнаяЧасть.П000010000901 = Результат["КоличТоварПер"];    // КоличТоварПер
	СодержательнаяЧасть.П000010001001 = Результат["ЕдинИзмерПер"];     // ЕдинИзмерПер
	СодержательнаяЧасть.П000010001101 = Результат["РегНомерТов"];      // РегНомерТов
	СодержательнаяЧасть.П000010001201 = Результат["СтТоварБезНДС"];    // СтТоварБезНДС
	
КонецФункции

Функция ТекстЗапросаРеквизитыДокумента(НомераТаблиц)
	
	НомераТаблиц.Вставить("ВременнаяТаблицаРеквизиты", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Реквизиты", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.РНПТ КАК РНПТ,
	|	Реквизиты.ПервичныйДокумент КАК Основание,
	|	Реквизиты.НомерКорректировки > 0 КАК ЭтоКорректировочноеУведомление,
	|	Реквизиты.ДокументУведомлениеОбОстатках КАК ДокументУведомлениеОбОстатках,
	|	Реквизиты.ПолученоПодтверждениеИзФНС КАК ПолученоПодтверждениеИзФНС
	|ПОМЕСТИТЬ Реквизиты
	|ИЗ
	|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Период КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.РНПТ КАК РНПТ,
	|	Реквизиты.Основание КАК Основание,
	|	Реквизиты.ДокументУведомлениеОбОстатках КАК ДокументУведомлениеОбОстатках,
	|	Реквизиты.ПолученоПодтверждениеИзФНС КАК ПолученоПодтверждениеИзФНС,
	|	Реквизиты.ЭтоКорректировочноеУведомление КАК ЭтоКорректировочноеУведомление
	|ИЗ
	|	Реквизиты КАК Реквизиты";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПоследнийДокументИсправления(ДокументОснование, ДокументИсключение)
	
	Запрос = Новый Запрос();
		ПараметрыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОснование, "НомерКорректировки,ДокументУведомлениеОбОстатках");
	Запрос.УстановитьПараметр("ДокументОснование", ?(ПараметрыДокумента.НомерКорректировки>0, 
			ПараметрыДокумента.ДокументУведомлениеОбОстатках, ДокументОснование));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка КАК Ссылка,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки КАК НомерКорректировки
	|ПОМЕСТИТЬ ТаблицаДокументов
	|ИЗ
	|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров КАК УведомлениеОбОстаткахПрослеживаемыхТоваров
	|ГДЕ
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ДокументУведомлениеОбОстатках = &ДокументОснование
	|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки
	|ИЗ
	|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров КАК УведомлениеОбОстаткахПрослеживаемыхТоваров
	|ГДЕ
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТаблицаДокументов.Ссылка КАК Ссылка,
	|	ТаблицаДокументов.НомерКорректировки КАК НомерКорректировки
	|ИЗ
	|	ТаблицаДокументов КАК ТаблицаДокументов
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаДокументов.НомерКорректировки УБЫВ"
	;
	
	Результат = Запрос.Выполнить().Выбрать();
	Если Результат.Следующий() Тогда
		Возврат Результат.Ссылка;
	Иначе
		Возврат ДокументОснование;
	КонецЕсли;
	
КонецФункции

Функция ЗаполнитьДокументДанными(Период, Организация, ДокументОснование = Неопределено, ДокументИсключение = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Запрос.УстановитьПараметр("ПоследнийДокументИсправления", ПоследнийДокументИсправления(ДокументОснование, ДокументИсключение));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка КАК Ссылка,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ВерсияДанных КАК ВерсияДанных,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ПометкаУдаления КАК ПометкаУдаления,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Дата КАК Дата,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Проведен КАК Проведен,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.КодТНВЭД
	|		ИНАЧЕ УведомлениеОбОстаткахПрослеживаемыхТоваров.КодТНВЭДПослеИзменения
	|	КОНЕЦ КАК КодТНВЭД,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.РНПТ КАК РНПТ,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ПервичныйДокумент КАК ПервичныйДокумент,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Ответственный КАК Ответственный,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Комментарий КАК Комментарий,
	|	ВЫБОР
	|		КОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.ЕдиницаИзмерения
	|		ИНАЧЕ УведомлениеОбОстаткахПрослеживаемыхТоваров.ЕдиницаИзмеренияПослеИзменения
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.ЕдиницаПрослеживаемости
	|		ИНАЧЕ УведомлениеОбОстаткахПрослеживаемыхТоваров.ЕдиницаПрослеживаемостиПослеИзменения
	|	КОНЕЦ КАК ЕдиницаПрослеживаемости,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки + 1 КАК НомерКорректировки,
	|	ВЫБОР
	|		КОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.ДокументУведомлениеОбОстатках = ЗНАЧЕНИЕ(Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.ПустаяСсылка)
	|			ТОГДА &ДокументОснование
	|		ИНАЧЕ УведомлениеОбОстаткахПрослеживаемыхТоваров.ДокументУведомлениеОбОстатках
	|	КОНЕЦ КАК ДокументУведомлениеОбОстатках,
	|	ВЫБОР
	|		КОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.КодТНВЭД
	|		ИНАЧЕ УведомлениеОбОстаткахПрослеживаемыхТоваров.КодТНВЭДПослеИзменения
	|	КОНЕЦ КАК КодТНВЭДПослеИзменения,
	|	ВЫБОР
	|		КОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.КодОКПД2
	|		ИНАЧЕ УведомлениеОбОстаткахПрослеживаемыхТоваров.КодОКПД2ПослеИзменения
	|	КОНЕЦ КАК КодОКПД2ПослеИзменения,
	|	ВЫБОР
	|		КОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.ЕдиницаИзмерения
	|		ИНАЧЕ УведомлениеОбОстаткахПрослеживаемыхТоваров.ЕдиницаИзмеренияПослеИзменения
	|	КОНЕЦ КАК ЕдиницаИзмеренияПослеИзменения,
	|	ВЫБОР
	|		КОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.ЕдиницаПрослеживаемости
	|		ИНАЧЕ УведомлениеОбОстаткахПрослеживаемыхТоваров.ЕдиницаПрослеживаемостиПослеИзменения
	|	КОНЕЦ КАК ЕдиницаПрослеживаемостиПослеИзменения,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ПризнакУведомления КАК ПризнакУведомления,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.КодФормыРеорганизации КАК КодФормыРеорганизации,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ИННДоРеорганизации КАК ИННДоРеорганизации,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.КППДоРеорганизации КАК КППДоРеорганизации,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ПризнакУведомления КАК ПризнакУведомленияДоИзменения
	|ИЗ
	|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров КАК УведомлениеОбОстаткахПрослеживаемыхТоваров
	|ГДЕ
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка = &ПоследнийДокументИсправления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Ссылка КАК Ссылка,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.НомерСтроки КАК НомерСтроки,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Количество
	|		ИНАЧЕ УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.КоличествоПослеИзменения
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.КоличествоПрослеживаемости
	|		ИНАЧЕ УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.КоличествоПрослеживаемостиПослеИзменения
	|	КОНЕЦ КАК КоличествоПрослеживаемости,
	|	ВЫБОР
	|		КОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Сумма
	|		ИНАЧЕ УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.СуммаПослеИзменения
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Количество
	|		ИНАЧЕ УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.КоличествоПослеИзменения
	|	КОНЕЦ КАК КоличествоПослеИзменения,
	|	ВЫБОР
	|		КОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.КоличествоПрослеживаемости
	|		ИНАЧЕ УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.КоличествоПрослеживаемостиПослеИзменения
	|	КОНЕЦ КАК КоличествоПрослеживаемостиПослеИзменения,
	|	ВЫБОР
	|		КОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Сумма
	|		ИНАЧЕ УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.СуммаПослеИзменения
	|	КОНЕЦ КАК СуммаПослеИзменения,
	|	ВЫБОР
	|		КОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.СтранаПроисхождения
	|		ИНАЧЕ УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.СтранаПроисхожденияПослеИзменения
	|	КОНЕЦ КАК СтранаПроисхождения,
	|	ВЫБОР
	|		КОГДА УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.СтранаПроисхождения
	|		ИНАЧЕ УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.СтранаПроисхожденияПослеИзменения
	|	КОНЕЦ КАК СтранаПроисхожденияПослеИзменения
	|ИЗ
	|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров КАК УведомлениеОбОстаткахПрослеживаемыхТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.Товары КАК УведомлениеОбОстаткахПрослеживаемыхТоваровТовары
	|		ПО УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка = УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Ссылка
	|ГДЕ
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка = &ПоследнийДокументИсправления";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ТаблицаРеквизиты = РезультатЗапроса[0].Выгрузить();
	ТаблицаТовары = РезультатЗапроса[1].Выгрузить();
	
	Возврат Новый Структура("Реквизиты,Товары", ТаблицаРеквизиты, ТаблицаТовары)
	
КонецФункции

Функция ПолучитьСоответствиеВидовУведомленийФормам() Экспорт

	ВидыУведомлений = ПрослеживаемостьБРУ.ВидыУведомлений();
	
	ФормыУведомленияОбОстатках = Новый Соответствие;
	ФормыУведомленияОбОстатках.Вставить(ВидыУведомлений.Уведомление, "ФормаДокументаОсновная");
	ФормыУведомленияОбОстатках.Вставить(ВидыУведомлений.КорректировочноеУведомление, "ФормаДокументаКорректировочная");
	
	Возврат ФормыУведомленияОбОстатках;

КонецФункции 

Функция ОпределитьВидУведомления(НомерКорректировки)

	ВидыУведомлений = ПрослеживаемостьБРУ.ВидыУведомлений();
	
	Если НомерКорректировки > 0 Тогда
		Возврат ВидыУведомлений.КорректировочноеУведомление;
	КонецЕсли;
	
	Возврат ВидыУведомлений.Уведомление;

КонецФункции 

Процедура ЗаполнитьКорректировочноеУведомлениеОбОстаткахПрослеживаемыхТоваров(УведомлениеОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	УведомлениеОбъект.Ответственный = Пользователи.ТекущийПользователь();
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, "Проведен,РНПТ");
	
	Если НЕ РеквизитыДокумента.Проведен Тогда
		
		ТекстОшибки = НСтр("ru='Документ %Документ% не проведен. Ввод на основании непроведенного документа запрещен.'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ДанныеЗаполнения);
		ВызватьИсключение ТекстОшибки;
		
	ИначеЕсли НЕ ЗначениеЗаполнено(РеквизитыДокумента.РНПТ) Тогда
		
		ТекстОшибки = НСтр("ru='В документе %Документ% не указан РНПТ. Ввод на основании документа запрещен.'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ДанныеЗаполнения);
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	ДанныеТаблиц = Документы.УведомлениеОбОстаткахПрослеживаемыхТоваров.ЗаполнитьДокументДанными(
		УведомлениеОбъект.Дата, УведомлениеОбъект.Организация, ДанныеЗаполнения);
		
	ЗаполнитьЗначенияСвойств(УведомлениеОбъект, 
		?(ДанныеТаблиц.Реквизиты.Количество()>0, ДанныеТаблиц.Реквизиты[0], Новый ТаблицаЗначений));
	
	УведомлениеОбъект.Товары.Загрузить(ДанныеТаблиц.Товары);
		
КонецПроцедуры

Функция ПолучитьТаблицуТоваровПоТНВЭД(Объект) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РегистрацияПрослеживаемыхТоваров.Номенклатура КАК Номенклатура,
	|	СУММА(РегистрацияПрослеживаемыхТоваров.Количество) КАК Количество,
	|	СУММА(РегистрацияПрослеживаемыхТоваров.Сумма) КАК Сумма,
	|	НоменклатураСправочник.КодТНВЭД КАК КодТНВЭД,
	|	КлассификаторЕдиницИзмерения.Ссылка КАК ОбщаяЕдиницаИзмерения,
	|	КлассификаторТНВЭД.ЕдиницаИзмерения КАК ОбщаяЕдиницаПрослеживаемости,
	|	РегистрацияПрослеживаемыхТоваров.Количество КАК КоличествоПрослеживаемости,
	|	НоменклатураСправочник.ЕдиницаХраненияОстатков КАК ЕдиницаИзмерения,
	|	НоменклатураСправочник.ЕдиницаИзмеренияПрослеживаемости КАК ЕдиницаПрослеживаемости,
	|	НоменклатураСправочник.СтранаПроисхождения
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ
	|	РегистрСведений.РегистрацияПрослеживаемыхТоваров КАК РегистрацияПрослеживаемыхТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСправочник
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторТНВЭД КАК КлассификаторТНВЭД
	|			ПО НоменклатураСправочник.КодТНВЭД = КлассификаторТНВЭД.Ссылка
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторЕдиницИзмерения КАК КлассификаторЕдиницИзмерения
	|			ПО НоменклатураСправочник.ЕдиницаХраненияОстатков.ЕдиницаПоКлассификатору = КлассификаторЕдиницИзмерения.Ссылка
	|		ПО РегистрацияПрослеживаемыхТоваров.Номенклатура = НоменклатураСправочник.Ссылка
	|ГДЕ
	|	НоменклатураСправочник.КодТНВЭД = &КодТНВЭД
	|	И РегистрацияПрослеживаемыхТоваров.Организация = &Организация
	|	И РегистрацияПрослеживаемыхТоваров.Основание = &ДокументОснование
	|
	|СГРУППИРОВАТЬ ПО
	|	НоменклатураСправочник.КодТНВЭД,
	|	КлассификаторТНВЭД.ЕдиницаИзмерения,
	|	РегистрацияПрослеживаемыхТоваров.Количество,
	|	РегистрацияПрослеживаемыхТоваров.Номенклатура,
	|	КлассификаторЕдиницИзмерения.Ссылка,
	|	НоменклатураСправочник.ЕдиницаХраненияОстатков,
	|	НоменклатураСправочник.ЕдиницаИзмеренияПрослеживаемости,
	|	НоменклатураСправочник.СтранаПроисхождения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	СУММА(ТаблицаТоваров.Количество) КАК Количество,
	|	СУММА(ТаблицаТоваров.Сумма) КАК Сумма,
	|	ТаблицаТоваров.КодТНВЭД КАК КодТНВЭД,
	|	ТаблицаТоваров.ОбщаяЕдиницаИзмерения КАК ОбщаяЕдиницаИзмерения,
	|	ТаблицаТоваров.ОбщаяЕдиницаПрослеживаемости КАК ОбщаяЕдиницаПрослеживаемости,
	|	СУММА(ТаблицаТоваров.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости,
	|	ТаблицаТоваров.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТаблицаТоваров.ЕдиницаПрослеживаемости КАК ЕдиницаПрослеживаемости,
	|	ТаблицаТоваров.СтранаПроисхождения
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|ГДЕ
	|	ТаблицаТоваров.Количество > 0
	|	И ТаблицаТоваров.ОбщаяЕдиницаИзмерения = &ЕдиницаИзмерения
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.ЕдиницаПрослеживаемости,
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.КодТНВЭД,
	|	ТаблицаТоваров.ОбщаяЕдиницаИзмерения,
	|	ТаблицаТоваров.ОбщаяЕдиницаПрослеживаемости,
	|	ТаблицаТоваров.ЕдиницаИзмерения,
	|	ТаблицаТоваров.СтранаПроисхождения";
	
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	
	МоментСписания = Новый Граница(Объект.Ссылка.МоментВремени(), ВидГраницы.Исключая);
	Запрос.УстановитьПараметр("МоментСписания", МоментСписания);
	Запрос.УстановитьПараметр("ДокументОснование", Объект.ПервичныйДокумент);
	Запрос.УстановитьПараметр("КодТНВЭД", Объект.КодТНВЭД);
	Запрос.УстановитьПараметр("ЕдиницаИзмерения", Объект.ЕдиницаИзмерения);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти

#КонецЕсли
