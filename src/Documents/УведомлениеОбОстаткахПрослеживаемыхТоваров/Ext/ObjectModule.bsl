
#Если Клиент Тогда

// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//  Структура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	СтруктПечатныхФорм = Новый Структура;
	СтруктПечатныхФорм.Вставить("УведомлениеОбОстаткахПрослеживаемыхТоваров", 
									"Уведомление об остатках прослеживаемых товаров");
	Возврат СтруктПечатныхФорм;
	
КонецФункции // ПолучитьСтруктуруПечатныхФорм()

#КонецЕсли

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура")
		ИЛИ ДанныеЗаполнения = Неопределено Тогда
		
		ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ЭтотОбъект, глЗначениеПеременной("глТекущийПользователь"));
		
	Иначе
		
		Документы.УведомлениеОбОстаткахПрослеживаемыхТоваров.ЗаполнитьКорректировочноеУведомлениеОбОстаткахПрослеживаемыхТоваров(
			ЭтотОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
		
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПараметрыПроведения = Документы.УведомлениеОбОстаткахПрослеживаемыхТоваров.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПрослеживаемостьБРУ.ОбработкаПроведенияУведомление(ПараметрыПроведения, Движения, Отказ);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПрослеживаемостьБРУ.ПроверитьДублированиеНомераКорректировкиВУведомлениях(ЭтотОбъект);
	
	ДополнительныеСвойства.Вставить("ПервичныйДокумент", Ссылка.ПервичныйДокумент);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если ЭтотОбъект.НомерКорректировки = 0 Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("КодТНВЭДПослеИзменения");
		МассивНепроверяемыхРеквизитов.Добавить("КодОКПД2ПослеИзменения");
		МассивНепроверяемыхРеквизитов.Добавить("ЕдиницаИзмеренияПослеИзменения");
		МассивНепроверяемыхРеквизитов.Добавить("ЕдиницаПрослеживаемостиПослеИзменения");
		МассивНепроверяемыхРеквизитов.Добавить("Товары.СтранаПроисхожденияПослеИзменения");
		МассивНепроверяемыхРеквизитов.Добавить("ДатаСоздания");
		МассивНепроверяемыхРеквизитов.Добавить("Товары.СтранаПроисхожденияПослеИзменения");
		
	Иначе
		
		МассивНепроверяемыхРеквизитов.Добавить("Товары.Количество");
		МассивНепроверяемыхРеквизитов.Добавить("Товары.КоличествоПрослеживаемости");
		МассивНепроверяемыхРеквизитов.Добавить("Товары.Сумма");

		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(РНПТ) Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("Товары.СтранаПроисхождения");
		
	КонецЕсли;
	
	// Удаляем из проверяемых реквизитов все, по которым автоматическая проверка не нужна:
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
		
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	Движения.Записать();
	
	РегистрыСведений.РегистрацияПрослеживаемыхТоваров.ОбновитьСостояние(Ссылка, ДополнительныеСвойства.ПервичныйДокумент, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции


#КонецОбласти

#КонецЕсли
