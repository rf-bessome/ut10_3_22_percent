#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
		ПодготовитьФормуНаСервере();
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
	ЗаполнитьВременныеРеквизиты();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ПрослеживаемостьФормыКлиент.ОбработкаВыбораУведомления(ЭтаФорма, ВыбранноеЗначение, ИсточникВыбора);
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
	ОбъектСсылка = Объект.Ссылка;
	Если ИмяСобытия = "Изменение сообщения" И ТипЗнч(Параметр) = Тип("Структура") И Параметр.Свойство("Предмет") И КонтекстЭДО.ОтчетЯвляетсяПредметом(Параметр.Предмет, ОбъектСсылка) Тогда
		РегламентированнаяОтчетность.ОбновитьПанельСостоянияОтправкиВУведомленииОКонтролируемыхСделках(ЭтаФорма);
	КонецЕсли;
	#КонецЕсли
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_УведомлениеОбОстаткахПрослеживаемыхТоваров", ПараметрыЗаписи, Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаполнитьВременныеРеквизиты();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРНПТ(Команда)
	
	Элементы.ЗаполнитьРНПТ.Видимость = Ложь;
	
	РНПТИзКвитанции = "";
	Для каждого ДокументРеализацииПолномочийНО Из ДокументыРеализацииПолномочийНО Цикл
		Если ЗначениеЗаполнено(ДокументРеализацииПолномочийНО.РНПТ) Тогда
			РНПТИзКвитанции = СокрЛП(ДокументРеализацииПолномочийНО.РНПТ);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ЗаполнитьРНПТНаСервере(РНПТИзКвитанции);
	
КонецПроцедуры

&НаКлиенте
Процедура ПервичныйДокументПриИзменении(Элемент)
	
	ОчиститьТабличнуюЧастьПриИзмененииРеквизита(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура КодТНВЭДПриИзменении(Элемент)
	
	КодТНВЭДПриИзмененииНаСервере();
	
	ОчиститьТабличнуюЧастьПриИзмененииРеквизита(Элемент);
		
КонецПроцедуры

&НаКлиенте
Процедура ЕдиницаИзмеренияПриИзменении(Элемент)
	
	ОчиститьТабличнуюЧастьПриИзмененииРеквизита(Элемент);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Номенклатура) Тогда
		
		ПараметрыНомеклатуры = ПрослеживаемостьФормыВызовСервера.ЕдиницыИзмеренийНоменклатуры(ТекущиеДанные.Номенклатура);
		ЗаполнитьЗначенияСвойств(ТекущиеДанные, ПараметрыНомеклатуры);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоТНВЭД(Команда)
	
	Если Объект.Товары.Количество() > 0 Тогда
		
		ТекстВопроса = НСтр("ru = 'При заполнении табличная часть будет очищена. Заполнить?'");
	
		Оповещение = Новый ОписаниеОповещения("ТоварыОбработкаЗаполненияЗавершение", ЭтаФорма);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
		
	Иначе
		
		ЗаполнитьПоТНВЭДПоДокументу();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыОбработкаЗаполненияЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт

	Если РезультатВыбора = КодВозвратаДиалога.Да Тогда
		
		ЗаполнитьПоТНВЭДПоДокументу();

	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПрослеживаемостьФормыКлиент.ТоварыКоличествоПриИзмененииУведомления(ЭтаФорма, ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаПечать(Команда)
	
	Если Модифицированность Тогда
		Записать();
	КонецЕсли;
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
		"Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров", "УведомлениеОбОстаткахПрослеживаемыхТоваров", 
		Объект.Ссылка, ЭтаФорма, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыгрузитьУведомлениеВXML(Команда)
	
	Если Модифицированность Тогда
		Записать();
	КонецЕсли;
	
	ПрослеживаемостьКлиент.ВыгрузитьВФайлУведомлениеПоПрослеживаемости(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВИнтернете(Команда)
	
	Если Модифицированность Тогда
		Записать();
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьВКонтролирующийОрган(Команда)
	
	Если Модифицированность Тогда
		Записать();
	КонецЕсли;
	
	Если ПроверитьЗаполнение() Тогда
		
		РегламентированнаяОтчетность.ПриНажатииНаКнопкуОтправкиВУведомленииОКонтролируемыхСделках(ЭтаФорма);
	
		РегламентированнаяОтчетность.ОбновитьПанельСостоянияОтправкиВУведомленииОКонтролируемыхСделках(ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьЦиклыОбмена(Команда)
	
	РегламентированнаяОтчетность.ПриНажатииНаКнопкуОтправкиВУведомленииОКонтролируемыхСделках(ЭтаФорма, "ПоказатьЦиклыОбмена");
	
КонецПроцедуры

#Область ПанельОтправкиВКонтролирующиеОрганы

&НаКлиенте
Процедура ОбновитьСостояниеОтправки(Команда)
	
	РегламентированнаяОтчетность.ОбновитьПанельСостоянияОтправкиВУведомленииОКонтролируемыхСделках(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПротоколОтправки(Команда)
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		КонтекстЭДО.ОткрытьПротоколОтправкиУведомленияОКонтролируемыхСделках(ЭтаФорма);
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОчиститьТабличнуюЧастьПриИзмененииРеквизита(Элемент)
	
	Реквизит = Элемент.Заголовок;
	
	Если Объект.Товары.Количество() <> 0 Тогда
		
		ШаблонВопроса = НСтр("ru = 'При изменении %1 табличная часть будет очищена. Продолжить?'");
		
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонВопроса, Реквизит);
		
		Оповещение = Новый ОписаниеОповещения("ИзменениеРеквизитаЗаполненияЗавершение", ЭтаФорма);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
		
	Иначе
		
		СтароеЗначениеЕдиницаИзмерения = Объект.ЕдиницаИзмерения;
		СтароеЗначениеПервичныйДокумент = Объект.ПервичныйДокумент;
		СтароеЗначениеТНВЭД = Объект.КодТНВЭД;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ИзменениеРеквизитаЗаполненияЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт

	Если РезультатВыбора = КодВозвратаДиалога.Да Тогда
	
		Объект.Товары.Очистить();
		
		СтароеЗначениеЕдиницаИзмерения = Объект.ЕдиницаИзмерения;
		СтароеЗначениеПервичныйДокумент = Объект.ПервичныйДокумент;
		СтароеЗначениеТНВЭД = Объект.КодТНВЭД;
		КодТНВЭДПриИзмененииНаСервере();

	Иначе
		
		Объект.ЕдиницаИзмерения = СтароеЗначениеЕдиницаИзмерения;
		Объект.ПервичныйДокумент = СтароеЗначениеПервичныйДокумент;
		Объект.КодТНВЭД = СтароеЗначениеТНВЭД;
		КодТНВЭДПриИзмененииНаСервере();
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура КодТНВЭДПриИзмененииНаСервере()
	
	Объект.ЕдиницаПрослеживаемости = 
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.КодТНВЭД, "ЕдиницаИзмерения");
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРНПТНаСервере(РНПТИзКвитанции)
	
	НайденныйРНПТ = ПрослеживаемостьБРУ.ПолучитьРНПТНаСервере(РНПТИзКвитанции);
	
	Объект.РНПТ = НайденныйРНПТ;
	ЭтаФорма.Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоТНВЭДПоДокументу();

	Если Объект.Ссылка.Пустая() Тогда
		Записать();
	ИначеЕсли Объект.Проведен Тогда
		Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.ОтменаПроведения));
	КонецЕсли;
	
	ТаблицаТоваров = Документы.УведомлениеОбОстаткахПрослеживаемыхТоваров.ПолучитьТаблицуТоваровПоТНВЭД(Объект);
	
	Объект.Товары.Загрузить(ТаблицаТоваров);
	
	ЗаполнитьВременныеРеквизиты();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	// ТоварыЕдиницаИзмерения, ТоварыЕдиницаПрослеживаемогоТовара, ТоварыКоличествоПрослеживаемости
	
	//ЭлементУО = УсловноеОформление.Элементы.Добавить();
	//
	//КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыЕдиницаИзмерения");
	//КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыЕдиницаПрослеживаемогоТовара");
	//КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыКоличествоПрослеживаемости");
	//
	//ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
	//	"Объект.ЕдиницаИзмерения", ВидСравненияКомпоновкиДанных.Равно, Объект.ЕдиницаПрослеживаемости);
	//
	//ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	ТипУведомления = ?(Объект.НомерКорректировки = 0, 0, 1);
	
	ПрослеживаемостьБРУ.УстановитьЗаголовокФормыУведомлениеОбОстатках(ЭтаФорма);
	
	УправлениеФормой(ЭтаФорма);

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВременныеРеквизиты()
	
	//Для каждого ТекущаяСтрока Из Объект.Товары Цикл
	//	
	//	ПараметрыНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТекущаяСтрока.Номенклатура,
	//		"ЕдиницаХраненияОстатков,ЕдиницаИзмеренияПрослеживаемости");
	//	
	//	ТекущаяСтрока.ЕдиницаИзмерения = ПараметрыНоменклатуры.ЕдиницаХраненияОстатков;
	//	ТекущаяСтрока.ЕдиницаПрослеживаемости = ПараметрыНоменклатуры.ЕдиницаИзмеренияПрослеживаемости;
	//	
	//КонецЦикла;
	
	СтароеЗначениеЕдиницаИзмерения = Объект.ЕдиницаИзмерения;
	СтароеЗначениеПервичныйДокумент = Объект.ПервичныйДокумент;
	СтароеЗначениеТНВЭД = Объект.КодТНВЭД;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	Форма.Элементы.ГруппаНомерДата.Видимость = Форма.ТипУведомления = 0;
	Форма.Элементы.Организация.ТолькоПросмотр = Форма.ТипУведомления <> 0;
	Форма.Элементы.РНПТ.ТолькоПросмотр = Форма.ТипУведомления <> 0;
	
	СтруктураДополнительныхРеквизитов = Новый Структура("ПризнакУведомления,КодФормыРеорганизации",
		Объект.ПризнакУведомления, Объект.КодФормыРеорганизации);
	
	Форма.НадписьДополнительныеСведения = 
		ПрослеживаемостьФормыВызовСервера.ТекстДополнительныеСведения(СтруктураДополнительныхРеквизитов);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьДополнительныеРеквизиты(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Форма = ЭтаФорма;
	
	Если НЕ Форма.ТолькоПросмотр Тогда
		Форма.ЗаблокироватьДанныеФормыДляРедактирования();
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ТолькоПросмотр",        Форма.ТолькоПросмотр);
	ПараметрыФормы.Вставить("ИННДоРеорганизации",    Объект.ИННДоРеорганизации);
	ПараметрыФормы.Вставить("КППДоРеорганизации",    Объект.КППДоРеорганизации);
	ПараметрыФормы.Вставить("ФормаОткрытаИзКорректировочногоУведомления",  Ложь);
	ПараметрыФормы.Вставить("ПризнакУведомления",    Объект.ПризнакУведомления);
	ПараметрыФормы.Вставить("КодФормыРеорганизации", Объект.КодФормыРеорганизации);
	ПараметрыФормы.Вставить("Продавцы", Объект.Продавцы);
	
	ОткрытьФорму(
		"Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.Форма.ФормаДополнительныхСведений",
		ПараметрыФормы, 
		Форма
	);
	
КонецПроцедуры

#КонецОбласти
