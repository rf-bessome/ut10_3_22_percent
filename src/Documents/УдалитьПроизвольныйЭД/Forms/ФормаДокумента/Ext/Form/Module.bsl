////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьЗначениеФункциональнойОпции("ИспользоватьОбменЭД") Тогда
		ТекстСообщения = ЭлектронныеДокументыСлужебныйВызовСервера.ТекстСообщенияОНеобходимостиНастройкиСистемы("РаботаСЭД");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , , Отказ);
	КонецЕсли;

	Если Параметры.Свойство("Объект") Тогда
		ЗначениеВРеквизитФормы(Параметры.Объект.ПолучитьОбъект(), "Объект");
	КонецЕсли;
		
	Если Параметры.Ключ.Пустая() Тогда
		Объект.СтатусДокумента = Перечисления.СтатусыЭД.Сформирован;
		Объект.Направление     = Перечисления.НаправленияЭД.Исходящий;
		
		// При создании произвольного ЭД на основании Присоединенного файла, структура данных файла помещается во
		// временное хранилище в обработке заполнения (в модуле объекта произвольного ЭД). Т.к. основание в созданном
		// приизвольном ЭД может быть изменено пользователем, то адрес временного хранилища данных файла надо сохранить
		// в реквизите формы, что бы после записи документа можно было по этим данным создать элемент ЭДПрисоединенныеФайлы.
		АдресВременногоХранилищаВложения = ЭлектронныеДокументыСлужебный.ПараметрИзПараметрыКлиентаНаСервере(Объект.ДокументОснование);
		
		ДокументОснование = Неопределено;
		Если Параметры.Свойство("ДокументОснование", ДокументОснование) И ЗначениеЗаполнено(ДокументОснование) Тогда
			Если ТипЗнч(ДокументОснование) = Тип("СправочникСсылка.ЭДПрисоединенныеФайлы") Тогда
				ДокументОснование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "ВладелецФайла");
			КонецЕсли;
			ЗаполнитьПоДокументуОснованию(ДокументОснование);
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ Объект.Прочитан И НЕ Объект.Ссылка.Пустая()
		И Объект.Направление = Перечисления.НаправленияЭД.Входящий Тогда
		Документ = РеквизитФормыВЗначение("Объект");
		Документ.Прочитан = Истина;
		УстановитьПривилегированныйРежим(Истина);
		Документ.Записать();
		ЗначениеВРеквизитФормы(Документ, "Объект");
		ИзмененСтатус = Истина;
	КонецЕсли;
	
	СопроводительнаяЗаписка = Объект.Текст;
	Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		ТекстДокументОснование = ЗаполнитьТекстДокументаОснования(Объект.ДокументОснование);
	КонецЕсли;
	
	ЗначениеКопирования = Неопределено;
	Если Параметры.Свойство("ЗначениеКопирования", ЗначениеКопирования)
		И ЗначениеЗаполнено(ЗначениеКопирования) Тогда
		
		ВидЭД = ПредопределенноеЗначение("Перечисление.ВидыЭД.ПроизвольныйЭД");
		Объект.ТребуетсяПодтверждение = ТребуетсяОтветнаяПодписьПоСоглашению(
			ВидЭД, Объект.Организация, Объект.Контрагент);
		ТребуетсяИзвещение = ТребуетсяИзвещениеПоСоглашению(ВидЭД, Объект.Организация, Объект.Контрагент);
	КонецЕсли;
	
	ОбновитьДанныеОВложении(Ложь);
	
	ЭДОтклонен = ЗначениеЗаполнено(ЭлектронныйДокументСсылка)
				И (ЭлектронныйДокументСсылка.СтатусЭД = Перечисления.СтатусыЭД.Отклонен
					ИЛИ ЭлектронныйДокументСсылка.СтатусЭД = Перечисления.СтатусыЭД.ОтклоненПолучателем
					ИЛИ ЭлектронныйДокументСсылка.СтатусЭД = Перечисления.СтатусыЭД.ОшибкаПередачи);
					
	ЭДАннулирован = ЗначениеЗаполнено(ЭлектронныйДокументСсылка)
				И (ЭлектронныйДокументСсылка.СтатусЭД = Перечисления.СтатусыЭД.Аннулирован
					ИЛИ ЭлектронныйДокументСсылка.СтатусЭД = Перечисления.СтатусыЭД.ОтправленоПредложениеОбАннулировании
					ИЛИ ЭлектронныйДокументСсылка.СтатусЭД = Перечисления.СтатусыЭД.СформированоПредложениеОбАннулировании
					ИЛИ ЭлектронныйДокументСсылка.СтатусЭД = Перечисления.СтатусыЭД.ПолученоПредложениеОбАннулировании);

	ПерезаполнитьКомментарии();
	
	ОбновитьИнформациюПодвала();
	ОбновитьЗаголовокФормы();
	
	УстановитьДоступностьЭлементов();
	
	// Установка доступности флага  Требуется подтверждение.
	УстановитьДоступностьТребуетсяПодтверждение();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ЗаполнитьПоДокументуОснованию(ВыбранноеЗначение);
	ОбновитьОтображениеДанных();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// До записи документа вложение хранится во временном хранилище, после записи документа
	// создается новый элемент справочника ЭДПрисоединенныеФайлы, в который записывается данные из временного хранилища.
	ДанныеВложенияОбновлены = ДобавитьВложениеИзВременногоХранилищаПослеЗаписиДокумента();
	
	ОбновитьЗаголовокФормы();
	
	Если ОбновитьРеквизитыВложения И НЕ ДанныеВложенияОбновлены Тогда
		ДобавитьОбновитьПрисоединенныеФайлыКСообщению();
	КонецЕсли;
	УстановитьДоступностьЭлементов();
	ОбновитьИнформациюПодвала();
	Если Объект.Направление  = Перечисления.НаправленияЭД.Входящий И ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		// при ручном выборе основания во входящем документе надо удалить запись регистра
		// ИдентификаторыДокументовОснованийВладельцевЭД для текущего документа
		НаборЗаписей = РегистрыСведений.ИдентификаторыДокументовОснованийВладельцевЭД.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ЭлектронныйДокумент.Установить(ЭлектронныйДокументСсылка);
		НаборЗаписей.Прочитать();
		Для Каждого ЗаписьРегистра Из НаборЗаписей Цикл
			НаборЗаписей.Удалить(ЗаписьРегистра);
		КонецЦикла;
		НаборЗаписей.Записать(Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ЭтоАдресВременногоХранилища(АдресВременногоХранилищаВложения) Тогда
		ПроверитьЗахватНаРедактирование(Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если НЕ ЕстьСоглашение(ТекущийОбъект) Тогда
		Отказ = Истина;
	Иначе
		// Если вложение записано, то при изменении перечисленных ниже реквизитов
		// надо принудительно перезаполнить их во вложении.
		Если Объект.Направление = Перечисления.НаправленияЭД.Исходящий И Не СтатусПередан Тогда
			ОбновитьРеквизитыВложения = (ТекущийОбъект.Контрагент <> Объект.Ссылка.Контрагент
				ИЛИ ТекущийОбъект.Организация <> Объект.Ссылка.Организация
				ИЛИ ТекущийОбъект.Номер <> Объект.Ссылка.Номер
				ИЛИ ТекущийОбъект.Дата <> Объект.Ссылка.Дата
				ИЛИ ТекущийОбъект.Текст <> Объект.Ссылка.Текст
				ИЛИ ТекущийОбъект.ДокументОснование <> Объект.Ссылка.ДокументОснование);
		КонецЕсли;
	КонецЕсли;
	
	ТекущийОбъект.Текст = СокрЛП(СопроводительнаяЗаписка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьСостояниеЭД" Тогда
		ВыполнитьОбработкуОповещенияНаСервере();
		ОбновитьОтображениеДанных();
		
	ИначеЕсли ВРег(ИмяСобытия) = ВРег("Запись_НаборКонстант")
		И ВРег(Источник) = ВРег("ИспользоватьЭлектронныеЦифровыеПодписи")
		И ВРег(Источник) = ВРег("ИспользоватьЭлектронныеПодписи") Тогда
		
		УстановитьДоступностьТребуетсяПодтверждение(Истина);
		УстановитьДоступностьЭлементов(Истина);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ПОЛЕЙ ФОРМЫ

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	ПроверитьВозможностьИзмененияУчастникаОбмена("Контрагент");
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ПроверитьВозможностьИзмененияУчастникаОбмена("Организация");
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	ОбновитьЗаголовокФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура НомерПриИзменении(Элемент)
	
	ОбновитьЗаголовокФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ТребуетсяПодтверждениеПриИзменении(Элемент)
	
	Модифицированность = Истина;
	ОбновитьСтатусСостояниеЭД();
	
КонецПроцедуры

&НаКлиенте
Процедура ЭЦПВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Найти(Поле.Имя, "ЭЦПКомуВыданСертификат")>0 Тогда
		ДобавитьСертификатВДоверенные(Элемент.ТекущиеДанные);
		Если Элемент.ТекущиеДанные <> Неопределено И НЕ Элемент.ТекущиеДанные.ОтсутствуетВСписке Тогда
			ПоказатьСертификат(Элемент.ТекущиеДанные.НомерСтроки, Элемент.ТекущиеДанные.Отпечаток);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭЦППриАктивизацииСтроки(Элемент)
	
	Если Элементы.ЭЦП.ТекущиеДанные <> Неопределено Тогда
		Элементы.ДоверятьСертификату.Доступность = Элементы.ЭЦП.ТекущиеДанные.ОтсутствуетВСписке;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстВложениеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеФайла = ДанныеФайлаВложения(АдресВременногоХранилищаВложения, ЭлектронныйДокументСсылка, УникальныйИдентификатор);
	
	Если НЕ ПроверитьДействиеРазрешено(ДанныеФайла) Тогда
		Возврат;
	КонецЕсли;
	
	ФайлРедактируется = ДанныеФайла.ФайлРедактируется И ДанныеФайла.ФайлРедактируетТекущийПользователь;
	ПрисоединенныеФайлыКлиент.ОткрытьФайл(ДанныеФайла, ФайлРедактируется);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектДополнительнаяИнформацияПриИзменении(Элемент)
	
	Модифицированность = Истина;
	Оповестить("ОбновитьСостояниеЭД");
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьДокументОснованиеОтсутствуетНажатие(Элемент)
	
	СписокТиповДокументовОснований = СписокТиповДокументовОснованийПроизвольногоДокумента();
	Если СписокТиповДокументовОснований.Количество() > 0 Тогда
		ВыбранныйТип = Неопределено;
		Если СписокТиповДокументовОснований.Количество() = 1 Тогда
			ВыбранныйТип = СписокТиповДокументовОснований[0].Значение;
		Иначе
			ЗаголовокСпискаВыбора = НСтр("ru = 'Выберите тип документа основания...'");
			ЭлементПоУмолчанию = СписокТиповДокументовОснований.НайтиПоЗначению("ПроизвольныйЭД");
			ВыбранныйТип = СписокТиповДокументовОснований.ВыбратьЭлемент(ЗаголовокСпискаВыбора, ЭлементПоУмолчанию);
		КонецЕсли;
		Если ВыбранныйТип <> Неопределено Тогда
			СтруктураОтбора = Новый Структура;
			Если ЗначениеЗаполнено(Объект.Организация) Тогда
				СтруктураОтбора.Вставить("Организация", Объект.Организация);
			КонецЕсли;
			Если ЗначениеЗаполнено(Объект.Контрагент) Тогда
				СтруктураОтбора.Вставить("Контрагент", Объект.Контрагент);
				Если ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
					СтруктураОтбора.Вставить("ДоговорКонтрагента", Объект.ДоговорКонтрагента);
				КонецЕсли;
			КонецЕсли;
			ПараметрыОткрытия = Новый Структура;
			ПараметрыОткрытия.Вставить("Отбор", СтруктураОтбора);
			ВыбранныйДокумент = ОткрытьФормуМодально(ВыбранныйТип.Значение + ".ФормаВыбора", ПараметрыОткрытия, ЭтаФорма);
			Если ЗначениеЗаполнено(ВыбранныйДокумент) И ВыбранныйДокумент <> Объект.Ссылка Тогда
				ЗаполнитьПоДокументуОснованию(ВыбранныйДокумент);
				Модифицированность = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументОснованиеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		ОткрытьЗначение(Объект.ДокументОснование);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаОчиститьОснованиеНажатие(Элемент)
	
	ТекстВопроса = НСтр("ru = 'Убрать документ-основание?'");
	Результат = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Если Результат = КодВозвратаДиалога.Да Тогда
		Объект.ДокументОснование = Неопределено;
		УстановитьДоступностьЭлементов();
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ОтменитьОтклонение(Команда)
	
	Если ОбработатьОтменуОтклоненияЭД() Тогда
		Оповестить("ОбновитьСостояниеЭД");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьИОтправить(Команда)
	
	ОчиститьСообщения();
	
	Если ПроверитьЗаполнение() И (ЗначениеЗаполнено(ЭлектронныйДокументСсылка)
									ИЛИ ЭтоАдресВременногоХранилища(АдресВременногоХранилищаВложения)) Тогда
		
		Если ДокументЗаписан() И ПроверитьЗахватНаРедактирование() Тогда
			ПодписатьИОтправитьДокумент(Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьДокумент(Команда)
	
	Если ПроверитьЗаполнение() И (ЗначениеЗаполнено(ЭлектронныйДокументСсылка)
									ИЛИ ЭтоАдресВременногоХранилища(АдресВременногоХранилищаВложения)) Тогда
		
		Если ДокументЗаписан() И ПроверитьЗахватНаРедактирование() Тогда
			ПодписатьИОтправитьДокумент(Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отклонить(Команда)
	
	Если ДокументЗаписан() И НЕ ЭДОтклонен И ЗначениеЗаполнено(ЭлектронныйДокументСсылка)
		И ЭлектронныеДокументыСлужебныйВызовСервера.МожноОтклонитьЭтотЭД(ЭлектронныйДокументСсылка) Тогда
		ОтклонитьАннулироватьЭД(Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПодписи(Команда)
	
	ОчиститьСообщения();
	
	Если ЗначениеЗаполнено(ЭлектронныйДокументСсылка) Тогда
		Если ЭлектронныеДокументыСлужебныйВызовСервера.ВыполнятьКриптооперацииНаСервере() Тогда
			ЭлектронныеДокументыСлужебныйВызовСервера.ОпределитьСтатусыПодписей(ЭлектронныйДокументСсылка, Истина);
		Иначе
			ЭлектронныеДокументыСлужебныйКлиент.ОпределитьСтатусыПодписей(ЭлектронныйДокументСсылка);
		КонецЕсли;
		
		ОбновитьОтображениеДанных();
		ЗаполнитьТаблицуЭЦП();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСертификат(Команда)
	
	Если Элементы.ЭЦП.ТекущиеДанные <> Неопределено Тогда
		ПоказатьСертификат(Элементы.ЭЦП.ТекущиеДанные.НомерСтроки, Элементы.ЭЦП.ТекущиеДанные.Отпечаток);
	Иначе
		ОчиститьСообщения();
		ТекстОшибки = НСтр("ru = 'Выберите сертификат в списке установленных подписей.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоверятьЭтомуСертификату(Команда)
	
	ДобавитьСертификатВДоверенные(Элементы.ЭЦП.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКомментарий(Команда)
	
	Если ДокументЗаписан() И ЗначениеЗаполнено(ЭлектронныйДокументСсылка) Тогда
		СтруктураПараметров = Новый Структура("Описание", Комментарий);
		ИзменитьЗначенияРеквизитовНаСервере(ЭлектронныйДокументСсылка, СтруктураПараметров);
		Комментарий = "";
		Оповестить("ОбновитьСостояниеЭД");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Перенаправить(Команда)
	
	Если ЗначениеЗаполнено(ЭлектронныйДокументСсылка) И ДокументЗаписан() Тогда
		МассивЭД = Новый Массив;
		МассивЭД.Добавить(ЭлектронныйДокументСсылка);
		ЭлектронныеДокументыСлужебныйКлиент.ИзменитьОтветственного(МассивЭД);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЖурналСобытийЭДО(Команда)
	
	ПараметрыФормы = Новый Структура;
	
	Отбор = Новый Структура;
	Отбор.Вставить("ПрисоединенныйФайл", ЭлектронныйДокументСсылка);
	
	ПараметрыФормы.Вставить("Отбор", Отбор);
	ПараметрыФормы.Вставить("РежимОткрытияОкна", РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	ОткрытьФорму("РегистрСведений.ЖурналСобытийЭД.ФормаСписка", ПараметрыФормы, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура АннулироватьЭД(Команда)
	
	Если ДокументЗаписан() И НЕ ЭДАннулирован И ЗначениеЗаполнено(ЭлектронныйДокументСсылка)
		И ЭлектронныеДокументыСлужебныйВызовСервера.МожноАннулироватьЭтотЭД(ЭлектронныйДокументСсылка) Тогда
		ОтклонитьАннулироватьЭД();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПринятьАннулирование(Команда)
	
	ОтклонитьАннулирование = Ложь;
	ОбработатьПредложениеОбАннулировании(ОтклонитьАннулирование);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтклонитьАннулирование(Команда)
	
	ОтклонитьАннулирование = Истина;
	ОбработатьПредложениеОбАннулировании(ОтклонитьАннулирование);
	
КонецПроцедуры


///////////////////////////////////////////////////////////////////////////////////
// Обработчики команд вложения

&НаКлиенте
Процедура СохранитьВложениеВФайл(Команда)
	
	Если ЗначениеЗаполнено(ЭлектронныйДокументСсылка) Тогда
		ДанныеФайла = ПолучитьДанныеФайла(ЭлектронныйДокументСсылка, УникальныйИдентификатор);
		ПрисоединенныеФайлыКлиент.СохранитьВместеСЭЦП(ЭлектронныйДокументСсылка, ДанныеФайла, УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Редактировать(Команда)
	
	ДанныеФайла = ДанныеФайлаВложения(АдресВременногоХранилищаВложения, ЭлектронныйДокументСсылка, УникальныйИдентификатор);
	
	Если НЕ ПроверитьДействиеРазрешено(ДанныеФайла) Тогда
		Возврат;
	КонецЕсли;
	
	Если (ДанныеФайла.ФайлРедактируется И НЕ ДанныеФайла.ФайлРедактируетТекущийПользователь)
		ИЛИ ДанныеФайла.Зашифрован ИЛИ ДанныеФайла.ПодписанЭЦП Тогда
		// Файл может быть изменен в другом сеансе.
		Если ДанныеФайла.Ссылка <> Неопределено Тогда
			ОповеститьОбИзменении(ДанныеФайла.Ссылка);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если НЕ ДанныеФайла.ФайлРедактируется Тогда
		ЗанятьФайлДляРедактированияСервер(ДанныеФайла);
		
		Если ДанныеФайла.Ссылка = Неопределено Тогда
			АдресВременногоХранилищаВложения = ПоместитьВоВременноеХранилище(ДанныеФайла, УникальныйИдентификатор);
		Иначе
			ОповеститьОбИзменении(ДанныеФайла.Ссылка);
		КонецЕсли;
		УстановитьДоступностьЭлементов();
	КонецЕсли;
	
	ПрисоединенныеФайлыКлиент.ОткрытьФайл(ДанныеФайла, Истина);
	ВложениеРедактируется = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакончитьРедактирование(Команда)
	
	ДанныеФайла = ДанныеФайлаВложения(АдресВременногоХранилищаВложения, ЭлектронныйДокументСсылка, УникальныйИдентификатор);
	
	Если НЕ ПроверитьДействиеРазрешено(ДанныеФайла)
		ИЛИ (НЕ ДанныеФайла.ФайлРедактируется ИЛИ НЕ ДанныеФайла.ФайлРедактируетТекущийПользователь) Тогда
		Возврат;
	КонецЕсли;
	
	ИнформацияОФайле = Новый Структура;
	Если ПрисоединенныеФайлыСлужебныйКлиент.ПоместитьРедактируемыйФайлНаДискеВХранилище(ДанныеФайла, ИнформацияОФайле, УникальныйИдентификатор) Тогда
		ЗакончитьРедактированиеВыполненоПомещение(ИнформацияОФайле, ДанныеФайла);
	КонецЕсли;
	УдалитьВременныйФайлВложения(ДанныеФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура Освободить(Команда)
	
	ДанныеФайла = ДанныеФайлаВложения(АдресВременногоХранилищаВложения, ЭлектронныйДокументСсылка, УникальныйИдентификатор);
	ОсвободитьФайл(ДанныеФайла);
	УстановитьДоступностьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВложениеИзПрисоединенныхФайлов(Команда)
	
	ОбщегоНазначения.СообщитьОбОшибке("Добавление вложений из присоединенных файлов не поддерживается.");
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВложениеИзФайлаНаДиске(Команда)
	
	ВыбранныйФайл   = "";
	АдресВременногоХранилища = "";
	Если ПоместитьФайл(АдресВременногоХранилища, "", ВыбранныйФайл, Истина, УникальныйИдентификатор) Тогда
		СтруктураФайла  = Новый Структура;
		ФайлКлиента = Новый Файл(ВыбранныйФайл);
		РабочийКаталог = Строка(УникальныйИдентификатор);
		РабочийКаталог = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(РабочийКаталог, "");
		РабочийКаталог = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(РабочийКаталог);
		СтруктураФайла  = Новый Структура;
		СтруктураФайла = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ВыбранныйФайл);
		СтруктураФайла.Вставить("ИмяФайла",                           СтруктураФайла.Имя);
		СтруктураФайла.Вставить("Наименование",                       СтруктураФайла.ИмяБезРасширения);
		СтруктураФайла.Вставить("Расширение",                         ОбщегоНазначенияКлиентСервер.РасширениеБезТочки(СтруктураФайла.Расширение));
		СтруктураФайла.Вставить("Размер",                             ФайлКлиента.Размер());
		СтруктураФайла.Вставить("Редактирует",                        Неопределено);
		СтруктураФайла.Вставить("ПодписанЭЦП",                        Ложь);
		СтруктураФайла.Вставить("Зашифрован",                         Ложь);
		СтруктураФайла.Вставить("ФайлРедактируется",                  Ложь);
		СтруктураФайла.Вставить("ОтносительныйПуть",                  РабочийКаталог);
		СтруктураФайла.Вставить("СсылкаНаДвоичныеДанныеФайла",        АдресВременногоХранилища);
		СтруктураФайла.Вставить("ДатаМодификацииУниверсальная",       ФайлКлиента.ПолучитьУниверсальноеВремяИзменения());
		СтруктураФайла.Вставить("ФайлРедактируетТекущийПользователь", Ложь);
		АдресВременногоХранилищаВложения = ПоместитьВоВременноеХранилище(СтруктураФайла, УникальныйИдентификатор);
		ОбновитьДанныеОВложении();
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Вложение

&НаКлиенте
Процедура УдалитьВременныйФайлВложения(ДанныеФайла)
	
	РабочийКаталогПользователя = ФайловыеФункцииКлиент.ПолучитьПутьККаталогуДанныхПользователя();
	ПолноеИмяФайлаНаКлиенте = РабочийКаталогПользователя + ДанныеФайла.ОтносительныйПуть + ДанныеФайла.ИмяФайла;
	УдалитьФайлы(ПолноеИмяФайлаНаКлиенте);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеФайлаВложения(АдресВременногоХранилищаВложения, ЭлектронныйДокументСсылка, УникальныйИдентификатор)
	
	ДанныеФайла = Неопределено;
	Если ЭтоАдресВременногоХранилища(АдресВременногоХранилищаВложения) Тогда
		// Файл во временном хранилище
		ДанныеФайла = ПолучитьИзВременногоХранилища(АдресВременногоХранилищаВложения);
		ДанныеФайла.Вставить("Ссылка", Неопределено);
	ИначеЕсли ЗначениеЗаполнено(ЭлектронныйДокументСсылка) Тогда
		// Файл записан в ИБ
		ДанныеФайла = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьДанныеФайла(ЭлектронныйДокументСсылка, УникальныйИдентификатор);
		ДанныеФайла.Вставить("Ссылка", ЭлектронныйДокументСсылка);
	КонецЕсли;
	
	Возврат ДанныеФайла;
	
КонецФункции

&НаКлиенте
Функция ПроверитьДействиеРазрешено(ДанныеФайла, Знач ТекущееДействие = "")
	
	Если ДанныеФайла = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ТекущееДействие = "ПометкаУдаления" И ДанныеФайла.ФайлРедактируется Тогда
		
		Если ДанныеФайла.ФайлРедактируетТекущийПользователь Тогда
			ТекстПредупреждения = НСтр("ru = 'Действие недоступно, так как файл занят для редактирования.'");
		Иначе
			ТекстПредупреждения = НСтр("ru = 'Действие недоступно, так как файл занят для редактирования
			                                 |другим пользователем.'");
		КонецЕсли;
		
		Предупреждение(ТекстПредупреждения);
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗанятьФайлДляРедактированияСервер(ДанныеФайла)
	
	Если ДанныеФайла.Ссылка = Неопределено Тогда
		ДанныеФайла.Редактирует                        = Пользователи.АвторизованныйПользователь();
		ДанныеФайла.ФайлРедактируется                  = Истина;
		ДанныеФайла.ФайлРедактируетТекущийПользователь = Истина;
	Иначе
		ПрисоединенныеФайлыСлужебный.ЗанятьФайлДляРедактированияСервер(ДанныеФайла.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОсвободитьФайл(ДанныеФайла)
	
	Если НЕ ПроверитьДействиеРазрешено(ДанныеФайла) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ДанныеФайла.ФайлРедактируется
	 ИЛИ НЕ ДанныеФайла.ФайлРедактируетТекущийПользователь Тогда
		Возврат;
	КонецЕсли;
	
	ОсвободитьФайлНаСервере(ДанныеФайла);
	Если ДанныеФайла.Ссылка = Неопределено Тогда
		АдресВременногоХранилищаВложения = ПоместитьВоВременноеХранилище(ДанныеФайла, УникальныйИдентификатор);
	Иначе
		ОповеститьОбИзменении(ДанныеФайла.Ссылка);
	КонецЕсли;
	УдалитьВременныйФайлВложения(ДанныеФайла);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОсвободитьФайлНаСервере(ДанныеФайла)
	
	Если ДанныеФайла.Ссылка = Неопределено Тогда
		ДанныеФайла.Редактирует                        = Справочники.Пользователи.ПустаяСсылка();
		ДанныеФайла.ФайлРедактируется                  = Ложь;
		ДанныеФайла.ФайлРедактируетТекущийПользователь = Ложь;
	Иначе
		ПрисоединенныеФайлыСлужебный.ОсвободитьФайл(ДанныеФайла.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакончитьРедактированиеВыполненоПомещение(ИнформацияОФайле, ДанныеФайла)
	
	Если ДанныеФайла.Ссылка = Неопределено Тогда
		ДанныеФайла.Редактирует                        = ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка");
		ДанныеФайла.ФайлРедактируется                  = Ложь;
		ДанныеФайла.ФайлРедактируетТекущийПользователь = Ложь;
		ДанныеФайла.СсылкаНаДвоичныеДанныеФайла        = ИнформацияОФайле.АдресФайлаВоВременномХранилище;
		ЗаполнитьЗначенияСвойств(ДанныеФайла, ИнформацияОФайле);
		Если ТипЗнч(ИнформацияОФайле.АдресФайлаВоВременномХранилище) = Тип("ДвоичныеДанные") Тогда
			ДвоичныеДанные = ИнформацияОФайле.АдресФайлаВоВременномХранилище;
		Иначе
			ДвоичныеДанные = ПолучитьИзВременногоХранилища(ИнформацияОФайле.АдресФайлаВоВременномХранилище);
		КонецЕсли;
		ДанныеФайла.Размер = ДвоичныеДанные.Размер();
		АдресВременногоХранилищаВложения = ПоместитьВоВременноеХранилище(ДанныеФайла, УникальныйИдентификатор);
	Иначе
		ПоместитьФайлВХранилищеИОсвободить(ДанныеФайла.Ссылка, ИнформацияОФайле);
		ОповеститьОбИзменении(ДанныеФайла.Ссылка);
	КонецЕсли;
	УдалитьВременныйФайлВложения(ДанныеФайла);
	ОбновитьДанныеОВложении();
	УстановитьДоступностьЭлементов();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПоместитьФайлВХранилищеИОсвободить(Знач ПрисоединенныйФайл, Знач ИнформацияОФайле)
	
	ПрисоединенныеФайлыСлужебный.ПоместитьФайлВХранилищеИОсвободить(ПрисоединенныйФайл, ИнформацияОФайле);
	
КонецПроцедуры

&НаСервере
Функция ДобавитьВложениеИзВременногоХранилищаПослеЗаписиДокумента()
	
	ДанныеОбновлены = Ложь;
	Если ЭтоАдресВременногоХранилища(АдресВременногоХранилищаВложения) Тогда
		СтруктураФайла = ПолучитьИзВременногоХранилища(АдресВременногоХранилищаВложения);
		АдресВременногоХранилищаВложения = Неопределено;
		Если ТипЗнч(СтруктураФайла) = Тип("Структура") И СтруктураФайла.Свойство("СсылкаНаДвоичныеДанныеФайла") Тогда
			Если ЗначениеЗаполнено(ЭлектронныйДокументСсылка) Тогда
				СтруктураФайла.Вставить("АдресФайлаВоВременномХранилище", СтруктураФайла.СсылкаНаДвоичныеДанныеФайла);
				СтруктураФайла.Вставить("АдресВременногоХранилищаТекста", "");
				СтруктураФайла.Вставить("ИмяБезРасширения",               СтруктураФайла.Наименование);
				ПрисоединенныеФайлы.ОбновитьПрисоединенныйФайл(ЭлектронныйДокументСсылка, СтруктураФайла);
			КонецЕсли;
			ДобавитьОбновитьПрисоединенныеФайлыКСообщению(СтруктураФайла.СсылкаНаДвоичныеДанныеФайла);
			ДанныеОбновлены = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДанныеОбновлены;
	
КонецФункции

&НаКлиенте
Функция ПроверитьЗахватНаРедактирование(Отказ = Ложь)
	
	ВложениеСвободно = Истина;
	ДанныеФайла = ДанныеФайлаВложения(АдресВременногоХранилищаВложения, ЭлектронныйДокументСсылка, УникальныйИдентификатор);
	Если ДанныеФайла.ФайлРедактируется Тогда
		Если ДанныеФайла.ФайлРедактируетТекущийПользователь Тогда
			// Вложение захвачено для редактирования текущим пользователем
			ТекстВопроса = НСтр("ru = 'Вложение ""%1"" захвачено для редактирования.
				|Для продолжения надо завершить редактирование.'");
			ТекстВопроса = СтрЗаменить(ТекстВопроса, "%1", ЭлектронныйДокументИмяФайла);
			СписокКнопок = Новый СписокЗначений;
			СписокКнопок.Добавить("ЗакончитьРедактирование", "Закончить редактирование");
			СписокКнопок.Добавить("Освободить", "Отменить редактирование");
			СписокКнопок.Добавить("Отменить", "Отменить");
			Результат = Вопрос(ТекстВопроса, СписокКнопок);
			Если Результат = "Освободить" Тогда
				ОсвободитьФайл(ДанныеФайла);
			ИначеЕсли Результат = "ЗакончитьРедактирование" Тогда
				ИнформацияОФайле = Новый Структура;
				Если ПрисоединенныеФайлыСлужебныйКлиент.ПоместитьРедактируемыйФайлНаДискеВХранилище(ДанныеФайла, ИнформацияОФайле, УникальныйИдентификатор) Тогда
					ЗакончитьРедактированиеВыполненоПомещение(ИнформацияОФайле, ДанныеФайла);
				КонецЕсли;
			Иначе
				Отказ = Истина;
			КонецЕсли;
		Иначе
			ТекстПредупреждения = НСтр("ru = 'Действие недоступно, так как файл занят для редактирования
			                                 |другим пользователем.'");
			Предупреждение(ТекстПредупреждения);
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ВложениеСвободно;
	
КонецФункции

&НаСервере
Процедура ДобавитьОбновитьПрисоединенныеФайлыКСообщению(АдресВХранилище = Неопределено)
	
	ПараметрыСоглашения = ЭлектронныеДокументыСлужебный.ОпределитьНастройкиОбменаЭДПоИсточнику(Объект.Ссылка);
	Если ЗначениеЗаполнено(ПараметрыСоглашения) Тогда
		ИдентификаторОрганизации = ПараметрыСоглашения.ИдентификаторОрганизации;
		ИдентификаторКонтрагента = ПараметрыСоглашения.ИдентификаторКонтрагента;
		СоглашениеЭД             = ПараметрыСоглашения.СоглашениеЭД;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПараметрыСоглашения) И НЕ ЗначениеЗаполнено(СоглашениеЭД) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураДокумента =  Новый Структура;
	Если ЗначениеЗаполнено(ЭлектронныйДокументСсылка) Тогда
		ДобавленныйФайл = ЭлектронныйДокументСсылка;
	Иначе
		Если НЕ (ЗначениеЗаполнено(ЭлектронныйДокументИмяФайлаБезРасширения)
				 И ЭтоАдресВременногоХранилища(АдресВХранилище)) Тогда
			Возврат;
		КонецЕсли;
		
		НоваяСсылкаЭД = Справочники.ЭДПрисоединенныеФайлы.ПолучитьСсылку();
		УстановитьПривилегированныйРежим(Истина);
		ДобавленныйФайл = ПрисоединенныеФайлы.ДобавитьФайл(Объект.Ссылка,
														   ЭлектронныйДокументИмяФайлаБезРасширения,
														   ЭлектронныйДокументРасширение,
														   ТекущаяДатаСеанса(),
														   ТекущаяДатаСеанса(),
														   АдресВХранилище,
														   ,
														   ,
														   НоваяСсылкаЭД);
	    УстановитьПривилегированныйРежим(Ложь);
		НомерДокумента = ЭлектронныеДокументыВнутренний.ВернутьИдЭД(ДобавленныйФайл);
		СтруктураДокумента.Вставить("ВладелецЭД",                Объект.Ссылка);
		СтруктураДокумента.Вставить("ВидЭД",                     Перечисления.ВидыЭД.ПроизвольныйЭД);
		СтруктураДокумента.Вставить("НаправлениеЭД",             Перечисления.НаправленияЭД.Исходящий);
		СтруктураДокумента.Вставить("Автор",                     Пользователи.АвторизованныйПользователь());
		СтруктураДокумента.Вставить("НомерЭД",                   НомерДокумента);
		СтруктураДокумента.Вставить("УникальныйИД",              НомерДокумента);
		СтруктураДокумента.Вставить("НомерВерсииЭД",             0);
		СтруктураДокумента.Вставить("ТипЭлементаВерсииЭД",       Перечисления.ТипыЭлементовВерсииЭД.ПервичныйЭД);
	КонецЕсли;
	СтруктураДокумента.Вставить("Организация",               Объект.Организация);
	СтруктураДокумента.Вставить("Контрагент",                Объект.Контрагент);
	СтруктураДокумента.Вставить("Отправитель",               ИдентификаторОрганизации);
	СтруктураДокумента.Вставить("Получатель",                ИдентификаторКонтрагента);
	СтруктураДокумента.Вставить("ПрофильНастроекЭДО",        ПрофильНастроекЭДО);
	СтруктураДокумента.Вставить("СоглашениеЭД",              СоглашениеЭД);
	СтруктураДокумента.Вставить("СтатусЭД",                  Перечисления.СтатусыЭД.Сформирован);
	СтруктураДокумента.Вставить("НаименованиеФайла",         ЭлектронныйДокументИмяФайлаБезРасширения);
	СтруктураДокумента.Вставить("ДополнительнаяИнформация",  Объект.Текст);
	СтруктураДокумента.Вставить("НомерДокументаОтправителя", Объект.Номер);
	СтруктураДокумента.Вставить("ДатаДокументаОтправителя",  Объект.Дата);
	СтруктураДокумента.Вставить("УдалитьЭП",  Истина);
	
	СтруктураДокумента.Вставить("ТребуетсяПодтверждение",    Объект.ТребуетсяПодтверждение);
	СтруктураДокумента.Вставить("ТребуетсяИзвещение",        ТребуетсяИзвещение);
	
	ТаблицаИдентификаторов = Новый ТаблицаЗначений;
	Если ЗначениеЗаполнено(Объект.ДокументОснование)
		И Метаданные.Документы.Содержит(Объект.ДокументОснование.Метаданные()) Тогда
		// Т.к. документом-основанием может быть документ ИБ, сформированный на основании входящего ЭД,
		// то чтобы на стороне получателя данный документ правильно определил основание, в качестве
		// идентификатора передаётся НомерЭД (всегда является идентификатором документа ИБ, на основании
		// которого формируется ЭДО).
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ЭДПрисоединенныеФайлы.НомерЭД КАК ИдентификаторДокументаОснования,
			|	ЭДПрисоединенныеФайлы.УникальныйИД КАК ИдентификаторЭДДокументаОснования
			|ИЗ
			|	Справочник.ЭДПрисоединенныеФайлы КАК ЭДПрисоединенныеФайлы
			|ГДЕ
			|	ЭДПрисоединенныеФайлы.ВладелецФайла = &ДокументОснование
			|	И (ЭДПрисоединенныеФайлы.ТипЭлементаВерсииЭД = ЗНАЧЕНИЕ(Перечисление.ТипыЭлементовВерсииЭД.ПервичныйЭД)
			|			ИЛИ ЭДПрисоединенныеФайлы.ТипЭлементаВерсииЭД = ЗНАЧЕНИЕ(Перечисление.ТипыЭлементовВерсииЭД.ЭСФ)
			|			ИЛИ ЭДПрисоединенныеФайлы.ТипЭлементаВерсииЭД = ЗНАЧЕНИЕ(Перечисление.ТипыЭлементовВерсииЭД.ДОП)
			|			ИЛИ ЭДПрисоединенныеФайлы.ТипЭлементаВерсииЭД = ЗНАЧЕНИЕ(Перечисление.ТипыЭлементовВерсииЭД.СЧФДОПУПД)
			|			ИЛИ ЭДПрисоединенныеФайлы.ТипЭлементаВерсииЭД = ЗНАЧЕНИЕ(Перечисление.ТипыЭлементовВерсииЭД.СЧФУПД)
			|			ИЛИ ЭДПрисоединенныеФайлы.ТипЭлементаВерсииЭД = ЗНАЧЕНИЕ(Перечисление.ТипыЭлементовВерсииЭД.ДОПУПД)
			|			ИЛИ ЭДПрисоединенныеФайлы.ТипЭлементаВерсииЭД = ЗНАЧЕНИЕ(Перечисление.ТипыЭлементовВерсииЭД.КСЧФДИСУКД)
			|			ИЛИ ЭДПрисоединенныеФайлы.ТипЭлементаВерсииЭД = ЗНАЧЕНИЕ(Перечисление.ТипыЭлементовВерсииЭД.КСЧФУКД)
			|			ИЛИ ЭДПрисоединенныеФайлы.ТипЭлементаВерсииЭД = ЗНАЧЕНИЕ(Перечисление.ТипыЭлементовВерсииЭД.ДИСУКД))";
		
		Запрос.УстановитьПараметр("ДокументОснование", Объект.ДокументОснование);
		ТаблицаИдентификаторов = Запрос.Выполнить().Выгрузить();
		Если ТаблицаИдентификаторов.Количество() = 0 Тогда
			СтрокаИдентификатора = ТаблицаИдентификаторов.Добавить();
			СтрокаИдентификатора.ИдентификаторДокументаОснования = Строка(Объект.ДокументОснование.УникальныйИдентификатор());
			СтрокаИдентификатора.ИдентификаторЭДДокументаОснования = Строка(Объект.ДокументОснование.УникальныйИдентификатор());
		КонецЕсли;
	КонецЕсли;
	// Т.к. основание может быть очищено пользователем на форме,
	// то надо при обновлении данных присоединенного файла очистить массив идентификаторов
	СтруктураДокумента.Вставить("ИдентификаторыДокументовИЭДОснований", ТаблицаИдентификаторов);
	
	ЭлектронныеДокументыСлужебный.ИзменитьПоСсылкеПрисоединенныйФайл(ДобавленныйФайл, СтруктураДокумента, Ложь);
	
	Прочитать();
	ОбновитьДанныеОВложении();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеОВложении(УстановитьДоступность = Истина)
	
	Если ЭтоАдресВременногоХранилища(АдресВременногоХранилищаВложения) Тогда
		СтруктураФайла = ПолучитьИзВременногоХранилища(АдресВременногоХранилищаВложения);
		Если ТипЗнч(СтруктураФайла) = Тип("Структура") И СтруктураФайла.Свойство("СсылкаНаДвоичныеДанныеФайла") Тогда
			ЭлектронныйДокументРасширение  = ОбщегоНазначенияКлиентСервер.РасширениеБезТочки(СтруктураФайла.Расширение);
			ЭлектронныйДокументИмяФайла    = СокрЛП(СтруктураФайла.ИмяФайла);
			ЭлектронныйДокументИмяФайлаБезРасширения = СокрЛП(СтруктураФайла.Наименование);
			ЭлектронныйДокументПиктограмма = ФайловыеФункцииКлиентСервер.ПолучитьИндексПиктограммыФайла(СтруктураФайла.Расширение);
			Элементы.ТекстВложение.Ширина  = СтрДлина(ЭлектронныйДокументИмяФайла);
		КонецЕсли;
	ИначеЕсли ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ЗапросВложений = Новый Запрос;
		ЗапросВложений.УстановитьПараметр("ВладелецФайла", Объект.Ссылка);
		ЗапросВложений.Текст =
		"ВЫБРАТЬ
		|	ЭДПрисоединенныеФайлы.Ссылка КАК Ссылка,
		|	ЭДПрисоединенныеФайлы.Наименование КАК ИмяФайла,
		|	ЭДПрисоединенныеФайлы.Расширение КАК Расширение,
		|	ЭДПрисоединенныеФайлы.Редактирует КАК Редактирует
		|ИЗ
		|	Справочник.ЭДПрисоединенныеФайлы КАК ЭДПрисоединенныеФайлы
		|ГДЕ
		|	ЭДПрисоединенныеФайлы.ВладелецФайла = &ВладелецФайла
		|	И ЭДПрисоединенныеФайлы.ВидЭД = ЗНАЧЕНИЕ(Перечисление.ВидыЭД.ПроизвольныйЭД)
		|	И НЕ ЭДПрисоединенныеФайлы.ПометкаУдаления";
		Результат = ЗапросВложений.Выполнить().Выбрать();
		
		Если Результат.Следующий() Тогда
			ЭлектронныйДокументСсылка      = Результат.Ссылка;
			ЭлектронныйДокументРасширение  = ОбщегоНазначенияКлиентСервер.РасширениеБезТочки(Результат.Расширение);
			ЭлектронныйДокументИмяФайла    = СокрЛП(Результат.ИмяФайла) + "." + ЭлектронныйДокументРасширение;
			ЭлектронныйДокументИмяФайлаБезРасширения = СокрЛП(Результат.ИмяФайла);
			ЭлектронныйДокументПиктограмма = ФайловыеФункцииКлиентСервер.ПолучитьИндексПиктограммыФайла(Результат.Расширение);
			Элементы.ТекстВложение.Ширина  = СтрДлина(ЭлектронныйДокументИмяФайла);
			Если ЗначениеЗаполнено(Результат.Редактирует) И Результат.Редактирует = Пользователи.АвторизованныйПользователь() Тогда
				ВложениеРедактируется = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Если УстановитьДоступность Тогда
		УстановитьДоступностьЭлементов();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДанныеФайла(ЭлектронныйДокументСсылка, УникальныйИдентификатор)
	
	Возврат ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьДанныеФайла(ЭлектронныйДокументСсылка, УникальныйИдентификатор);
	
КонецФункции

// Основание

&НаКлиенте
Процедура ПроверитьВозможностьИзмененияУчастникаОбмена(ИмяРеквизита)
	
	Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		НаименованиеУчастникаВОсновании = Неопределено;
		УчастникВОсновании = Неопределено;
		Если УчастникОбменаВОбъектеИОснованииНеСовпадает(ИмяРеквизита, НаименованиеУчастникаВОсновании, УчастникВОсновании) Тогда
			ТекстВопроса = НСтр("ru = '%1 в основании (%2) отличается от выбранного значения!
				|Очистить основание?'");
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстВопроса,
				ИмяРеквизита, НаименованиеУчастникаВОсновании);
			Результат = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
			Если Результат = КодВозвратаДиалога.Да Тогда
				Объект.ДокументОснование = Неопределено;
				УстановитьДоступностьЭлементов();
			Иначе
				Объект[ИмяРеквизита] = УчастникВОсновании;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	УстановитьТребуетсяПодтверждениеПоСоглашению();

КонецПроцедуры

&НаСервере
Функция УчастникОбменаВОбъектеИОснованииНеСовпадает(ИмяРеквизита, НаименованиеУчастникаВОсновании, УчастникВОсновании)
	
	РеквизитыОснования = ЭлектронныеДокументыСлужебный.ЗаполнитьПараметрыЭДПоИсточнику(Объект.ДокументОснование);
	УчастникВОсновании = Неопределено;
	РеквизитыОснования.Свойство(ИмяРеквизита, УчастникВОсновании);
	Если ЗначениеЗаполнено(УчастникВОсновании) Тогда
		НаименованиеУчастникаВОсновании = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчастникВОсновании, "Наименование");
		Результат = Объект[ИмяРеквизита] <> УчастникВОсновании;
	Иначе
		Результат = Ложь;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция СписокТиповДокументовОснованийПроизвольногоДокумента()
	
	Основания = Метаданные.Документы.УдалитьПроизвольныйЭД.ВводитсяНаОсновании;
	СписокТипов = Новый СписокЗначений;
	Для Каждого ТекущееОснование Из Основания Цикл
		СписокТипов.Добавить(ТекущееОснование.ПолноеИмя(), ТекущееОснование.Синоним);
	КонецЦикла;
	
	Возврат СписокТипов;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьПоДокументуОснованию(ДокументОснование)
	
	Если ЗначениеЗаполнено(ДокументОснование) Тогда
		Документ = РеквизитФормыВЗначение("Объект");
		Документ.Заполнить(ДокументОснование);
		ЗначениеВРеквизитФормы(Документ, "Объект");
		ТекстДокументОснование = ЗаполнитьТекстДокументаОснования(Объект.ДокументОснование);
		АдресХранилища = ЭлектронныеДокументыСлужебный.ПараметрИзПараметрыКлиентаНаСервере(Объект.ДокументОснование);
		Если ЭтоАдресВременногоХранилища(АдресХранилища) Тогда
			АдресВременногоХранилищаВложения = АдресХранилища;
			// У документа уже есть сохраненное вложение, надо будет в нем обновить двоичные данные.
			ОбновитьДвоичныеДанныеФайла = ЗначениеЗаполнено(ЭлектронныйДокументСсылка);
		КонецЕсли;
		ТребуетсяПодтверждение = Объект.ТребуетсяПодтверждение;
		Модифицированность = Истина;
	КонецЕсли;
	ОбновитьДанныеОВложении();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЗаполнитьТекстДокументаОснования(СсылкаНаОснование)
	
	ТекстОснование = ПредставлениеОснования(СсылкаНаОснование);
	
	Возврат ТекстОснование;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПредставлениеОснования(СсылкаНаОснование)
	
	ТекстДокументОснование = "";
	Если ЗначениеЗаполнено(СсылкаНаОснование) Тогда
		Если Найти(СсылкаНаОснование.Метаданные().ПолноеИмя(), "Справочник") > 0 Тогда
			Структура = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаОснование, "Наименование");
			ТекстДокументОснование = НСтр("ru = '%1 %2'");
			ТекстДокументОснование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстДокументОснование,
				ТипЗнч(СсылкаНаОснование), СокрЛП(Структура.Наименование));
		Иначе
			Структура = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаОснование, "Номер, Дата");
			ТекстДокументОснование = НСтр("ru = '%1 №%2 от %3'");
			ТекстДокументОснование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстДокументОснование,
				ТипЗнч(СсылкаНаОснование), СокрЛП(Структура.Номер), Формат(Структура.Дата, "ДЛФ=D"));
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТекстДокументОснование;
	
КонецФункции

// Подписание, отправка документа и ЭЦП

&НаКлиенте
Процедура ПодписатьИОтправитьДокумент(ФлагОтправить = Ложь) Экспорт
	
	МассивЭД = Новый Массив;
	МассивЭД.Добавить(ЭлектронныйДокументСсылка);
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("МассивПодписей",   МассивОтпечатков());
	ДополнительныеПараметры.Вставить("МассивНаОтправку", МассивЭД);
	
	Действия = "УтвердитьПодписать" + ?(ФлагОтправить, "Отправить", "");
	МассивСсылокНаОбъект = Новый Массив;
	МассивСсылокНаОбъект.Добавить(Объект.Ссылка);
	ЭлектронныеДокументыСлужебныйКлиент.ОбработатьЭД(МассивСсылокНаОбъект, Действия, , МассивЭД);
	Подписан = СравнитьПодписи(ДополнительныеПараметры.МассивПодписей);
	
	Прочитать();
	Если ФлагОтправить Тогда
		ОбновитьСтатусДокумента(ДополнительныеПараметры);
		Оповестить("ОбновитьСостояниеЭД");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция МассивОтпечатков()
	
	Подписи = РеквизитФормыВЗначение("ЭЦП");
	МассивОтпечатков = Подписи.ВыгрузитьКолонку("Отпечаток");
	
	Возврат МассивОтпечатков;
	
КонецФункции

&НаКлиенте
Функция СравнитьПодписи(МассивОтпечатковДо)
	
	ДокументПодписан = Ложь;
	
	МассивОтпечатковПосле = МассивОтпечатков();
	
	Для Каждого Отпечаток Из МассивОтпечатковПосле Цикл
		
		Если МассивОтпечатковДо.Найти(Отпечаток) = Неопределено Тогда
			ДокументПодписан = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ДокументПодписан;
	
КонецФункции

&НаСервере
Функция ПолучитьОтпечаткиОжидаемыхСертификатов(ТекущееСоглашение)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СертификатыЭЦП.Отпечаток КАК Отпечаток
	|ИЗ
	|	Справочник.СертификатыЭЦП КАК СертификатыЭЦП
	|ГДЕ
	|	СертификатыЭЦП.Организация = &Организация
	|	И СертификатыЭЦП.Отозван = ЛОЖЬ
	|	И СертификатыЭЦП.ПометкаУдаления = ЛОЖЬ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СоглашенияОбИспользованииЭДСертификатыПодписейКонтрагента.Отпечаток
	|ИЗ
	|	Справочник.СоглашенияОбИспользованииЭД.СертификатыПодписейКонтрагента КАК СоглашенияОбИспользованииЭДСертификатыПодписейКонтрагента
	|ГДЕ
	|	СоглашенияОбИспользованииЭДСертификатыПодписейКонтрагента.Ссылка = &СоглашениеОбИспользованииЭД";
	Запрос.УстановитьПараметр("Организация", ТекущееСоглашение.Организация);
	Запрос.УстановитьПараметр("СоглашениеОбИспользованииЭД", ТекущееСоглашение);
	
	МассивОтпечатков = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Отпечаток");
	
	Возврат МассивОтпечатков;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьТаблицуЭЦП()
	
	Если НЕ ЗначениеЗаполнено(СоглашениеЭД)
		ИЛИ НЕ СоглашениеЭД.ПроверятьСертификатыПодписей Тогда
		
		ТаблицаЭЦП = РеквизитФормыВЗначение("ЭЦП");
		ТаблицаЭЦП.Очистить();
		Для Каждого ТекСтрока Из ЭлектронныйДокументСсылка.ЭлектронныеЦифровыеПодписи Цикл
			НоваяСтрока = ТаблицаЭЦП.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
			ЗаполнитьСтатусПодписи(НоваяСтрока, ТекСтрока);
		КонецЦикла;
		
		ЗначениеВРеквизитФормы(ТаблицаЭЦП, "ЭЦП");
		Возврат;
	КонецЕсли;
	
	МассивОтпечатковОжидаемыхСертификатов = ПолучитьОтпечаткиОжидаемыхСертификатов(СоглашениеЭД);
	
	ТаблицаЭЦП = РеквизитФормыВЗначение("ЭЦП");
	ТаблицаЭЦП.Очистить();
	
	Для Каждого ТекСтрока Из ЭлектронныйДокументСсылка.ЭлектронныеЦифровыеПодписи Цикл
		НоваяСтрока = ТаблицаЭЦП.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
		Если МассивОтпечатковОжидаемыхСертификатов.Найти(ТекСтрока.Отпечаток) = Неопределено Тогда
			НоваяСтрока.ОтсутствуетВСписке = Истина;
			НоваяСтрока.ВыводКартинки = 1;
		Иначе
			НоваяСтрока.ВыводКартинки = 0;
		КонецЕсли;
		ЗаполнитьСтатусПодписи(НоваяСтрока, ТекСтрока);
	КонецЦикла;
	ЗначениеВРеквизитФормы(ТаблицаЭЦП, "ЭЦП");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтатусПодписи(НоваяСтрока, ТекСтрока)
	
	Если ЗначениеЗаполнено(ТекСтрока.ДатаПроверкиПодписи) Тогда
		НоваяСтрока.ПодписьВерна = ?(ТекСтрока.ПодписьВерна, НСтр("ru = 'Верна'"), НСтр("ru = 'Неверна'"))
			+" (" + ТекСтрока.ДатаПроверкиПодписи + ")";
	Иначе
		НоваяСтрока.ПодписьВерна = НСтр("ru = 'Не проверена'");
	КонецЕсли
	
КонецПроцедуры

&НаСервере
Функция АдресДанныхСертификата(НомерСтроки)
	
	СсылкаНаХранилищеДанныхСертификата = Неопределено;
	Если ЗначениеЗаполнено(ЭлектронныйДокументСсылка) Тогда
		ДвоичныеДанныеСертификата = ЭлектронныйДокументСсылка.ЭлектронныеЦифровыеПодписи[НомерСтроки-1].Сертификат.Получить();
		СсылкаНаХранилищеДанныхСертификата = ПоместитьВоВременноеХранилище(ДвоичныеДанныеСертификата, УникальныйИдентификатор);
	КонецЕсли;
	
	Возврат СсылкаНаХранилищеДанныхСертификата;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьСертификат(НомерСтроки, Отпечаток)
	
	АдресДанныхСертификата = АдресДанныхСертификата(НомерСтроки);
	
	СтруктураСертификата = ЭлектронныеДокументыСлужебныйВызовСервера.СвойстваСертификата(АдресДанныхСертификата);
	Если СтруктураСертификата <> Неопределено Тогда
		ПараметрыФормы = Новый Структура("СтруктураСертификата, Отпечаток, АдресСертификата",
			СтруктураСертификата, Отпечаток, АдресДанныхСертификата);
		СтруктураВозврата = ОткрытьФормуМодально("ОбщаяФорма.СертификатЭЦП", ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьСертификатПодписиВСоглашение(Отпечаток, СертификатДобавлен)
	
	Если НЕ (СоглашениеОпределялось И ЗначениеЗаполнено(СоглашениеЭД)) ИЛИ НЕ ЗначениеЗаполнено(ЭлектронныйДокументСсылка) Тогда
		Возврат ;
	КонецЕсли;
	
	НайденнаяСтрока = ЭлектронныйДокументСсылка.ЭлектронныеЦифровыеПодписи.Найти(Отпечаток, "Отпечаток");
	Если НЕ НайденнаяСтрока = Неопределено Тогда
		СоглашениеОбъект = СоглашениеЭД.ПолучитьОбъект();
		
		НоваяСтрока = СоглашениеОбъект.СертификатыПодписейКонтрагента.Добавить();
		НоваяСтрока.Сертификат = НайденнаяСтрока.Сертификат;
		НоваяСтрока.Отпечаток  = Отпечаток;
		СоглашениеОбъект.Записать();
		
		СертификатДобавлен = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСертификатВДоверенные(ДанныеПодписи)
	
	Если ДанныеПодписи <> Неопределено И ДанныеПодписи.ОтсутствуетВСписке Тогда
		Текст = НСтр("ru = 'Добавить сертификат %1 в список ожидаемых сертификатов контрагента?'");
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Текст, ДанныеПодписи.КомуВыданСертификат);
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			ПоказатьСертификат(Элементы.ЭЦП.ТекущиеДанные.НомерСтроки,Элементы.ЭЦП.ТекущиеДанные.Отпечаток);
			Возврат;
		КонецЕсли;
		
		// Добавим сертификат в Соглашение.
		СертификатДобавлен = Ложь;
		ДобавитьСертификатПодписиВСоглашение(ДанныеПодписи.Отпечаток, СертификатДобавлен);
		Если НЕ СертификатДобавлен Тогда 
			ТекстСообщения = НСтр("ru = 'Ошибка добавления сертификата подписи в список ожидаемых сертификатов!'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Иначе
			ЗаполнитьТаблицуЭЦП();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьПредложениеОбАннулировании(ОтклонитьАннулирование)
	
	ЭлектронныеДокументыСлужебныйКлиент.ОбработатьПредложениеОбАннулировании(ЭлектронныйДокументСсылка, ОтклонитьАннулирование);
	Если ОтклонитьАннулирование Тогда
		ЭДАннулирован = Ложь;
	Иначе
		ЭДАннулирован = Истина;
	КонецЕсли;
	
	ИзменитьСтатусПредложениеОбАннулировании();
	Оповестить("ОбновитьСостояниеЭД");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтклонитьАннулироватьЭД(Отклонить = Ложь)
	
	ПараметрыЭД = Новый Структура("Организация, Отклонить", Объект.Организация, Отклонить);
	Если ЭлектронныеДокументыСлужебныйКлиент.ОбработатьОтклонениеАннулированиеЭД(ЭлектронныйДокументСсылка, ПараметрыЭД) Тогда
		Если Отклонить Тогда
			ЭДОтклонен = Истина;
			ИзменитьСтатусОтклонить();
		Иначе
			ЭДАннулирован = Истина;
			ИзменитьСтатусАннулировать();
		
		КонецЕсли;
		Оповестить("ОбновитьСостояниеЭД");
	КонецЕсли;
	
КонецПроцедуры

// Прочие

&НаСервере
Процедура УстановитьДоступностьЭлементов(ОбновитьКомандыЭДО = Ложь)
	
	НаправлениеИсходящий   = (Объект.Направление = Перечисления.НаправленияЭД.Исходящий);
	ТребуетсяПодтверждение = Объект.ТребуетсяПодтверждение;
	
	СтатусПередан = Документы.УдалитьПроизвольныйЭД.СтатусОбъектаПередан(Объект);
	
	СтатусПредполагаетПодпись = Документы.УдалитьПроизвольныйЭД.СтатусПредполагаетПодпись(Объект);
	
	ДОЗакрыт = ЭлектронныеДокументыСлужебныйВызовСервера.ДОЗакрытПринудительно(Объект.Ссылка);
	
	ЕстьВложения = ЗначениеЗаполнено(ЭлектронныйДокументСсылка)
					ИЛИ ЭтоАдресВременногоХранилища(АдресВременногоХранилищаВложения);
	
	ДоступностьКомандыЭДО = (НЕ (СтатусПередан ИЛИ ЭДОтклонен ИЛИ ЭДАннулирован Или ДОЗакрыт)
													И ЕстьВложения И (НаправлениеИсходящий ИЛИ ТребуетсяПодтверждение));
	
	ФОИспользоватьЭЦП = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьЗначениеФункциональнойОпции(
			"ИспользоватьЭлектронныеЦифровыеПодписи");
			
	ЕстьПравоПодписи = ЭлектронныеДокументыПереопределяемый.ЕстьПравоПодписанияЭД();														
	
	ВидимостьКомандПодписи = (ФОИспользоватьЭЦП И ЕстьПравоПодписи И (СтатусПредполагаетПодпись ИЛИ Не СтатусПередан))
		Или (СтатусПередан И МассивОтпечатков().Количество() >0);
		
	Если ВидимостьКомандПодписи Тогда
		
		Элементы.КомандаУтвердить.Видимость = Ложь;
		Элементы.КомандаУтвердитьИОтправить.Видимость = Ложь;
		
		Если ЭлектронныеДокументыСлужебный.НемедленнаяОтправкаЭД() Тогда
			Элементы.Подписать.Видимость = Ложь;
			
			Элементы.ПодписатьИОтправитьЭД.Видимость = Истина;
			Элементы.ПодписатьИОтправитьЭД.Доступность = ДоступностьКомандыЭДО;
		Иначе
			Элементы.ПодписатьИОтправитьЭД.Видимость = Ложь;
			
			Элементы.Подписать.Видимость = Истина;
			Элементы.Подписать.Доступность = ДоступностьКомандыЭДО;
		КонецЕсли;
		
	Иначе
		
		Элементы.Подписать.Видимость = Ложь;
		Элементы.ПодписатьИОтправитьЭД.Видимость = Ложь;
		
		Если ЭлектронныеДокументыСлужебный.НемедленнаяОтправкаЭД() Тогда
			
			Элементы.КомандаУтвердить.Видимость = Ложь;
			Элементы.КомандаУтвердитьИОтправить.Видимость = Истина;
			
			Элементы.КомандаУтвердитьИОтправить.Доступность = ДоступностьКомандыЭДО
				И Объект.СтатусДокумента = Перечисления.СтатусыЭД.Сформирован;
			
		Иначе
			
			Элементы.КомандаУтвердить.Видимость = Истина;
			Элементы.КомандаУтвердитьИОтправить.Видимость = Ложь;
			
			Элементы.КомандаУтвердить.Доступность = ДоступностьКомандыЭДО
				И Объект.СтатусДокумента = Перечисления.СтатусыЭД.Сформирован;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОбновитьКомандыЭДО Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.СохранитьВложениеВФайл.Доступность = ЗначениеЗаполнено(ЭлектронныйДокументСсылка);
	
	Если ЗначениеЗаполнено(Объект.Ссылка) И НаправлениеИсходящий
		И (Объект.СтатусДокумента = Перечисления.СтатусыЭД.Сформирован
			ИЛИ Объект.СтатусДокумента = Перечисления.СтатусыЭД.Подписан
			ИЛИ Объект.СтатусДокумента = Перечисления.СтатусыЭД.ПодготовленКОтправке) Тогда
		МожноРедактироватьПередаваемыеПараметры = Истина;
	Иначе
		МожноРедактироватьПередаваемыеПараметры = НЕ ЗначениеЗаполнено(Объект.Ссылка); // Если документ еще не записан, то можно редактировать.
	КонецЕсли;
	
	// Вложение
	Если НаправлениеИсходящий Тогда
		Элементы.КоманднаяПанельВложения.Видимость = Истина;
		ПрисоединенныеФайлыОснования = Новый Массив;
		Если Объект.ДокументОснование <> Неопределено Тогда
			ПрисоединенныеФайлы.ПолучитьПрикрепленныеФайлыКОбъекту(Объект.ДокументОснование, ПрисоединенныеФайлыОснования);
		КонецЕсли;
		Если ПрисоединенныеФайлыОснования.Количество() > 0 Тогда
			КомандаДобавить = Элементы.Добавить;
			Элементы.ДобавитьВложениеИзФайлаНаДискеОдиночная.Видимость = Ложь;
		Иначе
			КомандаДобавить = Элементы.ДобавитьВложениеИзФайлаНаДискеОдиночная;
			Элементы.Добавить.Видимость = Ложь;
		КонецЕсли;
		КомандаДобавить.Видимость = Истина;
		Если ЕстьВложения Тогда
			ДанныеФайла = ДанныеФайлаВложения(АдресВременногоХранилищаВложения, ЭлектронныйДокументСсылка, УникальныйИдентификатор);
			Если ДанныеФайла.ФайлРедактируется Тогда
				ТекстРедактируетФайл = НСтр("ru = 'Файл захвачен на редактирование пользователем %1'");
				ТекстРедактируетФайл = СтрЗаменить(ТекстРедактируетФайл, "%1", ДанныеФайла.Редактирует);
				Элементы.ТекстРедактируетФайл.ЦветТекста = ЦветаСтиля[?(ДанныеФайла.ФайлРедактируетТекущийПользователь,
					"ФайлЗанятыйТекущимПользователем", "ТекстЗапрещеннойЯчейкиЦвет")];
			Иначе
				ТекстРедактируетФайл = "";
			КонецЕсли;
			КомандаДобавить.Заголовок = СтрЗаменить(КомандаДобавить.Заголовок, НСтр("ru = 'Добавить'"), НСтр("ru = 'Заменить на'"));
			Если МожноРедактироватьПередаваемыеПараметры И НЕ ДанныеФайла.ПодписанЭЦП Тогда
				КомандаДобавить.Доступность = Истина;
				Элементы.Редактировать.Доступность = НЕ ДанныеФайла.ФайлРедактируется;
				Элементы.ЗакончитьРедактирование.Доступность = ДанныеФайла.ФайлРедактируется И ДанныеФайла.ФайлРедактируетТекущийПользователь;
				Элементы.Освободить.Доступность = ДанныеФайла.ФайлРедактируется И ДанныеФайла.ФайлРедактируетТекущийПользователь;
			Иначе
				КомандаДобавить.Доступность = Ложь;
				Элементы.Редактировать.Доступность = Ложь;
				Элементы.ЗакончитьРедактирование.Доступность = Ложь;
				Элементы.Освободить.Доступность = Ложь;
			КонецЕсли;
		Иначе
			КомандаДобавить.Заголовок = СтрЗаменить(КомандаДобавить.Заголовок, НСтр("ru = 'Заменить на'"), НСтр("ru = 'Добавить'"));
			КомандаДобавить.Доступность = МожноРедактироватьПередаваемыеПараметры;
			Элементы.Редактировать.Доступность = Ложь;
			Элементы.ЗакончитьРедактирование.Доступность = Ложь;
			Элементы.Освободить.Доступность = Ложь;
		КонецЕсли;
		
	Иначе
		Элементы.КоманднаяПанельВложения.Видимость = Ложь;
		Элементы.ТекстРедактируетФайл.Видимость = Ложь;
	КонецЕсли;
	Элементы.ВложенияСтраницы.ТекущаяСтраница = ?(ЕстьВложения, Элементы.ВложенияСтраницаОтобразитьФайл, Элементы.ВложенияСтраницаОтсутствуетФайл);
	
	Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		Элементы.ДокументОснованиеСтраницы.ТекущаяСтраница = Элементы.ДокументОснованиеСтраницаОтобразитьДокумент;
		Элементы.ДокументОснование.Ширина = СтрДлина(ТекстДокументОснование);
	Иначе
		Элементы.ДокументОснованиеСтраницы.ТекущаяСтраница = Элементы.ДокументОснованиеСтраницаДобавить;
	КонецЕсли;
	
	Элементы.ТребуетсяПодтверждение.ТолькоПросмотр = НЕ МожноРедактироватьПередаваемыеПараметры;
	Элементы.ТекстВложение.ТолькоПросмотр = НЕ МожноРедактироватьПередаваемыеПараметры;
	Элементы.ОбъектДополнительнаяИнформация.ТолькоПросмотр = НЕ МожноРедактироватьПередаваемыеПараметры;
	Элементы.ФормаПеренаправить.Доступность = ЕстьВложения;
	Элементы.ГруппаПодвалСтраницыКомментарии.Картинка = ОбщегоНазначения.ПолучитьКартинкуКомментария(ВсеКомментарии);
	Элементы.ГруппаКомментарииГоризонтальная.Доступность = ЕстьВложения;
	Элементы.СтраницаСопроводительнаяЗаписка.Картинка = ОбщегоНазначения.ПолучитьКартинкуКомментария(СопроводительнаяЗаписка);
	
	Элементы.ФормаОтменитьОтклонение.Видимость = НаправлениеИсходящий;
	
	Если ЭДОтклонен Тогда
		
		Если ЕстьВложения Тогда
			ПричинаОтклонения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭлектронныйДокументСсылка, "ПричинаОтклонения");
			Элементы.ПричинаОтклонения.Видимость = Истина;
		КонецЕсли;
		Элементы.ФормаОтклонить.Доступность = Ложь;
		Элементы.ФормаОтменитьОтклонение.Доступность = (Объект.СтатусДокумента = Перечисления.СтатусыЭД.Отклонен);
	Иначе
		Элементы.ПричинаОтклонения.Видимость = Ложь;
		Элементы.ФормаОтклонить.Доступность = ЭлектронныеДокументыСлужебныйВызовСервера.МожноОтклонитьЭтотЭД(ЭлектронныйДокументСсылка);
		Элементы.ФормаОтменитьОтклонение.Доступность = Ложь;
	КонецЕсли;
	
	МожноАннулироватьЭтотЭД = ЭлектронныеДокументыСлужебныйВызовСервера.МожноАннулироватьЭтотЭД(ЭлектронныйДокументСсылка);
	Элементы.КомандаАннулировать.Доступность = МожноАннулироватьЭтотЭД;
	Если ЭДАннулирован Тогда
		Элементы.ПричинаОтклонения.Видимость = Истина;
		ПричинаОтклонения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭлектронныйДокументСсылка, "ПричинаОтклонения");
		Элементы.ПричинаОтклонения.Заголовок = НСтр("ru = 'Причина аннулирования'");
		
		СвойстваВладельцаЭД = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЭлектронныйДокументСсылка, "СтатусЭД, ТребуетсяПодтверждение");
		
		Если ЭлектронныйДокументСсылка.СтатусЭД = Перечисления.СтатусыЭД.ПолученоПредложениеОбАннулировании Тогда
			
			Элементы.ГруппаКомандАннулирование.Видимость = Истина;
			Элементы.ПодписатьИОтправитьЭД.Видимость = Ложь;
			Элементы.ФормаОтклонить.Видимость = Ложь;
			Элементы.КомандаАннулировать.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	ЗаголовокПодписи = НСтр("ru = 'Подписи'");
	Если ЗначениеЗаполнено(ЭлектронныйДокументСсылка) Тогда
		КоличествоЭП = ЭлектронныйДокументСсылка.ЭлектронныеЦифровыеПодписи.Количество();
		Если КоличествоЭП > 0 Тогда
			ЗаголовокПодписи = СтрЗаменить(НСтр("ru = 'Подписи (%1)'"), "%1", КоличествоЭП);
		КонецЕсли;
	КонецЕсли;
	Элементы.СтраницаПодписиИСтатусы.Заголовок = ЗаголовокПодписи;
	
	Элементы.Организация.ТолькоПросмотр = Не МожноРедактироватьПередаваемыеПараметры;
	Элементы.Контрагент.ТолькоПросмотр  = Не МожноРедактироватьПередаваемыеПараметры;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЗаголовокФормы()
	
	Если Объект.Направление = Перечисления.НаправленияЭД.Исходящий Тогда
		Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
			ТекстЗаголовка = НСтр("ru = 'Исходящий произвольный документ %1 от %2'");
			Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ТекстЗаголовка, Объект.Номер, Объект.Дата);
		Иначе
			Заголовок = НСтр("ru = 'Исходящий произвольный документ (создание)'");
		КонецЕсли;
	Иначе
		ТекстЗаголовка = НСтр("ru = 'Входящий произвольный документ %1 от %2'");
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстЗаголовка, Объект.Номер, Объект.Дата);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Функция ДокументЗаписан()
	
	ФлагЗаписи = Истина;
	
	Если Модифицированность ИЛИ Объект.Ссылка.Пустая() Тогда
		ФлагЗаписи = Ложь;
		
		ТекстВопроса = НСтр("ru = 'Для продолжения операции необходимо записать документ.
			|Записать документ?'");
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
		Если Ответ = КодВозвратаДиалога.Да И Записать() Тогда
			ФлагЗаписи = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ФлагЗаписи;
	
КонецФункции

&НаСервере
Процедура ОбновитьСтатусДокумента(ПараметрыДокумента)
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	
	Если ПараметрыДокумента.Свойство("МассивНаОтправку") И ПараметрыДокумента.МассивНаОтправку.Количество() > 0 Тогда
		ЭД = ПараметрыДокумента.МассивНаОтправку[0];
		ДокументОбъект.СтатусДокумента = ЭД.СтатусЭД;
	КонецЕсли;
	
	Если ПараметрыДокумента.Свойство("Подписан") И ПараметрыДокумента.Подписан Тогда
		Если ДокументОбъект.Направление = Перечисления.НаправленияЭД.Входящий Тогда
			ДокументОбъект.СтатусДокумента = ЭлектронныеДокументыСлужебный.ПолучитьДопустимыйСтатусЭД(Перечисления.СтатусыЭД.ПодготовленоПодтверждение);
		Иначе
			ДокументОбъект.СтатусДокумента = ЭлектронныеДокументыСлужебный.ПолучитьДопустимыйСтатусЭД(Перечисления.СтатусыЭД.ПодготовленКОтправке);
		КонецЕсли;
	КонецЕсли;
	
	Если (СоглашениеОпределялось И ЗначениеЗаполнено(СоглашениеЭД))
		ИЛИ (НЕ СоглашениеОпределялось И ЕстьСоглашение(ДокументОбъект)) Тогда
		ДокументОбъект.Записать();
		ОбновитьИнформациюПодвала();
		ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
		Прочитать();
		ОбновитьЗаголовокФормы();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЕстьСоглашение(ОбъектДокумент)
	
	ВыводитьСообщение = НЕ ЗначениеЗаполнено(Объект.Ссылка) ИЛИ Модифицированность;
	НастройкиЭД = ЭлектронныеДокументыСлужебный.ОпределитьНастройкиОбменаЭДПоИсточнику(ОбъектДокумент,
		ВыводитьСообщение, , , , Истина);
	СоглашениеОпределялось = Истина;
	Если ЗначениеЗаполнено(НастройкиЭД) Тогда
		ОбъектДокумент.ДополнительныеСвойства.Вставить("ЕстьСоглашение", Истина);
		СоглашениеЭД = НастройкиЭД.СоглашениеЭД;
		ПрофильНастроекЭДО = НастройкиЭД.ПрофильНастроекЭДО;
		Возврат Истина;
	Иначе
		СоглашениеЭД = Справочники.СоглашенияОбИспользованииЭД.ПустаяСсылка();
		ПрофильНастроекЭДО = Справочники.ПрофилиНастроекЭДО.ПустаяСсылка();
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаКлиенте
Процедура УстановитьТребуетсяПодтверждениеПоСоглашению()
	
	Если ЗначениеЗаполнено(Объект.Организация)
		И ЗначениеЗаполнено(Объект.Контрагент) Тогда
		
		ВидЭД = ПредопределенноеЗначение("Перечисление.ВидыЭД.ПроизвольныйЭД");
		Объект.ТребуетсяПодтверждение = ТребуетсяОтветнаяПодписьПоСоглашению(
			ВидЭД, Объект.Организация, Объект.Контрагент);
		ТребуетсяИзвещение = ТребуетсяИзвещениеПоСоглашению(ВидЭД, Объект.Организация, Объект.Контрагент);
	
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТребуетсяОтветнаяПодписьПоСоглашению(ВидЭД, Организация, Контрагент)
	
	Требуется = Ложь;
	
	ФОИспользоватьЭЦП = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьЗначениеФункциональнойОпции(
			"ИспользоватьЭлектронныеЦифровыеПодписи");
	
	Если Не ФОИспользоватьЭЦП Тогда
		Возврат Требуется;
	КонецЕсли;

	ПараметрыЭД = Новый Структура();
	ПараметрыЭД.Вставить("ВидЭД", 		  ВидЭД);
	ПараметрыЭД.Вставить("Организация",   Организация);
	ПараметрыЭД.Вставить("Контрагент",	  Контрагент);
	ПараметрыЭД.Вставить("НаправлениеЭД", Перечисления.НаправленияЭД.Исходящий);
	
	НастройкиЭД = ЭлектронныеДокументыСлужебный.ОпределитьНастройкиОбменаЭД(ПараметрыЭД);
	
	Если ЗначениеЗаполнено(НастройкиЭД) Тогда

		Требуется = НастройкиЭД.ТребуетсяПодтверждение;
	
	КонецЕсли;
	
	Возврат Требуется;
	
КонецФункции

&НаСервереБезКонтекста
Функция ТребуетсяИзвещениеПоСоглашению(ВидЭД, Организация, Контрагент)
	
	Требуется = Ложь;
	
	ПараметрыЭД = Новый Структура();
	ПараметрыЭД.Вставить("ВидЭД", 		  ВидЭД);
	ПараметрыЭД.Вставить("Организация",   Организация);
	ПараметрыЭД.Вставить("Контрагент",	  Контрагент);
	ПараметрыЭД.Вставить("НаправлениеЭД", Перечисления.НаправленияЭД.Исходящий);
	
	НастройкиЭД = ЭлектронныеДокументыСлужебный.ОпределитьНастройкиОбменаЭД(ПараметрыЭД);
	
	Если ЗначениеЗаполнено(НастройкиЭД) Тогда

		Требуется = НастройкиЭД.ТребуетсяИзвещение;
	
	КонецЕсли;
	
	Возврат Требуется;
	
КонецФункции

&НаСервере
Процедура ОбновитьСтатусСостояниеЭД()
	
	ТекстСостоянияЭДО = ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСостоянияЭД(Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИнформациюПодвала()
	
	ОбновитьСтатусСостояниеЭД();
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если НЕ (СоглашениеОпределялось И ЗначениеЗаполнено(СоглашениеЭД)) Тогда
			ДокументОбъект = РеквизитФормыВЗначение("Объект");
			ЕстьСоглашение = ЕстьСоглашение(ДокументОбъект);
		Иначе
			ЕстьСоглашение = Истина;
		КонецЕсли;
		ЗаполнитьТаблицуЭЦП();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьСтатусОтклонить()
	
	ТекущийСтатус = ?(ЭДОтклонен, Перечисления.СтатусыЭД.Отклонен, Перечисления.СтатусыЭД.Сформирован);
	СтруктураПараметров = Новый Структура("СтатусЭД", ТекущийСтатус);
	ЭлектронныеДокументыСлужебный.ИзменитьПоСсылкеПрисоединенныйФайл(ЭлектронныйДокументСсылка, СтруктураПараметров, Ложь);
	Объект.СтатусДокумента = ТекущийСтатус;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьСтатусАннулировать()
	
	ТекущийСтатус = ?(ЭДАннулирован и Объект.ТребуетсяПодтверждение, Перечисления.СтатусыЭД.СформированоПредложениеОбАннулировании, Перечисления.СтатусыЭД.Аннулирован);
	СтруктураПараметров = Новый Структура("СтатусЭД", ТекущийСтатус);
	ЭлектронныеДокументыСлужебный.ИзменитьПоСсылкеПрисоединенныйФайл(ЭлектронныйДокументСсылка, СтруктураПараметров, Ложь);
	Объект.СтатусДокумента = ТекущийСтатус;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьСтатусПредложениеОбАннулировании()
	
	Если Объект.Направление = Перечисления.НаправленияЭД.Входящий Тогда
		
		Если ЭДАннулирован Тогда
			ТекущийСтатус = Перечисления.СтатусыЭД.Аннулирован;
		Иначе
			Если ТребуетсяПодтверждение Тогда
				ТекущийСтатус = Перечисления.СтатусыЭД.ОтправленоПодтверждение;
			Иначе
				ТекущийСтатус = Перечисления.СтатусыЭД.Получен;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Объект.Направление = Перечисления.НаправленияЭД.Исходящий Тогда
		Если ЭДАннулирован Тогда
			ТекущийСтатус = Перечисления.СтатусыЭД.Аннулирован;
		Иначе
			Если ТребуетсяПодтверждение Тогда
				ТекущийСтатус = Перечисления.СтатусыЭД.ПолученоПодтверждение;
			Иначе
				ТекущийСтатус = Перечисления.СтатусыЭД.Отправлен;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	СтруктураПараметров = Новый Структура("СтатусЭД", ТекущийСтатус);
	ЭлектронныеДокументыСлужебный.ИзменитьПоСсылкеПрисоединенныйФайл(ЭлектронныйДокументСсылка, СтруктураПараметров, Ложь);
	Объект.СтатусДокумента = ТекущийСтатус;
	
КонецПроцедуры

&НаКлиенте
Функция ОбработатьОтменуОтклоненияЭД()
	
	ВозвращаемоеЗначение = Ложь;
	Если ДокументЗаписан() И ЭДОтклонен И ОтменитьОтклонениеНаСервере() Тогда
		ВозвращаемоеЗначение = Истина;
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

&НаСервере
Функция ОтменитьОтклонениеНаСервере()
	
	ЭДОтклонен = Ложь;
	ЭлектронныйДокумент = ЭлектронныйДокументСсылка.ПолучитьОбъект();
	ЭлектронныйДокумент.ПричинаОтклонения = "";
	Если Объект.Направление = Перечисления.НаправленияЭД.Исходящий Тогда
		ЭлектронныйДокумент.ЭлектронныеЦифровыеПодписи.Очистить();
	КонецЕсли;
	ЭлектронныйДокумент.Записать();
	ИзменитьСтатусОтклонить();
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура ПерезаполнитьКомментарии()
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ВсеКомментарии = "";
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЖурналСобытийЭД.Пользователь.Представление КАК Пользователь,
		|	ЖурналСобытийЭД.Дата КАК Дата,
		|	ЖурналСобытийЭД.СтатусЭД,
		|	ЖурналСобытийЭД.Ответственный.Представление КАК Ответственный,
		|	ЖурналСобытийЭД.Комментарий
		|ИЗ
		|	РегистрСведений.ЖурналСобытийЭД КАК ЖурналСобытийЭД
		|ГДЕ
		|	ЖурналСобытийЭД.ВладелецЭД = &Ссылка
		|	И ЖурналСобытийЭД.Комментарий <> &ПустаяСтрока
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата";
			
		Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
		Запрос.УстановитьПараметр("ПустаяСтрока", "");
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		ШаблонКомментария = НСтр("ru = '%1, %2 (статус - %3, ответственный - %4):
			|%5'");
		ПредыдущийКоммент = "";
		ПервыйКоммент = Истина;
		Массив = Новый Массив;
		Пока Выборка.Следующий() Цикл
			ТекущийКоммент = СокрЛП(Выборка.Комментарий);
			Если ПредыдущийКоммент = ТекущийКоммент Тогда
				Продолжить;
			КонецЕсли;
			ПредыдущийКоммент = ТекущийКоммент;
			СтрокаКомментария = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонКомментария,
					Выборка.Дата, Выборка.Пользователь, Выборка.СтатусЭД, Выборка.Ответственный, ТекущийКоммент);
			Массив.Добавить(СтрокаКомментария);
			ПервыйКоммент = Ложь;
		КонецЦикла;
		Если Массив.Количество() > 0 Тогда
			ПервыйКоммент = Истина;
			Для Сч = -Массив.Количество() + 1 По 0 Цикл
				СтрокаКомментария = Массив[-Сч];
				ВсеКомментарии = ВсеКомментарии
					+ СтрокаКомментария
					+ ?(ПервыйКоммент, Символы.ПС + "------------------------------------", "")
					+ Символы.ПС
					+ Символы.ПС;
				ПервыйКоммент = Ложь;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьОбработкуОповещенияНаСервере()
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ЗначениеВРеквизитФормы(Объект.Ссылка.ПолучитьОбъект(), "Объект");
	КонецЕсли;
	
	ЗаполнитьТаблицуЭЦП();
	ОбновитьСтатусСостояниеЭД();
	ПерезаполнитьКомментарии();
	УстановитьДоступностьЭлементов();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ИзменитьЗначенияРеквизитовНаСервере(Знач Ссылка, Знач СтруктураПараметров)
	
	Если СтруктураПараметров.Свойство("Описание") И ЗначениеЗаполнено(Ссылка.Ответственный) Тогда
		СтруктураПараметров.Вставить("Ответственный", Ссылка.Ответственный);
	КонецЕсли;
	ЭлектронныеДокументыСлужебный.ИзменитьПоСсылкеПрисоединенныйФайл(Ссылка, СтруктураПараметров, Ложь);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьТребуетсяПодтверждение(ОбновитьЗначение = Ложь)
	
	ФОИспользоватьЭЦП = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьЗначениеФункциональнойОпции(
			"ИспользоватьЭлектронныеЦифровыеПодписи");
	
	Если ОбновитьЗначение И Не ФОИспользоватьЭЦП Тогда
		Объект.ТребуетсяПодтверждение = Ложь;
	КонецЕсли;
	
	Элементы.ТребуетсяПодтверждение.Доступность = ФОИспользоватьЭЦП;
	
КонецПроцедуры

&НаКлиенте
Процедура УтвердитьЭД(Команда)
	Если ПроверитьЗаполнение() И (ЗначениеЗаполнено(ЭлектронныйДокументСсылка)
		ИЛИ ЭтоАдресВременногоХранилища(АдресВременногоХранилищаВложения)) Тогда
		
		Если ДокументЗаписан() И ПроверитьЗахватНаРедактирование() Тогда
			УтвердитьИОтправитьДокумент(Ложь);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УтвердитьИОтправить(Команда)
	
	Если ПроверитьЗаполнение() И (ЗначениеЗаполнено(ЭлектронныйДокументСсылка)
		ИЛИ ЭтоАдресВременногоХранилища(АдресВременногоХранилищаВложения)) Тогда
		
		Если ДокументЗаписан() И ПроверитьЗахватНаРедактирование() Тогда
			УтвердитьИОтправитьДокумент(Истина);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УтвердитьИОтправитьДокумент(ФлагОтправить)
	
	МассивЭД = Новый Массив;
	МассивЭД.Добавить(ЭлектронныйДокументСсылка);
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("МассивНаОтправку", МассивЭД);
	
	Действия = "Утвердить" + ?(ФлагОтправить, "Отправить", "");
	МассивСсылокНаОбъект = Новый Массив;
	МассивСсылокНаОбъект.Добавить(Объект.Ссылка);
	ЭлектронныеДокументыСлужебныйКлиент.ОбработатьЭД(МассивСсылокНаОбъект, Действия, , МассивЭД);
	
	Прочитать();
	Если ФлагОтправить Тогда
		ОбновитьСтатусДокумента(ДополнительныеПараметры);
		Оповестить("ОбновитьСостояниеЭД");
	КонецЕсли;
	
КонецПроцедуры


