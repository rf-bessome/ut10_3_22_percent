#Область ОписаниеПеременных

&НаКлиенте
Перем КэшированныеЗначения; //используется механизмом обработки изменения реквизитов ТЧ

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ИнтеграцияИСМП.ЗапрещеноИспользованиеОбъектаВИСМП(Объект.Ссылка) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
	ИнтеграцияИС.НастроитьВидимостьДокументаОснования(ЭтаФорма);
	Элементы.ДокументОснование.ДоступныеТипы = ОпределяемыеТипы().ОснованиеУточненияСведенийОКодахМаркировкиИСМП.Тип;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыИС.ПриСозданииНаСервере(ЭтаФорма);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ВерсионированиеОбъектов") Тогда
		МодульВерсионированиеОбъектов = ОбщегоНазначения.ОбщийМодуль("ВерсионированиеОбъектов");
		МодульВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	ДоступныеВидыПродукцииИС = Документы.УточнениеСведенийОКодахМаркировкиИСМП.ДоступныеВидыПродукцииУточненияСведенийОКодахМаркировки();
	
	СобытияФормИСМП.НастроитьВидПродукцииПриСозданииНаСервере(ЭтаФорма, ДоступныеВидыПродукцииИС);
	
	СобытияФормИСПереопределяемый.УстановитьСвязиПараметровВыбораСНоменклатурой(ЭтаФорма,   "ТоварыХарактеристика");
	СобытияФормИСПереопределяемый.УстановитьСвязиПараметровВыбораСНоменклатурой(ЭтаФорма,   "ТоварыУпаковка");
	СобытияФормИСПереопределяемый.УстановитьСвязиПараметровВыбораСНоменклатурой(ЭтаФорма,   "ТоварыСерия");
	СобытияФормИСПереопределяемый.УстановитьСвязиПараметровВыбораСХарактеристикой(ЭтаФорма, "ТоварыСерия");
	
	Если Объект.Ссылка.Пустая() Тогда
		ПриСозданииЧтенииНаСервере();
		ИнтеграцияИСПереопределяемый.ЗаполнитьСтатусыУказанияСерий(Объект, ПараметрыУказанияСерий);
	КонецЕсли;
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма,"ДокументОснование");
	
	// Режим отладки
	Элементы.СтраницаШтрихкодыУпаковок.Видимость = ОбщегоНазначения.РежимОтладки() И Пользователи.ЭтоПолноправныйПользователь();
	
	ПоддерживаемыеТипыПодключаемогоОборудования = "СканерШтрихкода";
	ИнтеграцияИС.НастроитьПодключаемоеОборудование(ЭтаФорма, ПоддерживаемыеТипыПодключаемогоОборудования);
	СобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	
	Если НовыйОбъект = Элементы.ДокументОснование.ДоступныеТипы.ПривестиЗначение(НовыйОбъект) Тогда
		Объект.ДокументОснование = НовыйОбъект;
		Модифицированность = Истина;
		Записать();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПриСозданииЧтенииНаСервере();
	
	СобытияФормИСПереопределяемый.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтаФорма, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЭтаФорма, "ВидимостьПодключаемыхКоманд") Тогда
		НастроитьЗависимыеЭлементыФормы(ЭтаФорма, "ДокументОснование");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОповещениеПриПодключении = Новый ОписаниеОповещения("ПодключитьОборудованиеЗавершение", ЭтаФорма);
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(
		ОповещениеПриПодключении,
		ЭтаФорма,
		ПоддерживаемыеТипыПодключаемогоОборудования);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	ОповещениеПриОтключении = Новый ОписаниеОповещения("ОтключитьОборудованиеЗавершение", ЭтаФорма);
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(ОповещениеПриОтключении, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = ИнтеграцияИСКлиентСервер.ИмяСобытияИзмененоСостояние(ИнтеграцияИСМПСлужебныйКлиент.ИмяПодсистемы())
		И Параметр.Ссылка = Объект.Ссылка Тогда
		
		Если Параметр.Свойство("ОбъектИзменен")
			И Параметр.ОбъектИзменен Тогда
			ОбновитьПредставленияНаФорме(Истина);
		Иначе
			ОбновитьПредставленияНаФорме(Ложь);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ИмяСобытия = ИнтеграцияИСКлиентСервер.ИмяСобытияВыполненОбмен(ИнтеграцияИСМПСлужебныйКлиент.ИмяПодсистемы())
	 И (Параметр = Неопределено
		Или (ТипЗнч(Параметр) = Тип("Структура") И Параметр.ОбновлятьСтатусВФормахДокументов)) Тогда
		
		ОбновитьПредставленияНаФорме(Истина);
		
	КонецЕсли;
	
	Если ИмяСобытия = ИнтеграцияИСКлиентСервер.ИмяСобытияОбновитьКэшМаркируемойПродукции(
		ИнтеграцияИСМПСлужебныйКлиент.ИмяПодсистемы())
		И Параметр = ЭтаФорма Тогда
		
		ОбновитьКэшМаркируемойПродукции();
		
	КонецЕсли;
	
	СобытияФормИСКлиентПереопределяемый.ОбработкаОповещения(ЭтаФорма, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	СобытияФормИСМПКлиентПереопределяемый.ОбработкаВыбораСерии(
		ЭтаФорма, ВыбранноеЗначение, ИсточникВыбора, ПараметрыУказанияСерий);
		
	Если ТекущиеДанные <> Неопределено Тогда
		
		ТребуетсяОбновитьКэшУпаковок = ПроверкаИПодборПродукцииИСКлиент.ПрименитьКешПоСтроке(
			ЭтаФорма,
			Объект.Товары,
			ТекущиеДанные,
			ДанныеКешаСтроки,
			Истина,
			);
		
		ЗаполнитьЗначенияСвойств(ДанныеКешаСтроки, ТекущиеДанные);
		
		Если ТребуетсяОбновитьКэшУпаковок Тогда
			ПрименитьКешШтрихкодовУпаковок();
		КонецЕсли;
		
	КонецЕсли;
	
	Если ИнтеграцияИСКлиент.ЭтоЗагрузкаКодовМаркировки(ИсточникВыбора, ЭтаФорма) Тогда 
		
		Если Объект.ОтчетПроизводственнойЛинии Тогда
			
			Объект.ДанныеОтчетаПроизводственнойЛинии.Очистить();
			Для Каждого ЭлементДанных Из ВыбранноеЗначение Цикл
				
				СтрокаТЧ = Объект.ДанныеОтчетаПроизводственнойЛинии.Добавить();
				СтрокаТЧ.ЗначениеШтрихкода         = ЭлементДанных.Штрихкод;
				Если ЭлементДанных.Свойство("ФорматBase64") Тогда
					СтрокаТЧ.ФорматBase64 = ЭлементДанных.ФорматBase64;
				КонецЕсли;
				
			КонецЦикла;
		
		Иначе
			//Формат загрузки из внешнего файла полностью соответствует формату загрузки из ТСД
			Подключаемый_ПолученыДанныеИзТСД(ВыбранноеЗначение, Неопределено);
		КонецЕсли;
		
	КонецЕсли;
	
	СобытияФормИСМПКлиентПереопределяемый.ОбработкаВыбора(ЭтаФорма, ВыбранноеЗначение, ИсточникВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ВнешнееСобытие(Источник, Событие, Данные)
	
	Если Не ВидПродукцииУказан()
		Или РедактированиеФормыНедоступно
		Или Не ПравоИзменения
		Или Объект.ОтчетПроизводственнойЛинии Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСоСканераСтруктура = СобытияФормИСКлиент.ВнешнееСобытиеПреобразоватьДанныеСоСканераВСтруктуру(ЭтаФорма, Источник, Событие, Данные);
	Если ДанныеСоСканераСтруктура = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбработатьКодМаркировки(ДанныеСоСканераСтруктура);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Не Отказ Тогда
		ОбновитьЗаписатьПараметрыОбновленияСтатуса(Отказ, ТекущийОбъект);
	КонецЕсли;
	
	ШтрихкодированиеИС.СохранитьНастройкиВыбораМаркируемойПродукции(ЭтаФорма, Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтаФорма, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ИнтеграцияИСПереопределяемый.ЗаполнитьСлужебныеРеквизитыВКоллекции(ЭтаФорма, Объект.Товары);
	Если Не РедактированиеФормыНедоступно Тогда
		ПроверкаИПодборПродукцииИС.ПрименитьКешШтрихкодовУпаковок(ЭтаФорма,,,КлючСвязиСтатусаСтрок(ЭтаФорма));
	КонецЕсли;
	
	ПараметрыОбновленияСтатуса = Неопределено;
	ОбновитьПредставленияНаФорме();
	
	ИнтеграцияИСПереопределяемый.ЗаполнитьСлужебныеРеквизитыВКоллекции(ЭтаФорма, Объект.Товары);
	
	НастроитьЭлементыФормы(ЭтаФорма);
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма);
	
	ИнтеграцияИС.ПослеЗаписиНаСервереВФормеОбъектаДокументаИС(
		ЭтаФорма,
		ТекущийОбъект,
		ИнтеграцияИСМПКлиентСервер.ИмяПодсистемы(),
		ПараметрыЗаписи);
	СобытияФормИСПереопределяемый.ПослеЗаписиНаСервере(ЭтаФорма, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ИнтеграцияИСКлиент.ПослеЗаписиВФормеОбъектаДокументаИС(
		ЭтаФорма,
		Объект,
		ИнтеграцияИСМПКлиентСервер.ИмяПодсистемы(),
		ПараметрыЗаписи);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды") Тогда
		МодульПодключаемыеКомандыКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиент");
		МодульПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтаФорма, Объект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	СобытияФормИСКлиентПереопределяемый.ПередЗаписью(ЭтаФорма, Отказ, ПараметрыЗаписи);
	
	Если Не Отказ Тогда
		
		Объект.ИдентификаторПроисхожденияВЕТИССтрокой = ИдентификаторПроисхожденияВЕТИССтрокой;
		
		ДоступноУказаниеПроизводственнойПлощадки =
			Объект.ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.БАДы")
			И Объект.Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.КорректировкаСведенийКМ");
		Если Не ДоступноУказаниеПроизводственнойПлощадки Тогда
			ОчиститьДанныеПроизводственнойПлощадкиВЕТИС(ЭтаФорма);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СобытияФормИСМПКлиент.ОбработкаНавигационнойСсылки(ЭтаФорма, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтатусПредставлениеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	ОчиститьСообщения();

	Если (Не ЗначениеЗаполнено(Объект.Ссылка)) Или (Не Объект.Проведен) Тогда

		ОписаниеОповещенияВопрос = Новый ОписаниеОповещения("СтатусПредставлениеОбработкаНавигационнойСсылкиЗавершение",
		                                                    ЭтаФорма,
		                                                    Новый Структура("НавигационнаяСсылкаФорматированнойСтроки", НавигационнаяСсылкаФорматированнойСтроки));
		ТекстВопроса = НСтр("ru = '""Уточнение кодов маркировки ИС МП"" не проведен. Провести?'");
		ПоказатьВопрос(ОписаниеОповещенияВопрос, ТекстВопроса, РежимДиалогаВопрос.ДаНет);

	ИначеЕсли Модифицированность Тогда

		ОписаниеОповещенияВопрос = Новый ОписаниеОповещения("СтатусПредставлениеОбработкаНавигационнойСсылкиЗавершение",
		                                                    ЭтаФорма,
		                                                    Новый Структура("НавигационнаяСсылкаФорматированнойСтроки", НавигационнаяСсылкаФорматированнойСтроки));
		ТекстВопроса = НСтр("ru = '""Уточнение кодов маркировки ИС МП"" был изменен. Провести?'");
		ПоказатьВопрос(ОписаниеОповещенияВопрос, ТекстВопроса, РежимДиалогаВопрос.ДаНет);

	Иначе

		ОбработатьНажатиеНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки);

	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВопросПриИзмененииОрганизацияПриЗавершении", ЭтаФорма, ВыбранноеЗначение);
	
	Если Объект.ДанныеОтчетаПроизводственнойЛинии.Количество() > 0 Тогда
		
		СтандартнаяОбработка = Ложь;
		ТекстВопроса = НСтр("ru = 'При изменении организации ""Коды маркировки"" будут очищены. Продолжить?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	ИначеЕсли Объект.ШтрихкодыУпаковок.Количество() > 0 Тогда
		
		СтандартнаяОбработка = Ложь;
		ТекстВопроса = НСтр("ru = 'При изменении организации ""Штрихкоды упаковок"" будут очищены. Продолжить?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		
		ВопросПриИзмененииОрганизацияПриЗавершении(КодВозвратаДиалога.Да, ВыбранноеЗначение);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ПриИзмененииОрганизации();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидПродукцииПриИзменении(Элемент, ПерезаполнитьПоОснованию = Ложь)
	
	ПриИзмененииВидПродукцииНаСервере(ПерезаполнитьПоОснованию);
	ПриИзмененииВидПродукцииНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидПродукцииОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение <> Объект.ВидПродукции И Объект.ДанныеОтчетаПроизводственнойЛинии.Количество() Тогда
		
		СтандартнаяОбработка = Ложь;
		ТекстВопроса = НСтр("ru = 'При изменении вида продукции табличная часть ""Коды маркировки""
		                          |будет очищена. Продолжить?'");
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросПриИзмененииВидаПродукцииПриЗавершении", ЭтаФорма, ВыбранноеЗначение);
		
		РежимДиалога = РежимДиалогаВопрос.ДаНет;
		
		Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
			РежимДиалога = Новый СписокЗначений;
			РежимДиалога.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Очистить'"));
			РежимДиалога.Добавить(Истина, НСтр("ru = 'Перезаполнить'"));
			РежимДиалога.Добавить(КодВозвратаДиалога.Нет, НСтр("ru = 'Отмена'"));
		КонецЕсли;
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалога);
		
	ИначеЕсли ВыбранноеЗначение <> Объект.ВидПродукции И Объект.Товары.Количество() Тогда
		
		СтандартнаяОбработка = Ложь;
		ТекстВопроса = НСтр("ru = 'При изменении вида продукции табличная часть ""Товары"" будет очищена. Продолжить?'");
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросПриИзмененииВидаПродукцииПриЗавершении", ЭтаФорма, ВыбранноеЗначение);
		
		РежимДиалога = РежимДиалогаВопрос.ДаНет;
		
		Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
			РежимДиалога = Новый СписокЗначений;
			РежимДиалога.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Очистить'"));
			РежимДиалога.Добавить(Истина, НСтр("ru = 'Перезаполнить'"));
			РежимДиалога.Добавить(КодВозвратаДиалога.Нет, НСтр("ru = 'Отмена'"));
		КонецЕсли;
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалога);
		
	ИначеЕсли ВыбранноеЗначение <> Объект.ВидПродукции И Объект.ДокументыСертификации.Количество() Тогда
	
		ВопросПриИзмененииВидаПродукцииПриЗавершении(КодВозвратаДиалога.Да, ВыбранноеЗначение);
		
	ИначеЕсли ВыбранноеЗначение = Объект.ВидПродукции Тогда
		
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидПродукцииОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументОснованиеПриИзменении(Элемент)
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма, "ДокументОснование");
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОтсканироватьПроверитьМаркируемуюПродукциюОбработкаНавигационнойСсылки(
	Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПроверкаИПодборПродукцииИСМПКлиент.ОткрытьФормуПроверкиИПодбора(ЭтаФорма, Объект.ВидПродукции, ПараметрыУказанияСерий);
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацияПриИзменении(Элемент)
	
	Действия = Новый Структура;
	Действия.Вставить("ОчиститьТабличныеЧасти", Ложь);
	
	ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ВопросПриИзмененииОперацииЗавершение", ЭтаФорма, Действия);
	
	Если ПредыдущаяОперация <> Объект.Операция Тогда
		
		Если Объект.ДанныеОтчетаПроизводственнойЛинии.Количество() Тогда
			
			Действия.ОчиститьТабличныеЧасти = Истина;
			ТекстВопроса = НСтр("ru = 'При изменении вида операции табличная часть ""Коды маркировки""
			                          |будет очищена. Продолжить?'");
			Кнопки = РежимДиалогаВопрос.ДаНет;
			ПоказатьВопрос(ОписаниеОповещенияОЗавершении, ТекстВопроса, Кнопки);
			
			Возврат;
			
		ИначеЕсли Объект.Товары.Количество() Или Объект.ШтрихкодыУпаковок.Количество() Тогда
		
			Действия.ОчиститьТабличныеЧасти = Истина;
			ТекстВопроса = НСтр("ru = 'При изменении вида операции табличная часть ""Товары""
			                          |будет очищена. Продолжить?'");
			Кнопки = РежимДиалогаВопрос.ДаНет;
			ПоказатьВопрос(ОписаниеОповещенияОЗавершении, ТекстВопроса, Кнопки);
			
			Возврат;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ОписаниеОповещенияОЗавершении, КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПредыдущаяОперация = Объект.Операция;
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацияОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетПроизводственнойЛинииПриИзменении(Элемент)
	
	ОчиститьТовары                            = Ложь;
	ОчиститьДанныеОтчетаПроизводственнойЛинии = Ложь;
	
	Действия = Новый Структура;
	Действия.Вставить("ОчиститьТовары", ОчиститьТовары);
	Действия.Вставить("ОчиститьДанныеОтчетаПроизводственнойЛинии", ОчиститьДанныеОтчетаПроизводственнойЛинии);
	
	ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ВопросПриИзмененииОтчетПроизводственнойЛинииЗавершение", ЭтаФорма, Действия);
	Если Объект.ОтчетПроизводственнойЛинии И (Объект.Товары.Количество() Или Объект.ШтрихкодыУпаковок.Количество()) Тогда
		Действия.ОчиститьТовары = Истина;
		ТекстВопроса = НСтр(
			"ru = 'При установке признака Отчет прозводственной линии табличная часть ""Товары"" будет очищена. Продолжить?'");
			Кнопки = РежимДиалогаВопрос.ДаНет; 
		ПоказатьВопрос(ОписаниеОповещенияОЗавершении, ТекстВопроса, Кнопки);
		Возврат;
	ИначеЕсли Не Объект.ОтчетПроизводственнойЛинии И Объект.ДанныеОтчетаПроизводственнойЛинии.Количество() Тогда
		Действия.ОчиститьДанныеОтчетаПроизводственнойЛинии = Истина;
		ТекстВопроса = НСтр(
			"ru = 'При снятии признака Отчет прозводственной линии табличная часть ""Коды маркировки"" будет очищена. Продолжить?'");
			Кнопки = РежимДиалогаВопрос.ДаНет; 
		ПоказатьВопрос(ОписаниеОповещенияОЗавершении, ТекстВопроса, Кнопки);
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ОписаниеОповещенияОЗавершении, КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеСохраненногоВыбораОбработкаНавигационнойСсылки(
	Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИнтеграцияИСКлиент.ПредставлениеСохраненногоВыбораОбработкаНавигационнойСсылки(
		ЭтаФорма, НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура КодТНВЭДНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("ТекущаяСтрока",                           Объект.КодТНВЭД);
	ПараметрыФормы.Вставить("ВидПродукции",                            Объект.ВидПродукции);
	ПараметрыФормы.Вставить("Организация",                             Объект.Организация);
	ПараметрыФормы.Вставить("РежимВыбора",                             Истина);
	ПараметрыФормы.Вставить("ВозвращатьСсылкуНаЭлементКлассификатора", Ложь);
	
	ОткрытьФорму(
		"РегистрСведений.КодыТНВЭДИСМП.Форма.ФормаСписка", ПараметрыФормы, Элемент,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура СертификацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ВидСертификации",   Объект.ВидДокументаСертификации);
	ПараметрыОткрытия.Вставить("ДатаСертификации",  Объект.ДатаДокументаСертификации);
	ПараметрыОткрытия.Вставить("НомерСертификации", Объект.НомерДокументаСертификации);
	ПараметрыОткрытия.Вставить("ВидПродукции",      Объект.ВидПродукции);
	ПараметрыОткрытия.Вставить("ИспользоватьНомерСкважины", Ложь);
	
//	ДополнительныеПараметры = Новый Структура("ВариантУточненияДанных", "Сертификация");
//	ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнениеСертификацииЗавершение", ЭтаФорма, ДополнительныеПараметры);

	ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнениеСертификацииЗавершение", ЭтаФорма);
	
	ОткрытьФорму(
		"ОбщаяФорма.УточнениеСертификацииИС",
		ПараметрыОткрытия,
		ЭтаФорма,,,,
		ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ИдентификаторПроисхожденияВЕТИСНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыСканирования = ШтрихкодированиеИСКлиент.ПараметрыСканирования(ЭтаФорма);
	
	ПараметрыОткрытия = ИнтеграцияИСМПВЕТИСКлиент.ПараметрыВыбораИдентификатораПросхождения(ПараметрыСканирования.ВидОперацииИСМП);
	ПараметрыОткрытия.ОповещениеВыбора = Новый ОписаниеОповещения("ВыборИдентификатораПроисхожденияВЕТИСЗавершение", ЭтаФорма);
	ПараметрыОткрытия.Организация                         = Объект.Организация;
	ПараметрыОткрытия.ОтчетПроизводственнойЛинии          = Истина;
	
	ИнтеграцияИСМПВЕТИСКлиент.ОткрытьФормуВыбораИдентификатораПроисхожденияВЕТИС(ПараметрыОткрытия, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ИдентификаторПроисхожденияВЕТИСПриИзменении(Элемент)
	
	ПриИзмененииИдентификаторПроисхожденияВЕТИС(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ИдентификаторПроисхожденияВЕТИСОчистка(Элемент, СтандартнаяОбработка)
	
	ПриИзмененииИдентификаторПроисхожденияВЕТИС(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ИдентификаторПроисхожденияВЕТИССтрокойПриИзменении(Элемент)
	
	ИдентификаторПроисхожденияВЕТИССтрокой = ИнтеграцияИСКлиентСервер.ПреобразоватьИдентификаторВСД(ИдентификаторПроисхожденияВЕТИССтрокой);
	
	Если ЗначениеЗаполнено(ИдентификаторПроисхожденияВЕТИССтрокой)
		И Не СтроковыеФункцииКлиентСервер.ЭтоУникальныйИдентификатор(ИдентификаторПроисхожденияВЕТИССтрокой) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Указан некорректный идентификатор ВСД'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СкоропортящаясяПродукцияПриИзменении(Элемент)
	
	УстановитьФорматСрокГодности(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроизводственнаяПлощадкаВЕТИСНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыОткрытияФормыВыбора = Новый Структура;
	
	ИнтеграцияИСМПВЕТИСКлиент.ПараметрыВыбораПроизводственнойПлощадкиВЕТИС(ЭтаФорма, ПараметрыОткрытияФормыВыбора);
	
	ПараметрыОткрытияФормыВыбора.Вставить("ОповещениеВыбора",
		Новый ОписаниеОповещения("ВыборПроизводственнойПлощадкиВЕТИСЗавершение", ЭтаФорма));
	
	ИнтеграцияИСМПВЕТИСКлиент.ОткрытьФормуВыбораПроизводственнойПлощадкиВЕТИС(ПараметрыОткрытияФормыВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроизводственнаяПлощадкаВЕТИСПриИзменении(Элемент)
	
	ПриИзмененииПроизводственнойПлощадкиВЕТИС();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроизводственнаяПлощадкаВЕТИСОчистка(Элемент, СтандартнаяОбработка)
	
	ПриИзмененииПроизводственнойПлощадкиВЕТИС();
	
КонецПроцедуры

&НаКлиенте
Процедура ИдентификаторПроизводственнойПлощадкиВЕТИСПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ИдентификаторПроизводственнойПлощадкиВЕТИС)
		И Не СтроковыеФункцииКлиентСервер.ЭтоУникальныйИдентификатор(Объект.ИдентификаторПроизводственнойПлощадкиВЕТИС) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Указан некорректный идентификатор производственной площадки ВетИС'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыПослеУдаления(Элемент)
	
	ТоварыПослеУдаленияСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ИнициализироватьКэшСтроки(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПередНачаломИзменения(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	ЗаполнитьЗначенияСвойств(ДанныеКешаСтроки, ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если Не ОтменаРедактирования Тогда
		
		ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
		ТребуетсяОбновитьКэшУпаковок = ПроверкаИПодборПродукцииИСКлиент.ПрименитьКешПоСтроке(
			ЭтаФорма,
			Объект.Товары,
			ТекущиеДанные,
			ДанныеКешаСтроки,
			Истина,
			КлючСвязиСтатусаСтрок(ЭтаФорма, ТекущиеДанные.Номенклатура));
	
		Если ТребуетсяОбновитьКэшУпаковок Тогда
			ПрименитьКешШтрихкодовУпаковок();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриАктивизацииЯчейки(Элемент)
	
	Если Элементы.Товары.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Элемент.ТекущийЭлемент = Элементы.ТоварыСрокГодности Тогда
		ОпределитьФорматРедактированияСрокГодности();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если РедактированиеФормыНедоступно Тогда
		СобытияФормИСМПКлиент.ВыборЭлементаТабличнойЧастиОткрытьФормуЭлемента(ЭтаФорма, Элемент, Поле);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СобытияФормИСКлиентПереопределяемый.ПриНачалеВыбораНоменклатуры(Элемент, Объект.ВидПродукции, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если ДанныеКешаСтроки.Номенклатура = ТекущиеДанные.Номенклатура Тогда
		Возврат;
	КонецЕсли;
	
	СобытияФормИСМПКлиентПереопределяемый.ПриИзмененииНоменклатуры(
		ЭтаФорма, ТекущиеДанные, КэшированныеЗначения, ПараметрыУказанияСерий);
	
	Если ИспользуетсяПодсистемаВЕТИС И ЗначениеЗаполнено(ТекущиеДанные.ИдентификаторПроисхожденияВЕТИС) Тогда
		
		НоменклатураСоответствуетСопоставленнойПродукции = НоменклатураСоответствуетСопоставленнойПродукцииВЕТИСПоИдентификаторуПроисхождения(
			ТекущиеДанные.ИдентификаторПроисхожденияВЕТИС,
			ТекущиеДанные.Номенклатура,
			ТекущиеДанные.Характеристика,
			ТекущиеДанные.Серия);
		
		Если Не НоменклатураСоответствуетСопоставленнойПродукции Тогда
			ТекущиеДанные.ИдентификаторПроисхожденияВЕТИС = Неопределено;
		Иначе
			ТекущиеДанные.СкоропортящаясяПродукция = ДанныеКешаСтроки.СкоропортящаясяПродукция;
		КонецЕсли;
		
	КонецЕсли;
	
	ТребуетсяОбновитьКэшУпаковок = ПроверкаИПодборПродукцииИСКлиент.ПрименитьКешПоСтроке(
		ЭтаФорма,
		Объект.Товары,
		ТекущиеДанные,
		ДанныеКешаСтроки,
		Истина,
		КлючСвязиСтатусаСтрок(ЭтаФорма, ТекущиеДанные.Номенклатура));
	
	ЗаполнитьЗначенияСвойств(ДанныеКешаСтроки, ТекущиеДанные);
	
	Если ТребуетсяОбновитьКэшУпаковок Тогда
		ПрименитьКешШтрихкодовУпаковок();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыХарактеристикаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	СобытияФормИСМПКлиентПереопределяемый.ПриНачалеВыбораХарактеристики(
		Элемент, ТекущиеДанные, СтандартнаяОбработка, "Номенклатура");
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыХарактеристикаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	СобытияФормИСМПКлиентПереопределяемый.ПриИзмененииХарактеристики(ЭтаФорма, ТекущиеДанные, КэшированныеЗначения);
	
	Если ИспользуетсяПодсистемаВЕТИС И ЗначениеЗаполнено(ТекущиеДанные.ИдентификаторПроисхожденияВЕТИС) Тогда
		
		НоменклатураСоответствуетСопоставленнойПродукции = НоменклатураСоответствуетСопоставленнойПродукцииВЕТИСПоИдентификаторуПроисхождения(
			ТекущиеДанные.ИдентификаторПроисхожденияВЕТИС,
			ТекущиеДанные.Номенклатура,
			ТекущиеДанные.Характеристика,
			ТекущиеДанные.Серия);
		
		Если Не НоменклатураСоответствуетСопоставленнойПродукции Тогда
			ТекущиеДанные.ИдентификаторПроисхожденияВЕТИС = Неопределено;
		КонецЕсли;
		
	КонецЕсли;
	
	ТребуетсяОбновитьКэшУпаковок = ПроверкаИПодборПродукцииИСКлиент.ПрименитьКешПоСтроке(
		ЭтаФорма,
		Объект.Товары,
		ТекущиеДанные,
		ДанныеКешаСтроки,
		Истина,
		КлючСвязиСтатусаСтрок(ЭтаФорма, ТекущиеДанные.Номенклатура));
	
	ЗаполнитьЗначенияСвойств(ДанныеКешаСтроки, ТекущиеДанные);
	
	Если ТребуетсяОбновитьКэшУпаковок Тогда
		ПрименитьКешШтрихкодовУпаковок();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСерияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ИнтеграцияИСКлиент.ОткрытьПодборСерий(ЭтаФорма,, Элемент.ТекстРедактирования, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСерияПриИзменении(Элемент)
	
	СобытияФормИСМПКлиентПереопределяемый.ПриИзмененииСерии(
		ЭтаФорма, Элементы.Товары.ТекущиеДанные, КэшированныеЗначения, ПараметрыУказанияСерий);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоУпаковокПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	СобытияФормИСМПКлиентПереопределяемый.ПриИзмененииКоличества(
		ЭтаФорма, ТекущиеДанные, КэшированныеЗначения, ПараметрыУказанияСерий);
	
	ПроверкаИПодборПродукцииИСКлиент.ПрименитьКешПоСтроке(
		ЭтаФорма,
		Объект.Товары,
		ТекущиеДанные,
		ДанныеКешаСтроки,
		Истина,
		КлючСвязиСтатусаСтрок(ЭтаФорма, ТекущиеДанные.Номенклатура));
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПотребительскихУпаковокПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	ПроверкаИПодборПродукцииИСКлиент.ПрименитьКешПоСтроке(
		ЭтаФорма,
		Объект.Товары,
		ТекущиеДанные,
		ДанныеКешаСтроки,
		Истина,
		КлючСвязиСтатусаСтрок(ЭтаФорма, ТекущиеДанные.Номенклатура));
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыУпаковкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	СобытияФормИСМПКлиентПереопределяемый.ПриИзмененииКоличества(
		ЭтаФорма, ТекущиеДанные, КэшированныеЗначения, ПараметрыУказанияСерий);
	
	ПроверкаИПодборПродукцииИСКлиент.ПрименитьКешПоСтроке(
		ЭтаФорма,
		Объект.Товары,
		ТекущиеДанные,
		ДанныеКешаСтроки,
		Истина,
		КлючСвязиСтатусаСтрок(ЭтаФорма, ТекущиеДанные.Номенклатура));
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКодТНВЭДНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		ПараметрыФормы = Новый Структура;
		
		ПараметрыФормы.Вставить("ТекущаяСтрока",                           ТекущиеДанные.КодТНВЭД);
		ПараметрыФормы.Вставить("ВидПродукции",                            Объект.ВидПродукции);
		ПараметрыФормы.Вставить("Организация",                             Объект.Организация);
		ПараметрыФормы.Вставить("РежимВыбора",                             Истина);
		ПараметрыФормы.Вставить("ВозвращатьСсылкуНаЭлементКлассификатора", Ложь);
		
		ОткрытьФорму(
			"РегистрСведений.КодыТНВЭДИСМП.Форма.ФормаСписка", ПараметрыФормы, Элемент,,,,,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыИдентификаторПроисхожденияВЕТИСНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ПараметрыСканирования = ШтрихкодированиеИСКлиент.ПараметрыСканирования(ЭтаФорма);

	ПараметрыОткрытия = ИнтеграцияИСМПВЕТИСКлиент.ПараметрыВыбораИдентификатораПросхождения(ПараметрыСканирования.ВидОперацииИСМП);
	ЗаполнитьЗначенияСвойств(ПараметрыОткрытия, ТекущиеДанные, "Номенклатура, Характеристика, Серия, ХарактеристикиИспользуются");
	ПараметрыОткрытия.ОповещениеВыбора = Новый ОписаниеОповещения("ВыборИдентификатораПроисхожденияВЕТИСЗавершение", ЭтаФорма);
	ПараметрыОткрытия.Организация                         = Объект.Организация;
	ПараметрыОткрытия.ТребуетсяУказаниеСерии = ИнтеграцияИСКлиентСервер.НеобходимоУказатьСерию(ТекущиеДанные.СтатусУказанияСерий);

	ИнтеграцияИСМПВЕТИСКлиент.ОткрытьФормуВыбораИдентификатораПроисхожденияВЕТИС(ПараметрыОткрытия, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура ТоварыИдентификаторПроисхожденияВЕТИСПриИзменении(Элемент)
	ЗаполнитьДанныеВЕТИС();
	ОпределитьФорматРедактированияСрокГодности();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыИдентификаторПроисхожденияВЕТИСОчистка(Элемент, СтандартнаяОбработка)
	ЗаполнитьДанныеВЕТИС();
	ОпределитьФорматРедактированияСрокГодности();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДанныеОтчетаПроизводственнойЛинии

&НаКлиенте
Процедура ЗначенияШтрихкодовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДокументыСертификации

&НаКлиенте
Процедура ДокументыСертификацииПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И Не Копирование
		И ЗначениеЗаполнено(ВидДокументаСертификации) Тогда
		ТекущиеДанные = Элемент.ТекущиеДанные;
		ТекущиеДанные.ВидДокументаСертификации = ВидДокументаСертификации;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#Область ПодключаемыеКоманды

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтаФорма, Команда, Объект);
КонецПроцедуры

//@skip-warning
&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтаФорма, Контекст, Объект, Результат);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтаФорма, Объект);
	ПодключаемыеКомандыИСКлиентСервер.УправлениеВидимостьюКомандПодключенныхКОбъекту(ЭтаФорма);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормИСКлиентПереопределяемый.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ВозобновитьПодборМаркируемойПродукции(Команда)
	
	ПроверкаИПодборПродукцииИСМПКлиент.ВозобновитьПроверку(ЭтаФорма, Объект.ВидПродукции, ПараметрыУказанияСерий);
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиДокумент(Команда)
	
	ОчиститьСообщения();
	ПараметрыЗаписи = Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение);
	Записать(ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьДокумент(Команда)
	
	ОчиститьСообщения();
	Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиИЗакрыть(Команда)
	
	ОчиститьСообщения();
	ПараметрыЗаписи = Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение);
	
	Если Записать(ПараметрыЗаписи) Тогда
		Закрыть();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура АрхивироватьДокумент(Команда)
	
	ИнтеграцияИСКлиент.АрхивироватьДокументы(ЭтаФорма, Объект.Ссылка, ИнтеграцияИСМПКлиент);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзВнешнегоФайла(Команда)
	
	ОчиститьСообщения();
	
	ИнтеграцияИСКлиент.ОткрытьФормуЗагрузкиКодовМаркировки(
		ЭтаФорма,
		Ложь,
		ИнтеграцияИСМПКлиент.ЗаголовокФормыЗагрузкиКодовМаркировки(ЭтаФорма));
	
КонецПроцедуры

&НаКлиенте
Процедура ПоискПоШтрихкоду(Команда)
	
	ОчиститьСообщения();
	
	Если Не ВидПродукцииУказан() Тогда
		Возврат;
	КонецЕсли;
	
	ШтрихкодированиеИСКлиент.ПоказатьВводШтрихкода(ОписаниеОповещенияОбработкиКодаМаркировки());
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПодбор(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("Подключаемый_ОбработкаРезультатаПодбораНоменклатуры", ЭтаФорма);
	СобытияФормИСМПКлиентПереопределяемый.ОткрытьФормуПодбораНоменклатуры(ЭтаФорма, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьДанныеВТСД(Команда)
	
	ОчиститьСообщения();
	СобытияФормИСМПКлиентПереопределяемый.ВыгрузитьДанныеВТСД(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанныеИзТСД(Команда)
	
	ОчиститьСообщения();
	
	МенеджерОборудованияИСКлиент.НачатьЗагрузкуДанныеИзТСД(
		Новый ОписаниеОповещения("ЗагрузитьИзТСДЗавершение", ЭтаФорма),
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерезаполнитьПоОснованию(Команда)
	ОбработчикПерезаполненияПоОснованию();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Оборудование

&НаКлиенте
Процедура ПодключитьОборудованиеЗавершение(РезультатВыполнения, Параметры) Экспорт

	Если Не РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр( "ru = 'При подключении оборудования произошла ошибка:""%ОписаниеОшибки%"".'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%" , РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОтключитьОборудованиеЗавершение(РезультатВыполнения, Параметры) Экспорт

	Если Не РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр( "ru = 'При отключении оборудования произошла ошибка: ""%ОписаниеОшибки%"".'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%" , РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ПриСозданииЧтенииНаСервере()
	
	ЦветГиперссылки = ЦветаСтиля.ЦветГиперссылкиГосИС;
	ЦветТекстаПоля  = ЦветаСтиля.ЦветТекстаПоля;
	
	Если Не Объект.Ссылка.Пустая() Тогда
		ДанныеДокумента = Новый Структура("ВидПродукции, Операция");
		ЗаполнитьЗначенияСвойств(ДанныеДокумента, Объект);
		Если ЭтоАдресВременногоХранилища(ДанныеДокументаАдрес) Тогда
			ПоместитьВоВременноеХранилище(ДанныеДокумента, ДанныеДокументаАдрес);
		Иначе
			ДанныеДокументаАдрес = ПоместитьВоВременноеХранилище(ДанныеДокумента, УникальныйИдентификатор);
		КонецЕсли;
	КонецЕсли;
	
	ИнтеграцияИСМП.НастроитьВидимостьКолонкиКоличестваКодовМаркировки(ЭтаФорма);

	ШтрихкодированиеИС.ВосстановитьНастройкиВыбораМаркируемойПродукции(ЭтаФорма, Объект.Ссылка);
	ШтрихкодированиеИСКлиентСервер.ОтобразитьСохраненныйВыборПоМаркируемойПродукции(ЭтаФорма);
	
	ПараметрыУказанияСерий = ИнтеграцияИС.ПараметрыУказанияСерийФормыОбъекта(Объект, Документы.УточнениеСведенийОКодахМаркировкиИСМП);
	ИспользуетсяПодсистемаВЕТИС = ИнтеграцияИСМПВЕТИС.ИспользуетсяПодсистемаВетИС();
	
	ПравоИзменения = ПравоДоступа("Изменение", Метаданные.Документы.УточнениеСведенийОКодахМаркировкиИСМП);
	
	ИнтеграцияИСПереопределяемый.ЗаполнитьСлужебныеРеквизитыВКоллекции(ЭтаФорма, Объект.Товары);
	
	СобытияФормИСКлиентСерверПереопределяемый.УстановитьПараметрыВыбораНоменклатуры(ЭтаФорма, Объект.ВидПродукции);
	
	ЗаполнитьДоступныеОперации();
	
	ПараметрыОбновленияСтатуса = Неопределено;
	
	ЗаполнитьПредставлениеСертификации(ЭтаФорма);
	ОбновитьПредставленияНаФорме();
	
	ЗаполнитьДоступныеВидыДокументовСертификации();
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма);
	НастроитьЭлементыФормы(ЭтаФорма);
	
	Если Не ПодключенаОбработкаКодовМаркировки Тогда
		ПроверкаИПодборПродукцииИС.ПодключитьОбработкуКодовМаркировки(ЭтаФорма,,
			"ИдентификаторПроисхожденияВЕТИС,СрокГодности,КоличествоПотребительскихУпаковок");
		ПодключенаОбработкаКодовМаркировки = Истина;
	КонецЕсли;
	
	ИнициализироватьКэшСтроки(ЭтаФорма);
	Если Не РедактированиеФормыНедоступно Тогда
		ПроверкаИПодборПродукцииИС.ЗаполнитьКешШтрихкодовУпаковок(ЭтаФорма);
		ПроверкаИПодборПродукцииИС.ПрименитьКешШтрихкодовУпаковок(ЭтаФорма,,,КлючСвязиСтатусаСтрок(ЭтаФорма));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьОбменОбработкаОжидания() Экспорт
	
	ИнтеграцияИСМПСлужебныйКлиент.ПродолжитьВыполнениеОбмена(ЭтаФорма, Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриЗавершенииОперации(Результат, ДополнительныеПараметры) Экспорт

	Прочитать();

КонецПроцедуры

&НаСервере
Процедура ОбновитьПредставленияНаФорме(Прочитать = Ложь)

	Если Прочитать Тогда
		Прочитать();
	Иначе
		ОбновитьСтатусИСМП();
	КонецЕсли;
	
	ОбновитьИнформациюОткрытияФормыПроверкиПодбора();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДоступныеОперации()
	
	Элементы.Операция.СписокВыбора.Очистить();
	
	ВидПродукции = Объект.ВидПродукции;
	
	Элементы.Операция.СписокВыбора.Добавить(
		ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.КорректировкаСведенийКМ"));
	
	ЭтоПродукцияПодконтрольнаяВЕТИС = ИнтеграцияИСКлиентСервер.ЭтоПродукцияПодконтрольнаяВЕТИС(ВидПродукции);
	ЭтоМолочнаяПродукция            = ИнтеграцияИСКлиентСервер.ЭтоМолочнаяПродукцияИСМП(ВидПродукции);
	Если ЭтоПродукцияПодконтрольнаяВЕТИС Тогда
		Элементы.Операция.СписокВыбора.Добавить(
			ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.КорректировкаСведенийКМСрокГодностиВСД"));
	ИначеЕсли Документы.УточнениеСведенийОКодахМаркировкиИСМП.ВидПродукцииИспользуетУточнениеСроковГодности(ВидПродукции) Тогда
		Элементы.Операция.СписокВыбора.Добавить(
			ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.КорректировкаСведенийКМСрокГодности"));
	КонецЕсли;
	
	Если ЭтоМолочнаяПродукция Тогда
		Элементы.Операция.СписокВыбора.Добавить(
			ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.КорректировкаСведенийКМФактическийВес"));
	КонецЕсли;
	
	Если ЭтоАдресВременногоХранилища(ДанныеДокументаАдрес) Тогда
		ДанныеДокумента = ПолучитьИзВременногоХранилища(ДанныеДокументаАдрес);
	Иначе
		ДанныеДокумента = Неопределено;
	КонецЕсли;
	
	Если ДанныеДокумента <> Неопределено
		И ДанныеДокумента.ВидПродукции = Объект.ВидПродукции
		И ЗначениеЗаполнено(ДанныеДокумента.Операция)
		И Элементы.Операция.СписокВыбора.НайтиПоЗначению(ДанныеДокумента.Операция) = Неопределено Тогда
		
		Элементы.Операция.СписокВыбора.Добавить(ДанныеДокумента.Операция);
		
	КонецЕсли;
	
	Элементы.Операция.СписокВыбора.СортироватьПоПредставлению();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЭлементыФормы(Форма)
	
	Элементы     = Форма.Элементы;
	
	УстановитьФорматДатыПроизводства(Форма);
	
	ЗависимыеОтСтатусаИСМП = Новый Массив;
	ЗависимыеОтСтатусаИСМП.Добавить("ГруппаНередактируемыеПослеОтправкиРеквизитыОсновное");
	ЗависимыеОтСтатусаИСМП.Добавить("СтраницаОтчетПроизводственнойЛинии"); 
	ЗависимыеОтСтатусаИСМП.Добавить("СтраницаДокументыСертификации");
	ЗависимыеОтСтатусаИСМП.Добавить("СтраницаТовары");
	ЗависимыеОтСтатусаИСМП.Добавить("ГруппаНередактируемыеПослеОтправкиКомандыОтчетПроизводственнойЛинии");
	
	ИнтеграцияИСКлиентСервер.УстановитьДоступностьЭлементовФормы(Форма,
		ЗависимыеОтСтатусаИСМП, Не Форма.РедактированиеФормыНеДоступно);
	
	ЗависимыеОтСтатусаПроверкиИПодбора = Новый Массив;
	ЗависимыеОтСтатусаПроверкиИПодбора.Добавить("ТоварыПодменюЗаполнить");
	ЗависимыеОтСтатусаПроверкиИПодбора.Добавить("ГруппаТорговоеОборудование");
	ЗависимыеОтСтатусаПроверкиИПодбора.Добавить("ТоварыНоменклатура");
	ЗависимыеОтСтатусаПроверкиИПодбора.Добавить("ТоварыХарактеристика");
	ЗависимыеОтСтатусаПроверкиИПодбора.Добавить("ТоварыСерия");
	ЗависимыеОтСтатусаПроверкиИПодбора.Добавить("ТоварыКоличествоУпаковок");
	ЗависимыеОтСтатусаПроверкиИПодбора.Добавить("ТоварыУпаковкаЕдиницаИзмерения");
	ЗависимыеОтСтатусаПроверкиИПодбора.Добавить("ТоварыДобавить");
	ЗависимыеОтСтатусаПроверкиИПодбора.Добавить("ТоварыСкопировать");
	ЗависимыеОтСтатусаПроверкиИПодбора.Добавить("ТоварыИзменить");
	ЗависимыеОтСтатусаПроверкиИПодбора.Добавить("ТоварыУдалить");
	ЗависимыеОтСтатусаПроверкиИПодбора.Добавить("ТоварыКонтекстноеМенюДобавить");
	ЗависимыеОтСтатусаПроверкиИПодбора.Добавить("ТоварыКонтекстноеМенюСкопировать");
	ЗависимыеОтСтатусаПроверкиИПодбора.Добавить("ТоварыКонтекстноеМенюИзменить");
	ЗависимыеОтСтатусаПроверкиИПодбора.Добавить("ТоварыКонтекстноеМенюУдалить");

	ИнтеграцияИСКлиентСервер.УстановитьДоступностьЭлементовФормы(Форма,
		ЗависимыеОтСтатусаПроверкиИПодбора,
		Не (Форма.РедактированиеФормыНеДоступно Или РедактированиеНедоступноПоСтатусуПроверкиИподбора(Форма)));
	
	Если Форма.РедактированиеФормыНеДоступно
	 Или НЕ Форма.ПравоИзменения Тогда
		Элементы.ВозобновитьПодборМаркируемойПродукции.Доступность = Ложь;
	ИначеЕсли Форма.СтатусПроверкиИПодбора = ПредопределенноеЗначение("Перечисление.СтатусыПроверкиИПодбораИС.Завершено") Тогда
		Элементы.ВозобновитьПодборМаркируемойПродукции.Доступность = Истина;
	Иначе
		Элементы.ВозобновитьПодборМаркируемойПродукции.Доступность = Ложь;
	КонецЕсли;
	
	Элементы.ТоварыСтатусПроверкиГосИС.Видимость = Не Форма.РедактированиеФормыНеДоступно;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ВидимостьПодключаемыхКоманд") Тогда
		ПодключаемыеКомандыИСКлиентСервер.УправлениеВидимостьюКомандПодключенныхКОбъекту(Форма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЗависимыеЭлементыФормы(Форма, СписокРеквизитов = "")
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	Инициализация = ПустаяСтрока(СписокРеквизитов);
	СтруктураРеквизитов = Новый Структура(СписокРеквизитов);
	
	ЭтоПродукцияПодконтрольнаяВЕТИС = ИнтеграцияИСКлиентСервер.ЭтоПродукцияПодконтрольнаяВЕТИС(Объект.ВидПродукции);
	ЭтоМолочнаяПродукция            = ИнтеграцияИСКлиентСервер.ЭтоМолочнаяПродукцияИСМП(Объект.ВидПродукции);
	ЭтоБАД                          = Объект.ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.БАДы");
	ЭтоПиво                         = Объект.ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Пиво");
	ЭтоМОТП                         = ИнтеграцияИСКлиентСервер.ЭтоПродукцияМОТП(Объект.ВидПродукции);
	
	Элементы.ОтчетПроизводственнойЛинии.ТолькоПросмотр = Ложь;
	
	Если СтруктураРеквизитов.Свойство("ДокументОснование")
		Или СтруктураРеквизитов.Свойство("Операция") Тогда
		ПодключаемыеКомандыИСКлиентСервер.УправлениеВидимостьюКомандПодключенныхКОбъекту(Форма);
		Элементы.ТоварыПерезаполнитьПоОснованию.Доступность = ЗначениеЗаполнено(Объект.ДокументОснование);
	КонецЕсли;
	
	Если Инициализация Или СтруктураРеквизитов.Свойство("ВидПродукции") Тогда
		
		Если ЭтоПродукцияПодконтрольнаяВЕТИС Тогда
			
			Если Не Форма.ИспользуетсяПодсистемаВЕТИС Тогда
				Элементы.ОтчетПроизводственнойЛинии.ТолькоПросмотр = Не Форма.ИспользуетсяПодсистемаВЕТИС;
				Форма.Объект.ОтчетПроизводственнойЛинии = Истина;
			Иначе
				ЗаголовокКолонкиВЕТИС = ИнтеграцияИСМПВЕТИСКлиентСервер.ИмяИдентификатораПроисхожденияВЕТИС();
				Элементы.ТоварыИдентификаторПроисхожденияВЕТИС.Заголовок            = ЗаголовокКолонкиВЕТИС;
				Элементы.ШтрихкодыУпаковокИдентификаторПроисхожденияВЕТИС.Заголовок = ЗаголовокКолонкиВЕТИС;
				Элементы.ИдентификаторПроисхожденияВЕТИС.Заголовок                  = ЗаголовокКолонкиВЕТИС;
			КонецЕсли;
			
		КонецЕсли;
		
		СтруктураРеквизитов.Вставить("Операция");
		
	КонецЕсли;
	
	Если Инициализация Или СтруктураРеквизитов.Свойство("ОтчетПроизводственнойЛинии") Тогда
		СтруктураРеквизитов.Вставить("Операция");
	КонецЕсли;
	
	Если Инициализация Или СтруктураРеквизитов.Свойство("Операция") Тогда
		
		Операция     = Форма.Объект.Операция;
		
		ЭтоОтчетОПеревзвешивании = Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ОтчетОПеревзвешивании")
			Или Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.КорректировкаСведенийКМФактическийВес");
		РедактированиеДополнительныхСведений   = Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.КорректировкаСведенийКМ");
		РедактированиеСертификации = Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.СведенияОРазрешительнойДокументацииДляВводаВОборот");
		РедактированиеВСД = Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.КорректировкаСведенийКМСрокГодностиВСД");
		РедактированиеСроковГодности = Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.КорректировкаСведенийКМСрокГодности");
		
		ДокументыСертификацииВШапке = Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.СведенияОКодахИдентификацииДляВводаВОборот");
		
		Если ЭтоОтчетОПеревзвешивании И Форма.Объект.ОтчетПроизводственнойЛинии Тогда
			Форма.Объект.ОтчетПроизводственнойЛинии = Ложь;
		КонецЕсли;
		
		ОтчетПроизводственнойЛинии = Форма.Объект.ОтчетПроизводственнойЛинии;
		
		Элементы.Сертификация.Видимость = ДокументыСертификацииВШапке;
		Элементы.СтраницаДокументыСертификации.Видимость =  РедактированиеСертификации Или РедактированиеДополнительныхСведений; 
		
		Элементы.ОтчетПроизводственнойЛинии.Видимость = Не ЭтоОтчетОПеревзвешивании;
		Элементы.ГруппаСведенияОтчетаПроизводственнойЛинии.Видимость = ОтчетПроизводственнойЛинии;
		
		РедактированиеТВНЭД = РедактированиеДополнительныхСведений И Не (ЭтоПиво Или ЭтоМОТП);
		
		Элементы.КодТНВЭД.Видимость = РедактированиеТВНЭД;
		Элементы.ТоварыКодТНВЭД.Видимость         = РедактированиеТВНЭД;
		
		Элементы.ДатаПроизводства.Видимость       = РедактированиеДополнительныхСведений И Не ЭтоПиво;
		Элементы.ТоварыДатаПроизводства.Видимость = РедактированиеДополнительныхСведений И Не ЭтоПиво;
		Элементы.СпособВводаВОборот.Видимость     = РедактированиеДополнительныхСведений И Не ЭтоПиво;
		
		Элементы.ГруппаВЕТИС.Видимость = РедактированиеВСД
			И ОтчетПроизводственнойЛинии И ЭтоПродукцияПодконтрольнаяВЕТИС;
		Элементы.ИдентификаторПроисхожденияВЕТИССтрокой.Видимость = Не Форма.ИспользуетсяПодсистемаВЕТИС;
		Элементы.ИдентификаторПроисхожденияВЕТИС.Видимость        = Форма.ИспользуетсяПодсистемаВЕТИС;
		Элементы.ТоварыИдентификаторПроисхожденияВЕТИС.Видимость = РедактированиеВСД
			И ЭтоПродукцияПодконтрольнаяВЕТИС И Не ОтчетПроизводственнойЛинии;
		
		Элементы.ГруппаМолочнаяПродукция.Видимость = ЭтоМолочнаяПродукция;
		Элементы.ГруппаСрокГодности.Видимость      = РедактированиеСроковГодности Или РедактированиеВСД;
		Элементы.ТоварыСрокГодности.Видимость      = РедактированиеСроковГодности Или РедактированиеВСД;
		
		Если РедактированиеСроковГодности Или РедактированиеВСД Тогда
			УстановитьФорматСрокГодности(Форма);
		КонецЕсли;
		
		Элементы.СтраницаОтчетПроизводственнойЛинии.Видимость = ОтчетПроизводственнойЛинии И Не ЭтоОтчетОПеревзвешивании;
		Элементы.СтраницаТовары.Видимость  = Не ОтчетПроизводственнойЛинии;
		
		ОтображатьИсходныеЗначенияКодов = 
			Не (Форма.СтатусИСМП = ПредопределенноеЗначение("Перечисление.СтатусыОбработкиУточненияСведенийОКодахМаркировкиИСМП.ПередачаКорректировкиСведенийКМОбработана")
				Или Форма.СтатусИСМП = ПредопределенноеЗначение("Перечисление.СтатусыОбработкиУточненияСведенийОКодахМаркировкиИСМП.ОтчетОПеревзвешиванииОбработан"));
		
		Элементы.ДанныеОтчетаПроизводственнойЛинииЗначениеШтрихкода.Видимость = ОтображатьИсходныеЗначенияКодов;
		Элементы.ДанныеОтчетаПроизводственнойЛинииФорматBase64.Видимость      = ОтображатьИсходныеЗначенияКодов;
		Элементы.ДанныеОтчетаПроизводственнойЛинииТекстОшибкиПодвал.Видимость = ОтображатьИсходныеЗначенияКодов;
		
		Элементы.ДанныеОтчетаПроизводственнойЛинииНормализованноеЗначениеШтрихкода.Видимость = Не ОтображатьИсходныеЗначенияКодов;
		
		Элементы.ГруппаПроизводственнаяПлощадкаВЕТИС.Видимость        = ЭтоБАД И РедактированиеДополнительныхСведений;
		Элементы.ИдентификаторПроизводственнойПлощадкиВЕТИС.Видимость = Не Форма.ИспользуетсяПодсистемаВЕТИС;
		Элементы.ПроизводственнаяПлощадкаВЕТИС.Видимость              = Форма.ИспользуетсяПодсистемаВЕТИС;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ВидПродукцииУказан()
	
	Если Не ЗначениеЗаполнено(Объект.ВидПродукции) Тогда
		
		ТекстСообщения = НСтр("ru = 'Не указан вид продукции'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура ОбновитьСтатусИСМП()
	
	МенеджерОбъекта = ИнтеграцияИС.МенеджерОбъектаПоСсылке(Объект.Ссылка);
	
	СтатусИСМП         = МенеджерОбъекта.СтатусПоУмолчанию();
	Реквизиты = Новый Структура("ВидПродукции, Операция");
	ЗаполнитьЗначенияСвойств(Реквизиты, Объект);
	
	СтатусПроверкиИПодбора = ПроверкаИПодборПродукцииИСМП.СтатусПроверкиИПодбораДокумента(
		Объект.Ссылка, Объект.ВидПродукции);
	
	ДальнейшееДействие = МенеджерОбъекта.ДальнейшееДействиеПоУмолчанию();
	
	Если СтатусПроверкиИПодбора = Перечисления.СтатусыПроверкиИПодбораИС.Выполняется Тогда
		
		ДальнейшееДействие = Новый Массив;
		
	ИначеЕсли ЗначениеЗаполнено(Объект.Ссылка)
		И ПараметрыОбновленияСтатуса = Неопределено Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Статусы.Статус КАК Статус,
		|	ВЫБОР
		|		КОГДА Статусы.ДальнейшееДействие1 В (&МассивДальнейшиеДействия)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюИСМП.НеТребуется)
		|		ИНАЧЕ Статусы.ДальнейшееДействие1
		|	КОНЕЦ КАК ДальнейшееДействие1,
		|	ВЫБОР
		|		КОГДА Статусы.ДальнейшееДействие2 В (&МассивДальнейшиеДействия)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюИСМП.НеТребуется)
		|		ИНАЧЕ Статусы.ДальнейшееДействие2
		|	КОНЕЦ КАК ДальнейшееДействие2,
		|	ВЫБОР
		|		КОГДА Статусы.ДальнейшееДействие3 В (&МассивДальнейшиеДействия)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюИСМП.НеТребуется)
		|		ИНАЧЕ Статусы.ДальнейшееДействие3
		|	КОНЕЦ КАК ДальнейшееДействие3
		|ИЗ
		|	РегистрСведений.СтатусыДокументовИСМП КАК Статусы
		|ГДЕ
		|	Статусы.Документ = &Документ");
		
		Запрос.УстановитьПараметр("Документ",                 Объект.Ссылка);
		Запрос.УстановитьПараметр("МассивДальнейшиеДействия", ИнтеграцияИСМП.НеотображаемыеВДокументахДальнейшиеДействия());
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		ИсключаемыеДействия = ИнтеграцияИСМП.НеотображаемыеВДокументахДальнейшиеДействия();
			
		Если Выборка.Следующий() Тогда
			
			СтатусИСМП = Выборка.Статус;
			
			ДальнейшееДействие = Новый Массив;
			Если ИсключаемыеДействия.Найти(Выборка.ДальнейшееДействие1) = Неопределено Тогда
				ДальнейшееДействие.Добавить(Выборка.ДальнейшееДействие1);
			КонецЕсли;
			Если ИсключаемыеДействия.Найти(Выборка.ДальнейшееДействие2) = Неопределено Тогда
				ДальнейшееДействие.Добавить(Выборка.ДальнейшееДействие2);
			КонецЕсли;
			Если ИсключаемыеДействия.Найти(Выборка.ДальнейшееДействие3) = Неопределено Тогда
				ДальнейшееДействие.Добавить(Выборка.ДальнейшееДействие3);
			КонецЕсли;
				
		КонецЕсли;
		
	ИначеЕсли ПараметрыОбновленияСтатуса <> Неопределено Тогда
		
		СтатусИСМП = ПараметрыОбновленияСтатуса.НовыйСтатус;
		
		ДальнейшееДействие = Новый Массив;
		ДальнейшееДействие.Добавить(ПараметрыОбновленияСтатуса.ДальнейшееДействие1);
		ДальнейшееДействие.Добавить(ПараметрыОбновленияСтатуса.ДальнейшееДействие2);
		ДальнейшееДействие.Добавить(ПараметрыОбновленияСтатуса.ДальнейшееДействие3);	
		
	КонецЕсли;
	
	ДопустимыеДействия = Новый Массив;
	ДопустимыеДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ПередайтеОтчет);
	ДопустимыеДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ПередайтеДанные);
	ДопустимыеДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ОтменитеОперацию);
	ДопустимыеДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ОтменитеПередачуДанных);

	СтатусПредставление = ИнтеграцияИСМП.ПредставлениеСтатуса(СтатусИСМП, ДальнейшееДействие, ДопустимыеДействия);
	
	РедактированиеФормыНеДоступно = СтатусИСМП <> Перечисления.СтатусыОбработкиУточненияСведенийОКодахМаркировкиИСМП.Черновик
	                              И СтатусИСМП <> Перечисления.СтатусыОбработкиУточненияСведенийОКодахМаркировкиИСМП.ОтчетОПеревзвешиванииОшибкаПередачи
	                              И СтатусИСМП <> Перечисления.СтатусыОбработкиУточненияСведенийОКодахМаркировкиИСМП.КорректировкаСведенийКМОшибкаПередачи;
	
	Если Не (Элементы.ТоварыСтатусПроверкиГосИС.Видимость ИЛИ РедактированиеФормыНедоступно) Тогда
		ПроверкаИПодборПродукцииИС.ЗаполнитьКешШтрихкодовУпаковок(ЭтаФорма);
		ПроверкаИПодборПродукцииИС.ПрименитьКешШтрихкодовУпаковок(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДальнейшиеДействия()
	
	Реквизиты = Новый Структура(
		"ВидПродукции, Операция");
	ЗаполнитьЗначенияСвойств(Реквизиты, Объект);
	
	ДальнейшиеДействия = Новый Массив;
	ДальнейшиеДействия.Добавить(Документы.УточнениеСведенийОКодахМаркировкиИСМП.ДальнейшееДействиеПоУмолчанию(Реквизиты));
	
	ПараметрыОбновленияСтатуса = РегистрыСведений.СтатусыДокументовИСМП.ВозвращаемоеЗначениеДальнейшиеДействияСтатус(
		СтатусИСМП,
		ДальнейшиеДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПриИзмененииОрганизацияПриЗавершении(РезультатВопроса, НоваяОрганизация) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Объект.Организация = НоваяОрганизация;
		ПриИзмененииОрганизации();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииОрганизации()
	
	ОчиститьДанныеДокумента(ЭтаФорма);
	Объект.ДокументОснование = Неопределено;
	ОбновлениеКешей();
	ОчиститьДанныеПроизводственнойПлощадкиВЕТИС(ЭтаФорма);
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПриИзмененииОтчетПроизводственнойЛинииЗавершение(РезультатВопроса, Параметры) Экспорт
	
	Если Не РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Объект.ОтчетПроизводственнойЛинии = Не Объект.ОтчетПроизводственнойЛинии;
		Возврат;
	КонецЕсли;
	
	Если Параметры.ОчиститьТовары Тогда
		Объект.Товары.Очистить();
		Объект.ШтрихкодыУпаковок.Очистить();
		Объект.ДокументыСертификации.Очистить();
	КонецЕсли;
	
	Если Параметры.ОчиститьДанныеОтчетаПроизводственнойЛинии Тогда
		
		Объект.ДанныеОтчетаПроизводственнойЛинии.Очистить();
		ОчиститьДанныеСертификацииВШапке(ЭтаФорма);
		
	КонецЕсли;
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма, "ОтчетПроизводственнойЛинии");
	НастроитьЭлементыФормы(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусПредставлениеОбработкаНавигационнойСсылкиЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если Не РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;

	Если ПроверитьЗаполнение() Тогда
		Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
		РазблокироватьДанныеФормыДляРедактирования();
	КонецЕсли;

	Если Не Модифицированность И Объект.Проведен Тогда
		ОбработатьНажатиеНавигационнойСсылки(ДополнительныеПараметры.НавигационнаяСсылкаФорматированнойСтроки);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьНажатиеНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ПередатьДанные" Тогда
		
		ПараметрыОбработкиДокументов = ИнтеграцияИСМПСлужебныйКлиентСервер.ПараметрыОбработкиДокументов();
		ПараметрыОбработкиДокументов.Ссылка             = Объект.Ссылка;
		ПараметрыОбработкиДокументов.Организация        = Объект.Организация;
		ПараметрыОбработкиДокументов.ДальнейшееДействие = ПредопределенноеЗначение("Перечисление.ДальнейшиеДействияПоВзаимодействиюИСМП.ПередайтеДанные");
		
		ОписаниеПриЗавершении = Новый ОписаниеОповещения(
			"Подключаемый_ПриЗавершенииОперации", ЭтаФорма, ПараметрыОбработкиДокументов);
			
		ИнтеграцияИСМПКлиент.ПодготовитьКПередаче(
			ЭтаФорма,
			ПараметрыОбработкиДокументов,
			ОписаниеПриЗавершении);
			
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ПередатьОтчет" Тогда
		
		ПараметрыОбработкиДокументов = ИнтеграцияИСМПСлужебныйКлиентСервер.ПараметрыОбработкиДокументов();
		ПараметрыОбработкиДокументов.Ссылка             = Объект.Ссылка;
		ПараметрыОбработкиДокументов.Организация        = Объект.Организация;
		ПараметрыОбработкиДокументов.ДальнейшееДействие = ПредопределенноеЗначение("Перечисление.ДальнейшиеДействияПоВзаимодействиюИСМП.ПередайтеОтчет");
		
		ОписаниеПриЗавершении = Новый ОписаниеОповещения(
			"Подключаемый_ПриЗавершенииОперации", ЭтаФорма, ПараметрыОбработкиДокументов);
		
		ИнтеграцияИСМПКлиент.ПодготовитьКПередаче(
			ЭтаФорма,
			ПараметрыОбработкиДокументов,
			ОписаниеПриЗавершении);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОтменитьОперацию" Тогда
		
		ИнтеграцияИСМПКлиент.ОтменитьПоследнююОперацию(Объект.Ссылка);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОтменитьПередачуДанных" Тогда
		
		ИнтеграцияИСМПКлиент.ОтменитьПередачу(Объект.Ссылка);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ПоказатьПричинуОшибки" Тогда
		
		ПараметрыОткрытияФормы = Новый Структура;
		ПараметрыОткрытияФормы.Вставить("Документ", Объект.Ссылка);
		
		ОткрытьФорму(
			"Справочник.ИСМППрисоединенныеФайлы.Форма.ФормаОшибки",
			ПараметрыОткрытияФормы,
			ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаРезультатаПодбораНоменклатуры(ВыбранноеЗначение, КэшированныеЗначения)
	
	ПараметрыЗаполнения = ИнтеграцияИС.ПараметрыЗаполненияТабличнойЧастиТовары(Объект.ВидПродукции);
	ПараметрыЗаполнения.Вставить("ПараметрыУказанияСерий", ПараметрыУказанияСерий);
	
	ДобавленныеСтроки = Новый Массив;
	
	СобытияФормИСМППереопределяемый.ОбработкаРезультатаПодбораНоменклатуры(
		ЭтаФорма, ВыбранноеЗначение, ПараметрыЗаполнения, КэшированныеЗначения, ДобавленныеСтроки);
	
	ИнтеграцияИСПереопределяемый.ЗаполнитьСлужебныеРеквизитыВКоллекции(ЭтаФорма, Объект.Товары);
	ИнтеграцияИСПереопределяемый.ЗаполнитьСтатусыУказанияСерий(Объект, ПараметрыУказанияСерий);
	
	ПроверкаИПодборПродукцииИС.ПрименитьКешШтрихкодовУпаковок(ЭтаФорма,,,КлючСвязиСтатусаСтрок(ЭтаФорма));
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДанныеОтчетаПроизводственнойЛинииЗначениеШтрихкода.Имя);
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДанныеОтчетаПроизводственнойЛинииНормализованноеЗначениеШтрихкода.Имя);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Объект.ДанныеОтчетаПроизводственнойЛинии.ТекстОшибкиЗначениеШтрихкода");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Заполнено;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаПроблемаГосИС);
	
	СобытияФормИСПереопределяемый.УстановитьУсловноеОформлениеЕдиницИзмерения(ЭтаФорма);
	СобытияФормИСПереопределяемый.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(ЭтаФорма);
	СобытияФормИСПереопределяемый.УстановитьУсловноеОформлениеСерийНоменклатуры(ЭтаФорма);
	
	// Коды отдельно от количества
	ИнтеграцияИСМП.УстановитьУсловноеОформлениеПолейКоличества(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПриИзмененииОперацииЗавершение(РезультатВопроса, Действия) Экспорт
	
	Если Не РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Объект.Операция = ПредыдущаяОперация;
		Возврат;
	КонецЕсли;
	
	Если Действия.ОчиститьТабличныеЧасти Тогда
		ОчиститьДанныеДокумента(ЭтаФорма);
	КонецЕсли;

	УстановитьДоступностьРежимаОтчетПроизводственнойЛинииПоВидуОперации(ЭтаФорма);
	ОперацияПриИзмененииНаСервере();
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма, "Операция");
	НастроитьЭлементыФормы(ЭтаФорма);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОчиститьДанныеДокумента(Форма)
	
	Объект = Форма.Объект;
	
	Объект.ДанныеОтчетаПроизводственнойЛинии.Очистить();
	Объект.ДокументыСертификации.Очистить();
	Объект.Товары.Очистить();
	Объект.ШтрихкодыУпаковок.Очистить();
	
	ОчиститьДанныеСертификацииВШапке(Форма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОчиститьДанныеСертификацииВШапке(Форма)
	
	Объект = Форма.Объект;
	
	Объект.ВидДокументаСертификации   = Неопределено;
	Объект.НомерДокументаСертификации = "";
	Объект.ДатаДокументаСертификации  = "";
	Форма.Сертификация                = "";
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьРежимаОтчетПроизводственнойЛинииПоВидуОперации(Форма)
	
	Если Форма.Объект.Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ОтчетОПеревзвешивании")
		Или Форма.Объект.Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.КорректировкаСведенийКМФактическийВес") Тогда
		Форма.Объект.ОтчетПроизводственнойЛинии = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОперацияПриИзмененииНаСервере()
	
	ОбновитьДальнейшиеДействия();
	ОбновитьСтатусИСМП();
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПриИзмененииВидаПродукцииПриЗавершении(РезультатВопроса, НовыйВидПродукции) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Или РезультатВопроса = Истина Тогда
		
		Объект.ВидПродукции = НовыйВидПродукции;
		
		ОчиститьДанныеДокумента(ЭтаФорма);
		Объект.ДокументОснование = Неопределено;
		
		ПриИзмененииВидПродукцииНаСервере(РезультатВопроса = Истина);
		ПриИзмененииВидПродукцииНаКлиенте();
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииВидПродукцииНаКлиенте()
	
	СобытияФормИСМПКлиент.ВидПродукцииПриИзменении(ЭтаФорма, Элементы.ВидПродукции);
	СобытияФормИСКлиентСерверПереопределяемый.УстановитьПараметрыВыбораНоменклатуры(ЭтаФорма, Объект.ВидПродукции);
	
КонецПроцедуры

&НаСервере
Процедура ОбновлениеКешей()
	
	Если Не РедактированиеФормыНедоступно Тогда
		ПроверкаИПодборПродукцииИС.ЗаполнитьКешШтрихкодовУпаковок(ЭтаФорма);
		ПроверкаИПодборПродукцииИС.ПрименитьКешШтрихкодовУпаковок(ЭтаФорма,,,КлючСвязиСтатусаСтрок(ЭтаФорма));
	КонецЕсли;
	ШтрихкодированиеИС.ОбновитьКэшМаркируемойПродукции(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииВидПродукцииНаСервере(ПерезаполнитьПоОснованию)
	
	ИнтеграцияИСМП.НастроитьВидимостьКолонкиКоличестваКодовМаркировки(ЭтаФорма);
	
	ЗаполнитьДоступныеОперации();
	Если Элементы.Операция.СписокВыбора.НайтиПоЗначению(Объект.Операция) = Неопределено Тогда
		Объект.Операция = Элементы.Операция.СписокВыбора.Получить(0).Значение;
	КонецЕсли;
	
	УстановитьДоступностьРежимаОтчетПроизводственнойЛинииПоВидуОперации(ЭтаФорма);
	ЗаполнитьДоступныеВидыДокументовСертификации();
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма, "ВидПродукции");
	
	Если Элементы.Операция.СписокВыбора.НайтиПоЗначению(Объект.Операция) = Неопределено Тогда
		Объект.Операция = Элементы.Операция.СписокВыбора.Получить(0).Значение;
	КонецЕсли;
	
	ОбновитьДальнейшиеДействия();
	
	Если ПерезаполнитьПоОснованию И ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		ПерезаполнитьПоОснованиюСервер();
	КонецЕсли;
	
	НастроитьЭлементыФормы(ЭтаФорма);
	
	ОбновитьИнформациюОткрытияФормыПроверкиПодбора();
	ОбновлениеКешей();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЗаписатьПараметрыОбновленияСтатуса(Отказ, ТекущийОбъект)
	
	Если ПараметрыОбновленияСтатуса = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РегистрыСведений.СтатусыДокументовИСМП.ОбновитьСтатус(
		ТекущийОбъект.Ссылка,
		ПараметрыОбновленияСтатуса);
	
	ПараметрыОбновленияСтатуса = Неопределено;
	
КонецПроцедуры

#Область Сертификация

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьПредставлениеСертификации(Форма)
	
	Объект = Форма.Объект;
	Если ЗначениеЗаполнено(Объект.ВидДокументаСертификации) Тогда
		
		ДанныеЗаполненияСертификации = ИнтеграцияИСМПКлиентСервер.ИнициализироватьДанныеЗаполненияСертификации();
		ДанныеЗаполненияСертификации.ВидСертификации   = Объект.ВидДокументаСертификации;
		ДанныеЗаполненияСертификации.ДатаСертификации  = Объект.ДатаДокументаСертификации;
		ДанныеЗаполненияСертификации.НомерСертификации = Объект.НомерДокументаСертификации;
		ДанныеЗаполненияСертификации.ИспользоватьНомерСкважины = Ложь;
		Форма.Сертификация = ИнтеграцияИСМПКлиентСервер.ПредставлениеСертификации(ДанныеЗаполненияСертификации);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнениеСертификацииЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт

	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеЗаполненияСертификации = ИнтеграцияИСМПКлиентСервер.ИнициализироватьДанныеЗаполненияСертификации();
	ЗаполнитьЗначенияСвойств(ДанныеЗаполненияСертификации, РезультатВыбора);
	ДанныеЗаполненияСертификации.ИспользоватьНомерСкважины = Ложь;
	
	Объект.ВидДокументаСертификации   = РезультатВыбора.ВидСертификации;
	Объект.НомерДокументаСертификации = РезультатВыбора.НомерСертификации;
	Объект.ДатаДокументаСертификации  = РезультатВыбора.ДатаСертификации;
	
	Сертификация = ИнтеграцияИСМПКлиентСервер.ПредставлениеСертификации(ДанныеЗаполненияСертификации);
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДоступныеВидыДокументовСертификации()
	
	ИнтеграцияИСМП.ЗаполнитьДоступныеВидыДокументовСертификации(
		Элементы.ДокументыСертификацииВидДокументаСертификации, Объект.ВидПродукции);
	
	Если Элементы.ДокументыСертификацииВидДокументаСертификации.СписокВыбора.Количество() = 1 Тогда
		ВидДокументаСертификации = Элементы.ДокументыСертификацииВидДокументаСертификации.СписокВыбора.Получить(0).Значение;
		Элементы.ДокументыСертификацииВидДокументаСертификации.ПропускатьПриВводе = Истина;
	Иначе
		ВидДокументаСертификации = Перечисления.ВидыДокументовОбязательнойСертификацииИС.ПустаяСсылка();
		Элементы.ДокументыСертификацииВидДокументаСертификации.ПропускатьПриВводе = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСТСД

#Область ЗагрузкаИзТСД

&НаКлиенте
Процедура ЗагрузитьИзТСДПослеАвторизации(РезультатАвторизации, Штрихкоды) Экспорт
	
	Подключаемый_ПолученыДанныеИзТСД(Штрихкоды, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолученыДанныеИзТСД(Штрихкоды, АвторизацияЗапрашивалась = Неопределено) Экспорт
	
	Если Штрихкоды.Количество() = 0 Тогда
		
		ПоказатьПредупреждение( ,НСтр("ru = 'В полученных данных не содержится информации о считанных штриховых кодах'"));
		Возврат;
		
	КонецЕсли;
	
	ШтрихкодированиеИСКлиент.ОповеститьОНачалеОбработкиДанныхТСД();
	ПараметрыСканирования = ШтрихкодированиеИСКлиент.ПараметрыСканирования(ЭтаФорма);
	ШтрихкодированиеИСКлиент.ПреобразоватьШтрихкодыТСДВBase64(Штрихкоды);
	ЗагрузкаДанныхТСД = ОбработатьПолученныеДанныеТСДНаСервере(Штрихкоды, ПараметрыСканирования, КэшированныеЗначения);
	
	Если ЗагрузкаДанныхТСД.ТребуетсяАвторизация Тогда
		
		Если АвторизацияЗапрашивалась = Истина Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Загрузка данных недоступна: авторизация в сервисе не пройдена'"));
			ЗагрузкаДанныхТСД = Неопределено;
			Возврат;
		КонецЕсли;
		
		ИнтерфейсАвторизацииИСМПКлиент.ЗапроситьКлючСессии(
			ИнтерфейсАвторизацииИСМПКлиент.ПараметрыЗапросаКлючаСессии(Объект.Организация, Объект.ВидПродукции),
			Новый ОписаниеОповещения("ЗагрузитьИзТСДПослеАвторизации", ЭтаФорма, Штрихкоды));
		Возврат;
		
	КонецЕсли;
	
	Если ЗагрузкаДанныхТСД.ОбщаяОшибка Тогда
		ПараметрыОткрытияФормы = ШтрихкодированиеИСКлиент.ПараметрыОткрытияФормыНевозможностиДобавленияОтсканированного();
		ПараметрыОткрытияФормы.ТекстОшибки = ЗагрузкаДанныхТСД.ТекстОбщейОшибки;
		ШтрихкодированиеИСКлиент.ОткрытьФормуНевозможностиДобавленияОтсканированного(ЭтаФорма, ПараметрыОткрытияФормы);
		ЗагрузкаДанныхТСД = Неопределено;
		Возврат;
	КонецЕсли;
	
	Если ЗагрузкаДанныхТСД.ШтрихкодыДляСопоставления.Количество() > 0 Тогда
		
		ДополнительныеПараметры = Новый Структура("ВсеШтрихкоды, ШтрихкодыДляСопоставления",
			ЗагрузкаДанныхТСД.ШтрихкодыТСД, ЗагрузкаДанныхТСД.ШтрихкодыДляСопоставления);
		ОписаниеОповещения = Новый ОписаниеОповещения("СопоставлениеШтрихкодовЗавершение", ЭтаФорма, ДополнительныеПараметры);
		ШтрихкодированиеИСКлиентПереопределяемый.ОткрытьФормуПодбораНоменклатурыПоШтрихкодам(
			ЗагрузкаДанныхТСД.ШтрихкодыДляСопоставления, ЭтаФорма, ОписаниеОповещения);
			
		Возврат;
	КонецЕсли;
	
	Если ЗагрузкаДанныхТСД.ЕстьОшибкиВДереве Тогда
		
		ПараметрыОткрытияФормы = ШтрихкодированиеИСКлиент.ПараметрыОткрытияФормыНевозможностиДобавленияОтсканированного(Объект.ВидПродукции);
		ПараметрыОткрытияФормы.АдресДереваУпаковок = ЗагрузкаДанныхТСД.АдресДереваУпаковок; 
		ШтрихкодированиеИСКлиент.ОткрытьФормуНевозможностиДобавленияОтсканированного(ЭтаФорма, ПараметрыОткрытияФормы);
		
	КонецЕсли;
	
	ОбработатьПолученныеДанныеТСД();
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставлениеШтрихкодовЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ШтрихкодированиеИСВызовСервера.ОчиститьОтложенныеКодыМаркировки(КэшМаркируемойПродукции);
		Возврат;
	КонецЕсли;
	
	Если Результат.НайденыНезарегистрированныеТовары Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("СопоставлениеШтрихкодовЗавершение", ЭтаФорма, ДополнительныеПараметры);
		ШтрихкодированиеИСКлиентПереопределяемый.ОткрытьФормуПодбораНоменклатурыПоШтрихкодам(
			Результат.ОтложенныеТовары, ЭтаФорма, ОписаниеОповещения);
		Возврат;
	КонецЕсли;
	
	Подключаемый_ПолученыДанныеИзТСД(ДополнительныеПараметры.ВсеШтрихкоды);
	
КонецПроцедуры

&НаСервере
Функция ОбработатьПолученныеДанныеТСДНаСервере(ШтрихкодыТСД, ПараметрыСканирования, КэшированныеЗначения)
	
	Результат = ГрупповаяОбработкаШтрихкодовИС.ОбработатьПолученныеДанныеТСДВДокументе(ЭтаФорма, ШтрихкодыТСД, ПараметрыСканирования);
	
	Если Результат.ТребуетсяАвторизация
		Или Результат.ОбщаяОшибка
		Или Результат.ШтрихкодыДляСопоставления.Количество() Тогда
		Возврат Результат;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	ПослеОбработкиШтрихкодовСервер(Результат, КэшированныеЗначения);
	ОбновлениеКешей();
	Результат.ДобавленныеСтроки = Новый Массив;
	Результат.ИзмененныеСтроки  = Новый Массив;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьПолученныеДанныеТСД()
	
	Если ЗагрузкаДанныхТСД = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если ЗагрузкаДанныхТСД.Обработано = ЗагрузкаДанныхТСД.Всего Тогда
		ШтрихкодированиеИСКлиент.ОповеститьОбОкончанииОбработкиДанныхТСД();
		ЗагрузкаДанныхТСД = Неопределено;
	Иначе
		Штрихкод = ЗагрузкаДанныхТСД.ШтрихкодыТСД[ЗагрузкаДанныхТСД.Обработано];
		ЗагрузкаДанныхТСД.Обработано = ЗагрузкаДанныхТСД.Обработано + 1;
		
		Если ЗначениеЗаполнено(Штрихкод.Штрихкод) Тогда
			
			ОбработатьКодМаркировки(Штрихкод.РезультатОбработки.ДанныеШтрихкода);
			
		Иначе
			
			ОбработатьОчереднойШтрихкод();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОчереднойШтрихкод()
	ПодключитьОбработчикОжидания("ОбработатьПолученныеДанныеТСД", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОчереднойШтрихкодОбработчикОповещения(Результат, ДополнительныеПараметры) Экспорт
	ОбработатьОчереднойШтрихкод();
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область Штрихкодирование

&НаКлиенте
Процедура ОбработатьКодМаркировки(ИсходныеДанные, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ИсходныеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыСканирования = ШтрихкодированиеИСКлиент.ПараметрыСканирования(ЭтаФорма);
	
	ШтрихкодированиеИСКлиентСервер.ЗакодироватьШтрихкодДанныхBase64(ИсходныеДанные);
	
	РезультатОбработки = ОбработатьВводШтрихкода(ИсходныеДанные, КэшированныеЗначения, ПараметрыСканирования);
	
	ПараметрыЗавершенияВводаШтрихкода = ПараметрыЗавершенияВводаШтрихкода(ИсходныеДанные, РезультатОбработки, ПараметрыСканирования);
	ШтрихкодированиеИСКлиент.ЗавершитьОбработкуШтрихкода(ПараметрыЗавершенияВводаШтрихкода);
	
КонецПроцедуры

&НаСервере
Функция ОбработатьВводШтрихкода(ШтрихкодКоличество, КэшированныеЗначения, ПараметрыСканирования) //ОбработатьВводШтрихкода
	
	РезультатОбработкиШтрихкода = ШтрихкодированиеИС.ОбработатьВводШтрихкода(
		ЭтаФорма, ШтрихкодКоличество, КэшированныеЗначения, ПараметрыСканирования);
	
	ПослеОбработкиШтрихкодовСервер(РезультатОбработкиШтрихкода, КэшированныеЗначения);
	
	РезультатОбработкиШтрихкода.ИзмененныеСтроки  = Новый Массив;
	РезультатОбработкиШтрихкода.ДобавленныеСтроки = Новый Массив;
	
	Возврат РезультатОбработкиШтрихкода;
	
КонецФункции

&НаКлиенте
Функция ОписаниеОповещенияОбработкиКодаМаркировки()
	
	Возврат Новый ОписаниеОповещения("ОбработатьКодМаркировки", ЭтаФорма);
	
КонецФункции

&НаКлиенте
Процедура ПослеОбработкиШтрихкодов(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		Если ДанныеВыбораПоМаркируемойПродукции<>Неопределено Тогда
			КлючиСвязи = Новый Структура(КлючСвязиСтатусаСтрок(ЭтаФорма, ДанныеВыбораПоМаркируемойПродукции.Номенклатура));
			КлючиСвязи.Вставить("Номенклатура");
			КлючиСвязи.Вставить("Характеристика");
			КлючиСвязи.Вставить("Серия");
			ЗаполнитьЗначенияСвойств(КлючиСвязи, ДанныеВыбораПоМаркируемойПродукции);
			Если ИнтеграцияИСКлиентСервер.ЭтоПродукцияПодконтрольнаяВЕТИС(Объект.ВидПродукции)
				И Объект.Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.КорректировкаСведенийКМСрокГодностиВСД") Тогда
				ДанныеВыбораПоМаркируемойПродукции.Свойство("ГоденДо", КлючиСвязи.СрокГодности);
			КонецЕсли;
			СтрокиТовар = Объект.Товары.НайтиСтроки(КлючиСвязи);
			Если СтрокиТовар.Количество() И СтрокиТовар[0].СтатусПроверкиГосИС = 1 Тогда
				ШтрихкодированиеИСКлиентСервер.ОбработатьСохраненныйВыборДанныхПоМаркируемойПродукции(ЭтаФорма,,Ложь);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ОбработатьОчереднойШтрихкод();
	
КонецПроцедуры

&НаКлиенте
Функция ПараметрыЗавершенияВводаШтрихкода(ИсходныеДанные, РезультатОбработки, ПараметрыСканирования)
	
	ПараметрыЗавершенияВводаШтрихкода = ШтрихкодированиеИСКлиент.ПараметрыЗавершенияОбработкиШтрихкода();
	ПараметрыЗавершенияВводаШтрихкода.РезультатОбработкиШтрихкода   = РезультатОбработки;
	ПараметрыЗавершенияВводаШтрихкода.Форма                         = ЭтаФорма;
	ПараметрыЗавершенияВводаШтрихкода.ПараметрыСканирования         = ПараметрыСканирования;
	ПараметрыЗавершенияВводаШтрихкода.ДанныеШтрихкода               = ИсходныеДанные;
	
	Возврат ПараметрыЗавершенияВводаШтрихкода;
	
КонецФункции

&НаКлиенте
Процедура ОбработкаКодаМаркировкиВыполнитьДействие(ДанныеДляВыполненияДействия, ДополнительныеПараметры) Экспорт
	
	РезультатВыбора             = ДанныеДляВыполненияДействия.РезультатВыбора;
	РезультатОбработкиШтрихкода = ДанныеДляВыполненияДействия.РезультатОбработкиШтрихкода;
	КэшированныеЗначения        = ДанныеДляВыполненияДействия.КэшированныеЗначения;
	ПараметрыСканирования       = ДанныеДляВыполненияДействия.ПараметрыСканирования;
	
	Действие = ДанныеДляВыполненияДействия.Действие;
	РезультатОбработкиШтрихкода = ОбработкаКодаМаркировкиВыполнитьДействиеСервер(Действие, РезультатВыбора, РезультатОбработкиШтрихкода, ПараметрыСканирования, КэшированныеЗначения);
	
	ПараметрыЗавершенияВводаШтрихкода = ПараметрыЗавершенияВводаШтрихкода(
		ДанныеДляВыполненияДействия.ИсходныеДанные, РезультатОбработкиШтрихкода, ДанныеДляВыполненияДействия.ПараметрыСканирования);
	ШтрихкодированиеИСКлиент.ЗавершитьОбработкуШтрихкода(ПараметрыЗавершенияВводаШтрихкода);
	
	Если ПараметрыСканирования.ВидОперацииИСМП = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ОтчетОПеревзвешивании")
		И РезультатВыбора.Свойство("ДополнитьПолныйКодМаркировки")
		И РезультатВыбора.ДополнитьПолныйКодМаркировки = "3103" Тогда
		ПечатьЭтикетокИСМПКлиент.РаспечататьСуществующийКодМаркировки(
			РезультатВыбора.ДанныеВыбора,
			РезультатВыбора.ДополнитьПолныйКодМаркировки);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОбработкаКодаМаркировкиВыполнитьДействиеСервер(Действие, РезультатВыбора, РезультатОбработкиШтрихкода, ПараметрыСканирования, КэшированныеЗначения)
	
	ПараметрыОбработкиВыбора    = ШтрихкодированиеИС.ИнициализироватьПараметрыОбработкиВыбора(РезультатВыбора, РезультатОбработкиШтрихкода, КэшированныеЗначения);
	РезультатОбработкиШтрихкода = ШтрихкодированиеИС.ВыполнитьДействие(ЭтаФорма, Действие, ПараметрыОбработкиВыбора);
	
	ПослеОбработкиШтрихкодовСервер(РезультатОбработкиШтрихкода, КэшированныеЗначения);
	
	РезультатОбработкиШтрихкода.ИзмененныеСтроки  = Новый Массив;
	РезультатОбработкиШтрихкода.ДобавленныеСтроки = Новый Массив;
	
	Возврат РезультатОбработкиШтрихкода;
	
КонецФункции

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ОткрытьФормуУточненияДанных()
	
	Если ШтрихкодированиеИСКлиент.НачатьПолучениеВесаАвтоматически(ЭтаФорма) Тогда
		Возврат;
	КонецЕсли;
	
	ШтрихкодированиеИСКлиент.Подключаемый_ОткрытьФормуУточненияДанных(ЭтаФорма, ОписаниеОповещенияОбработкиКодаМаркировки());
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьВесЗавершение(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыполнения = Неопределено
		Или Не РезультатВыполнения.Результат Тогда
		ШтрихкодированиеИСКлиент.Подключаемый_ОткрытьФормуУточненияДанных(ЭтаФорма, ОписаниеОповещенияОбработкиКодаМаркировки());
		Возврат;
	КонецЕсли;
	
	КодМаркировкиДляУточнения.РезультатОбработкиШтрихкода.ДанныеШтрихкода.Количество = РезультатВыполнения.Вес;
	КодМаркировкиДляУточнения.РезультатОбработкиШтрихкода.ДанныеШтрихкода.ТребуетсяОбновлениеШтрихкода = Истина;
	
	Если РезультатВыполнения.Вес = 0
		Или ВесПредыдущегоВзвешивания = 0
		Или РезультатВыполнения.Вес = ВесПредыдущегоВзвешивания
		Или ШтрихкодированиеИСМПКлиентСервер.ТребуетсяУточнениеДанных(КодМаркировкиДляУточнения) Тогда
		
		Если РезультатВыполнения.Вес <> 0 Тогда
			ВесПредыдущегоВзвешивания = РезультатВыполнения.Вес;
		КонецЕсли;
		ШтрихкодированиеИСКлиент.Подключаемый_ОткрытьФормуУточненияДанных(ЭтаФорма, ОписаниеОповещенияОбработкиКодаМаркировки());
		
	Иначе
		
		ВесПредыдущегоВзвешивания = РезультатВыполнения.Вес;
		ДанныеШтрихкода = КодМаркировкиДляУточнения.РезультатОбработкиШтрихкода.ДанныеШтрихкода;
		
		КодМаркировкиДляУточнения = Неопределено;
		ВыполнитьОбработкуОповещения(ОписаниеОповещенияОбработкиКодаМаркировки(), ДанныеШтрихкода);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ОбновитьКэшМаркируемойПродукции()
	
	ОбновитьКэшМаркируемойПродукцииСервер();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьКэшМаркируемойПродукцииСервер()
	
	ШтрихкодированиеИС.ОбновитьКэшМаркируемойПродукции(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПослеОбработкиШтрихкодовСервер(РезультатОбработкиШтрихкода, КэшированныеЗначения)
	
	ПроверкаИПодборПродукцииИС.ПрименитьКешШтрихкодовУпаковок(ЭтаФорма,,,КлючСвязиСтатусаСтрок(ЭтаФорма));
	ШтрихкодированиеИС.ОбновитьКэшМаркируемойПродукции(ЭтаФорма);
	
	ИзмененныеСтроки  = РезультатОбработкиШтрихкода.ИзмененныеСтроки;
	ДобавленныеСтроки = РезультатОбработкиШтрихкода.ДобавленныеСтроки;
	
	Если ДобавленныеСтроки.Количество() = 0 И ИзмененныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ДобавленнаяСтрока Из ДобавленныеСтроки Цикл
		
		СобытияФормИСМППереопределяемый.ПриИзмененииНоменклатуры(
			ЭтаФорма, ДобавленнаяСтрока, Неопределено, ПараметрыУказанияСерий);
		
	КонецЦикла;
	
	Для Каждого ИзмененнаяСтрока Из ИзмененныеСтроки Цикл
		
		СобытияФормИСМППереопределяемый.ПриИзмененииКоличестваЕдиниц(
			ЭтаФорма, ИзмененнаяСтрока, Неопределено, ПараметрыУказанияСерий);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ТоварыПослеУдаленияСервер()
	
	ПрименитьКешШтрихкодовУпаковок();
	ШтрихкодированиеИС.ОбновитьКэшМаркируемойПродукции(ЭтаФорма);
	
КонецПроцедуры

#Область КэшСтроки

&НаСервере
Процедура ПрименитьКешШтрихкодовУпаковок()
	
	Если Не РедактированиеФормыНедоступно Тогда
		ПроверкаИПодборПродукцииИС.ПрименитьКешШтрихкодовУпаковок(ЭтаФорма, , Истина, КлючСвязиСтатусаСтрок(ЭтаФорма));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ИнициализироватьКэшСтроки(Форма)
	
	Форма.ДанныеКешаСтроки = Новый Структура;
	Форма.ДанныеКешаСтроки.Вставить("Номенклатура");
	Форма.ДанныеКешаСтроки.Вставить("Характеристика");
	Форма.ДанныеКешаСтроки.Вставить("Серия");
	Форма.ДанныеКешаСтроки.Вставить("GTIN");
	Форма.ДанныеКешаСтроки.Вставить("ИдентификаторПроисхожденияВЕТИС");
	Форма.ДанныеКешаСтроки.Вставить("СрокГодности");
	Форма.ДанныеКешаСтроки.Вставить("СкоропортящаясяПродукция");
	
КонецПроцедуры

#КонецОбласти

#Область ФормаПроверкиИПодбора

&НаСервере
Процедура ОбновитьИнформациюОткрытияФормыПроверкиПодбора()
	
	Элементы.ГруппаСканированиеИПроверкаМаркируемойПродукции.Видимость = Истина;
	
	ТекстНадписи = "";
	
	СтатусПроверкиИПодбора = ПроверкаИПодборПродукцииИСМП.СтатусПроверкиИПодбораДокумента(
		Объект.Ссылка, Объект.ВидПродукции);
	
	Если СтатусПроверкиИПодбора = Перечисления.СтатусыПроверкиИПодбораИС.Выполняется Тогда
		
		Если ПравоИзменения Тогда
			
			ТекстНадписи = НСтр("ru = 'Продолжить подбор и проверку маркированной продукции'");
			Элементы.ГруппаИнформацияОСканированииВДругойФорме.Видимость = Истина;
			
		Иначе
			
			ТекстНадписи = НСтр("ru = 'Промежуточные результаты подбора маркированной продукции'");
			Элементы.ГруппаИнформацияОСканированииВДругойФорме.Видимость = Ложь;
			
		КонецЕсли;
		
	ИначеЕсли СтатусИСМП = Перечисления.СтатусыОбработкиУточненияСведенийОКодахМаркировкиИСМП.Черновик
		Или СтатусИСМП = Перечисления.СтатусыОбработкиУточненияСведенийОКодахМаркировкиИСМП.ОтчетОПеревзвешиванииОшибкаПередачи
		Или СтатусИСМП = Перечисления.СтатусыОбработкиУточненияСведенийОКодахМаркировкиИСМП.КорректировкаСведенийКМОшибкаПередачи Тогда
		
		Если СтатусПроверкиИПодбора = Перечисления.СтатусыПроверкиИПодбораИС.Завершено Тогда
			
			ТекстНадписи = НСтр("ru = 'Результаты подбора маркированной продукции'");
			Элементы.ГруппаИнформацияОСканированииВДругойФорме.Видимость = Ложь;
			
		ИначеЕсли ПравоИзменения Тогда
			
			ТекстНадписи = НСтр("ru = 'Подобрать и проверить маркированную продукцию'");
			Элементы.ГруппаИнформацияОСканированииВДругойФорме.Видимость = Ложь;
			
		Иначе
			
			Элементы.ГруппаСканированиеИПроверкаМаркируемойПродукции.Видимость = Ложь;
			
		КонецЕсли;
		
	ИначеЕсли СтатусПроверкиИПодбора = Перечисления.СтатусыПроверкиИПодбораИС.ПустаяСсылка() Тогда
		
		Элементы.ГруппаСканированиеИПроверкаМаркируемойПродукции.Видимость = Ложь;
		
	Иначе
		
		ТекстНадписи = НСтр("ru = 'Результаты подбора маркированной продукции'");
		Элементы.ГруппаИнформацияОСканированииВДругойФорме.Видимость = Ложь;
		
	КонецЕсли;
	
	ТекстГиперссылки = ПроверкаИПодборПродукцииИСМПКлиентСервер.НавигационнаяСсылкаОткрытьФормуПроверкиПродукцииИСМП();
	
	СтрокаОткрытиеФормыСканирования = Новый ФорматированнаяСтрока(ТекстНадписи,
		Новый Шрифт(,,,,Истина), ЦветаСтиля.ЦветГиперссылкиГосИС,, ТекстГиперссылки);
	
	Элементы.ДекорацияОтсканироватьПроверитьМаркируемуюПродукцию.Заголовок = СтрокаОткрытиеФормыСканирования;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ЗагрузитьИзТСДЗавершение(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	
	СобытияФормИСМПКлиентПереопределяемый.ПриПолученииДанныхИзТСД(
		Новый ОписаниеОповещения("Подключаемый_ПолученыДанныеИзТСД", ЭтаФорма),
		ЭтаФорма, РезультатВыполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПослеВыбораОснования(ДанныеВыбора, ДополнительныеПараметры) Экспорт
	
	Если ДанныеВыбора = Элементы.ДокументОснование.ДоступныеТипы.ПривестиЗначение(ДанныеВыбора) Тогда
		Объект.ДокументОснование = ДанныеВыбора;
		Модифицированность       = Истина;
	КонецЕсли;
	
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
		И ДополнительныеПараметры.Свойство("ОбработатьПерезаполнение") Тогда
		ОбработчикПерезаполненияПоОснованию(Ложь);
	КонецЕсли;
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма, "ДокументОснование");
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработкаРезультатаПодбораНоменклатуры(Результат, ДополнительныеПараметры) Экспорт
	
	ОбработкаРезультатаПодбораНоменклатуры(Результат, КэшированныеЗначения);
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикПерезаполненияПоОснованию(ЗадаватьВопрос = Истина)
	
	ОчиститьСообщения();
	
	Если Объект.Товары.Количество() > 0 И ЗадаватьВопрос Тогда
		
		ТекстВопроса = НСтр("ru = 'Данные документа будут перезаполнены. Продолжить?'");
		ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ВопросОПерезаполнениииПоОснованиюПриЗавершении", ЭтаФорма);
		ПоказатьВопрос(ОписаниеОповещенияОЗавершении, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		
		ПерезаполнитьПоОснованиюСервер();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросОПерезаполнениииПоОснованиюПриЗавершении(Результат, Параметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		
		ПерезаполнитьПоОснованиюСервер();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьПоОснованиюСервер()
	
	ТекущийОбъект = РеквизитФормыВЗначение("Объект");
	ТекущийОбъект.Заполнить(Объект.ДокументОснование);
	
	ЗначениеВРеквизитФормы(ТекущийОбъект, "Объект");
	
	ПриСозданииЧтенииНаСервере();
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция РедактированиеНедоступноПоСтатусуПроверкиИподбора(Форма)
	
	Если Форма.СтатусПроверкиИПодбора = ПредопределенноеЗначение("Перечисление.СтатусыПроверкиИПодбораИС.Выполняется")
		Или Форма.СтатусПроверкиИПодбора = ПредопределенноеЗначение("Перечисление.СтатусыПроверкиИПодбораИС.Завершено") Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ВыборИдентификатораПроисхожденияВЕТИСЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.ОтчетПроизводственнойЛинии Тогда
		
		Объект.ИдентификаторПроисхожденияВЕТИС = Результат;
		ЗаполнитьДанныеВЕТИСШапка(ЭтаФорма);
		УстановитьФорматСрокГодности(ЭтаФорма);
		
	Иначе
		
		ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		ТекущиеДанные.ИдентификаторПроисхожденияВЕТИС = Результат;
		ЗаполнитьДанныеВЕТИС();
		ОпределитьФорматРедактированияСрокГодности();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПриИзмененииИдентификаторПроисхожденияВЕТИС(Форма)
	
	ЗаполнитьДанныеВЕТИСШапка(Форма);
	УстановитьФорматСрокГодности(Форма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьДанныеВЕТИСШапка(Форма)
	
	Объект = Форма.Объект;
	
	Если Не Объект.ОтчетПроизводственнойЛинии Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ИнтеграцияИСКлиентСервер.ЭтоПродукцияПодконтрольнаяВЕТИС(Объект.ВидПродукции) Тогда
		Возврат;
	КонецЕсли;
	
	СрокГодности    = '00010101';
	СкоропортящаясяПродукция = Ложь;
	Идентификатор   = "";
	
	Если ЗначениеЗаполнено(Объект.ИдентификаторПроисхожденияВЕТИС) Тогда
		
		ДанныеВЕТИС = ДанныеИдентификаторовПроисхожденияВЕТИС(Объект.ИдентификаторПроисхожденияВЕТИС);
		
		СрокГодности    = ДанныеВЕТИС[Объект.ИдентификаторПроисхожденияВЕТИС].СрокГодности;
		СкоропортящаясяПродукция = ДанныеВЕТИС[Объект.ИдентификаторПроисхожденияВЕТИС].СкоропортящаясяПродукция;
		Идентификатор   = ДанныеВЕТИС[Объект.ИдентификаторПроисхожденияВЕТИС].Идентификатор;
		
	КонецЕсли;
	
	Объект.СрокГодности             = СрокГодности;
	Объект.СкоропортящаясяПродукция = СкоропортящаясяПродукция;
	
	Форма.ИдентификаторПроисхожденияВЕТИССтрокой = Идентификатор;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДанныеВЕТИС()
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьДанныеВЕТИСПоСтроке(ЭтаФорма, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ТекущиеДанные));
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьДанныеВЕТИСПоСтроке(Форма, ДобавленныеСтроки)
	
	Если Не ИнтеграцияИСКлиентСервер.ЭтоПродукцияПодконтрольнаяВЕТИС(Форма.Объект.ВидПродукции) Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторыВЕТИС = Новый Массив;
	Для Каждого СтрокаДанныхЗаполнения Из ДобавленныеСтроки Цикл
		Если Не ЗначениеЗаполнено(СтрокаДанныхЗаполнения.ИдентификаторПроисхожденияВЕТИС) Тогда
			Продолжить;
		КонецЕсли;
		ИдентификаторыВЕТИС.Добавить(СтрокаДанныхЗаполнения.ИдентификаторПроисхожденияВЕТИС);
	КонецЦикла;
	
	ДанныеВЕТИС = ДанныеИдентификаторовПроисхожденияВЕТИС(ИдентификаторыВЕТИС);
	
	Для Каждого ТекущиеДанные Из ДобавленныеСтроки Цикл
		
		СрокГодности    = '00010101';
		СкоропортящаясяПродукция = Ложь;
		
		Если ЗначениеЗаполнено(ТекущиеДанные.ИдентификаторПроисхожденияВЕТИС) Тогда
			СрокГодности    = ДанныеВЕТИС[ТекущиеДанные.ИдентификаторПроисхожденияВЕТИС].СрокГодности;
			СкоропортящаясяПродукция = ДанныеВЕТИС[ТекущиеДанные.ИдентификаторПроисхожденияВЕТИС].СкоропортящаясяПродукция;
		КонецЕсли;
		
		ТекущиеДанные.СрокГодности = СрокГодности;
		ТекущиеДанные.СкоропортящаясяПродукция = СкоропортящаясяПродукция;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеИдентификаторовПроисхожденияВЕТИС(ИдентификаторыПроисхождения)
	Возврат ИнтеграцияИСМПВЕТИС.ДанныеИдентификаторовПроисхождения(ИдентификаторыПроисхождения);
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция КлючСвязиСтатусаСтрок(Форма, Номенклатура = Неопределено)
	
	МассивКлючейСвязи = Новый Массив;
	
	Если Форма.Объект.Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.КорректировкаСведенийКМСрокГодности") Тогда
		МассивКлючейСвязи.Добавить("СрокГодности");
	ИначеЕсли Форма.Объект.Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.КорректировкаСведенийКМСрокГодностиВСД") Тогда
		МассивКлючейСвязи.Добавить("ИдентификаторПроисхожденияВЕТИС");
		МассивКлючейСвязи.Добавить("СрокГодности");
	КонецЕсли;
	
	Возврат СтрСоединить(МассивКлючейСвязи, ",");
	
КонецФункции

&НаКлиенте
Процедура ОпределитьФорматРедактированияСрокГодности()
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ИнтеграцияИСКлиентСервер.ЭтоМолочнаяПродукцияИСМП(Объект.ВидПродукции) Тогда
		ФорматДаты = "ДФ=dd.MM.yyyy;";
		Если ТекущиеДанные.СкоропортящаясяПродукция Тогда
			ФорматДаты = "ДФ='dd.MM.yyyy HH:mm';";
		КонецЕсли;
		Элементы.ТоварыСрокГодности.ФорматРедактирования = ФорматДаты;
	ИначеЕсли ИнтеграцияИСМПКлиентСервер.ЭтоВидПродукцииСоСрокамиГодности(Объект.ВидПродукции, Ложь) Тогда
		ФорматДаты = "ДФ=dd.MM.yyyy;";
		Элементы.ТоварыСрокГодности.ФорматРедактирования = ФорматДаты;
	Иначе
		ТекущиеДанные.СрокГодности = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьФорматСрокГодности(Форма)

	Если ИнтеграцияИСКлиентСервер.ЭтоМолочнаяПродукцияИСМП(Форма.Объект.ВидПродукции) Тогда
		ФорматДаты = "ДФ=dd.MM.yyyy;";

		Если Форма.Объект.СкоропортящаясяПродукция Тогда
			ФорматДаты = "ДФ='dd.MM.yyyy HH:mm';";
		Иначе
			Форма.Объект.СрокГодности = НачалоДня(Форма.Объект.СрокГодности);
		КонецЕсли;

		Форма.Элементы.СрокГодности.Формат               = ФорматДаты;
		Форма.Элементы.СрокГодности.ФорматРедактирования = ФорматДаты;
	ИначеЕсли ИнтеграцияИСМПКлиентСервер.ЭтоВидПродукцииСоСрокамиГодности(Форма.Объект.ВидПродукции, Ложь) Тогда
		ФорматДаты = "ДФ=dd.MM.yyyy;";
		Форма.Элементы.СрокГодности.Формат               = ФорматДаты;
		Форма.Элементы.СрокГодности.ФорматРедактирования = ФорматДаты;
	Иначе
		Форма.Объект.СрокГодности = Неопределено;
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьФорматДатыПроизводства(Форма)
	
	ФорматДатыПроизводства = "ДФ=dd.MM.yyyy;";
	
	Форма.Элементы.ДатаПроизводства.Формат               = ФорматДатыПроизводства;
	Форма.Элементы.ДатаПроизводства.ФорматРедактирования = ФорматДатыПроизводства;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НоменклатураСоответствуетСопоставленнойПродукцииВЕТИСПоИдентификаторуПроисхождения(
	ИдентификаторПроисхожденияВЕТИС, Номенклатура, Характеристика, Серия)

	ДанныеСопоставления = Новый Структура;
	ДанныеСопоставления.Вставить("Номенклатура", Номенклатура);
	ДанныеСопоставления.Вставить("Характеристика", Характеристика);
	ДанныеСопоставления.Вставить("Серия", Серия);

	Возврат ИнтеграцияИСМПВЕТИС.НоменклатураСоответствуетСопоставленнойПродукцииВЕТИСПоИдентификаторуПроисхождения(
		ИдентификаторПроисхожденияВЕТИС,
		ДанныеСопоставления);

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОчиститьДанныеПроизводственнойПлощадкиВЕТИС(Форма)
	
	Если ЗначениеЗаполнено(Форма.Объект.ИдентификаторПроизводственнойПлощадкиВЕТИС)
		Или ЗначениеЗаполнено(Форма.Объект.ПроизводственнаяПлощадкаВЕТИС) Тогда
		Форма.Объект.ИдентификаторПроизводственнойПлощадкиВЕТИС = Неопределено;
		Форма.Объект.ПроизводственнаяПлощадкаВЕТИС = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборПроизводственнойПлощадкиВЕТИСЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ПроизводственнаяПлощадкаВЕТИС = Результат;
	
	ПриИзмененииПроизводственнойПлощадкиВЕТИС();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПроизводственнойПлощадкиВЕТИС()
	
	Если ЗначениеЗаполнено(Объект.ПроизводственнаяПлощадкаВЕТИС) Тогда
		Объект.ИдентификаторПроизводственнойПлощадкиВЕТИС = ИдентификаторПроизводственнойПлощадкиВЕТИС(Объект.ПроизводственнаяПлощадкаВЕТИС);
	Иначе
		Объект.ИдентификаторПроизводственнойПлощадкиВЕТИС = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИдентификаторПроизводственнойПлощадкиВЕТИС(ПроизводственнаяПлощадкаВЕТИС)
	Возврат ИнтеграцияИСМПВЕТИС.ИдентификаторПроизводственнойПлощадкиВЕТИС(ПроизводственнаяПлощадкаВЕТИС);
КонецФункции

#КонецОбласти



