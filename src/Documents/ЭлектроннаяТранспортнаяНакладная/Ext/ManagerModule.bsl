#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	Номер = Данные.Номер;
	Направление = "";
	Если ЗначениеЗаполнено(Данные.Ссылка) Тогда
		РеквизитыСсылки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Данные.Ссылка, "ТитулГрузоотправителяТранспортнаяНакладнаяНомер,ЭтоВходящий", Истина);
		Если ЗначениеЗаполнено(РеквизитыСсылки.ТитулГрузоотправителяТранспортнаяНакладнаяНомер) Тогда
			Номер = РеквизитыСсылки.ТитулГрузоотправителяТранспортнаяНакладнаяНомер;
		КонецЕсли;
		Если ЗначениеЗаполнено(РеквизитыСсылки.ЭтоВходящий) Тогда
			Направление = ?(РеквизитыСсылки.ЭтоВходящий, " (входящая)", "");
		КонецЕсли;
	КонецЕсли;
	Дата = Формат(Данные.Дата, "ДФ=dd.MM.yyyy");
	Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Электронная транспортная накладная%1 № %2 от %3'"), Направление, Номер, Дата);
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом


// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
		
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ЭТрН";
	КомандаПечати.Представление = НСтр("ru = 'Транспортная накладная'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
КонецПроцедуры

// Формирует печатные формы
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр).
//  ОбъектыПечати         - СписокЗначений:
//  * Значение - ДокументСсылка.РеализацияТоваровУслуг - ссылка на объект;
//  * Представление - Строка - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной
//                                            параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЭТрН") Тогда
		
		ИмяПечатнойФормы = Неопределено;
		Если МассивОбъектов.Количество() = 1 Тогда
			ДатаДокумента = МассивОбъектов[0].Дата;
			ИмяПечатнойФормы = ОбменСГИСЭПДПереопределяемый.ИмяПечатнойФормыЭТрН(ДатаДокумента);
		КонецЕсли;
	
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ЭТрН",
			НСтр("ru='Транспортная накладная'"),
			СформироватьТранспортнуюНакладную(МассивОбъектов, ОбъектыПечати, ИмяПечатнойФормы),
			,
			ИмяПечатнойФормы);
			
	КонецЕсли;
	
КонецПроцедуры


// Формирует печатную форму транспортной накладной.
//
// Параметры:
//  МассивОбъектов            - Массив из ДокументСсылка.РеализацияТоваровУслуг - документы РТУ на печать.
//  ОбъектыПечати             - Список значений - объекты печати (значение - ссылка на объект, 
//                                                           представление - имя области, в которой был выведен объект).
//
// Возвращаемое значение:
//  Табличный документ
//
Функция СформироватьТранспортнуюНакладную(МассивОбъектов, ОбъектыПечати, Знач ИмяПечатнойФормы) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб         = Истина;
	ТабличныйДокумент.ОриентацияСтраницы  = ОриентацияСтраницы.Портрет;
	
	ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ЭлектроннаяТранспортнаяНакладная_ТранспортнаяНакладная";
	ТабличныйДокумент.ИмяПараметровПечати = ИмяПараметровПечати;
	
	УстановитьМинимальныеПоляПечати(ТабличныйДокумент);
	
	МакетПереданный = Неопределено;
	Если ИмяПечатнойФормы <> Неопределено Тогда
		МакетПереданный = УправлениеПечатью.МакетПечатнойФормы(ИмяПечатнойФормы);
	КонецЕсли;
	
	Для Каждого СсылкаНаОбъект Из МассивОбъектов Цикл
		
		ДанныеФормирования = ОбменСГИСЭПД.ДанныеРеквизитовЭПД(СсылкаНаОбъект);
		
		Если МакетПереданный = Неопределено Тогда
			ИмяПечатнойФормы = ОбменСГИСЭПДПереопределяемый.ИмяПечатнойФормыЭТрН(СсылкаНаОбъект.Дата);
			Макет = УправлениеПечатью.МакетПечатнойФормы(ИмяПечатнойФормы);
		Иначе
			Макет = МакетПереданный;
		КонецЕсли;
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ПараметрыМакета = ПараметрыМакетаЭТрН(СсылкаНаОбъект, ДанныеФормирования);
		
		ОбластьМакета = Макет.ПолучитьОбласть("ГоризонтальнаяЛицеваяСторона");
		ОбластьМакета.Параметры.Заполнить(ПараметрыМакета);
		
		ТабличныйДокумент.Вывести(ОбластьМакета);
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		ОбластьМакетаОборотная = Макет.ПолучитьОбласть("ГоризонтальнаяОборотнаяСторона");
		ОбластьМакетаОборотная.Параметры.Заполнить(ПараметрыМакета);
		
		ЗаполнитьОтметкиУчастников("ТитулГрузоотправителяОтметкиГрузоотправителя", 
									"ТитулГрузоотправителяРасчетыШтрафов", 
									"ТитулГрузоотправителяПодписант",
									ОбластьМакетаОборотная,
									ДанныеФормирования);
									
		ЗаполнитьОтметкиУчастников("ТитулПеревозчикаПриемкаОтметкиПеревозчикаПриПриемеГруза", 
									"ТитулПеревозчикаПриемкаРасчетыШтрафов", 
									"ТитулПеревозчикаПриемкаПодписант",
									ОбластьМакетаОборотная,
									ДанныеФормирования);
									
		ЗаполнитьОтметкиУчастников("ТитулГрузополучателяОтметкиГрузополучателя", 
									"ТитулГрузополучателяРасчетыШтрафов", 
									"ТитулГрузополучателяПодписант",
									ОбластьМакетаОборотная,
									ДанныеФормирования);
									
		ЗаполнитьОтметкиУчастников("ТитулПеревозчикаВыдачаОтметкиПриВыдачеГруза", 
									"ТитулПеревозчикаВыдачаРасчетыШтрафов", 
									"ТитулПеревозчикаВыдачаПодписант",
									ОбластьМакетаОборотная,
									ДанныеФормирования);
					
		ТабличныйДокумент.Вывести(ОбластьМакетаОборотная);
		
		// Добавить штампы
		ДанныеДляФормированияШтампа = ОбменСГИСЭПД.НовыеДанныеДляФормированияШтампа();
		
		Если ЗначениеЗаполнено(СсылкаНаОбъект.УИДМинтранс) Тогда
			ИдентификаторДокумента = СсылкаНаОбъект.УИДМинтранс;
		Иначе
			ИдентификаторДокумента = СсылкаНаОбъект.ТитулГрузоотправителяИдентификаторФайла;
		КонецЕсли;
		
		ДанныеДляФормированияШтампа.ИдентификаторДокумента = ИдентификаторДокумента;
		ДанныеДляФормированияШтампа.ЭтоИнформацияОтправителя = СсылкаНаОбъект.ЭтоВходящий = Ложь;
		ДанныеДляФормированияШтампа.Организация = СсылкаНаОбъект.Организация;
		ДанныеДляФормированияШтампа.ЭтоИсходящийДокумент = СсылкаНаОбъект.ЭтоВходящий = Ложь;
		ДанныеДляФормированияШтампа.ОсновноеСостояние = ОбменСГИСЭПД.ОсновныеСостоянияДокументов().Подписан;
		
		СообщенияДокумента = ОбменСГИСЭПД.ПолучитьСообщенияДокумента(СсылкаНаОбъект);
		
		ЭлектронныеПодписиОтправителя = Новый Массив;
		ЭлектронныеПодписиПолучателя = Новый Массив;
		РазличныеВладельцы = Новый Массив;
		Для Каждого КиЗ Из СообщенияДокумента Цикл
			ПодписиТитула = ОбменСГИСЭПД.УстановленныеПодписиСУчетомДоверенностей(КиЗ.Значение);
			Для Каждого Подпись Из ПодписиТитула Цикл
				Если РазличныеВладельцы.Найти(Подпись.СвойстваПодписи.Владелец) = Неопределено Тогда
					РазличныеВладельцы.Добавить(Подпись.СвойстваПодписи.Владелец);
				Иначе
					Продолжить;
				КонецЕсли;
				Если (КиЗ.Значение.Направление = Перечисления.НаправленияЭД.Входящий
						И ДанныеДляФормированияШтампа.ЭтоИсходящийДокумент = Истина) 
					Или (КиЗ.Значение.Направление = Перечисления.НаправленияЭД.Исходящий
						И ДанныеДляФормированияШтампа.ЭтоИсходящийДокумент = Ложь) Тогда
					ЭлектронныеПодписиПолучателя.Добавить(Подпись);
				ИначеЕсли (КиЗ.Значение.Направление = Перечисления.НаправленияЭД.Входящий
						И ДанныеДляФормированияШтампа.ЭтоИсходящийДокумент = Ложь) 
					Или (КиЗ.Значение.Направление = Перечисления.НаправленияЭД.Исходящий
						И ДанныеДляФормированияШтампа.ЭтоИсходящийДокумент = Истина) Тогда
					ЭлектронныеПодписиОтправителя.Добавить(Подпись);			
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
			
		ДанныеДляФормированияШтампа.ПодписиОтправителя = ЭлектронныеПодписиОтправителя;
		ДанныеДляФормированияШтампа.ПодписиПолучателя = ЭлектронныеПодписиПолучателя;
		
		ДанныеДляФормированияШтампа.ЕстьОтветнаяПодпись = ЭлектронныеПодписиОтправителя.Количество() > 0
															И ЭлектронныеПодписиПолучателя.Количество() > 0;
		
		Штамп = ОбменСГИСЭПД.ШтампЭлектроннойПодписи(ДанныеДляФормированияШтампа);
		Если Штамп.ВысотаТаблицы > 0 Тогда
			ОбменСГИСЭПД.ПрисоединитьОбластьКТабличномуДокументу(ТабличныйДокумент, Штамп, "Штамп");
		КонецЕсли;
		
		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, СсылкаНаОбъект);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
 
КонецФункции

Процедура ЗаполнитьОтметкиУчастников(ИмяТаблицыОтметок, ИмяТаблицыШтрафов, ПрефиксРеквизитовПодписанта, ОбластьЗаполнения, ДанныеЗаполнения)
	
	ОтметкиУчастника = Неопределено;
	Если ДанныеЗаполнения.Свойство(ИмяТаблицыОтметок, ОтметкиУчастника) Тогда
		Для Каждого СтрОтметка Из ОтметкиУчастника Цикл
			ОбластьЗамечания = ОбластьЗаполнения.Область("R37:R38");
			ОбластьЗаполнения.ВставитьОбласть(ОбластьЗамечания, ОбластьЗамечания, ТипСмещенияТабличногоДокумента.ПоВертикали, );
			
			ОбластьЗаполнения.Область(37, 2).Текст = СтрОтметка.ОписаниеОбстоятельств;
			
			МассивШтрафов = Новый Массив;	
			СтрокиШтрафов = НайтиСтрокиВМассивеСтруктур(
									ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, ИмяТаблицыШтрафов), 
									"ИдентификаторСтрокиРодителя", СтрОтметка.ИдентификаторСтроки);
			Для Каждого СтрШтраф Из СтрокиШтрафов Цикл
				Если ЗначениеЗаполнено(СтрШтраф.РазмерШтрафаВВалюте) Тогда
					ПредставлениеШтрафа = Строка(СтрШтраф.РазмерШтрафаВВалюте) + " " + СтрШтраф.НаименованиеВалютыШтрафа +
						?(ЗначениеЗаполнено(СтрШтраф.Курс), " по курсу " + СтрШтраф.Курс, "");	
				Иначе
					ПредставлениеШтрафа = Строка(СтрШтраф.РазмерШтрафа) + " руб.";
				КонецЕсли;
				ПредставлениеШтрафа = ПредставлениеШтрафа + " (" + СтрШтраф.РасчетШтрафа + ")";
				МассивШтрафов.Добавить(ПредставлениеШтрафа);	
			КонецЦикла;
			ОбластьЗаполнения.Область(37, 29).Текст = СтрСоединить(МассивШтрафов, Символы.ПС);
			ОбластьЗаполнения.Область(37, 44).Текст = Строка(СтрОтметка.ДатаВремя) + ", " 
																	+ ПредставлениеПодписи(ДанныеЗаполнения, 
																							ПрефиксРеквизитовПодписанта);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьЗначениеСтруктуры(Структура, ИмяКлюча, ЗначениеПоУмолчанию = Неопределено)
	
	Результат = Неопределено;
	
	Структура.Свойство(ИмяКлюча, Результат);
	
	Если ЗначениеПоУмолчанию <> Неопределено Тогда
		Если Результат = Неопределено Тогда
			Результат = ЗначениеПоУмолчанию;
		ИначеЕсли ТипЗнч(Результат) <> ТипЗнч(ЗначениеПоУмолчанию) Тогда
			Если ТипЗнч(ЗначениеПоУмолчанию) = Тип("Строка") Тогда
				Результат = Строка(Результат);
			ИначеЕсли ТипЗнч(ЗначениеПоУмолчанию) = Тип("Дата") Тогда 
				Результат = ОбменСГИСЭПДКлиентСервер.ПреобразоватьВДату(Строка(Результат));
			ИначеЕсли ТипЗнч(ЗначениеПоУмолчанию) = Тип("Число") Тогда 
				Результат = ОбменСГИСЭПДКлиентСервер.ПреобразоватьВЧисло(Строка(Результат));
			ИначеЕсли ТипЗнч(ЗначениеПоУмолчанию) = Тип("Булево") Тогда 
				Результат = ОбменСГИСЭПДКлиентСервер.ПреобразоватьВБулево(Строка(Результат));
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция НайтиСтрокиВМассивеСтруктур(Массив, ИмяКлюча, Значение)
	
	Результат = Новый Массив;
	
	Если ТипЗнч(Массив) <> Тип("Массив") Тогда
		Возврат Результат;
	КонецЕсли;
	
	Для Каждого СтруктураСтроки Из Массив Цикл
		Если ТипЗнч(СтруктураСтроки) <> Тип("Структура") Тогда
			Продолжить;
		КонецЕсли;
		ЗначениеВСтроке = Неопределено;
		Если СтруктураСтроки.Свойство(ИмяКлюча, ЗначениеВСтроке) И ЗначениеВСтроке = Значение Тогда
			Результат.Добавить(СтруктураСтроки);	
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ПараметрыМакетаЭТрН(СсылкаНаОбъект, ДанныеЗаполнения)
		
	Результат = Новый Структура;
	
	// Шапка
	Результат.Вставить("Пункт0_2", Формат(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяТранспортнаяНакладнаяДата"), "ДЛФ=D"));
	Результат.Вставить("Пункт0_5", ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяТранспортнаяНакладнаяНомер"));
	
	Результат.Вставить("Пункт0_2", Формат(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяЗаказДата"), "ДЛФ=D"));
	Результат.Вставить("Пункт0_3", ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяЗаказНомер"));

	Результат.Вставить("Пункт0_1", СсылкаНаОбъект.РольУчастника);
	
	// Грузоотправитель
	Результат.Вставить("Пункт_Э", ?(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяЭкспедитор") = Истина, "V", ""));
	Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяОснованиеРасчетовИнымЛицомНомер")) Тогда
		Результат.Вставить("Пункт1_2", ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяОснованиеРасчетовИнымЛицомНаименование", "")
										+ " №" + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяОснованиеРасчетовИнымЛицомНомер", "")
										+ " от " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяОснованиеРасчетовИнымЛицомДата", ""));
	КонецЕсли;
	Результат.Вставить("Пункт1_1", ПредставлениеУчастника(ДанныеЗаполнения, "ТитулГрузоотправителяГрузоотправитель"));
	
	// Заказчик
	Результат.Вставить("Пункт1а_1", ПредставлениеУчастника(ДанныеЗаполнения, "ТитулГрузоотправителяЗаказчик"));
	Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяДоговорНаУсловияПеревозкиНомер")) Тогда
		Результат.Вставить("Пункт1а_2", ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяДоговорНаУсловияПеревозкиНаименование", "")
										+ " №" + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяДоговорНаУсловияПеревозкиНомер", "")
										+ " от " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяДоговорНаУсловияПеревозкиДата", ""));
	КонецЕсли;
	
	// Грузополучатель
	Результат.Вставить("Пункт2_1", ПредставлениеУчастника(ДанныеЗаполнения, "ТитулГрузоотправителяГрузополучатель"));
	Результат.Вставить("Пункт2_2", ПредставлениеАдреса(ДанныеЗаполнения, "ТитулГрузоотправителяГрузополучательАдресДоставки"));
	
	// Груз
	Если ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяКодМетодаОпределенияМассыГруза") = "01" Тогда
		МетодОпределенияМассы = "определено взвешиванием по общей массе";
	ИначеЕсли ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяКодМетодаОпределенияМассыГруза") = "01" Тогда
		МетодОпределенияМассы = "определено взвешиванием поосно";
	Иначе
		МетодОпределенияМассы = "определено расчетным способом";
	КонецЕсли;
	
	МассивПункт3_1 = Новый Массив;
	МассивПункт3_2 = Новый Массив;
	МассивПункт3_3 = Новый Массив;
	МассивПункт3_4 = Новый Массив;
	МассаБрутто = 0;
	МассаНетто = 0;
	ОбъемГруза = 0;
	ВсегоМест = 0;
	СчетчикМест = 0;
	НомераМест = "";
	НужныНомераМест = ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяГрузы", Новый Массив).Количество() > 1;
	Для Каждого СтрГруз Из ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяГрузы", Новый Массив) Цикл
		МассивПункт3_1.Добавить(СтрГруз.ОтгрузочноеНаименованиеГруза);
		
		// Маркировка
		МассивМаркировок = Новый Массив;	
		СтрокиМаркировки = НайтиСтрокиВМассивеСтруктур(
								ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяПереченьМаркировокГруза"), 
								"ИдентификаторСтрокиРодителя", СтрГруз.ИдентификаторСтроки);
		Для Каждого СтрМарк Из СтрокиМаркировки Цикл
			МассивМаркировок.Добавить(СтрМарк.МаркировкаГруза);	
		КонецЦикла;
		
		Если НужныНомераМест = Истина Тогда
			НомераМест = "Места N N " 
					+ Строка(СчетчикМест + 1) + "-" + Строка(СчетчикМест + СтрГруз.КоличествоГрузовыхМест) + ": ";
		КонецЕсли;
		
		// Опасный груз
		МассивДОПОГ = Новый Массив;
		СтрокиДОПОГ = НайтиСтрокиВМассивеСтруктур(
						ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяСведенияОбОпасныхГрузах"), 
						"ИдентификаторСтрокиРодителя", СтрГруз.ИдентификаторСтроки);
		Для Каждого СтрДОПОГ Из СтрокиДОПОГ Цикл
			МассивДОПОГ.Добавить("номер ООН:" + СтрДОПОГ.НомерООН
								+ "; класс: " + СтрДОПОГ.Класс
								+ "; описание: " + СтрДОПОГ.Описание);	
		КонецЦикла;
		Если МассивДОПОГ.Количество() > 0 Тогда
			Пункт3_4 = НомераМест + СтрСоединить(МассивДОПОГ);
			МассивПункт3_4.Добавить(Пункт3_4);
		КонецЕсли;
		
		ВсегоМест = ВсегоМест + СтрГруз.КоличествоГрузовыхМест;
		
		МассивПункт3_2.Добавить("Мест: " + СтрГруз.КоличествоГрузовыхМест
								+ ", маркировка: " + СтрСоединить(МассивМаркировок, ", ")
								+ ", вид тары: " + СтрГруз.ВидТары
								+ ", способ упаковки: " + СтрГруз.СпособУпаковки);
								
		Пункт3_3 = НомераМест
				+ СтрГруз.МассаБрутто + "кг (брутто)" 
				+ ?(СтрГруз.МассаНетто = 0, "", " / " + СтрГруз.МассаНетто + "кг (нетто)")
				+ " (" + МетодОпределенияМассы + ")"
				+ ?(СтрГруз.Высота + СтрГруз.Ширина + СтрГруз.Длина = 0, "", 
					"; В x Ш x Д: " + СтрГруз.Высота + "м x " + СтрГруз.Ширина + "м x " + СтрГруз.Длина + "м") + ", "
				+ ?(СтрГруз.ОбъемГруза = 0, "", "; объем: " + СтрГруз.ОбъемГруза + "м3")
				+ ?(СтрГруз.Плотность = 0, "", "; плотность: " + СтрГруз.Плотность + "кг/м3");
				
		МассивПункт3_3.Добавить(Пункт3_3);
								
		МассаБрутто = МассаБрутто + СтрГруз.МассаБрутто;
		МассаНетто = МассаНетто + СтрГруз.МассаНетто;	
		ОбъемГруза = ОбъемГруза + СтрГруз.ОбъемГруза;
		СчетчикМест = СчетчикМест + СтрГруз.КоличествоГрузовыхМест;
	КонецЦикла;
	
	Если НужныНомераМест = Истина Тогда
		МассивПункт3_2.Добавить("Всего мест: " + ВсегоМест);
		
		МассивПункт3_3.Добавить("Всего: " + МассаБрутто + "кг (брутто)" 
				+ ?(МассаНетто = 0, "", " / " + МассаНетто + "кг (нетто)")
				+ ?(ОбъемГруза = 0, "", "; объем: " + ОбъемГруза + "м3"));
	КонецЕсли; 	
		
	Результат.Вставить("Пункт3_1", СтрСоединить(МассивПункт3_1, Символы.ПС));
	Результат.Вставить("Пункт3_2", СтрСоединить(МассивПункт3_2, Символы.ПС));
	Результат.Вставить("Пункт3_3", СтрСоединить(МассивПункт3_3, Символы.ПС));
	Результат.Вставить("Пункт3_4", СтрСоединить(МассивПункт3_4, Символы.ПС));
	Результат.Вставить("Пункт3_5", Строка(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяСтоимостьГруза")) 
									+ ?(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяКодВалюты") = "643", " RUR",
									 	" (код валюты: " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяКодВалюты") + ")"));
									 	
	// Документы на груз
	МассивПункт4_1 = Новый Массив;
	Для Каждого СтрСопрДок Из ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяСопроводительныеДокументы", Новый Массив) Цикл
		МассивСостСопрДок = Новый Массив;
		СтрокиСостСопрДок = НайтиСтрокиВМассивеСтруктур(
						ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяРеквизитыСторонСопроводительныхДокументов"), 
						"ИдентификаторСтрокиРодителя", СтрСопрДок.ИдентификаторСтроки);
		Для Каждого СтрСостСопрДок Из СтрокиСостСопрДок Цикл
			МассивСостСопрДок.Добавить(ПредставлениеСтороныПодписавшейДокумент(СтрСостСопрДок));	
		КонецЦикла;
		Составители = "";
		Если МассивСостСопрДок.Количество() > 0 Тогда
			Составители = "; Составители: " + СтрСоединить(МассивСостСопрДок, ", ")	
		КонецЕсли;
		МассивПункт4_1.Добавить(СтрСопрДок.НаименованиеДокумента 
								+ " №" + СтрСопрДок.НомерДокумента
								+ " от " + СтрСопрДок.ДатаДокумента
								+ Составители);
	КонецЦикла;
	Результат.Вставить("Пункт4_1", СтрСоединить(МассивПункт4_1, Символы.ПС));
	
	МассивПункт4_2 = Новый Массив;
	Для Каждого СтрСерт Из ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяСертификатыУдостоверения", Новый Массив) Цикл
		МассивСостСерт = Новый Массив;
		СтрокиСостСерт = НайтиСтрокиВМассивеСтруктур(
						ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяРеквизитыСторонСертификатовУдостоверений"), 
						"ИдентификаторСтрокиРодителя", СтрСерт.ИдентификаторСтроки);
		Для Каждого СтрСостСерт Из СтрокиСостСерт Цикл
			МассивСостСерт.Добавить(ПредставлениеСтороныПодписавшейДокумент(СтрСостСерт));	
		КонецЦикла;
		Составители = "";
		Если МассивСостСерт.Количество() > 0 Тогда
			Составители = "; Составители: " + СтрСоединить(МассивСостСерт, ", ")	
		КонецЕсли;
		МассивПункт4_2.Добавить(СтрСерт.НаименованиеДокумента 
								+ " №" + СтрСерт.НомерДокумента
								+ " от " + СтрСерт.ДатаДокумента
								+ Составители);
	КонецЦикла;
	Результат.Вставить("Пункт4_2", СтрСоединить(МассивПункт4_2, Символы.ПС));
	
	МассивПункт4_3 = Новый Массив;
	Для Каждого СтрПодтв Из ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяДокументыПодтверждающиеОтгрузку", Новый Массив) Цикл
		МассивСостПодтв = Новый Массив;
		СтрокиСостПодтв = НайтиСтрокиВМассивеСтруктур(
						ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяРеквизитыСторонДокументаПодтверждающегоОтгрузку"), 
						"ИдентификаторСтрокиРодителя", СтрПодтв.ИдентификаторСтроки);
		Для Каждого СтрСостПодтв Из СтрокиСостПодтв Цикл
			МассивСостПодтв.Добавить(ПредставлениеСтороныПодписавшейДокумент(СтрСостПодтв));	
		КонецЦикла;
		Составители = "";
		Если МассивСостПодтв.Количество() > 0 Тогда
			Составители = "; Составители: " + СтрСоединить(МассивСостПодтв, ", ")	
		КонецЕсли;
		МассивПункт4_3.Добавить(СтрПодтв.НаименованиеДокумента 
								+ " №" + СтрПодтв.НомерДокумента
								+ " от " + СтрПодтв.ДатаДокумента
								+ Составители);
	КонецЦикла;
	Результат.Вставить("Пункт4_3", СтрСоединить(МассивПункт4_3, Символы.ПС));
	
	// Условия перевозки
	Пункт5_1 = ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяНаименованиеМаршрута", "")
				+ ?(ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяДатаИВремяДоставки")),
					" дата доставки: " + Формат(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяДатаИВремяДоставки"), "ДФ='dd.MM.yyyy ЧЧ:мм';"), "");
	Результат.Вставить("Пункт5_1", Пункт5_1);
	
	Пункт5_2 = ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяУказанияВОтношенииВыполненияНормПеревозки");
	Результат.Вставить("Пункт5_2", Пункт5_2);
	
	МассивТелефоныПереадресовщика = Новый Массив;
	Для Каждого СтрТелефоны Из ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяОтветственныйЗаПереадресовкуНомераТелефонов", Новый Массив) Цикл
		МассивТелефоныПереадресовщика.Добавить(СтрТелефоны.Телефон);
	КонецЦикла;
	ТелефоныПереадресовщика = "";
	Если МассивТелефоныПереадресовщика.Количество() > 0 Тогда
		ТелефоныПереадресовщика = " тел.: " + СтрСоединить(МассивТелефоныПереадресовщика);
	КонецЕсли;	
	МассивЭлПочтыПереадресовщика = Новый Массив;
	Для Каждого СтрЭлПочта Из ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяОтветственныйЗаПереадресовкуАдресаЭлектроннойПочты", Новый Массив) Цикл
		МассивЭлПочтыПереадресовщика.Добавить(СтрЭлПочта.АдресЭлектроннойПочты);
	КонецЦикла;
	ЭлПочтаПереадресовщика = "";
	Если МассивЭлПочтыПереадресовщика.Количество() > 0 Тогда
		ЭлПочтаПереадресовщика = " эл. почта: " + СтрСоединить(ЭлПочтаПереадресовщика);
	КонецЕсли;	
	Лицо = НРег(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяЛицоИмеющееПолномочияПереадресовки", ""));
	Результат.Вставить("Пункт5_3", ТелефоныПереадресовщика + " (" + Лицо + ")");
	
	Пункт5_4 = ?(ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяНижнийПределТемпературыПриПеревозке")),
					"от " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяНижнийПределТемпературыПриПеревозке"), "")
			+ ?(ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяВерхнийПределТемпературыПриПеревозке")),
					" до " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяВерхнийПределТемпературыПриПеревозке"), "");
	Результат.Вставить("Пункт5_4", Пункт5_4);
	
	// Перевозчик
	Результат.Вставить("Пункт6_1", ПредставлениеУчастника(ДанныеЗаполнения, "ТитулГрузоотправителяПеревозчик"));
	
	// Водители
	МассивПункт6_2 = Новый Массив;
	Для Каждого СтрВодитель Из ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяВодители", Новый Массив) Цикл
		МассивТелефоны = Новый Массив;
		СтрокиТелефоны = НайтиСтрокиВМассивеСтруктур(
						ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяВодительНомераТелефонов"), 
						"ИдентификаторСтрокиРодителя", СтрВодитель.ИдентификаторСтроки);
		Для Каждого СтрТелефон Из СтрокиТелефоны Цикл
			МассивТелефоны.Добавить(СтрТелефон.Телефон);	
		КонецЦикла;
		Телефоны = "";
		Если МассивТелефоны.Количество() > 0 Тогда
			Телефоны = ", тел.: " + СтрСоединить(МассивТелефоны, ", ")	
		КонецЕсли;
		МассивПункт6_2.Добавить(СтрВодитель.Фамилия 
								+ " " + СтрВодитель.Имя
								+ " " + СтрВодитель.Отчество
								+ ?(ЗначениеЗаполнено(СтрВодитель.ФЛ_ИНН),
									", ИНН: " + СтрВодитель.ФЛ_ИНН, "")
								+ ?(ЗначениеЗаполнено(СтрВодитель.НомерВУ),
									", ВУ " 
										+ СтрВодитель.СерияВУ + " " + СтрВодитель.НомерВУ
										+ " выдано " + Формат(СтрВодитель.ДатаВыдачиВУ, "ДЛФ=D"), "")
								+ Телефоны);
	КонецЦикла;
	Результат.Вставить("Пункт6_2", СтрСоединить(МассивПункт6_2, Символы.ПС));
	
	// Транспортное средство
	МассивТС = Новый Массив;
	МассивРегНомеров = Новый Массив;
	СтрокаТС = ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяТранспортноеСредствоТип", "")
				+ " " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяТранспортноеСредствоМарка")
				+ ", грузоподъемность " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяТранспортноеСредствоГрузоподъемность") + " тонн"
				+ ", объем кузова " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяТранспортноеСредствоВместимость") + " м3";
	МассивТС.Добавить(СтрокаТС);
	МассивРегНомеров.Добавить(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяТранспортноеСредствоРегистрационныйНомер"));
	Для Каждого СтрПрицеп Из ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяПрицепы", Новый Массив) Цикл
		МассивТС.Добавить(СтрПрицеп.Тип
							+ " " + СтрПрицеп.Марка
							+ ", грузоподъемность " + СтрПрицеп.Грузоподъемность + " тонн"
							+ ", вместимость " + СтрПрицеп.Вместимость + " м3");	
		МассивРегНомеров.Добавить(СтрПрицеп.РегистрационныйНомер);
	КонецЦикла;				
	Результат.Вставить("Пункт7_1", СтрСоединить(МассивТС, Символы.ПС));			
	Результат.Вставить("Пункт7_2", СтрСоединить(МассивРегНомеров, Символы.ПС));
	Результат.Вставить("Пункт7_3", ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяТранспортноеСредствоТипВладения"));
	
	МассивОснВладТС = Новый Массив;
	Для Каждого СтрОснВладТС Из ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяОснованияВладенияТС", Новый Массив) Цикл
		МассивОснВладТС.Добавить(СтрОснВладТС.НаименованиеДокумента
								+ " №" + СтрОснВладТС.НомерДокумента
								+ " от " + Формат(СтрОснВладТС.ДатаДокумента, "ДЛФ=D"));	
	КонецЦикла;
	Для Каждого СтрОснВладПрицеп Из ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяОснованияВладенияПрицепом", Новый Массив) Цикл
		МассивОснВладТС.Добавить(СтрОснВладПрицеп.НаименованиеДокумента
								+ " №" + СтрОснВладПрицеп.НомерДокумента
								+ " от " + Формат(СтрОснВладПрицеп.ДатаДокумента, "ДЛФ=D"));	
	КонецЦикла;
	Результат.Вставить("Пункт7_4", СтрСоединить(МассивОснВладТС, Символы.ПС));
	
	МассивСпецУсл = Новый Массив;
	Для Каждого СтрСпецУсл Из ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяСпециальныеУсловияДвижения", Новый Массив) Цикл
		МассивСпецУсл.Добавить("Специальное разрешение №" + СтрСпецУсл.СпециальноеРазрешенияНомер
								+ " от " + Формат(СтрСпецУсл.СпециальноеРазрешенияДата, "ДЛФ=D")
								+ ", срок действия " + СтрСпецУсл.СпециальноеРазрешенияСрокДействия
								+ ", маршрут: " + СтрСпецУсл.МаршрутДвижения);		
	КонецЦикла;
	Результат.Вставить("Пункт7_5", СтрСоединить(МассивСпецУсл, Символы.ПС));
	
	// Прием груза
	Результат.Вставить("Пункт8_0", ПредставлениеУчастника(ДанныеЗаполнения, 
									?(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяЛицоОсуществляющееПогрузкуЯвляетсяГрузоотправителем", "1") = "1", 
									"ТитулГрузоотправителяГрузоотправитель",
									"ТитулГрузоотправителяЛицоОсуществляющееПогрузку")));	
	Результат.Вставить("Пункт8_1", ПредставлениеУчастника(ДанныеЗаполнения, 
									?(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяВладелецПунктаПогрузкиЯвляетсяГрузоотправителем", "1") = "1",
									"ТитулГрузоотправителяГрузоотправитель",
									"ТитулГрузоотправителяВладелецПунктаПогрузки")));
	Результат.Вставить("Пункт8_2", ПредставлениеАдреса(ДанныеЗаполнения, "ТитулГрузоотправителяАдресПогрузки"));
	Результат.Вставить("Пункт8_3", Формат(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяЗаявленныеДатаВремяПодачи"), "ДФ='dd.MM.yyyy ЧЧ:мм';"));
	Результат.Вставить("Пункт8_4", Формат(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяФактическиеДатаВремяПодачи"), "ДФ='dd.MM.yyyy ЧЧ:мм';"));
	Результат.Вставить("Пункт8_41", Формат(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяФактическиеДатаВремяУбытия"), "ДФ='dd.MM.yyyy ЧЧ:мм';"));
	Результат.Вставить("Пункт8_5", ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяМассаБруттоГруза", "")
									+ " тонн (" + МетодОпределенияМассы + ")");
	Результат.Вставить("Пункт8_51", ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяКоличествоГрузовыхМест"));
	
	МассивПункт8_6 = Новый Массив;
	Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаПриемкаЗамечанияДатаВремяПрибытия")) Тогда
		МассивПункт8_6.Добавить("дата/время подачи: " + Формат(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаПриемкаЗамечанияДатаВремяПрибытия"), "ДФ='dd.MM.yyyy ЧЧ:мм';"));
	КонецЕсли;
	Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаПриемкаЗамечанияДатаВремяУбытия")) Тогда
		МассивПункт8_6.Добавить("дата/время убытия: " + Формат(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаПриемкаЗамечанияДатаВремяУбытия"), "ДФ='dd.MM.yyyy ЧЧ:мм';"));
	КонецЕсли;
	Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаПриемкаЗамечанияДатаВремяПостановкиНаПогрузку")) Тогда
		МассивПункт8_6.Добавить("дата/время постановки на погрузку: " + Формат(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаПриемкаЗамечанияДатаВремяПостановкиНаПогрузку"), "ДФ='dd.MM.yyyy ЧЧ:мм';"));
	КонецЕсли;
	МассивПункт8_6.Добавить(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаПриемкаЗамечанияВОтношенииФактическогоСостояниеГруза"));
	МассивПункт8_6.Добавить(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаПриемкаЗамечанияВОтношенииМассыГруза"));
	МассивПункт8_6.Добавить(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаПриемкаЗамечанияВОтношенииКоличестваГрузовыхМест"));
	МассивПункт8_6.Добавить(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаПриемкаЗамечанияВОтношенииПроведенияПогрузочныхРабот"));
	
	Результат.Вставить("Пункт8_6", СтрСоединить(МассивПункт8_6, Символы.ПС)); 
	
	// Переадресовка
	Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПереадресовкаИдентификаторФайла")) Тогда
		Результат.Вставить("Пункт9_1", Формат(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПереадресовкаДатаВремя"), "ДФ='dd.MM.yyyy ЧЧ:мм';")
										+ " (" + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПереадресовкаФормаПереадресовки")
										+ " переадресовка, " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПереадресовкаВидДоставкиДокументаОПереадресовке") + ")");
		Результат.Вставить("Пункт9_2", ПредставлениеАдреса(ДанныеЗаполнения, "ТитулПереадресовкаАдресВыгрузки")
										+ ?(ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПереадресовкаНовыеДатаВремяПодачиПодВыгрузку")),
										", дата/время подачи под выгрузку: " + Формат(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПереадресовкаНовыеДатаВремяПодачиПодВыгрузку"), "ДФ='dd.MM.yyyy ЧЧ:мм';"), "")); 
		Результат.Вставить("Пункт9_4", ПредставлениеУчастника(ДанныеЗаполнения, "ТитулПереадресовкаНовыйГрузополучатель"));
	КонецЕсли;
	
	// Выдача груза
	АдресВыгрузки = ПредставлениеАдреса(ДанныеЗаполнения, "ТитулГрузополучателяАдресВыгрузки");	
	Результат.Вставить("Пункт10_1", АдресВыгрузки);
	Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузополучателяИдентификаторФайла")) Тогда
		Результат.Вставить("Пункт10_2", Формат(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузополучателяЗаявленныеДатаВремяПрибытия"), "ДФ='dd.MM.yyyy ЧЧ:мм';"));
		Результат.Вставить("Пункт10_3", Формат(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузополучателяФактическиеДатаВремяПрибытия"), "ДФ='dd.MM.yyyy ЧЧ:мм';"));
		Результат.Вставить("Пункт10_31", Формат(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузополучателяФактическиеДатаВремяУбытия"), "ДФ='dd.MM.yyyy ЧЧ:мм';"));
		
		Результат.Вставить("Пункт10_4", ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузополучателяФактическоеСостояниеГруза"));
		Результат.Вставить("Пункт10_5", ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузополучателяОбщееКоличествоПринятыхГрузовыхМест"));
		
		Результат.Вставить("Пункт10_6", Строка(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузополучателяМассаБруттоПринятогоГруза")) + " кг (брутто)"
										+ ?(ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузополучателяМассаНеттоПринятогоГруза")),
											" / " + Строка(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузополучателяМассаНеттоПринятогоГруза")) + " кг (нетто)", ""));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаВыдачаИдентификаторФайла")) Тогда
		МассивПункт10_7 = Новый Массив;
		Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаВыдачаДатаВремяПрибытия")) Тогда
			МассивПункт10_7.Добавить("дата/время прибытия: " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаВыдачаДатаВремяПрибытия"));
		КонецЕсли;
		Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаВыдачаДатаИВремяПостановкиНаВыгрузку")) Тогда
			МассивПункт10_7.Добавить("дата/время постановки на выгрузку: " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаВыдачаДатаИВремяПостановкиНаВыгрузку"));
		КонецЕсли;
		Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаВыдачаДатаВремяУбытия")) Тогда
			МассивПункт10_7.Добавить("дата/время убытия: " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаВыдачаДатаВремяУбытия"));
		КонецЕсли;
		Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаВыдачаЗамечанияВОтношенииФактическогоСостоянияГруза")) Тогда
			МассивПункт10_7.Добавить(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаВыдачаЗамечанияВОтношенииФактическогоСостоянияГруза"));
		КонецЕсли;
		Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаВыдачаЗамечанияВОтношенииПроведенияРаботПоВыгрузке")) Тогда
			МассивПункт10_7.Добавить(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаВыдачаЗамечанияВОтношенииПроведенияРаботПоВыгрузке"));
		КонецЕсли;
		Результат.Вставить("Пункт10_7", СтрСоединить(МассивПункт10_7, Символы.ПС));
	КонецЕсли;
	
	// Стоимость
	Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаФХЖИдентификаторФайла")) Тогда
		Результат.Вставить("Пункт12_1", ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаФХЖСтоимостьПеревозкиБезНалога"));
		Результат.Вставить("Пункт12_2", ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаФХЖНалоговаяСтавка"));
		Результат.Вставить("Пункт12_3", ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаФХЖСуммаНДС"));
		Результат.Вставить("Пункт12_4", ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаФХЖСтоимостьПеревозкиСНалогом"));
		Результат.Вставить("Пункт12_5", ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаФХЖПорядокМеханизмРасчетаПлаты")); 
		
		МассивПункт12_6 = Новый Массив;
		Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаФХЖПеревозчикОКПО")) Тогда
			МассивПункт12_6.Добавить("ОКПО: " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаФХЖПеревозчикОКПО"));
		КонецЕсли;
		Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаФХЖПеревозчикСтруктурноеПодразделение")) Тогда
			МассивПункт12_6.Добавить("структурное подразделение: " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаФХЖПеревозчикСтруктурноеПодразделение"));
		КонецЕсли;
		Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаФХЖПеревозчикКраткоеНаименование")) Тогда
			МассивПункт12_6.Добавить("краткое наименование: " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаФХЖПеревозчикКраткоеНаименование"));
		КонецЕсли;
		Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаФХЖБанковскиеРеквизитыНомерБанковскогоСчета")) Тогда
			МассивПункт12_6.Добавить("р/с: " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаФХЖБанковскиеРеквизитыНомерБанковскогоСчета"));
		КонецЕсли;
		Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаФХЖБанковскиеРеквизитыНаименованиеБанка")) Тогда
			МассивПункт12_6.Добавить("банк: " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаФХЖБанковскиеРеквизитыНаименованиеБанка"));
		КонецЕсли;
		Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаФХЖБанковскиеРеквизитыБИК")) Тогда
			МассивПункт12_6.Добавить("БИК: " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаФХЖБанковскиеРеквизитыБИК"));
		КонецЕсли;
		Результат.Вставить("Пункт12_6", СтрСоединить(МассивПункт12_6, ", ")); 
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяФХЖИдентификаторФайла")) Тогда
		МассивПункт12_7 = Новый Массив;
		Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяФХЖГрузоотправительОКПО")) Тогда
			МассивПункт12_7.Добавить("ОКПО: " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяФХЖГрузоотправительОКПО"));
		КонецЕсли;
		Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяФХЖГрузоотправительСтруктурноеПодразделение")) Тогда
			МассивПункт12_7.Добавить("структурное подразделение: " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяФХЖГрузоотправительСтруктурноеПодразделение"));
		КонецЕсли;
		Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяФХЖГрузоотправительКраткоеНаименование")) Тогда
			МассивПункт12_7.Добавить("краткое наименование: " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяФХЖГрузоотправительКраткоеНаименование"));
		КонецЕсли;
		Результат.Вставить("Пункт12_7", СтрСоединить(МассивПункт12_7, ", "));
		
		Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяФХЖОснованиеПлатежаНаименование")) Тогда
			Результат.Вставить("Пункт12_9", ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяФХЖОснованиеПлатежаНаименование", "")
											+ " №" + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяФХЖОснованиеПлатежаНомер")
											+ " от " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяФХЖОснованиеПлатежаДата")); 
		КонецЕсли;
		
		МассивПункт12_10 = Новый Массив;
		Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяФХЖНаименованиеПлательщика")) Тогда
			МассивПункт12_10.Добавить("плательщик: " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяФХЖНаименованиеПлательщика"));
		КонецЕсли;
		Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяФХЖБанковскиеРеквизитыНомерБанковскогоСчета")) Тогда
			МассивПункт12_10.Добавить("р/с: " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяФХЖБанковскиеРеквизитыНомерБанковскогоСчета"));
		КонецЕсли;
		Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяФХЖБанковскиеРеквизитыНаименованиеБанка")) Тогда
			МассивПункт12_10.Добавить("банк: " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяФХЖБанковскиеРеквизитыНаименованиеБанка"));
		КонецЕсли;
		Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяФХЖБанковскиеРеквизитыБИК")) Тогда
			МассивПункт12_10.Добавить("БИК: " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяФХЖБанковскиеРеквизитыБИК"));
		КонецЕсли;
		Результат.Вставить("Пункт12_10", СтрСоединить(МассивПункт12_10, ", "));
	КонецЕсли;
	
	// Подписи
	Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяИдентификаторФайла")) Тогда
		Результат.Вставить("Пункт8_7", ПредставлениеПодписи(ДанныеЗаполнения, "ТитулГрузоотправителяПодписант"));	
	КонецЕсли;
	Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаПриемкаИдентификаторФайла")) Тогда
		Результат.Вставить("Пункт8_8", ПредставлениеПодписи(ДанныеЗаполнения, "ТитулПеревозчикаПриемкаПодписант"));	
	КонецЕсли;
	Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузополучателяИдентификаторФайла")) Тогда
		Результат.Вставить("Пункт10_8", ПредставлениеПодписи(ДанныеЗаполнения, "ТитулГрузополучателяПодписант"));	
	КонецЕсли;
	Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаВыдачаИдентификаторФайла")) Тогда
		Результат.Вставить("Пункт10_9", ПредставлениеПодписи(ДанныеЗаполнения, "ТитулПеревозчикаВыдачаПодписант"));	
	КонецЕсли;
	Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаФХЖИдентификаторФайла")) Тогда
		МассивПункт12_11 = Новый Массив;
		Для Каждого СтрПодпись Из ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаФХЖПодписанты", Новый Массив) Цикл
			МассивПункт12_11.Добавить(ПредставлениеПодписи(СтрПодпись, ""));
		КонецЦикла;
		Результат.Вставить("Пункт12_11", СтрСоединить(МассивПункт12_11, Символы.ПС));	
	КонецЕсли;
	Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяФХЖИдентификаторФайла")) Тогда
		МассивПункт12_12 = Новый Массив;
		Для Каждого СтрПодпись Из ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяФХЖПодписанты", Новый Массив) Цикл
			МассивПункт12_12.Добавить(ПредставлениеПодписи(СтрПодпись, ""));
		КонецЦикла;
		Результат.Вставить("Пункт12_12", СтрСоединить(МассивПункт12_12, Символы.ПС));	
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

Функция ПредставлениеПодписи(ДанныеЗаполнения, ПрефиксРеквизитов)
	
	МассивРезультат = Новый Массив;
	МассивРезультат.Добавить(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, ПрефиксРеквизитов + "Должность"));
	МассивРезультат.Добавить("(Электронная подпись)");
	МассивРезультат.Добавить(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, ПрефиксРеквизитов + "Фамилия"));
	МассивРезультат.Добавить(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, ПрефиксРеквизитов + "Имя"));
	МассивРезультат.Добавить(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, ПрефиксРеквизитов + "Отчество"));	
	
	Возврат СтрСоединить(МассивРезультат, " ");
	
КонецФункции

Функция ПредставлениеСтороныПодписавшейДокумент(ДанныеЗаполнения)
	
	Результат = "";
	
	Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ЮЛ_ИНН")) Тогда
		Результат = "ИНН: " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ЮЛ_ИНН");
	ИначеЕсли ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ФЛ_ИНН")) Тогда 
		Результат = "ИНН: " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ФЛ_ИНН");
	ИначеЕсли ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ОрганВластиВыдавшийДокумент")) Тогда 
		Результат = ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ОрганВластиВыдавшийДокумент");
	ИначеЕсли ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ИнострЛицоБезУчетаСтатусФЛ_ЮЛ")) Тогда 
		Результат = ?(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ИнострЛицоБезУчетаСтатусФЛ_ЮЛ") = "ЮЛ", "иностранное юр. лицо", "иностранное физ. лицо")
					+ ?(ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ИнострЛицоБезУчетаНаименование")),
						" " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ИнострЛицоБезУчетаНаименование"), "")
					+ ?(ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ИнострЛицоБезУчетаСтрана")),
						" (" + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ИнострЛицоБезУчетаСтрана") + ")", "");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПредставлениеУчастника(ДанныеЗаполнения, ПрефиксРеквизитов)
	
	РезультатМассив = Новый Массив;
	
	// ЮЛ
	Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, ПрефиксРеквизитов + "ЮЛ_ИНН")) Тогда
		РезультатМассив.Добавить(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, ПрефиксРеквизитов + "ЮЛНаименование"));	
		РезультатМассив.Добавить("ИНН " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, ПрефиксРеквизитов + "ЮЛ_ИНН"));
	// ИП
	ИначеЕсли ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, ПрефиксРеквизитов + "ИП_ИНН")) Тогда
		РезультатМассив.Добавить(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, ПрефиксРеквизитов + "ИПФамилия")
									+ " " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, ПрефиксРеквизитов + "ИПИмя")
									+ " " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, ПрефиксРеквизитов + "ИПОтчество"));
		РезультатМассив.Добавить("ИНН " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, ПрефиксРеквизитов + "ИП_ИНН"));	
	// ФЛ
	ИначеЕсли ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, ПрефиксРеквизитов + "ФЛФамилия")) Тогда
		РезультатМассив.Добавить(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, ПрефиксРеквизитов + "ФЛФамилия")
									+ " " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, ПрефиксРеквизитов + "ФЛИмя")
									+ " " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, ПрефиксРеквизитов + "ФЛОтчество"));
	// Иностр. лицо
	ИначеЕсли ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, ПрефиксРеквизитов + "ИнострЛицоБезУчетаСтатусФЛ_ЮЛ")) Тогда
		РезультатМассив.Добавить(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, ПрефиксРеквизитов + "ИнострЛицоБезУчетаНаименование"));
		РезультатМассив.Добавить(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, ПрефиксРеквизитов + "ИнострЛицоБезУчетаСтрана"));	
	КонецЕсли;
	
	РезультатМассив.Добавить(ПредставлениеАдреса(ДанныеЗаполнения, ПрефиксРеквизитов + "Адрес"));
	
	Телефоны = Новый Массив;
	Для Каждого СтрТел Из ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, ПрефиксРеквизитов + "НомераТелефонов", Новый Массив) Цикл
		Телефоны.Добавить(СтрТел.Телефон);	
	КонецЦикла;
	Если Телефоны.Количество() > 0 Тогда
		РезультатМассив.Добавить("тел.: " + СтрСоединить(Телефоны, ", "));
	КонецЕсли;	
	
	Возврат СтрСоединить(РезультатМассив, ", ");
	
КонецФункции

Функция ПредставлениеАдреса(ДанныеЗаполнения, ПрефиксРеквизитов)
	
	РезультатМассив = Новый Массив;
	
	МассивРеквизитов = Новый Массив;
	МассивРеквизитов.Добавить("Индекс");
	МассивРеквизитов.Добавить("Город");
	МассивРеквизитов.Добавить("НаселенныйПункт");
	МассивРеквизитов.Добавить("Улица");
	МассивРеквизитов.Добавить("Дом");
	МассивРеквизитов.Добавить("Корпус");
	
	Для Каждого ИмяРеквизита Из МассивРеквизитов Цикл
		ПолноеИмяРеквизита = ПрефиксРеквизитов + ИмяРеквизита;
		Если ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, ПолноеИмяРеквизита)) Тогда
			РезультатМассив.Добавить(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, ПолноеИмяРеквизита));	
		ИначеЕсли ИмяРеквизита = "Город" Тогда
			Если ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, ПрефиксРеквизитов + "КодРегиона") = "77" Тогда
				РезультатМассив.Добавить("Москва г");
			ИначеЕсли ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, ПрефиксРеквизитов + "КодРегиона") = "78" Тогда
				РезультатМассив.Добавить("Санкт-Петербург г");
			ИначеЕсли ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, ПрефиксРеквизитов + "КодРегиона") = "92" Тогда
				РезультатМассив.Добавить("Севастополь г");	
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если РезультатМассив.Количество() = 0 
		И ЗначениеЗаполнено(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, ПрефиксРеквизитов + "Представление")) Тогда
			КодСтраны = ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, ПрефиксРеквизитов + "КодСтраны");
			Если КодСтраны <> "643" Тогда
				РезультатМассив.Добавить("Код страны: " + ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, ПрефиксРеквизитов + "КодСтраны"));	
			КонецЕсли;
			РезультатМассив.Добавить(ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, ПрефиксРеквизитов + "Представление"));
	КонецЕсли;
	
	Возврат СтрСоединить(РезультатМассив, ", ");
	
КонецФункции

Процедура УстановитьМинимальныеПоляПечати(ТабличныйДокумент)

	// Проверка на веб-клиент
	СисИнфо = Новый СистемнаяИнформация;
	ЗначениеБоковогоПоля = ?(ПустаяСтрока(СисИнфо.ИнформацияПрограммыПросмотра), 5, 10); 
	
	Если ТабличныйДокумент.ПолеСлева < ЗначениеБоковогоПоля Тогда
		ТабличныйДокумент.ПолеСлева = ЗначениеБоковогоПоля;
	КонецЕсли; 
	
	Если ТабличныйДокумент.ПолеСправа < ЗначениеБоковогоПоля Тогда
		ТабличныйДокумент.ПолеСправа = ЗначениеБоковогоПоля;
	КонецЕсли;
	
	Если ТабличныйДокумент.ПолеСверху < 5 Тогда
		ТабличныйДокумент.ПолеСверху = 5;
	КонецЕсли; 

	Если ТабличныйДокумент.ПолеСнизу < 5 Тогда
		ТабличныйДокумент.ПолеСнизу = 5;
	КонецЕсли;

КонецПроцедуры

#КонецЕсли