#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВидДокумента) = Ложь Тогда
		ВидДокумента = ОбменСГИСЭПД.ВидДокументаПоТипу(Перечисления.ВидыЭД.ЭТрН);
	КонецЕсли;
	
	Если РольУчастника = 0 Тогда 
		РольУчастника = 3;
	КонецЕсли;
	
	НазваниеСправочникаОрганизации = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьИмяПрикладногоСправочника("Организации");
	
	Если РольУчастника = 1 И ТипЗнч(СсылкаТитулГрузоотправителяПеревозчик) = Тип("СправочникСсылка." + НазваниеСправочникаОрганизации) Тогда
		Организация = СсылкаТитулГрузоотправителяПеревозчик;
	ИначеЕсли РольУчастника = 2 Тогда
		Если ТипЗнч(СсылкаТитулГрузоотправителяГрузополучатель) = Тип("СправочникСсылка." + НазваниеСправочникаОрганизации) Тогда
			Организация = СсылкаТитулГрузоотправителяГрузополучатель;
		ИначеЕсли ТипЗнч(СсылкаДопТитулПереадресовкаНовыйГрузополучатель) = Тип("СправочникСсылка." + НазваниеСправочникаОрганизации) Тогда
			Организация = СсылкаДопТитулПереадресовкаНовыйГрузополучатель;
		ИначеЕсли ТипЗнч(СсылкаТитулПереадресовкаНовыйГрузополучатель) = Тип("СправочникСсылка." + НазваниеСправочникаОрганизации) Тогда
			Организация = СсылкаТитулПереадресовкаНовыйГрузополучатель;
		КонецЕсли;
	ИначеЕсли РольУчастника = 3 И ТипЗнч(СсылкаТитулГрузоотправителяГрузоотправитель) = Тип("СправочникСсылка." + НазваниеСправочникаОрганизации) Тогда
		Если ТипЗнч(СсылкаТитулГрузоотправителяГрузоотправитель) = Тип("СправочникСсылка." + НазваниеСправочникаОрганизации) Тогда
			Организация = СсылкаТитулГрузоотправителяГрузоотправитель;
		ИначеЕсли ТипЗнч(СсылкаТитулГрузоотправителяСоставитель) = Тип("СправочникСсылка." + НазваниеСправочникаОрганизации) Тогда
			Организация = СсылкаТитулГрузоотправителяСоставитель;
		КонецЕсли;
	КонецЕсли;
	
	ОбменСГИСЭПД.УстановитьТекущийШаг(ЭтотОбъект);
	
КонецПроцедуры


Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	// Запишем новую версию
	ВерсияТитула = Неопределено;
	Если ДополнительныеСвойства.Свойство("ВерсияТитула", ВерсияТитула) Тогда
		ЗаписьВерсии = РегистрыСведений.ВерсииТитуловЭПД.СоздатьМенеджерЗаписи();
		ЗаписьВерсии.Документ = Ссылка;
		ЗаписьВерсии.Организация = Организация;
		ЗаписьВерсии.Титул = ВерсияТитула.Титул;
		ЗаписьВерсии.НомерВерсии = ВерсияТитула.НомерВерсии;
		
		ЗаписьВерсии.ИдентификаторФайла = ВерсияТитула.ИдентификаторФайла;
		ЗаписьВерсии.ДатаВерсии = ВерсияТитула.ДатаВерсии;
		ЗаписьВерсии.Записать();
	КонецЕсли;
	
	// Запишем реквизиты
	Если ВерсияТитула <> Неопределено Тогда
		СтруктураРеквизитовТитула = Неопределено;
		Если ДополнительныеСвойства.Свойство("СтруктураРеквизитов", СтруктураРеквизитовТитула) Тогда
			НомерВерсии = Неопределено;
			ДополнительныеСвойства.Свойство("НомерВерсии", НомерВерсии);
			Если НомерВерсии = Неопределено Тогда
				НомерВерсии = 0;
			КонецЕсли;
			НаборЗаписейЗначений = РегистрыСведений.ЗначенияРеквизитовДокументовЭПД.СоздатьНаборЗаписей();
			НаборЗаписейЗначений.Отбор.Документ.Установить(Ссылка);
			НаборЗаписейЗначений.Отбор.Титул.Установить(ВерсияТитула.Титул);
			НаборЗаписейЗначений.Отбор.НомерВерсии.Установить(НомерВерсии);
			Для Каждого КиЗ Из СтруктураРеквизитовТитула Цикл
				МассивЧастей = ОбменСГИСЭПДКлиентСервер.РазделитьСтрокуСоСложнымРазделителем(КиЗ.Ключ, "__");
				Если МассивЧастей.Количество() = 3 Тогда
					ИмяТабличнойЧасти = МассивЧастей[0];
					НомерСтрокиРеквизита = Число(МассивЧастей[1]);
					ИмяРеквизита = МассивЧастей[2];
				Иначе
					ИмяТабличнойЧасти = "";
					НомерСтрокиРеквизита = 0;
					ИмяРеквизита = КиЗ.Ключ;
				КонецЕсли;	
				НоваяЗапись = НаборЗаписейЗначений.Добавить();
				НоваяЗапись.Документ = Ссылка;
				НоваяЗапись.Организация = Организация;
				НоваяЗапись.Титул = ВерсияТитула.Титул;
				НоваяЗапись.НомерВерсии = НомерВерсии;
				НоваяЗапись.ИмяТабличнойЧасти = ИмяТабличнойЧасти;
				НоваяЗапись.НомерСтрокиРеквизита = НомерСтрокиРеквизита;
				НоваяЗапись.ИмяРеквизита = ИмяРеквизита;
				Если КиЗ.Значение = Неопределено Или ТипЗнч(КиЗ.Значение) = Тип("Строка") Тогда 
					НоваяЗапись.ЗначениеРеквизитаСтрока = КиЗ.Значение;
					НоваяЗапись.ТипРеквизита = Перечисления.ТипыРеквизитовЭПД.НеограниченнаяСтрока;
				ИначеЕсли ТипЗнч(КиЗ.Значение) = Тип("Число")
					Или ТипЗнч(КиЗ.Значение) = Тип("Дата")
					Или ТипЗнч(КиЗ.Значение) = Тип("Булево") Тогда
						НоваяЗапись.ЗначениеРеквизита = КиЗ.Значение;
						НоваяЗапись.ТипРеквизита = Перечисления.ТипыРеквизитовЭПД.ПростойКромеСтроки;
				Иначе
					НоваяЗапись.ЗначениеРеквизитаСсылка = КиЗ.Значение;
					НоваяЗапись.ТипРеквизита = Перечисления.ТипыРеквизитовЭПД.СсылочныйТип;
				КонецЕсли;
			КонецЦикла;
			НаборЗаписейЗначений.Записать();
		КонецЕсли;
	КонецЕсли;
	
	СтруктураДляРеестра = ОбменСГИСЭПДКлиентСервер.НоваяСтруктураДляРеестраЭПД();
	
	СтруктураДляРеестра.Ссылка = Ссылка;
	Если ЗначениеЗаполнено(СсылкаТитулГрузоотправителяСоставитель) Тогда
		СтруктураДляРеестра.Грузоотправитель = СсылкаТитулГрузоотправителяСоставитель;
	Иначе
		СтруктураДляРеестра.Грузоотправитель = СсылкаТитулГрузоотправителяГрузоотправитель;
	КонецЕсли;
	СтруктураДляРеестра.Грузополучатель = СсылкаТитулГрузоотправителяГрузополучатель;
	СтруктураДляРеестра.ДатаДокументаИБ = Дата;
	СтруктураДляРеестра.Комментарий = Комментарий;
	СтруктураДляРеестра.НомерДокументаИБ = Номер;
	СтруктураДляРеестра.НомерЭПД = ТитулГрузоотправителяТранспортнаяНакладнаяНомер;
	СтруктураДляРеестра.ДатаЭПД = ТитулГрузоотправителяТранспортнаяНакладнаяДата;
	СтруктураДляРеестра.Организация = Организация;
	СтруктураДляРеестра.Перевозчик = СсылкаТитулГрузоотправителяПеревозчик;
	СтруктураДляРеестра.ПометкаУдаления = ПометкаУдаления;
	СтруктураДляРеестра.Проведен = Проведен;	
	СтруктураДляРеестра.ТипСсылки = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(Ссылка));
	СтруктураДляРеестра.ТекущийШаг = ТекущийШаг;
	СтруктураДляРеестра.ТекущийШагВыполнен = ТекущийШагВыполнен;
	
	ОбменСГИСЭПД.ЗаписатьВРеестрЭПД(СтруктураДляРеестра);

КонецПроцедуры



Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ОбменСГИСЭПДПереопределяемый.ОбработкаЗаполненияЭТрН(ЭтотОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);

КонецПроцедуры


Процедура ПриКопировании(ОбъектКопирования)
	
	Если ОбъектКопирования.ЭтоВходящий = Истина Тогда
		ВызватьИсключение НСтр("ru='Нельзя копировать входящий документ'");
	КонецЕсли;
	
	ЭтотОбъект.УИДМинтранс = Неопределено;
	ЭтотОбъект.ТитулГрузоотправителяТранспортнаяНакладнаяНомер = Неопределено;
	ЭтотОбъект.ТитулГрузоотправителяТранспортнаяНакладнаяДата = Неопределено;
	
	ПрефиксыТитулов = ОбменСГИСЭПДКлиентСервер.ПрефиксыТитуловДокумента("Документ.ЭлектроннаяТранспортнаяНакладная");
	Для Каждого ПрефиксТитула Из ПрефиксыТитулов Цикл
		ЭтотОбъект[ПрефиксТитула + "ИдентификаторФайла"] = Неопределено;
		ЭтотОбъект[ПрефиксТитула + "ДатаФормированияФайла"] = Неопределено;
		ЭтотОбъект[ПрефиксТитула + "ВремяФормированияФайла"] = Неопределено;
	КонецЦикла;
	ЭтотОбъект.СсылкаТитулПереадресовкаНовыйГрузополучатель = Неопределено;
	ЭтотОбъект.СсылкаДопТитулПереадресовкаНовыйГрузополучатель = Неопределено;
	ЭтотОбъект.ИныеПолучателиСтрока = Неопределено;
	ЭтотОбъект.ТекущийШаг = Неопределено;
	ЭтотОбъект.ТекущийШагВыполнен = Ложь;
	ЭтотОбъект.РольУчастника = 3;	
	
	ЭтотОбъект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ЭТрН_Титул1");
	ЭтотОбъект.ТекущийПолученныйТитул = Неопределено;
	
КонецПроцедуры

#КонецЕсли
