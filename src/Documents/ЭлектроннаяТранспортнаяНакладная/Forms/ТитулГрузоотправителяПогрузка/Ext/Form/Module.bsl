&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	                                                           
	ОбменСГИСЭПД.ПриСозданииНаСервереПодчиненнойФормы(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
	//ЭтаФорма.ОписаниеРеквизитовФормы = ОписаниеРеквизитовФормыСервер();
	
	ОбменСГИСЭПДКлиентСервер.ЗаполнитьПредставлениеАдреса("ПредставлениеТитулГрузоотправителяАдресПогрузки", ЭтаФорма);
	
	КонтрагентОписаниеТипов = Новый ОписаниеТипов("СправочникСсылка." + ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьИмяПрикладногоСправочника("Контрагенты"));
		
	Если Параметры.Свойство("ФормаБезОбработки") = Ложь И ЭтаФорма.ЗапретитьИзменение = Ложь Тогда
		Элементы.СсылкаТитулГрузоотправителяЛицоОсуществляющееПогрузку.ОграничениеТипа = КонтрагентОписаниеТипов;
		
		ТекущаяДата = ТекущаяДатаСеанса();
		Если ЗначениеЗаполнено(ТитулГрузоотправителяЗаявленныеДатаВремяПодачи) = Ложь Тогда
			ТитулГрузоотправителяЗаявленныеДатаВремяПодачи = ОбменСГИСЭПДКлиентСервер.ОкруглениеДатыПриИзменении(ТекущаяДата);	
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТитулГрузоотправителяФактическиеДатаВремяПодачи) = Ложь Тогда
			ТитулГрузоотправителяФактическиеДатаВремяПодачи = ОбменСГИСЭПДКлиентСервер.ОкруглениеДатыПриИзменении(ТекущаяДата);	
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТитулГрузоотправителяФактическиеДатаВремяУбытия) = Ложь Тогда
			ТитулГрузоотправителяФактическиеДатаВремяУбытия = ОбменСГИСЭПДКлиентСервер.ОкруглениеДатыПриИзменении(ТекущаяДата);	
		КонецЕсли;
		
		ОбменСГИСЭПДКлиентСервер.ЗаполнениеСпискаСохраненныхАдресов(ЭтаФорма, 
					СсылкаТитулГрузоотправителяГрузоотправитель,
					Элементы.ПредставлениеТитулГрузоотправителяАдресПогрузки,
					Элементы.ПредставлениеТитулГрузоотправителяАдресПогрузки.СписокВыбора, Истина);
		
		МассивЭлементов = Новый Массив;
		МассивЭлементов.Добавить(Элементы.СсылкаТитулГрузоотправителяЛицоОсуществляющееПогрузку);
		ОбменСГИСЭПДКлиентСервер.ПоказатьОшибкиПоКонтрагентам(ЭтаФорма, МассивЭлементов);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТитулГрузоотправителяЗаявленныеДатаВремяПодачиРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	ОбменСГИСЭПДКлиентСервер.ОкруглениеДатыПриРегулировании(ТитулГрузоотправителяЗаявленныеДатаВремяПодачи, Направление, СтандартнаяОбработка);	

КонецПроцедуры

&НаКлиенте
Процедура ТитулГрузоотправителяФактическиеДатаВремяПодачиРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	ОбменСГИСЭПДКлиентСервер.ОкруглениеДатыПриРегулировании(ТитулГрузоотправителяФактическиеДатаВремяПодачи, Направление, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТитулГрузоотправителяФактическиеДатаВремяУбытияРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	ОбменСГИСЭПДКлиентСервер.ОкруглениеДатыПриРегулировании(ТитулГрузоотправителяФактическиеДатаВремяУбытия, Направление, СтандартнаяОбработка);
	
КонецПроцедуры




&НаКлиенте
Процедура ТитулГрузоотправителяЛицоОсуществляющееПогрузкуЯвляетсяГрузоотправителемПриИзменении(Элемент)
	
	ОбменСГИСЭПДКлиентСервер.ИзменитьОформлениеФормы(ЭтаФорма, Элемент.Имя);
	ИзменитьОформлениеКнопок(Новый ФиксированнаяСтруктура("ИмяКнопки, ИдентификаторСтроки"));
	
КонецПроцедуры


&НаКлиенте
Процедура Сохранить(Команда)
		
	ОбменСГИСЭПДКлиент.СохранитьПараметрыПодчиненнойФормы(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбменСГИСЭПДКлиент.ПриОткрытииПодчиненнойФормы(ЭтаФорма);
																		
КонецПроцедуры
			
&НаКлиенте
Функция ОписаниеРеквизитовФормы() Экспорт
	
	Возврат ОписаниеРеквизитовФормыСервер();
	
КонецФункции

&НаСервере
Функция ОписаниеРеквизитовФормыСервер()
	
	Возврат ОбменСГИСЭПД.ОписаниеРеквизитовФормы(ЭтаФорма);
		
КонецФункции

&НаКлиенте
Процедура ЗаполнитьТитулГрузоотправителяОснованиеОсуществленияПогрузки(Команда)
	
	ОбменСГИСЭПДКлиент.ВывестиФормуВводаДанных(ЭтаФорма, Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТитулГрузоотправителяРаботникНаПогрузке(Команда)
	
	ОбменСГИСЭПДКлиент.ВывестиФормуВводаДанных(ЭтаФорма, Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТитулГрузоотправителяВладелецПунктаПогрузки(Команда)
	
	ОбменСГИСЭПДКлиент.ВывестиФормуВводаДанных(ЭтаФорма, Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТитулГрузоотправителяОснованиеДоступаКПунктуПогрузки(Команда)
	
	ОбменСГИСЭПДКлиент.ВывестиФормуВводаДанных(ЭтаФорма, Команда.Имя);
	
КонецПроцедуры




&НаКлиенте
Процедура ПредставлениеТитулГОАдресПогрузкиАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Элементы.ПредставлениеТитулГрузоотправителяАдресПогрузки.СписокВыбора.Очистить();
	
	ОбменСГИСЭПДКлиент.АвтоПодборАдреса(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
	ОбменСГИСЭПДКлиентСервер.ЗаполнениеСпискаСохраненныхАдресов(ЭтаФорма, 
					СсылкаТитулГрузоотправителяГрузоотправитель,
					Элементы.ПредставлениеТитулГрузоотправителяАдресПогрузки,
					ДанныеВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеТитулГОАдресПогрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбменСГИСЭПДКлиент.ВводАдреса(Элемент.Имя, ЭтаФорма, СсылкаТитулГрузоотправителяГрузоотправитель);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеТитулГОАдресПогрузкиОбработкаВыбора(Элемент, Результат, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОбменСГИСЭПДКлиентСервер.ЗаполнитьРеквизитыАдреса(ЭтаФорма, Результат, Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура СсылкаТитулГрузоотправителяЛицоОсуществляющееПогрузкуПриИзменении(Элемент)
	
	ОбменСГИСЭПДКлиентСервер.ЗаполнитьРеквизитыПоСсылке(Элемент, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеТитулГОАдресПогрузкиПриИзменении(Элемент)
	
	Если ПустаяСтрока(ПредставлениеТитулГрузоотправителяАдресПогрузки) Тогда
		Элементы.ПредставлениеТитулГрузоотправителяАдресПогрузки_Ошибка.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры



&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОтметитьОбязательныеНеЗаполненныеЭлементыФормы" Тогда
		Если УникальныйИдентификатор <> Параметр Тогда
			Возврат;
		КонецЕсли;
		ОтметитьОбязательныеНеЗаполненныеЭлементыФормы(Параметр);
	ИначеЕсли ИмяСобытия = "ИзменитьОформлениеКнопокФормы" Тогда
		Если УникальныйИдентификатор <> Параметр.УникальныйИдентификаторОбновляемойФормы Тогда
			Возврат;
		КонецЕсли;
		ИзменитьОформлениеКнопок(Параметр);	 
	КонецЕсли;
	
КонецПроцедуры

#Область ОбъектыОбязательныеДляЗаполнения

&НаКлиенте
Процедура ИзменитьОформлениеКнопок(Параметр) Экспорт
	
	Если Не ЭтаФорма.НачальноеОформлениеВыполнено Тогда
		ЭтаФорма.ТребуетсяДополнительноеОформлениеКнопок = Истина;
		Если ЭтаФорма.СтруктураДополнительногоОформленияКнопок <> Неопределено Тогда
			ЭтаФорма.СтруктураДополнительногоОформленияКнопок = 
				Новый Структура("ИмяКнопки, ИдентификаторСтроки");
		Иначе
			ЭтаФорма.СтруктураДополнительногоОформленияКнопок = Параметр;
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	СтруктураСТекущимиДанными = ОбменСГИСЭПДКлиентСервер.ПолучитьСтруктуруПоТитулуИВерсии(ЭтаФорма);
	СтруктураДанныхОбъекта = ОбменСГИСЭПДКлиентСервер.ПолучитьСериализуемыйОбъектСДаннымиДокумента(ЭтаФорма);
	СтруктураСДаннымиФормыДляОформленияКнопок = ОбменСГИСЭПДКлиентСервер.СтруктураСДаннымиФормыДляОформленияКнопок(ЭтаФорма);
	
	Результат = ИзменитьОформлениеКнопокНаСервере(СтруктураСТекущимиДанными,
		Параметр.ИмяКнопки,
		Параметр.ИдентификаторСтроки,
		СтруктураДанныхОбъекта,
		СтруктураСДаннымиФормыДляОформленияКнопок);
		
	Если Результат.Успешно Тогда
		ЭтаФорма.АдресДереваСоответствийИтаблицыКнопок = Результат.НовыйАдресВХранилище;	
		МассивОформления = Результат.МассивОформления;
		ОбменСГИСЭПДКлиентСервер.ОформлениеКнопокНаФорме(ЭтаФорма, СтруктураСТекущимиДанными, МассивОформления);	
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИзменитьОформлениеКнопокНаСервере(Знач СтруктураСТекущимиДанными,
	ИмяКнопки = Неопределено,
	ИдентификаторСтроки = Неопределено,
	Знач СтруктураДанныхОбъекта,
	Знач СтруктураСДаннымиФормыДляОформленияКнопок)
	
	Результат = Новый Структура("Успешно, НовыйАдресВХранилище, МассивОформления", Ложь, Неопределено, Неопределено);
	
	НовыйАдресВХранилище = ОбменСГИСЭПД.ЗапуститьИзменениеОформленияКнопок(СтруктураСДаннымиФормыДляОформленияКнопок,
		СтруктураСТекущимиДанными, ИмяКнопки, ИдентификаторСтроки, СтруктураДанныхОбъекта);
		
	СтруктураДанных = ПолучитьИзВременногоХранилища(НовыйАдресВХранилище);
	Если СтруктураДанных = Неопределено Тогда
		Возврат Результат;	
	Иначе
		Результат.Успешно = Истина;
		Результат.НовыйАдресВХранилище = НовыйАдресВХранилище;
		Результат.МассивОформления = СтруктураДанных.МассивОформления;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции


&НаКлиенте
Процедура ОтметитьОбязательныеНеЗаполненныеЭлементыФормы(УникальныйИдентификаторОбновляемойФормы)
	
	ЭтаФорма.НачальноеОформлениеВыполнено = Истина;
	
	СтруктураСТекущимиДанными = ОбменСГИСЭПДКлиентСервер.ПолучитьСтруктуруПоТитулуИВерсии(ЭтаФорма);
	ОтметитьОбязательныеНеЗаполненныеЭлементыФормыНаСервере(СтруктураСТекущимиДанными);
	
КонецПроцедуры

&НаСервере
Процедура ОтметитьОбязательныеНеЗаполненныеЭлементыФормыНаСервере(Знач СтруктураСТекущимиДанными)
	
	ОбменСГИСЭПД.ОтметитьОбязательныеНеЗаполненныеЭлементыФормы(ЭтаФорма, СтруктураСТекущимиДанными);
	
КонецПроцедуры

#КонецОбласти