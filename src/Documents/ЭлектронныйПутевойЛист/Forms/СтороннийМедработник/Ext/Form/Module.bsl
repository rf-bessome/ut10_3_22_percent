&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	                                                           
	ОбменСГИСЭПД.ПриСозданииНаСервереПодчиненнойФормы(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
	Если ЗначениеЗаполнено(ЭтаФорма.Организация) И ЗначениеЗаполнено(МедорганизацияНаименование) = Ложь Тогда
			МедорганизацияНаименование = ЭтаФорма.Организация.Наименование;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФамилияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	ВыбратьМедработника();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьМедработника()
	
	ПараметрыВыбора = Новый Структура;
	ПараметрыВыбора.Вставить("РежимВыбора",Истина);

	ОбработкаВыбора = Новый ОписаниеОповещения("ВыбратьМедработника_Завершение", ЭтаФорма, );

	ОткрытьФорму("Справочник.ФизическиеЛица.ФормаВыбора", ПараметрыВыбора,
	        ЭтаФорма, , , , ОбработкаВыбора);
	        
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьМедработника_Завершение(Значение, ТекущаяСтрока) Экспорт
	
	Если Значение <> Неопределено Тогда  
		РеквизитыКонтрагента = РеквизитыФизЛица(Значение);
		РеквизитыКонтрагента.Свойство("Фамилия", Фамилия);
		РеквизитыКонтрагента.Свойство("Имя", Имя);
		РеквизитыКонтрагента.Свойство("Отчество", Отчество);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция РеквизитыФизЛица(ФизЛицо)
	
	Возврат ОбменСГИСЭПД.ПолучитьРеквизитыФизЛица(ФизЛицо);
	
КонецФункции


&НаКлиенте
Процедура Сохранить(Команда)
		
	ОбменСГИСЭПДКлиент.СохранитьПараметрыПодчиненнойФормы(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбменСГИСЭПДКлиент.ПриОткрытииПодчиненнойФормы(ЭтаФорма);
																		
КонецПроцедуры
			
&НаКлиенте
Функция ОписаниеРеквизитовФормы() Экспорт
	
	Возврат ОписаниеРеквизитовФормыСервер();
	
КонецФункции

&НаСервере
Функция ОписаниеРеквизитовФормыСервер()
	
	Возврат ОбменСГИСЭПД.ОписаниеРеквизитовФормы(ЭтаФорма);
		
КонецФункции

//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_ДобавлениеПоляВвода(Команда)
	
	ИмяТаблицыИПоля = СтрЗаменить(Команда.Имя, "ДобавлениеПоляВвода", "");
	МассивЧастей = ОбменСГИСЭПДКлиентСервер.РазделитьСтрокуСоСложнымРазделителем(ИмяТаблицыИПоля, "__");
	
	ДобавлениеПоляВводаСервер(МассивЧастей[0], МассивЧастей[1]);
	
КонецПроцедуры

&НаСервере
Процедура ДобавлениеПоляВводаСервер(ИмяТаблицы, ИмяПоля)
	
	ОбменСГИСЭПД.СоздатьЭлементыВводаЗначенийСписка(ЭтаФорма, ИмяТаблицы, ИмяПоля, Истина);
	
КонецПроцедуры

//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_ПолеЗначенияСпискаИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;	
	МассивЧастей = ОбменСГИСЭПДКлиентСервер.РазделитьСтрокуСоСложнымРазделителем(Элемент.Имя, "__");	
	УстановитьФорматированноеЗначениеПоляВвода(МассивЧастей[0], МассивЧастей[1], Число(МассивЧастей[2]), Текст);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФорматированноеЗначениеПоляВвода(ИмяТаблицы, ИмяПоля, НомерСтроки, Текст)
	
	ТекущиеДанные = ЭтаФорма[ИмяТаблицы][НомерСтроки];	 
	ОбменСГИСЭПД.УстановитьФорматированноеЗначениеПоляВвода(ЭтаФорма, ТекущиеДанные, ИмяПоля, Текст);
	
КонецПроцедуры