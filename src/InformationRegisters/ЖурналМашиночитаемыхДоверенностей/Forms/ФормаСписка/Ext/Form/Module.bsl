// @strict-types

#Область ОписаниеПеременных

&НаКлиенте
Перем ТекущаяДоверенность; // ОпределяемыйТип.МашиночитаемаяДоверенность

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДобавлениеИзменениеМЧДОрганизацийЭДО = Справочники.МашиночитаемыеДоверенностиОрганизаций.ЕстьПравоИзменения();
	ЕстьПравоИзменения = МашиночитаемыеДоверенности.ИмеетсяПравоИзмененияМЧД();
	Элементы.ЗагрузитьИзРеестраФНС.Видимость = ЕстьПравоИзменения;
	Элементы.ЗагрузитьИзФайла.Видимость = ЕстьПравоИзменения;
	Элементы.СоздатьЗаявлениеНаРегистрацию.Видимость = ЕстьПравоИзменения И ДобавлениеИзменениеМЧДОрганизацийЭДО;
	Элементы.СоздатьЗаявлениеНаРегистрациюПередоверия.Видимость = ЕстьПравоИзменения И ДобавлениеИзменениеМЧДОрганизацийЭДО;
	
	Поля = Новый Массив; // Массив Из Строка
	Поля.Добавить("ВсеДоверители, ВсеПредставители");
	Список.УстановитьОграниченияИспользованияВОтборе(Поля);
	УстановитьУсловноеОформление();
	РежимВыбора = МашиночитаемыеДоверенностиКлиентСервер.СвойствоСтруктуры(Параметры, "РежимВыбора", Ложь);
	Элементы.Выбрать.Видимость = РежимВыбора;
	ОбновитьИспользованиеПараметровСписка();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПодключитьОбработчикОжидания("Подключаемый_ОбновитьОбластьПредпросмотра", 0.2, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаПерехода(ОбъектПерехода, СтандартнаяОбработка)
	ВыделитьДоверенность(ОбъектПерехода);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = МашиночитаемыеДоверенностиКлиент.ИмяОповещенияПоказатьДоверенностиОрганизации() И Параметр
		<> ВыбраннаяОрганизация Тогда
		ВыбраннаяОрганизация = Параметр;
		ОбновитьОтборПоОрганизации();
	КонецЕсли;
	
	Если ИмяСобытия = МашиночитаемыеДоверенностиКлиент.ИмяОповещенияОИзмененииМЧД() Тогда
		
		Элементы.Список.Обновить();
		
		Если Параметр.Доверенности.Найти(ТекущаяДоверенность) <> Неопределено Тогда
			ПодключитьОбработчикОжидания("Подключаемый_ОбновитьОбластьПредпросмотра", 0.2, Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)

	Элементы.ПоказыватьПомеченныеНаУдаление.Пометка = ПоказыватьПомеченныеНаУдаление;
	ОбновитьИспользованиеПараметровСписка();

	Элементы.ПоказыватьПредпросмотр.Пометка = ПоказыватьПредпросмотр;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияОтборПоОрганизацииОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ВыбраннаяОрганизация = Неопределено;
	ОбновитьОтборПоОрганизации();
	ПодключитьОбработчикОжидания("Подключаемый_ОбновитьРасположениеЭлементов", 0.1, Истина);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, КлючЗаписиИСтрокиДинамическогоСписка)
	
	Для Каждого КлючИЗначение Из КлючЗаписиИСтрокиДинамическогоСписка Цикл
		
		СтрокаСписка = КлючИЗначение.Значение; // СтрокаДинамическогоСписка
		УстановитьОформлениеСубъектаДоверенности(СтрокаСписка, "ВсеДоверители");
		УстановитьОформлениеСубъектаДоверенности(СтрокаСписка, "ВсеПредставители");
		
		Если СтрокаСписка.Данные.ДатаОкончанияДействия < ТекущаяДатаСеанса() Тогда
			ДатаОкончанияДействия = СтрокаСписка.Оформление.Получить("ДатаОкончанияДействия");
			Если ЗначениеЗаполнено(ДатаОкончанияДействия) Тогда
				ДатаОкончанияДействия.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветОтрицательногоЧисла);
			КонецЕсли;
		КонецЕсли;
		
		ТекстПередоверия = "";
		Если ЗначениеЗаполнено(СтрокаСписка.Данные.Номер) Тогда
			КоличествоПередоверий = 
				КоличествоПередоверий(СтрокаСписка.Данные.Номер, КлючЗаписиИСтрокиДинамическогоСписка);
		Иначе
			КоличествоПередоверий = 0;
		КонецЕсли;
		
		Если ТипЗнч(СтрокаСписка.Данные.Доверенность) <> Тип("СправочникСсылка.МЧД003") Тогда
			ТекстПередоверия = "";
		ИначеЕсли КоличествоПередоверий > 0 Тогда
			ТекстПередоверия = НСтр("ru='Передоверена %1 раз(а)'");
			ТекстПередоверия = СтрШаблон(ТекстПередоверия, КоличествоПередоверий);
		ИначеЕсли СтрокаСписка.Данные.ПередовериеРазрешено Тогда
			ТекстПередоверия = НСтр("ru='Разрешено передоверие'");
		ИначеЕсли ЗначениеЗаполнено(СтрокаСписка.Данные.НомерРодительскойДоверенности) Тогда
			ТекстПередоверия = НСтр("ru='Это передоверие'");
		Иначе
			ТекстПередоверия = НСтр("ru='Без права передоверия'");
		КонецЕсли;
		
		
		СтрокаСписка.Данные.Передоверие = ТекстПередоверия;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.Доверенность) Тогда
		РежимВыбора = МашиночитаемыеДоверенностиКлиентСервер.СвойствоСтруктуры(Параметры, "РежимВыбора", Ложь); // Булево
		Если РежимВыбора Тогда
			ОповеститьОВыборе(ТекущиеДанные.Доверенность);
		Иначе
			ОбработкаВыбора = Новый ОписаниеОповещения("ВыборДоверенностиЗавершение", ЭтаФорма);
			ПоказатьЗначение(ОбработкаВыбора, ТекущиеДанные.Доверенность);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	Доверенность = МашиночитаемыеДоверенностиКлиентСервер.СвойствоСтруктуры(Элементы.Список.ТекущиеДанные, "Доверенность",
		Неопределено);
	Если ТекущаяДоверенность <> Доверенность Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ОбновитьОбластьПредпросмотра", 0.2, Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#Область ДействияНадДоверенностями

&НаКлиенте
Процедура СоздатьЗаявлениеНаРегистрацию(Команда)
	
	ОбработчикПослеСоздания = Новый ОписаниеОповещения("Подключаемый_СоздатьДоверенностьЗавершение", ЭтаФорма);
	МашиночитаемыеДоверенностиКлиент.СоздатьМЧД(ОбработчикПослеСоздания);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗаявлениеНаРегистрациюПередоверия(Команда)
	
	ОбработчикПослеСоздания = Новый ОписаниеОповещения("Подключаемый_СоздатьДоверенностьЗавершение", ЭтаФорма);
	МашиночитаемыеДоверенностиКлиент.СоздатьМЧДПередоверия(ОбработчикПослеСоздания);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзРеестраФНС(Команда)
	
	ОбработчикПослеЗагрузки = Новый ОписаниеОповещения("Подключаемый_ЗагрузитьДоверенностьИзРеестраЗавершение",
		ЭтаФорма);
	МашиночитаемыеДоверенностиКлиент.ПолучитьДанныеМЧД(ОбработчикПослеЗагрузки, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда)
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.Заголовок          = НСтр("ru = 'Выберите архив с доверенностью и подписью'"); 
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
	ДиалогВыбораФайла.Фильтр             = НСтр("ru = 'Архив|*.zip'");
	ДиалогВыбораФайла.Расширение         = "zip";	 
	
	МассивПомещенных = Новый Массив;
	Если ПоместитьФайлы(, МассивПомещенных, ДиалогВыбораФайла,,УникальныйИдентификатор) Тогда
		Для Каждого ПомещенныйФайл из МассивПомещенных Цикл 
			МашиночитаемыеДоверенностиКлиент.ЗагрузитьМЧДИзФайла(ПомещенныйФайл.Хранение);
			Элементы.Список.Обновить();
		КонецЦикла;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьДоверенность(Команда)
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ОповеститьОВыборе(ТекущиеДанные.Доверенность);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ПрочиеКоманды

&НаКлиенте
Процедура ПоказыватьПомеченныеНаУдаление(Команда)
	ПоказыватьПомеченныеНаУдаление = Не ПоказыватьПомеченныеНаУдаление;
	Элементы.ПоказыватьПомеченныеНаУдаление.Пометка = ПоказыватьПомеченныеНаУдаление;
	ОбновитьИспользованиеПараметровСписка();
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьПредпросмотр(Команда)
	ПоказыватьПредпросмотр = Не ПоказыватьПредпросмотр;
	Элементы.ПоказыватьПредпросмотр.Пометка = ПоказыватьПредпросмотр;
	ПодключитьОбработчикОжидания("Подключаемый_ОбновитьОбластьПредпросмотра", 0.2, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКлассификаторПолномочий(Команда)
	ОткрытьФорму("Справочник.КлассификаторПолномочийФНСМЧД003.ФормаСписка");
КонецПроцедуры

&НаКлиенте
Процедура Поиск(Команда)
	ТекущийЭлемент = Элементы.ПоисковаяСтрока;
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОформлениеКолонокДинамическогоСписка

&НаСервереБезКонтекста
Процедура УстановитьОформлениеСубъектаДоверенности(СтрокаСписка, ПолеОписанияСубъекта)
	
	ДанныеСубъекта = МашиночитаемыеДоверенности.JSONЗначение(СтрокаСписка.Данные[ПолеОписанияСубъекта]);
	Если ЗначениеЗаполнено(ДанныеСубъекта) Тогда
		Массив = Новый Массив;
		Для Каждого Субъект Из ДанныеСубъекта Цикл
			Массив.Добавить(МашиночитаемыеДоверенности.ПредставлениеСубъектаДоверенности(Субъект));
		КонецЦикла;
		КоличествоСубъектов = Массив.Количество();
		Если КоличествоСубъектов > 1 Тогда
			Массив[0] = СтрШаблон("[%2] %1", Массив[0], КоличествоСубъектов);
		КонецЕсли;
		СтрокаСписка.Данные[ПолеОписанияСубъекта] = СтрСоединить(Массив, "; ");
	Иначе
		СтрокаСписка.Данные[ПолеОписанияСубъекта] = "";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура Подключаемый_СоздатьДоверенностьЗавершение(Доверенность, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(Доверенность) Тогда
		ОбновитьСписокИСфокусироватьсяНаДоверенности(Доверенность);
		
		ТекущиеДанные = Элементы.Список.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено И Не ТекущиеДанные.Действует Тогда
			Текст = НСтр("ru = 'Не удалось обновить статус доверенности.
				|Обновить вручную статус можно в форме доверенности по кнопке ""Обновить статус из реестра ФНС"".'");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокИСфокусироватьсяНаДоверенности(Доверенность)
	Элементы.Список.Обновить();
	ВыделитьДоверенность(Доверенность);
КонецПроцедуры

&НаСервере
Процедура ВыделитьДоверенность(Доверенность)
	КлючЗаписи = РегистрыСведений.ЖурналМашиночитаемыхДоверенностей.СоздатьКлючЗаписи(Новый Структура("Хеш",
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Доверенность, "ХешФайла")));
	Элементы.Список.ТекущаяСтрока = КлючЗаписи;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОтозватьДоверенностьЗавершение(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗагрузитьДоверенностьИзРеестраЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат.СсылкаНаДоверенность) Тогда
		ПоказатьЗначение(Новый ОписаниеОповещения, Результат.СсылкаНаДоверенность);
		Элементы.Список.Обновить();
		
		ЗагруженныеМЧД = МашиночитаемыеДоверенностиКлиентСервер.ЗначениеВМассиве(Результат.СсылкаНаДоверенность);
		МашиночитаемыеДоверенностиКлиент.ОповеститьОИзмененииМЧД(ЗагруженныеМЧД);
	ИначеЕсли Результат.Свойство("ТекстОшибки") Тогда
		МашиночитаемыеДоверенностиКлиент.ПоказатьПредупреждениеПриЗагрузкеМЧД(Результат.ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗагрузитьДоверенностьИзФайлаЗавершение(ПомещенныйФайл, ДополнительныеПараметры) Экспорт
	
	Если ПомещенныйФайл = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	АдресВоВременномХранилище = ПомещенныйФайл.Хранение;
	МашиночитаемыеДоверенностиКлиент.ЗагрузитьМЧДИзФайла(АдресВоВременномХранилище);
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СохранитьДоверенностьЗавершение(ПолученныеФайлы, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(ПолученныеФайлы) Тогда
		ШаблонСообщения = НСтр(
			"ru = 'Подписанная доверенность сохранена в файл %1 и может быть передана контрагентам любым удобным способом.'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, ПолученныеФайлы[0].ПолноеИмя);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборДоверенностиЗавершение(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	Элементы.Список.Обновить();
КонецПроцедуры

#КонецОбласти

#Область ОбластьПредпросмотра

&НаКлиенте
Процедура Подключаемый_ОбновитьОбластьПредпросмотра() Экспорт
	
	Доверенность = МашиночитаемыеДоверенностиКлиентСервер.СвойствоСтруктуры(Элементы.Список.ТекущиеДанные, "Доверенность",
		Неопределено);
	Элементы.ГруппаПредпросмотра.Видимость = ПоказыватьПредпросмотр;
	Если ПоказыватьПредпросмотр И ЗначениеЗаполнено(Доверенность) Тогда
		ПереформироватьТабличныйДокумент(Доверенность);
		ПоказатьСтатусыДоверенности();
		ТекущаяДоверенность = Доверенность;
	Иначе
		ТабличныйДокументПредпросмотра = Новый ТабличныйДокумент;
		ТекущаяДоверенность = Неопределено;
		Элементы.ГруппаСостоянияИСтатусы.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьРасположениеЭлементов() Экспорт
	
	Элементы.ГруппаОбновленияРасположенияРеквизитов.Видимость = Не Элементы.ГруппаОбновленияРасположенияРеквизитов.Видимость;
	
	Если Элементы.ГруппаОбновленияРасположенияРеквизитов.Видимость Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ОбновитьРасположениеЭлементов", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПереформироватьТабличныйДокумент(Знач Ссылка)

	Попытка
		ТабличныйДокументПредпросмотра = МашиночитаемыеДоверенности.ТабличныйДокументМЧДПоСсылке(Ссылка);
		СтатусыДоверенности = ПолучитьСтатусыДоверенности(Ссылка);
	Исключение
		ТабличныйДокументПредпросмотра = Новый ТабличныйДокумент;
		СтатусыДоверенности = Неопределено;
	КонецПопытки;

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСтатусыДоверенности()
	
	Если СтатусыДоверенности <> Неопределено Тогда
		Элементы.ГруппаСостоянияИСтатусы.Видимость = Истина;
		
		МашиночитаемыеДоверенностиКлиент.ОформитьГруппуСостоянияИСтатусыМЧД(ЭтаФорма, СтатусыДоверенности.Подписана,
			СтатусыДоверенности.Верна, СтатусыДоверенности.Отозвана, СтатусыДоверенности.ДатаОтзыва,
			СтатусыДоверенности.СтатусВРеестреФНС);
	Иначе
		Элементы.ГруппаСостоянияИСтатусы.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервереБезКонтекста
Функция КоличествоПередоверий(НомерДоверенности, СтрокиДинамическогоСписка)
	
	Счетчик = 0;
	Для Каждого КлючИЗначение Из СтрокиДинамическогоСписка Цикл
		СтрокаСписка = КлючИЗначение.Значение; // СтрокаДинамическогоСписка
		Если СтрокаСписка.Данные.НомерРодительскойДоверенности = НомерДоверенности
			И (СтрокаСписка.Данные.ДатаВыдачи <= ТекущаяДатаСеанса()
				И СтрокаСписка.Данные.ДатаОкончанияДействия >= ТекущаяДатаСеанса())
			И СтрокаСписка.Данные.Подписана
			И СтрокаСписка.Данные.Действует Тогда
			Счетчик = Счетчик + 1;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Счетчик;
	
КонецФункции

&НаСервере
Процедура ОбновитьИспользованиеПараметровСписка()
	ПараметрПоказыватьПомеченныеНаУдаление = Новый ПараметрКомпоновкиДанных("ПоказыватьПомеченныеНаУдаление");
	ПараметрыДанных = Список.КомпоновщикНастроек.Настройки.ПараметрыДанных;
	ЗначениеПараметра = ПараметрыДанных.НайтиЗначениеПараметра(ПараметрПоказыватьПомеченныеНаУдаление);
	ЗначениеПараметра.Значение = ПоказыватьПомеченныеНаУдаление;
	ЗначениеПараметра.Использование = Не ПоказыватьПомеченныеНаУдаление;
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтборПоОрганизации()
	
	СвойстваДинамическогоСписка = Новый Структура("ТекстЗапроса, ОсновнаяТаблица, ДинамическоеСчитываниеДанных");	
	СвойстваДинамическогоСписка.ДинамическоеСчитываниеДанных = Истина;
	СвойстваДинамическогоСписка.ОсновнаяТаблица = "РегистрСведений.ЖурналМашиночитаемыхДоверенностей";
	Если ЗначениеЗаполнено(ВыбраннаяОрганизация) Тогда
		Элементы.ДекорацияОтборПоОрганизации.Заголовок = МашиночитаемыеДоверенности.ФорматированнаяСтрока(НСтр(
			"ru='<b>Отбор:</b> %1 <a href=""ОтменитьОтбор"" ><img src=""ПолеВводаОчиститьБЭД""></a>';"),
			ВыбраннаяОрганизация);
		Элементы.ДекорацияОтборПоОрганизации.Видимость = Истина;
		СвойстваДинамическогоСписка.ТекстЗапроса =
		"ВЫБРАТЬ
		|	ЖурналМашиночитаемыхДоверенностей.Хеш,
		|	ЖурналМашиночитаемыхДоверенностей.Номер,
		|	ЖурналМашиночитаемыхДоверенностей.ВсеДоверители,
		|	ЖурналМашиночитаемыхДоверенностей.ВсеПредставители,
		|	ЖурналМашиночитаемыхДоверенностей.ПредставлениеДоверители,
		|	ЖурналМашиночитаемыхДоверенностей.ПредставлениеПредставители,		
		|	ЖурналМашиночитаемыхДоверенностей.ДатаВыдачи,
		|	ЖурналМашиночитаемыхДоверенностей.ДатаОкончанияДействия,
		|	ЖурналМашиночитаемыхДоверенностей.Верна,
		|	ЖурналМашиночитаемыхДоверенностей.Действует,
		|	ЖурналМашиночитаемыхДоверенностей.ПометкаУдаления,
		|	ЖурналМашиночитаемыхДоверенностей.Доверенность,
		|	ЖурналМашиночитаемыхДоверенностей.ДатаИзменения,
		|	ВЫБОР
		|		КОГДА ЖурналМашиночитаемыхДоверенностей.Доверенность ССЫЛКА Справочник.МЧД003
		|		И ВЫРАЗИТЬ(ЖурналМашиночитаемыхДоверенностей.Доверенность КАК Справочник.МЧД003).Подписана = ЛОЖЬ
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ИндексКартинки,
		|	ВЫБОР
		|		КОГДА ЖурналМашиночитаемыхДоверенностей.Доверенность ССЫЛКА Справочник.МЧД003
		|		И ВЫРАЗИТЬ(ЖурналМашиночитаемыхДоверенностей.Доверенность КАК Справочник.МЧД003).Подписана = ЛОЖЬ
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоЧерновик,
		|	ВЫБОР
		|		КОГДА ЖурналМашиночитаемыхДоверенностей.Доверенность ССЫЛКА Справочник.МЧД003
		|			ТОГДА ВЫРАЗИТЬ(ЖурналМашиночитаемыхДоверенностей.Доверенность КАК Справочник.МЧД003).ПередовериеРазрешено
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ПередовериеРазрешено,
		|	ВЫБОР
		|		КОГДА ЖурналМашиночитаемыхДоверенностей.Доверенность ССЫЛКА Справочник.МЧД003
		|			ТОГДА ВЫРАЗИТЬ(ЖурналМашиночитаемыхДоверенностей.Доверенность КАК Справочник.МЧД003).НомерРодительскойДоверенности
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК НомерРодительскойДоверенности,
		|	ВЫБОР
		|		КОГДА ЖурналМашиночитаемыхДоверенностей.Доверенность ССЫЛКА Справочник.МЧД003
		|			ТОГДА ВЫРАЗИТЬ(ЖурналМашиночитаемыхДоверенностей.Доверенность КАК Справочник.МЧД003).Подписана
		|		ИНАЧЕ Ложь
		|	КОНЕЦ КАК Подписана,
		|	"""" КАК Передоверие		
		|
		|ИЗ
		|	РегистрСведений.ЖурналМашиночитаемыхДоверенностей КАК ЖурналМашиночитаемыхДоверенностей
		|ГДЕ
		|	ЖурналМашиночитаемыхДоверенностей.Доверенность.Организация = &Организация
		|	ИЛИ (ЖурналМашиночитаемыхДоверенностей.Доверенность.Доверители.ИНН = &ОрганизацияИНН
		|		И ЖурналМашиночитаемыхДоверенностей.Доверенность.Доверители.КПП = &ОрганизацияКПП)
		|{ГДЕ
		|	ЖурналМашиночитаемыхДоверенностей.ПометкаУдаления = &ПоказыватьПомеченныеНаУдаление}
		|";	 
		 
		РеквизитИНН = ЭлектронныеДокументыПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("ИННОрганизации");
		РеквизитКПП = ЭлектронныеДокументыПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("КППОрганизации");
		ИННКППОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВыбраннаяОрганизация, РеквизитИНН+", "+РеквизитКПП);
		УстановитьСвойстваДинамическогоСписка(Элементы.Список, СвойстваДинамическогоСписка);
		Список.Параметры.УстановитьЗначениеПараметра("Организация", ВыбраннаяОрганизация);
		Список.Параметры.УстановитьЗначениеПараметра("ОрганизацияИНН", ИННКППОрганизации[РеквизитИНН]);
		Список.Параметры.УстановитьЗначениеПараметра("ОрганизацияКПП", ИННКППОрганизации[РеквизитКПП]);

	Иначе
		Элементы.ДекорацияОтборПоОрганизации.Видимость = Ложь;
		СвойстваДинамическогоСписка.ТекстЗапроса =
		"ВЫБРАТЬ
		|	ЖурналМашиночитаемыхДоверенностей.Хеш,
		|	ЖурналМашиночитаемыхДоверенностей.Номер,
		|	ЖурналМашиночитаемыхДоверенностей.ПредставлениеДоверители,
		|	ЖурналМашиночитаемыхДоверенностей.ПредставлениеПредставители,		
		|	ЖурналМашиночитаемыхДоверенностей.ВсеДоверители,
		|	ЖурналМашиночитаемыхДоверенностей.ВсеПредставители,
		|	ЖурналМашиночитаемыхДоверенностей.ДатаВыдачи,
		|	ЖурналМашиночитаемыхДоверенностей.ДатаОкончанияДействия,
		|	ЖурналМашиночитаемыхДоверенностей.Верна,
		|	ЖурналМашиночитаемыхДоверенностей.Действует,
		|	ЖурналМашиночитаемыхДоверенностей.ПометкаУдаления,
		|	ЖурналМашиночитаемыхДоверенностей.Доверенность,
		|	ЖурналМашиночитаемыхДоверенностей.ДатаИзменения,
		|	ВЫБОР
		|		КОГДА ЖурналМашиночитаемыхДоверенностей.Доверенность ССЫЛКА Справочник.МЧД003
		|		И ВЫРАЗИТЬ(ЖурналМашиночитаемыхДоверенностей.Доверенность КАК Справочник.МЧД003).Подписана = ЛОЖЬ
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ИндексКартинки,
		|	ВЫБОР
		|		КОГДА ЖурналМашиночитаемыхДоверенностей.Доверенность ССЫЛКА Справочник.МЧД003
		|		И ВЫРАЗИТЬ(ЖурналМашиночитаемыхДоверенностей.Доверенность КАК Справочник.МЧД003).Подписана = ЛОЖЬ
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоЧерновик,
		|	ВЫБОР
		|		КОГДА ЖурналМашиночитаемыхДоверенностей.Доверенность ССЫЛКА Справочник.МЧД003
		|			ТОГДА ВЫРАЗИТЬ(ЖурналМашиночитаемыхДоверенностей.Доверенность КАК Справочник.МЧД003).ПередовериеРазрешено
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ПередовериеРазрешено,
		|	ВЫБОР
		|		КОГДА ЖурналМашиночитаемыхДоверенностей.Доверенность ССЫЛКА Справочник.МЧД003
		|			ТОГДА ВЫРАЗИТЬ(ЖурналМашиночитаемыхДоверенностей.Доверенность КАК Справочник.МЧД003).НомерРодительскойДоверенности
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК НомерРодительскойДоверенности,
		|	ВЫБОР
		|		КОГДА ЖурналМашиночитаемыхДоверенностей.Доверенность ССЫЛКА Справочник.МЧД003
		|			ТОГДА ВЫРАЗИТЬ(ЖурналМашиночитаемыхДоверенностей.Доверенность КАК Справочник.МЧД003).Подписана
		|		ИНАЧЕ Ложь
		|	КОНЕЦ КАК Подписана,
		|	"""" КАК Передоверие
		|
		|ИЗ
		|	РегистрСведений.ЖурналМашиночитаемыхДоверенностей КАК ЖурналМашиночитаемыхДоверенностей
		|{ГДЕ
		|	ЖурналМашиночитаемыхДоверенностей.ПометкаУдаления = &ПоказыватьПомеченныеНаУдаление}";
		
		УстановитьСвойстваДинамическогоСписка(Элементы.Список, СвойстваДинамическогоСписка);
	КонецЕсли;
КонецПроцедуры

// Установить текст запроса, основную таблицу или динамическое считывание в динамическом списке.
// Устанавливать эти свойства следует за один вызов этой процедуры, чтобы не снижалась производительность.
//
// Параметры:
//  Список - ТаблицаФормы - элемент формы динамического списка, для которого устанавливаются свойства.
//  СвойстваСписка - см. СтруктураСвойствДинамическогоСписка
//
Процедура УстановитьСвойстваДинамическогоСписка(Список, СвойстваСписка) Экспорт
	
	Форма = Список.Родитель;
	ТипФормаКлиентскогоПриложения = Тип("ФормаКлиентскогоПриложения");
	
	Пока ТипЗнч(Форма) <> ТипФормаКлиентскогоПриложения Цикл
		Форма = Форма.Родитель;
	КонецЦикла;
	
	ДинамическийСписок = Форма[Список.ПутьКДанным];
	ТекстЗапроса = СвойстваСписка.ТекстЗапроса;
	
	Если Не ПустаяСтрока(ТекстЗапроса) Тогда
		ДинамическийСписок.ТекстЗапроса = ТекстЗапроса;
	КонецЕсли;
	
	ОсновнаяТаблица = СвойстваСписка.ОсновнаяТаблица;
	
	ДинамическийСписок.ОсновнаяТаблица = ОсновнаяТаблица;
	
	ДинамическоеСчитываниеДанных = СвойстваСписка.ДинамическоеСчитываниеДанных;
	
	Если ТипЗнч(ДинамическоеСчитываниеДанных) = Тип("Булево") Тогда
		ДинамическийСписок.ДинамическоеСчитываниеДанных = ДинамическоеСчитываниеДанных;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	ОформлениеПроблемныхЗаписей = УсловноеОформление.Элементы.Добавить();
	ПоляСписка = ОформлениеПроблемныхЗаписей.Поля.Элементы.Добавить();
	ПоляСписка.Поле = Новый ПолеКомпоновкиДанных(Элементы.Список.Имя);
	ГруппаУсловийИ = ОформлениеПроблемныхЗаписей.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаУсловийИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	УсловиеНеЧерновик = ГруппаУсловийИ.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	УсловиеНеЧерновик.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ЭтоЧерновик");
	УсловиеНеЧерновик.ПравоеЗначение = Ложь;
	УсловиеНеЧерновик.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ГруппаУсловий = ГруппаУсловийИ.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаУсловий.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	УсловиеНеВерна = ГруппаУсловий.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	УсловиеНеВерна.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.Верна");
	УсловиеНеВерна.ПравоеЗначение = Ложь;
	УсловиеНеВерна.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	УсловиеНеДействует = ГруппаУсловий.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	УсловиеНеДействует.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.Действует");
	УсловиеНеДействует.ПравоеЗначение = Ложь;
	УсловиеНеДействует.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОформлениеПроблемныхЗаписей.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветОтрицательногоЧисла);

	ОформлениеПомеченныхНаУдаление = УсловноеОформление.Элементы.Добавить();
	ПоляСписка = ОформлениеПомеченныхНаУдаление.Поля.Элементы.Добавить();
	ПоляСписка.Поле = Новый ПолеКомпоновкиДанных(Элементы.Список.Имя);
	УсловиеПометкаУдаления = ОформлениеПомеченныхНаУдаление.Отбор.Элементы.Добавить(Тип(
		"ЭлементОтбораКомпоновкиДанных"));
	УсловиеПометкаУдаления.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ПометкаУдаления");
	УсловиеПометкаУдаления.ПравоеЗначение = Истина;
	УсловиеПометкаУдаления.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЗачеркнутыйШрифтБЭД = Новый Шрифт(Элементы.ПредставлениеДоверители.Шрифт,,,,,,Истина);
	ОформлениеПомеченныхНаУдаление.Оформление.УстановитьЗначениеПараметра("Шрифт", ЗачеркнутыйШрифтБЭД);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКлассификаторПолномочий002(Команда)
	ОткрытьФорму("Справочник.КлассификаторПолномочийФНСМЧД002.ФормаСписка");
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСтатусыДоверенности(Ссылка)
	
	Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.МЧД003") Тогда
		СтатусыДоверенности = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка,
			"Подписана, Верна, СтатусВРеестреФНС, ДатаПрекращения");
			
		СтатусыДоверенности.Вставить("ДатаОтзыва", СтатусыДоверенности.ДатаПрекращения);
		СтатусыДоверенности.Вставить("Отозвана", СтатусыДоверенности.ДатаПрекращения > Дата(1, 1, 1));
	Иначе
		СтатусыДоверенности = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка,
			"Подписана, Верна, СтатусВРеестреФНС, ДатаОтзыва, Отозвана");
	КонецЕсли;
	
	Возврат СтатусыДоверенности;
	
КонецФункции

#КонецОбласти

#КонецОбласти