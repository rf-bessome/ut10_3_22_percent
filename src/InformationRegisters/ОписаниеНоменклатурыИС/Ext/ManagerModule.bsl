#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает новые параметры описания номенклатуры.
//
// Возвращаемое значение:
//  Структура - Инициализировать данные описания:
// * Номенклатура                        - ОпределяемыйТип.Номенклатура                                - Номенклатура.
// * ЕмкостьПотребительскойУпаковки      - Число                                                       - Емкость потребительской упаковки для часчтиного выбытия.
// * КоличествоВПотребительскойУпаковке  - Число                                                       - Количество учетных единиц товара в потребительской упаковке.
// * ВариантЧастичногоВыбытия            - ПеречислениеСсылка.ВариантыУчетаЧастичногоВыбытияИС       - Вариант частичного выбытия.
// * ВариантИспользованияЕдиницыХранения - ПеречислениеСсылка.ВариантыИспользованияЕдиницыХраненияИС - Вариант использования единицы измерения.
Функция ИнициализироватьДанныеОписанияНоменклатуры() Экспорт

	ДанныеНоменклатуры = Новый Структура();
	ДанныеНоменклатуры.Вставить("Номенклатура");
	ДанныеНоменклатуры.Вставить("ЕмкостьПотребительскойУпаковки",      0);
	ДанныеНоменклатуры.Вставить("КоличествоВПотребительскойУпаковке",  1);
	ДанныеНоменклатуры.Вставить("ВариантЧастичногоВыбытия",            Перечисления.ВариантыУчетаЧастичногоВыбытияИС.ПустаяСсылка());
	ДанныеНоменклатуры.Вставить("ВариантИспользованияЕдиницыХранения", Перечисления.ВариантыИспользованияЕдиницыХраненияИС.ПотребительскаяУпаковка);
	ДанныеНоменклатуры.Вставить("ПотребительскаяУпаковка",             Неопределено);
	ДанныеНоменклатуры.Вставить("УпаковкаЧастичногоВыбытия",           Неопределено);
	
	Возврат ДанныеНоменклатуры;

КонецФункции

// Возвращает описание входящей номенклатуры. Разворачивает описание из подходящего класса номенклатуры при необходимости.
//
// Параметры:
//   Номенклатура - ОпределяемыйТип.ИсточникОписанияНоменклатурыИС, Массив Из ОпределяемыйТип.ИсточникОписанияНоменклатурыИС - 
//     номенклатура (класс номенклатуры) для которой требуется получить описание ИС
//
// Возвращаемое значение:
//   Соответствие - номенклатура с описанием:
//    * Ключ - - ОпределяемыйТип.ИсточникОписанияНоменклатурыИС - номенклатура,
//    * Значение - см. ИнтеграцияИСМП.ИнициализироватьДанныеОписанияНоменклатуры.
Функция ПолучитьОписание(Знач Номенклатура) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Новый Соответствие;
	
	Если ТипЗнч(Номенклатура) = Тип("Массив") Тогда
		МассивНоменклатур = Номенклатура;
	Иначе 
		МассивНоменклатур = Новый Массив;
		МассивНоменклатур.Добавить(Номенклатура);
	КонецЕсли;

	Для Каждого ЭлементМассива Из МассивНоменклатур Цикл
		ДанныеНоменклатуры = ИнициализироватьДанныеОписанияНоменклатуры();
		Результат.Вставить(ЭлементМассива, ДанныеНоменклатуры);
	КонецЦикла;

	СоответствиеРезультатаВходящимДанным = ИнтеграцияИС.ДополнитьИсточникиОписанияНоменклатуры(МассивНоменклатур);
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ОписаниеНоменклатурыИС.Номенклатура                        КАК Номенклатура,
		|	ОписаниеНоменклатурыИС.ЕмкостьПотребительскойУпаковки      КАК ЕмкостьПотребительскойУпаковки,
		|	ОписаниеНоменклатурыИС.КоличествоВПотребительскойУпаковке  КАК КоличествоВПотребительскойУпаковке,
		|	ОписаниеНоменклатурыИС.ВариантЧастичногоВыбытия            КАК ВариантЧастичногоВыбытия,
		|	ОписаниеНоменклатурыИС.ВариантИспользованияЕдиницыХранения КАК ВариантИспользованияЕдиницыХранения,
		|	ОписаниеНоменклатурыИС.ПотребительскаяУпаковка             КАК ПотребительскаяУпаковка,
		|	ОписаниеНоменклатурыИС.УпаковкаЧастичногоВыбытия           КАК УпаковкаЧастичногоВыбытия
		|ИЗ
		|	РегистрСведений.ОписаниеНоменклатурыИС КАК ОписаниеНоменклатурыИС
		|ГДЕ
		|	ОписаниеНоменклатурыИС.Номенклатура В (&Номенклатура)";
	ИнтеграцияИСПереопределяемый.ДополнитьТекстЗапросаОписанияНоменклатуры(ТекстЗапроса);
	Запрос = Новый Запрос(ТекстЗапроса);
	
	Запрос.УстановитьПараметр("Номенклатура", МассивНоменклатур);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если СоответствиеРезультатаВходящимДанным.Получить(Выборка.Номенклатура) <> Неопределено Тогда
			Для Каждого ЗначениеСоответствия Из СоответствиеРезультатаВходящимДанным[Выборка.Номенклатура] Цикл
				ЗаполнитьЗначения(Результат[ЗначениеСоответствия], Выборка);
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Выборка.Сбросить();
	Пока Выборка.Следующий() Цикл
		Если Результат.Получить(Выборка.Номенклатура) <> Неопределено Тогда
			ЗаполнитьЗначения(Результат[Выборка.Номенклатура], Выборка);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

// Сохраняет описание номенклатуры
//
// Параметры:
//   ДанныеОписания    - см. ИнтеграцияИСМП.ИнициализироватьДанныеОписанияНоменклатуры.
//   СохраненныеДанные - см. ИнтеграцияИСМП.ИнициализироватьДанныеОписанияНоменклатуры.
Процедура УстановитьОписание(ДанныеОписания, СохраненныеДанные = Неопределено) Экспорт

	УстановитьПривилегированныйРежим(Истина);

	НаборЗаписей = РегистрыСведений.ОписаниеНоменклатурыИС.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Номенклатура.Установить(ДанныеОписания.Номенклатура, Истина);

	Если СохраненныеДанные = Неопределено Тогда
		НаборЗаписей.Прочитать();
	КонецЕсли;

	Если НаборЗаписей.Выбран() И СохраненныеДанные = Неопределено Тогда

		Если НаборЗаписей.Количество() Тогда
			ЗаписьНабора = НаборЗаписей[0];
		Иначе
			ЗаписьНабора = НаборЗаписей.Добавить();
			ЗаписьНабора.Номенклатура = ДанныеОписания.Номенклатура;
		КонецЕсли;

		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеОписания, "ЕмкостьПотребительскойУпаковки")
			И ДанныеОписания.ЕмкостьПотребительскойУпаковки <> Неопределено Тогда
			ЗаписьНабора.ЕмкостьПотребительскойУпаковки = ДанныеОписания.ЕмкостьПотребительскойУпаковки;
		КонецЕсли;

		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеОписания, "КоличествоВПотребительскойУпаковке")
			И ДанныеОписания.КоличествоВПотребительскойУпаковке <> Неопределено Тогда
			ЗаписьНабора.КоличествоВПотребительскойУпаковке = ДанныеОписания.КоличествоВПотребительскойУпаковке;
		КонецЕсли;

		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеОписания, "ВариантЧастичногоВыбытия")
			И ДанныеОписания.ВариантЧастичногоВыбытия <> Неопределено Тогда
			ЗаписьНабора.ВариантЧастичногоВыбытия = ДанныеОписания.ВариантЧастичногоВыбытия;
		КонецЕсли;
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеОписания, "ВариантИспользованияЕдиницыХранения")
			И ДанныеОписания.ВариантИспользованияЕдиницыХранения <> Неопределено Тогда
			ЗаписьНабора.ВариантИспользованияЕдиницыХранения = ДанныеОписания.ВариантИспользованияЕдиницыХранения;
		КонецЕсли;

		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеОписания, "ПотребительскаяУпаковка")
			И ДанныеОписания.ПотребительскаяУпаковка <> Неопределено Тогда
			ЗаписьНабора.ПотребительскаяУпаковка = ДанныеОписания.ПотребительскаяУпаковка;
		КонецЕсли;

		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеОписания, "УпаковкаЧастичногоВыбытия")
			И ДанныеОписания.УпаковкаЧастичногоВыбытия <> Неопределено Тогда
			ЗаписьНабора.УпаковкаЧастичногоВыбытия = ДанныеОписания.УпаковкаЧастичногоВыбытия;
		КонецЕсли;

	ИначеЕсли СохраненныеДанные <> Неопределено Тогда

		ЗаписьНабора = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(ЗаписьНабора, ДанныеОписания);

		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеОписания, "ЕмкостьПотребительскойУпаковки")
			И ДанныеОписания.ЕмкостьПотребительскойУпаковки = Неопределено Тогда
			ЗаписьНабора.ЕмкостьПотребительскойУпаковки = СохраненныеДанные.ЕмкостьПотребительскойУпаковки;
		КонецЕсли;

		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеОписания, "КоличествоВПотребительскойУпаковке")
			И ДанныеОписания.КоличествоВПотребительскойУпаковке = Неопределено Тогда
			ЗаписьНабора.КоличествоВПотребительскойУпаковке = СохраненныеДанные.КоличествоВПотребительскойУпаковке;
		КонецЕсли;

		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеОписания, "ВариантЧастичногоВыбытия")
			И ДанныеОписания.ВариантЧастичногоВыбытия = Неопределено Тогда
			ЗаписьНабора.ВариантЧастичногоВыбытия = СохраненныеДанные.ВариантЧастичногоВыбытия;
		КонецЕсли;
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеОписания, "ВариантИспользованияЕдиницыХранения")
			И ДанныеОписания.ВариантИспользованияЕдиницыХранения = Неопределено Тогда
			ЗаписьНабора.ВариантИспользованияЕдиницыХранения = СохраненныеДанные.ВариантИспользованияЕдиницыХранения;
		КонецЕсли;
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеОписания, "ПотребительскаяУпаковка")
			И ДанныеОписания.ПотребительскаяУпаковка = Неопределено Тогда
			ЗаписьНабора.ПотребительскаяУпаковка = СохраненныеДанные.ПотребительскаяУпаковка;
		КонецЕсли;
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеОписания, "УпаковкаЧастичногоВыбытия")
			И ДанныеОписания.УпаковкаЧастичногоВыбытия = Неопределено Тогда
			ЗаписьНабора.УпаковкаЧастичногоВыбытия = СохраненныеДанные.УпаковкаЧастичногоВыбытия;
		КонецЕсли;
		
	Иначе

		ЗаписьНабора = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(ЗаписьНабора, ДанныеОписания);

	КонецЕсли;

	НаборЗаписей.Записать();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьЗначения(Приемник, Источник)
	
	Для Каждого ЭлементСтруктуры Из Приемник Цикл
		Если Источник[ЭлементСтруктуры.Ключ] <> Неопределено Тогда
 			Приемник[ЭлементСтруктуры.Ключ] = Источник[ЭлементСтруктуры.Ключ];
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

