
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	АдресДанныхКодаМаркировки = Параметры.АдресДанныхКодаМаркировки;
	
	СобытияФормИСМППереопределяемый.УстановитьПараметрыВыбораШаблонаЭтикетки(
		ЭтаФорма,
		Элементы.ШаблонЭтикетки.Имя);
	
	СобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Печать(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеДляПечати = ПолучитьДанныеДляПечати();
	ПечатьЭтикетокИСМПКлиент.НапечататьЭтикеткиИСМП(ДанныеДляПечати, ЭтаФорма);
	
	Закрыть(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьДанныеДляПечати()
	
	ДанныеКодаМаркировки = ПолучитьИзВременногоХранилища(АдресДанныхКодаМаркировки);
	
	ПараметрыСканирования = ДанныеКодаМаркировки.ПараметрыСканирования;
	ДанныеШтрихкода       = ДанныеКодаМаркировки.ДанныеШтрихкода;
	
	Если Не ДанныеШтрихкода.ЕстьВПулеКодовМаркировки Тогда
		ШтрихкодированиеИСМПСлужебный.ПроверитьПолныйКодМаркировкиПоДаннымРазбора(
			Неопределено, ДанныеШтрихкода, ПараметрыСканирования);
		ШтрихкодированиеИСМПСлужебный.СохранениеКодаМаркировкиВПул(
			ШтрихкодированиеИС.ИнициализироватьРезультатОбработкиШтрихкода(),
			ДанныеШтрихкода,
			ПараметрыСканирования);
	КонецЕсли;
	
	Если ИнтеграцияИСПовтИсп.ЭтоПродукцияМОТП(ДанныеШтрихкода.ВидПродукции) Тогда
		КодМаркировки = РазборКодаМаркировкиИССлужебныйКлиентСервер.НормализоватьКодМаркировки(
			ДанныеШтрихкода.ДанныеРазбора,
			ДанныеШтрихкода.ВидПродукции,
			РазборКодаМаркировкиИССлужебныйКлиентСервер.НастройкиРазбораКодаМаркировкиДляСохраненияВПул());
	ИначеЕсли ИнтеграцияИСПовтИсп.ЭтоПродукцияИСМП(ДанныеШтрихкода.ВидПродукции) Тогда
		КодМаркировки = ДанныеШтрихкода.Штрихкод;
	КонецЕсли;
	
	ХешСуммаКодаМаркировки = ИнтеграцияИС.ХешированиеДанныхSHA256(КодМаркировки);
	
	ОбъектыПечати = Новый Массив;
	
	ПараметрыШтрихкода = ПечатьЭтикетокИСМПКлиентСервер.СтруктураПечатиЭтикетки();
	ЗаполнитьЗначенияСвойств(ПараметрыШтрихкода, ДанныеШтрихкода);
	
	ПараметрыШтрихкода.ШаблонЭтикетки         = ШаблонЭтикетки;
	ПараметрыШтрихкода.Количество             = 1;
	ПараметрыШтрихкода.КодМаркировки          = КодМаркировки;
	ПараметрыШтрихкода.ХешСуммаКодаМаркировки = ХешСуммаКодаМаркировки;
	
	ОбъектыПечати.Добавить(ПараметрыШтрихкода);
	
	ДанныеДляПечати = Новый Структура;
	ДанныеДляПечати.Вставить("ОбъектыПечати",              ОбъектыПечати);
	ДанныеДляПечати.Вставить("Документ",                   Неопределено);
	ДанныеДляПечати.Вставить("КаждаяЭтикеткаНаНовомЛисте", Ложь);
	ДанныеДляПечати.Вставить("ПакетнаяПечать",             Ложь);
	ДанныеДляПечати.Вставить("РазрешитьПовторнуюПечать",   Истина);
	ДанныеДляПечати.Вставить("РежимПечати", "Выборочно");
	
	Возврат ДанныеДляПечати;
	
КонецФункции

#КонецОбласти

