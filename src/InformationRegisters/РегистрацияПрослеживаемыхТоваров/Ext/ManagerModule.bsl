#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Выполняет обновления регистра сведений регистрация прослеживаемых товаров
//
// Параметры:
// Регистратор - ДокументСсылка - Документ из которого вызывают процедуру
// ДокументОснования - ДокументСсылка -  Документ основания (первичный документ, по которому сформировано уведомление или не сформировано еще)
// УдалениеПроведения - Булево - флаг, показывающий, что процедуры вызывана при отмене проведения Регистратора
// 
Процедура ОбновитьСостояние(Регистратор, ДокументОснование, УдалениеПроведения = Ложь) Экспорт
	
	ПараметрыДокументаОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОснование, "Дата, Организация");
	
	Если НеОбновлятьСостояние(ДокументОснование, ПараметрыДокументаОснования.Дата) Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаЗапроса = ПолучитьТаблицуДляОбновленияРегистрацииПрослеживаемогоТовара(
		Регистратор, ДокументОснование, УдалениеПроведения, ПараметрыДокументаОснования);
		
	Если ТаблицаЗапроса.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
		
	Набор = РегистрыСведений.РегистрацияПрослеживаемыхТоваров.СоздатьНаборЗаписей();
	Набор.Отбор.Основание.Установить(ДокументОснование);
	
	Для каждого ТекущаяСтрока Из ТаблицаЗапроса Цикл
		
		Набор.Отбор.Организация.Установить(ТекущаяСтрока.Организация);
		Набор.Отбор.ВидОперации.Установить(ТекущаяСтрока.ВидОперации);
		
		Запись = Набор.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, ТекущаяСтрока);
	
	КонецЦикла;
	
	Набор.Записать(Истина);
	
КонецПроцедуры

// Процедура выполняет удаление записей в регистре при отмене первичного документа
//
// Параметры:
// 
// Регистратор - ДокументСсылка - Первичный документ, из которого вызывают процедуру
// 
Процедура ОбновитьСостояниеУдалениеПроведенияПервичногоДокумента(Регистратор) Экспорт
	
	ПараметрыДокументаОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Регистратор, "Дата, Организация");
	
	Если НеОбновлятьСостояние(Регистратор, ПараметрыДокументаОснования.Дата) Тогда
		Возврат;
	КонецЕсли;

	Набор = РегистрыСведений.РегистрацияПрослеживаемыхТоваров.СоздатьНаборЗаписей();
	Набор.Отбор.Основание.Установить(Регистратор);
	Набор.Отбор.Организация.Установить(ПараметрыДокументаОснования.Организация);
	
	Набор.Записать(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Проверяет необходимость обновления регистра сведений Регистрация прослеживаемого товара
//
// Параметры:
// ДокументОснование - ДокументСсылка - Документ, по которому будет формироваться запись в регистре и расчет остатка 
// Период - Дата - Дата документа основания
// 
// Возвращаемое значение:
// Булево - Истина если не надо обновлять запись в регистре
//
Функция НеОбновлятьСостояние(ДокументОснование, Период) Экспорт
	
	Если Не ПрослеживаемостьБРУ.ВедетсяУчетПрослеживаемыхТоваров(Период) Тогда
		
		// Очистим регистр по основанию, если пользователь поменял дату документа основания 
		// и дата не попадает в период ведения прослеживаемости
		Набор = РегистрыСведений.РегистрацияПрослеживаемыхТоваров.СоздатьНаборЗаписей();
		Набор.Отбор.Основание.Установить(ДокументОснование);
		Набор.Записать(Истина);
		
		Возврат Истина;
		
	КонецЕсли;
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ИнвентаризацияТоваровНаСкладе") Тогда
		
		// Не обновляем состояния инвентаризаций товаров с датой больше разрешенной даты
		Возврат Период > ПрослеживаемостьБРУ.ДатаОкончанияРегистрацииУведомленийОбОСтатках();
			
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Считает количество товара по которому необходимо сделать запись в регистре Регистрация прослеживаемого товара
//
// Параметры:
// ДокументОснование - ДокументСсылка - Документ, по которому будет формироваться запись в регистре и расчет остатка 
// ПараметрыДокументаОснования - Структура - 
//												Дата - Дата - дата документа основания
//							 					Организация - СправочникСсылка.Организация - организация 
// 
// Возвращаемое значение:
//  ТаблицаЗначений - таблица готовых записей в регистр Регистрация прослеживаемого товара
//
Функция ПолучитьТаблицуДляОбновленияРегистрацииПрослеживаемогоТовара(ДокументРегистратор, ДокументОснование, УдалениеПроведения, ПараметрыДокументаОснования) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	Запрос.УстановитьПараметр("Основание", ДокументОснование);
	Запрос.УстановитьПараметр("НачалоПериода", ПрослеживаемостьБРУ.ДатаНачалаУчетаПрослеживаемыхТоваров());
	Запрос.УстановитьПараметр("КонецПериода", ПараметрыДокументаОснования.Дата);
	Запрос.УстановитьПараметр("ПустаяСсылкаРнпт", Справочники.НомераГТД.ПустаяСсылка());
	Запрос.УстановитьПараметр("Организация", ПараметрыДокументаОснования.Организация);
	Запрос.УстановитьПараметр("ДокументИсключение",
		?(УдалениеПроведения, ДокументРегистратор, Документы.УведомлениеОбОстаткахПрослеживаемыхТоваров.ПустаяСсылка()));
	
	Запрос.Текст = ТекстЗапросаПоОснованию(ДокументОснование, ПараметрыДокументаОснования)
				+ ТекстЗапросаПоУведомлениямИВычислениюОстатка();
	
	Результат = Запрос.Выполнить();
	
	Возврат Результат.Выгрузить();
	
КонецФункции

Функция ПодготовитьПараметрыТаблицыРегистрацииПрослеживаемыхТоваров(ТаблицаРеквизитов, ТаблицаТоваров)

	Параметры = Новый Структура;
	
	// Подготовка таблицы Параметры.таблицаТоваров
	
	СписокОбязательныхКолонок = ""
		+ "Ссылка,"                                   // <ДокументСсылка.*> - документ-регистратор движений
		+ "НомерСтроки,"                              // <Число> - номер строки в таблице товаров
		+ "Номенклатура,"                             // <СправочникСсылка.Номенклатура> - номенклатура
		+ "Количество,"		                          // <Число ,15,3> - количество
		+ "КоличествоПрослеживаемости,"               // <Число, 15,3> - количество по прослеживаемости
		+ "Сумма,"		                              // <Число, 15,3> - сумма
		+ "СтранаПроисхождения";                      // <СправочникСсылка.СтраныМира> - страна происхождения товара
		
	Параметры.Вставить("ТаблицаТоваров", ПрослеживаемостьБП.ПолучитьТаблицуПараметровПроведения(
		ТаблицаТоваров, СписокОбязательныхКолонок));
		
	// Подготовка таблицы Параметры.Реквизиты
	
	СписокОбязательныхКолонок = ""
		+ "Период,"							// <Дата> - период движений - дата документа
		+ "Регистратор,"					// <ДокументСсылка.*> - документ-регистратор движений
		+ "Организация,"					// <СправочникСсылка.Организации> - организация
		+ "РНПТ,"							// <ОпределяемыеТипы.РНПТ> - РНПТ
		+ "ЭтоКорректировочноеУведомление,"	// <Булево> - признак, показывающий, что данные их корректировочного документа
		+ "ПолученоПодтверждениеИзФНС,"		// <Булево> - признак, показывающий, что получено подтверждение от ФНС
		+ "Основание";						//<ДокументСсылка.*> - документ-основания
		
	Параметры.Вставить("Реквизиты", ПрослеживаемостьБП.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизитов, СписокОбязательныхКолонок));
		
	Возврат Параметры;

КонецФункции

Функция ТекстЗапросаПоОснованию(ДокументОснование, ПараметрыДокументаОснования)
	
	ОбъектМетаданных = ДокументОснование.Метаданные();
	
	ТекстЗапроса = Документы[ОбъектМетаданных.Имя].ТекстЗапросаТаблицаПрослеживаемыхТоваровПоОснованию();
	
	Возврат ТекстЗапроса + ПрослеживаемостьБП.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаПоУведомлениямИВычислениюОстатка()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Номенклатура КАК Номенклатура,
	|	СУММА(УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Количество) КАК Количество,
	|	СУММА(УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Сумма) КАК Сумма,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.КодТНВЭД КАК ТНВЭД,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки КАК НомерКорректировки,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка КАК СсылкаНаУведомление,
	|	СУММА(0) КАК КоличествоПрослеживаемости
	|ПОМЕСТИТЬ ВТ_ВыписанныеУведомления
	|ИЗ
	|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.Товары КАК УведомлениеОбОстаткахПрослеживаемыхТоваровТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров КАК УведомлениеОбОстаткахПрослеживаемыхТоваров
	|		ПО УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Ссылка = УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка
	|ГДЕ
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ПервичныйДокумент = &Основание
	|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.Проведен
	|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка <> &ДокументИсключение
	|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки = 0
	|
	|СГРУППИРОВАТЬ ПО
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.КодТНВЭД,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Номенклатура,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УведомлениеОВвозеПрослеживаемыхТоваровТовары.Номенклатура,
	|	СУММА(УведомлениеОВвозеПрослеживаемыхТоваровТовары.Количество),
	|	СУММА(УведомлениеОВвозеПрослеживаемыхТоваровТовары.Сумма),
	|	УведомлениеОВвозеПрослеживаемыхТоваров.КодТНВЭД,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка,
	|	УведомлениеОВвозеПрослеживаемыхТоваровТовары.КоличествоПрослеживаемости
	|ИЗ
	|	Документ.УведомлениеОВвозеПрослеживаемыхТоваров.Товары КАК УведомлениеОВвозеПрослеживаемыхТоваровТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОВвозеПрослеживаемыхТоваров КАК УведомлениеОВвозеПрослеживаемыхТоваров
	|		ПО УведомлениеОВвозеПрослеживаемыхТоваровТовары.Ссылка = УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка
	|ГДЕ
	|	УведомлениеОВвозеПрослеживаемыхТоваров.ПервичныйДокумент = &Основание
	|	И УведомлениеОВвозеПрослеживаемыхТоваров.Проведен
	|	И УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка <> &ДокументИсключение
	|
	|СГРУППИРОВАТЬ ПО
	|	УведомлениеОВвозеПрослеживаемыхТоваровТовары.Номенклатура,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.КодТНВЭД,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка,
	|	УведомлениеОВвозеПрослеживаемыхТоваровТовары.КоличествоПрослеживаемости
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Номенклатура,
	|	СУММА(УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.КоличествоПослеИзменения),
	|	СУММА(УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.СуммаПослеИзменения),
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.КодТНВЭДПослеИзменения,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка,
	|	0
	|ИЗ
	|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.Товары КАК УведомлениеОбОстаткахПрослеживаемыхТоваровТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров КАК УведомлениеОбОстаткахПрослеживаемыхТоваров
	|		ПО УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Ссылка = УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка
	|ГДЕ
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.ПервичныйДокумент = &Основание
	|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.Проведен
	|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка <> &ДокументИсключение
	|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки > 0
	|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.ПолученоПодтверждениеИзФНС
	|
	|СГРУППИРОВАТЬ ПО
	|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Номенклатура,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.КодТНВЭДПослеИзменения,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваровПоОснованию.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваровПоОснованию.Количество КАК Количество,
	|	ТаблицаТоваровПоОснованию.Сумма КАК Сумма,
	|	ТаблицаТоваровПоОснованию.ТНВЭД КАК ТНВЭД,
	|	ТаблицаТоваровПоОснованию.Организация КАК Организация,
	|	ТаблицаТоваровПоОснованию.ВидОперации КАК ВидОперации,
	|	ТаблицаТоваровПоОснованию.ПериодСобытия КАК ПериодСобытия,
	|	ТаблицаТоваровПоОснованию.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ТаблицаТоваровПоОснованию.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТаблицаТоваровПоОснованию.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости
	|ПОМЕСТИТЬ ВТ_ТаблицаТоваровПоОснованию
	|ИЗ
	|	ВТ_ТоварыПоОснованию КАК ТаблицаТоваровПоОснованию
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВТ_ВыписанныеУведомления.СсылкаНаУведомление) КАК СсылкаНаУведомление,
	|	МАКСИМУМ(ВТ_ВыписанныеУведомления.НомерКорректировки) КАК НомерКорректировки,
	|	ВЫБОР
	|		КОГДА ВТ_ВыписанныеУведомления.СсылкаНаУведомление.ДокументУведомлениеОбОстатках = ЗНАЧЕНИЕ(Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.ПустаяСсылка)
	|			ТОГДА ВТ_ВыписанныеУведомления.СсылкаНаУведомление
	|		ИНАЧЕ ВТ_ВыписанныеУведомления.СсылкаНаУведомление.ДокументУведомлениеОбОстатках
	|	КОНЕЦ КАК Основание
	|ПОМЕСТИТЬ ПоследниеУведомления
	|ИЗ
	|	ВТ_ВыписанныеУведомления КАК ВТ_ВыписанныеУведомления
	|ГДЕ
	|	ВТ_ВыписанныеУведомления.СсылкаНаУведомление ССЫЛКА Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА ВТ_ВыписанныеУведомления.СсылкаНаУведомление.ДокументУведомлениеОбОстатках = ЗНАЧЕНИЕ(Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.ПустаяСсылка)
	|			ТОГДА ВТ_ВыписанныеУведомления.СсылкаНаУведомление
	|		ИНАЧЕ ВТ_ВыписанныеУведомления.СсылкаНаУведомление.ДокументУведомлениеОбОстатках
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВТ_ВыписанныеУведомления.СсылкаНаУведомление),
	|	МАКСИМУМ(ВТ_ВыписанныеУведомления.НомерКорректировки),
	|	ВТ_ВыписанныеУведомления.СсылкаНаУведомление
	|ИЗ
	|	ВТ_ВыписанныеУведомления КАК ВТ_ВыписанныеУведомления
	|ГДЕ
	|	ВТ_ВыписанныеУведомления.СсылкаНаУведомление ССЫЛКА Документ.УведомлениеОВвозеПрослеживаемыхТоваров
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ВыписанныеУведомления.СсылкаНаУведомление
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваровПоУведомлениям.Номенклатура КАК Номенклатура,
	|	СУММА(ТаблицаТоваровПоУведомлениям.Количество) КАК Количество,
	|	СУММА(ТаблицаТоваровПоУведомлениям.Сумма) КАК Сумма,
	|	ТаблицаТоваровПоУведомлениям.ТНВЭД КАК ТНВЭД,
	|	СУММА(ТаблицаТоваровПоУведомлениям.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости
	|ПОМЕСТИТЬ ВТ_ТаблицаТоваровПоУведомлениям
	|ИЗ
	|	ПоследниеУведомления КАК ПоследниеУведомления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВыписанныеУведомления КАК ТаблицаТоваровПоУведомлениям
	|		ПО ПоследниеУведомления.СсылкаНаУведомление = ТаблицаТоваровПоУведомлениям.СсылкаНаУведомление
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваровПоУведомлениям.Номенклатура,
	|	ТаблицаТоваровПоУведомлениям.ТНВЭД
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТНВЭД,
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваровПоОснованию.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваровПоОснованию.Количество - ЕСТЬNULL(ТаблицаТоваровПоУведомлениям.Количество, 0) < 0
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаТоваровПоОснованию.Количество - ЕСТЬNULL(ТаблицаТоваровПоУведомлениям.Количество, 0)
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваровПоОснованию.Количество - ЕСТЬNULL(ТаблицаТоваровПоУведомлениям.Количество, 0) < 0
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаТоваровПоОснованию.Сумма - ЕСТЬNULL(ТаблицаТоваровПоУведомлениям.Сумма, 0)
	|	КОНЕЦ КАК Сумма,
	|	ТаблицаТоваровПоОснованию.ТНВЭД КАК ТНВЭД,
	|	ТаблицаТоваровПоОснованию.Организация КАК Организация,
	|	ТаблицаТоваровПоОснованию.ВидОперации КАК ВидОперации,
	|	&Основание КАК Основание,
	|	ТаблицаТоваровПоОснованию.ПериодСобытия КАК ПериодСобытия,
	|	ТаблицаТоваровПоОснованию.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ТаблицаТоваровПоОснованию.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваровПоОснованию.КоличествоПрослеживаемости - ЕСТЬNULL(ТаблицаТоваровПоУведомлениям.КоличествоПрослеживаемости, 0) < 0
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаТоваровПоОснованию.КоличествоПрослеживаемости - ЕСТЬNULL(ТаблицаТоваровПоУведомлениям.КоличествоПрослеживаемости, 0)
	|	КОНЕЦ КАК КоличествоПрослеживаемости
	|ИЗ
	|	ВТ_ТаблицаТоваровПоОснованию КАК ТаблицаТоваровПоОснованию
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТаблицаТоваровПоУведомлениям КАК ТаблицаТоваровПоУведомлениям
	|		ПО ТаблицаТоваровПоОснованию.ТНВЭД = ТаблицаТоваровПоУведомлениям.ТНВЭД
	|			И ТаблицаТоваровПоОснованию.Номенклатура = ТаблицаТоваровПоУведомлениям.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ТаблицаТоваровТолькоОприходование.Номенклатура,
	|	ВТ_ТаблицаТоваровТолькоОприходование.Количество,
	|	ВТ_ТаблицаТоваровТолькоОприходование.Сумма,
	|	ВТ_ТаблицаТоваровТолькоОприходование.ТНВЭД,
	|	ВТ_ТаблицаТоваровТолькоОприходование.Организация,
	|	ВТ_ТаблицаТоваровТолькоОприходование.ВидОперации,
	|	&Основание,
	|	ВТ_ТаблицаТоваровТолькоОприходование.ПериодСобытия,
	|	NULL,
	|	NULL,
	|	0
	|ИЗ
	|	ВТ_ТаблицаТоваровРазмеченный КАК ВТ_ТаблицаТоваровТолькоОприходование
	|ГДЕ
	|	ВТ_ТаблицаТоваровТолькоОприходование.РанееРегистрировалсяВПрослеживаемости
	|	И ВТ_ТаблицаТоваровТолькоОприходование.Количество > ВТ_ТаблицаТоваровТолькоОприходование.КоличествоУчет";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецЕсли
