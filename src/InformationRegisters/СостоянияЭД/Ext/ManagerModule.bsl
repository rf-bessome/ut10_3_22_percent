#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// обработчик обновления на версию 1.1.25
Процедура ЗаполнитьОписаниеОснованийЭД() Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1000
	|	СостоянияЭД.СсылкаНаОбъект КАК СсылкаНаОбъект
	|ИЗ
	|	РегистрСведений.СостоянияЭД КАК СостоянияЭД
	|ГДЕ
	|	СостоянияЭД.СостояниеВерсииЭД = &СостояниеВерсииЭД
	|	И СостоянияЭД.Вид = """"
	|
	|УПОРЯДОЧИТЬ ПО
	|	СсылкаНаОбъект УБЫВ
	|АВТОУПОРЯДОЧИВАНИЕ");
	
	Запрос.УстановитьПараметр("СостояниеВерсииЭД", Перечисления.СостоянияВерсийЭД.НеСформирован);
	
	Результат = Запрос.Выполнить();
	Пока Не Результат.Пустой() Цикл
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			НачатьТранзакцию();
			
			Попытка
				
				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СостоянияЭД");
				ЭлементБлокировки.УстановитьЗначение("СсылкаНаОбъект", Выборка.СсылкаНаОбъект);
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				Блокировка.Заблокировать();
				
				НаборЗаписей = РегистрыСведений.СостоянияЭД.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.СсылкаНаОбъект.Установить(Выборка.СсылкаНаОбъект);
				НаборЗаписей.Прочитать();
				
				Если ОбщегоНазначения.ЭтоДокумент(Выборка.СсылкаНаОбъект.Метаданные()) Тогда
					ОписаниеОснования = ЭлектронныеДокументыСлужебный.ОписаниеОснованияЭлектронногоДокумента(Выборка.СсылкаНаОбъект);
					ЗаполнитьЗначенияСвойств(НаборЗаписей[0], ОписаниеОснования);
				Иначе
					НаборЗаписей.Очистить();
				КонецЕсли;
				
				НаборЗаписей.Записать();
				
				ЗафиксироватьТранзакцию();
				
			Исключение
				
				ОтменитьТранзакцию();
				Операция = НСтр("ru = 'Обновление подсистемы обмена с контрагентами'");
				ПодробныйТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ЭлектронныеДокументыСлужебныйВызовСервера.ОбработатьОшибку(Операция, ПодробныйТекстОшибки);
				ВызватьИсключение;
				
			КонецПопытки;
			
		КонецЦикла;
		
		Результат = Запрос.Выполнить();
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли