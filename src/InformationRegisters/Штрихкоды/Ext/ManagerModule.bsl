#Область ПрограммныйИнтерфейс

// Добавляет запись в регистр сведений Штрихкоды
//
// Параметры:
//  ДанныеШтрихкодаУпаковки - Структура - содержит данные о номенклатуре и коде маркировки
//
Процедура ЗаписатьШтрихкодНоменклатуры(ДанныеШтрихкодаУпаковки) Экспорт
	
	Если ШтрихкодированиеИСКлиентСервер.ЭтоШтрихкодВводаОстатков(ДанныеШтрихкодаУпаковки.Штрихкод) Тогда
		Возврат;
	КонецЕсли;
	
	ПримечаниеКРезультатуРазбора = Неопределено;
	ДанныеРазбора = РазборКодаМаркировкиИССлужебный.РазобратьКодМаркировки(
		ДанныеШтрихкодаУпаковки.Штрихкод, ДанныеШтрихкодаУпаковки.ВидПродукции, ПримечаниеКРезультатуРазбора);
		
	Если ДанныеРазбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	EAN = ДанныеРазбора.СоставКодаМаркировки.EAN;
	
	Если ШтрихкодЗаписан(EAN) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПараметрыЗаписиШтрихкода = СтруктураЗаписиШтрихкода();
	ПараметрыЗаписиШтрихкода.Владелец = ДанныеШтрихкодаУпаковки.Номенклатура;
	ПараметрыЗаписиШтрихкода.Штрихкод = EAN;
	ПараметрыЗаписиШтрихкода.ХарактеристикаНоменклатуры = ДанныеШтрихкодаУпаковки.Характеристика;
	ПараметрыЗаписиШтрихкода.СерияНоменклатуры = ДанныеШтрихкодаУпаковки.Серия;
	
	МенеджерЗаписи = РегистрыСведений.Штрихкоды.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ПараметрыЗаписиШтрихкода);
	
	МенеджерЗаписи.Прочитать();
	
	Если Не МенеджерЗаписи.Выбран() Тогда
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ПараметрыЗаписиШтрихкода);
		МенеджерЗаписи.Записать();
	КонецЕсли;
		
КонецПроцедуры

Функция СтруктураЗаписиШтрихкода()
	
	СтруктураЗаписи = Новый Структура;
	
	СтруктураЗаписи.Вставить("Владелец");
	СтруктураЗаписи.Вставить("ХарактеристикаНоменклатуры");
	СтруктураЗаписи.Вставить("СерияНоменклатуры");
	СтруктураЗаписи.Вставить("ТипШтрихкода", ПланыВидовХарактеристик.ТипыШтрихкодов.EAN13);
	СтруктураЗаписи.Вставить("Качество", Справочники.Качество.Новый);
	СтруктураЗаписи.Вставить("Штрихкод");
	
	Возврат СтруктураЗаписи;
	
КонецФункции

Функция ШтрихкодЗаписан(Штрихкод)
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	РегШтрихкоды.Владелец КАК Владелец,
	|	РегШтрихкоды.ШтрихКод КАК Штрихкод
	|ИЗ
	|	РегистрСведений.Штрихкоды КАК РегШтрихкоды
	|ГДЕ
	|	РегШтрихкоды.ШтрихКод = &ШтрихКод
	|");

	Запрос.УстановитьПараметр("ШтрихКод", ШтрихКод);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат ?(РезультатЗапроса.Пустой(), Ложь, Истина);

КонецФункции

#КонецОбласти