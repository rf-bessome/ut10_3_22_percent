#Если Клиент Тогда

Перем ОтчетСформирован Экспорт;

Функция ТекстЗапроса()
	Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЧекККМ.Ссылка КАК Документ,
	|	ЧекККМ.НомерЧекаККМ КАК НомерЧекаККМ,
	|	ЧекККМ.Дата КАК Дата,
	|	ЧекККМ.СуммаДокумента КАК СуммаДокумента,
	|	ВЫБОР
	|		КОГДА ЧекККМ.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Продажа)
	|			ТОГДА ""Продажа""
	|		ИНАЧЕ ""Возврат""
	|	КОНЕЦ КАК ВидОперации
	|ИЗ
	|	Документ.ЧекККМ КАК ЧекККМ
	|ГДЕ
	|	ЧекККМ.ЧекПробитНаККМ
	|	И ЧекККМ.КассаККМ = &ККМ
	|	И ЧекККМ.Дата >= &НачалоПериода
	|	И ЧекККМ.Дата <= &КонецПериода
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПриходныйКассовыйОрдер.Ссылка,
	|	ПриходныйКассовыйОрдер.НомерЧекаККМ,
	|	ПриходныйКассовыйОрдер.Дата,
	|	ПриходныйКассовыйОрдер.СуммаДокумента,
	|	ПриходныйКассовыйОрдер.ВидОперации
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер КАК ПриходныйКассовыйОрдер
	|ГДЕ
	|	ПриходныйКассовыйОрдер.ККМ = &ККМ
	|	И ПриходныйКассовыйОрдер.Дата >= &НачалоПериода
	|	И ПриходныйКассовыйОрдер.Дата <= &КонецПериода
	|	И ПриходныйКассовыйОрдер.НомерЧекаККМ <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасходныйКассовыйОрдер.Ссылка,
	|	РасходныйКассовыйОрдер.НомерЧекаККМ,
	|	РасходныйКассовыйОрдер.Дата,
	|	РасходныйКассовыйОрдер.СуммаДокумента,
	|	РасходныйКассовыйОрдер.ВидОперации
	|ИЗ
	|	Документ.РасходныйКассовыйОрдер КАК РасходныйКассовыйОрдер
	|ГДЕ
	|	РасходныйКассовыйОрдер.ККМ = &ККМ
	|	И РасходныйКассовыйОрдер.Дата >= &НачалоПериода
	|	И РасходныйКассовыйОрдер.Дата <= &КонецПериода
	|	И РасходныйКассовыйОрдер.НомерЧекаККМ <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОплатаОтПокупателяПлатежнойКартой.Ссылка,
	|	ОплатаОтПокупателяПлатежнойКартой.НомерЧекаККМ,
	|	ОплатаОтПокупателяПлатежнойКартой.Дата,
	|	ОплатаОтПокупателяПлатежнойКартой.СуммаДокумента,
	|	ОплатаОтПокупателяПлатежнойКартой.ВидОперации
	|ИЗ
	|	Документ.ОплатаОтПокупателяПлатежнойКартой КАК ОплатаОтПокупателяПлатежнойКартой
	|ГДЕ
	|	ОплатаОтПокупателяПлатежнойКартой.ККМ = &ККМ
	|	И ОплатаОтПокупателяПлатежнойКартой.Дата >= &НачалоПериода
	|	И ОплатаОтПокупателяПлатежнойКартой.Дата <= &КонецПериода
	|	И ОплатаОтПокупателяПлатежнойКартой.НомерЧекаККМ <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВозвратТоваровОтПокупателя.Ссылка,
	|	ВозвратТоваровОтПокупателя.НомерЧекаККМ,
	|	ВозвратТоваровОтПокупателя.Дата,
	|	ВозвратТоваровОтПокупателя.СуммаДокумента,
	|	""Возврат товаров от покупателя""
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВозвратТоваровОтПокупателяТовары.Ссылка КАК Ссылка,
	|		МАКСИМУМ(ВозвратТоваровОтПокупателяТовары.Ссылка.НомерЧекаККМ) КАК НомерЧекаККМ,
	|		МАКСИМУМ(ВозвратТоваровОтПокупателяТовары.Ссылка.Дата) КАК Дата,
	|		СУММА(ВЫБОР
	|				КОГДА ВозвратТоваровОтПокупателяТовары.Ссылка.СуммаВключаетНДС
	|					ТОГДА ВозвратТоваровОтПокупателяТовары.Сумма
	|				ИНАЧЕ ВозвратТоваровОтПокупателяТовары.Сумма + ВозвратТоваровОтПокупателяТовары.СуммаНДС
	|			КОНЕЦ) КАК СуммаДокумента
	|	ИЗ
	|		Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
	|	ГДЕ
	|		ВозвратТоваровОтПокупателяТовары.Ссылка.ККМ = &ККМ
	|		И ВозвратТоваровОтПокупателяТовары.Ссылка.Дата >= &НачалоПериода
	|		И ВозвратТоваровОтПокупателяТовары.Ссылка.Дата <= &КонецПериода
	|		И ВозвратТоваровОтПокупателяТовары.Ссылка.НомерЧекаККМ <> 0
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ВозвратТоваровОтПокупателяТовары.Ссылка) КАК ВозвратТоваровОтПокупателя
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка,
	|	РеализацияТоваровУслуг.НомерЧекаККМ,
	|	РеализацияТоваровУслуг.Дата,
	|	РеализацияТоваровУслуг.СуммаДокумента,
	|	""Реализация""
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВложенныйЗапрос.Ссылка КАК Ссылка,
	|		МАКСИМУМ(ВложенныйЗапрос.НомерЧекаККМ) КАК НомерЧекаККМ,
	|		МАКСИМУМ(ВложенныйЗапрос.Дата) КАК Дата,
	|		СУММА(ВложенныйЗапрос.СуммаДокумента) КАК СуммаДокумента
	|	ИЗ
	|		(ВЫБРАТЬ
	|			РеализацияТоваровУслугТовары.Ссылка КАК Ссылка,
	|			РеализацияТоваровУслугТовары.Ссылка.НомерЧекаККМ КАК НомерЧекаККМ,
	|			РеализацияТоваровУслугТовары.Ссылка.Дата КАК Дата,
	|			ВЫБОР
	|				КОГДА РеализацияТоваровУслугТовары.Ссылка.СуммаВключаетНДС
	|					ТОГДА РеализацияТоваровУслугТовары.Сумма
	|				ИНАЧЕ РеализацияТоваровУслугТовары.Сумма + РеализацияТоваровУслугТовары.СуммаНДС
	|			КОНЕЦ КАК СуммаДокумента
	|		ИЗ
	|			Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|		ГДЕ
	|			РеализацияТоваровУслугТовары.Ссылка.ККМ = &ККМ
	|			И РеализацияТоваровУслугТовары.Ссылка.Дата >= &НачалоПериода
	|			И РеализацияТоваровУслугТовары.Ссылка.Дата <= &КонецПериода
	|			И РеализацияТоваровУслугТовары.Ссылка.НомерЧекаККМ <> 0
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			РеализацияТоваровУслугУслуги.Ссылка,
	|			РеализацияТоваровУслугУслуги.Ссылка.НомерЧекаККМ,
	|			РеализацияТоваровУслугУслуги.Ссылка.Дата,
	|			ВЫБОР
	|				КОГДА РеализацияТоваровУслугУслуги.Ссылка.СуммаВключаетНДС
	|					ТОГДА РеализацияТоваровУслугУслуги.Сумма
	|				ИНАЧЕ РеализацияТоваровУслугУслуги.Сумма + РеализацияТоваровУслугУслуги.СуммаНДС
	|			КОНЕЦ
	|		ИЗ
	|			Документ.РеализацияТоваровУслуг.Услуги КАК РеализацияТоваровУслугУслуги
	|		ГДЕ
	|			РеализацияТоваровУслугУслуги.Ссылка.ККМ = &ККМ
	|			И РеализацияТоваровУслугУслуги.Ссылка.Дата >= &НачалоПериода
	|			И РеализацияТоваровУслугУслуги.Ссылка.Дата <= &КонецПериода
	|			И РеализацияТоваровУслугУслуги.Ссылка.НомерЧекаККМ <> 0) КАК ВложенныйЗапрос
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ВложенныйЗапрос.Ссылка) КАК РеализацияТоваровУслуг
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата";
	Возврат Текст;
КонецФункции

Процедура СформироватьОтчет(ТабличныйДокумент, ТолькоЗаголовок = Ложь) Экспорт
	
	МакетОтчета = ПолучитьМакет("Макет");
	
	ТабличныйДокумент.Очистить();
	
	Если ПоказыватьЗаголовок Тогда
		ОбластьЗаголовок = МакетОтчета.ПолучитьОбласть("Заголовок");
		ОбластьЗаголовок.Параметры.НазваниеОтчета = ЭтотОбъект.Метаданные().Комментарий;
		ОбластьЗаголовок.Параметры.ПредставлениеПериода     = Формат(НачалоПериода, "ДФ=dd.MM.yyyy; ДЛФ=D")+ " - " +
			Формат(КонецПериода, "ДФ=dd.MM.yyyy; ДЛФ=D");
		ОбластьЗаголовок.Параметры.ПредставлениеОрганизации = Организация.НаименованиеПолное;
		ОбластьЗаголовок.Параметры.ПредставлениеККМ         = ККМ.Наименование;
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
	КонецЕсли;
	
	Если ТолькоЗаголовок Тогда
		Возврат;
	КонецЕсли;
		
	ОбластьШапка = МакетОтчета.ПолучитьОбласть("Шапка");
	ТабличныйДокумент.Вывести(ОбластьШапка);
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ККМ", 			ККМ);
	Запрос.УстановитьПараметр("НачалоПериода",  НачалоДня(НачалоПериода));
	Запрос.УстановитьПараметр("КонецПериода", 	КонецДня(КонецПериода));
	
	Запрос.Текст = ТекстЗапроса();
	
	ОбластьСтрока = МакетОтчета.ПолучитьОбласть("Строка");
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		ОбластьСтрока.Параметры.Заполнить(Выборка);
		ТабличныйДокумент.Вывести(ОбластьСтрока);
	КонецЦикла;
	
	УправлениеОтчетами.УстановитьКолонтитулыПоУмолчанию(
		ТабличныйДокумент, , Строка(глЗначениеПеременной("глТекущийПользователь")));
		
	ОтчетСформирован = Истина;
	
КонецПроцедуры

#КонецЕсли
