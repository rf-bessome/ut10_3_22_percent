#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура СформироватьОтчет(ПараметрыОтчета, АдресХранилища) Экспорт
	
	СтруктураПараметров = Новый Структура;

	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ЖурналУчетаСчетовФактур";

	СведенияОрганизации = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(
		ПараметрыОтчета.Организация, ПараметрыОтчета.НалоговыйПериод);
	НаименованиеОрганизацииДляПечатныхФорм = ФормированиеПечатныхФорм.ОписаниеОрганизации(
		СведенияОрганизации, "ПолноеНаименование,");
		
	СписокОрганизаций = Новый СписокЗначений;
	
	Если ПараметрыОтчета.ЗаполнениеДекларации Тогда
		СписокОрганизаций = ПараметрыОтчета.СписокОрганизаций;
	ИначеЕсли ПараметрыОтчета.ВключатьОбособленныеПодразделения Тогда
		//СписокОрганизаций = ОбщегоНазначения.ПолучитьСписокОбособленныхПодразделенийОрганизации(
		//	ПараметрыОтчета.Организация);
		СписокОрганизаций.Добавить(ПараметрыОтчета.Организация);
	Иначе
		СписокОрганизаций.Добавить(ПараметрыОтчета.Организация);
	КонецЕсли;

	СтруктураПараметров.Вставить("Организация", ПараметрыОтчета.Организация);
	СтруктураПараметров.Вставить("НалоговыйПериод", ПараметрыОтчета.НалоговыйПериод);
	СтруктураПараметров.Вставить("НаименованиеОрганизацииДляПечатныхФорм", НаименованиеОрганизацииДляПечатныхФорм);
	СтруктураПараметров.Вставить("СписокОрганизаций", СписокОрганизаций);
	
	СтруктураПараметров.Вставить("СформироватьОтчетПоСтандартнойФорме", ПараметрыОтчета.СформироватьОтчетПоСтандартнойФорме);
	СтруктураПараметров.Вставить("ГруппироватьПоКонтрагентам",          ПараметрыОтчета.ГруппироватьПоКонтрагентам);
	СтруктураПараметров.Вставить("ОтбиратьПоКонтрагенту",               ПараметрыОтчета.ОтбиратьПоКонтрагенту);
	СтруктураПараметров.Вставить("КонтрагентДляОтбора",                 ПараметрыОтчета.КонтрагентДляОтбора);
	СтруктураПараметров.Вставить("ЗаполнениеДекларации",                ПараметрыОтчета.ЗаполнениеДекларации);
	СтруктураПараметров.Вставить("ЗаполнениеДокумента",
		?(ПараметрыОтчета.Свойство("ЗаполнениеДокумента"), ПараметрыОтчета.ЗаполнениеДокумента, Ложь));
	СтруктураПараметров.Вставить("ФорматПоПостановлению735",
		?(ПараметрыОтчета.Свойство("ФорматПоПостановлению735"), ПараметрыОтчета.ФорматПоПостановлению735, Ложь));
	СтруктураПараметров.Вставить("ПравилаЗаполнения", 
		?(ПараметрыОтчета.Свойство("ПравилаЗаполнения"), ПараметрыОтчета.ПравилаЗаполнения, "2021Кв3"));
	
	ИнициализироватьТаблицыДанныхНДС(СтруктураПараметров);
	
	ВерсияПостановленияНДС1137 = УчетНДС.ВерсияПостановленияНДС1137(ПараметрыОтчета.НалоговыйПериод);
	
	СтруктураПараметров.Вставить("ВерсияПостановленияНДС1137", ВерсияПостановленияНДС1137);
	
	Если ВерсияПостановленияНДС1137 >= 5 Тогда
		Макет = ПолучитьОбщийМакет("ЖурналУчетаСчетовФактур534");
	ИначеЕсли ВерсияПостановленияНДС1137 >= 4 Тогда
		Макет = ПолучитьОбщийМакет("ЖурналУчетаСчетовФактур981");
	ИначеЕсли ВерсияПостановленияНДС1137 = 3 Тогда
		Если СтруктураПараметров.ЗаполнениеДекларации
			И СтруктураПараметров.ПравилаЗаполнения = "2017Кв1" Тогда
			Макет = ПолучитьОбщийМакет("ЖурналУчетаСчетовФактур1137_2017");
		Иначе
			Макет = ПолучитьОбщийМакет("ЖурналУчетаСчетовФактур735");
		КонецЕсли;
	Иначе
		Макет = ПолучитьОбщийМакет("ЖурналУчетаСчетовФактур1137");
	КонецЕсли;
	
	СтруктураЗаписейЖурнала = ПолучитьЗаписиЖурналаСчетовФактур(СтруктураПараметров);
	
	// ШАПКА
	
	Отступ = Макет.ПолучитьОбласть("Отступ");
	
	Если СтруктураПараметров.ЗаполнениеДекларации Тогда
		
		СтруктураСекций = Новый Структура();
		СтруктураСекций.Вставить("ШапкаРаздел10", Макет.ПолучитьОбласть("ШапкаРаздел10"));
		ШапкаТаблицыРаздел10 = Макет.ПолучитьОбласть("ШапкаТаблицыРаздел10");
		ШапкаТаблицыРаздел10.Область("НомераГрафЖурналЧ1").Видимость = Ложь;
		СтруктураСекций.Вставить("ШапкаТаблицыРаздел10", ШапкаТаблицыРаздел10);
		
		ТабличныйДокумент.Вывести(СтруктураСекций.ШапкаРаздел10);
		ТабличныйДокумент.Вывести(СтруктураСекций.ШапкаТаблицыРаздел10);
		
	Иначе
		
		Если ПараметрыОтчета.СформироватьОтчетПоСтандартнойФорме Тогда
			Секция = Макет.ПолучитьОбласть("ШапкаИнформация");
			ТабличныйДокумент.Вывести(Секция);
		КонецЕсли;
		
		Шапка = Макет.ПолучитьОбласть("Шапка");
		
		Шапка.Параметры.Организация = НаименованиеОрганизацииДляПечатныхФорм;
		Шапка.Параметры.ИННКПП  = "" + СведенияОрганизации.ИНН 
			+ ?(НЕ ЗначениеЗаполнено(СведенияОрганизации.КПП), "", ("/" + СведенияОрганизации.КПП));
		
		Шапка.Параметры.Квартал = Формат(ПараметрыОтчета.НалоговыйПериод, "ДФ = к");
		Шапка.Параметры.Год     = Формат(ПараметрыОтчета.НалоговыйПериод, "ДФ = гггг");	
			
		ТабличныйДокумент.Вывести(Шапка);
			
		Если ВерсияПостановленияНДС1137 < 5 Тогда
			ТабличныйДокумент.Вывести(Отступ);
		КонецЕсли;
		
		Часть1Заголовок = Макет.ПолучитьОбласть("Часть1Заголовок");
		
		Если Часть1Заголовок.Области.Найти("НомераГрафКДекларацияР10") <> Неопределено Тогда
			Часть1Заголовок.Область("НомераГрафКДекларацияР10").Видимость = Ложь;
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(Часть1Заголовок);
		
	КонецЕсли;

	Часть1Строка_Обычная = Макет.ПолучитьОбласть("Часть1Строка");
	Если ВерсияПостановленияНДС1137 >= 3 Тогда
		Часть1Строка_Многострочная1 = Макет.ПолучитьОбласть("Часть1Строка_Многострочная1");
		Часть1Строка_Многострочная2 = Макет.ПолучитьОбласть("Часть1Строка_Многострочная2");
		Часть1Строка_Многострочная3 = Макет.ПолучитьОбласть("Часть1Строка_Многострочная3");
	КонецЕсли;
	
	Если ВерсияПостановленияНДС1137 >= 5 Тогда
		Часть1СтрокаРНПТ = Макет.ПолучитьОбласть("Часть1СтрокаРНПТ");
		ПараметрыСтрокиРНПТЧасть1 = Часть1СтрокаРНПТ.Параметры;
		Часть2СтрокаРНПТ = Макет.ПолучитьОбласть("Часть2СтрокаРНПТ");
		ПараметрыСтрокиРНПТЧасть2 = Часть2СтрокаРНПТ.Параметры;
	КонецЕсли;
	
	СтрокиЧасть1 = СтруктураЗаписейЖурнала.ВыставленныеСчетаФактуры;
	
	НомерПП = 1;
	СчетчикСтрокТекущегоСегмента = 0;
	
	Если НЕ ПараметрыОтчета.СформироватьОтчетПоСтандартнойФорме И ПараметрыОтчета.ГруппироватьПоКонтрагентам Тогда
		
		// Режим построения с группировкой по контрагенту
		СекцияКонтрагента = Макет.ПолучитьОбласть("Контрагент");
		ГруппировкаПоКонтрагенту = СтрокиЧасть1.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Если ГруппировкаПоКонтрагенту.Количество() > 0 Тогда
			
			ТабличныйДокумент.НачатьАвтогруппировкуСтрок();
			
			Пока ГруппировкаПоКонтрагенту.Следующий() Цикл
				
				СекцияКонтрагента.Параметры.Заполнить(ГруппировкаПоКонтрагенту);
				ТабличныйДокумент.Вывести(СекцияКонтрагента, 1);
				
				ГруппировкаПоСчетамФактурам = ГруппировкаПоКонтрагенту.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				
				Пока ГруппировкаПоСчетамФактурам.Следующий() Цикл
												
					СчетаФактурыЗаписи = ГруппировкаПоСчетамФактурам.Выбрать();
					КоличествоСгруппированных = СчетаФактурыЗаписи.Количество();
					СтруктураПараметров.Вставить("КоличествоСгруппированных", КоличествоСгруппированных);
					ИмяОбластиЧасть1 = "ОбластьЗаписьОсновнаяЧасть1";
					
					НомерСтрокиВГруппировке = 1;
					// Итоговые данные по счету-фактуре
					СуммаНДСИтоговая 							 = ГруппировкаПоСчетамФактурам.СуммаНДСИтоговая;
					СуммаНДСРазницаУменьшениеИтоговая 			 = ГруппировкаПоСчетамФактурам.СуммаНДСРазницаУменьшениеИтоговая;
					СуммаНДСРазницаУвеличениеИтоговая 			 = ГруппировкаПоСчетамФактурам.СуммаНДСРазницаУвеличениеИтоговая;
					
					Пока СчетаФактурыЗаписи.Следующий() Цикл
						
							ВыводитьОстатокПоСчетуФактуре = Ложь;
							
							Если ВерсияПостановленияНДС1137 >= 3 Тогда
								// Пересчитаем остаток по счету-фактуре
								СуммаНДСИтоговая 							 = СуммаНДСИтоговая - СчетаФактурыЗаписи.СуммаНДС;
								СуммаНДСРазницаУменьшениеИтоговая 			 = СуммаНДСРазницаУменьшениеИтоговая - СчетаФактурыЗаписи.СуммаНДСРазницаУменьшение;
								СуммаНДСРазницаУвеличениеИтоговая 			 = СуммаНДСРазницаУвеличениеИтоговая - СчетаФактурыЗаписи.СуммаНДСРазницаУвеличение;
							КонецЕсли;
							
							Если КоличествоСгруппированных > 1 
								И ВерсияПостановленияНДС1137 >= 3
								И НЕ СчетаФактурыЗаписи.СводныйКорректировочный Тогда
								
								ЕстьПодстроки = Истина;
								
								// Переопределим макет
								Если НомерСтрокиВГруппировке = 1 Тогда 
									Часть1Строка = Макет.ПолучитьОбласть("Часть1Строка_Многострочная1");
									ИмяОбластиЧасть1 = "ОбластьЗаписьОсновнаяЧасть1Многострочная1";
									Часть1Строка.Параметры.Ном = НомерПП;
									НомерПП = НомерПП + 1;
								Иначе
									Если НомерСтрокиВГруппировке < КоличествоСгруппированных Тогда
										Часть1Строка = Макет.ПолучитьОбласть("Часть1Строка_Многострочная2");		
									Иначе
										// Последняя строка
										Если НЕ СчетаФактурыЗаписи.СводныйКомиссионный 
											И (СуммаНДСИтоговая <> 0
											ИЛИ СуммаНДСРазницаУменьшениеИтоговая <> 0
											ИЛИ СуммаНДСРазницаУвеличениеИтоговая <> 0) Тогда
											ВыводитьОстатокПоСчетуФактуре = Истина;
											Часть1Строка = Макет.ПолучитьОбласть("Часть1Строка_Многострочная2");
										Иначе
											Часть1Строка = Макет.ПолучитьОбласть("Часть1Строка_Многострочная3");
										КонецЕсли;
									КонецЕсли;
								КонецЕсли;
								
							Иначе
								
								ЕстьПодстроки = Ложь;
								
								Если ВерсияПостановленияНДС1137 >= 3
									И НЕ СчетаФактурыЗаписи.СводныйКорректировочный
									И НЕ СчетаФактурыЗаписи.СводныйКомиссионный
									И (СуммаНДСИтоговая <> 0
									ИЛИ СуммаНДСРазницаУменьшениеИтоговая <> 0
									ИЛИ СуммаНДСРазницаУвеличениеИтоговая <> 0) Тогда
									// Если пока только один СФ от комитента
									ВыводитьОстатокПоСчетуФактуре = Истина;
									Часть1Строка = Часть1Строка_Многострочная1;
									ИмяОбластиЧасть1 = "ОбластьЗаписьОсновнаяЧасть1Многострочная1";
								Иначе
									Часть1Строка = Часть1Строка_Обычная;
									ИмяОбластиЧасть1 = "ОбластьЗаписьОсновнаяЧасть1";
								КонецЕсли;
								
								Часть1Строка.Параметры.Ном = НомерПП;
								НомерПП = НомерПП + 1;
							КонецЕсли;
							
							ЗаполнитьСтрокуЖурналаУчетаСчетовФактур(
								Часть1Строка.Параметры, СчетаФактурыЗаписи, СтруктураПараметров, , НомерПП, ЕстьПодстроки);
							Если НомерСтрокиВГруппировке = 1 Тогда
								ВывестиГраницуСтроки(СтруктураПараметров, Часть1Строка, ИмяОбластиЧасть1);
							КонецЕсли;
							ОбластьТабличногоДокумента = ТабличныйДокумент.Вывести(Часть1Строка, 2);
							Если НомерСтрокиВГруппировке = 1 Тогда
								ЗаполнитьПодстрочникРНПТ(
									СчетаФактурыЗаписи,
									ПараметрыСтрокиРНПТЧасть1,
									СтруктураПараметров,
									Часть1СтрокаРНПТ,
									ТабличныйДокумент,
									"ОбластьЗаписьЧасть1");
							КонецЕсли;
							Если ВыводитьОстатокПоСчетуФактуре Тогда 
								СтрокаОстаток = Макет.ПолучитьОбласть("Часть1Строка_Многострочная3");
								СтрокаОстаток.Параметры.Валюта = СокрП(СчетаФактурыЗаписи.НаименованиеВалюты) + ", " + СчетаФактурыЗаписи.КодВалюты;	
								СтрокаОстаток.Параметры.СуммаДокумента = ?(ГруппировкаПоСчетамФактурам.СуммаДокумента = 0, "", ГруппировкаПоСчетамФактурам.СуммаДокумента);
								СтрокаОстаток.Параметры.СуммаНДС = ?(СуммаНДСИтоговая = 0, "", СуммаНДСИтоговая);
								СтрокаОстаток.Параметры.СуммаДокументаРазницаУменьшение = ?(ГруппировкаПоСчетамФактурам.СуммаДокументаРазницаУменьшение = 0, 
									"", ГруппировкаПоСчетамФактурам.СуммаДокументаРазницаУменьшение);
								СтрокаОстаток.Параметры.СуммаДокументаРазницаУвеличение = ?(ГруппировкаПоСчетамФактурам.СуммаДокументаРазницаУвеличение = 0, 
									"", ГруппировкаПоСчетамФактурам.СуммаДокументаРазницаУвеличение);
								СтрокаОстаток.Параметры.СуммаНДСРазницаУменьшение = ?(СуммаНДСРазницаУменьшениеИтоговая = 0, "", СуммаНДСРазницаУменьшениеИтоговая);
								СтрокаОстаток.Параметры.СуммаНДСРазницаУвеличение = ?(СуммаНДСРазницаУвеличениеИтоговая = 0, "", СуммаНДСРазницаУвеличениеИтоговая);
								ОбластьТабличногоДокумента = ТабличныйДокумент.Вывести(СтрокаОстаток, 2);
							КонецЕсли;
							
							НомерСтрокиВГруппировке = НомерСтрокиВГруппировке + 1;
							
						КонецЦикла;
				КонецЦикла;
				
			КонецЦикла;
			ТабличныйДокумент.ЗакончитьАвтогруппировкуСтрок();
		КонецЕсли;
		
	Иначе
		
		ГруппировкаПоСчетамФактурам = СтрокиЧасть1.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ГруппировкаПоСчетамФактурам.Следующий() Цикл
						
			СчетаФактурыЗаписи = ГруппировкаПоСчетамФактурам.Выбрать();
			КоличествоСгруппированных = СчетаФактурыЗаписи.Количество();
			СтруктураПараметров.Вставить("КоличествоСгруппированных", КоличествоСгруппированных);
			ИмяОбластиЧасть1 = "ОбластьЗаписьОсновнаяЧасть1";
			НомерСтрокиВГруппировке = 1;
			// Итоговые данные по счету-фактуре
			СуммаНДСИтоговая                             = ГруппировкаПоСчетамФактурам.СуммаНДСИтоговая;
			СуммаПоСчетуФактуреРазницаУменьшениеИтоговая = ГруппировкаПоСчетамФактурам.СуммаПоСчетуФактуреРазницаУменьшениеИтоговая;
			СуммаПоСчетуФактуреРазницаУвеличениеИтоговая = ГруппировкаПоСчетамФактурам.СуммаПоСчетуФактуреРазницаУвеличениеИтоговая;
			СуммаНДСРазницаУменьшениеИтоговая            = ГруппировкаПоСчетамФактурам.СуммаНДСРазницаУменьшениеИтоговая;
			СуммаНДСРазницаУвеличениеИтоговая            = ГруппировкаПоСчетамФактурам.СуммаНДСРазницаУвеличениеИтоговая;
			
			Пока СчетаФактурыЗаписи.Следующий() Цикл
				
					
					ВыводитьОстатокПоСчетуФактуре = Ложь;
					
					Если ВерсияПостановленияНДС1137 >= 3 Тогда
						// Пересчитаем остаток по счету-фактуре
						СуммаНДСИтоговая                             = СуммаНДСИтоговая - СчетаФактурыЗаписи.СуммаНДС;
						СуммаНДСРазницаУменьшениеИтоговая            = СуммаНДСРазницаУменьшениеИтоговая - СчетаФактурыЗаписи.СуммаНДСРазницаУменьшение;
						СуммаНДСРазницаУвеличениеИтоговая            = СуммаНДСРазницаУвеличениеИтоговая - СчетаФактурыЗаписи.СуммаНДСРазницаУвеличение;
					КонецЕсли;
					
					Если КоличествоСгруппированных > 1 
						И ВерсияПостановленияНДС1137 >= 3
						И НЕ СчетаФактурыЗаписи.СводныйКорректировочный Тогда
						
						ЕстьПодстроки = Истина;
						
						// Переопределим макет
						Если НомерСтрокиВГруппировке = 1 Тогда 
							Часть1Строка = Макет.ПолучитьОбласть("Часть1Строка_Многострочная1");
							ИмяОбластиЧасть1 = "ОбластьЗаписьОсновнаяЧасть1Многострочная1";
							Часть1Строка.Параметры.Ном = НомерПП;
							Часть1Строка.Область(1, , Часть1Строка.ВысотаТаблицы).Имя = "Строка_" + НомерПП;
							НомерПП = НомерПП + 1;
							СчетчикСтрокТекущегоСегмента = СчетчикСтрокТекущегоСегмента + 1;
						Иначе
							Если НомерСтрокиВГруппировке < КоличествоСгруппированных Тогда
								Часть1Строка = Макет.ПолучитьОбласть("Часть1Строка_Многострочная2");
							Иначе
								// Последняя строка
								Если НЕ СчетаФактурыЗаписи.СводныйКомиссионный
									И (СуммаНДСИтоговая <> 0
									ИЛИ СуммаНДСРазницаУменьшениеИтоговая <> 0
									ИЛИ СуммаНДСРазницаУвеличениеИтоговая <> 0) Тогда
									ВыводитьОстатокПоСчетуФактуре = Истина;
								Иначе
									Часть1Строка = Часть1Строка_Обычная;
								КонецЕсли;
							КонецЕсли;
						КонецЕсли;
						
					Иначе
						
						ЕстьПодстроки = Ложь;
						
						Если ВерсияПостановленияНДС1137 >= 3
							И НЕ СчетаФактурыЗаписи.СводныйКорректировочный
							И НЕ СчетаФактурыЗаписи.СводныйКомиссионный
							И (СуммаНДСИтоговая <> 0
							ИЛИ СуммаНДСРазницаУменьшениеИтоговая <> 0
							ИЛИ СуммаНДСРазницаУвеличениеИтоговая <> 0) Тогда
							// Если пока только один СФ от комитента
							ВыводитьОстатокПоСчетуФактуре = Истина;
							Часть1Строка = Макет.ПолучитьОбласть("Часть1Строка_Многострочная1");
							ИмяОбластиЧасть1 = "ОбластьЗаписьОсновнаяЧасть1Многострочная1";
						Иначе
							Часть1Строка = Макет.ПолучитьОбласть("Часть1Строка");
							ИмяОбластиЧасть1 = "ОбластьЗаписьОсновнаяЧасть1";
						КонецЕсли;
						
						Часть1Строка.Параметры.Ном = НомерПП;
						Часть1Строка.Область(1, , Часть1Строка.ВысотаТаблицы).Имя = "Строка_" + НомерПП;
						НомерПП = НомерПП + 1;
						СчетчикСтрокТекущегоСегмента = СчетчикСтрокТекущегоСегмента + 1;
					КонецЕсли;
					
					ЗаполнитьСтрокуЖурналаУчетаСчетовФактур(
						Часть1Строка.Параметры, СчетаФактурыЗаписи, СтруктураПараметров, , НомерПП - 1, ЕстьПодстроки);
					Если НомерСтрокиВГруппировке = 1 Тогда
						ВывестиГраницуСтроки(СтруктураПараметров, Часть1Строка, ИмяОбластиЧасть1);
					КонецЕсли;
					ОбластьТабличногоДокумента = ТабличныйДокумент.Вывести(Часть1Строка);
					Если НомерСтрокиВГруппировке = 1 Тогда
						ЗаполнитьПодстрочникРНПТ(
							СчетаФактурыЗаписи,
							ПараметрыСтрокиРНПТЧасть1,
							СтруктураПараметров,
							Часть1СтрокаРНПТ,
							ТабличныйДокумент,
							"ОбластьЗаписьЧасть1");
					КонецЕсли;
					Если ВыводитьОстатокПоСчетуФактуре Тогда 
						
						Если СтруктураПараметров.ЗаполнениеДекларации Тогда

							СтруктураСвПосрДеят = Новый Структура(
								"НомСчФОтПрод,ДатаСчФОтПрод,ОКВ,СтоимТовСчФВс,СумНДССчФ,РазСтКСчФУм,РазСтКСчФУв,РазНДСКСчФУм,РазНДСКСчФУв,СвПрод");
							
							СтруктураСвПосрДеят.ОКВ           = СчетаФактурыЗаписи.КодВалюты;
							
							СтруктураСвПосрДеят.СтоимТовСчФВс = ?(ГруппировкаПоСчетамФактурам.СуммаДокумента = 0,
								"", ГруппировкаПоСчетамФактурам.СуммаДокумента);
							СтруктураСвПосрДеят.СумНДССчФ     = ?(СуммаНДСИтоговая = 0, "", СуммаНДСИтоговая);
							
							СтруктураСвПосрДеят.РазСтКСчФУм   = ?(ГруппировкаПоСчетамФактурам.СуммаДокументаРазницаУменьшение = 0,
								"", ГруппировкаПоСчетамФактурам.СуммаДокументаРазницаУменьшение);
							СтруктураСвПосрДеят.РазСтКСчФУв   = ?(ГруппировкаПоСчетамФактурам.СуммаДокументаРазницаУвеличение = 0, 
								"", ГруппировкаПоСчетамФактурам.СуммаДокументаРазницаУвеличение);
							
							СтруктураСвПосрДеят.РазНДСКСчФУм  = ?(СуммаНДСРазницаУменьшениеИтоговая = 0, "", СуммаНДСРазницаУменьшениеИтоговая);
							СтруктураСвПосрДеят.РазНДСКСчФУв  = ?(СуммаНДСРазницаУвеличениеИтоговая = 0, "", СуммаНДСРазницаУвеличениеИтоговая);
						
							СтрокаРаздела = СтруктураПараметров.ТаблицаРаздел10[СчетчикСтрокТекущегоСегмента - 1];
							Если СтруктураПараметров.ПравилаЗаполнения = "2015Кв1" Тогда
								МассивСвПосрДеят = СтрокаРаздела.СвПосрДеят;
							ИначеЕсли СтруктураПараметров.ПравилаЗаполнения = "2017Кв1"
								ИЛИ СтруктураПараметров.ПравилаЗаполнения = "2019Кв1"
								ИЛИ СтруктураПараметров.ПравилаЗаполнения = "2020Кв4"
								ИЛИ СтруктураПараметров.ПравилаЗаполнения = "2021Кв3" Тогда
								МассивСвПосрДеят = СтрокаРаздела.СвСчФОтПрод;
								
							КонецЕсли;
							МассивСвПосрДеят.Добавить(СтруктураСвПосрДеят);

						КонецЕсли;
						
						СтрокаОстаток = Макет.ПолучитьОбласть("Часть1Строка_Многострочная3");
						СтрокаОстаток.Параметры.Валюта = СокрП(СчетаФактурыЗаписи.НаименованиеВалюты) + ", " + СчетаФактурыЗаписи.КодВалюты;
						СтрокаОстаток.Параметры.СуммаДокумента = ?(ГруппировкаПоСчетамФактурам.СуммаДокумента = 0,
							"", ГруппировкаПоСчетамФактурам.СуммаДокумента);
						СтрокаОстаток.Параметры.СуммаНДС = ?(СуммаНДСИтоговая = 0, "", СуммаНДСИтоговая);
						СтрокаОстаток.Параметры.СуммаДокументаРазницаУменьшение = ?(ГруппировкаПоСчетамФактурам.СуммаДокументаРазницаУменьшение = 0, 
							"", ГруппировкаПоСчетамФактурам.СуммаДокументаРазницаУменьшение);
						СтрокаОстаток.Параметры.СуммаДокументаРазницаУвеличение = ?(ГруппировкаПоСчетамФактурам.СуммаДокументаРазницаУвеличение = 0, 
							"", ГруппировкаПоСчетамФактурам.СуммаДокументаРазницаУвеличение);
						СтрокаОстаток.Параметры.СуммаНДСРазницаУменьшение = ?(СуммаНДСРазницаУменьшениеИтоговая = 0, "", СуммаНДСРазницаУменьшениеИтоговая);
						СтрокаОстаток.Параметры.СуммаНДСРазницаУвеличение = ?(СуммаНДСРазницаУвеличениеИтоговая = 0, "", СуммаНДСРазницаУвеличениеИтоговая);
						ОбластьТабличногоДокумента = ТабличныйДокумент.Вывести(СтрокаОстаток);
						
					КонецЕсли;
					
					НомерСтрокиВГруппировке = НомерСтрокиВГруппировке + 1;
					
					Если СтруктураПараметров.ЗаполнениеДекларации 
						И СчетчикСтрокТекущегоСегмента = ПараметрыОтчета.ПредельноеКоличествоЗаписейВРазделеДекларацииПоНДС Тогда
						
					КонецЕсли;

					
				КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;

	// Запишем последний сегмент
	
	ТабличныйДокумент.Вывести(Отступ);
		
	Часть2Заголовок = Макет.ПолучитьОбласть("Часть2Заголовок");
		
	Если Часть2Заголовок.Области.Найти("НомераГрафКДекларацияР11") <> Неопределено Тогда
		Часть2Заголовок.Область("НомераГрафКДекларацияР11").Видимость = Ложь;
	КонецЕсли;
		
	ТабличныйДокумент.Вывести(Часть2Заголовок);
	
	Часть2Строка = Макет.ПолучитьОбласть("Часть2Строка");
	СтруктураПараметров.Вставить("КоличествоСгруппированных", 0);

	СтрокиЧасть2 = СтруктураЗаписейЖурнала.ПолученныеСчетаФактуры;
	ДополнительныеСвойства = СтруктураЗаписейЖурнала.ДополнительныеСвойства;
	ДополнительныеСвойства.Индексы.Добавить("СчетФактура");
	
	НомерПП = 1;
	СчетчикСтрокТекущегоСегмента = 0;
	
	Если НЕ ПараметрыОтчета.СформироватьОтчетПоСтандартнойФорме И ПараметрыОтчета.ГруппироватьПоКонтрагентам Тогда
		// Режим построения с группировкой по контрагенту
		СекцияКонтрагента = Макет.ПолучитьОбласть("Контрагент");
		ГруппировкаПоКонтрагенту = СтрокиЧасть2.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Если ГруппировкаПоКонтрагенту.Количество() > 0 Тогда
			ТабличныйДокумент.НачатьАвтогруппировкуСтрок();
			Пока ГруппировкаПоКонтрагенту.Следующий() Цикл
				
				СекцияКонтрагента.Параметры.Заполнить(ГруппировкаПоКонтрагенту);
				ТабличныйДокумент.Вывести(СекцияКонтрагента, 1);
				ГруппировкаПоСчетуФактуре = ГруппировкаПоКонтрагенту.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ГруппировкаПоСчетуФактуре.Следующий() Цикл
					СтруктураПараметров.Вставить("КоличествоСгруппированных", 0);
					ЗаполнитьСтрокуЖурналаУчетаСчетовФактур(
						Часть2Строка.Параметры, ГруппировкаПоСчетуФактуре, СтруктураПараметров, ДополнительныеСвойства, НомерПП);
					ОбластьТабличногоДокумента = ТабличныйДокумент.Вывести(Часть2Строка, 2);
					ЗаполнитьПодстрочникРНПТ(
						ГруппировкаПоСчетуФактуре,
						ПараметрыСтрокиРНПТЧасть2,
						СтруктураПараметров,
						Часть2СтрокаРНПТ,
						ТабличныйДокумент,
						"ОбластьЗаписьЧасть2");
					НомерПП = НомерПП + 1;
					
				КонецЦикла;
				
			КонецЦикла;
			ТабличныйДокумент.ЗакончитьАвтогруппировкуСтрок();
		КонецЕсли;
		
	Иначе
		
		Выборка = СтрокиЧасть2.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ВывестиГраницуСтроки(СтруктураПараметров, Часть2Строка, "ОбластьЗаписьОсновнаяЧасть2");
			ЗаполнитьСтрокуЖурналаУчетаСчетовФактур(
				Часть2Строка.Параметры, Выборка, СтруктураПараметров, ДополнительныеСвойства, НомерПП);
			Часть2Строка.Параметры.Ном = НомерПП;
			Часть2Строка.Область(1, , Часть2Строка.ВысотаТаблицы).Имя = "Строка_" + НомерПП;
			ОбластьТабличногоДокумента = ТабличныйДокумент.Вывести(Часть2Строка);
			ЗаполнитьПодстрочникРНПТ(
				Выборка,
				ПараметрыСтрокиРНПТЧасть2,
				СтруктураПараметров,
				Часть2СтрокаРНПТ,
				ТабличныйДокумент,
				"ОбластьЗаписьЧасть2");
			НомерПП = НомерПП + 1;
			СчетчикСтрокТекущегоСегмента = СчетчикСтрокТекущегоСегмента + 1;
			
			// Запись сегмента
			
			Если СтруктураПараметров.ЗаполнениеДекларации 
				И СчетчикСтрокТекущегоСегмента = ПараметрыОтчета.ПредельноеКоличествоЗаписейВРазделеДекларацииПоНДС Тогда
				
			КонецЕсли;
		
		КонецЦикла;
		
	КонецЕсли;
	
	// Запишем последний сегмент
	
	Если СтруктураПараметров.ЗаполнениеДекларации
		И СчетчикСтрокТекущегоСегмента <> 0
		И СчетчикСтрокТекущегоСегмента <> ПараметрыОтчета.ПредельноеКоличествоЗаписейВРазделеДекларацииПоНДС Тогда
		
	КонецЕсли;
	
	ТабличныйДокумент.Вывести(Отступ);
	
	Если ПараметрыОтчета.СформироватьОтчетПоСтандартнойФорме
		И НЕ СтруктураПараметров.ЗаполнениеДекларации Тогда
	
		// ПОДВАЛ
		
		СписокПоказателей = Новый Массив;
		СписокПоказателей.Добавить("ФИОРук");
		СписокПоказателей.Добавить("ФИО");
		
		СведенияОбОрганизации = РегламентированнаяОтчетность.ПолучитьСведенияОбОрганизации(
			СтруктураПараметров.Организация, ПараметрыОтчета.НалоговыйПериод, СписокПоказателей);
		СведенияОЮрФизЛице  = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(
			СтруктураПараметров.Организация, ПараметрыОтчета.НалоговыйПериод);
		
		Если СтруктураПараметров.Организация.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
			ИмяРук = "";
			ИмяОрг = СведенияОбОрганизации.ФИО;
			Свидетельство = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОЮрФизЛице, "Свидетельство,");
		Иначе
			ИмяРук = СведенияОбОрганизации.ФИОРук;
			ИмяОрг = "";
			Свидетельство = "";
		КонецЕсли;
		
		Подвал = Макет.ПолучитьОбласть("Подвал");
		
		Подвал.Параметры.ИмяРук = ИмяРук;
		Подвал.Параметры.ИмяОрг = ИмяОрг;
		Подвал.Параметры.Свидетельство = Свидетельство;
		
		ТабличныйДокумент.Вывести(Подвал);
		
	КонецЕсли;
	
	УправлениеОтчетами.УстановитьКолонтитулыПоУмолчанию(ТабличныйДокумент, , Строка(глЗначениеПеременной("глТекущийПользователь")));
	
	СтруктураПараметров.Вставить("СформированныйЖурнал", ТабличныйДокумент);
	
	Результат = СтруктураПараметров;
	
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	
КонецПроцедуры

Процедура ИнициализироватьТаблицыДанныхНДС(СтруктураПараметров)
	
	Если СтруктураПараметров.ЗаполнениеДекларации Тогда 
		
		Раздел10 = Новый ТаблицаЗначений; // Часть 1: выставленные счета-фактуры
		
		Раздел10.Колонки.Добавить("НомерПор", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(12, 0)));
		
		Раздел10.Колонки.Добавить("НомСчФПрод", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(0)));
		Раздел10.Колонки.Добавить("ДатаСчФПрод", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(10)));
		
		Раздел10.Колонки.Добавить("НомИспрСчФ", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(3, 0)));
		Раздел10.Колонки.Добавить("ДатаИспрСчФ", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(10)));
		
		Раздел10.Колонки.Добавить("НомКСчФПрод", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(0)));
		Раздел10.Колонки.Добавить("ДатаКСчФПрод", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(10)));
		
		Раздел10.Колонки.Добавить("НомИспрКСчФ", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(3, 0)));
		Раздел10.Колонки.Добавить("ДатаИспрКСчФ", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(10)));
		
		Раздел10.Колонки.Добавить("КодВидОпер", Новый ОписаниеТипов("Массив"));
		
		Раздел10.Колонки.Добавить("СвПокуп");
		
		Если СтруктураПараметров.ПравилаЗаполнения = "2015Кв1" Тогда
			Раздел10.Колонки.Добавить("ДатаВыст", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(10)));
			Раздел10.Колонки.Добавить("СвПосрДеят"); 
		ИначеЕсли СтруктураПараметров.ПравилаЗаполнения = "2017Кв1"
		      ИЛИ СтруктураПараметров.ПравилаЗаполнения = "2019Кв1"
		      ИЛИ СтруктураПараметров.ПравилаЗаполнения = "2020Кв4"
		      ИЛИ СтруктураПараметров.ПравилаЗаполнения = "2021Кв3" Тогда  
			  
			Раздел10.Колонки.Добавить("СвСчФОтПрод");
		КонецЕсли;
		
		Раздел11 = Новый ТаблицаЗначений; // Часть 2: полученные счета-фактуры
		
		Раздел11.Колонки.Добавить("НомерПор", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(12, 0)));
		
		Если СтруктураПараметров.ПравилаЗаполнения = "2015Кв1" Тогда
			Раздел11.Колонки.Добавить("ДатаПолуч", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(10)));
		КонецЕсли;
		
		Если СтруктураПараметров.ПравилаЗаполнения = "2021Кв3" Тогда
			Раздел11.Колонки.Добавить("СвРегНом", Новый ОписаниеТипов("Массив"));
		КонецЕсли;
		
		Раздел11.Колонки.Добавить("НомСчФПрод", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(0)));
		Раздел11.Колонки.Добавить("ДатаСчФПрод", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(10)));
		
		Раздел11.Колонки.Добавить("НомИспрСчФ", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(3, 0)));
		Раздел11.Колонки.Добавить("ДатаИспрСчФ", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(10)));
		
		Раздел11.Колонки.Добавить("НомКСчФПрод", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(0)));
		Раздел11.Колонки.Добавить("ДатаКСчФПрод", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(10)));
		
		Раздел11.Колонки.Добавить("НомИспрКСчФ", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(3, 0)));
		Раздел11.Колонки.Добавить("ДатаИспрКСчФ", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(10)));
		
		Раздел11.Колонки.Добавить("КодВидСд", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(1)));
		Раздел11.Колонки.Добавить("ОКВ", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(3)));
		
		Раздел11.Колонки.Добавить("СтоимТовСчФВс", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19, 2)));
		Раздел11.Колонки.Добавить("СумНДССчФ", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19, 2)));
		
		Раздел11.Колонки.Добавить("РазСтКСчФУм", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19, 2)));
		Раздел11.Колонки.Добавить("РазСтКСчФУв", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19, 2)));
		Раздел11.Колонки.Добавить("РазНДСКСчФУм", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19, 2)));
		Раздел11.Колонки.Добавить("РазНДСКСчФУв", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19, 2)));
		
		Раздел11.Колонки.Добавить("КодВидОпер", Новый ОписаниеТипов("Массив"));
		Раздел11.Колонки.Добавить("СвПрод");
		Раздел11.Колонки.Добавить("СвКомис");
		Раздел11.Колонки.Добавить("ДополнительныеСведения");
		
		СтруктураПараметров.Вставить("ТаблицаРаздел10", Раздел10);
		СтруктураПараметров.Вставить("ТаблицаРаздел11", Раздел11);
		
		СтруктураПараметров.Вставить("ТабличныйДокументРаздел10");
		СтруктураПараметров.Вставить("ТабличныйДокументРаздел11");
		
	ИначеЕсли СтруктураПараметров.ЗаполнениеДокумента Тогда
		
		ЗаписиВыставленных = Новый ТаблицаЗначений; // Часть 1: выставленные счета-фактуры
		
		ЗаписиВыставленных.Колонки.Добавить("НомПП",       Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(12, 0)));
		ЗаписиВыставленных.Колонки.Добавить("ДатаВыстСчФ", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(10)));
		
		ЗаписиВыставленных.Колонки.Добавить("НомерСчФ", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(1000)));
		ЗаписиВыставленных.Колонки.Добавить("ДатаСчФ", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(10)));
		
		ЗаписиВыставленных.Колонки.Добавить("НомерИспрСчФ", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(3, 0)));
		ЗаписиВыставленных.Колонки.Добавить("ДатаИспрСчФ", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(10)));
		
		ЗаписиВыставленных.Колонки.Добавить("НомерКСчФ", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(1000)));
		ЗаписиВыставленных.Колонки.Добавить("ДатаКСчФ", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(10)));
		
		ЗаписиВыставленных.Колонки.Добавить("НомерИспрКСчФ", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(3, 0)));
		ЗаписиВыставленных.Колонки.Добавить("ДатаИспрКСчФ", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(10)));
		
		ЗаписиВыставленных.Колонки.Добавить("НаимПок", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(1000)));
		
		ЗаписиВыставленных.Колонки.Добавить("ИННЮЛ", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(10)));
		ЗаписиВыставленных.Колонки.Добавить("КПП", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(9)));
		ЗаписиВыставленных.Колонки.Добавить("ИННФЛ", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(12)));
		
		Если СтруктураПараметров.НалоговыйПериод >= '20210701' Тогда
			// Массив структур СвГр10_23 с ключами:
			// КодОКВ, СтТовУчНалВсего, СвПосредн, ВтчСумНДС, РазнСтоимКСчФ, РазнНДСКСчФ, СвРегНом.
			ЗаписиВыставленных.Колонки.Добавить("СвГр10_23");
		ИначеЕсли СтруктураПараметров.НалоговыйПериод >= '20171001' Тогда
			// Массив структур СвГр10_19 с ключами:
			// КодОКВ, СтТовУчНалВсего, СвПосредн, ВтчСумНДС, РазнСтоимКСчФ, РазнНДСКСчФ
			ЗаписиВыставленных.Колонки.Добавить("СвГр10_19");
		Иначе
			ЗаписиВыставленных.Колонки.Добавить("КодОКВ", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(3)));
			ЗаписиВыставленных.Колонки.Добавить("СтТовУчНалВсего", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19, 2)));
			ЗаписиВыставленных.Колонки.Добавить("СвПосредн");
			ЗаписиВыставленных.Колонки.Добавить("ВтчСумНДС",  Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19, 2)));
			ЗаписиВыставленных.Колонки.Добавить("РазнСтоимКСчФ");
			ЗаписиВыставленных.Колонки.Добавить("РазнНДСКСчФ");
		КонецЕсли;
		
		ЗаписиВыставленных.Колонки.Добавить("КодВидОпер", Новый ОписаниеТипов("Массив"));
		
		
		ЗаписиПолученных = Новый ТаблицаЗначений; // Часть 2: полученные счета-фактуры
		
		ЗаписиПолученных.Колонки.Добавить("НомПП", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(12, 0)));
		ЗаписиПолученных.Колонки.Добавить("ДатаПолСчФ", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(10)));
		
		ЗаписиПолученных.Колонки.Добавить("НомерСчФ", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(1000)));
		ЗаписиПолученных.Колонки.Добавить("ДатаСчФ", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(10)));
		
		ЗаписиПолученных.Колонки.Добавить("НомерИспрСчФ", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(3, 0)));
		ЗаписиПолученных.Колонки.Добавить("ДатаИспрСчФ", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(10)));
		
		ЗаписиПолученных.Колонки.Добавить("НомерКСчФ", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(1000)));
		ЗаписиПолученных.Колонки.Добавить("ДатаКСчФ", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(10)));
		
		ЗаписиПолученных.Колонки.Добавить("НомерИспрКСчФ", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(3, 0)));
		ЗаписиПолученных.Колонки.Добавить("ДатаИспрКСчФ", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(10)));
		
		ЗаписиПолученных.Колонки.Добавить("НаимПрод", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(1000)));
		ЗаписиПолученных.Колонки.Добавить("ИННЮЛ", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(10)));
		ЗаписиПолученных.Колонки.Добавить("КПП", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(9)));
		ЗаписиПолученных.Колонки.Добавить("ИННФЛ", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(12)));
		
		ЗаписиПолученных.Колонки.Добавить("КодОКВ", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(3)));
		ЗаписиПолученных.Колонки.Добавить("СтТовУчНалВсего", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19, 2)));
		ЗаписиПолученных.Колонки.Добавить("КодВидОпер", Новый ОписаниеТипов("Массив"));
		ЗаписиПолученных.Колонки.Добавить("СвПосредн");
		
		ЗаписиПолученных.Колонки.Добавить("ВтчСумНДС", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19, 2)));
		
		ЗаписиПолученных.Колонки.Добавить("РазнСтоимКСчФ");
		ЗаписиПолученных.Колонки.Добавить("РазнНДСКСчФ");
		
		Если СтруктураПараметров.НалоговыйПериод >= '20210701' Тогда
			ЗаписиПолученных.Колонки.Добавить("СвРегНом");
		КонецЕсли;
		
		СтруктураПараметров.Вставить("ЗаписиВыставленных", ЗаписиВыставленных);
		СтруктураПараметров.Вставить("ЗаписиПолученных", ЗаписиПолученных);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСтрокуЖурналаУчетаСчетовФактур(
	ПараметрыСтроки, ЗаписьЖурналаУчетаСчетовФактур, СтруктураПараметров, ДополнительныеСвойства = Неопределено, НомерПП = 1, ЕстьПодстроки = Ложь) Экспорт
	
	// В соответствии с порядком заполнения журнала учета счетов-фактур, описанным в Постановлении № 981,
	// графы 14,16,17 части 1 журнала заполняются по-разному в зависимости от учетной ситуации.
	// Важно знать, разбивается ли строка на подстроки или нет.
	// В случае если не разбивается, то указанные графы заполняются полной суммой счета-фактуры.
	// Если разбивается, то графы заполняются стоимостью товаров(работ,услуг), приходящейся на продавца, указанного в графе 10.
	
	ВерсияПостановленияНДС1137 = УчетНДС.ВерсияПостановленияНДС1137(СтруктураПараметров.НалоговыйПериод);
	Правила981 = ВерсияПостановленияНДС1137 >= 4;
	РазбиватьСумму = Правила981 И ЕстьПодстроки;
	
	ТекстБезНДС = "без НДС";
	ТекстПустаяСтрока = "";
	
	ЗаполнитьЗначенияСвойств(ПараметрыСтроки, ЗаписьЖурналаУчетаСчетовФактур);
	
	// Универсальная структура для заполнения журнала по разным версиям постановления
	СоставСтроки = Новый Структура;

	СоставСтроки.Вставить("КодОперации",       ЗаписьЖурналаУчетаСчетовФактур.КодВидаОперации);
	СоставСтроки.Вставить("НомерСчетаФактуры", ЗаписьЖурналаУчетаСчетовФактур.НомерСчетаФактуры);
	СоставСтроки.Вставить("ДатаСчетаФактуры",  Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаСчетаФактуры, "ДФ=dd.MM.yyyy"));
	СоставСтроки.Вставить("НомерИсправления",  ЗаписьЖурналаУчетаСчетовФактур.НомерИсправления);
	СоставСтроки.Вставить("ДатаИсправления",   Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаИсправления, "ДФ=dd.MM.yyyy"));
	СоставСтроки.Вставить("НомерКорректировочногоСчетаФактуры", ЗаписьЖурналаУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры);
	СоставСтроки.Вставить("ДатаКорректировочногоСчетаФактуры",  Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаКорректировочногоСчетаФактуры, "ДФ=dd.MM.yyyy"));
	
	СоставСтроки.Вставить("НомерДата",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1;%2'"),
		СокрЛП(ЗаписьЖурналаУчетаСчетовФактур.НомерСчетаФактуры),
		Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаСчетаФактуры, "ДФ=dd.MM.yyyy")));
	СоставСтроки.Вставить("НомерОтДата", 
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 от %2'"),
		СокрЛП(ЗаписьЖурналаУчетаСчетовФактур.НомерСчетаФактуры),
		Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаСчетаФактуры, "ДФ=dd.MM.yyyy")));
		
	Если ЗначениеЗаполнено(СокрЛП(ЗаписьЖурналаУчетаСчетовФактур.НомерИсправления)) Тогда
		СоставСтроки.Вставить("НомерДатаИсправления",
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1;%2'"),
			СокрЛП(ЗаписьЖурналаУчетаСчетовФактур.НомерИсправления),
			Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаИсправления, "ДФ=dd.MM.yyyy")));
		СоставСтроки.Вставить("НомерОтДатаИсправленный",
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 от %2'"),
			СокрЛП(ЗаписьЖурналаУчетаСчетовФактур.НомерИсправления),
			Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаИсправления, "ДФ=dd.MM.yyyy")));
	Иначе
		СоставСтроки.Вставить("НомерДатаИсправления", "");
		СоставСтроки.Вставить("НомерОтДатаИсправленный", "");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СокрЛП(ЗаписьЖурналаУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры)) Тогда
		СоставСтроки.Вставить("НомерДатаКорректировки",
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1;%2'"), 
			СокрЛП(ЗаписьЖурналаУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры),
			Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаКорректировочногоСчетаФактуры, "ДФ=dd.MM.yyyy")));
		СоставСтроки.Вставить("НомерОтДатаКорректировочный",
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 от %2'"), 
			СокрЛП(ЗаписьЖурналаУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры),
			Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаКорректировочногоСчетаФактуры, "ДФ=dd.MM.yyyy")));
	Иначе
		СоставСтроки.Вставить("НомерДатаКорректировки", "");
		СоставСтроки.Вставить("НомерОтДатаКорректировочный", "");
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(СокрЛП(ЗаписьЖурналаУчетаСчетовФактур.НомерИсправленияКорректировочногоСчетаФактуры)) Тогда
		СоставСтроки.Вставить("НомерДатаИсправленияКорректировки",
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1;%2'"), 
			СокрЛП(ЗаписьЖурналаУчетаСчетовФактур.НомерИсправленияКорректировочногоСчетаФактуры),
			Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаИсправленияКорректировочногоСчетаФактуры, "ДФ=dd.MM.yyyy")));
		СоставСтроки.Вставить("НомерОтДатаИсправленныйКорректировочный",
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 от %2'"), 
			СокрЛП(ЗаписьЖурналаУчетаСчетовФактур.НомерИсправленияКорректировочногоСчетаФактуры),
			Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаИсправленияКорректировочногоСчетаФактуры, "ДФ=dd.MM.yyyy")));
	Иначе
		СоставСтроки.Вставить("НомерДатаИсправленияКорректировки", "");
		СоставСтроки.Вставить("НомерОтДатаИсправленныйКорректировочный", "");
	КонецЕсли; 
	
	СоставСтроки.Вставить("ДатаПередачиПолучения", Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаПередачиПолучения, "ДФ=dd.MM.yyyy"));
	СоставСтроки.Вставить("КонтрагентИННКПП", СокрЛП(ЗаписьЖурналаУчетаСчетовФактур.КонтрагентИНН)
		+ ?(НЕ ПустаяСтрока(ЗаписьЖурналаУчетаСчетовФактур.КонтрагентКПП),"/", "") + СокрЛП(ЗаписьЖурналаУчетаСчетовФактур.КонтрагентКПП));
	СоставСтроки.Вставить("Валюта", СокрП(ЗаписьЖурналаУчетаСчетовФактур.НаименованиеВалюты) 
		+ ", " + ЗаписьЖурналаУчетаСчетовФактур.КодВалюты);
		
	СвПоКомиссионеруДляДокумента= 	Новый Структура();
	СвПоКомиссионеруДляДокумента.Вставить("КодВидСдел", ЗаписьЖурналаУчетаСчетовФактур.КодВидаСделки);
	Если ДополнительныеСвойства <> Неопределено Тогда
		ДопСвойстваСчетаФактуры = ДополнительныеСвойства.НайтиСтроки(Новый Структура("СчетФактура", ЗаписьЖурналаУчетаСчетовФактур.СчетФактураДокумент));
		Субкомиссионер          = "";
		СубкомиссионерИНН_КПП   = "";
		Если ДопСвойстваСчетаФактуры.Количество() > 0 Тогда
			ТаблицаДопСвойств = ДополнительныеСвойства.СкопироватьКолонки();
			Для каждого СтрокаДопСвойства Из ДопСвойстваСчетаФактуры Цикл
				НоваяСтрока = ТаблицаДопСвойств.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДопСвойства);
			КонецЦикла;
			ТаблицаДопСвойств.Сортировать("НомерСтроки");
			Для каждого СтрокаТаблицы Из ТаблицаДопСвойств Цикл
				Субкомиссионер = Субкомиссионер + ?(ПустаяСтрока(Субкомиссионер), "", ";") 
					+ СтрокаТаблицы.Субкомиссионер;
				СубкомиссионерИНН_КПП = СубкомиссионерИНН_КПП + ?(ПустаяСтрока(СубкомиссионерИНН_КПП), "", ";")
					+ СокрЛП(СтрокаТаблицы.СубкомиссионерИНН)
					+ ?(НЕ ПустаяСтрока(СтрокаТаблицы.СубкомиссионерКПП),"/", "") 
					+ СокрЛП(СтрокаТаблицы.СубкомиссионерКПП);
			КонецЦикла;
			
			// Формат выгрузки раздела 11 декларации по НДС (с 1 квартала 2015 года)
			// не учитывает возможность реализации товаров комитента через нескольких субкомиссионеров
			// в один день (и получение на все реализации сводного счета-фактуры от комитента)
			// Для выгрузки используется один из субкомиссионеров (по последней или единственной строке ТаблицаДопСвойств)
			
			Если НЕ ПустаяСтрока(СтрокаТаблицы.СубкомиссионерИНН) Тогда
				
				СвПоКомиссионеру = Новый Структура();
				СвПоКомиссионеру = УчетНДС.ПолучитьСтруктуруРеквизитовКонтрагента(СтрокаТаблицы.СубкомиссионерИНН, СтрокаТаблицы.СубкомиссионерКПП, СтрокаТаблицы.Субкомиссионер);
				
				СвПоКомиссионеруДляДекларации = Новый Структура();
				
				Если СвПоКомиссионеру.Свойство("ИННФЛ") Тогда
					СвПоКомиссионеруДляДекларации.Вставить("ИННФЛ", 	СвПоКомиссионеру.ИННФЛ);
					
					СвПоКомиссионеруДляДокумента.Вставить("ИННФЛСубКом",СвПоКомиссионеру.ИННФЛ);
					СвПоКомиссионеруДляДокумента.Вставить("Фамилия", 	СвПоКомиссионеру.Фамилия);
					СвПоКомиссионеруДляДокумента.Вставить("Имя", 		СвПоКомиссионеру.Имя);
					СвПоКомиссионеруДляДокумента.Вставить("Отчество", 	СвПоКомиссионеру.Отчество);
				Иначе
					СвПоКомиссионеруДляДекларации.Вставить("ИННЮЛ", 	СвПоКомиссионеру.ИННЮЛ);
					СвПоКомиссионеруДляДекларации.Вставить("КПП", 		СвПоКомиссионеру.КПП);
					
					СвПоКомиссионеруДляДокумента.Вставить("ИННЮЛСубКом",СвПоКомиссионеру.ИННЮЛ);
					СвПоКомиссионеруДляДокумента.Вставить("КППСубКом", 	СвПоКомиссионеру.КПП);
					СвПоКомиссионеруДляДокумента.Вставить("НаимСубКом", СвПоКомиссионеру.НаимОрг);
				КонецЕсли;
				
				СоставСтроки.Вставить("СвКомис", 	СвПоКомиссионеруДляДекларации);
				
			КонецЕсли;
			
		КонецЕсли;
		
		СоставСтроки.Вставить("Субкомиссионер",        Субкомиссионер);
		СоставСтроки.Вставить("СубкомиссионерИНН_КПП", СубкомиссионерИНН_КПП);
	КонецЕсли;
	
	// РНПТ с 3 кв 2021 года.
	МассивСвРегНом = Новый Массив();
	Если СтруктураПараметров.ПравилаЗаполнения = "2021Кв3"
	   И СтруктураПараметров.Свойство("СведенияОРНПТ") Тогда
		
		СтрокаСведенийОРНПТ = СтруктураПараметров.СведенияОРНПТ.Найти(ЗаписьЖурналаУчетаСчетовФактур.СчетФактураДокумент, "Ссылка");
		
		Если СтрокаСведенийОРНПТ <> Неопределено
		   И ЗначениеЗаполнено(СтрокаСведенийОРНПТ.СвРегНом) Тогда
			МассивСвРегНом = ОбщегоНазначенияКлиентСервер.СвернутьМассив(СтрокаСведенийОРНПТ.СвРегНом);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗаписьЖурналаУчетаСчетовФактур.СуммаДокумента < 0 Тогда
		УчетНДСПереопределяемый.ПреобразоватьПоказателиРНПТКСторнирующейЗаписи(МассивСвРегНом);
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(МассивСвРегНом) Тогда
		// Выводим первую строку и сохраняем коллекцию в параметрах формирования
		ПервыйРНПТ = МассивСвРегНом[0];
		СоставСтроки.Вставить("РегНомер",     ПервыйРНПТ.РегНомПросл);
		СоставСтроки.Вставить("КолПрослеж",   ПервыйРНПТ.КолТовПросл);
		СоставСтроки.Вставить("СтоимПрослеж", ПервыйРНПТ.СтоимТовПросл);
		СоставСтроки.Вставить("ЕдПрослеж",    ПервыйРНПТ.ОКЕИ);
		СтруктураПараметров.Вставить("МассивСвРегНом", МассивСвРегНом);
	Иначе
		СоставСтроки.Вставить("РегНомер",     "");
		СоставСтроки.Вставить("КолПрослеж",   "");
		СоставСтроки.Вставить("СтоимПрослеж", "");
		СоставСтроки.Вставить("ЕдПрослеж",    "");
		СтруктураПараметров.Вставить("МассивСвРегНом", Неопределено);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СокрЛП(ЗаписьЖурналаУчетаСчетовФактур.НомерСчетаФактурыКомитента)) Тогда
		СоставСтроки.Вставить("НомерДатаСчетаФактурыКомитента",
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1;%2'"), 
			СокрЛП(ЗаписьЖурналаУчетаСчетовФактур.НомерСчетаФактурыКомитента),
			Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаСчетаФактурыКомитента, "ДФ=dd.MM.yyyy")));
		СоставСтроки.Вставить("НомерОтДатаСчетаФактурыКомитента",
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 от %2'"), 
			СокрЛП(ЗаписьЖурналаУчетаСчетовФактур.НомерСчетаФактурыКомитента),
			Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаСчетаФактурыКомитента, "ДФ=dd.MM.yyyy")));
	КонецЕсли;
		
	Если ЗаписьЖурналаУчетаСчетовФактур.КорректировочныйСчетФактура Тогда
		
		СоставСтроки.Вставить("СуммаДокумента", ТекстПустаяСтрока);
		СоставСтроки.Вставить("СуммаНДС", ТекстПустаяСтрока);
		
		СоставСтроки.Вставить("СуммаДокументаРазницаУменьшение", ?(РазбиватьСумму,
			ЗаписьЖурналаУчетаСчетовФактур.СуммаДокументаРазницаУменьшениеКомиссия,
			ЗаписьЖурналаУчетаСчетовФактур.СуммаДокументаРазницаУменьшение));
		СоставСтроки.Вставить("СуммаДокументаРазницаУвеличение", ?(РазбиватьСумму,
			ЗаписьЖурналаУчетаСчетовФактур.СуммаДокументаРазницаУвеличениеКомиссия,
			ЗаписьЖурналаУчетаСчетовФактур.СуммаДокументаРазницаУвеличение));
			
		СоставСтроки.Вставить("СуммаНДСРазницаУменьшение", ЗаписьЖурналаУчетаСчетовФактур.СуммаНДСРазницаУменьшение);
		СоставСтроки.Вставить("СуммаНДСРазницаУвеличение", ЗаписьЖурналаУчетаСчетовФактур.СуммаНДСРазницаУвеличение);
		
	Иначе
		
		СоставСтроки.Вставить("СуммаДокумента", ?(РазбиватьСумму,
			ЗаписьЖурналаУчетаСчетовФактур.СуммаДокументаКомиссия,
			ЗаписьЖурналаУчетаСчетовФактур.СуммаДокумента));
		СоставСтроки.Вставить("СуммаНДС", ЗаписьЖурналаУчетаСчетовФактур.СуммаНДС);
		
		СоставСтроки.Вставить("НомерКорректировочногоСчетаФактуры", ТекстПустаяСтрока);
		СоставСтроки.Вставить("ДатаКорректировочногоСчетаФактуры", ТекстПустаяСтрока);
		
		СоставСтроки.Вставить("СуммаДокументаРазницаУменьшение", ТекстПустаяСтрока);
		СоставСтроки.Вставить("СуммаДокументаРазницаУвеличение", ТекстПустаяСтрока);
		СоставСтроки.Вставить("СуммаНДСРазницаУменьшение", ТекстПустаяСтрока);
		СоставСтроки.Вставить("СуммаНДСРазницаУвеличение", ТекстПустаяСтрока);
		
	КонецЕсли;
	
	Если ЗаписьЖурналаУчетаСчетовФактур.Контрагент = ЗаписьЖурналаУчетаСчетовФактур.Организация Тогда
		СоставСтроки.Вставить("КонтрагентНаименование", СтруктураПараметров.НаименованиеОрганизацииДляПечатныхФорм);
	КонецЕсли;
	
	Если ЗаписьЖурналаУчетаСчетовФактур.СчетФактураБезНДС Тогда
		
		СодержаниеГрафНДС = ?(ВерсияПостановленияНДС1137 >= 4, 0, ТекстБезНДС);
		Если ЗаписьЖурналаУчетаСчетовФактур.КорректировочныйСчетФактура Тогда
			СоставСтроки.Вставить("СуммаНДСРазницаУменьшение", СодержаниеГрафНДС);
			СоставСтроки.Вставить("СуммаНДСРазницаУвеличение", СодержаниеГрафНДС);
		Иначе
			СоставСтроки.Вставить("СуммаНДС", СодержаниеГрафНДС);
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ПараметрыСтроки, СоставСтроки);
	
	Если СтруктураПараметров.ЗаполнениеДекларации Тогда
		
		Если ЗаписьЖурналаУчетаСчетовФактур.ЧастьЖурнала = Перечисления.ЧастиЖурналаУчетаСчетовФактур.ВыставленныеСчетаФактуры Тогда
			
			// Раздел 10
			
			// Массив структур со сведениями о посреднической деятельности
			СтруктураСвПосрДеят = Новый Структура(
				"НомСчФОтПрод,ДатаСчФОтПрод,ОКВ,СтоимТовСчФВс,СумНДССчФ,РазСтКСчФУм,РазСтКСчФУв,РазНДСКСчФУм,РазНДСКСчФУв,СвПрод");
			
			Если СтруктураПараметров.ПравилаЗаполнения = "2021Кв3" Тогда
				СтруктураСвПосрДеят.Вставить("СвРегНом", МассивСвРегНом);
			КонецЕсли;
			
			СтруктураСвПосрДеят.НомСчФОтПрод  = СокрЛП(ЗаписьЖурналаУчетаСчетовФактур.НомерСчетаФактурыКомитента);
			СтруктураСвПосрДеят.ДатаСчФОтПрод = Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаСчетаФактурыКомитента, "ДФ=dd.MM.yyyy");
			СтруктураСвПосрДеят.ОКВ           = ЗаписьЖурналаУчетаСчетовФактур.КодВалюты;
			
			СтруктураСвПосрДеят.СтоимТовСчФВс =
				ПреобразоватьСтрокуВФорматДекларации(СоставСтроки.СуммаДокумента, ТекстБезНДС, ТекстПустаяСтрока);
			СтруктураСвПосрДеят.СумНДССчФ =
				ПреобразоватьСтрокуВФорматДекларации(СоставСтроки.СуммаНДС, ТекстБезНДС, ТекстПустаяСтрока);
			СтруктураСвПосрДеят.РазСтКСчФУм =
				ПреобразоватьСтрокуВФорматДекларации(СоставСтроки.СуммаДокументаРазницаУменьшение, ТекстБезНДС, ТекстПустаяСтрока);
			СтруктураСвПосрДеят.РазСтКСчФУв =
				ПреобразоватьСтрокуВФорматДекларации(СоставСтроки.СуммаДокументаРазницаУвеличение, ТекстБезНДС, ТекстПустаяСтрока);
			СтруктураСвПосрДеят.РазНДСКСчФУм =
				ПреобразоватьСтрокуВФорматДекларации(СоставСтроки.СуммаНДСРазницаУменьшение, ТекстБезНДС, ТекстПустаяСтрока);
			СтруктураСвПосрДеят.РазНДСКСчФУв =
				ПреобразоватьСтрокуВФорматДекларации(СоставСтроки.СуммаНДСРазницаУвеличение, ТекстБезНДС, ТекстПустаяСтрока);
			
			Если НЕ ПустаяСтрока(ЗаписьЖурналаУчетаСчетовФактур.КомитентИНН) Тогда
				
				СтруктураСвПосрДеят.СвПрод = УчетНДС.ПолучитьСтруктуруРеквизитовКонтрагента(
					ЗаписьЖурналаУчетаСчетовФактур.КомитентИНН, ЗаписьЖурналаУчетаСчетовФактур.КомитентКПП);
				
			КонецЕсли;
			
			СтруктураОтбора = Новый Структура("НомерПор", НомерПП);
			// Осуществляем поиск по таблице, в случе если записи нет - добавляем новую строку, 
			// иначе добавляем сведения о комитенте в колонку "СвПосрДеят" (с 1 кв 2017 года в "СвСчФОтПрод").
			СтрокиРаздела10 = СтруктураПараметров.ТаблицаРаздел10.НайтиСтроки(СтруктураОтбора);
			
			Если СтрокиРаздела10.Количество() = 0 Тогда
				
				СтрокаРаздела = СтруктураПараметров.ТаблицаРаздел10.Добавить();
				
				СоставСтроки.Вставить("НомерПор", НомерПП);
				
				Если СтруктураПараметров.ПравилаЗаполнения = "2015Кв1" Тогда
					СоставСтроки.Вставить("ДатаВыст", Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаПередачиПолучения, "ДФ=dd.MM.yyyy"));
				КонецЕсли;
				
				СоставСтроки.Вставить("НомСчФПрод", ЗаписьЖурналаУчетаСчетовФактур.НомерСчетаФактуры);
				СоставСтроки.Вставить("ДатаСчФПрод", Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаСчетаФактуры, "ДФ=dd.MM.yyyy"));
				
				СоставСтроки.Вставить("НомИспрСчФ", ЗаписьЖурналаУчетаСчетовФактур.НомерИсправления);
				СоставСтроки.Вставить("ДатаИспрСчФ", Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаИсправления, "ДФ=dd.MM.yyyy"));
				
				СоставСтроки.Вставить("НомКСчФПрод", ЗаписьЖурналаУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры);
				СоставСтроки.Вставить("ДатаКСчФПрод", Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаКорректировочногоСчетаФактуры, "ДФ=dd.MM.yyyy"));
				
				СоставСтроки.Вставить("НомИспрКСчФ", ЗаписьЖурналаУчетаСчетовФактур.НомерИсправленияКорректировочногоСчетаФактуры);
				СоставСтроки.Вставить("ДатаИспрКСчФ", Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаИсправленияКорректировочногоСчетаФактуры, "ДФ=dd.MM.yyyy"));
				
				МассивКодовОпераций = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ЗаписьЖурналаУчетаСчетовФактур.КодВидаОперации, ";");
				СоставСтроки.Вставить("КодВидОпер", МассивКодовОпераций);
				
				Если НЕ ПустаяСтрока(ЗаписьЖурналаУчетаСчетовФактур.КонтрагентИНН) Тогда
					СоставСтроки.Вставить("СвПокуп", 
						УчетНДС.ПолучитьСтруктуруРеквизитовКонтрагента(ЗаписьЖурналаУчетаСчетовФактур.КонтрагентИНН, ЗаписьЖурналаУчетаСчетовФактур.КонтрагентКПП));
				КонецЕсли;
				
				МассивСвПосрДеят = Новый Массив();
				МассивСвПосрДеят.Добавить(СтруктураСвПосрДеят);
				
				Если СтруктураПараметров.ПравилаЗаполнения = "2015Кв1" Тогда
					СоставСтроки.Вставить("СвПосрДеят", МассивСвПосрДеят);
				ИначеЕсли СтруктураПараметров.ПравилаЗаполнения = "2017Кв1"
						ИЛИ СтруктураПараметров.ПравилаЗаполнения = "2019Кв1"
						ИЛИ СтруктураПараметров.ПравилаЗаполнения = "2020Кв4"
						ИЛИ СтруктураПараметров.ПравилаЗаполнения = "2021Кв3" Тогда  
						
					СоставСтроки.Вставить("СвСчФОтПрод", МассивСвПосрДеят);
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(СтрокаРаздела, СоставСтроки);
				
			Иначе
				
				СтрокаРаздела = СтрокиРаздела10[0];
				Если СтруктураПараметров.ПравилаЗаполнения = "2015Кв1" Тогда
					МассивСвПосрДеят = СтрокаРаздела.СвПосрДеят;
				ИначеЕсли СтруктураПараметров.ПравилаЗаполнения = "2017Кв1"
						ИЛИ СтруктураПараметров.ПравилаЗаполнения = "2019Кв1"
						ИЛИ СтруктураПараметров.ПравилаЗаполнения = "2020Кв4"
						ИЛИ СтруктураПараметров.ПравилаЗаполнения = "2021Кв3" Тогда
					МассивСвПосрДеят = СтрокаРаздела.СвСчФОтПрод;
				КонецЕсли;
				МассивСвПосрДеят.Добавить(СтруктураСвПосрДеят);
				
				Если СтруктураПараметров.ПравилаЗаполнения = "2021Кв3" Тогда
					СтруктураСвПосрДеятБезРНПТ = Новый Структура("НомСчФОтПрод,ДатаСчФОтПрод,ОКВ,СтоимТовСчФВс,СумНДССчФ,
						|РазСтКСчФУм,РазСтКСчФУв,РазНДСКСчФУм,РазНДСКСчФУв,СвПрод");
					ЗаполнитьЗначенияСвойств(СтруктураСвПосрДеятБезРНПТ, СтруктураСвПосрДеят);
					СтруктураСвПосрДеятБезРНПТ.Вставить("СвРегНом", Новый Массив());
					МассивСвПосрДеят.Добавить(СтруктураСвПосрДеятБезРНПТ);
				Иначе
					МассивСвПосрДеят.Добавить(СтруктураСвПосрДеят);
				КонецЕсли;	
			КонецЕсли;
			
		Иначе
			
			// Раздел 11
			
			СоставСтроки.Вставить("НомерПор", НомерПП);
			Если СтруктураПараметров.ПравилаЗаполнения = "2015Кв1" Тогда
				СоставСтроки.Вставить("ДатаПолуч", Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаПередачиПолучения, "ДФ=dd.MM.yyyy"));
			КонецЕсли;
			
			Если СтруктураПараметров.ПравилаЗаполнения = "2021Кв3" Тогда
				СоставСтроки.Вставить("СвРегНом", МассивСвРегНом);
			КонецЕсли;
			
			СоставСтроки.Вставить("НомСчФПрод", ЗаписьЖурналаУчетаСчетовФактур.НомерСчетаФактуры);
			СоставСтроки.Вставить("ДатаСчФПрод", Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаСчетаФактуры, "ДФ=dd.MM.yyyy"));
			
			СоставСтроки.Вставить("НомИспрСчФ", ЗаписьЖурналаУчетаСчетовФактур.НомерИсправления);
			СоставСтроки.Вставить("ДатаИспрСчФ", Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаИсправления, "ДФ=dd.MM.yyyy"));
			
			СоставСтроки.Вставить("НомКСчФПрод", ЗаписьЖурналаУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры);
			СоставСтроки.Вставить("ДатаКСчФПрод", Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаКорректировочногоСчетаФактуры, "ДФ=dd.MM.yyyy"));
			
			СоставСтроки.Вставить("НомИспрКСчФ", ЗаписьЖурналаУчетаСчетовФактур.НомерИсправленияКорректировочногоСчетаФактуры);
			СоставСтроки.Вставить("ДатаИспрКСчФ", Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаИсправленияКорректировочногоСчетаФактуры, "ДФ=dd.MM.yyyy"));
			
			МассивКодовОпераций = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ЗаписьЖурналаУчетаСчетовФактур.КодВидаОперации, ";");
			СоставСтроки.Вставить("КодВидОпер", МассивКодовОпераций);
			СоставСтроки.Вставить("КодВидСд", ЗаписьЖурналаУчетаСчетовФактур.КодВидаСделки);
			СоставСтроки.Вставить("ОКВ", ЗаписьЖурналаУчетаСчетовФактур.КодВалюты);
			
			СоставСтроки.Вставить("СтоимТовСчФВс",
				ПреобразоватьСтрокуВФорматДекларации(СоставСтроки.СуммаДокумента, ТекстБезНДС, ТекстПустаяСтрока));
			СоставСтроки.Вставить("СумНДССчФ",
				ПреобразоватьСтрокуВФорматДекларации(СоставСтроки.СуммаНДС, ТекстБезНДС, ТекстПустаяСтрока));
			СоставСтроки.Вставить("РазСтКСчФУм",
				ПреобразоватьСтрокуВФорматДекларации(СоставСтроки.СуммаДокументаРазницаУменьшение, ТекстБезНДС, ТекстПустаяСтрока));
			СоставСтроки.Вставить("РазСтКСчФУв",
				ПреобразоватьСтрокуВФорматДекларации(СоставСтроки.СуммаДокументаРазницаУвеличение, ТекстБезНДС, ТекстПустаяСтрока));
			СоставСтроки.Вставить("РазНДСКСчФУм",
				ПреобразоватьСтрокуВФорматДекларации(СоставСтроки.СуммаНДСРазницаУменьшение, ТекстБезНДС, ТекстПустаяСтрока));
			СоставСтроки.Вставить("РазНДСКСчФУв",
				ПреобразоватьСтрокуВФорматДекларации(СоставСтроки.СуммаНДСРазницаУвеличение, ТекстБезНДС, ТекстПустаяСтрока));
			
			Если НЕ ПустаяСтрока(ЗаписьЖурналаУчетаСчетовФактур.КонтрагентИНН) Тогда
				СоставСтроки.Вставить("СвПрод", 
					УчетНДС.ПолучитьСтруктуруРеквизитовКонтрагента(ЗаписьЖурналаУчетаСчетовФактур.КонтрагентИНН, ЗаписьЖурналаУчетаСчетовФактур.КонтрагентКПП));
			КонецЕсли;
				
			СтрокаРаздела = СтруктураПараметров.ТаблицаРаздел11.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаРаздела, СоставСтроки);
			
		КонецЕсли;
		
	ИначеЕсли СтруктураПараметров.ЗаполнениеДокумента 
		И СтруктураПараметров.ФорматПоПостановлению735 Тогда 
		
		Если ЗаписьЖурналаУчетаСчетовФактур.ЧастьЖурнала = Перечисления.ЧастиЖурналаУчетаСчетовФактур.ВыставленныеСчетаФактуры Тогда
			// С 01.10.2017 сведения о посреднической деятельности передаются в массиве структур
			СвГр10_19 = Новый Структура("КодОКВ,СтТовУчНалВсего,СвПосредн,ВтчСумНДС,РазнСтоимКСчФ,РазнНДСКСчФ");
			СвГр10_23 = Новый Структура("КодОКВ,СтТовУчНалВсего,СвПосредн,ВтчСумНДС,РазнСтоимКСчФ,РазнНДСКСчФ,СвРегНом");

			СоставСтроки.Вставить("КодОКВ", 	ЗаписьЖурналаУчетаСчетовФактур.КодВалюты);
			СоставСтроки.Вставить("СтТовУчНалВсего",
			ПреобразоватьСтрокуВФорматДекларации(СоставСтроки.СуммаДокумента, ТекстБезНДС, ТекстПустаяСтрока));
			МассивКодовОпераций = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ЗаписьЖурналаУчетаСчетовФактур.КодВидаОперации, ";");	
			СоставСтроки.Вставить("КодВидОпер", МассивКодовОпераций);
			
			СвПосредн = Новый Структура();
			СвПосредн.Вставить("НомерСчФпрод", СокрЛП(ЗаписьЖурналаУчетаСчетовФактур.НомерСчетаФактурыКомитента));
			СвПосредн.Вставить("ДатаСчФпрод",  Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаСчетаФактурыКомитента, "ДФ=dd.MM.yyyy"));
			
			Если НЕ ПустаяСтрока(ЗаписьЖурналаУчетаСчетовФактур.КомитентИНН) Тогда
				
				СтруктураРеквизитовКонтрагента = УчетНДС.ПолучитьСтруктуруРеквизитовКонтрагента(ЗаписьЖурналаУчетаСчетовФактур.КомитентИНН, ЗаписьЖурналаУчетаСчетовФактур.КомитентКПП, ЗаписьЖурналаУчетаСчетовФактур.Комитент);
				
				Если СтруктураРеквизитовКонтрагента.Свойство("ИННФЛ") Тогда
					СвПосредн.Вставить("ИННФЛ", 	СтруктураРеквизитовКонтрагента.ИННФЛ);
					СвПосредн.Вставить("Фамилия", 	СтруктураРеквизитовКонтрагента.Фамилия);
					СвПосредн.Вставить("Имя", 		СтруктураРеквизитовКонтрагента.Имя);
					СвПосредн.Вставить("Отчество", 	СтруктураРеквизитовКонтрагента.Отчество);
				Иначе
					СвПосредн.Вставить("ИННЮЛ", 	СтруктураРеквизитовКонтрагента.ИННЮЛ);
					СвПосредн.Вставить("КПП", 		СтруктураРеквизитовКонтрагента.КПП);
					СвПосредн.Вставить("НаимОрг ", 	СтруктураРеквизитовКонтрагента.НаимОрг );
				КонецЕсли;
				
				Если СвПосредн.НомерСчФпрод <> "" Тогда 
					СоставСтроки.Вставить("СвПосредн", СвПосредн);
				КонецЕсли;
				
			КонецЕсли;
			
			СоставСтроки.Вставить("ВтчСумНДС",
			ПреобразоватьСтрокуВФорматДекларации(СоставСтроки.СуммаНДС, ТекстБезНДС, ТекстПустаяСтрока));
			
			РазнСтоимКСчФ = Новый Структура();
			Ум = ПреобразоватьСтрокуВФорматДекларации(СоставСтроки.СуммаДокументаРазницаУменьшение, ТекстБезНДС, ТекстПустаяСтрока);
			Если Ум <> 0 Тогда 
				РазнСтоимКСчФ.Вставить("Ум", Ум);
			КонецЕсли;
			Увел = ПреобразоватьСтрокуВФорматДекларации(СоставСтроки.СуммаДокументаРазницаУвеличение, ТекстБезНДС, ТекстПустаяСтрока);
			Если Увел <> 0 Тогда 
				РазнСтоимКСчФ.Вставить("Увел", Увел);
			КонецЕсли;
			
			Если РазнСтоимКСчФ.Свойство("Ум")
				ИЛИ РазнСтоимКСчФ.Свойство("Увел") Тогда 
				СоставСтроки.Вставить("РазнСтоимКСчФ", РазнСтоимКСчФ);
			КонецЕсли;
			
			РазнНДСКСчФ = Новый Структура();
			Ум = ПреобразоватьСтрокуВФорматДекларации(СоставСтроки.СуммаНДСРазницаУменьшение, ТекстБезНДС, ТекстПустаяСтрока);
			Если Ум <> 0 Тогда 
				РазнНДСКСчФ.Вставить("Ум", Ум);
			КонецЕсли;
			Увел = ПреобразоватьСтрокуВФорматДекларации(СоставСтроки.СуммаНДСРазницаУвеличение, ТекстБезНДС, ТекстПустаяСтрока);
			Если Увел <> 0 Тогда 
				РазнНДСКСчФ.Вставить("Увел", Увел);
			КонецЕсли;
			
			Если РазнНДСКСчФ.Свойство("Ум")
				ИЛИ РазнНДСКСчФ.Свойство("Увел") Тогда 
				СоставСтроки.Вставить("РазнНДСКСчФ", РазнНДСКСчФ);
			КонецЕсли;
			
			Если СтруктураПараметров.ПравилаЗаполнения = "2021Кв3"
			   И ЗначениеЗаполнено(МассивСвРегНом) Тогда
				СоставСтроки.Вставить("СвРегНом", МассивСвРегНом);
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(СвГр10_19, СоставСтроки);
			ЗаполнитьЗначенияСвойств(СвГр10_23, СоставСтроки);
			
			СтруктураОтбора = Новый Структура("НомПП", НомерПП);
			// Осуществляем поиск по таблице, в случе если записи нет - добавляем новую строку,
			// иначе не добавляем - не предусмотрено форматом
			СтрокиЧ1 = СтруктураПараметров.ЗаписиВыставленных.НайтиСтроки(СтруктураОтбора);
			Если СтрокиЧ1.Количество()= 0 Тогда 
				
				СоставСтроки.Вставить("НомПП", 			НомерПП);
				СоставСтроки.Вставить("ДатаВыстСчФ", 	Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаПередачиПолучения, "ДФ=dd.MM.yyyy"));
				СоставСтроки.Вставить("НомерСчФ", 		ЗаписьЖурналаУчетаСчетовФактур.НомерСчетаФактуры);
				СоставСтроки.Вставить("ДатаСчФ", 		Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаСчетаФактуры, "ДФ=dd.MM.yyyy"));
				СоставСтроки.Вставить("НомерИспрСчФ", 	ЗаписьЖурналаУчетаСчетовФактур.НомерИсправления);
				СоставСтроки.Вставить("ДатаИспрСчФ", 	Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаИсправления, "ДФ=dd.MM.yyyy"));
				СоставСтроки.Вставить("НомерКСчФ", 		ЗаписьЖурналаУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры);
				СоставСтроки.Вставить("ДатаКСчФ", 		Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаКорректировочногоСчетаФактуры, "ДФ=dd.MM.yyyy"));
				СоставСтроки.Вставить("НомерИспрКСчФ", 	ЗаписьЖурналаУчетаСчетовФактур.НомерИсправленияКорректировочногоСчетаФактуры);
				СоставСтроки.Вставить("ДатаИспрКСчФ", 	Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаИсправленияКорректировочногоСчетаФактуры, "ДФ=dd.MM.yyyy"));
				СоставСтроки.Вставить("НаимПок", 		ПараметрыСтроки.КонтрагентНаименование);
				
				Если НЕ ПустаяСтрока(ЗаписьЖурналаУчетаСчетовФактур.КонтрагентИНН) Тогда
					СтруктураРеквизитовКонтрагента = УчетНДС.ПолучитьСтруктуруРеквизитовКонтрагента(ЗаписьЖурналаУчетаСчетовФактур.КонтрагентИНН, ЗаписьЖурналаУчетаСчетовФактур.КонтрагентКПП);
					Если СтруктураРеквизитовКонтрагента.Свойство("ИННФЛ") Тогда
						СоставСтроки.Вставить("ИННФЛ", 	СтруктураРеквизитовКонтрагента.ИННФЛ);
					Иначе
						СоставСтроки.Вставить("ИННЮЛ", 	СтруктураРеквизитовКонтрагента.ИННЮЛ);
						СоставСтроки.Вставить("КПП",	СтруктураРеквизитовКонтрагента.КПП);
					КонецЕсли;
				КонецЕсли;
				
				Если СтруктураПараметров.НалоговыйПериод >= '20210701' Тогда
					МассивСвПосрДеят = Новый Массив();
					МассивСвПосрДеят.Добавить(СвГр10_23);
					СоставСтроки.Вставить("СвГр10_23", МассивСвПосрДеят);
				ИначеЕсли СтруктураПараметров.НалоговыйПериод >= '20171001' Тогда
					МассивСвПосрДеят = Новый Массив();
					МассивСвПосрДеят.Добавить(СвГр10_19);
					СоставСтроки.Вставить("СвГр10_19", МассивСвПосрДеят);
				КонецЕсли; 
				
				СтрокаРаздела = СтруктураПараметров.ЗаписиВыставленных.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаРаздела, СоставСтроки);
			
			ИначеЕсли СтруктураПараметров.НалоговыйПериод >= '20210701' Тогда
				
				СтрокаРаздела = СтрокиЧ1[0];
					
				МассивСвПосрДеят = СтрокаРаздела.СвГр10_23;
				СвГр10_23_БезРНПТ = Новый Структура("КодОКВ,СтТовУчНалВсего,СвПосредн,ВтчСумНДС,РазнСтоимКСчФ,РазнНДСКСчФ,СвРегНом");
				ЗаполнитьЗначенияСвойств(СвГр10_23_БезРНПТ, СвГр10_23,,"СвРегНом");
				МассивСвПосрДеят.Добавить(СвГр10_23_БезРНПТ);
			
			ИначеЕсли СтруктураПараметров.НалоговыйПериод >= '20171001' Тогда
				
				СтрокаРаздела = СтрокиЧ1[0];
				МассивСвПосрДеят = СтрокаРаздела.СвГр10_19;
				МассивСвПосрДеят.Добавить(СвГр10_19);
				
			КонецЕсли;
			
		Иначе
			
			СоставСтроки.Вставить("НомПП", НомерПП);
			СоставСтроки.Вставить("ДатаПолСчФ", Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаПередачиПолучения, "ДФ=dd.MM.yyyy"));
			
			СоставСтроки.Вставить("НомерСчФ", ЗаписьЖурналаУчетаСчетовФактур.НомерСчетаФактуры);
			СоставСтроки.Вставить("ДатаСчФ", Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаСчетаФактуры, "ДФ=dd.MM.yyyy"));
			
			СоставСтроки.Вставить("НомерИспрСчФ", ЗаписьЖурналаУчетаСчетовФактур.НомерИсправления);
			СоставСтроки.Вставить("ДатаИспрСчФ", Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаИсправления, "ДФ=dd.MM.yyyy"));
			
			СоставСтроки.Вставить("НомерКСчФ", ЗаписьЖурналаУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры);
			СоставСтроки.Вставить("ДатаКСчФ", Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаКорректировочногоСчетаФактуры, "ДФ=dd.MM.yyyy"));
			
			СоставСтроки.Вставить("НомерИспрКСчФ", ЗаписьЖурналаУчетаСчетовФактур.НомерИсправленияКорректировочногоСчетаФактуры);
			СоставСтроки.Вставить("ДатаИспрКСчФ", Формат(ЗаписьЖурналаУчетаСчетовФактур.ДатаИсправленияКорректировочногоСчетаФактуры, "ДФ=dd.MM.yyyy"));
			
			СоставСтроки.Вставить("НаимПрод", ПараметрыСтроки.КонтрагентНаименование);
			
			Если НЕ ПустаяСтрока(ЗаписьЖурналаУчетаСчетовФактур.КонтрагентИНН) Тогда
				СтруктураРеквизитовКонтрагента = УчетНДС.ПолучитьСтруктуруРеквизитовКонтрагента(ЗаписьЖурналаУчетаСчетовФактур.КонтрагентИНН, ЗаписьЖурналаУчетаСчетовФактур.КонтрагентКПП, ПараметрыСтроки.КонтрагентНаименование);
				Если СтруктураРеквизитовКонтрагента.Свойство("ИННФЛ") Тогда
					СоставСтроки.Вставить("ИННФЛ", 	СтруктураРеквизитовКонтрагента.ИННФЛ);
				Иначе
					СоставСтроки.Вставить("ИННЮЛ", 	СтруктураРеквизитовКонтрагента.ИННЮЛ);
					СоставСтроки.Вставить("КПП",	СтруктураРеквизитовКонтрагента.КПП);
				КонецЕсли;
				//!+ Формат выгрузки предполагает обязательное присутствие субкомиссионера
				Если НЕ СвПоКомиссионеруДляДокумента.Свойство("НаимСубКом")
					И НЕ СвПоКомиссионеруДляДокумента.Свойство("Фамилия")
					И СвПоКомиссионеруДляДокумента.КодВидСдел <> "" Тогда
					Если СоставСтроки.Свойство("ИННЮЛ") Тогда 
						СвПоКомиссионеруДляДокумента.Вставить("ИННЮЛСубКом", СоставСтроки.ИННЮЛ);
						СвПоКомиссионеруДляДокумента.Вставить("КППСубКом", СоставСтроки.КПП);
						СвПоКомиссионеруДляДокумента.Вставить("НаимСубКом", ПараметрыСтроки.КонтрагентНаименование);
					Иначе
						СвПоКомиссионеруДляДокумента.Вставить("ИННФЛСубКом ", СоставСтроки.ИННФЛ);
						СвПоКомиссионеруДляДокумента.Вставить("Фамилия", СтруктураРеквизитовКонтрагента.Фамилия);
						СвПоКомиссионеруДляДокумента.Вставить("Имя", СтруктураРеквизитовКонтрагента.Имя);
						СвПоКомиссионеруДляДокумента.Вставить("Отчество", СтруктураРеквизитовКонтрагента.Отчество);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			СоставСтроки.Вставить("КодОКВ", 	ЗаписьЖурналаУчетаСчетовФактур.КодВалюты);
			СоставСтроки.Вставить("СтТовУчНалВсего",
					ПреобразоватьСтрокуВФорматДекларации(СоставСтроки.СуммаДокумента, ТекстБезНДС, ТекстПустаяСтрока));
			МассивКодовОпераций = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ЗаписьЖурналаУчетаСчетовФактур.КодВидаОперации, ";");
			СоставСтроки.Вставить("КодВидОпер", МассивКодовОпераций);
			
			Если СвПоКомиссионеруДляДокумента.КодВидСдел <> "" Тогда 
				СоставСтроки.Вставить("СвПосредн",	СвПоКомиссионеруДляДокумента);
			КонецЕсли;
			СоставСтроки.Вставить("ВтчСумНДС",
			ПреобразоватьСтрокуВФорматДекларации(СоставСтроки.СуммаНДС, ТекстБезНДС, ТекстПустаяСтрока));
			
			РазнСтоимКСчФ = Новый Структура();
			Ум = ПреобразоватьСтрокуВФорматДекларации(СоставСтроки.СуммаДокументаРазницаУменьшение, ТекстБезНДС, ТекстПустаяСтрока);
			Если Ум <> 0 Тогда 
				РазнСтоимКСчФ.Вставить("Ум", Ум);
			КонецЕсли;
			Увел = ПреобразоватьСтрокуВФорматДекларации(СоставСтроки.СуммаДокументаРазницаУвеличение, ТекстБезНДС, ТекстПустаяСтрока);
			Если Увел <> 0 Тогда 
				РазнСтоимКСчФ.Вставить("Увел", Увел);
			КонецЕсли;
			
			Если РазнСтоимКСчФ.Свойство("Ум")
				ИЛИ РазнСтоимКСчФ.Свойство("Увел") Тогда 
				СоставСтроки.Вставить("РазнСтоимКСчФ", РазнСтоимКСчФ);
			КонецЕсли;
			
			РазнНДСКСчФ = Новый Структура();
			Ум = ПреобразоватьСтрокуВФорматДекларации(СоставСтроки.СуммаНДСРазницаУменьшение, ТекстБезНДС, ТекстПустаяСтрока);
			Если Ум <> 0 Тогда 
				РазнНДСКСчФ.Вставить("Ум", Ум);
			КонецЕсли;
			Увел = ПреобразоватьСтрокуВФорматДекларации(СоставСтроки.СуммаНДСРазницаУвеличение, ТекстБезНДС, ТекстПустаяСтрока);
			Если Увел <> 0 Тогда 
				РазнНДСКСчФ.Вставить("Увел", Увел);
			КонецЕсли;
			
			Если РазнНДСКСчФ.Свойство("Ум")
				ИЛИ РазнНДСКСчФ.Свойство("Увел") Тогда 
				СоставСтроки.Вставить("РазнНДСКСчФ", РазнНДСКСчФ);
			КонецЕсли;
			
			Если СтруктураПараметров.ПравилаЗаполнения = "2021Кв3"
			   И ЗначениеЗаполнено(МассивСвРегНом) Тогда
				СоставСтроки.Вставить("СвРегНом", МассивСвРегНом);
			КонецЕсли;
			
			СтрокаРаздела = СтруктураПараметров.ЗаписиПолученных.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаРаздела, СоставСтроки);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПреобразоватьСтрокуВФорматДекларации(ИсходныйПоказатель, ТекстБезНДС, ТекстПустаяСтрока)
	
	Если Строка(ИсходныйПоказатель) = ТекстБезНДС
		ИЛИ Строка(ИсходныйПоказатель) = ТекстПустаяСтрока Тогда
		Возврат 0;
	Иначе
		Возврат ИсходныйПоказатель;
	КонецЕсли;
	
КонецФункции

Функция ТекстЗапросаЖурналСчетовФактур1137()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЖурналУчетаСчетовФактурСрезПоследних.Организация,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура КАК СчетФактура,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СчетФактураВыданныйПокупателю,
	|	ПОДСТРОКА(ЖурналУчетаСчетовФактурСрезПоследних.Контрагент.НаименованиеПолное, 1, 250) КАК Комитент,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуре,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДС,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерСчетаФактуры КАК НомерСчетаФактурыКомитента,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаСчетаФактуры КАК ДатаСчетаФактурыКомитента,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Контрагент.ИНН КАК КомитентИНН,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Контрагент.КПП КАК КомитентКПП,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУменьшение,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУвеличение,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУменьшение,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУвеличение,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ПоСтавкеБезНДС,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерСчетаФактурыПродавца
	|ПОМЕСТИТЬ ВТ_СчетаФактурыОтКомитента
	|ИЗ
	|	РегистрСведений.ЖурналУчетаСчетовФактур.СрезПоследних(
	|			,
	|			&ПравилаПостановления735
	|				И ЧастьЖурнала = ЗНАЧЕНИЕ(Перечисление.ЧастиЖурналаУчетаСчетовФактур.ПолученныеСчетаФактуры)
	|				И СчетФактураВыданныйПокупателю <> ЗНАЧЕНИЕ(Документ.СчетФактураВыданный.ПустаяСсылка)
	|				И ДатаВыставленияПолучения < ДАТАВРЕМЯ(2015, 1, 1)
	|				И Организация = &Организация) КАК ЖурналУчетаСчетовФактурСрезПоследних
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЖурналУчетаСчетовФактурСрезПоследних.Организация,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СчетФактураВыданныйПокупателю,
	|	ПОДСТРОКА(ЖурналУчетаСчетовФактурСрезПоследних.Контрагент.НаименованиеПолное, 1, 250),
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреКомиссия,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСКомиссия,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Контрагент.ИНН,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Контрагент.КПП,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУменьшениеКомиссия,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУвеличениеКомиссия,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУменьшениеКомиссия,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУвеличениеКомиссия,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ПоСтавкеБезНДС,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерСчетаФактурыПродавца
	|ИЗ
	|	РегистрСведений.ЖурналУчетаСчетовФактур.СрезПоследних(
	|			,
	|			&ПравилаПостановления735
	|				И ЧастьЖурнала = ЗНАЧЕНИЕ(Перечисление.ЧастиЖурналаУчетаСчетовФактур.ПолученныеСчетаФактуры)
	|				И СчетФактураВыданныйПокупателю <> ЗНАЧЕНИЕ(Документ.СчетФактураВыданный.ПустаяСсылка)
	|				И ДатаВыставленияПолучения >= ДАТАВРЕМЯ(2015, 1, 1)
	|				И Организация = &Организация) КАК ЖурналУчетаСчетовФактурСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерСтроки,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Организация,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактурСрезПоследних.Продавец <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				И ЖурналУчетаСчетовФактурСрезПоследних.ЧастьЖурнала = ЗНАЧЕНИЕ(Перечисление.ЧастиЖурналаУчетаСчетовФактур.ПолученныеСчетаФактуры)
	|			ТОГДА ЖурналУчетаСчетовФактурСрезПоследних.Продавец
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ЧастьЖурнала,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаВыставленияПолучения,
	|	ЖурналУчетаСчетовФактурСрезПоследних.КодСпособаВыставленияПолучения,
	|	ЖурналУчетаСчетовФактурСрезПоследних.КодВидаОперации,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерИсправления,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаИсправления,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерИсправленияКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаИсправленияКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Валюта,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Валюта.НаименованиеПолное КАК НаименованиеВалюты,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Валюта.Код КАК КодВалюты,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуре КАК СуммаПоСчетуФактуре,
	|	ВЫБОР
	|		КОГДА &ПравилаПостановления735
	|			ТОГДА ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.СуммаНДС, ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДС)
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДС
	|	КОНЕЦ КАК СуммаНДС,
	|	ВЫБОР
	|		КОГДА &ПравилаПостановления735
	|			ТОГДА ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.СуммаПоСчетуФактуреРазницаУменьшение, ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУменьшение)
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУменьшение
	|	КОНЕЦ КАК СуммаПоСчетуФактуреРазницаУменьшение,
	|	ВЫБОР
	|		КОГДА &ПравилаПостановления735
	|			ТОГДА ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.СуммаПоСчетуФактуреРазницаУвеличение, ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУвеличение)
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУвеличение
	|	КОНЕЦ КАК СуммаПоСчетуФактуреРазницаУвеличение,
	|	ВЫБОР
	|		КОГДА &ПравилаПостановления735
	|			ТОГДА ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.СуммаНДСРазницаУменьшение, ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУменьшение)
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУменьшение
	|	КОНЕЦ КАК СуммаНДСРазницаУменьшение,
	|	ВЫБОР
	|		КОГДА &ПравилаПостановления735
	|			ТОГДА ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.СуммаНДСРазницаУвеличение, ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУвеличение)
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУвеличение
	|	КОНЕЦ КАК СуммаНДСРазницаУвеличение,
	|	ВЫБОР
	|		КОГДА &ПравилаПостановления735
	|			ТОГДА ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.ПоСтавкеБезНДС, ЖурналУчетаСчетовФактурСрезПоследних.ПоСтавкеБезНДС)
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.ПоСтавкеБезНДС
	|	КОНЕЦ КАК ПоСтавкеБезНДС,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СчетФактураНеВыставляется,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактурСрезПоследних.Продавец <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				И ЖурналУчетаСчетовФактурСрезПоследних.ЧастьЖурнала = ЗНАЧЕНИЕ(Перечисление.ЧастиЖурналаУчетаСчетовФактур.ПолученныеСчетаФактуры)
	|			ТОГДА """"
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.КППКонтрагента
	|	КОНЕЦ КАК КППКонтрагента,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СчетФактураВыданныйПокупателю,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Субкомиссионер КАК СубкомиссионерСсылка,
	|	ВЫБОР
	|		КОГДА ПОДСТРОКА(ЖурналУчетаСчетовФактурСрезПоследних.Субкомиссионер.НаименованиеПолное, 1, 250) ПОДОБНО """"
	|			ТОГДА ЖурналУчетаСчетовФактурСрезПоследних.Субкомиссионер.Наименование
	|		ИНАЧЕ ПОДСТРОКА(ЖурналУчетаСчетовФактурСрезПоследних.Субкомиссионер.НаименованиеПолное, 1, 250)
	|	КОНЕЦ КАК Субкомиссионер,
	|	ЖурналУчетаСчетовФактурСрезПоследних.КодВидаСделки,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Субкомиссионер.ИНН,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Субкомиссионер.КПП,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактурСрезПоследних.НомерСчетаФактурыПродавца = """"
	|			ТОГДА ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.Комитент, """")
	|		ИНАЧЕ ПОДСТРОКА(ЖурналУчетаСчетовФактурСрезПоследних.Продавец.НаименованиеПолное, 1, 250)
	|	КОНЕЦ КАК Комитент,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактурСрезПоследних.НомерСчетаФактурыПродавца = """"
	|			ТОГДА ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.КомитентИНН, """")
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.Продавец.ИНН
	|	КОНЕЦ КАК КомитентИНН,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактурСрезПоследних.НомерСчетаФактурыПродавца = """"
	|			ТОГДА ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.КомитентКПП, """")
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.Продавец.КПП
	|	КОНЕЦ КАК КомитентКПП,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактурСрезПоследних.НомерСчетаФактурыПродавца = """"
	|			ТОГДА ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.НомерСчетаФактурыКомитента, """")
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.НомерСчетаФактурыПродавца
	|	КОНЕЦ КАК НомерСчетаФактурыКомитента,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактурСрезПоследних.НомерСчетаФактурыПродавца = """"
	|			ТОГДА ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.ДатаСчетаФактурыКомитента, """")
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.ДатаСчетаФактуры
	|	КОНЕЦ КАК ДатаСчетаФактурыКомитента,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДС КАК СуммаНДСИтоговая,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУменьшение КАК СуммаПоСчетуФактуреРазницаУменьшениеИтоговая,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУвеличение КАК СуммаПоСчетуФактуреРазницаУвеличениеИтоговая,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУменьшение КАК СуммаНДСРазницаУменьшениеИтоговая,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУвеличение КАК СуммаНДСРазницаУвеличениеИтоговая,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура ССЫЛКА Документ.СчетФактураВыданный
	|			ТОГДА ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура.СводныйКорректировочный
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СводныйКорректировочный,
	|	ЛОЖЬ КАК СводныйКомиссионный,
	|	0 КАК СуммаДокументаКомиссия,
	|	0 КАК СуммаДокументаРазницаУменьшениеКомиссия,
	|	0 КАК СуммаДокументаРазницаУвеличениеКомиссия
	|ПОМЕСТИТЬ ВТ_ЖурналУчетаСчетовФактурСрезПоследних
	|ИЗ
	|	РегистрСведений.ЖурналУчетаСчетовФактур.СрезПоследних(
	|			,
	|			Организация В (&Организация)
	|				И ДатаВыставленияПолучения < ДАТАВРЕМЯ(2015, 1, 1)
	|				И (ДатаВыставленияПолучения МЕЖДУ &НачПериода И &КонПериода)) КАК ЖурналУчетаСчетовФактурСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СчетаФактурыОтКомитента КАК ВТ_СчетаФактурыОтКомитента
	|		ПО ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура = ВТ_СчетаФактурыОтКомитента.СчетФактураВыданныйПокупателю
	|ГДЕ
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаВыставленияПолучения <> ДАТАВРЕМЯ(1, 1, 1)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерСтроки,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Организация,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактурСрезПоследних.Продавец <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				И ЖурналУчетаСчетовФактурСрезПоследних.ЧастьЖурнала = ЗНАЧЕНИЕ(Перечисление.ЧастиЖурналаУчетаСчетовФактур.ПолученныеСчетаФактуры)
	|			ТОГДА ЖурналУчетаСчетовФактурСрезПоследних.Продавец
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.Контрагент
	|	КОНЕЦ,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ЧастьЖурнала,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактурСрезПоследних.Период >= ДАТАВРЕМЯ(2017, 10, 1)
	|			ТОГДА ЖурналУчетаСчетовФактурСрезПоследних.Период
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.ДатаВыставленияПолучения
	|	КОНЕЦ,
	|	ЖурналУчетаСчетовФактурСрезПоследних.КодСпособаВыставленияПолучения,
	|	ЖурналУчетаСчетовФактурСрезПоследних.КодВидаОперацииКомиссия,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерИсправления,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаИсправления,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерИсправленияКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаИсправленияКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Валюта,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Валюта.НаименованиеПолное,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Валюта.Код,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактурСрезПоследних.ЧастьЖурнала = ЗНАЧЕНИЕ(Перечисление.ЧастиЖурналаУчетаСчетовФактур.ВыставленныеСчетаФактуры)
	|			ТОГДА ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуре
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреКомиссия
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ПравилаПостановления735
	|			ТОГДА ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.СуммаНДС, ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСКомиссия)
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСКомиссия
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ПравилаПостановления735
	|			ТОГДА ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.СуммаПоСчетуФактуреРазницаУменьшение, ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУменьшениеКомиссия)
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУменьшениеКомиссия
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ПравилаПостановления735
	|			ТОГДА ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.СуммаПоСчетуФактуреРазницаУвеличение, ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУвеличениеКомиссия)
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУвеличениеКомиссия
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ПравилаПостановления735
	|			ТОГДА ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.СуммаНДСРазницаУменьшение, ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУменьшениеКомиссия)
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУменьшениеКомиссия
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ПравилаПостановления735
	|			ТОГДА ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.СуммаНДСРазницаУвеличение, ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУвеличениеКомиссия)
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУвеличениеКомиссия
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ПравилаПостановления735
	|			ТОГДА ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.ПоСтавкеБезНДС, ЖурналУчетаСчетовФактурСрезПоследних.ПоСтавкеБезНДС)
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.ПоСтавкеБезНДС
	|	КОНЕЦ,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СчетФактураНеВыставляется,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактурСрезПоследних.Продавец <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				И ЖурналУчетаСчетовФактурСрезПоследних.ЧастьЖурнала = ЗНАЧЕНИЕ(Перечисление.ЧастиЖурналаУчетаСчетовФактур.ПолученныеСчетаФактуры)
	|			ТОГДА """"
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.КППКонтрагента
	|	КОНЕЦ,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СчетФактураВыданныйПокупателю,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Субкомиссионер,
	|	ВЫБОР
	|		КОГДА ПОДСТРОКА(ЖурналУчетаСчетовФактурСрезПоследних.Субкомиссионер.НаименованиеПолное, 1, 250) ПОДОБНО """"
	|			ТОГДА ЖурналУчетаСчетовФактурСрезПоследних.Субкомиссионер.Наименование
	|		ИНАЧЕ ПОДСТРОКА(ЖурналУчетаСчетовФактурСрезПоследних.Субкомиссионер.НаименованиеПолное, 1, 250)
	|	КОНЕЦ,
	|	ЖурналУчетаСчетовФактурСрезПоследних.КодВидаСделки,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Субкомиссионер.ИНН,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Субкомиссионер.КПП,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактурСрезПоследних.НомерСчетаФактурыПродавца = """"
	|			ТОГДА ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.Комитент, """")
	|		ИНАЧЕ ПОДСТРОКА(ЖурналУчетаСчетовФактурСрезПоследних.Продавец.НаименованиеПолное, 1, 250)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактурСрезПоследних.НомерСчетаФактурыПродавца = """"
	|			ТОГДА ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.КомитентИНН, """")
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.Продавец.ИНН
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактурСрезПоследних.НомерСчетаФактурыПродавца = """"
	|			ТОГДА ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.КомитентКПП, """")
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.Продавец.КПП
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактурСрезПоследних.НомерСчетаФактурыПродавца = """"
	|			ТОГДА ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.НомерСчетаФактурыКомитента, """")
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.НомерСчетаФактурыПродавца
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактурСрезПоследних.НомерСчетаФактурыПродавца = """"
	|			ТОГДА ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.ДатаСчетаФактурыКомитента, """")
	|		КОГДА ЖурналУчетаСчетовФактурСрезПоследних.ДатаСчетаФактурыПродавца <> ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ЖурналУчетаСчетовФактурСрезПоследних.ДатаСчетаФактурыПродавца
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.ДатаСчетаФактуры
	|	КОНЕЦ,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСКомиссия,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУменьшениеКомиссия,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУвеличениеКомиссия,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУменьшениеКомиссия,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУвеличениеКомиссия,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура ССЫЛКА Документ.СчетФактураВыданный
	|			ТОГДА ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура.СводныйКорректировочный
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура ССЫЛКА Документ.СчетФактураВыданный
	|			ТОГДА ВЫРАЗИТЬ(ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура КАК Документ.СчетФактураВыданный).СводныйКомиссионный
	|		КОГДА ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура ССЫЛКА Документ.СчетФактураПолученный
	|			ТОГДА ВЫРАЗИТЬ(ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура КАК Документ.СчетФактураПолученный).СводныйКомиссионный
	//|		КОГДА ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура ССЫЛКА Документ.ЗаписьЖурналаУчетаСчетовФактур
	//|			ТОГДА ВЫРАЗИТЬ(ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура КАК Документ.ЗаписьЖурналаУчетаСчетовФактур).СводныйКомиссионный
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	0,
	|	0,
	|	0
	|ИЗ
	|	РегистрСведений.ЖурналУчетаСчетовФактур.СрезПоследних(
	|			,
	|			Организация В (&Организация)
	|				И ДатаВыставленияПолучения >= ДАТАВРЕМЯ(2015, 1, 1)
	|				И (ДатаВыставленияПолучения МЕЖДУ &НачПериода И &КонПериода)) КАК ЖурналУчетаСчетовФактурСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СчетаФактурыОтКомитента КАК ВТ_СчетаФактурыОтКомитента
	|		ПО ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура = ВТ_СчетаФактурыОтКомитента.СчетФактураВыданныйПокупателю
	|ГДЕ
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаВыставленияПолучения <> ДАТАВРЕМЯ(1, 1, 1)
	|	И (ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреКомиссия <> 0
	|			ИЛИ ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСКомиссия <> 0
	|			ИЛИ ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУменьшениеКомиссия <> 0
	|			ИЛИ ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУвеличениеКомиссия <> 0
	|			ИЛИ ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУменьшениеКомиссия <> 0
	|			ИЛИ ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУвеличениеКомиссия <> 0)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Контрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЖурналУчетаСчетовФактур.Организация,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактур.Контрагент.ОбособленноеПодразделение
	|			ТОГДА ЖурналУчетаСчетовФактур.Контрагент.ГоловнойКонтрагент
	|		ИНАЧЕ ЖурналУчетаСчетовФактур.Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактур.Контрагент.ОбособленноеПодразделение
	|			ТОГДА ВЫБОР
	|					КОГДА ПОДСТРОКА(ЖурналУчетаСчетовФактур.Контрагент.ГоловнойКонтрагент.НаименованиеПолное, 1, 250) ПОДОБНО """"
	|						ТОГДА ЖурналУчетаСчетовФактур.Контрагент.ГоловнойКонтрагент.Наименование
	|					ИНАЧЕ ПОДСТРОКА(ЖурналУчетаСчетовФактур.Контрагент.ГоловнойКонтрагент.НаименованиеПолное, 1, 250)
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ПОДСТРОКА(ЖурналУчетаСчетовФактур.Контрагент.НаименованиеПолное, 1, 250) ПОДОБНО """"
	|					ТОГДА ЖурналУчетаСчетовФактур.Контрагент.Наименование
	|				ИНАЧЕ ПОДСТРОКА(ЖурналУчетаСчетовФактур.Контрагент.НаименованиеПолное, 1, 250)
	|			КОНЕЦ
	|	КОНЕЦ КАК КонтрагентНаименование,
	|	ЖурналУчетаСчетовФактур.Контрагент.ИНН КАК КонтрагентИНН,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактур.КППКонтрагента ПОДОБНО """"
	|			ТОГДА ЖурналУчетаСчетовФактур.Контрагент.КПП
	|		ИНАЧЕ ЖурналУчетаСчетовФактур.КППКонтрагента
	|	КОНЕЦ КАК КонтрагентКПП,
	|	ЖурналУчетаСчетовФактур.СчетФактура,
	|	ЖурналУчетаСчетовФактур.ЧастьЖурнала,
	|	ЖурналУчетаСчетовФактур.ДатаВыставленияПолучения,
	|	ЖурналУчетаСчетовФактур.КодСпособаВыставленияПолучения,
	|	ЖурналУчетаСчетовФактур.КодВидаОперации,
	|	ЖурналУчетаСчетовФактур.НомерСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.ДатаСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.ДатаКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.НомерИсправления,
	|	ЖурналУчетаСчетовФактур.ДатаИсправления,
	|	ЖурналУчетаСчетовФактур.НомерИсправленияКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.ДатаИсправленияКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.Валюта,
	|	ЖурналУчетаСчетовФактур.НаименованиеВалюты,
	|	ЖурналУчетаСчетовФактур.КодВалюты,
	|	ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУменьшение,
	|	ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУвеличение,
	|	ЖурналУчетаСчетовФактур.СуммаНДСРазницаУменьшение,
	|	ЖурналУчетаСчетовФактур.СуммаНДСРазницаУвеличение,
	|	ЖурналУчетаСчетовФактур.ПоСтавкеБезНДС,
	|	ЖурналУчетаСчетовФактур.СчетФактураНеВыставляется,
	|	ЖурналУчетаСчетовФактур.КодВидаСделки,
	|	ЖурналУчетаСчетовФактур.Комитент,
	|	ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуре,
	|	ЖурналУчетаСчетовФактур.СуммаНДС,
	|	ЖурналУчетаСчетовФактур.КомитентИНН,
	|	ЖурналУчетаСчетовФактур.КомитентКПП,
	|	ЖурналУчетаСчетовФактур.НомерСчетаФактурыКомитента,
	|	ЖурналУчетаСчетовФактур.ДатаСчетаФактурыКомитента,
	|	ЖурналУчетаСчетовФактур.СуммаНДСИтоговая,
	|	ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУменьшениеИтоговая,
	|	ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУвеличениеИтоговая,
	|	ЖурналУчетаСчетовФактур.СуммаНДСРазницаУменьшениеИтоговая,
	|	ЖурналУчетаСчетовФактур.СуммаНДСРазницаУвеличениеИтоговая,
	|	ЖурналУчетаСчетовФактур.СводныйКорректировочный,
	|	ЖурналУчетаСчетовФактур.СводныйКомиссионный,
	|	ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(ЖурналУчетаСчетовФактур.СчетФактура КАК Документ.СчетФактураВыданный).ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный)
	|			ТОГДА ИСТИНА
	|		КОГДА ВЫРАЗИТЬ(ЖурналУчетаСчетовФактур.СчетФактура КАК Документ.СчетФактураПолученный).ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыПолученного.Корректировочный)
	|			ТОГДА ИСТИНА
	//|		КОГДА ВЫРАЗИТЬ(ЖурналУчетаСчетовФактур.СчетФактура КАК Документ.ЗаписьЖурналаУчетаСчетовФактур).Корректировка = ИСТИНА
	//|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК КорректировочныйСчетФактура,
	|	ВЫРАЗИТЬ(ЖурналУчетаСчетовФактур.СчетФактура КАК Документ.СчетФактураВыданный).Номер КАК СчетФактураВыданныйНомер
	|ПОМЕСТИТЬ ВТ_ЖурналУчетаСчетовФактурПоКонтрагентам
	|ИЗ
	|	ВТ_ЖурналУчетаСчетовФактурСрезПоследних КАК ЖурналУчетаСчетовФактур
	|ГДЕ
	|	ЖурналУчетаСчетовФактур.Контрагент В(&МассивКонтрагентов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЖурналУчетаСчетовФактур.СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЖурналУчетаСчетовФактур.Организация КАК Организация,
	|	ЖурналУчетаСчетовФактур.СчетФактура КАК СчетФактураДокумент,
	|	ЖурналУчетаСчетовФактур.ДатаВыставленияПолучения КАК ДатаПередачиПолучения,
	|	ЖурналУчетаСчетовФактур.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.НомерСчетаФактуры КАК НомерСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.Контрагент КАК Контрагент,
	|	ЖурналУчетаСчетовФактур.КонтрагентИНН КАК КонтрагентИНН,
	|	ЖурналУчетаСчетовФактур.КонтрагентКПП КАК КонтрагентКПП,
	|	ЖурналУчетаСчетовФактур.КонтрагентНаименование КАК КонтрагентНаименование,
	|	ЖурналУчетаСчетовФактур.НаименованиеВалюты КАК НаименованиеВалюты,
	|	ЖурналУчетаСчетовФактур.КодВалюты КАК КодВалюты,
	|	ЖурналУчетаСчетовФактур.ЧастьЖурнала КАК ЧастьЖурнала,
	|	ЖурналУчетаСчетовФактур.КодВидаОперации КАК КодВидаОперации,
	|	ЖурналУчетаСчетовФактур.ПоСтавкеБезНДС КАК СчетФактураБезНДС,
	|	ЖурналУчетаСчетовФактур.КодСпособаВыставленияПолучения КАК КодСпособаВыставления,
	|	ЖурналУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры КАК НомерКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.ДатаКорректировочногоСчетаФактуры КАК ДатаКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.НомерИсправления КАК НомерИсправления,
	|	ЖурналУчетаСчетовФактур.ДатаИсправления КАК ДатаИсправления,
	|	ЖурналУчетаСчетовФактур.НомерИсправленияКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.ДатаИсправленияКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.СчетФактураНеВыставляется КАК СчетФактураНеВыставляется,
	|	ЖурналУчетаСчетовФактур.КорректировочныйСчетФактура КАК КорректировочныйСчетФактура,
	|	ЖурналУчетаСчетовФактур.СчетФактураВыданныйНомер КАК СчетФактураВыданныйНомер,
	|	ЖурналУчетаСчетовФактур.КодВидаСделки,
	|	ЖурналУчетаСчетовФактур.Комитент,
	|	ЖурналУчетаСчетовФактур.КомитентИНН,
	|	ЖурналУчетаСчетовФактур.КомитентКПП,
	|	ЖурналУчетаСчетовФактур.НомерСчетаФактурыКомитента,
	|	ЖурналУчетаСчетовФактур.СводныйКорректировочный,
	|	ЖурналУчетаСчетовФактур.СводныйКомиссионный,
	|	ЖурналУчетаСчетовФактур.ДатаСчетаФактурыКомитента,
	|	СУММА(ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУменьшение) КАК СуммаДокументаРазницаУменьшение,
	|	СУММА(ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУвеличение) КАК СуммаДокументаРазницаУвеличение,
	|	СУММА(ЖурналУчетаСчетовФактур.СуммаНДСРазницаУменьшение) КАК СуммаНДСРазницаУменьшение,
	|	СУММА(ЖурналУчетаСчетовФактур.СуммаНДСРазницаУвеличение) КАК СуммаНДСРазницаУвеличение,
	|	СУММА(ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуре) КАК СуммаДокумента,
	|	СУММА(ЖурналУчетаСчетовФактур.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ЖурналУчетаСчетовФактур.СуммаНДСИтоговая) КАК СуммаНДСИтоговая,
	|	СУММА(ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУменьшениеИтоговая) КАК СуммаПоСчетуФактуреРазницаУменьшениеИтоговая,
	|	СУММА(ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУвеличениеИтоговая) КАК СуммаПоСчетуФактуреРазницаУвеличениеИтоговая,
	|	СУММА(ЖурналУчетаСчетовФактур.СуммаНДСРазницаУменьшениеИтоговая) КАК СуммаНДСРазницаУменьшениеИтоговая,
	|	СУММА(ЖурналУчетаСчетовФактур.СуммаНДСРазницаУвеличениеИтоговая) КАК СуммаНДСРазницаУвеличениеИтоговая,
	|	0 КАК СуммаДокументаКомиссия,
	|	0 КАК СуммаДокументаРазницаУменьшениеКомиссия,
	|	0 КАК СуммаДокументаРазницаУвеличениеКомиссия
	|ПОМЕСТИТЬ ЗаписиРегистраЖурналУчетаСчетовФактур
	|ИЗ
	|	ВТ_ЖурналУчетаСчетовФактурПоКонтрагентам КАК ЖурналУчетаСчетовФактур
	|
	|СГРУППИРОВАТЬ ПО
	|	ЖурналУчетаСчетовФактур.Организация,
	|	ЖурналУчетаСчетовФактур.СчетФактура,
	|	ЖурналУчетаСчетовФактур.ДатаВыставленияПолучения,
	|	ЖурналУчетаСчетовФактур.ДатаСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.НомерСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.Контрагент,
	|	ЖурналУчетаСчетовФактур.КонтрагентИНН,
	|	ЖурналУчетаСчетовФактур.КонтрагентКПП,
	|	ЖурналУчетаСчетовФактур.КонтрагентНаименование,
	|	ЖурналУчетаСчетовФактур.НаименованиеВалюты,
	|	ЖурналУчетаСчетовФактур.КодВалюты,
	|	ЖурналУчетаСчетовФактур.ЧастьЖурнала,
	|	ЖурналУчетаСчетовФактур.КодВидаОперации,
	|	ЖурналУчетаСчетовФактур.ПоСтавкеБезНДС,
	|	ЖурналУчетаСчетовФактур.КодСпособаВыставленияПолучения,
	|	ЖурналУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.ДатаКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.НомерИсправления,
	|	ЖурналУчетаСчетовФактур.ДатаИсправления,
	|	ЖурналУчетаСчетовФактур.НомерИсправленияКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.ДатаИсправленияКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.СчетФактураНеВыставляется,
	|	ЖурналУчетаСчетовФактур.КорректировочныйСчетФактура,
	|	ЖурналУчетаСчетовФактур.СчетФактураВыданныйНомер,
	|	ЖурналУчетаСчетовФактур.КодВидаСделки,
	|	ЖурналУчетаСчетовФактур.Комитент,
	|	ЖурналУчетаСчетовФактур.КомитентИНН,
	|	ЖурналУчетаСчетовФактур.КомитентКПП,
	|	ЖурналУчетаСчетовФактур.НомерСчетаФактурыКомитента,
	|	ЖурналУчетаСчетовФактур.СводныйКорректировочный,
	|	ЖурналУчетаСчетовФактур.СводныйКомиссионный,
	|	ЖурналУчетаСчетовФактур.ДатаСчетаФактурыКомитента
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЧастьЖурнала
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура КАК СчетФактура,
	|	ВТ_ЖурналУчетаСчетовФактурСрезПоследних.Субкомиссионер КАК Субкомиссионер,
	|	ВТ_ЖурналУчетаСчетовФактурСрезПоследних.СубкомиссионерИНН КАК СубкомиссионерИНН,
	|	ВТ_ЖурналУчетаСчетовФактурСрезПоследних.СубкомиссионерКПП КАК СубкомиссионерКПП,
	|	МИНИМУМ(ВТ_ЖурналУчетаСчетовФактурСрезПоследних.НомерСтроки) КАК НомерСтроки
	|ИЗ
	|	ВТ_ЖурналУчетаСчетовФактурСрезПоследних КАК ВТ_ЖурналУчетаСчетовФактурСрезПоследних
	|ГДЕ
	|	ВТ_ЖурналУчетаСчетовФактурСрезПоследних.СубкомиссионерСсылка <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура,
	|	ВТ_ЖурналУчетаСчетовФактурСрезПоследних.Субкомиссионер,
	|	ВТ_ЖурналУчетаСчетовФактурСрезПоследних.СубкомиссионерИНН,
	|	ВТ_ЖурналУчетаСчетовФактурСрезПоследних.СубкомиссионерКПП";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаЖурналСчетовФактур981()
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЖурналУчетаСчетовФактурСрезПоследних.Организация,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура КАК СчетФактура,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СчетФактураВыданныйПокупателю,
	|	ПОДСТРОКА(ЖурналУчетаСчетовФактурСрезПоследних.Контрагент.НаименованиеПолное, 1, 250) КАК Комитент,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуре КАК СуммаПоСчетуФактуре,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреКомиссия КАК СуммаПоСчетуФактуреКомиссия,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСКомиссия КАК СуммаНДС,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерСчетаФактуры КАК НомерСчетаФактурыКомитента,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаСчетаФактуры КАК ДатаСчетаФактурыКомитента,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Контрагент.ИНН КАК КомитентИНН,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Контрагент.КПП КАК КомитентКПП,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУменьшениеКомиссия КАК СуммаПоСчетуФактуреРазницаУменьшение,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУвеличениеКомиссия КАК СуммаПоСчетуФактуреРазницаУвеличение,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУменьшениеКомиссия КАК СуммаНДСРазницаУменьшение,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУвеличениеКомиссия КАК СуммаНДСРазницаУвеличение,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ПоСтавкеБезНДС,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерСчетаФактурыПродавца,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаКорректировочногоСчетаФактуры
	|ПОМЕСТИТЬ ВТ_СчетаФактурыОтКомитента
	|ИЗ
	|	РегистрСведений.ЖурналУчетаСчетовФактур.СрезПоследних(
	|			,
	|			ЧастьЖурнала = ЗНАЧЕНИЕ(Перечисление.ЧастиЖурналаУчетаСчетовФактур.ПолученныеСчетаФактуры)
	|				И СчетФактураВыданныйПокупателю <> ЗНАЧЕНИЕ(Документ.СчетФактураВыданный.ПустаяСсылка)
	|				И Организация = &Организация) КАК ЖурналУчетаСчетовФактурСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЖурналУчетаСчетовФактурСрезПоследних.Период,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Регистратор КАК Регистратор,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерСтроки,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Активность,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Организация,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Контрагент,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура КАК СчетФактура,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ЧастьЖурнала,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ИндексСтроки,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СчетФактураВыданныйПокупателю,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаВыставленияПолучения,
	|	ЖурналУчетаСчетовФактурСрезПоследних.КодСпособаВыставленияПолучения,
	|	ЖурналУчетаСчетовФактурСрезПоследних.КодВидаОперации,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерИсправления,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаИсправления,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерИсправленияКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаИсправленияКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Валюта,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуре,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДС,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУменьшение,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУвеличение,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУменьшение,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУвеличение,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ПоСтавкеБезНДС,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СчетФактураНеВыставляется,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Продавец,
	|	ЖурналУчетаСчетовФактурСрезПоследних.КППКонтрагента,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Посредник,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Субкомиссионер,
	|	ЖурналУчетаСчетовФактурСрезПоследних.КодВидаСделки,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерСчетаФактурыПродавца,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреКомиссия,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСКомиссия,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУменьшениеКомиссия,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУвеличениеКомиссия,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУменьшениеКомиссия,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУвеличениеКомиссия,
	|	ЖурналУчетаСчетовФактурСрезПоследних.КодВидаОперацииКомиссия
	|ПОМЕСТИТЬ ВТ_ЖурналСчетовФактурПредварительная
	|ИЗ
	|	РегистрСведений.ЖурналУчетаСчетовФактур.СрезПоследних(
	|			,
	|			Организация = &Организация
	|				И &ОтборПоКонтрагенту
	|				И (ДатаВыставленияПолучения МЕЖДУ &НачПериода И &КонПериода)) КАК ЖурналУчетаСчетовФактурСрезПоследних
	|ГДЕ
	|	(ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреКомиссия <> 0
	|			ИЛИ ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСКомиссия <> 0
	|			ИЛИ ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУменьшениеКомиссия <> 0
	|			ИЛИ ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУвеличениеКомиссия <> 0
	|			ИЛИ ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУменьшениеКомиссия <> 0
	|			ИЛИ ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУвеличениеКомиссия <> 0)
	|	И ЖурналУчетаСчетовФактурСрезПоследних.ДатаВыставленияПолучения <> ДАТАВРЕМЯ(1, 1, 1)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЖурналУчетаСчетовФактур.СчетФактура КАК СчетФактура,
	|	СУММА(ЖурналУчетаСчетовФактур.СуммаНДСКомиссия) КАК СуммаНДСКомиссия
	|ПОМЕСТИТЬ ВТ_НДСПоСчетуФактуреНАПеревыставленному
	|ИЗ
	|	РегистрСведений.ЖурналУчетаСчетовФактур КАК ЖурналУчетаСчетовФактур
	|ГДЕ
	|	ЖурналУчетаСчетовФактур.Организация В(&Организация)
	|	И ЖурналУчетаСчетовФактур.СчетФактура В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				ВТ_ЖурналСчетовФактурПредварительная.СчетФактура
	|			ИЗ
	|				ВТ_ЖурналСчетовФактурПредварительная КАК ВТ_ЖурналСчетовФактурПредварительная)
	|	И ЖурналУчетаСчетовФактур.СчетФактура ССЫЛКА Документ.СчетФактураВыданный
	|	И ЖурналУчетаСчетовФактур.ЧастьЖурнала = ЗНАЧЕНИЕ(Перечисление.ЧастиЖурналаУчетаСчетовФактур.ПолученныеСчетаФактуры)
	|	И ЖурналУчетаСчетовФактур.СуммаНДСКомиссия <> 0
	|	И ЖурналУчетаСчетовФактур.ДатаВыставленияПолучения МЕЖДУ &НачПериода И &КонПериода
	|
	|СГРУППИРОВАТЬ ПО
	|	ЖурналУчетаСчетовФактур.СчетФактура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерСтроки,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Организация,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактурСрезПоследних.Продавец <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				И ЖурналУчетаСчетовФактурСрезПоследних.ЧастьЖурнала = ЗНАЧЕНИЕ(Перечисление.ЧастиЖурналаУчетаСчетовФактур.ПолученныеСчетаФактуры)
	|			ТОГДА ЖурналУчетаСчетовФактурСрезПоследних.Продавец
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ЧастьЖурнала,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаВыставленияПолучения,
	|	ЖурналУчетаСчетовФактурСрезПоследних.КодСпособаВыставленияПолучения,
	|	ЖурналУчетаСчетовФактурСрезПоследних.КодВидаОперацииКомиссия КАК КодВидаОперации,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерИсправления,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаИсправления,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерИсправленияКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаИсправленияКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Валюта,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Валюта.НаименованиеПолное КАК НаименованиеВалюты,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Валюта.Код КАК КодВалюты,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуре КАК СуммаПоСчетуФактуре,
	|	ВЫБОР
	|		КОГДА &ПравилаПостановления735
	|			ТОГДА ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.СуммаНДС, ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСКомиссия)
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСКомиссия
	|	КОНЕЦ КАК СуммаНДС,
	|	ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.СуммаПоСчетуФактуреРазницаУменьшение, ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУменьшениеКомиссия) КАК СуммаПоСчетуФактуреРазницаУменьшение,
	|	ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.СуммаПоСчетуФактуреРазницаУвеличение, ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУвеличениеКомиссия) КАК СуммаПоСчетуФактуреРазницаУвеличение,
	|	ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.СуммаНДСРазницаУменьшение, ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУменьшениеКомиссия) КАК СуммаНДСРазницаУменьшение,
	|	ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.СуммаНДСРазницаУвеличение, ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУвеличениеКомиссия) КАК СуммаНДСРазницаУвеличение,
	|	ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.ПоСтавкеБезНДС, ЖурналУчетаСчетовФактурСрезПоследних.ПоСтавкеБезНДС) КАК ПоСтавкеБезНДС,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СчетФактураНеВыставляется,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактурСрезПоследних.Продавец <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				И ЖурналУчетаСчетовФактурСрезПоследних.ЧастьЖурнала = ЗНАЧЕНИЕ(Перечисление.ЧастиЖурналаУчетаСчетовФактур.ПолученныеСчетаФактуры)
	|			ТОГДА """"
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.КППКонтрагента
	|	КОНЕЦ КАК КППКонтрагента,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СчетФактураВыданныйПокупателю,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Субкомиссионер КАК СубкомиссионерСсылка,
	|	ВЫБОР
	|		КОГДА ПОДСТРОКА(ЖурналУчетаСчетовФактурСрезПоследних.Субкомиссионер.НаименованиеПолное, 1, 250) ПОДОБНО """"
	|			ТОГДА ЖурналУчетаСчетовФактурСрезПоследних.Субкомиссионер.Наименование
	|		ИНАЧЕ ПОДСТРОКА(ЖурналУчетаСчетовФактурСрезПоследних.Субкомиссионер.НаименованиеПолное, 1, 250)
	|	КОНЕЦ КАК Субкомиссионер,
	|	ЖурналУчетаСчетовФактурСрезПоследних.КодВидаСделки,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Субкомиссионер.ИНН,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Субкомиссионер.КПП,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактурСрезПоследних.НомерСчетаФактурыПродавца = """"
	|			ТОГДА ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.Комитент, """")
	|		ИНАЧЕ ПОДСТРОКА(ЖурналУчетаСчетовФактурСрезПоследних.Продавец.НаименованиеПолное, 1, 250)
	|	КОНЕЦ КАК Комитент,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактурСрезПоследних.НомерСчетаФактурыПродавца = """"
	|			ТОГДА ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.КомитентИНН, """")
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.Продавец.ИНН
	|	КОНЕЦ КАК КомитентИНН,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактурСрезПоследних.НомерСчетаФактурыПродавца = """"
	|			ТОГДА ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.КомитентКПП, """")
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.Продавец.КПП
	|	КОНЕЦ КАК КомитентКПП,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактурСрезПоследних.НомерСчетаФактурыПродавца <> """"
	|			ТОГДА ЖурналУчетаСчетовФактурСрезПоследних.НомерСчетаФактурыПродавца
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЖурналУчетаСчетовФактурСрезПоследних.НомерКорректировочногоСчетаФактуры <> """"
	|					ТОГДА ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.НомерКорректировочногоСчетаФактуры, """")
	|				ИНАЧЕ ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.НомерСчетаФактурыКомитента, """")
	|			КОНЕЦ
	|	КОНЕЦ КАК НомерСчетаФактурыКомитента,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактурСрезПоследних.НомерСчетаФактурыПродавца <> """"
	|			ТОГДА ЖурналУчетаСчетовФактурСрезПоследних.ДатаСчетаФактуры
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЖурналУчетаСчетовФактурСрезПоследних.ДатаКорректировочногоСчетаФактуры <> ДАТАВРЕМЯ(1, 1, 1)
	|					ТОГДА ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.ДатаКорректировочногоСчетаФактуры, """")
	|				ИНАЧЕ ЕСТЬNULL(ВТ_СчетаФактурыОтКомитента.ДатаСчетаФактурыКомитента, """")
	|			КОНЕЦ
	|	КОНЕЦ КАК ДатаСчетаФактурыКомитента,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСКомиссия КАК СуммаНДСИтоговая,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУменьшениеКомиссия КАК СуммаПоСчетуФактуреРазницаУменьшениеИтоговая,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУвеличениеКомиссия КАК СуммаПоСчетуФактуреРазницаУвеличениеИтоговая,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУменьшениеКомиссия КАК СуммаНДСРазницаУменьшениеИтоговая,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУвеличениеКомиссия КАК СуммаНДСРазницаУвеличениеИтоговая,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура ССЫЛКА Документ.СчетФактураВыданный
	|			ТОГДА ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура.СводныйКорректировочный
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СводныйКорректировочный,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура ССЫЛКА Документ.СчетФактураВыданный
	|			ТОГДА ВЫРАЗИТЬ(ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура КАК Документ.СчетФактураВыданный).СводныйКомиссионный
	|		КОГДА ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура ССЫЛКА Документ.СчетФактураПолученный
	|			ТОГДА ВЫРАЗИТЬ(ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура КАК Документ.СчетФактураПолученный).СводныйКомиссионный
	//|		КОГДА ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура ССЫЛКА Документ.ЗаписьЖурналаУчетаСчетовФактур
	//|			ТОГДА ВЫРАЗИТЬ(ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура КАК Документ.ЗаписьЖурналаУчетаСчетовФактур).СводныйКомиссионный
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СводныйКомиссионный,
	|	ВЫБОР
	|		КОГДА НЕ ВТ_СчетаФактурыОтКомитента.СуммаПоСчетуФактуреКомиссия ЕСТЬ NULL
	|			ТОГДА ВТ_СчетаФактурыОтКомитента.СуммаПоСчетуФактуреКомиссия
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреКомиссия
	|	КОНЕЦ КАК СуммаДокументаКомиссия,
	|	ВЫБОР
	|		КОГДА НЕ ВТ_СчетаФактурыОтКомитента.СуммаПоСчетуФактуреРазницаУменьшение ЕСТЬ NULL
	|			ТОГДА ВТ_СчетаФактурыОтКомитента.СуммаПоСчетуФактуреРазницаУменьшение
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУменьшениеКомиссия
	|	КОНЕЦ КАК СуммаДокументаРазницаУменьшениеКомиссия,
	|	ВЫБОР
	|		КОГДА НЕ ВТ_СчетаФактурыОтКомитента.СуммаПоСчетуФактуреРазницаУвеличение ЕСТЬ NULL
	|			ТОГДА ВТ_СчетаФактурыОтКомитента.СуммаПоСчетуФактуреРазницаУвеличение
	|		ИНАЧЕ ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУвеличениеКомиссия
	|	КОНЕЦ КАК СуммаДокументаРазницаУвеличениеКомиссия
	|ПОМЕСТИТЬ ВТ_ЖурналУчетаСчетовФактурСрезПоследних
	|ИЗ
	|	ВТ_ЖурналСчетовФактурПредварительная КАК ЖурналУчетаСчетовФактурСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СчетаФактурыОтКомитента КАК ВТ_СчетаФактурыОтКомитента
	|		ПО ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура = ВТ_СчетаФактурыОтКомитента.СчетФактураВыданныйПокупателю
	|ГДЕ
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаВыставленияПолучения <> ДАТАВРЕМЯ(1, 1, 1)
	|	И (ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреКомиссия <> 0
	|			ИЛИ ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСКомиссия <> 0
	|			ИЛИ ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУменьшениеКомиссия <> 0
	|			ИЛИ ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУвеличениеКомиссия <> 0
	|			ИЛИ ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУменьшениеКомиссия <> 0
	|			ИЛИ ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУвеличениеКомиссия <> 0)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Контрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЖурналУчетаСчетовФактур.Организация,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактур.Контрагент.ОбособленноеПодразделение
	|			ТОГДА ЖурналУчетаСчетовФактур.Контрагент.ГоловнойКонтрагент
	|		ИНАЧЕ ЖурналУчетаСчетовФактур.Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактур.Контрагент.ОбособленноеПодразделение
	|			ТОГДА ВЫБОР
	|					КОГДА ПОДСТРОКА(ЖурналУчетаСчетовФактур.Контрагент.ГоловнойКонтрагент.НаименованиеПолное, 1, 250) ПОДОБНО """"
	|						ТОГДА ЖурналУчетаСчетовФактур.Контрагент.ГоловнойКонтрагент.Наименование
	|					ИНАЧЕ ПОДСТРОКА(ЖурналУчетаСчетовФактур.Контрагент.ГоловнойКонтрагент.НаименованиеПолное, 1, 250)
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ПОДСТРОКА(ЖурналУчетаСчетовФактур.Контрагент.НаименованиеПолное, 1, 250) ПОДОБНО """"
	|					ТОГДА ЖурналУчетаСчетовФактур.Контрагент.Наименование
	|				ИНАЧЕ ПОДСТРОКА(ЖурналУчетаСчетовФактур.Контрагент.НаименованиеПолное, 1, 250)
	|			КОНЕЦ
	|	КОНЕЦ КАК КонтрагентНаименование,
	|	ЖурналУчетаСчетовФактур.Контрагент.ИНН КАК КонтрагентИНН,
	|	ВЫБОР
	|		КОГДА ЖурналУчетаСчетовФактур.КППКонтрагента ПОДОБНО """"
	|			ТОГДА ЖурналУчетаСчетовФактур.Контрагент.КПП
	|		ИНАЧЕ ЖурналУчетаСчетовФактур.КППКонтрагента
	|	КОНЕЦ КАК КонтрагентКПП,
	|	ЖурналУчетаСчетовФактур.СчетФактура,
	|	ЖурналУчетаСчетовФактур.ЧастьЖурнала,
	|	ЖурналУчетаСчетовФактур.ДатаВыставленияПолучения,
	|	ЖурналУчетаСчетовФактур.КодСпособаВыставленияПолучения,
	|	ЖурналУчетаСчетовФактур.КодВидаОперации,
	|	ЖурналУчетаСчетовФактур.НомерСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.ДатаСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.ДатаКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.НомерИсправления,
	|	ЖурналУчетаСчетовФактур.ДатаИсправления,
	|	ЖурналУчетаСчетовФактур.НомерИсправленияКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.ДатаИсправленияКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.Валюта,
	|	ЖурналУчетаСчетовФактур.НаименованиеВалюты,
	|	ЖурналУчетаСчетовФактур.КодВалюты,
	|	ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУменьшение,
	|	ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУвеличение,
	|	ЖурналУчетаСчетовФактур.СуммаНДСРазницаУменьшение,
	|	ЖурналУчетаСчетовФактур.СуммаНДСРазницаУвеличение,
	|	ЖурналУчетаСчетовФактур.ПоСтавкеБезНДС,
	|	ЖурналУчетаСчетовФактур.СчетФактураНеВыставляется,
	|	ЖурналУчетаСчетовФактур.КодВидаСделки,
	|	ЖурналУчетаСчетовФактур.Комитент,
	|	ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуре,
	|	ЖурналУчетаСчетовФактур.СуммаНДС,
	|	ЖурналУчетаСчетовФактур.КомитентИНН,
	|	ЖурналУчетаСчетовФактур.КомитентКПП,
	|	ЖурналУчетаСчетовФактур.НомерСчетаФактурыКомитента,
	|	ЖурналУчетаСчетовФактур.ДатаСчетаФактурыКомитента,
	|	ЖурналУчетаСчетовФактур.СуммаНДСИтоговая,
	|	ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУменьшениеИтоговая,
	|	ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУвеличениеИтоговая,
	|	ЖурналУчетаСчетовФактур.СуммаНДСРазницаУменьшениеИтоговая,
	|	ЖурналУчетаСчетовФактур.СуммаНДСРазницаУвеличениеИтоговая,
	|	ЖурналУчетаСчетовФактур.СводныйКорректировочный,
	|	ЖурналУчетаСчетовФактур.СводныйКомиссионный,
	|	ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(ЖурналУчетаСчетовФактур.СчетФактура КАК Документ.СчетФактураВыданный).ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный)
	|			ТОГДА ИСТИНА
	|		КОГДА ВЫРАЗИТЬ(ЖурналУчетаСчетовФактур.СчетФактура КАК Документ.СчетФактураПолученный).ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыПолученного.Корректировочный)
	|			ТОГДА ИСТИНА
	//|		КОГДА ВЫРАЗИТЬ(ЖурналУчетаСчетовФактур.СчетФактура КАК Документ.ЗаписьЖурналаУчетаСчетовФактур).Корректировка = ИСТИНА
	//|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК КорректировочныйСчетФактура,
	|	ВЫРАЗИТЬ(ЖурналУчетаСчетовФактур.СчетФактура КАК Документ.СчетФактураВыданный).Номер КАК СчетФактураВыданныйНомер,
	|	ЖурналУчетаСчетовФактур.СуммаДокументаКомиссия КАК СуммаДокументаКомиссия,
	|	ЖурналУчетаСчетовФактур.СуммаДокументаРазницаУменьшениеКомиссия КАК СуммаДокументаРазницаУменьшениеКомиссия,
	|	ЖурналУчетаСчетовФактур.СуммаДокументаРазницаУвеличениеКомиссия КАК СуммаДокументаРазницаУвеличениеКомиссия
	|ПОМЕСТИТЬ ВТ_ЖурналУчетаСчетовФактурПоКонтрагентам
	|ИЗ
	|	ВТ_ЖурналУчетаСчетовФактурСрезПоследних КАК ЖурналУчетаСчетовФактур
	|ГДЕ
	|	ЖурналУчетаСчетовФактур.Контрагент В(&МассивКонтрагентов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЖурналУчетаСчетовФактур.СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЖурналУчетаСчетовФактур.Организация КАК Организация,
	|	ЖурналУчетаСчетовФактур.СчетФактура КАК СчетФактураДокумент,
	|	ЖурналУчетаСчетовФактур.ДатаВыставленияПолучения КАК ДатаПередачиПолучения,
	|	ЖурналУчетаСчетовФактур.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.НомерСчетаФактуры КАК НомерСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.Контрагент КАК Контрагент,
	|	ЖурналУчетаСчетовФактур.КонтрагентИНН КАК КонтрагентИНН,
	|	ЖурналУчетаСчетовФактур.КонтрагентКПП КАК КонтрагентКПП,
	|	ЖурналУчетаСчетовФактур.КонтрагентНаименование КАК КонтрагентНаименование,
	|	ЖурналУчетаСчетовФактур.НаименованиеВалюты КАК НаименованиеВалюты,
	|	ЖурналУчетаСчетовФактур.КодВалюты КАК КодВалюты,
	|	ЖурналУчетаСчетовФактур.ЧастьЖурнала КАК ЧастьЖурнала,
	|	ЖурналУчетаСчетовФактур.КодВидаОперации КАК КодВидаОперации,
	|	ЖурналУчетаСчетовФактур.ПоСтавкеБезНДС КАК СчетФактураБезНДС,
	|	ЖурналУчетаСчетовФактур.КодСпособаВыставленияПолучения КАК КодСпособаВыставления,
	|	ЖурналУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры КАК НомерКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.ДатаКорректировочногоСчетаФактуры КАК ДатаКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.НомерИсправления КАК НомерИсправления,
	|	ЖурналУчетаСчетовФактур.ДатаИсправления КАК ДатаИсправления,
	|	ЖурналУчетаСчетовФактур.НомерИсправленияКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.ДатаИсправленияКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.СчетФактураНеВыставляется КАК СчетФактураНеВыставляется,
	|	ЖурналУчетаСчетовФактур.КорректировочныйСчетФактура КАК КорректировочныйСчетФактура,
	|	ЖурналУчетаСчетовФактур.СчетФактураВыданныйНомер КАК СчетФактураВыданныйНомер,
	|	ЖурналУчетаСчетовФактур.КодВидаСделки,
	|	ЖурналУчетаСчетовФактур.Комитент,
	|	ЖурналУчетаСчетовФактур.КомитентИНН,
	|	ЖурналУчетаСчетовФактур.КомитентКПП,
	|	ЖурналУчетаСчетовФактур.НомерСчетаФактурыКомитента,
	|	ЖурналУчетаСчетовФактур.СводныйКорректировочный,
	|	ЖурналУчетаСчетовФактур.СводныйКомиссионный,
	|	ЖурналУчетаСчетовФактур.ДатаСчетаФактурыКомитента,
	|	СУММА(ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУменьшение) КАК СуммаДокументаРазницаУменьшение,
	|	СУММА(ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУвеличение) КАК СуммаДокументаРазницаУвеличение,
	|	СУММА(ЖурналУчетаСчетовФактур.СуммаНДСРазницаУменьшение) КАК СуммаНДСРазницаУменьшение,
	|	СУММА(ЖурналУчетаСчетовФактур.СуммаНДСРазницаУвеличение) КАК СуммаНДСРазницаУвеличение,
	|	СУММА(ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуре) КАК СуммаДокумента,
	|	СУММА(ЖурналУчетаСчетовФактур.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ЖурналУчетаСчетовФактур.СуммаНДСИтоговая) КАК СуммаНДСИтоговая,
	|	СУММА(ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУменьшениеИтоговая) КАК СуммаПоСчетуФактуреРазницаУменьшениеИтоговая,
	|	СУММА(ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУвеличениеИтоговая) КАК СуммаПоСчетуФактуреРазницаУвеличениеИтоговая,
	|	СУММА(ЖурналУчетаСчетовФактур.СуммаНДСРазницаУменьшениеИтоговая) КАК СуммаНДСРазницаУменьшениеИтоговая,
	|	СУММА(ЖурналУчетаСчетовФактур.СуммаНДСРазницаУвеличениеИтоговая) КАК СуммаНДСРазницаУвеличениеИтоговая,
	|	ЖурналУчетаСчетовФактур.СуммаДокументаКомиссия КАК СуммаДокументаКомиссия,
	|	ЖурналУчетаСчетовФактур.СуммаДокументаРазницаУменьшениеКомиссия КАК СуммаДокументаРазницаУменьшениеКомиссия,
	|	ЖурналУчетаСчетовФактур.СуммаДокументаРазницаУвеличениеКомиссия КАК СуммаДокументаРазницаУвеличениеКомиссия
	|ПОМЕСТИТЬ ЗаписиРегистраЖурналУчетаСчетовФактур
	|ИЗ
	|	ВТ_ЖурналУчетаСчетовФактурПоКонтрагентам КАК ЖурналУчетаСчетовФактур
	|
	|СГРУППИРОВАТЬ ПО
	|	ЖурналУчетаСчетовФактур.Организация,
	|	ЖурналУчетаСчетовФактур.СчетФактура,
	|	ЖурналУчетаСчетовФактур.ДатаВыставленияПолучения,
	|	ЖурналУчетаСчетовФактур.ДатаСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.НомерСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.Контрагент,
	|	ЖурналУчетаСчетовФактур.КонтрагентИНН,
	|	ЖурналУчетаСчетовФактур.КонтрагентКПП,
	|	ЖурналУчетаСчетовФактур.КонтрагентНаименование,
	|	ЖурналУчетаСчетовФактур.НаименованиеВалюты,
	|	ЖурналУчетаСчетовФактур.КодВалюты,
	|	ЖурналУчетаСчетовФактур.ЧастьЖурнала,
	|	ЖурналУчетаСчетовФактур.КодВидаОперации,
	|	ЖурналУчетаСчетовФактур.ПоСтавкеБезНДС,
	|	ЖурналУчетаСчетовФактур.КодСпособаВыставленияПолучения,
	|	ЖурналУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.ДатаКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.НомерИсправления,
	|	ЖурналУчетаСчетовФактур.ДатаИсправления,
	|	ЖурналУчетаСчетовФактур.НомерИсправленияКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.ДатаИсправленияКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.СчетФактураНеВыставляется,
	|	ЖурналУчетаСчетовФактур.КорректировочныйСчетФактура,
	|	ЖурналУчетаСчетовФактур.СчетФактураВыданныйНомер,
	|	ЖурналУчетаСчетовФактур.КодВидаСделки,
	|	ЖурналУчетаСчетовФактур.Комитент,
	|	ЖурналУчетаСчетовФактур.КомитентИНН,
	|	ЖурналУчетаСчетовФактур.КомитентКПП,
	|	ЖурналУчетаСчетовФактур.НомерСчетаФактурыКомитента,
	|	ЖурналУчетаСчетовФактур.СводныйКорректировочный,
	|	ЖурналУчетаСчетовФактур.СводныйКомиссионный,
	|	ЖурналУчетаСчетовФактур.ДатаСчетаФактурыКомитента,
	|	ЖурналУчетаСчетовФактур.СуммаДокументаКомиссия,
	|	ЖурналУчетаСчетовФактур.СуммаДокументаРазницаУменьшениеКомиссия,
	|	ЖурналУчетаСчетовФактур.СуммаДокументаРазницаУвеличениеКомиссия
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЧастьЖурнала
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура КАК СчетФактура,
	|	ВТ_ЖурналУчетаСчетовФактурСрезПоследних.Субкомиссионер КАК Субкомиссионер,
	|	ВТ_ЖурналУчетаСчетовФактурСрезПоследних.СубкомиссионерИНН КАК СубкомиссионерИНН,
	|	ВТ_ЖурналУчетаСчетовФактурСрезПоследних.СубкомиссионерКПП КАК СубкомиссионерКПП,
	|	МИНИМУМ(ВТ_ЖурналУчетаСчетовФактурСрезПоследних.НомерСтроки) КАК НомерСтроки
	|ИЗ
	|	ВТ_ЖурналУчетаСчетовФактурСрезПоследних КАК ВТ_ЖурналУчетаСчетовФактурСрезПоследних
	|ГДЕ
	|	ВТ_ЖурналУчетаСчетовФактурСрезПоследних.СубкомиссионерСсылка <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура,
	|	ВТ_ЖурналУчетаСчетовФактурСрезПоследних.Субкомиссионер,
	|	ВТ_ЖурналУчетаСчетовФактурСрезПоследних.СубкомиссионерИНН,
	|	ВТ_ЖурналУчетаСчетовФактурСрезПоследних.СубкомиссионерКПП";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПолучитьЗаписиЖурналаСчетовФактур(СтруктураПараметров) Экспорт
	
	Перем СформироватьОтчетПоСтандартнойФорме;
	Перем ГруппироватьПоКонтрагентам;
	Перем ОтбиратьПоКонтрагенту;
	Перем КонтрагентДляОтбора;
	Перем МассивКонтрагентовДляОтбора;
	
	Если НЕ СтруктураПараметров.Свойство("СформироватьОтчетПоСтандартнойФорме", СформироватьОтчетПоСтандартнойФорме) Тогда
		СформироватьОтчетПоСтандартнойФорме = Истина;
	КонецЕсли;
	
	Если НЕ СтруктураПараметров.Свойство("ЗаполнениеДекларации") Тогда
		СтруктураПараметров.Вставить("ЗаполнениеДекларации", Ложь);
	ИначеЕсли СтруктураПараметров.ЗаполнениеДекларации И СтруктураПараметров.НалоговыйПериод < '20150101' Тогда
		СтруктураПараметров.ЗаполнениеДекларации = Ложь;
	КонецЕсли;
	
	Если НЕ СтруктураПараметров.Свойство("ГруппироватьПоКонтрагентам", ГруппироватьПоКонтрагентам) Тогда
		ГруппироватьПоКонтрагентам = Ложь;
	КонецЕсли;
	ГруппироватьПоКонтрагентам = НЕ СформироватьОтчетПоСтандартнойФорме И ГруппироватьПоКонтрагентам;
	
	Если НЕ СтруктураПараметров.Свойство("ОтбиратьПоКонтрагенту", ОтбиратьПоКонтрагенту) Тогда
		ОтбиратьПоКонтрагенту = Ложь;
	КонецЕсли;
	ОтбиратьПоКонтрагенту = НЕ СформироватьОтчетПоСтандартнойФорме И ОтбиратьПоКонтрагенту 
		И СтруктураПараметров.Свойство("КонтрагентДляОтбора", КонтрагентДляОтбора);

	// Сформируем отбор по контрагенту с учетом его обособленных подразделений
	Если ОтбиратьПоКонтрагенту Тогда
		
		// Массив контрагентов и их обособленных подразделений
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Контрагент", КонтрагентДляОтбора);
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Контрагенты.Ссылка КАК Контрагент
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.Ссылка = Контрагенты.ГоловнойКонтрагент
		|	И Контрагенты.Ссылка В ИЕРАРХИИ(&Контрагент)
		|	И НЕ Контрагенты.ЭтоГруппа
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	Контрагенты.Ссылка
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.ОбособленноеПодразделение
		|	И Контрагенты.ГоловнойКонтрагент В ИЕРАРХИИ(&Контрагент)
		|	И НЕ Контрагенты.ЭтоГруппа
		|	И НЕ Контрагенты.ПометкаУдаления";
		
		Результат = Запрос.Выполнить();
		МассивКонтрагентовДляОтбора = Результат.Выгрузить().ВыгрузитьКолонку("Контрагент");
		
	КонецЕсли;
	
	ВерсияПостановленияНДС1137 = УчетНДС.ВерсияПостановленияНДС1137(СтруктураПараметров.НалоговыйПериод);
		
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация",             СтруктураПараметров.Организация);
	Запрос.УстановитьПараметр("Квартал",                 НачалоКвартала(СтруктураПараметров.НалоговыйПериод));
	Запрос.УстановитьПараметр("НачПериода",              НачалоКвартала(СтруктураПараметров.НалоговыйПериод));
	Запрос.УстановитьПараметр("КонПериода",              КонецКвартала(СтруктураПараметров.НалоговыйПериод));
	Запрос.УстановитьПараметр("МассивКонтрагентов",      МассивКонтрагентовДляОтбора);
	Запрос.УстановитьПараметр("ПравилаПостановления735", ВерсияПостановленияНДС1137 >= 3);
	
	Если ВерсияПостановленияНДС1137 >= 4 Тогда
		Запрос.Текст = ТекстЗапросаЖурналСчетовФактур981();
	Иначе
		Запрос.Текст = ТекстЗапросаЖурналСчетовФактур1137();
	КонецЕсли;
	
	Если ОтбиратьПоКонтрагенту Тогда
			ОтборПоКонтрагенту = "И (Контрагент В (&МассивКонтрагентов)
			|					ИЛИ ЧастьЖурнала = ЗНАЧЕНИЕ(Перечисление.ЧастиЖурналаУчетаСчетовФактур.ПолученныеСчетаФактуры)
			|					И Продавец В (&МассивКонтрагентов))";
	Иначе
		ОтборПоКонтрагенту = "";
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "И &ОтборПоКонтрагенту", ОтборПоКонтрагенту);
	
	Если НЕ ОтбиратьПоКонтрагенту Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ЖурналУчетаСчетовФактур.Контрагент В(&МассивКонтрагентов)", "ИСТИНА");
	КонецЕсли;
	
	ДополнительныеСвойства = Запрос.Выполнить().Выгрузить();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СчетФактураДокумент КАК СчетФактура
	|ПОМЕСТИТЬ ПредварительныйСписокСчетовФактур
	|ИЗ
	|	ЗаписиРегистраЖурналУчетаСчетовФактур КАК ЗаписиРегистраЖурналУчетаСчетовФактур
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПредварительныйСписокСчетовФактур.СчетФактура КАК СчетФактура,
	|	ПредварительныйСписокСчетовФактур.СчетФактура КАК ДокументОснование
	|ПОМЕСТИТЬ СписокСчетовФактур
	|ИЗ
	|	ПредварительныйСписокСчетовФактур КАК ПредварительныйСписокСчетовФактур
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПредварительныйСписокСчетовФактур.СчетФактура,
	|	СчетФактураВыданныйДокументыОснования.ДокументОснование
	|ИЗ
	|	ПредварительныйСписокСчетовФактур КАК ПредварительныйСписокСчетовФактур
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ПО ПредварительныйСписокСчетовФактур.СчетФактура = СчетФактураВыданныйДокументыОснования.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПредварительныйСписокСчетовФактур.СчетФактура,
	|	СчетФактураПолученныйДокументыОснования.ДокументОснование
	|ИЗ
	|	ПредварительныйСписокСчетовФактур КАК ПредварительныйСписокСчетовФактур
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученныйДокументыОснования
	|		ПО ПредварительныйСписокСчетовФактур.СчетФактура = СчетФактураПолученныйДокументыОснования.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура";
	
	Запрос.Выполнить();
	
	СведенияОРНПТ = НовыйСведенияОРНПТ();
	СтруктураПараметров.Вставить("МенеджерВременныхТаблиц", Запрос.МенеджерВременныхТаблиц);
	СтруктураПараметров.Вставить("ПорядокОтраженияВОтчетностиПоПрослеживаемости",
		Перечисления.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ЖурналСФ);
	УчетНДСПереопределяемый.ДополнитьИнформациейОРНПТ(СведенияОРНПТ, СтруктураПараметров, Запрос, Истина);
	СтруктураПараметров.Вставить("СведенияОРНПТ", СведенияОРНПТ);
	
	// Выданные счета-фактуры сортируем по дате передачи и далее по номеру с учетом префиксов,
	// чтобы обеспечить правильную сортировку для номеров разной значности.
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.Организация,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СчетФактураДокумент КАК СчетФактураДокумент,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.КодВидаОперации,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.ДатаПередачиПолучения,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.ДатаСчетаФактуры,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.НомерСчетаФактуры,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.Контрагент,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.КонтрагентНаименование КАК КонтрагентНаименование,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.НаименованиеВалюты,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.КодВалюты,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаДокумента КАК СуммаДокумента,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаНДС КАК СуммаНДС,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СчетФактураБезНДС,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СводныйКорректировочный КАК СводныйКорректировочный,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.ЧастьЖурнала,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.КонтрагентИНН,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.КонтрагентКПП,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаДокументаРазницаУменьшение КАК СуммаДокументаРазницаУменьшение,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаДокументаРазницаУвеличение КАК СуммаДокументаРазницаУвеличение,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаНДСРазницаУменьшение КАК СуммаНДСРазницаУменьшение,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаНДСРазницаУвеличение КАК СуммаНДСРазницаУвеличение,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.КодСпособаВыставления,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.ДатаКорректировочногоСчетаФактуры,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.НомерИсправленияКорректировочногоСчетаФактуры,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.ДатаИсправленияКорректировочногоСчетаФактуры,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.КодВидаСделки,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.Комитент,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.КомитентИНН,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.КомитентКПП,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.НомерСчетаФактурыКомитента,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.ДатаСчетаФактурыКомитента,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаНДСИтоговая КАК СуммаНДСИтоговая,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУменьшениеИтоговая КАК СуммаПоСчетуФактуреРазницаУменьшениеИтоговая,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУвеличениеИтоговая КАК СуммаПоСчетуФактуреРазницаУвеличениеИтоговая,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаНДСРазницаУменьшениеИтоговая КАК СуммаНДСРазницаУменьшениеИтоговая,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаНДСРазницаУвеличениеИтоговая КАК СуммаНДСРазницаУвеличениеИтоговая,
	|	ВЫБОР
	|		КОГДА НЕ &ПравилаПостановления735
	|				И ЗаписиРегистраЖурналУчетаСчетовФактур.КорректировочныйСчетФактура
	|			ТОГДА ЗаписиРегистраЖурналУчетаСчетовФактур.НомерИсправленияКорректировочногоСчетаФактуры
	|		ИНАЧЕ ЗаписиРегистраЖурналУчетаСчетовФактур.НомерИсправления
	|	КОНЕЦ КАК НомерИсправления,
	|	ВЫБОР
	|		КОГДА НЕ &ПравилаПостановления735
	|				И ЗаписиРегистраЖурналУчетаСчетовФактур.КорректировочныйСчетФактура
	|			ТОГДА ЗаписиРегистраЖурналУчетаСчетовФактур.ДатаИсправленияКорректировочногоСчетаФактуры
	|		ИНАЧЕ ЗаписиРегистраЖурналУчетаСчетовФактур.ДатаИсправления
	|	КОНЕЦ КАК ДатаИсправления,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СчетФактураНеВыставляется,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.КорректировочныйСчетФактура,
	|	ЕСТЬNULL(ЗаписиРегистраЖурналУчетаСчетовФактур.СчетФактураВыданныйНомер, ЗаписиРегистраЖурналУчетаСчетовФактур.НомерСчетаФактуры) КАК НомерСчетаФактурыДляСортировки,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СводныйКомиссионный,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаДокументаКомиссия КАК СуммаДокументаКомиссия,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаДокументаРазницаУменьшениеКомиссия КАК СуммаДокументаРазницаУменьшениеКомиссия,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаДокументаРазницаУвеличениеКомиссия КАК СуммаДокументаРазницаУвеличениеКомиссия
	|ИЗ
	|	ЗаписиРегистраЖурналУчетаСчетовФактур КАК ЗаписиРегистраЖурналУчетаСчетовФактур
	|ГДЕ
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.ЧастьЖурнала = ЗНАЧЕНИЕ(Перечисление.ЧастиЖурналаУчетаСчетовФактур.ВыставленныеСчетаФактуры)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.ДатаПередачиПолучения,
	|	НомерСчетаФактурыДляСортировки
	|ИТОГИ
	|	МАКСИМУМ(КонтрагентНаименование),
	|	МАКСИМУМ(СуммаДокумента),
	|	СУММА(СуммаНДС),
	|	МАКСИМУМ(СводныйКорректировочный),
	|	СУММА(СуммаДокументаРазницаУменьшение),
	|	СУММА(СуммаДокументаРазницаУвеличение),
	|	СУММА(СуммаНДСРазницаУменьшение),
	|	СУММА(СуммаНДСРазницаУвеличение),
	|	МАКСИМУМ(СуммаНДСИтоговая),
	|	МАКСИМУМ(СуммаПоСчетуФактуреРазницаУменьшениеИтоговая),
	|	МАКСИМУМ(СуммаПоСчетуФактуреРазницаУвеличениеИтоговая),
	|	МАКСИМУМ(СуммаНДСРазницаУменьшениеИтоговая),
	|	МАКСИМУМ(СуммаНДСРазницаУвеличениеИтоговая),
	|	СУММА(СуммаДокументаКомиссия),
	|	СУММА(СуммаДокументаРазницаУменьшениеКомиссия),
	|	СУММА(СуммаДокументаРазницаУвеличениеКомиссия)
	|ПО
	|	СчетФактураДокумент";
	
	Если ГруппироватьПоКонтрагентам Тогда
		
		ЗаменяемыйСтандартныйТекст = "ПО
		|	СчетФактураДокумент";
		
		НовыйТекст = "ПО
		|	Контрагент,
		|	СчетФактураДокумент";
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ЗаменяемыйСтандартныйТекст, НовыйТекст);
			
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "УПОРЯДОЧИТЬ ПО", "УПОРЯДОЧИТЬ ПО
			|КонтрагентНаименование, ");

	КонецЕсли;	
			
	ВыставленныеСчетаФактуры = Запрос.Выполнить();
	
	// Полученные счета-фактуры сортируем по дате получения, наименованию контрагента,
	// номер счета-фактуры контрагента
	                                  
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.Организация,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СчетФактураДокумент,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.КодВидаОперации,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.ДатаПередачиПолучения КАК ДатаПередачиПолучения,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.НомерСчетаФактуры,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.Контрагент КАК Контрагент,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.КонтрагентНаименование КАК КонтрагентНаименование,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.НаименованиеВалюты,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.КодВалюты,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаДокумента КАК СуммаДокумента,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаДокументаКомиссия,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаНДС КАК СуммаНДС,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СчетФактураБезНДС,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.ЧастьЖурнала,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.КонтрагентИНН,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.КонтрагентКПП,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаДокументаРазницаУменьшение,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаДокументаРазницаУвеличение,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаНДСРазницаУменьшение,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаНДСРазницаУвеличение,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.КодСпособаВыставления,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.ДатаКорректировочногоСчетаФактуры,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.НомерИсправленияКорректировочногоСчетаФактуры,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.ДатаИсправленияКорректировочногоСчетаФактуры,
	|	ВЫБОР
	|		КОГДА НЕ &ПравилаПостановления735
	|				И ЗаписиРегистраЖурналУчетаСчетовФактур.КорректировочныйСчетФактура
	|			ТОГДА ЗаписиРегистраЖурналУчетаСчетовФактур.НомерИсправленияКорректировочногоСчетаФактуры
	|		ИНАЧЕ ЗаписиРегистраЖурналУчетаСчетовФактур.НомерИсправления
	|	КОНЕЦ КАК НомерИсправления,
	|	ВЫБОР
	|		КОГДА НЕ &ПравилаПостановления735
	|				И ЗаписиРегистраЖурналУчетаСчетовФактур.КорректировочныйСчетФактура
	|			ТОГДА ЗаписиРегистраЖурналУчетаСчетовФактур.ДатаИсправленияКорректировочногоСчетаФактуры
	|		ИНАЧЕ ЗаписиРегистраЖурналУчетаСчетовФактур.ДатаИсправления
	|	КОНЕЦ КАК ДатаИсправления,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СчетФактураНеВыставляется,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.КорректировочныйСчетФактура,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.КодВидаСделки,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.Комитент,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.КомитентИНН,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.КомитентКПП,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.НомерСчетаФактурыКомитента,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.ДатаСчетаФактурыКомитента,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаНДСИтоговая,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУменьшениеИтоговая,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУвеличениеИтоговая,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаНДСРазницаУменьшениеИтоговая,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаНДСРазницаУвеличениеИтоговая
	|ИЗ
	|	ЗаписиРегистраЖурналУчетаСчетовФактур КАК ЗаписиРегистраЖурналУчетаСчетовФактур
	|ГДЕ
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.ЧастьЖурнала = ЗНАЧЕНИЕ(Перечисление.ЧастиЖурналаУчетаСчетовФактур.ПолученныеСчетаФактуры)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаПередачиПолучения,
	|	КонтрагентНаименование,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.НомерСчетаФактуры";
	
	Если ГруппироватьПоКонтрагентам Тогда
		Запрос.Текст = Запрос.Текст + "
			|ИТОГИ
			|	МАКСИМУМ(СуммаДокумента),
			|	СУММА(СуммаДокументаКомиссия),
			|	СУММА(СуммаНДС),
			|	МАКСИМУМ(СуммаНДСИтоговая),
			|	МАКСИМУМ(СуммаПоСчетуФактуреРазницаУменьшениеИтоговая),
			|	МАКСИМУМ(СуммаПоСчетуФактуреРазницаУвеличениеИтоговая),
			|	МАКСИМУМ(СуммаНДСРазницаУменьшениеИтоговая),
			|	МАКСИМУМ(СуммаНДСРазницаУвеличениеИтоговая),
			|	МАКСИМУМ(КонтрагентНаименование)
			|ПО
			|	Контрагент";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "УПОРЯДОЧИТЬ ПО", "УПОРЯДОЧИТЬ ПО
			|КонтрагентНаименование, ");
	КонецЕсли;
	
	ПолученныеСчетаФактуры = Запрос.Выполнить();
	
	Запрос.МенеджерВременныхТаблиц = Неопределено;
	СтруктураПараметров.МенеджерВременныхТаблиц = Неопределено;

	Возврат Новый Структура("ВыставленныеСчетаФактуры, ПолученныеСчетаФактуры, ДополнительныеСвойства, СведенияОРНПТ",
		ВыставленныеСчетаФактуры, ПолученныеСчетаФактуры, ДополнительныеСвойства, СведенияОРНПТ);

КонецФункции

Функция НовыйСведенияОРНПТ()

	НовыйСведенияОРНПТ = Новый ТаблицаЗначений;
	НовыйСведенияОРНПТ.Колонки.Добавить("Ссылка", Документы.ТипВсеСсылки());
	НовыйСведенияОРНПТ.Индексы.Добавить("Ссылка");
	// С 1 июля 2021 года указывается свойство СвРегНом
	НовыйСведенияОРНПТ.Колонки.Добавить("СвРегНом", Новый ОписаниеТипов("Массив"));

	Возврат НовыйСведенияОРНПТ;

КонецФункции

Процедура ВывестиГраницуСтроки(СтруктураПараметров, СекцияСтрока, ИмяОбласти) Экспорт
	
	Если СтруктураПараметров.ВерсияПостановленияНДС1137 < 5 Тогда
		Возврат;
	КонецЕсли;

	ОбластьЗапись = СекцияСтрока.Область(ИмяОбласти);
	ОбластьЗапись.ГраницаСнизу = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.НетЛинии);
	Если Не ЗначениеЗаполнено(СтруктураПараметров.МассивСвРегНом)
		Или СтруктураПараметров.МассивСвРегНом.Количество() = 1 Тогда
		ОбластьЗапись.ГраницаСнизу = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 1);
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьПодстрочникРНПТ(ЗаписьОтчета, ПараметрыСтрокиРНПТ, СтруктураПараметров, СекцияСтрокаРНПТ, ТабличныйДокумент, ИмяОбласти) Экспорт

	Перем ОбластьТабличногоДокумента, СтруктураРНПТ, ЭлементСвРегНом;

	Если СтруктураПараметров.ВерсияПостановленияНДС1137 < 5 Тогда
		Возврат;
	КонецЕсли;
	
	// Добавим подстрочник, если по счету-фактуре разные РНПТ
	Если ЗначениеЗаполнено(СтруктураПараметров.МассивСвРегНом)
		И СтруктураПараметров.МассивСвРегНом.Количество() > 1 Тогда
		
		Для ЭлементСвРегНом = 1 По СтруктураПараметров.МассивСвРегНом.Количество() - 1 Цикл
			СтруктураРНПТ = СтруктураПараметров.МассивСвРегНом.Получить(ЭлементСвРегНом);
			ПараметрыСтрокиРНПТ.РегНомер     = СтруктураРНПТ.РегНомПросл;
			ПараметрыСтрокиРНПТ.КолПрослеж   = СтруктураРНПТ.КолТовПросл;
			ПараметрыСтрокиРНПТ.СтоимПрослеж = СтруктураРНПТ.СтоимТовПросл;
			ПараметрыСтрокиРНПТ.ЕдПрослеж    = СтруктураРНПТ.ОКЕИ;
			
			ОбластьЗапись = СекцияСтрокаРНПТ.Область(ИмяОбласти);
			Если ЭлементСвРегНом = СтруктураПараметров.МассивСвРегНом.Количество() - 1
			   И СтруктураПараметров.КоличествоСгруппированных < 2 Тогда
				// Выводим границу в конце подстрочника, но только в том случае, если после не следуют другие подстрочники (с данными СФ).
				ОбластьЗапись.ГраницаСнизу = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 1);
			Иначе
				ОбластьЗапись.ГраницаСнизу = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.НетЛинии);
			КонецЕсли;
			
			ТабличныйДокумент.Вывести(СекцияСтрокаРНПТ);

		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

#КонецЕсли